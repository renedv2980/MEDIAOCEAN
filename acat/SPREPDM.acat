*          DATA SET SPREPDM    AT LEVEL 034 AS OF 11/14/17                      
*CATALP SPDTAMGR                                                                
         TITLE 'MODULE TO AID DATAMGR CALLS'                                    
***********************************************************************         
* USER    JIRA       DATE                  CHANGE LOG                 *         
* ---- ----------  -------- ----------------------------------------- *         
* AKAT MOSTK-62    03/21/16 NEW -CM BAND SUPPORT FOR IHEART RADIO     *         
***********************************************************************         
SPDMGR   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 SPDMDX-SPDMD,**SPDM**                                            
         USING SPDMD,RC                                                         
         L     RA,=V(SPWORKC)                                                   
         USING SPWORKD,RA                                                       
         LA    R9,2048(RA)                                                      
         LA    R9,2048(R9)                                                      
         USING SPWORKD+4096,R9                                                  
         MVI   DMCB+8,0            RESET ERROR IND                              
         SRL   RF,24                                                            
         ST    RF,SAVENUM                                                       
         BCTR  RF,0                                                             
         SLL   RF,2                                                             
         LA    R2,INFOLIST(RF)                                                  
         SR    R3,R3                                                            
         IC    R3,0(R2)                                                         
         SLL   R3,2                                                             
         B     BRANCH(R3)                                                       
         SPACE 2                                                                
BRANCH   B     DIRREAD                                                          
         B     DIRHIGH                                                          
         B     DIRSEQ                                                           
         B     DIRWRITE                                                         
         B     DIRADD                                                           
         B     FILGET                                                           
         B     FILPUT                                                           
         B     FILADD                                                           
         B     STAREAD                                                          
         B     STAHIGH                                                          
         B     STASEQ                                                           
         B     STAWRITE                                                         
         EJECT                                                                  
*              DIRECTORY READ/WRITE                                             
         SPACE 3                                                                
DIRREAD  MVC   COMMAND,DMREAD                                                   
         B     DIRLINK                                                          
         SPACE 2                                                                
DIRHIGH  MVC   COMMAND,DMRDHI                                                   
         MVC   KEYSAVE,KEY                                                      
         B     DIRLINK                                                          
         SPACE 2                                                                
DIRSEQ   MVC   COMMAND,DMRSEQ                                                   
         B     DIRLINK                                                          
         SPACE 2                                                                
DIRWRITE MVC   COMMAND,DMWRT                                                    
         CLI   RCWRITE,C'N'                                                     
         BE    DMEND                                                            
         B     DIRLINK                                                          
         SPACE 2                                                                
DIRADD   MVC   COMMAND,DMADD                                                    
         CLI   RCWRITE,C'N'                                                     
         BE    DMEND                                                            
         SPACE 2                                                                
DIRLINK  MVC   TRACEKEY,KEY                                                     
         MVC   FILE,SPTDIR                                                      
         CLI   KEY,X'0A'           IS THIS A TRAFFIC RECORD                     
         BNE   DIRLINK1                                                         
         MVC   FILE,=CL8'TRFDIR'   YES - SET TRAFFIC DIR                        
*                                                                               
DIRLINK1 GOTO1 DATAMGR,DMCB,(DMINBTS,COMMAND),FILE,KEY,KEY,(0,DMWORK)           
         B     DMCHECK                                                          
         EJECT                                                                  
*              SPOT FILE GET/PUT/ADD                                            
         SPACE 3                                                                
FILGET   MVC   COMMAND,GETREC                                                   
         LA    R6,KEY+14                                                        
         B     FILELINK                                                         
         SPACE 2                                                                
FILPUT   MVC   COMMAND,PUTREC                                                   
         CLI   UNWOPTS,0           NEVER WRITE AN UNWOUND RECORD BACK!          
         BE    *+6                                                              
         DC    H'0'                                                             
         CLI   RCWRITE,C'N'                                                     
         BE    DMEND                                                            
         LA    R6,KEY+14                                                        
         B     FILELINK                                                         
         SPACE 2                                                                
FILADD   MVC   COMMAND,ADDREC                                                   
         LA    R6,KEY                                                           
         CLI   RCWRITE,C'N'                                                     
         BE    DMEND                                                            
         SPACE 2                                                                
FILELINK SR    R4,R4                                                            
         ICM   R4,7,1(R2)          A(RECORD) IN INFO LIST                       
         BNZ   *+8                                                              
         L     R4,AREC             OR USER HAS SUPPLIED                         
         MVC   FILE,SPTFILE                                                     
         CLI   KEY,X'0A'           IS THIS A TRAFFIC RECORD                     
         BNE   FILLINK1                                                         
         MVC   FILE,=CL8'TRFFIL'   YES - SET TRAFFIC FILE                       
*                                                                               
FILLINK1 GOTO1 DATAMGR,DMCB,(DMINBTS,COMMAND),FILE,(R6),(R4),(0,DMWORK)         
         B     DMCHECK                                                          
         EJECT                                                                  
*              STATION FILE READ/WRITE                                          
         SPACE 3                                                                
STAREAD  MVC   COMMAND,DMREAD                                                   
         B     STALINK                                                          
         SPACE 2                                                                
STAHIGH  MVC   COMMAND,DMRDHI                                                   
         MVC   KEYSAVE,KEY                                                      
         B     STALINK                                                          
         SPACE 2                                                                
STASEQ   MVC   COMMAND,DMRSEQ                                                   
         B     STALINK                                                          
         SPACE 2                                                                
STAWRITE MVC   COMMAND,DMWRT                                                    
         CLI   RCWRITE,C'N'                                                     
         BE    DMEND                                                            
         SPACE 2                                                                
STALINK  MVC   TRACEKEY,KEY                                                     
         MVC   FILE,STATION                                                     
         L     R4,0(R2)            A(RECORD) IN INFO LIST                       
         GOTO1 DATAMGR,DMCB,(DMINBTS,COMMAND),FILE,KEY,(R4),(0,DMWORK)          
         EJECT                                                                  
*              CHECK FOR ERRORS                                                 
         SPACE 3                                                                
DMCHECK  MVC   BYTE,DMOUTBTS                                                    
         NC    BYTE,DMCB+8                                                      
         BZ    ANYTRACE                                                         
         MVC   DMERRMSG+5(2),RCPROG                                             
         MVC   DMERRMSG+7(2),RCAGENCY                                           
         OI    RCRQTOT+2,X'0F'                                                  
         UNPK  DMERRMSG+34(4),RCRQTOT                                           
         GOTO1 LOGIO,MYPARA,1,(40,DMERRMSG)                                     
*                                                                               
         MVC   P,SPACES                                                         
         MVC   P(40),DMERRMSG                                                   
         MVI   SKIPSPEC,C'Y'                                                    
         L     R0,HEADHOOK         MAKE DOUBLE DEAD SURE                        
         XC    HEADHOOK,HEADHOOK   NOT TO CALL HEADHOOK !                       
         GOTO1 REPORT                                                           
         BAS   RE,DMTRACE                                                       
         ST    R0,HEADHOOK         RESTORE HEADHOOK ADDRESS                     
         MVI   MODE,DISKERR                                                     
         B     DMEND                                                            
         SPACE 2                                                                
ANYTRACE CLI   RCTRACE,C'Y'        TRACE OPTION                                 
         BNE   *+8                                                              
         BAS   RE,DMTRACE                                                       
         EJECT                                                                  
*================================================================               
* SUPPRESS BUY/GOAL COSTS FOR USERID MVMFI                                      
*================================================================               
         SPACE 1                                                                
         L     RE,VMASTC                                                        
         CLC   =X'28BB',MCORIGID-MASTD(RE)                                      
         BNE   ANYTR2                                                           
         B     ANYTR2  ===> NOP - ID DOES NOT EXIST 1/28/05 MHER                
         L     RE,DMCB                                                          
         CLC   =C'GET',0(RE)                                                    
         BNE   ANYTR2                                                           
         BRAS  RE,ZAPDOLS                                                       
         SPACE 1                                                                
*================================================================               
* DO DATA EXTRACTION AS REQUIRED                                                
*================================================================               
         SPACE 1                                                                
ANYTR2   L     R2,DMCB+12          POINT R2 TO RECORD                           
         L     R3,SAVENUM          ARE WE DOING A LOGICAL GET (6-17)            
         CHI   R3,25                                                            
         BE    EXTMKT                                                           
         CHI   R3,26                                                            
         BE    EXTREP                                                           
         CHI   R3,6                                                             
         BL    DMEXT                                                            
         CHI   R3,17                                                            
         BH    DMEXT                                                            
         AHI   R3,-6                                                            
         SLL   R3,2                                                             
         B     EXTRACT(R3)                                                      
         SPACE 2                                                                
EXTRACT  B     EXTAGY                                                           
         B     EXTCLT                                                           
         B     EXTPRD                                                           
         B     EXTEST                                                           
         B     EXTBIL                                                           
         B     EXTGOL                                                           
         B     EXTADV                                                           
         B     EXTPRF                                                           
         B     EXTDAY                                                           
         B     EXTBUY                                                           
         B     EXTPGR                                                           
         B     EXTMGR                                                           
         EJECT                                                                  
*              ROUTINES TO EXTRACT VALUES FOR AGENCY/CLIENT/PROD/EST            
         SPACE 3                                                                
EXTAGY   DS    0H                                                               
         USING AGYHDR,R2                                                        
         MVC   AGENCY,AGYKAGY                                                   
         CLC   AGYNM,SPACES        AGENCY NAME & ADDRESS SHOULD HAVE            
         BNE   *+16                BEEN SET UP WITH ORIGIN= CARD                
         MVC   AGYNM,AGYNAME                                                    
         MVC   AGYADR,AGYADDR                                                   
EXTAGY2  DC    0H'0'                                                            
         CLI   QBYID,C'Y'                                                       
         BNE   EXTAGYX                                                          
         LA    RE,AGYHDR+24                                                     
         SR    R0,R0                                                            
EXTAGY4  IC    R0,1(RE)            FIND ID ELEMENT                              
         AR    RE,R0                                                            
         CLI   0(RE),0                                                          
         BE    EXTAGYX                                                          
         CLI   0(RE),X'70'                                                      
         BNE   EXTAGY4                                                          
         MVC   BUYIDNAM(10),2(RE)                                               
EXTAGYX  B     DMEXT                                                            
         SPACE 2                                                                
EXTCLT   DS    0H                                                               
         USING CLTHDR,R2                                                        
         GOTO1 CLUNPK,MYPARA,(CPROF+6,CKEYCLT),CLIENT                           
         MVC   CLTNM,SPACES                                                     
         MVC   CLTNM(20),CNAME                                                  
*                                                                               
* BUILD ONE CONTIGUOUS CLIST                                                    
         L     R0,VCLIST                                                        
         LHI   R1,880                                                           
         LA    RE,CLIST                                                         
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LHI   R1,140              LENGTH OF CLIST2                             
         LA    RE,CLIST2                                                        
         LR    RF,R1                                                            
         CLC   CKEY+13(2),=H'1280'  TEST CLIST2 IN RECORD                       
         BH    *+8                 YES                                          
         SR    R1,R1               ELSE JUST CLEAR REST OF LIST                 
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         CLI   CEXTRA+2,C'Y'       TEST BUY ID REQ'D                            
         BE    *+12                                                             
         L     R2,ADAGY                                                         
         B     EXTAGY2                                                          
*                                                                               
         MVC   BUYIDNAM,SPACES                                                  
         MVC   BUYIDNAM(10),CTITLE                                              
         B     DMEXT                                                            
         SPACE 2                                                                
EXTPRD   DS    0H                                                               
         USING PRDHDR,R2                                                        
         MVC   PRODUCT,PKEYPRD                                                  
         MVC   PRDNM,SPACES                                                     
         MVC   PRDNM(20),PNAME                                                  
         B     DMEXT                                                            
         EJECT                                                                  
EXTEST   DS    0H                                                               
         USING ESTHDR,R2                                                        
         SR    R4,R4                                                            
         IC    R4,EKEYEST                                                       
         CVD   R4,MYPARA                                                        
         UNPK  ESTIMATE,MYPARA(8)                                               
         OI    ESTIMATE+2,X'F0'                                                 
         MVC   ESTNM,EDESC                                                      
         MVC   ESTNM+20(4),SPACES                                               
         MVC   ESTDATES,ESTART                                                  
         SPACE 2                                                                
EXTEST2  GOTO1 DATCON,MYPARA,(0,ESTST),(3,ESTSTB)                               
         GOTO1 (RF),(R1),,(2,ESTSTP)                                            
         GOTO1 (RF),(R1),(0,ESTND),(3,ESTNDB)                                   
         GOTO1 (RF),(R1),,(2,ESTNDP)                                            
         B     DMEXT                                                            
         EJECT                                                                  
*              ROUTINES TO EXTRACT VALUES FOR BUYS                              
         SPACE 3                                                                
EXTBUY   DS    0H                                                               
         USING BUYREC,R2                                                        
         CLI   0(R2),X'10'         TEST BUYREC PRESENT                          
         BNH   DMEXT               NO- DON'T EXTRACT                            
         MVC   BIGSTA,SPACES                                                    
         GOTO1 MSUNPK,MYPARA,(X'80',BUYMSTA),DUMMKT,BIGSTA                      
         ORG   *-2                                                              
         CLI   COUNTRY,C'C'                                                     
         BNE   EXTBUY2                                                          
         SR    R0,R0                                                            
         IC    R0,0(R2)                                                         
         N     R0,=X'0000000F'                                                  
         CHI   R0,3                TEST MEDIA N                                 
         BNE   EXTBUY2             NO                                           
         CLI   BUYMSTA+4,X'B0'     TEST CABLE                                   
         BL    EXTBUY2                                                          
         OI    8(R1),X'80'                                                      
         BASR  RE,RF               CALL STAPACK                                 
         MVC   STA(5),BIGSTA                                                    
         B     EXTBUY40                                                         
*                                                                               
EXTBUY2  BASR  RE,RF                                                            
         MVC   STA(5),BIGSTA       PRESERVE THE OLD WAY                         
*                                                                               
         MVC   STAPRINT(5),STA                                                  
         MVC   STAPRINT+4(3),=C'- M'                                            
         MVC   STAPRINT+5(1),STA+4                                              
*                                                                               
         CLI   STA+4,C'A'                                                       
         BE    EXTBUY10                                                         
         CLI   STA+4,C'F'                                                       
         BE    EXTBUY10                                                         
         CLI   STA+4,C'S'          TEST STREAMING RADIO                         
         BE    EXTBUY3                                                          
         CLI   STA+4,C'D'          TEST DIGITAL TV                              
         BE    EXTBUY3                                                          
         CLI   STA+4,C'C'          TEST CM BAND FOR IHEART RADIO                
         BE    EXTBUY3                                                          
         CLI   STA+4,C'L'          TEST LOW POWER                               
         BNE   EXTBUY4                                                          
*                                                                               
EXTBUY3  MVI   STAPRINT+6,C' '     YES - JUST PRINT -L/-D/-S/-C                 
         B     EXTBUY10                                                         
*                                                                               
EXTBUY4  CLI   STAPRINT,C'0'       TEST STATION NUMERIC                         
         BL    EXTBUY6                                                          
         MVI   BIGSTA+4,C'/'       CABLE BE NUMERIC                             
         MVI   BIGSTA+8,C' '       AND CABLE ONLY NEEDS 8 CHARS                 
         B     EXTBUY40                                                         
*                                                                               
EXTBUY6  MVC   STAPRINT+5(2),=C'TV'                                             
         CLI   QMED,C'T'                                                        
         BE    EXTBUY10                                                         
         MVC   STAPRINT+4(3),=C'   '                                            
*                                                                               
EXTBUY10 MVC   BIGSTA,SPACES                                                    
         MVC   BIGSTA(7),STAPRINT                                               
         CLI   QMED,C'N'                                                        
         BE    EXTBUY20                                                         
         CLI   QMED,C'C'                                                        
         BNE   EXTBUY40                                                         
* NEED TO APPEND -T OR -N FOR CANAD CMBD                                        
         MVI   STAPRINT+4,C'-'                                                  
         MVI   STAPRINT+5,C'T'                                                  
         MVC   MYPARA,BUYREC                                                    
         NI    MYPARA,X'0F'                                                     
         CLI   MYPARA,3                                                         
         BNE   EXTBUY40                                                         
         MVI   STAPRINT+5,C'N'                                                  
*                                                                               
EXTBUY20 DS    0H                  LOOK FOR 6 BYTE X'68' ELEM                   
         LA    R6,BDELEM                                                        
*                                                                               
EXTBUY30 ZIC   R0,1(R6)                                                         
         AR    R6,R0                                                            
         CLI   0(R6),0                                                          
         BE    EXTBUY40                                                         
         CLI   0(R6),X'68'                                                      
         BNE   EXTBUY30                                                         
         CLI   1(R6),6             EXPLODED BUY HAS 6 BYTE ELEM                 
         BNE   EXTBUY40                                                         
* APPEND NETWORK TO STATION                                                     
         LA    R4,BIGSTA+3                                                      
         CLI   0(R4),C' '                                                       
         BNE   *+6                                                              
         BCTR  R4,0                                                             
         MVI   1(R4),C'/'                                                       
         MVC   2(4,R4),2(R6)                                                    
         SPACE 2                                                                
EXTBUY40 MVC   COSTIND,BDCIND      MOVE COST TYPE                               
         TM    BDCIND2,X'04'       TEST BDCIND IS CHAR                          
         BO    DMEXT               YUP - DONE                                   
*                                                                               
         LA    R4,CINDTAB                                                       
         MVC   MYPARA,BDCIND                                                    
         NI    MYPARA,X'FE'                                                     
         SPACE 2                                                                
EXTBUY50 CLC   0(1,R4),MYPARA                                                   
         BE    EXTBUY60                                                         
         LA    R4,2(R4)                                                         
         B     EXTBUY50                                                         
*                                                                               
EXTBUY60 MVC   COSTIND,1(R4)                                                    
         B     DMEXT                                                            
         DROP  R2                                                               
         SPACE 2                                                                
CINDTAB  DS    0H                                                               
         DC    X'20'                                                            
         DC    C' '                                                             
         DC    X'10'                                                            
         DC    C'N'                                                             
         DC    X'08'                                                            
         DC    C'V'                                                             
         DC    X'04'                                                            
         DC    C'S'                                                             
         DC    X'02'                                                            
         DC    C'X'                                                             
         DC    X'80'                                                            
         DC    C'F'                                                             
         DC    X'40'                                                            
         DC    C'Q'                                                             
         DC    X'00'                                                            
         DC    C'P'                                                             
         EJECT                                                                  
*==========================================================                     
* ROUTINE TO EXTRACT PRODUCT AND MARKET GROUPS                                  
*==========================================================                     
         SPACE 1                                                                
EXTPGR   DS    0H                                                               
         USING PRGRECD,R2                                                       
         LA    R3,PGR1                                                          
         MVI   GROUPSW,C'P'                                                     
         MVC   MYPARA(1),PRGKID                                                 
         UNPK  MYPARA+1(5),PRGKGRP(3)                                           
         B     MP2                                                              
         SPACE 2                                                                
EXTMGR   DS    0H                                                               
         USING MKGRECD,R2                                                       
         LA    R3,MGR1                                                          
         MVI   GROUPSW,C'M'                                                     
         MVC   MYPARA(1),MKGKMID                                                
         UNPK  MYPARA+1(5),MKGKMGRP(3)                                          
*                                                                               
MP2      AH    R2,DATADISP                                                      
*                                                                               
MP4      CLI   0(R2),X'01'                                                      
         BNE   MP6                                                              
         USING PRGEL01,R2                                                       
         MVC   06(12,R3),PRGBK1    BREAK NAMES                                  
         MVC   48(12,R3),PRGBK2                                                 
         MVC   90(12,R3),PRGBK3                                                 
*                                                                               
         SR    R4,R4               BREAK LENGTHS                                
         SR    R5,R5                                                            
         IC    R4,PRGBK1LN                                                      
         STC   R4,5(R3)                                                         
         IC    R5,PRGBK2LN                                                      
         AR    R4,R5                                                            
         STC   R4,47(R3)                                                        
         IC    R5,PRGBK3LN                                                      
         AR    R4,R5                                                            
         STC   R4,89(R3)                                                        
*                                                                               
MP6      CLI   0(R2),X'10'                                                      
         BNE   MP8                                                              
         USING PRGEL10,R2                                                       
         MVC   18(24,R3),PRGNAM1   GROUP NAMES                                  
         MVC   60(24,R3),PRGNAM2                                                
         MVC   102(24,R3),PRGNAM3                                               
         SR    R4,R5                                                            
         LA    R5,3                                                             
*                                                                               
MP7      MVC   0(5,R3),SPACES      GROUP CODES                                  
         IC    R4,5(R3)                                                         
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),MYPARA                                                   
         LA    R3,42(R3)                                                        
         BCT   R5,MP7                                                           
*                                                                               
         CLI   GROUPSW,C'M'        TEST DOING MKTGRPS                           
         BNE   DMEXT               NO                                           
*                                                                               
         MVC   MGR1N(5),MGR1       COPY MGR1 GROUPS TO NEW                      
         MVI   MGR1N+5,C' '                                                     
         MVC   MGR2N(5),MGR2                                                    
         MVI   MGR2N+5,C' '                                                     
         MVC   MGR3N(5),MGR3                                                    
         MVI   MGR3N+5,C' '                                                     
* LOOK UP ALPHA MKTGRP CODE                                                     
         MVI   MGRLEN,1            SET 1 CHAR ALPHA CODE                        
         L     RE,=A(SPMGRTAB)                                                  
         LHI   RF,(SPMGRTBX-SPMGRTAB)/3                                         
*                                                                               
MP7A     CLC   MGR1(1),2(RE)       MATCH 1-BYTE GROUP CODE YSFI                 
         BE    MP7B                                                             
         LA    RE,3(RE)                                                         
         BCT   RF,MP7A                                                          
         DC    H'0'                                                             
*                                                                               
MP7B     CLI   1(RE),C' '          TEST 2 CHAR GROUP                            
         BNH   DMEXT               NO - DONE                                    
         MVI   MGRLEN,2            SET 2 CHAR ALPHA CODE                        
         MVC   MGR1N(2),0(RE)                                                   
         MVC   MGR1N+2(4),MGR1+1                                                
         MVC   MGR2N(2),0(RE)                                                   
         MVC   MGR2N+2(4),MGR2+1                                                
         MVC   MGR3N(2),0(RE)                                                   
         MVC   MGR3N+2(4),MGR3+1                                                
         B     DMEXT                                                            
         SPACE 2                                                                
MP8      SR    R5,R5                                                            
         IC    R5,1(R2)                                                         
         AR    R2,R5                                                            
         CLI   0(R2),0                                                          
         BNE   MP4                                                              
         B     DMEXT                                                            
         EJECT                                                                  
*              ROUTINES TO EXTRACT OTHER VALUES                                 
         SPACE 3                                                                
EXTMKT   DS    0H                                                               
         USING MKTREC,R2                                                        
         MVC   MKT,MKTKMKT                                                      
         MVC   MKTNM,MKTNAME                                                    
         CLI   QMGR,C'0'           MARKET RANK OPTION                           
         BL    DMEXT                                                            
         MVI   MRANK,C'('                                                       
         MVC   MRANK+1(3),MKTRANK                                               
         MVI   MRANK+4,C')'                                                     
         B     DMEXT                                                            
         SPACE 2                                                                
EXTREP   DS    0H                                                               
         USING REPREC,R2                                                        
         MVC   REP,REPKREP                                                      
         MVC   REPNM,SPACES                                                     
         MVC   REPNM(22),RNAME                                                  
         B     DMEXT                                                            
         SPACE 2                                                                
EXTBIL   DS    0H                                                               
EXTGOL   DS    0H                                                               
EXTADV   DS    0H                                                               
EXTPRF   DS    0H                                                               
EXTDAY   DS    0H                                                               
DMEXT    DS    0H                                                               
         SPACE 2                                                                
DMEND    XMOD1 1                                                                
*                                                                               
DMERRMSG DC    CL40'* SP099XX * DATAMGR ERROR REQUEST 9999'                     
         EJECT                                                                  
*              TRACE FACILITY                                                   
         SPACE 3                                                                
DMTRACE  NTR1                                                                   
         L     R0,HEADHOOK                                                      
         XC    HEADHOOK,HEADHOOK                                                
         MVC   P,SPACES                                                         
         MVC   P(8),COMMAND                                                     
         MVC   P+10(8),FILE                                                     
         LM    R2,R3,DMCB+8                                                     
         CLC   FILE,SPTFILE                                                     
         BE    DMTRACE2                                                         
         CLC   =C'TRFFIL',FILE                                                  
         BE    DMTRACE2                                                         
         MVC   P+20(20),TRACEKEY                                                
         MVI   SKIPSPEC,C'Y'                                                    
         GOTO1 HEXOUT,MYPARA,TRACEKEY,P+45,20,=C'N'                             
         GOTO1 REPORT                                                           
         MVC   P+20(20),KEY                                                     
         GOTO1 HEXOUT,MYPARA,KEY                                                
         B     DMTRACE4                                                         
         SPACE 2                                                                
DMTRACE2 GOTO1 HEXOUT,MYPARA,(0,(R2)),P+20,4,=C'N'                              
         GOTO1 (RF),(R1),(0,(R3)),P+30,30,=C'N'                                 
         SPACE 2                                                                
DMTRACE4 GOTO1 HEXOUT,MYPARA,DMCB,P+100,1,=C'N'                                 
         GOTO1 (RF),(R1),DMCB+8,P+104                                           
         MVI   SKIPSPEC,C'Y'                                                    
         GOTO1 REPORT                                                           
         ST    R0,HEADHOOK                                                      
         XIT1                                                                   
         EJECT                                                                  
*              INFORMATION LIST                                                 
         SPACE 3                                                                
INFOLIST DS    0F                                                               
         DC    AL1(0),AL3(0)       DIRECTORY I/O                                
         DC    AL1(1),AL3(0)                                                    
         DC    AL1(2),AL3(0)                                                    
         DC    AL1(3),AL3(0)                                                    
         DC    AL1(4),AL3(0)                                                    
         SPACE 1                                                                
         DC    AL1(5),VL3(SPAGYBUF)     GETS                                    
         DC    AL1(5),VL3(SPCLTBUF)                                             
         DC    AL1(5),VL3(SPPRDBUF)                                             
         DC    AL1(5),VL3(SPESTBUF)                                             
         DC    AL1(5),VL3(SPBILBUF)                                             
         DC    AL1(5),VL3(SPBUYBUF)                                             
         DC    AL1(5),VL3(SPADVBUF)                                             
         DC    AL1(5),VL3(SPPRFBUF)                                             
         DC    A(0)                                                             
         DC    AL1(5),VL3(SPBUYBUF)                                             
         DC    AL1(5),VL3(SPPGRBUF)                                             
         DC    AL1(5),VL3(SPBUYBUF)                                             
         SPACE 1                                                                
         DC    AL1(6),VL3(SPESTBUF)     PUTS                                    
         DC    AL1(6),VL3(SPBILBUF)                                             
         DC    AL1(6),VL3(SPBUYBUF)                                             
         DC    AL1(6),VL3(SPBUYBUF)                                             
         SPACE 1                                                                
         DC    AL1(7),VL3(SPBILBUF)     ADDS                                    
         SPACE 1                                                                
         DC    AL1(8),VL3(SPSTABUF)     STATION READS                           
         DC    AL1(8),VL3(SPSADBUF)                                             
         DC    AL1(8),VL3(SPMKTBUF)                                             
         DC    AL1(8),VL3(SPREPBUF)                                             
         SPACE 1                                                                
         DC    AL1(10),VL3(SPSTABUF)     STATION SEQ                            
         DC    AL1(10),VL3(SPSADBUF)                                            
         DC    AL1(10),VL3(SPMKTBUF)                                            
         DC    AL1(10),VL3(SPREPBUF)                                            
         SPACE 1                                                                
         DC    AL1(9),VL3(SPSTABUF)     STATION HIGH                            
         DC    AL1(9),VL3(SPSADBUF)                                             
         DC    AL1(9),VL3(SPMKTBUF)                                             
         DC    AL1(9),VL3(SPREPBUF)                                             
         SPACE 1                                                                
         DC    AL1(5),AL3(0)            GET                                     
         DC    AL1(6),AL3(0)            PUT                                     
         DC    AL1(7),AL3(0)            ADD                                     
         EJECT                                                                  
       ++INCLUDE SPMGRTAB                                                       
         EJECT                                                                  
*==============================================================                 
* SUPPRESS ALL BUY/GOAL COSTS FOR USERID MVMFI                                  
*==============================================================                 
         SPACE 1                                                                
ZAPDOLS  NTR1  BASE=*,LABEL=*                                                   
         MVI   RCWRITE,C'N'        NEVER LET THIS ID WRITE A RECORD!            
*                                                                               
         L     R2,DMCB+12          GET RECORD ADDRESS                           
         L     RE,DMCB+4           GET A(FILENAME)                              
         CLC   0(3,RE),=C'SPT'                                                  
         BNE   ZAPXSP                                                           
* SPTFILE                                                                       
         CLI   0(R2),X'10'                                                      
         BH    ZAPB                                                             
         CLI   0(R2),X'02'         TEST GOAL                                    
         BE    ZAPG                                                             
         B     ZAPX                                                             
*                                                                               
ZAPXSP   CLC   0(3,RE),=C'XSP'                                                  
         BNE   ZAPX                                                             
* XSPFILE                                                                       
         CLC   0(2,R2),=X'0D73'    STATION LOCKIN                               
         BE    ZAPL                                                             
         B     ZAPX                                                             
*                                                                               
         USING BUYREC,R2                                                        
ZAPB     XC    BDCOST,BDCOST                                                    
         MVI   BDCIND,X'20'                                                     
         MVI   BDCIND2,0                                                        
         CLI   BUYKPRD,X'FF'                                                    
         BNE   ZAPX                                                             
         LA    R2,BDELEM                                                        
         SR    R0,R0                                                            
         DROP  R2                                                               
*                                                                               
ZAPB2    ICM   R0,1,1(R2)                                                       
         AR    R2,R0                                                            
         CLI   0(R2),0                                                          
         BE    ZAPX                                                             
         CLI   0(R2),X'0B'                                                      
         BL    ZAPB2                                                            
         CLI   0(R2),X'0C'                                                      
         BH    ZAPB2                                                            
         USING REGELEM,R2                                                       
         NI    RSTATUS,X'FF'-X'20'                                              
         XC    RPCOST,RPCOST                                                    
         B     ZAPB2                                                            
         DROP  R2                                                               
*                                                                               
         USING GOALREC,R2                                                       
ZAPG     LA    R2,GDELEM                                                        
         SR    R0,R0                                                            
         DROP  R2                                                               
ZAPG2    ICM   R0,1,1(R2)                                                       
         AR    R2,R0                                                            
         CLI   0(R2),0                                                          
         BE    ZAPX                                                             
         CLI   0(R2),X'21'                                                      
         BNE   ZAPG4                                                            
         USING GLEMENT,R2                                                       
         XC    GLBUDGET,GLBUDGET                                                
         B     ZAPG2                                                            
         DROP  R2                                                               
*                                                                               
ZAPG4    CLI   0(R2),X'30'                                                      
         BL    ZAPG2                                                            
         CLI   0(R2),X'33'                                                      
         BH    ZAPG2                                                            
         USING GLKELEM,R2                                                       
         XC    GLKDLR,GLKDLR                                                    
         B     ZAPG2                                                            
         DROP  R2                                                               
*                                                                               
         USING SLKRECD,R2                                                       
ZAPL     LA    R2,SLKEL                                                         
         SR    R0,R0                                                            
         DROP  R2                                                               
*                                                                               
ZAPL2    ICM   R0,1,1(R2)                                                       
         AR    R2,R0                                                            
         CLI   0(R2),0                                                          
         BE    ZAPX                                                             
         CLI   0(R2),X'03'                                                      
         BNE   ZAPL2                                                            
         USING LOKEL,R2                                                         
         XC    LOKDOLS,LOKDOLS                                                  
         XC    LOKNET(12),LOKNET                                                
         B     ZAPL2                                                            
         DROP  R2                                                               
ZAPX     XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*              DSECT FOR MODULE                                                 
SPDMD    DSECT                                                                  
MYPARA   DS    6F                                                               
MYWORK   DS    CL30                                                             
TRACEWRK DS    CL30                                                             
TRACEKEY DS    CL32                                                             
SAVENUM  DS    F                                                                
DUMMKT   DS    CL4                                                              
GROUPSW  DS    C                                                                
SPDMDX   EQU   *                                                                
         PRINT OFF                                                              
       ++INCLUDE SPREPMODES                                                     
         PRINT OFF                                                              
         EJECT                                                                  
       ++INCLUDE SPREPWORKD                                                     
       ++INCLUDE SPGENAGY                                                       
       ++INCLUDE SPGENCLT                                                       
       ++INCLUDE SPGENPRD                                                       
       ++INCLUDE SPGENEST                                                       
       ++INCLUDE SPGENBILL                                                      
       ++INCLUDE SPGENGOAL                                                      
       ++INCLUDE SPGENADV                                                       
       ++INCLUDE SPGENDAYPT                                                     
       ++INCLUDE SPGENBUY                                                       
       ++INCLUDE SPGENPRG                                                       
       ++INCLUDE SPGENMKG                                                       
       ++INCLUDE SPGENREP                                                       
       ++INCLUDE SPGENMKT                                                       
       ++INCLUDE SPGENXLK                                                       
       ++INCLUDE DDMASTD                                                        
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'034SPREPDM   11/14/17'                                      
         END                                                                    
