*          DATA SET FAVERCHK   AT LEVEL 002 AS OF 04/24/02                      
*CATALP FAVERCHK                                                                
         TITLE 'FAVERCHK - CHECK VERSION TABLE FOR DISALLOWED PROGRAMS'         
***********************************************************************         
*                                                                     *         
* THIS MODULE WILL SCAN THE VERSION TABLE FOR SPECIAL ENTRIES WHICH   *         
* HAVE NO TEST LEVEL. THESE ENTRIES INDICATE THAT A PARTICULAR        *         
* PROGRAM IS TO BE UNAVAILABLE FOR A PARTICULAR SE SYSTEM (OR         *         
* SYSTEMS) ON A GIVEN FAC REGION, WHILE REMAINING AVAILABLE FOR OTHER *         
* SE SYSTEMS ON THAT FAC REGION.                                      *         
*                                                                     *         
* P1 = XL1: RETURN CODE (X'FF' = PROGRAM UNAVAILABLE, OTHERWISE 0)    *         
*      AL3: A(SYSFAC)                                                 *         
* P2 = XL2: SYSTEM/PROGRAM (E.G., X'0213' = SPOT/PAY)                 *         
*      XL1: SENUM (E.G., X'02' = SPOT1)                               *         
*      XL1: 0                                                         *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
         PRINT NOGEN                                                            
FAVERCHK CSECT                                                                  
         NMOD1 0,**VRCK**                                                       
         LA    RE,*+6              SET 24-BIT MODE                              
         BSM   0,RE                                                             
*                                                                               
         LR    R3,R1               SAVE A(PARMS)                                
*                                                                               
         SR    R9,R9                                                            
         ICM   R9,7,1(R3)          R9=A(SYSFACS)                                
         BNZ   *+6                                                              
         DC    H'0'                                                             
         USING SYSFACD,R9                                                       
*                                                                               
         L     RA,VSSB                                                          
         USING SSBD,RA                                                          
*                                                                               
         STAR  CLEAR=YES,ARS=OFF                                                
*                                                                               
         MVI   0(R3),0             ASSUME PROGRAM IS AVAILABLE                  
*                                                                               
         LAM   R4,R4,SSBTBLET                                                   
         ICM   R4,15,VVRSNTAB      GET VERSION TABLE ADDRESS                    
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         SAC   512                                                              
*                                                                               
         USING DMSPACED,R4                                                      
         XR    RE,RE               PREPARE FOR BXLE                             
         ICM   RE,3,DSPTWIDE                                                    
         ICM   RF,15,DSPTEND                                                    
         XR    R0,R0                                                            
         ICM   R0,7,DSPTFRST+1                                                  
         DROP  R4                                                               
         LR    R4,R0                                                            
         USING VRSNTABD,R4                                                      
*                                                                               
* FIRST LOOK FOR AN ENTRY FOR THIS PARTICULAR SENUM                             
*                                                                               
FV10     OC    VRSNPGM(VRSNLEN),VRSNPGM FIRST EMPTY = EOT                       
         BZ    FV30                                                             
         CLI   VRSNABC,0           LOOK AT ENTRIES WITHOUT A TEST LEVEL         
         BNE   FV20                                                             
         CLC   VRSNPGM,4(R3)       SAME OVSYS/PRG?                              
         BNE   FV20                                                             
         CLC   VRSNSEN,6(R3)       SAME SE SYSTEM?                              
         BNE   FV20                                                             
         TM    VRSNFLAG,VRSNNOOP   NO-OP FLAG ON?                               
         BZ    FVX                                                              
         MVI   0(R3),X'FF'         YES -- DON'T LET USER IN                     
         B     FVX                                                              
FV20     BXLE  R4,RE,FV10                                                       
*                                                                               
* NOW LOOK FOR AN ENTRY FOR THIS FAC REGION                                     
*                                                                               
FV30     LR    R4,R0               POINT BACK TO BEGINNING OF TABLE             
FV40     OC    VRSNPGM(VRSNLEN),VRSNPGM FIRST EMPTY = EOT                       
         BZ    FVX                                                              
         CLI   VRSNABC,0           LOOK AT ENTRIES WITHOUT A TEST LEVEL         
         BNE   FV50                                                             
         CLC   VRSNPGM,4(R3)       SAME OVSYS/PRG?                              
         BNE   FV50                                                             
         CLC   VRSNADV,SSBSYSID    SAME FACPAK?                                 
         BNE   FV50                                                             
         TM    VRSNFLAG,VRSNNOOP   NO-OP FLAG ON?                               
         BZ    FVX                                                              
         MVI   0(R3),X'FF'         YES -- DON'T LET USER IN                     
         B     FVX                                                              
FV50     BXLE  R4,RE,FV40                                                       
         DROP  R4                                                               
*                                                                               
FVX      REAR  ARS=OFF                                                          
*                                                                               
         XMOD1                                                                  
         ORG   *-2                                                              
         BSM   0,RE                RESTORE CALLERS ADDRESSING MODE              
         SPACE 3                                                                
         LTORG                                                                  
         EJECT                                                                  
*FAVRSNTAB                                                                      
       ++INCLUDE FAVRSNTAB                                                      
         SPACE 2                                                                
*FAUTL                                                                          
         PRINT OFF                                                              
       ++INCLUDE FAUTL                                                          
         PRINT ON                                                               
         SPACE 2                                                                
*FASSB                                                                          
         PRINT OFF                                                              
       ++INCLUDE FASSB                                                          
         PRINT ON                                                               
         SPACE 2                                                                
*FASYSFAC                                                                       
         PRINT OFF                                                              
       ++INCLUDE FASYSFAC                                                       
         PRINT ON                                                               
         SPACE 2                                                                
*DMSPACED                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMSPACED                                                       
         PRINT ON                                                               
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002FAVERCHK  04/24/02'                                      
         END                                                                    
