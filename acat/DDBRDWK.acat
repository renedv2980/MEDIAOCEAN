*          DATA SET DDBRDWK    AT LEVEL 016 AS OF 05/01/02                      
*CATALP BRDWK                                                                   
         TITLE '*** BRDWK - BROADCAST DATE GENERATOR ***'                       
*                                                                               
* REGISTER 1 PARAMETER LIST -                                                   
*                                                                               
*              WORD 1 BYTE 0 - INPUT FORMAT CODE                                
*                              X'00' - 6 BYTE EBCDIC YYMMDD                     
*                              X'01' - 3 BYTE BINARY YMD                        
*                              X'02' - 2 BYTE CMPRSD YMD                        
*        IF BRDCST MONTH DATES ARE REQ'D, OI BYTE 0 WITH X'80'                  
*                                                                               
*                   BYTES 1-3- ADDRESS OF FROM-TO DATES CONSECUTIVELY           
*                              LOCATED IN USER CORE                             
*                                                                               
*              WORD 2 BYTE 0 - OUTPUT FORMAT CODE                               
*                              X'00' - 6 BYTE EBCDIC YYMMDD                     
*                              X'01' - 3 BYTE BINARY YMD                        
*                              X'02' - 2 BYTE CMPRSD YMD                        
*                              X'04' - 5 BYTE EBCDIC MMMDD                      
*        IF SUNDAY DATES ARE REQUIRED, OI BYTE 0 WITH X'80'                     
*                                                                               
*                   BYTES 1-3- ADDRESS OF LIST AREA IN USER CORE                
*                                                                               
*                                                                               
*        THE PARAMETER LIST IS RETURNED UNTOUCHED. THE END OF THE               
*              DATE LIST DENOTED BY XL2'00'                                     
*                                                                               
*              * * * DDS - E. CHINN - 1972                                      
*                                                                               
BRDWK    CSECT                                                                  
         NMOD1 6,*BRDWK*                                                        
*****    NMOD  6                                                                
         USING BRDWRK,12                                                        
         XC    BRDWRK(48),BRDWRK   CLEAR TEMP STOREAGE                          
         MVC   BPAR,0(1)                                                        
         TM    BPAR,128            TEST FOR MONTH OPTION                        
         BC    14,*+8                                                           
         MVI   BTYP,128            SET FOR MONTH DATES ONLY                     
         TM    BPAR+4,128          TEST FOR LAST SUNDAY OPTION                  
         BC    14,*+8              NO                                           
         MVI   BLSM,128            YES-TURN ON LAST-SUN-OF-MTH SW               
         NI    BPAR,X'7F'          TURN OFF MONTH BIT                           
         NI    BPAR+4,X'7F'        TURN OFF LST SUNDAY BIT                      
         LA    14,BRW01                                                         
         CLI   BPAR,1                                                           
         BC    4,BRDTZ             CONVERT USER DATES                           
         BC    8,BRDTB             TO 3-BYTE BINARY                             
         BC    2,BRDTC             AND STORE IN BRDWRK                          
*                                                                               
BRW01    EQU   *                                                                
         LA    9,DAY1TAB-3                                                      
         CLI   BLSM,0              TEST IF LST SUN OPTION                       
         BE    *+8                 NO                                           
         LA    9,LSMTAB-3          YES-POINT LST SUN TBL                        
         CLC   3(3,9),BDTF+1       FIND 1ST MONDAY OF CORRECT YEAR              
         BC    2,*+12                                                           
         LA    9,3(9)                                                           
         B     *-14                                                             
*                                                                               
         MVC   BDATE+1(3),0(9)     MOVE 1ST MONDAY TO BDATE                     
         SR    0,0                                                              
         SR    1,1                                                              
         SR    2,2                                                              
         IC    0,BDATE+1           YEARS IN R0                                  
         IC    1,BDATE+2           MONTHS IN R1                                 
         IC    2,BDATE+3           DAYS IN R2                                   
         MVC   BDTER,BDATE         INITIALIZE NEXT WEEK DATE                    
*                                                                               
         LA    3,MTHDAYTB-2(1)     POINT REG 3                                  
         LA    3,0(1,3)            TO HALF WORD NO OF DAYS IN MONTH             
         LA    8,2                 SET BCT REG FOR EXTRA DATE PAST END          
*                                                                               
BRW01R   TM    BTYP,128            TEST IF BRDMN MODE                           
         BC    1,BRW05             YES                                          
*                                                                               
BRW02    LA    2,7(2)              BUMP DAYS BY 7 TO NEXT MONDAY                
         CH    2,0(3)              TEST IF OVER MNTH LMT                        
         BC    13,BRW04            NO                                           
         CH    2,=H'29'            TEST IF FEB29                                
         BC    2,BRW03             NO                                           
         TM    BDTER+1,3           TEST IF LEAP YESR                            
         BC    8,BRW04             YES - NOT OVER MONTH LIMIT                   
BRW03    BAS   14,BMPMTH           BUMP BDTER TO NXT MNTH                       
BRW04    STC   2,BDTER+3           UPDTE DAY                                    
         TM    BLSM,128            EST FOR SUNDAY OPTION                        
         BC    8,*+14              NO                                           
         CLC   BDATE,BDTF          SUPPRESS 1ST DATE                            
         BC    4,*+18                                                           
         CLC   BDTER,BDTF          TEST IF WITHIN RANGE                         
         BC    12,*+8              NO                                           
         BAS   14,PUTDTE           YES - STORE PREVIOUS IN USER TABLE           
         MVC   BDATE,BDTER         MOVE NXT WK DATE                             
         B     BRW10               TEST IF FINISHED                             
*                                                                               
* BRDCST MNTH RTN                                                               
*                                                                               
BRW05    LA    2,7(2)              BUMP TO NXT WK                               
         CH    2,0(3)              TEST IF OVER MONTH LIMIT                     
         BC    2,*+12              YES - TEST FOR OUTPUT                        
BRW06    STC   2,BDTER+3           UPDTE BDTER DAYS                             
         B     BRW05               TRY NXT WK                                   
         CH    2,=H'29'            TEST IF FEB29                                
         BC    7,*+12              NO                                           
         TM    BDTER+1,3           TEST IF LEAP YEAR                            
         BC    8,BRW06             YES - TRY NXT WK                             
         L     5,BPAR+4            SAVE OUTPUT ADDRESS                          
BRW07    BAS   14,PUTDTE           PUT BDATE TO USER LIST                       
         CLI   BLSM,0              TEST IF LSM MODE                             
         BC    7,BRW08             YES                                          
         MVC   BDATE,BDTER         SAVE NXT BRDMON DATE                         
         BAS   14,BMPMTH           BUMP TO NXT MTH                              
         TM    BTYP,2              TEST IF MTH STARTS ON 1ST                    
         BC    14,*+10                                                          
         MVC   BDATE,BDTER         YES-BDATE MUST BE REVISED                    
         NI    BTYP,253                                                         
         CLC   BDATE,BDTF          TEST IF PREV BRDMON DATE WITHIN RANG         
         BC    2,BRW10             YES                                          
         ST    5,BPAR+4            NO-RESET PNTR                                
         MVI   BCNT+3,0            RESET OUTPUT COUNTER                         
         B     BRW10                                                            
* LAST-SUNDAY-OF-MONTH RTN                                                      
BRW08    CLC   BDATE,BDTF          TEST IF WITHIN RANGE                         
         BC    2,BW09+8            YES                                          
         BC    4,BW09              NO                                           
         CLC   BDATE,BDTER         IF EQUAL, TEST IF 1ST CYCLE                  
         BC    2,*+12              NO - DATE IS OK                              
BW09     ST    5,BPAR+4            NO - RESET PNTR                              
         MVI   BCNT+3,0            RESSET OUTPUT COUNTER                        
         MVC   BDATE,BDTER         SAVE NXT DATE                                
         BAS   14,BMPMTH           BUMP TO NXT MTH                              
         CLC   BDATE,BDTT          EST IF NXY DATE SHOULD BE LAST               
         BC    7,BRW10                                                          
         BCT   8,BRW10             YES IF EXACTLY EQUAL TO BDTT                 
*                                                                               
* TEST IF FINISHED                                                              
*                                                                               
BRW10    CLC   BDATE,BDTT          TEST IF PUT PAST REQUEST END DATE            
         BC    13,BRW01R           NO - RETURN                                  
         BCT   8,BRW01R            PUT EXTRA DATE AFTER END                     
*                                                                               
         XMOD1 1                                                                
*                                  CONVERT INPUT EBCDIC DATES TO BINARY         
BRDTZ    L     1,BPAR                                                           
         SLL   1,8                                                              
         SRL   1,8                                                              
         LA    2,BDTF                                                           
         LA    3,1                 SET EXECUTE LENGTH                           
         LA    4,6                 SET BCT VALUE                                
BRDTZ1   EX    3,BPCK              PACK DUB,0(0,1)                              
         CVB   5,BDUB                                                           
         STC   5,0(2)                                                           
         LA    1,2(1)              BUMP TO NEXT FIELD                           
         LA    2,1(2)              BUMP OUTPUT TO NEXT FIELD                    
         BCT   4,BRDTZ1                                                         
*                                                                               
         LM    2,3,BDTF                                                         
         SRDL  2,8                 RIGHT JUSTIFY                                
         SRL   3,8                 FROM AND TO DATES                            
         STM   2,3,BDTF            STORE                                        
         BR    14                  RETURN                                       
*                                                                               
BPCK     PACK  BDUB,0(0,1)                                                      
*                                                                               
BRDTB    L     1,BPAR                                                           
         SLL   1,8                                                              
         SRL   1,8                                                              
         MVC   BDTF(6),0(1)        MOVE TO WORD ALIGN                           
         LM    2,3,BDTF                                                         
         SRDL  2,8                 SEPARATE FROM                                
         SRL   3,8                               AND TO DATES                   
         STM   2,3,BDTF            STORE IN WORK AREAS                          
         BR    14                                                               
*                                                                               
BRDTC    L     1,BPAR              CONVERT 2 BYTE DATE TO 3 BYTE BINARY         
         SLL   1,8                                                              
         SRL   1,8                                                              
         MVC   BDTF,0(1)           MOVE 4 BYTES TO WORD ALIGN                   
         L     2,BDTF              LOAD FROM AND TO DATES                       
         LA    4,BDTT                                                           
         LA    5,2                 SET BCT VALUE                                
BRDTC1   SRDL  2,5                 SHIFT DAY TO R3                              
         SRL   3,27                RT JSTFY                                     
         STC   3,3(4)              STORE DAY                                    
         SRDL  2,4                 SHIFT MTH TO R3                              
         SRL   3,28                RT JSTFY                                     
         STC   3,2(4)              STORE MONTH                                  
         SRDL  2,7                 SHIFT YR TO R3                               
         SRL   3,25                RT JSTFY                                     
         STC   3,1(4)              STORE YEAR                                   
         MVI   0(4),0              ZERO BYTE 0                                  
         LA    4,BDTF              POINT FROM DATE                              
         BCT   5,BRDTC1                                                         
         BR    14                                                               
*                                                                               
* * * BUMP REGISTER DATE TO NEXT MONTH * * *                                    
*                                                                               
BMPMTH   SH    2,0(3)              SUB DAY LIMIT FOR MONTH                      
         CLC   0(2,3),=H'28'       TEST IF FEB                                  
         BNE   BMP01               NO                                           
         TM    BDTER+1,3           TEST IF LEAP YEAR                            
         BC    7,BMP01             NO                                           
         BCTR  2,0                 SUB 1 FOR LEAP YEAR                          
         LTR   2,2                 TEST FOR FEB 29                              
         BC    2,BMP01             NO                                           
         LA    2,29                YES- RESET AND RETURN                        
         BR    14                                                               
*                                                                               
BMP01    LA    1,1(1)              BUMP MONTH                                   
         LA    3,2(3)                                                           
         CH    1,=H'12'            TEST FOR DEC + 1                             
         BC    13,BMP02            NO                                           
         LA    1,1                 YES- RESET TO JAN                            
         LA    3,MTHDAYTB          RESET TO JAN                                 
         AH    0,=H'1'             BUMP YEAR                                    
*                                                                               
BMP02    STC   2,BDTER+3           UPDTE DAY                                    
         STC   0,BDTER+1           STORE YEARS                                  
         STC   1,BDTER+2                       AND MONTH OF NEXT DATE           
         CH    2,=H'1'                                                          
         BCR   7,14                NO - RETURN                                  
         OI    BTYP,2                                                           
         BR    14                                                               
*                                                                               
* * * CONVERT DATE TO USER FORMAT AND STORE IN LIST                             
*                                                                               
PUTDTE   STM   14,12,12(13)                                                     
* MAXIMUM 14 MONTH DATES OR 54 WEEKLY DATES                                     
         L     15,BCNT                                                          
         LA    15,1(15)            BUMP OUTPUT COUNT                            
         LA    14,54                                                            
         TM    BTYP,128            TEST IF MNTHLY MODE                          
         BC    8,*+8                                                            
         LA    14,14               SET MTHLY LIMIT                              
         CR    15,14                                                            
         BC    2,PDTX                                                           
         ST    15,BCNT             UPDTE COUNTER                                
         L     15,BPAR+4                                                        
         CLI   BPAR+4,1            TEST OUTPUT FORMAT CODE                      
         BC    11,*+12                                                          
         LA    9,5                 SET LGTH FOR 6 CHAR EBCDIC                   
         B     PDT1                BR TO CONVERT                                
         LA    9,2                 SET LGTH FOR 3 BYTE BINARY                   
         BC    8,PDT2              BR TO CONVERT                                
         CLI   BPAR+4,4            TEST OUTPUT FORMAT CODE AGAIN                
         BC    8,*+12                                                           
         LA    9,1                 SET LGTH FOR 2 BYTE CMPRSD                   
         B     PDT3                BR TO CONVERT                                
         LA    9,4                 SET LGTH FOR 5 BYTE LITERAL                  
*                                                                               
PDT0     BAS   14,PDTPAR           UPDATE POINTER ADDRESS                       
         LH    2,BDATE+2           LOAD MTH & DAY IN R2                         
         SRDL  2,8                 MOVE DAY TO R3                               
         SRL   3,24                RT JSTFY                                     
         CVD   3,BDUB                                                           
         OI    BDUB+7,15                                                        
         UNPK  BDUB+2(3),BDUB+6(2)                                              
         MH    2,=H'3'             MULT MTH BY 3                                
         LA    1,MTHTAB-3(2)       POINT TO MONTH NAME ABBR                     
         MVC   BDUB(3),0(1)                                                     
         EX    9,MVLST             MVC  0(0,15),BDUB                            
         B     PDTX                EXIT                                         
*                                                                               
* * * CONVERT DATE TO 6 BYTE EBCDIC YYMMDD                                      
*                                                                               
PDT1     BAS   14,PDTPAR           UPDATE LIST ADDRESS                          
         LH    1,BDATE             YEAR IN R1                                   
         LH    2,BDATE+2           '                                            
         SRDL  2,8                 MONTH IN R2, DAY IN R3                       
         SRL   3,24                                                             
         CVD   1,BDUB                                                           
         BAS   14,PDT1PK           UNPK                                         
         L     1,BDUB              SAVE                                         
         CVD   2,BDUB                                                           
         BAS   14,PDT1PK           UNPK                                         
         L     2,BDUB              SAVE                                         
         CVD   3,BDUB                                                           
         BAS   14,PDT1PK           UNPK                                         
         L     3,BDUB              SAVE                                         
         STH   1,BDUB                                                           
         STH   2,BDUB+2                                                         
         STH   3,BDUB+4                                                         
         MVC   0(6,15),BDUB                                                     
         B     PDTX                EXIT                                         
*                                                                               
PDT1PK   OI    BDUB+7,15           REMOVE SIGN                                  
         UNPK  BDUB(4),BDUB+6(2)   UNPACK AND WORD ALIGN                        
         BR    14                  RETURN                                       
*                                                                               
* * * MOVE 3 BYTE DATE TO USER LIST                                             
*                                                                               
PDT2     BAS   14,PDTPAR           UPDATE LIST ADDRESS                          
         MVC   0(3,15),BDATE+1     MOVE TO LIST                                 
         B     PDTX                EXIT                                         
*                                                                               
* * * CONVERT 3 BYTE BINARY DATE TO 2 BYTE COMPRSD                              
*                                                                               
PDT3     BAS   14,PDTPAR           UPDATE LIST ADDRESS                          
         SR    0,0                                                              
         SR    1,1                                                              
         SR    2,2                                                              
         IC    0,BDATE+1           YEAR                                         
         IC    1,BDATE+2           MONTH                                        
         IC    2,BDATE+3           DAY                                          
         SLL   0,25                POSITION                                     
         SLL   1,21                         FOR                                 
         SLL   2,16                             COMPRESSION                     
         AR    0,1                                                              
         AR    0,2                                                              
         ST    0,BDUB                                                           
         EX    9,MVLST             MOVE TO LIST                                 
         B     PDTX                EXIT                                         
*                                                                               
MVLST    MVC   0(0,15),BDUB                                                     
*                                                                               
PDTPAR   LA    8,1(9,15)           BUMP ADDRESS BY OUTPUT LENGTH                
         XC    0(2,8),0(8)         E-O-L INDICATOR                              
         XC    BPAR+5(3),BPAR+5                                                 
         O     8,BPAR+4                                                         
         ST    8,BPAR+4                                                         
         LA    15,0(15)            DROP FORMAT CODE                             
         BR    14                  RETURN                                       
*                                                                               
PDTX     LM    14,12,12(13)                                                     
         BR    14                                                               
         EJECT                                                                  
* * * DATE CONSTANTS FOR BRDWK                                                  
*                                                                               
DAY1TAB  DC    X'460C1C'           FIRST MONDAY 1971 - 701228                   
         DC    X'470C1B'           FIRST MONDAY 1972 - 711227                   
         DC    X'490101'           FIRST MONDAY 1973 - 730101                   
         DC    X'490C1F'           FIRST MONDAY 1974 - 731231                   
         DC    X'4A0C1E'           FIRST MONDAY 1975 - 741230                   
         DC    X'4B0C1D'           FIRST MONDAY 1976 - 751229                   
         DC    X'4C0C1B'           FIRST MONDAY 1977 - 761227                   
         DC    X'4D0C1A'           FIRST MONDAY 1978 - 771226                   
         DC    X'4F0101'           FIRST MONDAY 1979 - 790101                   
         DC    X'4F0C1F'           FIRST MONDAY 1980 - 791231                   
         DC    X'500C1D'           FIRST MONDAY 1981 - 801229                   
         DC    X'510C1C'           FIRST MONDAY 1982 - 811228                   
         DC    X'520C1B'           FIRST MONDAY 1983 - 821227                   
         DC    X'530C1A'           FIRST MONDAY 1984 - 831226                   
         DC    X'540C1F'           FIRST MONDAY 1985 - 841231                   
         DC    X'550C1E'           FIRST MONDAY 1986 - 851230                   
         DC    AL1(86,12,29)       FIRST MONDAY 1987 - DEC29/86                 
*                                                                               
* LAST SUNDAY OF YEAR TABLE                                                     
*                                                                               
         DC    X'000000'                                                        
LSMTAB   DC    X'460C1B'           LAST SUNDAY 1970 - 701227                    
         DC    X'470C1A'                              711226                    
         DC    X'480C1F'                              721231                    
         DC    X'490C1E'                              731230                    
         DC    X'4A0C1D'                              741229                    
         DC    X'4B0C1C'                              751228                    
         DC    X'4C0C1A'                              761226                    
         DC    X'4D0C19'                              771225                    
         DC    X'4E0C1F'                              781231                    
         DC    X'4F0C1E'                              791230                    
         DC    X'500C1C'           LAST SUNDAY 1980 - 801228                    
         DC    X'510C1B'           LAST SUNDAY 1981 - 811227                    
         DC    X'520C1A'           LAST SUNDAY 1982 - 821226                    
         DC    X'530C19'           LAST SUNDAY 1983 - 831225                    
         DC    X'540C1E'           LAST SUNDAY 1984 - 841230                    
         DC    X'550C1D'           LAST SUNDAY 1985 - 851229                    
         DC    X'560C1C'           LAST SUNDAY 1986 - 861228                    
         DC    AL1(87,12,27)       LAST SUNDAY 1987 - DEC27/87                  
*                                                                               
MTHDAYTB DC    H'31',H'28',H'31',H'30',H'31',H'30',H'31',H'31',H'30'            
         DC    H'31',H'30',H'31'                                                
*                                                                               
MTHTAB   DC    C'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'                          
*                                                                               
         TITLE '* * * WORK DSECT FOR BRDWK * * *'                               
BRDWRK   DSECT                                                                  
BPAR     DS    D                   USER PARAMETER LIST                          
BDTF     DS    F                   FROM DATE - 3 BYTE BINARY YMD                
BDTT     DS    F                    TO  DATE - 3 BYTE BINARY YMD                
BDATE    DS    F                   BROADCAST WEEK DATE YMD                      
BDTM     DS    F                   NEXT BRDCST MONTH                            
BDTER    DS    F                   NEXT BRDCST WEEK DATE                        
BCNT     DS    F                   OUTPUT COUNTER                               
BDUB     DS    D                   DOUBLE WORD WORK AREA                        
BTYP     DS    C                   MODE SW                                      
BLSM     DS    C                   MON/SUN CTL SW                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'016DDBRDWK   05/01/02'                                      
         END                                                                    
