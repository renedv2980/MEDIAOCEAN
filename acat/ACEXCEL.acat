*          DATA SET ACEXCEL    AT LEVEL 015 AS OF 12/10/14                      
*CATALP EXCEL                                                                   
         SPACE 3                                                                
***********************************************************************         
* ACEXCEL - MODULE TO VALIDATE  EXPENSE AND ANALYSIS FIELDS FOR X-JOBS*         
*           AND TO GENERATE THE NECESSARY ANALYSIS POSTINGS           *         
*                                                                     *         
* AT ENTRY  R1=A(EXCEL INTERFACE BLOCK)                               *         
*           MODULE ASSUMES CLIENT/PRODUCT/JOB HAVE BEEN EDITED        *         
*                                                                     *         
* ON EXIT - CC=EQ FOR NO ERRORS                                       *         
*           CC=NEQ FOR ERRORS - FVADDR AND FVMSGNO WILL BE SET        *         
*                                                                     *         
***********************************************************************         
EXCEL    TITLE 'X JOBS EXPENSE AND ANALYSIS POSTING VALIDATION'                 
EXCEL    CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 LWSX-LWSD,**EXCL**,R7,CLEAR=YES,RR=RE                            
         USING LWSD,RC             RC=A(LOCAL WORKING STORAGE)                  
         LR    R8,R1                                                            
         USING EXCELD,R8           R8=A(EXCEL INTERFACE BLOCK)                  
         ST    RE,RELO                                                          
         ST    RD,AWORK                                                         
         L     R9,EXCAGWS          R9=A(BATCH PROGRAM GLOBAL STORAGE)           
         USING WORKD,R9                                                         
         L     RA,ATWA             RA=A(TWA0)                                   
         USING TWAD,RA                                                          
         CLI   EXCACT,EXCAVAL      TEST FOR VALIDATION                          
         BE    EXCEL2              YES                                          
         CLI   EXCACT,EXCAPST      TEST FOR POSTING                             
         BE    EXCEL10                                                          
         B     ERR1                                                             
*                                                                               
* VALIDATE ACTION                                                               
*                                                                               
EXCEL2   BAS   RE,EXP              VALIDATE EXPENSE ACCOUNT                     
         MVC   OFFICE,BCSPACES                                                  
         MVC   EXCANAO,OFFICE      INITIALIZE ANALYSIS OFFICE                   
         MVC   DEPT,BCSPACES                                                    
         MVC   STAFF,BCSPACES                                                   
         XC    OFFICEL,OFFICEL                                                  
         XC    DEPTL,DEPTL                                                      
         XC    STAFFL,STAFFL                                                    
         CLI   EXCDPOS,0           TEST DEPT CODE IN EXPENSE KEY                
         BE    EXCEL4                                                           
*                                                                               
         ZIC   RE,EXCDPOS                                                       
         LA    RE,EXCSEAC+2(RE)                                                 
         ZIC   R1,EXCDLEN                                                       
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   DEPT(0),0(RE)       EXTRACT DEPT CODE                            
*                                                                               
EXCEL4   BAS   RE,VALOFF           GET AND VALIDATE OFFICE CODE                 
         BAS   RE,VALDEP           GET AND VALIDATE DEPARTMENT                  
         BAS   RE,VALSTF           GET AND VALIDATE STAFF                       
         CLI   EXCSTAFF,C'Y'       IS STAFF NEEDED ?                            
         BNE   *+8                 NO, SKIP VALIDATE                            
         BAS   RE,VALALL           VALIDATE FIELDS AGAINST HIERARCY             
         BAS   RE,OFF              GET THE OFFICE NAME                          
         CLI   EXCDEPT,C'Y'        TEST FOR DEPARTMENT ANALYSIS                 
         BNE   *+8                                                              
         BAS   RE,DEP              YES-CONSTRUCT AND VALIDATE KEYS              
         TM    BCCPYST5,CPYSNCST   TEST NEW COST ACCOUNTING                     
         BO    EXCEL5              YES                                          
*                                                                               
         MVI   EXCCOST,C'N'                                                     
         TM    BCCPYST1,CPYSCOST   TEST OLD COST ACCOUNTING                     
         BZ    EXCEL8                                                           
         CLI   COSTCODE,C' '       TEST NO ANALYSIS FLAG                        
         BE    *+8                                                              
         MVI   EXCCOST,C'Y'                                                     
         B     EXCEL8                                                           
*                                                                               
EXCEL5   LA    R1,CATBLK                                                        
         USING CATD,R1                                                          
         XC    CATD(CATLNQ),CATD                                                
         MVC   CATDMGR,VDMGR                                                    
         MVC   CATSEAC,EXCSEAC                                                  
         MVC   CATOFF,OFFICE                                                    
         MVC   CATDPT,DEPT                                                      
         GOTO1 VCATCALL                                                         
         CLI   CATERR,0                                                         
         BE    EXCEL6                                                           
         MVC   FVMSGNO,=AL2(AE$IANAL)                                           
         B     EXCELR                                                           
*                                                                               
EXCEL6   MVC   EXCCOST,CATPST      SAVE COST SWITCH                             
*                                                                               
EXCEL8   BAS   RE,SET1C            SET 1C ACCOUNT CODE                          
         CLI   EXCCOST,C'Y'        TEST FOR COST ACCOUNTING                     
         BNE   *+8                                                              
         BAS   RE,COST                                                          
         CLI   EXCSTAFF,C'Y'       TEST FOR STAFF ANALYSIS                      
         BNE   *+8                                                              
         BAS   RE,STF                                                           
         B     EXCELX                                                           
*                                                                               
* POST ACTION                                                                   
*                                                                               
EXCEL10  BAS   RE,POST                                                          
*                                                                               
* EXITS                                                                         
*                                                                               
EXCELX   MVC   FVMSGNO,=AL2(FVFOK)                                              
         MVI   FVERRNDX,0                                                       
         MVC   FVXTRA,BCSPACES                                                  
         CR    RB,RB               SET CC=EQ                                    
         B     EXIT                                                             
*                                                                               
EXCELR   L     RD,AWORK            SET FOR EXIT OUT OF MODULE                   
         CLI   *,0                 SET CC=NEQ                                   
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* VALIDATE EXPENSE ACCOUNT FIELD                                      *         
***********************************************************************         
         SPACE 1                                                                
EXP      NTR1  ,                                                                
         L     R1,EXCAEXP          R1=A(EXPENSE A/C FIELD)                      
         MVI   FVMINL,1            REQUIRE SOME INPUT                           
         GOTO1 AFVAL                                                            
         BNE   EXCELR                                                           
*                                                                               
EXP2     LA    R4,IOKEY                                                         
         USING ACTRECD,R4                                                       
         MVC   ACTKEY,BCSPACES                                                  
         MVC   ACTKCPY,CUABIN                                                   
         MVC   ACTKUNT(2),=C'SE'                                                
         CLI   FVIFLD,C'*'         TEST LEDGER OVERRIDE                         
         BE    EXP4                                                             
*                                                                               
         CLI   FVILEN,L'ACTKACT    TEST MORE CHARACTERS THAN A/C CODE           
         BNH   EXP3                                                             
         MVC   FVMSGNO,=AL2(FVFLONG)                                            
         MVI   FVERRNDX,L'ACTKACT                                               
         B     EXCELR                                                           
*                                                                               
EXP3     MVC   ACTKACT,FVIFLD      EXTRACT ACCOUNT CODE                         
         B     EXP10               NOW READ ACCOUNT AND VALIDATE                
*                                                                               
EXP4     CLC   FVIFLD+1(2),=C'SB'  TEST FOR OVERRIDE LEDGER                     
         BE    EXP6                                                             
         CLC   FVIFLD+1(2),=C'SF'                                               
         BE    EXP6                                                             
         CLC   FVIFLD+1(2),=C'SL'                                               
         BE    EXP6                                                             
         MVC   FVMSGNO,=AL2(FVFNOTV)                                            
         MVI   FVERRNDX,1                                                       
         B     EXCELR                                                           
*                                                                               
EXP6     CLI   FVILEN,3            TEST FOR ANY ACCOUNT CODE                    
         BH    *+14                                                             
         MVC   FVMSGNO,=AL2(FVFSHRT)                                            
         B     EXCELR                                                           
*                                                                               
         ZIC   R1,FVXLEN                                                        
         BCTR  R1,0                DEDUCT ONE FOR STAR                          
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   ACTKUNT(0),FVIFLD+1 MOVE UNIT/LEDGER/ACCOUNT                     
*                                                                               
* READ FOR ACCOUNT RECORD AND CHECK WHETHER ELEGIBLE FOR POSTING                
*                                                                               
EXP10    GOTO1 AGETACT,0                                                        
         BNE   EXCELR              NOTE ERROR FIELDS NOT SET HERE               
*                                                                               
         BAS   RE,CHECKACC         CHECK ACCOUNT FOR POSTING                    
         L     R4,AIO1             ADDRESS THE RECORD                           
         MVC   EXCSEAC,ACCODE      EXTRACT ACCOUNT KEY                          
         MVC   EXCSENM,ACNAME      EXTRACT ACCOUNT NAME                         
*                                                                               
         MVI   EXCDEPT,C'Y'        SET DEPARTMENT ANALYSIS FLAG                 
         TM    ACBSTAT,ACBSDEPT                                                 
         BO    *+8                                                              
         MVI   EXCDEPT,C'N'                                                     
*                                                                               
         MVI   EXCSTAFF,C'Y'                                                    
         TM    ACBSTAT,ACBSPERS                                                 
         BO    *+8                                                              
         MVI   EXCSTAFF,C'N'                                                    
*                                                                               
         MVC   COSTCODE,ACCOST     SAVE COST ANALYSIS BYTE                      
         LR    RE,R4                                                            
         SR    RF,RF                                                            
         ICM   RF,3,ACTRLEN                                                     
         LA    R0,EXPREC           A(EXPENSE RECORD)                            
         LA    R1,L'EXPREC                                                      
         MVCL  R0,RE               SAVE THE EXPENSE ACCOUNT                     
*                                                                               
         CLC   ACTKUNT(2),=C'SE'   TEST SE LEDGER                               
         BNE   EXP12                                                            
         ICM   R1,15,ACALDG        R1=A(LEDGER TABLE ENTRY)                     
         USING LDGTABD,R1                                                       
         ZIC   R0,LDGTDDL                                                       
         STC   R0,EXCDLEN                                                       
         NI    EXCDLEN,X'0F'       TURN OFF POSITION BITS                       
         SRL   R0,4                SHIFT OUT LENGTH BITS                        
         STC   R0,EXCDPOS                                                       
*                                                                               
EXP12    GOTO1 VHELLO,DMCB,(C'G',ACCMST),('RSTELQ',AIO1),0                      
         CLI   12(R1),0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         L     R6,12(R1)                                                        
         USING RSTELD,R6                                                        
         MVC   EXCCNTR,RSTCCTR                                                  
         MVC   EXCCPOS,RSTCCTRR                                                 
*                                                                               
EXP15    CLI   EXCSTAFF,C'Y'       TEST STAFF ANALYSIS                          
         BNE   EXPX                NO                                           
*                                                                               
         LA    R6,BCCPYEL                                                       
         USING CPYELD,R6                                                        
         CLI   CPYTENO,X'F0'       TEST OPTION FOR RHS OF EXP A/C               
         BL    EXPX                NO                                           
*                                                                               
         PACK  DUB,CPYTENO                                                      
         CVB   R0,DUB                                                           
         LA    R1,L'ACTKACT                                                     
         LA    RE,EXCSEAC+L'EXCSEAC-1                                           
         CLI   0(RE),C' '          COMPUTE L'SE A/C CODE                        
         BH    *+10                                                             
         BCTR  RE,0                                                             
         BCT   R1,*-10                                                          
         CR    R1,R0               TEST L'CODE >= TENO LENGTH                   
         BNL   EXPX                YES                                          
*                                                                               
         MVC   FVMSGNO,=AL2(FVFSHRT)                                            
         B     EXCELR                                                           
*                                                                               
EXPX     B     EXIT                                                             
         DROP  R1,R4,R6                                                         
         EJECT                                                                  
***********************************************************************         
* GET AND VALIDATE OFFICE CODE                                        *         
***********************************************************************         
         SPACE 1                                                                
VALOFF   NTR1  ,                                                                
         L     R1,EXCAANO                                                       
         GOTO1 AFVAL                                                            
         L     R2,EXCAANO                                                       
         BH    VALOERR                                                          
         BNE   VALO4               OFFICE WAS NOT INPUT                         
*                                                                               
         MVC   OFFICEL,FVILEN      GET L'FIELD                                  
         MVC   OFFICE,FVIFLD       GET OFFICE                                   
         MVC   EXCANAO,FVIFLD      SET ANALYSIS OFFICE                          
*                                                                               
VALO2    MVC   FVMSGNO,=AL2(FVFLONG)                                            
         CLC   OFFICEL,BCOFFLEN    TEST OFFICE INPUT TOO LONG                   
         BH    VALOERR                                                          
         GOTO1 AVALOFF,DMCB,(X'80',OFFICE)                                      
         BNE   VALOERR             ITS NOT OK                                   
         B     VALOX                                                            
*                                                                               
         USING ACLELD,R4                                                        
VALO4    CLI   EXC2DLEN,12         SPECIAL DEPT?                                
         BE    VALOX               YES, OFFICE NOT REQUIRED                     
         CLI   EXCDEPT,C'Y'        IS DEPARTMENT NEEDED ?                       
         BE    VALO6               YES, THEN WE NEED OFFICE                     
         CLI   EXCSTAFF,C'Y'       IS STAFF NEEDED ?                            
         BNE   VALOX               NO, EXIT                                     
*                                                                               
VALO6    MVC   FVMSGNO,=AL2(FVFNONE)                                            
         MVC   FVXTRA(6),=C'OFFICE'                                             
VALOERR  STCM  R2,15,EXCERRF                                                    
         B     EXCELR                                                           
*                                                                               
VALOX    B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* GET AND VALIDATE DEPARTMENT CODE                                    *         
***********************************************************************         
         SPACE 1                                                                
VALDEP   NTR1  ,                                                                
         L     R1,EXCADEP                                                       
         GOTO1 AFVAL                                                            
         L     R2,EXCADEP                                                       
         BH    VALDERR                                                          
         BNE   VALD4               DEPARTMENT WAS NOT INPUT                     
*                                                                               
         MVC   DEPTL,FVILEN                                                     
         MVC   DEPT,FVIFLD                                                      
*                                                                               
         CLI   EXCDEPT,C'Y'        IS DEPARTMENT NEEDED ?                       
         BE    VALD2               YES                                          
         CLI   EXCSTAFF,C'Y'       NO, IS STAFF NEEDED ?                        
         BE    VALD2               YES, THEN DEPARTMENT IS OK                   
         MVC   FLD,BCSPACES        NO, CLEAR THE FIELD                          
         BAS   RE,MOVEFLD                                                       
         B     VALDX                                                            
*                                                                               
VALD2    MVC   FVMSGNO,=AL2(FVFLONG)                                            
         CLC   DEPTL,BCDPTLEN      IS CODE TOO LONG ?                           
         BH    VALDERR             YES                                          
         B     VALDX                                                            
*                                                                               
VALD4    CLI   EXCDEPT,C'Y'        IS DEPARTMENT NEEDED ?                       
         BE    VALD6               YES                                          
         CLI   EXCSTAFF,C'Y'       NO, IS STAFF NEEDED                          
         BNE   VALDX               NO                                           
*                                                                               
VALD6    MVC   FVMSGNO,=AL2(FVFNONE)                                            
         MVC   FVXTRA(10),=C'DEPARTMENT'                                        
VALDERR  STCM  R2,15,EXCERRF                                                    
         B     EXCELR              YES, TELL THEM                               
*                                                                               
VALDX    B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* GET AND VALIDATE STAFF CODE                                         *         
***********************************************************************         
         SPACE 1                                                                
VALSTF   NTR1  ,                                                                
         L     R1,EXCASTF                                                       
         GOTO1 AFVAL                                                            
         L     R2,EXCASTF                                                       
         BH    VALSERR                                                          
         BNE   VALS4               STAFF WAS NOT INPUT                          
*                                                                               
         MVC   STAFFL,FVILEN                                                    
         MVC   STAFF,FVIFLD                                                     
*                                                                               
         CLI   EXCSTAFF,C'Y'       IS STAFF NEEDED ?                            
         BE    VALSX               YES                                          
         MVC   FLD,BCSPACES        NO, CLEAR THE FIELD                          
         BAS   RE,MOVEFLD                                                       
         B     VALSX                                                            
*                                                                               
VALS4    CLI   EXCSTAFF,C'Y'       IS STAFF REQUIRED ?                          
         BNE   VALSX               NO                                           
         MVC   FVMSGNO,=AL2(FVFNONE)                                            
         MVC   FVXTRA(5),=C'STAFF'                                              
VALSERR  STCM  R2,15,EXCERRF                                                    
         B     EXCELR              YES, TELL THEM                               
*                                                                               
VALSX    B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* VALIDATE ALL AGAINST HIERARCHY                                      *         
***********************************************************************         
         SPACE 1                                                                
VALALL   NTR1  ,                                                                
         USING LDGRECD,R4                                                       
         LA    R4,IOKEY                                                         
         MVC   LDGKEY,BCSPACES                                                  
         MVC   LDGKCPY,CUABIN                                                   
         MVC   LDGKUNT(2),=C'2P'   GET THE 2P LEDGER                            
         GOTO1 AGETLDG                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         ICM   R1,15,ACALDG                                                     
         USING LDGTABD,R1                                                       
         MVI   LEVEL,3                                                          
         ZIC   R5,LDGTLVB          DISPLACEMENT TO STAFF CODE                   
         CLI   LDGTLVC,L'ACTKACT   TEST 3 LEVEL LEDGER                          
         BE    VALA2               YES                                          
         ZIC   R5,LDGTLVA          DISPLACEMENT TO STAFF CODE                   
         MVI   LEVEL,2                                                          
         CLI   LDGTLVB,L'ACTKACT                                                
         BE    VALA2                                                            
         SR    R5,R5                                                            
         MVI   LEVEL,1                                                          
         CLI   LDGTLVA,L'ACTKACT                                                
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
VALA2    CLI   LEVEL,2             TEST STAFF LEVEL ONLY                        
         BL    VALA6               YES                                          
         BE    VALA4               DEPARTMENT/STAFF ONLY                        
         CLI   OFFICEL,0                                                        
         BNE   VALA4                                                            
         MVC   FVADDR,EXCAANO                                                   
         MVC   FVMSGNO,=AL2(FVFNONE)                                            
         MVC   FVXTRA(6),=C'OFFICE'                                             
         B     EXCELR                                                           
*                                                                               
VALA4    CLC   DEPT,BCSPACES       TEST DEPARTMENT AVAILABLE                    
         BNE   VALA6               YES                                          
*                                                                               
         MVC   FVADDR,EXCADEP                                                   
         MVC   FVMSGNO,=AL2(FVFNONE)                                            
         MVC   FVXTRA(10),=C'DEPARTMENT'                                        
         B     EXCELR                                                           
*                                                                               
VALA6    CLI   STAFFL,0                                                         
         BNE   VALA8                                                            
         MVC   FVADDR,EXCASTF                                                   
         MVC   FVMSGNO,=AL2(FVFNONE)                                            
         MVC   FVXTRA(5),=C'STAFF'                                              
         B     EXCELR                                                           
*                                                                               
VALA8    LA    RE,L'ACTKACT        COMPUTE L'STAFF CODE                         
         SR    RE,R5                                                            
         CLM   RE,1,STAFFL         TEST IF STAFF CODE TOO BIG                   
         BNL   VALAX               NO                                           
         MVC   FVADDR,EXCASTF                                                   
         MVC   FVMSGNO,=AL2(FVFLONG)                                            
         B     EXCELR                                                           
*                                                                               
VALAX    B     EXIT                                                             
         DROP  R1,R4                                                            
         EJECT                                                                  
***********************************************************************         
* READ OFFICE FOR NAME                                                *         
***********************************************************************         
         SPACE 1                                                                
         USING ACTRECD,R4                                                       
OFF      NTR1  ,                                                                
         LA    R4,IOKEY                                                         
         MVC   FVADDR,EXCAANO      POINT TO ANALYSIS FIELD                      
         MVC   ACTKEY,BCSPACES                                                  
         MVC   ACTKCPY,CUABIN                                                   
         MVC   ACTKUNT(2),=C'2D'   VALIDATE 2D ACCOUNT                          
         MVC   ACTKACT(L'OFFICE),OFFICE                                         
*                                                                               
         GOTO1 AGETACT,0                                                        
         BNE   EXCELR                                                           
         MVC   EXC2DNM,ACNAME                                                   
*                                                                               
OFFX     B     EXIT                                                             
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* VALIDATE DEPARTMENT ANALYSIS ACCOUNTS                               *         
***********************************************************************         
         SPACE 1                                                                
DEP      NTR1  ,                                                                
         MVC   FVADDR,EXCAEXP      SET ERROR POINTER TO EXPENSE                 
         LA    R4,IOKEY                                                         
         USING ACTRECD,R4                                                       
         MVC   ACTKEY,BCSPACES                                                  
         MVC   ACTKCULA,EXCSEAC    INITIALIZE WITH EXPENSE A/C                  
         MVC   ACTKUNT(2),=C'28'   VALIDATE 28 ACCOUNT                          
         GOTO1 AGETACT,0                                                        
         BNE   EXCELR                                                           
         BAS   RE,CHECKACC                                                      
         MVC   EXC28AC,ACCODE                                                   
         MVC   EXC28NM,ACNAME                                                   
*                                                                               
DEP2     MVC   FVADDR,EXCADEP      POINT TO DEPARTMENT FIELD                    
         MVC   ACTKEY,BCSPACES                                                  
         MVC   ACTKCPY,CUABIN                                                   
         MVC   ACTKUNT(2),=C'2D'   VALIDATE 2D ACCOUNT                          
         MVC   ACTKACT(L'OFFICE),OFFICE                                         
         CLI   EXC2DLEN,12                                                      
         BNE   *+12                                                             
         LA    R1,ACTKACT                                                       
         B     DEP5                                                             
         ZIC   R1,BCOFFLEN                                                      
         LA    R1,ACTKACT(R1)      INDEX INTO ACCOUNT CODE                      
DEP5     MVC   0(L'DEPT,R1),DEPT                                                
*                                                                               
         GOTO1 AGETACT,0                                                        
         BNE   EXCELR                                                           
         BAS   RE,CHECKACC                                                      
         MVC   EXC2DAC,ACCODE                                                   
         MVC   EXC2DNM,ACNAME                                                   
*                                                                               
DEPX     B     EXIT                                                             
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* VALIDATE COST ACCOUNTING ACCOUNTS                                   *         
***********************************************************************         
         SPACE 1                                                                
COST     NTR1  ,                                                                
         MVC   FVADDR,EXCAEXP      SET ERROR POINTER TO EXPENSE                 
         LA    R4,IOKEY                                                         
         USING ACTRECD,R4                                                       
         MVC   ACTKEY,BCSPACES                                                  
         TM    BCCPYST5,CPYSNCST   TEST NEW COST ACCOUNTING                     
         BO    COST2               YES                                          
*                                                                               
         MVC   ACTKCPY,CUABIN                                                   
         MVC   ACTKUNT(2),=C'13'                                                
         MVC   ACTKACT(1),COSTCODE                                              
         B     COST4                                                            
*                                                                               
COST2    LA    R1,CATBLK                                                        
         USING CATD,R1                                                          
         MVC   ACTKCULA,CATACC3                                                 
*                                                                               
COST4    MVC   EXC13AC,ACTKCULA    13 ACCOUNT CODE                              
*                                                                               
COST6    MVC   ACTKUNT(2),=C'1P'                                                
         MVC   ACTKACT,BCSPACES                                                 
         LA    R2,ACTKACT                                                       
         TM    BCCPYST5,CPYSNCST   ON NEW COST ACCOUNTING ?                     
         BZ    COST7               NO                                           
         MVC   0(12,R2),=C'999999999999'                                        
         B     COST10                                                           
*                                                                               
COST7    TM    BCCPYST1,CPYSOROE   TEST OFFICES REQUIRED                        
         BZ    COST8               NO                                           
*                                                                               
         ZIC   R1,BCOFFLEN                                                      
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),OFFICE                                                   
         LA    R2,1(R1,R2)         NEXT KEY POSITION                            
*                                                                               
COST8    LA    R3,=C'9999'         DEFAULT DEPARTMENT                           
         CLI   EXCDEPT,C'Y'        TEST FOR DEPARTMENT ANALYSIS                 
         BNE   *+8                                                              
         LA    R3,DEPT                                                          
         ZIC   R1,BCDPTLEN                                                      
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),0(R3)                                                    
         LA    R2,1(R1,R2)                                                      
         MVC   0(1,R2),COSTCODE                                                 
*                                                                               
COST10   GOTO1 AGETACT,0                                                        
         BNE   EXCELR                                                           
         BAS   RE,CHECKACC                                                      
         MVC   EXC1PAC,ACCODE                                                   
         MVC   EXC1PNM,ACNAME                                                   
*                                                                               
COST12   MVC   ACTKEY,BCSPACES                                                  
         MVC   ACTKCULA,EXC1CAC    VALIDATE 1C ACCOUNT                          
         GOTO1 AGETACT,0                                                        
         BNE   EXCELR                                                           
         BAS   RE,CHECKACC                                                      
         MVC   EXC1CNM,ACNAME                                                   
*                                                                               
COSTX    B     EXIT                                                             
         DROP  R1,R4                                                            
         EJECT                                                                  
***********************************************************************         
* VALIDATE STAFF ANALYSIS ACCOUNTS                                    *         
***********************************************************************         
         SPACE 1                                                                
STF      NTR1  ,                                                                
         MVC   FVADDR,EXCASTF                                                   
         LA    R4,IOKEY                                                         
         USING ACTRECD,R4                                                       
         MVC   ACTKEY,BCSPACES                                                  
         MVC   ACTKCULA,EXC1CAC    START WITH THE 1C ACCOUNT                    
         MVC   ACTKUNT(2),=C'29'   VALIDATE 29 ACCOUNT                          
         GOTO1 AGETACT,0                                                        
         BNE   EXCELR                                                           
         BAS   RE,CHECKACC                                                      
         MVC   EXC29AC,ACCODE                                                   
         MVC   EXC29NM,ACNAME                                                   
*                                                                               
* VALIDATE 2P ACCOUNT                                                           
*                                                                               
STF2     MVC   ACTKACT,BCSPACES                                                 
         MVC   ACTKUNT(2),=C'2P'                                                
         LA    R2,ACTKACT          R2=ACCOUNT CODE POINTER                      
         CLI   LEVEL,3             TEST 3 LEVEL LEDGER                          
         BL    STF4                NO                                           
         MVC   0(L'OFFICE,R2),OFFICE                                            
         ZIC   R1,BCOFFLEN                                                      
         LA    R2,0(R1,R2)         POINT TO NEXT FIELD                          
*                                                                               
STF4     CLI   LEVEL,2             TEST FOR AT LEAST 2 LEVELS                   
         BL    STF6                NO                                           
         MVC   0(L'DEPT,R2),DEPT   INSERT DEPARTMENT IN KEY                     
         ZIC   R1,BCDPTLEN                                                      
         LA    R2,0(R1,R2)                                                      
*                                                                               
STF6     ZIC   R1,STAFFL                                                        
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),STAFF                                                    
         GOTO1 AGETACT,0                                                        
         BNE   EXCELR                                                           
         BAS   RE,CHECKACC                                                      
         MVC   EXC2PAC,ACCODE                                                   
         MVC   EXC2PNM,ACNAME                                                   
*                                                                               
STFX     B     EXIT                                                             
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* GENERATE POSTINGS BASED ON INTERFACE BLOCK VALUES                   *         
***********************************************************************         
         SPACE 1                                                                
POST     NTR1  ,                                                                
         LA    R6,TRSELEM                                                       
         USING TRSELD,R6                                                        
         MVI   TRSEL,TRSELQ        BUILD SKELETAL TRANS. STATUS ELEMENT         
         MVI   TRSLN,TRSLNQ                                                     
         OI    TRSSTAT3,TRSSXJOB   SET XJOB BIT                                 
*                                                                               
         CLC   EXCCLNT,BCSPACES                                                 
         BE    POST1                                                            
         LA    R6,FFTELEM                                                       
         USING FFTELD,R6                                                        
         MVI   FFTEL,FFTELQ                                                     
         MVI   FFTLN,FFTLN1Q+L'FFTDLEN+L'FFTCLPRA                               
         MVI   FFTTYPE,FFTTCLPR                                                 
         MVI   FFTSEQ,0                                                         
         MVI   FFTDLEN,L'FFTCLPRA                                               
         MVC   FFTCLAC,EXCCLNT                                                  
*                                                                               
         CLC   EXCPROD,BCSPACES                                                 
         BE    *+10                                                             
         MVC   FFTPRAC,EXCPROD                                                  
         OC    FFTCLPRA,BCSPACES                                                
*                                                                               
POST1    CLC   EXCANAO,BCSPACES    TEST FOR ANALYSIS OFFICE                     
         BE    POST2               NO                                           
         CLC   EXCANAO,EXCFINO     ARE THE OFFICES THE SAME ?                   
         BE    POST2               YES                                          
*                                                                               
         LA    R6,ANOELEM                                                       
         USING ANOELD,R6                                                        
         MVI   ANOEL,ANOELQ                                                     
         MVI   ANOLN,ANOLNQ                                                     
         MVI   ANOTYPE,ANOTPER                                                  
         MVC   ANOOFFC,EXCANAO     ANALYSIS OFFICE                              
*                                                                               
* EXPENSE DEBIT WITH CASH DISCOUNT SCIEL (OPTIONAL)                             
*                                                                               
POST2    ICM   R2,15,EXCADAY                                                    
         BNZ   *+6                                                              
         DC    H'0'                BIG PROBLEMS IF NO ACCDAY POINTER            
*                                                                               
         OC    EXCDAMNT,EXCDAMNT   TEST FOR CASH DISCOUNT                       
         BZ    POST4                                                            
         CP    EXCDAMNT,=P'0'      TEST AMOUNT=0                                
         BE    POST4               YES                                          
*                                                                               
         USING SCIELD,R2                                                        
         MVI   SCIEL,SCIELQ                                                     
         MVI   SCILN,SCILN1Q                                                    
         MVI   SCITYPE,SCITCDSC                                                 
         ZAP   SCIAMNT,EXCDAMNT    CASH DISCOUNT AMOUNT                         
         LA    R2,SCILN1Q(R2)                                                   
*                                                                               
         USING DLPOSTD,R2                                                       
POST4    MVC   DLPOSTD(L'TRSELEM),TRSELEM  ADD TRANS. STATUS ELEM               
         LA    R2,L'TRSELEM(R2)                                                 
*                                                                               
         CLI   ANOELEM,0           DO WE HAVE AN ANALYSIS ELEMENT ?             
         BE    *+14                NO                                           
         MVC   DLPOSTD(L'ANOELEM),ANOELEM                                       
         LA    R2,L'ANOELEM(R2)                                                 
*                                                                               
         CLI   FFTELEM,X'00'       DO WE HAVE AN FFTEL ELEMENT?                 
         BE    *+14                NO                                           
         MVC   0(L'FFTELEM,R2),FFTELEM                                          
         LA    R2,L'FFTELEM(R2)                                                 
*                                                                               
         XC    DLPOSTD(DLPSLNQ),DLPOSTD                                         
         MVI   DLPSEL,DLPSEDRQ     DEBIT EXPENSE                                
         MVI   DLPSLEN,DLPSLNQ                                                  
         MVC   DLPSDBAC,EXCSEAC                                                 
         MVC   DLPSDBNM,EXCSENM                                                 
         MVC   DLPSCRAC,EXCSECAC   EXPENSE CAC                                  
         MVC   DLPSCRNM,EXCSECAN   EXPENSE CAN                                  
         MVC   DLPSANAL,EXCFINO    FINANCIAL OFFICE                             
         ZAP   DLPSAMNT,EXCAMNT                                                 
         LA    R2,DLPSLNQ(R2)                                                   
         ST    R2,EXCADAY          UPDATE ACCDAY POINTER                        
*                                                                               
* DEPARTMENT ANALYSIS POSTINGS                                                  
*                                                                               
POST6    CLI   EXCDEPT,C'Y'        TEST FOR DEPARTMENT ANALYSIS                 
         BNE   POST8               NO                                           
*                                                                               
         MVC   DLPOSTD(L'TRSELEM),TRSELEM                                       
         LA    R2,L'TRSELEM(R2)                                                 
*                                                                               
         CLI   ANOELEM,0           DO WE HAVE AN ANALYSIS ELEMENT ?             
         BE    *+14                NO                                           
         MVC   DLPOSTD(L'ANOELEM),ANOELEM                                       
         LA    R2,L'ANOELEM(R2)                                                 
*                                                                               
         CLI   FFTELEM,X'00'       DO WE HAVE AN FFTEL ELEMENT?                 
         BE    *+14                NO                                           
         MVC   0(L'FFTELEM,R2),FFTELEM                                          
         LA    R2,L'FFTELEM(R2)                                                 
*                                                                               
         XC    DLPOSTD(DLPSLNQ),DLPOSTD                                         
         MVI   DLPSEL,DLPSEDCQ     DOUBLE POSTING                               
         MVI   DLPSLEN,DLPSLNQ                                                  
         OI    DLPSTYPE,X'80'                                                   
         MVC   DLPSDBAC,EXC2DAC    2D                                           
         MVC   DLPSDBNM,EXC2DNM                                                 
         MVC   DLPSCRAC,EXC28AC    28                                           
         MVC   DLPSCRNM,EXC28NM                                                 
         MVC   DLPSANAL,EXCFINO    FINANCIAL OFFICE                             
         ZAP   DLPSAMNT,EXCAMNT                                                 
         LA    R2,DLPSLNQ(R2)                                                   
         ST    R2,EXCADAY          UPDATE ACCDAY POINTER                        
*                                                                               
* COST ACCOUNTING POSTINGS                                                      
*                                                                               
POST8    CLI   EXCCOST,C'Y'        TEST COST ACCOUNTING POSTINGS                
         BNE   POST12              NO                                           
*                                                                               
         MVC   DLPOSTD(L'TRSELEM),TRSELEM                                       
         LA    R2,L'TRSELEM(R2)                                                 
*                                                                               
         CLI   ANOELEM,0           DO WE HAVE AN ANALYSIS ELEMENT ?             
         BE    *+14                NO                                           
         MVC   DLPOSTD(L'ANOELEM),ANOELEM                                       
         LA    R2,L'ANOELEM(R2)                                                 
*                                                                               
         CLI   FFTELEM,X'00'       DO WE HAVE AN FFTEL ELEMENT?                 
         BE    *+14                NO                                           
         MVC   0(L'FFTELEM,R2),FFTELEM                                          
         LA    R2,L'FFTELEM(R2)                                                 
*                                                                               
         XC    DLPOSTD(DLPSLNQ),DLPOSTD                                         
         MVI   DLPSEL,DLPSEDRQ     DEBIT POSTING                                
         MVI   DLPSLEN,DLPSLNQ                                                  
         OI    DLPSTYPE,X'80'                                                   
         MVC   DLPSDBAC,EXC1PAC    DEBIT 1P                                     
         MVC   DLPSDBNM,EXC1PNM                                                 
         MVC   DLPSCRAC,EXC1CAC    CONTRA-ACCOUNT=1C                            
         MVC   DLPSCRNM,EXC1CNM                                                 
         MVC   DLPSANAL,EXCFINO                                                 
         ZAP   DLPSAMNT,EXCAMNT                                                 
         LR    R3,R2               SAVE A(DEBIT POSTING)                        
         LA    R2,DLPSLNQ(R2)                                                   
*                                                                               
POST10   MVC   DLPOSTD(L'TRSELEM),TRSELEM                                       
         LA    R2,L'TRSELEM(R2)                                                 
*                                                                               
         CLI   ANOELEM,0           DO WE HAVE AN ANALYSIS ELEMENT ?             
         BE    *+14                NO                                           
         MVC   0(L'ANOELEM,R2),ANOELEM                                          
         LA    R2,L'ANOELEM(R2)                                                 
*                                                                               
         CLI   FFTELEM,X'00'       DO WE HAVE AN FFTEL ELEMENT?                 
         BE    *+14                NO                                           
         MVC   0(L'FFTELEM,R2),FFTELEM                                          
         LA    R2,L'FFTELEM(R2)                                                 
*                                                                               
         MVC   0(DLPSLNQ,R2),0(R3) COPY DEBIT POSTING                           
         MVI   DLPSEL,DLPSECRQ     CREDIT POSTING TO 1C                         
         MVC   DLPSDBAC,EXC13AC    CONTRA-ACCOUNT IS 13                         
         LA    R2,DLPSLNQ(R2)                                                   
         ST    R2,EXCADAY          UPDATE ACCDAY POINTER                        
*                                                                               
* STAFF ANALYSIS POSTINGS                                                       
*                                                                               
POST12   CLI   EXCSTAFF,C'Y'       TEST FOR STAFF ANALYSIS POSTINGS             
         BNE   POSTX                                                            
*                                                                               
         MVC   DLPOSTD(L'TRSELEM),TRSELEM                                       
         LA    R2,L'TRSELEM(R2)                                                 
*                                                                               
         CLI   ANOELEM,0           DO WE HAVE AN ANALYSIS ELEMENT ?             
         BE    *+14                NO                                           
         MVC   0(L'ANOELEM,R2),ANOELEM                                          
         LA    R2,L'ANOELEM(R2)                                                 
*                                                                               
         CLI   FFTELEM,X'00'       DO WE HAVE AN FFTEL ELEMENT?                 
         BE    *+14                NO                                           
         MVC   0(L'FFTELEM,R2),FFTELEM                                          
         LA    R2,L'FFTELEM(R2)                                                 
*                                                                               
         XC    DLPOSTD(DLPSLNQ),DLPOSTD                                         
         MVI   DLPSEL,DLPSEDRQ     DR TO 2P                                     
         MVI   DLPSLEN,DLPSLNQ                                                  
         OI    DLPSTYPE,X'80'                                                   
         MVC   DLPSDBAC,EXC2PAC                                                 
         MVC   DLPSDBNM,EXC2PNM                                                 
         MVC   DLPSANAL,EXCFINO                                                 
         ZAP   DLPSAMNT,EXCAMNT                                                 
         MVC   DLPSCRNM,EXC29NM                                                 
         MVC   DLPSCRAC,BCSPACES                                                
         MVI   DLPSCRAC,C'*'       CONSTRUCT THE CONTRA-ACCOUNT                 
         MVC   DLPSCRAC+1(L'ACTKACT),EXCSEAC+(ACTKACT-ACTKEY)                   
*                                                                               
         LA    R3,L'ACTKACT        COMPUTE LENGTH OF SE ACCOUNT CODE            
         LA    RE,DLPSCRAC+L'ACTKACT                                            
         CLI   0(RE),C' '                                                       
         BH    *+10                                                             
         BCTR  RE,0                                                             
         BCT   R3,*-10                                                          
*                                                                               
* CONTRA-ACCOUNT IS *(SE A/C CODE)-(29 A/C CODE) FOR THE TENO OPTION            
* ONLY 'N' BYTES FROM THE RIGHT HAND SIDE OF THE SE A/C CODE ARE USED           
*                                                                               
POST13   LA    R6,BCCPYEL                                                       
         USING CPYELD,R6                                                        
         CLI   CPYTENO,X'F0'       TEST FOR USING RHS OF SE CODE                
         BL    POST14              NO                                           
*                                                                               
         PACK  DUB,CPYTENO                                                      
         CVB   R5,DUB                                                           
         MVC   DLPSCRAC+1(14),BCSPACES                                          
         LR    RE,R3               RE=L'SE ACCOUNT CODE                         
         SR    RE,R5               RE=DISP TO RHS OF CODE                       
         LA    RE,EXCSEAC+3(RE)    RE=A(RHS)                                    
         BCTR  R5,0                                                             
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   DLPSCRAC+1(0),0(RE) EXTRACT RHS OF SE A/C CODE                   
         LA    R3,1(R5)            R3=L'RHS OF SE A/C CODE                      
*                                                                               
POST14   LA    RF,DLPSCRAC+1(R3)   RF=A(END OF A/C CODE)                        
         MVI   0(RF),C'-'          INSERT A DASH AFTER IT                       
         LA    R1,DLPSCRAC+L'DLPSCRAC-1  R1=A(END OF A/C)                       
         SR    R1,RF               L'REST OF KEY                                
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   1(0,RF),EXC29AC+3   ATTACH 29 A/C CODE                           
         LR    R5,R2               SAVE A(POSTING)                              
         LA    R2,DLPSLNQ(R2)                                                   
*                                                                               
POST16   MVC   0(L'TRSELEM,R2),TRSELEM                                          
         LA    R2,L'TRSELEM(R2)                                                 
*                                                                               
         CLI   ANOELEM,0           DO WE HAVE AN ANALYSIS ELEMENT ?             
         BE    *+14                NO                                           
         MVC   0(L'ANOELEM,R2),ANOELEM                                          
         LA    R2,L'ANOELEM(R2)                                                 
*                                                                               
         CLI   FFTELEM,X'00'       DO WE HAVE AN FFTEL ELEMENT?                 
         BE    *+14                NO                                           
         MVC   0(L'FFTELEM,R2),FFTELEM                                          
         LA    R2,L'FFTELEM(R2)                                                 
*                                                                               
* CREDIT TO 29--CAC IS *(SE A/C CODE FORM 2P DEBIT)-STAFF CODE                  
*                                                                               
         MVC   DLPOSTD(DLPSLNQ),0(R5) COPY DEBIT POSTING                        
         MVI   DLPSEL,DLPSECRQ     CREDIT POSTING                               
         MVC   DLPSDBAC,DLPSCRAC   COPY CAC FROM DEBIT POSTING                  
         MVC   DLPSDBNM,EXC2PNM    USE THE 2P A/C NAME AS CAC NAME              
         MVC   DLPSCRAC,EXC29AC    CREDIT 29                                    
         LA    RF,DLPSDBAC+1(R3)   POINT TO END OF EXPENSE CODE                 
         MVI   0(RF),C'-'                                                       
         LA    R1,DLPSDBAC+L'DLPSDBAC-1                                         
         SR    R1,RF               R1=L'REST OF KEY                             
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   1(0,RF),EXC2PAC+3   ATTACH REST OF 2P CODE                       
         LA    R2,DLPSLNQ(R2)                                                   
         ST    R2,EXCADAY          UPDATE ACCDAY POINTER                        
*                                                                               
POSTX    B     EXIT                                                             
         DROP  R2,R6                                                            
         EJECT                                                                  
***********************************************************************         
* CHECK WHETHER ACCOUNT IS ELEGIBLE FOR POSTING                       *         
***********************************************************************         
         SPACE 1                                                                
CHECKACC MVC   FVMSGNO,=AL2(AE$INACP)                                           
         TM    ACBSTAT,ACBSABAL    TEST FOR BALANCE ELEMENT                     
         BZ    CHECKNO             NO                                           
         TM    ACBSTAT,ACBSLOCK    TEST FOR LOCKED ACCOUNT                      
         BZR   RE                  NO-ACCOUNT IS OK                             
         SPACE 1                                                                
CHECKNO  MVC   FVXTRA,BCSPACES                                                  
         MVC   FVXTRA(ACTKEND-L'ACTKCPY),ACCODE                                 
         L     RD,AWORK            SET FOR EXIT OUT OF MODULE                   
         CLI   *,0                 SET CC=NEQ                                   
         B     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* ROUTINE TO SET THE 1C ACCOUNT POINTER                               *         
***********************************************************************         
         SPACE 1                                                                
SET1C    ST    RE,DUB                                                           
         GOTO1 AMRGPRF                                                          
         L     RE,ATWA                                                          
         LA    RE,PSCOMPPR-TWAD(RE) POINT TO COMPOSITE PROFILE                  
         USING PPRELD,RE                                                        
         MVC   EXC1CAC,PPRCOST     1C ACCOUNT                                   
         OC    EXCCNTR,BCSPACES                                                 
         LA    R3,EXC1CAC+7        R3=A(REPLACEMENT POSITION POINTER)           
         CLI   EXCCPOS,0           TEST COST POSITION OVERRIDE                  
         BE    SET1C2              NO                                           
*                                                                               
         ZIC   R3,EXCCPOS          YES                                          
         LA    R3,EXC1CAC+2(R3)                                                 
*                                                                               
SET1C2   LA    R1,EXCCNTR          R1=A(OVERRIDE COST CENTER)                   
         LA    RE,3                RE=LOOP COUNTER                              
*                                                                               
SET1C4   CLI   0(R1),C' '          TEST FOR OVERRIDE CHARACTER                  
         BE    *+10                NO                                           
         MVC   0(1,R3),0(R1)                                                    
         LA    R1,1(R1)                                                         
         LA    R3,1(R3)                                                         
         BCT   RE,SET1C4                                                        
*                                                                               
SET1CX   L     RE,DUB                                                           
         BR    RE                                                               
         DROP  RE                                                               
         SPACE 2                                                                
EXIT     XIT1  ,                                                                
         EJECT                                                                  
*                                                                               
MOVEFLD  ST    RE,SAVERE                                                        
         ST    R3,SAVER3                                                        
         ZIC   R1,0(R2)                                                         
         LA    RE,8+1                                                           
         TM    1(R2),X'02'         TEST FOR EXTENDED FIELD                      
         BZ    *+8                                                              
         LA    RE,8+8+1                                                         
         SR    R1,RE                                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   8(0,R2),FLD                                                      
*                                                                               
         LA    R3,8(R2,R1)         GET LAST BYTE OF FIELD                       
         LA    RF,1(R1)            GET LENGTH OF FIELD                          
         CLI   0(R3),C' '          LOOK FOR SIGNIFICANT DATA                    
         BH    *+10                                                             
         BCTR  R3,0                                                             
         BCT   RF,*-10                                                          
         OI    6(R2),X'80'                                                      
*                                                                               
MOVEFLDX L     R3,SAVER3                                                        
         L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
ERR1     DC    H'0',C'INVALID ACTION CALL '                                     
         SPACE 2                                                                
ACCMST   DC    C'ACCMST'                                                        
SCPARM   DC    C',=,',X'00'                                                     
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
LWSD     DSECT                     ** WORKING STORAGE DSECT **                  
RELO     DS    A                                                                
AWORK    DS    A                                                                
SAVERE   DS    A                                                                
SAVER3   DS    A                                                                
DUB      DS    D                                                                
DMCB     DS    6F                                                               
WORK     DS    XL64                                                             
FLD      DS    CL80                                                             
*                                                                               
COSTCODE DS    CL1                                                              
OFFICE   DS    CL2                                                              
OFFICEL  DS    XL1                                                              
DEPT     DS    CL3                                                              
DEPTL    DS    XL1                                                              
STAFF    DS    CL12                                                             
STAFFL   DS    XL1                                                              
LEVEL    DS    XL1                                                              
*                                                                               
TRSELEM  DS    XL(TRSLNQ)          TRANSACTION STATUS ELEMENT                   
ANOELEM  DS    XL(ANOLNQ)          ANALYSIS OFFICE ELEMENT                      
FFTELEM  DS    CL(FFTLN1Q+L'FFTDLEN+L'FFTCLPRA) CLIENT/PRODUCT ELEMENT          
*                                                                               
         DS    0F                                                               
CATBLK   DS    CL(CATLNQ)          CATCALL BLOCK AREA                           
*                                                                               
         DS    0D                                                               
EXPREC   DS    XL2000              I/O AREA                                     
LWSX     EQU   *                                                                
         EJECT                                                                  
* ACEXCELD                                                                      
*                                                                               
       ++INCLUDE ACEXCELD                                                       
         EJECT                                                                  
* ACBATWORK                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACBATWORK                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* ACCATCALLD                                                                    
         PRINT OFF                                                              
       ++INCLUDE ACCATCALLD                                                     
         PRINT ON                                                               
         SPACE 1                                                                
* ACGENDAY                                                                      
         PRINT OFF                                                              
       ++INCLUDE ACGENDAY                                                       
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'015ACEXCEL   12/10/14'                                      
         END                                                                    
