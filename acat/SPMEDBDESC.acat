*          DATA SET SPMEDBDESC AT LEVEL 100 AS OF 01/28/10                      
*CATALP MEDBDESC                                                                
         TITLE 'SPMEDBDESC - EXTRACT BUY DESCRIPTION'                           
VMDBDESC CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,VMDBDESC                                                       
         L     RA,0(R1)                                                         
         LA    RC,2048(RA)                                                      
         LA    RC,2048(RC)                                                      
         USING SPWORKD,RA,RC                                                    
*                                                                               
         L     R2,ADBUY                                                         
         USING BUYREC,R2                                                        
*                                                                               
         L     R3,4(R1)                                                         
         USING BDEXTD,R3                                                        
*                                                                               
         LA    RF,BDPEND           CLEAR AREA                                   
         SR    RF,R3                                                            
         LR    RE,R3                                                            
         XCEF                                                                   
         GOTO1 DATCON,DMCB,(X'05',WORK),(X'03',DUB)                             
         CLC   DUB(3),BDCHG                                                     
         BNE   PBD10                                                            
         MVI   BDPAST,C'*'         SET ACTIVITY INDICATOR                       
                                                                                
* CONVERT BUY DESCRIPTION FIELDS                                                
                                                                                
PBD10    EDIT  (1,BUYKEST),(3,BDPEST)                                           
         SR    RE,RE                                                            
         IC    RE,BUYKBUY                                                       
         TM    BUYREC+15,BUYRLN2   TEST 2-BYTE LINE NUMBERS                     
         BZ    *+8                                                              
         ICM   RE,3,BUYKBUY                                                     
         CVD   RE,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  BDPLIN,DUB+6(2)                                                  
         CLI   BDDAY,0             PROTECT ME                                   
         BE    PBD26                                                            
         CLI   RCLANG,4            FRENCH LANG?                                 
         BE    PBD20                                                            
         GOTO1 CODAY,DMCB,(BDSEDAY,BDDAY),BDPDAY  (REALLY IS DAYUNPK)           
         B     PBD22                                                            
*                                                                               
PBD20    GOTO1 CODAY,DMCB,(BDSEDAY,BDDAY),(X'80',BDPDAY)                        
*                                                                               
PBD22    EDIT  (1,BDNOWK),(2,BDPNPWK)                                           
*                                                                               
         XC    DMCB,DMCB                                                        
         CLI   RCLANG,4            IS IT FRENCH?                                
         BE    PBD24                                                            
         GOTO1 UNTIME,DMCB,BDPROG,BDPTIME,0                                     
         B     PBD26                                                            
*                                                                               
PBD24    DS    0H                                                               
         GOTO1 UNTIME,DMCB,BDPROG,BDPTIME,=C'MIL'                               
*                                                                               
PBD26    DS    0H                                                               
         GOTO1 DATCON,DMCB,(3,BDSTART),(4,BDPSDTE)                              
         CLC   BDSTART,BDEND                                                    
         BE    PBD28                                                            
         MVI   BDPSDTE+5,C' '                                                   
         GOTO1 DATCON,DMCB,(3,BDEND),(4,BDPEDTE)                                
*                                                                               
PBD28    TM    BDCFD,1             TEST CONFIRMED END                           
         BZ    *+8                                                              
         MVI   BDPCFDE,C')'                                                     
         TM    BDCFD,2             TEST CONFIRMED START                         
         BZ    *+8                                                              
         MVI   BDPCFDS,C'('                                                     
         MVC   BDPDPT,BDDAYPT                                                   
         EDIT  (1,BDWKS),(2,BDPWKS)                                             
* WEEK INDICATOR                                                                
         CLI   BDWKIND,C'0'                                                     
         BE    *+10                                                             
         MVC   BDPWIND,BDWKIND                                                  
         CLI   BDINPUT,3                                                        
         BNE   PBD30                                                            
         MVI   BDPDASHE,C'E'                                                    
         CLI   BDPWIND,0                                                        
         BNE   PBD30                                                            
         MVC   BDPWIND,BDPDASHE                                                 
         MVI   BDPDASHE,C' '                                                    
*                                                                               
PBD30    MVC   BDPPURP,BDPURP                                                   
         MVC   BDPPROG,BDPROG+4                                                 
         MVI   BDPPROG+17,0                                                     
         L     R9,ADCLT                                                         
         USING CLTHDR,R9                                                        
         CLI   CPROF+9,C'1'                                                     
         BNE   PBD32                                                            
         CLI   BDPROGT,0                                                        
         BE    PBD40                                                            
         MVC   BDPPTYP,BDPROGT                                                  
PBD32    CLI   CPROF+9,C'2'                                                     
         BNE   PBD40                                                            
         ZIC   RE,BDPROGT                                                       
         SRDL  RE,4                                                             
         SRL   RF,28                                                            
         STC   RE,BDPADJ                                                        
         STC   RF,BDPADJ+1                                                      
         OC    BDPADJ,=C'00'                                                    
* CONVERT LENGTH                                                                
PBD40    EDIT  (1,BDSEC),(3,BDPSLN)                                             
*                                                                               
         XC    COST(4),COST                                                     
         MVC   COST+1(3),BDCOST                                                 
*                                                                               
         CLI   QCOST2,C'Y'         TEST REPORT COS2 VALUE                       
         BNE   PBD42                                                            
         MVC   SPOTS(4),=C'COS2'                                                
         GOTO1 GETRATE,DMCB,SPOTS,BUYREC,0                                      
         MVC   COST,GROSS                                                       
*                                                                               
PBD42    L     RF,COST                                                          
         TM    BDCIND2,X'20'       TEST CANADIAN BUY                            
         BNO   PBD52                                                            
         CLC   QPROG,=C'N5'        N5 REPORT (ALREADY CODE @ PBD62              
         BE    PBD52                              TO CHECK FOR PENNIES)         
         CLC   QPROG,=C'RN'        RN REPORT (ALREADY CODE @ PBD62              
         BE    PBD52                              TO CHECK FOR PENNIES)         
         OC    BUYKEY+4(2),BUYKEY+4  TEST NTWK LEVEL BUY (MKT=0)                
         BNZ   PBD52               NO - SPOT BUY                                
         TM    BDCIND2,X'01'       CAN NET - IS THE RATE IN PENNIES             
         BO    PBD54               YES                                          
         B     *+12                MUST BE DOLLARS (NO PENNIES O/RIDE)          
PBD52    TM    BDCIND2,X'10'       TEST RATE IN DOLLARS                         
         BZ    *+8                                                              
         MHI   RF,100              CONVERT TO PENNIES                           
PBD54    TM    BDCIND2,X'04'       BDCIND IS A CHARACTER                        
         BZ    PBD56                                                            
         TM    BDCIND2,BDC2NEG                                                  
         BZ    PBD60                                                            
         B     PBD58                                                            
PBD56    TM    BDCIND,X'01'                                                     
         BZ    *+6                                                              
PBD58    LNR   RF,RF                                                            
*                                                                               
PBD60    ST    RF,COST                                                          
*                                                                               
         CLI   QPWCV,C'Y'          TEST WI PW CLT VERSION                       
         BE    PBD62                                                            
         CLI   RQGETBF,C'X'        TEST COSTS AT NET                            
         BE    *+12                                                             
         CLI   RQGETBF,C'Y'        TEST COSTS ADJ BY BILLFORM                   
         BNE   PBD62               NO                                           
* COMPUTE AVERAGE COST PER SPOT FOR PERIOD                                      
         L     R5,MEDBUFF                                                       
         LA    R5,MEDPERD-MEDBLOCK(R5)                                          
         L     R4,4(R5)            POINT TO PERIOD DATA                         
         USING MEDDATA,R4                                                       
         OC    MEDBYSPT,MEDBYSPT                                                
         BZ    PBD62                                                            
         L     R1,MEDBYD           BUY DOLLARS                                  
         AR    R1,R1               X 2                                          
         SR    R0,R0                                                            
         D     R0,MEDBYSPT                                                      
         LTR   R1,R1                                                            
         BM    *+8                                                              
         AHI   R1,1                                                             
         SRA   R1,1                                                             
         ST    R1,COST                                                          
*                                                                               
PBD62    MVI   CURTAB+3,0                                                       
         CLC   QPROG,=C'N5'        TEST N5 REPORT                               
         BE    *+10                                                             
         CLC   QPROG,=C'RN'        TEST RN REPORT                               
         BNE   *+12                                                             
         TM    BDCIND2,X'01'       YES-TEST RATE IN PENNIES                     
         BZ    PBD70                                                            
* CONVERT PENNIES TO DOLLARS AND OUTPUT TO BDPCOST2                             
         SR    RE,RE                                                            
         SLDA  RE,1                                                             
         D     RE,=F'100'                                                       
         AHI   RF,1                                                             
         SRA   RF,1                                                             
         MVI   CURTAB+3,0                                                       
         CURED (RF),(12,BDPCOST2),CURTAB,DMCB=CDMCB,CURSYMB=YES,FLOAT=-         
* OUTPUT PENNIES TO BDPCOST                                                     
         L     RF,COST                                                          
         MVI   CURTAB+3,2                                                       
         CURED (RF),(12,BDPCOST),CURTAB,DMCB=CDMCB,CURSYMB=YES,FLOAT=-          
         ORG   *-2                 REMOVE BASR RE,RF                            
         CLI   RCLANG,4            THERE'S NO ROOM ON FRENCH D4'S               
         BNE   PBD64                                                            
         CLC   =C'D4',QPROG                                                     
         BNE   PBD64                                                            
         OI    0(R1),X'04'         SET SUPPRESS CURRENCY SYMBOL                 
*                                                                               
PBD64    BASR  RE,RF                                                            
*                                                                               
         L     RF,COST                                                          
         C     RF,=F'999999'                                                    
         BL    PBD72                                                            
* DOESN'T FIT IN PENNIES SO DO DOLLARS TO BDPCOST                               
         SR    RE,RE                                                            
         SLDA  RE,1                                                             
         D     RE,=F'100'                                                       
         A     RF,=F'1'                                                         
         SRA   RF,1                                                             
         MVI   CURTAB+3,0                                                       
*                                                                               
PBD70    CURED (RF),(12,BDPCOST),CURTAB,DMCB=CDMCB,CURSYMB=YES,FLOAT=-          
*                                                                               
PBD72    DS    0C                                                               
         CLI   QMED,C'R'                                                        
         BNE   PBD80                                                            
         TM    RQOPTS,RQOPTS_CENTS  TEST WANT CENTS TO $10000                   
         BO    PBD80                                                            
         ICM   RF,15,COST                                                       
         C     RF,=F'99999'                                                     
         BH    *+8                                                              
         B     PBD80                                                            
* FOR RADIO > 999.99, OUTPUT DOLLARS                                            
         SR    RE,RE                                                            
         SLDA  RE,1                                                             
         D     RE,=F'100'                                                       
         AHI   RF,1                                                             
         SRA   RF,1                                                             
         MVI   CURTAB+3,0                                                       
         CURED (RF),(12,BDPCOST),CURTAB,DMCB=CDMCB,CURSYMB=YES,FLOAT=-          
*                                                                               
PBD80    DS    0H                                                               
         TM    BDCIND2,X'04'       BDCIND IS A CHARACTER                        
         BZ    PBD84                                                            
         MVC   BDPCIND(1),BDCIND                                                
         B     PBD86                                                            
PBD84    MVI   BDPCIND,C' '                                                     
         TM    BDCIND,X'80'                                                     
         BZ    *+8                                                              
         MVI   BDPCIND,C'F'                                                     
         TM    BDCIND,X'40'                                                     
         BZ    *+8                                                              
         MVI   BDPCIND,C'Q'                                                     
         TM    BDCIND,X'10'                                                     
         BZ    *+8                                                              
         MVI   BDPCIND,C'N'                                                     
         TM    BDCIND,X'FE'                                                     
         BNZ   *+8                                                              
         MVI   BDPCIND,C'P'                                                     
         TM    BDCIND,X'08'                                                     
         BZ    *+8                                                              
         MVI   BDPCIND,C'V'                                                     
         TM    BDCIND,X'04'                                                     
         BZ    *+8                                                              
         MVI   BDPCIND,C'S'                                                     
         TM    BDCIND,X'02'                                                     
         BZ    *+8                                                              
         MVI   BDPCIND,C'X'                                                     
         TM    BDCIND2,X'80'                                                    
         BZ    *+8                                                              
         MVI   BDPCIND,C'C'                                                     
PBD86    TM    BDCIND2,X'02'       TEST TRADE SPOT                              
         BZ    *+8                                                              
         MVI   BDPCIND,C'T'                                                     
         CLI   BDPCIND,C' '                                                     
         BE    PBD88                                                            
         LA    R1,BDPCOST+L'BDPCOST-1                                           
         CLI   0(R1),C'$'                                                       
         BE    *+8                                                              
         BCT   R1,*-8                                                           
         MVC   0(1,R1),BDPCIND                                                  
*                                                                               
PBD88    L     R1,ADAGY            TEST CANADIAN AGENCY                         
         CLI   AGYPROF+7-AGYHDR(R1),C'C'                                        
         BNE   PBD100              NO                                           
         TM    BDCIND2,X'40'       YES-TEST EXCHANGE ELEMENT                    
         BZ    PBD100              NO                                           
         SR    R0,R0               YES-LOCATE THE EXCHANGE ELEMENT              
         LA    R6,BDELEM                                                        
*                                                                               
PBD90    CLI   0(R6),0                                                          
         BE    PBD100                                                           
         CLI   0(R6),XCHCODEQ                                                   
         BE    *+14                                                             
         IC    R0,1(R6)                                                         
         AR    R6,R0                                                            
         B     PBD90                                                            
         USING XCHELEM,R6                                                       
         CLC   RQCRRNCY,XCHDTYP    CURRENCY SAME AS REQ                         
         BE    PBD100              DON'T PRINT IT                               
         CLI   XCHCTYP,C'C'        TEST CLIENT IS CANADIAN                      
         BNE   PBD100              NO                                           
         CLI   XCHSTYP,C'U'        YES-TEST STATION IS USA                      
         BNE   PBD100              NO                                           
         CLI   XCHDTYP,C'U'        YES-TEST COST IS IN USA DOLLARS              
         BNE   PBD100              NO                                           
         CLI   BDPCOST+11,C'-'     YES-INDICATE USA DOLLARS                     
         BE    *+12                                                             
         MVI   BDPCOST+11,C'U'                                                  
         B     PBD100                                                           
         LA    RE,BDPCOST                                                       
         LA    RF,12                                                            
         CLI   0(RE),C' '                                                       
         BH    *+16                                                             
         LA    RE,1(RE)                                                         
         BCT   RF,*-12                                                          
         B     PBD100                                                           
         CLI   0(RE),C'$'                                                       
         BNE   *+12                                                             
         MVI   0(RE),C'U'                                                       
         B     PBD100                                                           
         BCTR  RE,0                                                             
         MVI   0(RE),C'U'                                                       
*                                                                               
PBD100   DS    0H                                                               
         GOTO1 VRCPACK,DMCB,(C'U',BDREP),BDPSREP                                
         XIT1                                                                   
CDMCB    DS    6F                                                               
COST     DS    F                                                                
         LTORG                                                                  
       ++INCLUDE SPMEDBDESD                                                     
       ++INCLUDE SPGENCLT                                                       
         PRINT OFF                                                              
       ++INCLUDE SPREPWORKD                                                     
       ++INCLUDE SPMEDBLOCK                                                     
       ++INCLUDE SPGENBUY                                                       
AGYHDRD  DSECT                                                                  
       ++INCLUDE SPGENAGY                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'100SPMEDBDESC01/28/10'                                      
         END                                                                    
