*          DATA SET DDCLPACK   AT LEVEL 012 AS OF 02/02/99                      
*CATALP CLPACK                                                                  
         TITLE 'MODULE CONVERTS THREE CHARACTER CLIENT CODES TO TWO'            
         SPACE 1                                                                
*--------------------------------------------------------------------*          
* THIS VERSION WENT LIVE IN 1986. IF YOU NEED TO UNPACK CLIENT CODES *          
* FROM BEFORE THEN, YOU CANNOT USE IT.                               *          
* THE OLDER VERSION JUST USED 5 BITS PER CHARACTER.                  *          
*--------------------------------------------------------------------*          
         SPACE 1                                                                
CLPACK   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 1,*CLPACK*                                                       
         USING CLPD,RC                                                          
         LM    R2,R3,0(R1)                                                      
         LR    R9,R1                                                            
         CLI   0(R2),C' '                                                       
         BE    INVALID                                                          
         CLI   1(R2),C' '                                                       
         BE    INVALID                                                          
         CLI   0(R2),C'*'          OFFICE FIELDS                                
         BE    TWOBYTE                                                          
         CLI   0(R2),C'$'          OFFICE LIST                                  
         BE    TWOBYTE                                                          
         CLI   0(R2),C'+'          MARKET                                       
         BE    TWOBYTE                                                          
         EJECT                                                                  
*              CHECK THE 3 CHARACTERS AGAINST VALIDITY TABLE                    
         SPACE 3                                                                
CPB      LA    R4,3                                                             
         SR    R5,R5                                                            
         SPACE 2                                                                
CP2      LA    R6,TABLE                                                         
         SR    R7,R7                                                            
         LA    R8,32                                                            
         SPACE 2                                                                
CP4      CLC   0(1,R2),0(R6)                                                    
         BE    CP6                                                              
         LA    R6,1(R6)                                                         
         LA    R7,1(R7)                                                         
         BCT   R8,CP4                                                           
         B     INVALID                                                          
         SPACE 2                                                                
CP6      SLL   R5,5                                                             
         AR    R5,R7                                                            
         LA    R2,1(R2)                                                         
         BCT   R4,CP2                                                           
         STC   R5,1(R3)                                                         
         SRL   R5,8                                                             
         STC   R5,0(R3)                                                         
         OI    0(R3),X'80'                                                      
         B     VALID                                                            
         SPACE 2                                                                
TWOBYTE  MVC   0(2,R3),0(R2)                                                    
         SPACE 2                                                                
VALID    MVI   0(R9),0                                                          
         SPACE 2                                                                
XIT      XIT1                                                                   
         SPACE 2                                                                
INVALID  CH    R4,=H'1'            TEST THIRD CHAR INVALID                      
         BNE   INVX                IF NOT 3RD CHAR IT'S STILL INVALID           
         LA    R6,EXTTABLE         ALLOW 6,7,8,9,0 IN LAST POS'N                
         LA    R7,5                6=F,7=G,8=H,9=I,0=J                          
         LA    R8,5                                                             
INV2     CLC   0(1,R2),0(R6)                                                    
         BE    CP6                                                              
         LA    R6,1(R6)                                                         
         LA    R7,1(R7)                                                         
         BCT   R8,INV2                                                          
INVX     MVI   0(R9),X'FF'         SET ERROR FLAG                               
         B     XIT                                                              
         SPACE 2                                                                
TABLE    DC    C'ABCDEFGHIJKLMNOPQRSTUVWXYZ12345 '                              
EXTTABLE DC    C'67890'                                                         
CLPD     DSECT                                                                  
DUB      DS    D                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'012DDCLPACK  02/02/99'                                      
         END                                                                    
