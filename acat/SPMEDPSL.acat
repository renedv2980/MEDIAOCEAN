*          DATA SET SPMEDPSL   AT LEVEL 022 AS OF 10/11/11                      
*CATALP MEDPSL                                                                  
         TITLE 'SCAN A BUY RECORD FOR ALL ACTIVE PRODUCTS AND SPOT LEN'         
*   PARAMETERS                                                                  
* WORD 1 = A(SPWORKC)                                                           
*  WORD 2 = A(PRODUCT/SPOT LENGTH LIST)                                         
*                                                                               
*  LIST FORMAT = 1 BYTE PRODUCT CODE                                            
*                1 BYTE SPOT LENGTH                                             
*                                                                               
VMDPSL   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,MEDPSL                                                         
         L     RA,0(R1)                                                         
         LA    RC,2048(RA)                                                      
         LA    RC,2048(RC)                                                      
         USING SPWORKD,RA,RC                                                    
         L     R3,ADBUY                                                         
         USING BUYRECD,R3                                                       
         MVC   HLFA,BUYRLEN        SET FOR END OF BXLE LOOP                     
         LH    RF,HLFA                                                          
         LA    RF,0(R3,RF)                                                      
         BCTR  RF,0                                                             
         LA    R2,BDELEM                                                        
         USING REGELEM,R2                                                       
FNDEL    CLI   RCODE,6                                                          
         BL    NXTEL                                                            
         CLI   RCODE,X'0D'                                                      
         BH    NXTEL                                                            
         TM    RSTATUS,X'04'       HIATUS                                       
         BO    NXTEL                                                            
         CLI   RCODE,X'0B'         BRAND ELEMENT                                
         BL    BRNDREC                                                          
         CLI   RLEN,12             UNALLOCATED                                  
         BNL   FNDEL1                                                           
         MVI   HLFA,219                                                         
         MVC   HLFA+1(1),BDSEC                                                  
         B     FNDEL1A                                                          
FNDEL1   MVC   HLFA,RPPRD          SET PRODUCT AND LENGTH                       
         CLI   Q2TRADE,C'Y'                                                     
         BNE   *+8                                                              
         NI    HLFA,X'7F'                                                       
FNDEL1A  BAS   R9,INSERT                                                        
         CLI   RLEN,15             PIGGYBACK                                    
         BL    NXTEL                                                            
         MVC   HLFA,RPPRD+4        SET PIGGY PRODUCT AND LENGTH                 
         BAS   R9,INSERT                                                        
         B     NXTEL                                                            
*                                                                               
BRNDREC  MVC   HLFA(1),BUYKPRD     BRAND RECORD                                 
         MVC   HLFA+1(1),BDSEC     SAVE LENGTH                                  
         CLI   BDTIME,0                                                         
         BE    *+10                                                             
         MVC   HLFA+1(1),BDTIME    ACTIVE SPOT LENGTH                           
         BAS   R9,INSERT                                                        
         CLI   BDTIME,0                                                         
         BE    EXIT                                                             
         DROP  R2                                                               
         LA    R2,BDELEM                                                        
         USING PBELEM,R2                                                        
FNDPBEL  CLI   PBCODE,4            GET PIGGYBACK ELEMENT                        
         BNE   NXTEL1                                                           
         SR    RF,RF                                                            
         IC    RF,PBLEN            SET FOR BXLE                                 
         LA    RF,0(R2,RF)                                                      
         LA    RE,7                                                             
FNDPBEL2 MVC   HLFA(1),PBPROD                                                   
         MVC   HLFA+1(1),PBTIME                                                 
         BAS   R9,INSERT                                                        
         BXLE  R2,RE,FNDPBEL2                                                   
         B     EXIT                                                             
*                                                                               
NXTEL1   SR    RE,RE                                                            
         IC    RE,PBLEN                                                         
         LTR   RE,RE               CHECK FOR END                                
         BZ    EXIT                                                             
         BXLE  R2,RE,FNDPBEL                                                    
         B     EXIT                                                             
*                                                                               
         DROP  R2                                                               
         USING REGELEM,R2                                                       
NXTEL    SR    RE,RE                                                            
         IC    RE,RLEN                                                          
         LTR   RE,RE               CHECK FOR END                                
         BZ    EXIT                                                             
         BXLE  R2,RE,FNDEL                                                      
*                                                                               
EXIT     XMOD1 1                                                                
*                                                                               
INSERT   CLI   QPGR,C' '           TEST PRDGRP REQUEST                          
         BE    INSERT4             NO                                           
         CLC   =C'POL',QPRD        TEST POL REQUEST                             
         BNE   INSERT4             NO                                           
         CLI   PGRUNA,C'Y'         INCLUDE UNA?                                 
         BNE   *+12                                                             
         CLI   HLFA,219                                                         
         BE    INSERT4                                                          
*                                                                               
* NEED TO CHECK IF PRD IN THIS PRDGRP                                           
         L     R8,SVPRDADR         POONT TO CURRENT PRD                         
INSERT2  CLC   5(1,R8),HLFA        MATCH PRODUCT CODE                           
         BE    INSERT4                                                          
         CLC   0(2,R8),6(R8)       NEXT PRD SAME PRDGRP                         
         BNER  R9                  NO - SKIP PRD                                
         LA    R8,6(R8)            ELSE NEXT ENTRY                              
         B     INSERT2                                                          
*                                                                               
INSERT4  L     R8,4(R1)            GET LIST ADDRESS                             
*                                                                               
INSERT6  CLI   0(R8),0                                                          
         BNE   *+12                                                             
         MVC   0(2,R8),HLFA                                                     
         BR    R9                                                               
         CLC   HLFA,0(R8)                                                       
         BER   R9                                                               
         LA    R8,2(R8)                                                         
         B     INSERT6                                                          
*                                                                               
HLFA     DS    CL2                                                              
         LTORG                                                                  
         PRINT OFF                                                              
         EJECT                                                                  
* SPREPWORKD                                                                    
       ++INCLUDE SPREPWORKD                                                     
BUYRECD  DSECT                                                                  
       ++INCLUDE SPGENBUY                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'022SPMEDPSL  10/11/11'                                      
         END                                                                    
