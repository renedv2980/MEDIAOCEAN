*          DATA SET TGETPRO    AT LEVEL 015 AS OF 06/09/77                      
*CATALP TGETPRO                                                                 
         TITLE 'GET COMMERCIAL PROFILE RECORD FROM BUFFER'                      
TGETPRO  CSECT                                                                  
*                                                                               
*        PARAMETERS  WORD 1 BYTE 0      X'01' PASS COMMERCIAL TYPE              
*                                       X'02' PASS DISK ADDRESS IF              
*                                              COMMENTS                         
*                           BYTE 1-3    A(KEY)                                  
*                                                                               
*                    WORK 2 BYTE 0-3    A(FILM TITLE)                           
*                                                                               
*                   WORD 3 BYTE 0-3    A(CMRCL TYPE)                            
*                                                                               
*                   WORD 4 BYTE 0-3    A(DISK ADDR) ONLY IF COMMENTS            
*                                                                               
         NMOD1 0,TGETPRO                                                        
         L     RA,=V(RPWORKC)                                                   
         USING RPWORK,RA                                                        
         L     RC,=V(TPWORK)                                                    
         USING TPWORK,RC                                                        
         USING ELEMD,R2                                                         
         ST    R1,STORE1                                                        
         L     R4,0(R1)                                                         
         XC    TPKEY(48),TPKEY                                                  
         MVC   TPKEY(13),0(R4)                                                  
         NI    TPKEY+8,X'7F'                                                    
         CLI   TPFRST,1                                                         
         BE    CLRTP                                                            
         CLC   TPKEY+2(2),TPAGM                                                 
         BNE   CLRTP                                                            
         B     SRCHTAB                                                          
TPREAD   MVC   TPKEY2,TPKEY                                                     
         GOTO1 =V(DATAMGR),DMCB,DMRDHI,SDIR,TPKEY2,TPKEY                        
         CLC   TPKEY(13),TPKEY2                                                 
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 =V(DATAMGR),DMCB,GETDSK,SPOT,TPKEY+14,TPREC,V(GETWORK)           
         TM    8(R1),X'50'                                                      
         BZ    *+6                                                              
         DC    H'0'                                                             
         LA    R2,TPREC+24                                                      
FT1      CLI   0(R2),0                                                          
         BNE   *+12                                                             
         LA    R2,TPREC+24                                                      
         B     FT3                                                              
         CLI   0(R2),X'20'                                                      
         BE    FT2                                                              
FT1A     SR    RE,RE                                                            
         IC    RE,1(R2)                                                         
         AR    R2,RE                                                            
         B     FT1                                                              
FT2      CLI   PRDISP,C'D'                                                      
         BE    FT1A                                                             
FT3      MVC   0(7,R7),TPKEY+4                                                  
         MVC   7(15,R7),PRTITLE                                                 
         CLC   PRTYPE1,=C'   '     NEW FORMAT                                   
         BE    *+14                 NO - CONVERT OLD                            
         OC    PRTYPE1,PRTYPE1                                                  
         BNZ   RFMTDON                                                          
         CLC   PRTYPE,=C'HB'                                                    
         BNE   *+10                                                             
         MVC   PRTYPE1,=C'HVT'                                                  
         CLC   PRTYPE,=C'LB'                                                    
         BNE   *+10                                                             
         MVC   PRTYPE1,=C'LVT'                                                  
         CLI   PRMED,C'T'                                                       
         BE    RFMTDON                                                          
         MVC   PRTYPE1(1),PRCOMP                                                
         MVI   PRTYPE1+1,C'F'                                                   
         MVC   PRTYPE1+2(1),PRTYPE+1                                            
RFMTDON  MVC   22(3,R7),PRTYPE1                                                 
         SR    RE,RE                                                            
         IC    RE,1(R2)                                                         
         AR    R2,RE                                                            
         CLI   0(R2),X'21'                                                      
         BNE   MOVETIT                                                          
         MVC   25(4,R7),TPKEY+14                                                
         B    MOVETIT                                                           
EXIT     XMOD  1                                                                
CLRTP    LA    R6,15                                                            
         MVI   TPFRST,0                                                         
         LA    R7,TPFTITLE                                                      
         XC    0(200,R7),0(R7)                                                  
         LA    R7,200(R7)                                                       
         BCT   R6,*-10                                                          
         MVI   TPREC-25,X'FF'                                                   
         MVC   TPREC-24(24),TPREC-25                                            
         MVC   TPAGM,TPKEY                                                      
SRCHTAB  LA    R7,TPFTITLE                                                      
         CLI   0(R7),X'FF'                                                      
         BE    CLRTP                                                            
         CLI   0(R7),X'00'                                                      
         BE    TPREAD                                                           
         CLC   0(7,R7),TPKEY+4                                                  
         BNE   BUMPTAB                                                          
MOVETIT  L     R1,STORE1                                                        
         L     R4,4(R1)                                                         
         MVC   0(15,R4),7(R7)                                                   
         TM    0(R1),X'01'                                                      
         BZ    CHKDADR                                                          
         L     R4,8(R1)                                                         
         MVC   0(3,R4),22(R7)                                                   
CHKDADR  TM    0(R1),2                                                          
         BZ    EXIT                                                             
         L     R4,12(R1)                                                        
         MVC   0(4,R4),25(R7)                                                   
         B     EXIT                                                             
BUMPTAB  LA    R7,29(R7)                                                        
         B     SRCHTAB+4                                                        
TPWORK   CSECT                                                                  
STORE1   DS    F                                                                
TPKEY    DS    CL24                                                             
TPKEY2   DS    CL24                                                             
TPFRST   DC    X'01'                                                            
TPAGM    DS    CL2                                                              
TPFTITLE DS    200D                                                             
TPREC    DS    200D                                                             
ELEMD    DSECT                                                                  
ELEMSTRT DS    0C                                                               
       ++INCLUDE FDSKEY                                                         
         ORG   ELEMSTRT                                                         
       ++INCLUDE PROFELEM                                                       
         PRINT OFF                                                              
       ++INCLUDE RPWORK                                                         
