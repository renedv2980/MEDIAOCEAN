*          DATA SET ACTEXTER   AT LEVEL 003 AS OF 05/01/02                      
*CATALP TEXTER                                                                  
*                                                                               
* SEE PARMS IN WORKD DSECT FOR DETAILS OF IN AND OUT PARAMETER VALUES           
         TITLE 'TEXTER - TEXT EDITOR MODULE'                                    
TEXTER   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,**TEXT**                                                       
         L     RC,8(R1)                                                         
         USING WORKD,RC                                                         
         LA    RF,WORKX                                                         
         SR    RF,RC                                                            
         LR    RE,RC                                                            
         XCEF                                                                   
         ST    R1,APARMS                                                        
         MVC   PARMS,0(R1)                                                      
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
         B     VALACT                                                           
         EJECT                                                                  
*              VALIDATE ACTION STRING - ALTERNATIVES ARE                        
*              1 EDIT,INSERT,N(-N)                                              
*              2 EDIT,DELETE,N(-N)                                              
*              3 EDIT,REPLACE,(N(-N)),OLD=STRING,NEW=STRING                     
*                                                                               
VALACT   L     RF,PAACTH                                                        
         MVC   CRDH(88),0(RF)                                                   
         ZIC   RE,CRDH+5                                                        
VALACT2  LA    RF,CRD-1(RE)        REPLACE - BY = TO MAKE LINE RANGE            
         CLI   0(RF),C'-'          N=N (- AND = ARE INVALID IN OLD OR           
         BNE   VALACT3             NEW STRINGS)                                 
         BCTR  RF,0                MUST BE N-N                                  
         TM    0(RF),C'0'                                                       
         BNO   VALACT3                                                          
         TM    2(RF),C'0'                                                       
         BNO   VALACT3                                                          
         MVI   1(RF),C'='                                                       
         B     *+8                                                              
VALACT3  BCT   RE,VALACT2                                                       
         GOTO1 PASCAN,DUB,(32,CRDH),TXTAREA                                     
         MVI   FERN,INVALID                                                     
         CLI   4(R1),1                                                          
         BL    ERROR                                                            
         MVI   FERN,MISSING                                                     
         MVI   FNDX,2                                                           
         BE    ERROR                                                            
         LA    R2,TXTAREA+32+22    LOOK FOR SUBACTION - MUST BE P2              
         ZIC   R1,0(R2)                                                         
         BCTR  R1,0                                                             
         MVI   FERN,INVACT                                                      
         LA    RF,ACTAB                                                         
VALACT4  CLI   0(RF),X'FF'                                                      
         BE    ERROR                                                            
         EX    R1,ACTCLC                                                        
         BE    VALACT6                                                          
         LA    RF,L'ACTAB(RF)                                                   
         B     VALACT4                                                          
ACTCLC   CLC   12(0,R2),0(RF)                                                   
ACTAB    DC    CL7'INSERT'                                                      
         DC    CL7'DELETE'                                                      
         DC    CL7'REPLACE'                                                     
         DC    X'FF'                                                            
*                                                                               
VALACT6  MVC   ACTION,12(R2)       LOOK FOR N-N, OLD AND NEW                    
         LA    R2,32+22(R2)                                                     
         MVI   FNDX,3                                                           
         MVI   FERN,MISSING                                                     
         OC    0(2,R2),0(R2)                                                    
         BZ    ERROR                                                            
*                                                                               
VALACT8  OC    0(2,R2),0(R2)       LOOP FOR AN ENTRY                            
         BZ    VALACT20                                                         
         MVI   FERN,INVALID                                                     
         CLI   0(R2),0                                                          
         BE    ERROR                                                            
*                                                                               
         TM    2(R2),X'80'         N-N                                          
         BNO   VALACT12                                                         
         MVI   FERN,DUPLICAT                                                    
         OC    LOLINE,LOLINE                                                    
         BNZ   ERROR                                                            
         MVC   LOLINE,6(R2)                                                     
         MVC   HILINE,LOLINE                                                    
         CLI   1(R2),0                                                          
         BE    VALACT10                                                         
         MVI   FERN,INVNUM                                                      
         TM    3(R2),X'80'                                                      
         BNO   ERROR                                                            
         MVC   HILINE,10(R2)                                                    
VALACT10 MVI   FERN,INVALID                                                     
         CLI   LOLINE+1,0                                                       
         BNH   ERROR                                                            
         CLC   PLINES,HILINE+1                                                  
         BL    ERROR                                                            
         CLC   LOLINE,HILINE                                                    
         BH    ERROR                                                            
         B     VALACT14                                                         
*                                                                               
VALACT12 LA    RF,LOLD             OLD OR NEW                                   
         ZIC   R1,0(R2)                                                         
         BCTR  R1,0                                                             
         EX    R1,OLDCLC                                                        
         BE    *+16                                                             
         LA    RF,LNEW                                                          
         EX    R1,NEWCLC                                                        
         BNE   ERROR                                                            
         CLI   ACTION,C'R'                                                      
         BNE   ERROR                                                            
         CLI   1(RF),0                                                          
         BE    *+12                                                             
         MVI   FERN,DUPLICAT                                                    
         B     ERROR                                                            
         CLI   1(R2),0                                                          
         BE    ERROR                                                            
         MVC   1(1,RF),1(R2)                                                    
         MVC   2(L'OLD,RF),22(R2)                                               
         B     VALACT14                                                         
OLDCLC   CLC   12(0,R2),=C'OLD'                                                 
NEWCLC   CLC   12(0,R2),=C'NEW'                                                 
*                                                                               
VALACT14 LA    R2,32+22(R2)        BUMP TO NEXT                                 
         ZIC   R1,FNDX                                                          
         LA    R1,1(R1)                                                         
         STC   R1,FNDX                                                          
         B     VALACT8                                                          
*                                                                               
VALACT20 MVI   FNDX,0              AT END DEAL WITH MISSING VALUES              
         CLI   ACTION,C'R'                                                      
         BNE   VALACTX                                                          
         CLI   LOLINE+1,0                                                       
         BNE   *+14                                                             
         MVI   LOLINE+1,1                                                       
         MVC   HILINE+1(1),PLINES                                               
         MVI   FERN,MISSING                                                     
         CLI   LOLD+1,0                                                         
         BE    ERROR                                                            
         CLI   LNEW+1,0                                                         
         BE    ERROR                                                            
VALACTX  B     MOVTXT                                                           
         EJECT                                                                  
*              MOVE TEXT FROM TWA TO W/S INSERTING SPECIAL SYMBOLS              
*                                                                               
MOVTXT   LA    R3,1                R3 = TEXT LINE NUMBER                        
         LR    R4,R3                                                            
         ZIC   R5,PLINES                                                        
         LA    R6,TXTAREA                                                       
         ST    R6,ASTATXT                                                       
         L     R2,PAFIRSTH                                                      
         LA    R2,0(R2)                                                         
         MVI   BYTE,C' '           BYTE = FORCE ANOTHER EOL INDICATOR           
         BCTR  R6,0                                                             
         MVI   0(R6),C' '          R6 = START OF TEXT MINUS 1                   
*                                                                               
MOVTXT2  OC    ALOTWA,ALOTWA       LOOK FOR START UPDATE POINT                  
         BNZ   MOVTXT3                                                          
         CH    R3,LOLINE                                                        
         BNE   MOVTXT10                                                         
         ST    R2,ALOTWA                                                        
MOVTXT3  LA    R1,8(R2)            CHECK FOR FORCED EOL                         
         BAS   RE,ISITEOL                                                       
         BNE   *+8                                                              
         OI    0(R6),EOL                                                        
         LA    R6,1(R6)                                                         
         ZIC   R1,0(R2)            FIND SIGNIFICANT LENGTH OF LINE              
         SH    R1,=H'8'                                                         
         LA    RE,7(R1,R2)                                                      
         CLI   0(RE),C' '                                                       
         BH    *+14                                                             
         BCT   R1,*-12                                                          
         BCTR  R6,0                EMPTY LINE - IGNORE                          
         B     MOVTXT6                                                          
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R6),8(R2)       MOVE LINE TO W/S                             
         LA    R6,1(R1,R6)         BUMP W/S POINTER                             
         MVC   0(1,R6),BYTE        FORCE ANOTHER EOL IF AFTER S= OR N=          
         MVI   BYTE,C' '                                                        
MOVTXT6  CH    R3,HILINE           INDICATE END OF UPDATE TEXT IF NOT           
         BNE   MOVTXT10            INSERT                                       
         CLI   ACTION,C'I'                                                      
         BE    MOVTXT10                                                         
         OI    0(R6),EOC                                                        
*                                                                               
MOVTXT10 BXLE  R3,R4,*+8           BUMP LINE                                    
         B     MOVTXT20            END OF TEXT                                  
         SR    R1,R1                                                            
         IC    R1,0(R2)                                                         
         AR    R2,R1                                                            
         TM    1(R2),X'20'                                                      
         BO    *-10                                                             
         B     MOVTXT2                                                          
*                                                                               
MOVTXT20 MVI   0(R6),EOT           SAVE END ADDRESSES                           
         ST    R2,AHITWA                                                        
         ST    R6,AENDTXT                                                       
         MVI   FERN,INVNOS         IF END OF TEXT NOT GTR THAN START            
         C     R6,ASTATXT          LINES NOT IN USE                             
         BNH   ERROR                                                            
         CLI   ACTION,C'R'         AND GO TO HANDLE ACTION                      
         BE    REPLACE                                                          
         CLI   ACTION,C'I'                                                      
         BE    INSERT                                                           
         B     DELETE                                                           
*                                                                               
ISITEOL  DS    0H                  CHECK FOR FORCE NEW LINE CHARS               
         CLC   0(2,R1),=C'N='      LINE STARTING WITH N=                        
         BE    *+14                                                             
         CLC   0(2,R1),=C'S='      OR S=                                        
         BNE   *+10                                                             
         MVI   BYTE,EOL            SET INDICATOR FOR ANOTHER EOL                
         BR    RE                                                               
         CLI   BYTE,EOL                                                         
         MVI   BYTE,C' '                                                        
         BER   RE                  2ND EOL AFTER N= OR S=                       
         CLI   0(R1),C' '          LINE STARTING WITH SPACE                     
         BR    RE                                                               
         EJECT                                                                  
*              REPLACE OLD WITH NEW IN W/S                                      
*                                                                               
REPLACE  MVI   HITS,C'N'           SET REGISTERS                                
         L     R6,ASTATXT          R6 = COMPARE CHAR POINTER                    
         LH    RE,LOLD             RE = L'OLD - 1                               
         LH    RF,LNEW             RF = L'NEW - 1                               
         BCTR  RE,0                                                             
         BCTR  RF,0                                                             
         LR    R4,RF                                                            
         SR    R4,RE               R4 = L'NEW - L'OLD IF POSITIVE               
         BP    *+8                                                              
         LA    R4,1                   = 1 OTHERWISE                             
         LR    R7,R4               R7 = R4 - 1                                  
         BCTR  R7,0                                                             
         L     R5,AENDTXT          R5 = A(END OF TEXT)                          
*                                                                               
REPLAC02 TM    0(R6),EOC           SCAN FOR MATCH WITH OLD STRING               
         BO    REPLAC20            UP TO EOC                                    
         EX    RE,*+16                                                          
         BE    REPLAC04                                                         
         LA    R6,1(R6)                                                         
         B     REPLAC02                                                         
         CLC   0(0,R6),OLD                                                      
*                                                                               
REPLAC04 MVI   HITS,C'H'           MATCH FOUND SO SWAP IN NEW                   
         LR    R1,R6               R1 = A(OLD)                                  
         AH    R6,LNEW             R6 = A(CHAR AFTER NEW)                       
         LA    R2,1(R1,RE)         R2 = A(CHAR AFTER OLD)                       
         MVC   BUFFER,0(R2)                                                     
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R1),NEW                                                      
         CR    RE,RF               COMPARE L'NEW AND L'OLD                      
         BE    REPLAC02            SAME                                         
         LR    R3,R6                                                            
         BL    REPLAC08                                                         
*                                                                               
REPLAC06 MVC   0(1,R3),0(R2)       NEW SHORTER THAN OLD - SHUFFLE UP            
         LA    R2,1(R2)                                                         
         BXLE  R3,R4,*-10                                                       
         AR    R5,RF               ADJUST A(END OF TEXT)                        
         SR    R5,RE                                                            
         B     REPLAC02                                                         
*                                                                               
REPLAC08 LA    R0,L'BUFFER         NEW LONGER THAN OLD - SHUFFLE DOWN           
         LR    R1,R0               VIA BUFFERS                                  
         LA    R2,BUFFER(R1)                                                    
         AR    R5,R4               ADJUST A(END OF TEXT)                        
REPLAC09 MVC   0(L'BUFFER,R2),0(R3)                                             
         XR    R1,R0               SWITCH BUFFERS                               
         LA    R2,BUFFER(R1)                                                    
         EX    R7,*+12                                                          
         BXLE  R3,R4,REPLAC09                                                   
         B     REPLAC02                                                         
         MVC   0(0,R3),0(R2)                                                    
*                                                                               
REPLAC20 ST    R5,AENDTXT          END                                          
         MVI   FERN,NOHITS                                                      
         CLI   HITS,C'H'                                                        
         BNE   ERROR               NO HITS                                      
*                                                                               
REPLAC22 L     R3,ASTATXT          RECHECK SPECIAL SYMBOLS (EOC/EOL)            
         BCTR  R3,0                STARTING AT TEXT MINUS 1                     
         LA    R4,1                                                             
         BCTR  R5,0                                                             
REPLAC24 CLI   0(R3),EOC           REMOVE EOC                                   
         BE    REPLAC26                                                         
         TM    0(R3),EOL                                                        
         BNO   REPLAC27                                                         
         LA    R1,1(R3)                                                         
         BAS   RE,ISITEOL                                                       
         BE    *+8                                                              
REPLAC26 MVI   0(R3),C' '                                                       
REPLAC27 BXLE  R3,R4,REPLAC24                                                   
         B     SCREEN                                                           
         EJECT                                                                  
*              INSERT LINE(S)                                                   
*                                                                               
INSERT   L     R6,ASTATXT          PREFIX TEXT WITH SPACE+EOL FOR               
         LH    R1,HILINE           EACH INSERT LINE                             
         SH    R1,LOLINE                                                        
         LA    R1,1(R1)                                                         
INSERT2  SH    R6,=H'2'                                                         
         MVI   0(R6),C' '                                                       
         MVI   1(R6),EOL                                                        
         BCT   R1,INSERT2                                                       
         ST    R6,ASTATXT          AND RESET A(START OF TEXT)                   
*                                                                               
INSERT4  L     R2,PAACTH           PREPARE RETURN PARMS                         
         ZIC   R1,0(R2)                                                         
         SH    R1,=H'9'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         XC    8(0,R2),8(R2)                                                    
         MVC   8(6,R2),=C'CHANGE'                                               
         OI    6(R2),X'80'                                                      
         MVC   PACURSOR,ALOTWA                                                  
         MVI   PFERN,OK                                                         
         LA    R1,COMPMESS                                                      
         ST    R1,PAMSG                                                         
         B     SCREEN                                                           
         SPACE 3                                                                
*              DELETE LINE(S)                                                   
*                                                                               
DELETE   L     R6,ASTATXT          RESET A(START OF TEXT) TO 1 PAST             
         TM    0(R6),EOC           EOC                                          
         BO    *+12                                                             
         LA    R6,1(R6)                                                         
         B     *-12                                                             
         CLI   0(R6),EOT                                                        
         BE    *+8                 OR TO EOC IF DELETE IS UP TO END             
         LA    R6,1(R6)                                                         
         ST    R6,ASTATXT                                                       
         B     SCREEN                                                           
         EJECT                                                                  
*              MOVE TEXT FROM W/S TO TWA                                        
*                                                                               
SCREEN   L     R3,ALOTWA           R3/4/5 BXLE REGS FOR TWA CONTROL             
         SR    R4,R4                                                            
         L     R5,AHITWA                                                        
         ZIC   R2,0(R3)                                                         
         SH    R2,=H'8'            R2 = FLD LENGTH (ASSUMED CONSTANT)           
         L     R7,ASTATXT          R7 = START OF TEXT LINE PNTR IN W/S          
         MVI   BYTE,0              BYTE=EOT IF FOUND                            
*                                                                               
SCREEN02 LR    R6,R7               SEARCH FOR EOT/EOL (FORCED EOL)              
         LA    R0,1                                                             
         LA    R1,0(R2,R7)                                                      
         MVC   CRD,SPACES                                                       
         TM    0(R6),EOL                                                        
         BO    *+12                                                             
         BXLE  R6,R0,*-8                                                        
         B     SCREEN04                                                         
         MVC   BYTE,0(R6)          SAVE SPECIAL SYMBOL                          
         SR    R6,R7                                                            
         SH    R6,=H'1'                                                         
         BM    SCREEN10                                                         
         EX    R6,MOVTOCRD         MOVE LINE UP TO EOL TO CRD                   
         LA    R7,2(R6,R7)         MOVE POINTER TO ONE PAST EOL                 
         B     SCREEN10                                                         
*                                                                               
SCREEN04 SH    R7,=H'2'            NO FORCED EOL SO FIND A CHOP POINT           
         LR    R1,R2                                                            
SCREEN05 LA    R6,0(R1,R7)                                                      
         CLI   2(R6),C' '          BEFORE A SPACE                               
         BE    SCREEN07                                                         
         CLI   1(R6),C','          OR AFTER A COMMA,PERIOD OR HYPHEN            
         BE    SCREEN06            PROVIDED IT ISN'T IN A NUMERIC               
         CLI   1(R6),C'.'          EXPRESSION                                   
         BE    SCREEN06                                                         
         CLI   1(R6),C'-'                                                       
         BNE   *+12                                                             
SCREEN06 TM    2(R6),X'F0'                                                      
         BNO   *+10                                                             
         BCT   R1,SCREEN05                                                      
         LR    R1,R2               NO BREAK POINT SO CHOP ON FULL LINE          
SCREEN07 LA    R7,2(R7)                                                         
         BCTR  R1,0                                                             
         EX    R1,MOVTOCRD         MOVE LINE TO CRD                             
         LA    R7,1(R1,R7)         REPOSITION POINTER                           
         CLI   0(R7),C' '                                                       
         BNE   SCREEN10                                                         
         LA    R7,1(R7)                                                         
*                                                                               
SCREEN10 BAS   RE,TOTWA                                                         
         BNZ   OKEND                                                            
         CLI   BYTE,EOT                                                         
         BNE   SCREEN02                                                         
         MVC   CRD,SPACES                                                       
         BAS   RE,TOTWA                                                         
         BZ    *-4                                                              
         B     OKEND                                                            
*                                                                               
MOVTOCRD MVC   CRD(0),0(R7)                                                     
*                                                                               
TOTWA    IC    R4,0(R3)                                                         
         SH    R4,=H'9'                                                         
         EX    R4,TOTWOC                                                        
         EX    R4,TOTWCLC                                                       
         BE    *+12                                                             
         EX    R4,TOTWMVC                                                       
         OI    6(R3),X'80'                                                      
TOTWA2   IC    R4,0(R3)                                                         
         BXLE  R3,R4,*+8                                                        
         LTR   RB,RB                                                            
         BR    RE                                                               
         TM    1(R3),X'20'                                                      
         BZR   RE                                                               
         B     TOTWA2                                                           
TOTWOC   OC    8(0,R3),SPACES                                                   
TOTWCLC  CLC   8(0,R3),CRD                                                      
TOTWMVC  MVC   8(0,R3),CRD                                                      
         EJECT                                                                  
*              ERRORS AND EXITS                                                 
*                                                                               
ERROR    MVC   PFERN,FERN          RETURN ERROR & FLD NUMBER                    
         MVC   PFNDX,FNDX                                                       
         CLI   FERN,200                                                         
         BL    END                                                              
         MVI   PFERN,X'FE'         SPECIAL - A(MESSAGE) IS IN P3                
         MVC   CRD,SPACES                                                       
         LA    R1,CRD                                                           
         ST    R1,PAMSG                                                         
         ZIC   R1,FERN                                                          
         SH    R1,=H'200'                                                       
         MH    R1,=H'20'                                                        
         LA    R1,ERRMESS(R1)                                                   
         MVC   CRD(09),=C'ERROR 000'                                            
         MVC   CRD+10(20),0(R1)                                                 
         B     END                                                              
*                                                                               
ERRMESS  DS    0CL20                                                            
         DC    CL20'INVALID SUBACTION'                                          
         DC    CL20'NO HITS'                                                    
         DC    CL20'INVALID LINE NO(S)'                                         
*                                                                               
COMPMESS DC    CL60'ACTION COMPLETED - NOW ENTER TEXT'                          
*                                                                               
         SPACE 3                                                                
OKEND    MVI   PFNDX,0                                                          
END      L     R1,APARMS                                                        
         MVC   0(L'PARMS,R1),PARMS                                              
EXIT     XIT1                                                                   
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
*              DSECT TO COVER WORKING STORAGE PROVIDED BY CALLER                
*              (1200 BYTE AREA ADDRESSED BY PARA 3)                             
*                                                                               
WORKD    DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
APARMS   DS    A                                                                
ALOTWA   DS    A                   A(FIRST UPDATE TEXT FLD HDR)                 
AHITWA   DS    A                   A(LAST TEXT FLD HDR)                         
ASTATXT  DS    A                   A(START OF TEXT IN W/S)                      
AENDTXT  DS    A                   A(END OF TEXT IN W/S)                        
*                                                                               
PARMS    DS    0CL16               INPUT PARAMETERS                             
PAACTH   DS    A                   P1 B0-3 = A(ACTION FLD HDR)                  
PLINES   DS    0CL1                P2 B0   = NUMBER OF TEXT LINES               
PAFIRSTH DS    A                      B1-3 = A(FIRST TEXT FLD HDR)              
PAWORK   DS    A                   P3      = A(1200 BYTE WORK AREA)             
PASCAN   DS    A                   P4      = A(SCANNER)                         
         ORG   PARMS               RETURNED PARAMETERS                          
PFERN    DS    0CL1                P1 B0   = X'00' - NOW VALIDATE TWA           
*                                            X'FF' - ACTION COMPLETE            
*                                            X'FE' - ERROR - MSG IN P3          
*                                            X'NN' - ACCPAK ERROR NUM           
PACURSOR DS    A                      B1-3 = A(CURSOR FLD HDR)                  
PFNDX    DS    0CL1                P2 B0   = FLD NUMBER IF ERROR                
         DS    A                      B1-3 = UNCHANGED                          
PAMSG    DS    A                   P3 B0-3 = A(MESSAGE IF P1B0 = FE/FF)         
         DS    A                   P4 B0-3 = UNCHANGED                          
*                                                                               
LOLD     DS    H                   L OF OLD CHAR STRING (REPLACE)               
OLD      DS    CL32                                                             
LNEW     DS    H                   L OF NEW CHAR STRING                         
NEW      DS    CL32                                                             
LOLINE   DS    H                   1ST LINE NUMBER                              
HILINE   DS    H                   LST LINE NUMBER                              
ACTION   DS    CL1                 R = REPLACE, I = INSERT, D = DELETE          
*                                                                               
CRDH     DS    CL8                                                              
CRD      DS    CL80                                                             
SPACES   DS    CL80                                                             
BUFFER   DS    2CL32               BUFFER FOR REPLACE TEXT SHUFFLING            
FERN     DS    CL1                                                              
FNDX     DS    CL1                                                              
BYTE     DS    CL1                                                              
HITS     DS    CL1                 H = REPLACEMENT HITS                         
*                                                                               
INSERTS  DS    CL40                EXTRA TEXT AREA FOR INSERT LINES             
TXTAREA  DS    800C                TEXT AREA                                    
WORKX    DS    0C                                                               
         SPACE 2                                                                
*              EQUATES                                                          
*                                                                               
*                                  SPECIAL SYMBOLS USED IN TEXT AREA            
EOT      EQU   X'FF'               END OF TEXT                                  
EOL      EQU   X'FC'               END OF LINE (FORCED)                         
EOC      EQU   X'FA'               END OF CHANGED TEXT                          
*                                                                               
*                                  FERN VALUES                                  
VALIDATE EQU   0                                                                
MISSING  EQU   1                                                                
INVALID  EQU   2                                                                
INVNUM   EQU   3                                                                
DUPLICAT EQU   35                                                               
INVACT   EQU   200                                                              
NOHITS   EQU   201                                                              
INVNOS   EQU   202                                                              
SPECIAL  EQU   X'FE'                                                            
OK       EQU   X'FF'                                                            
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003ACTEXTER  05/01/02'                                      
         END                                                                    
