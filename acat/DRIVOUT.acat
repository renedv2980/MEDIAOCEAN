*          DATA SET DRIVOUT    AT LEVEL 065 AS OF 05/01/02                      
*CATALP DRIVOUT                                                                 
         TITLE 'CHANGE LOG'                                                     
* ROSA 3/15/91 EXTEND PRINT SUPPRESSION FROM 2 TO 4 LINES OF PRINT L01          
*                                                                               
         TITLE 'DRIVOUT - OUTPUT HANDLER FOR DRIVER'                            
DRIVOUT  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 DOUTDEND-DOUTD,**DROUT*,R9,R8,R7,CLEAR=YES                       
         USING DOUTD,RC                                                         
         L     RA,0(R1)                                                         
         USING GLOBALD,RA                                                       
         MVI   SAVEBXCL,C' '                                                    
         MVC   SAVEBXCL+1(197),SAVEBXCL                                         
         CLI   GLANYERR,C'Y'                                                    
         BE    XIT                                                              
         CLI   GLABCSW,0           ABC HANDLES OWN LOGIC                        
         BNE   OUTABC                                                           
         CLI   GLANYSRT,C'Y'       TEST ANY RECORDS PUT TO SORTER               
         BE    DROUT2                                                           
         GOTO1 =V(DRIVPUT),DMCB,(RA),0  NO-DRIVPUT MAY HAVE TO EMPTY            
         CLI   GLANYSRT,C'Y'               ITS STACK                            
         BNE   DUMOUT                                                           
         SPACE 1                                                                
DROUT2   BAS   RE,OUTINIT                                                       
         B     OUT                                                              
         SPACE 1                                                                
OUTABC   BAS   RE,OUTINIT                                                       
         CLI   SPLATSW,0           MAY BE USING DRIVOUT JUST TO SPACE           
         BNE   OUTSPLAT                                                         
         L     R5,GLAIO            R5=A(RECORD FORMATTED BY DRIVIN)             
         MVI   1(R5),1             ABC RECORDS ARE AT LEVEL 1                   
         BAS   RE,OUTPOST                                                       
         BAS   RE,DETAIL                                                        
         B     XIT                                                              
         SPACE 1                                                                
OUTSPLAT CLI   SPLATSW,X'FF'                                                    
         BE    OUTLAST                                                          
         MVC   NLINES,SPLATSW      PASS LINES TO SPACE IN SPLATSW               
         BAS   RE,SPLAT                                                         
         MVI   SPLATSW,0                                                        
         B     XIT                                                              
         SPACE 1                                                                
OUTLAST  BAS   RE,DOWNLAST         LAST TIME FOR ABC                            
         B     XIT                                                              
         SPACE 1                                                                
DUMOUT   L     R2,GLAIO            THERE WERE NO RECORDS                        
*******  GOTO1 SORTER,DMCB,=C'PUT',(R2)   THIS IS TO STOP                       
*******  GOTO1 SORTER,DMCB,=C'GET',(R2)   SORTER BLOWING UP                     
*******  GOTO1 SORTER,DMCB,=C'GET',(R2)   WHEN WE EXIT TO SPOOF                 
         B     XIT                                                              
         EJECT                                                                  
*              MAIN I/O AND PROGRAM FLOW                                        
         SPACE 3                                                                
OUT      GOTO1 =V(DRIVSORT),DMCB,(RA)     RECORD INTO GLANEXT                   
         CLI   8(R1),X'80'         EOF HANDLING                                 
         BNE   OUT2                                                             
         BAS   RE,CLRTMPBF         CLEAR TEMPBUFF FOR NEXT REQUEST              
         MVI   CBLEV,0                                                          
         L     R1,GLAIO                                                         
         CLI   0(R1),0             IF WE ARE NOT ON FIRST TIME                  
         BE    XIT                                                              
         BAS   RE,MULTLT                                                        
         BAS   RE,DOWNLAST                                                      
         B     XIT                                                              
         SPACE 1                                                                
OUT2     CLI   0(R1),1             REJECT INACTIVE RECORDS                      
         BE    OUT                                                              
         L     R5,GLANEXT                                                       
         BAS   RE,SETGLINT                                                      
         USING GLINTD,R2                                                        
         CLC   1(1,R5),GLDETLEV    ONLY INTERESTED IN DETAILS                   
         BNE   OUTTOT                                                           
         BAS   RE,CHECKVAL         CHECK FOR MIN/MAX                            
         BE    OUT4                                                             
         BAS   RE,BACKOUT          FAILED - BACK THIS OUT OF TOTALS             
         BAS   RE,BACKTEMP                                                      
         B     OUT                          AND REJECT THIS RECORD              
         SPACE 1                                                                
OUT4     BAS   RE,SETCB            SET CONTROL BREAK LEVEL                      
         L     R1,GLAIO                                                         
         CLI   0(R1),0             IF WE ARE NOT ON FIRST TIME                  
         BE    *+8                                                              
         BAS   RE,MULTLT           DEAL WITH LAST TIMES                         
         SPACE 1                                                                
         CLC   0(1,R1),0(R5)       IF THIS IS A NEW RECORD TYPE                 
         BE    OUT6                                                             
         L     R1,AMYM1            CLEAR MIDLINES                               
         MVC   0(198,R1),SPACES                                                 
         MVC   198(198,R1),SPACES                                               
         MVI   COMPSTAT,0          RESET COMPUTES STATUS                        
         MVC   LASTPDET,SPACES     RESET CONTINUATION FEATURE                   
         MVC   LASTP2,SPACES                                                    
         XC    TOTCNTS,TOTCNTS     RESET TOTAL COUNTERS                         
         XC    DETTOTS,DETTOTS     RESET DETAILED TOTAL INDICATORS              
         L     R1,GLAFHEAD                                                      
         LTR   R1,R1                                                            
         BZ    OUT6                                                             
         MVI   0(R1),C'Y'          FORCE HEADLINES AFTER LAST TIMES             
         TM    GLINDS2,GLMIDHED    OR OPTIONALLY                                
         BNO   OUT6                                                             
         MVI   0(R1),C'M'          FORCE MIDHEAD                                
         SPACE 1                                                                
OUT6     BAS   RE,BUFFPOST         POST TOTALS FROM TEMPBUFF                    
         L     R6,GLAIO            MOVE DETAIL INTO GLAIO                       
         LH    R1,GLRECLEN                                                      
         MOVE  ((R6),(R1)),(R5)                                                 
         L     R5,GLAIO                                                         
         BAS   RE,BUFFPO4          POST, INT. COMPUTE ETC                       
         BAS   RE,SETGLINT                                                      
         BAS   RE,MULTFT           HANDLE ANY FIRST TIME ROUTINES               
         OC    GLARANK,GLARANK     UNLESS RECORD RANKED                         
         BNZ   OUT8                                                             
         TM    GLRECIND,GLCLRANK                                                
         BO    OUT8                                                             
         BAS   RE,DETAIL           DO THE DETAILS                               
         B     OUT                                                              
         SPACE 1                                                                
OUT8     BAS   RE,PUTRANK          IF WE'RE RANKING RECORDS                     
         B     OUT                 WE NEED TO GO TO BUFFALO NOW                 
         SPACE 1                                                                
OUTTOT   BAS   RE,BUFFSAVE         SAVE TOTAL RECORDS FOR THE MOMENT            
         B     OUT                                                              
         EJECT                                                                  
*              BUFFSAVE - TUCK AWAY RECORD                                      
         SPACE 3                                                                
*              INPUT               R5=A(RECORD)                                 
*              OUTPUT              TUCK AWAY INTO TEMPBUFF                      
         SPACE 1                                                                
BUFFSAVE NTR1                                                                   
         L     R3,=A(TEMPBUFF)                                                  
         LA    R3,8(R3)                                                         
         USING GLINTD,R2                                                        
         BAS   RE,SETGLINT                                                      
         LH    R2,GLRECLEN         R1=RECORD LENGTH +2 (FOR LENGTH)             
         DROP  R2                                                               
         LA    R2,2(R2)                                                         
         SPACE 1                                                                
BUFFSV2  OC    0(2,R3),0(R3)       LOOK FOR AN EMPTY SLOT                       
         BZ    BUFFSV4                                                          
         CLC   2(2,R3),0(R5)       OR FOR RECORD AT SAME LEVEL                  
         BE    BUFFSV6                                                          
         LH    R0,0(R3)                                                         
         AR    R3,R0                                                            
         B     BUFFSV2                                                          
         SPACE 1                                                                
BUFFSV4  L     R4,=A(TEMPBUFF)                                                  
         L     RE,0(R4)            N'BYTES IN BUFF SO FAR                       
         AR    RE,R2               ADD THIS ONE                                 
         C     RE,4(R4)            CHECK V MAX                                  
         BL    *+6                                                              
         DC    H'0'                **** INCREASE TEMPBUFF OR SOMETHING          
         ST    RE,0(R4)                                                         
         SPACE 1                                                                
BUFFSV6  SH    R5,=H'2'            ABOUT TO MOVE 2 EXTRA BYTES                  
         MOVE  ((R3),(R2)),(R5)                                                 
         STH   R2,0(R3)            SAVE RECORD LENGTH                           
         B     XIT                                                              
         SPACE 1                                                                
SETGLINT ZIC   R2,0(R5)            FROM RECORD NUMBER                           
         LTR   R2,R2               PROTECT FOR BUDGET (NO RECORDS)              
         BNZ   *+8                                                              
         LA    R2,1                                                             
         BCTR  R2,0                                                             
         SLL   R2,2                                                             
         LA    R2,GLAINTD(R2)                                                   
         L     R2,0(R2)                                                         
         BR    RE                                                               
         EJECT                                                                  
*              BUFFPOST - EMPTY TEMPBUFF AND POST                               
         SPACE 3                                                                
*              INPUT               TEMPBUFF                                     
         SPACE 1                                                                
BUFFPOST NTR1                                                                   
         L     R3,=A(TEMPBUFF)                                                  
         XC    0(4,R3),0(R3)       RESET BYTES USED TO 0                        
         LA    R3,8(R3)                                                         
         L     R5,GLAIO                                                         
         SPACE 1                                                                
BUFFPO2  OC    0(2,R3),0(R3)       LOOK FOR AN EMPTY SLOT                       
         BZ    XIT                 ALL DONE                                     
         LH    R4,0(R3)            LENGTH OF THIS RECORD (+2)                   
         SH    R4,=H'2'                                                         
         MOVE  ((R5),(R4)),2(R3)   RETURN RECORD TO GLAIO                       
         BAS   RE,BUFFPO4                                                       
         LA    R4,2(R4)                                                         
         XCEF  (R3),(R4)           CLEAR                                        
         AR    R3,R4                                                            
         B     BUFFPO2                                                          
         SPACE 1                                                                
BUFFPO4  NTR1                                                                   
         MVC   GLRECNO,0(R5)       SET RECORD                                   
         MVC   GLLEVEL,1(R5)       AND LEVEL NUMBER                             
         BAS   RE,SETGLINT                                                      
         ST    R2,GLATHID                                                       
         GOTO1 =V(DRIVICMP),DMCB,(RA)   CONTROL INTERNAL COMPUTES               
         BAS   RE,OUTPOST          POST RECORD IN CASE OF VERTICAL %            
         BAS   RE,COMPREC          CHECK COMPUTES FOR RECORD                    
         BAS   RE,OUTPOST          THEN REPOST THIS                             
         B     XIT                                                              
         SPACE 3                                                                
* ROUTINE TO CLEAR TEMPBUFF                                                     
*                                                                               
         SPACE 1                                                                
CLRTMPBF LR    R0,RE                                                            
         L     RE,=A(TEMPBUFF)                                                  
         XC    0(4,RE),0(RE)                                                    
         L     RF,4(RE)                                                         
         LA    RE,8(RE)                                                         
         XCEFL ,                                                                
         LR    RE,R0                                                            
         BR    RE                                                               
         EJECT                                                                  
*              INITIALIZATION FOR OUT ROUTINES                                  
         SPACE 3                                                                
OUTINIT  NTR1                                                                   
         USING DOUTD,RC,RF                                         L01          
         LA    RF,4095(RC)                                         L01          
         LA    RF,1(RF)                                            L01          
         LA    RE,DOUT2                                            L01          
         ST    RE,ADOUT2                                           L01          
         DROP  RF                                                               
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(197),SPACES                                             
         MVC   LASTPDET,SPACES                                                  
         MVC   LASTP2,SPACES                                                    
         MVC   LASTMID,SPACES                                                   
         MVC   SAVEDET,SPACES                                                   
         MVC   SAVEP2,SPACES                                                    
         MVC   SAVEP3,SPACES                                       L01          
         MVC   SAVEP4,SPACES                                       L01          
         MVC   LASTP3,SPACES                                       L01          
         MVC   LASTP4,SPACES                                       L01          
         MVC   P,SPACES                                                         
         SPACE 1                                                                
         GOTO1 =V(DRIVRANK),DMCB,(RA),=C'CLR'                                   
         L     R2,GLAHEDHK         INTERCEPT SO WE GET                          
         LA    R1,HOOK             THE HEAD HOOK                                
         ST    R1,0(R2)                                                         
         XC    TOTCNTS,TOTCNTS                                                  
         CLI   GLABCSW,0           DON'T CLEAR FOR ABC                          
         BNE   XIT                 IT HAS THE RECORD THERE ALREADY!             
         L     RE,GLAIO                                                         
         LA    RF,1000                                                          
         XCEF                                                                   
         GOTO1 =V(DRIVVTST),DMCB,(X'FF',(RA))  INITIALIZE                       
         B     XIT                                                              
         EJECT                                                                  
*              CHECK FOR MIN/MAX VALUES                                         
         SPACE 3                                                                
*              INPUT               R5=A(RECORD TO BE CHECKED)                   
         SPACE 1                                                                
CHECKVAL NTR1                                                                   
         BAS   RE,SETGLINT                                                      
         USING GLINTD,R2                                                        
         LH    R2,GLRECLEN         R1=RECORD LENGTH +2 (FOR LENGTH)             
         CH    R2,=H'4096'                                                      
         BL    *+8                                                              
         CH    R2,=H'4096'                                                      
         L     R3,=A(TEMPREC)      SAVE RECORD SO WE CAN TEST IT                
         MOVE  ((R3),(R2)),(R5)                                                 
         LR    R5,R3                   NOW R5 ADDRESSES TEMP RECORD             
         GOTO1 =V(DRIVICMP),DMCB,(RA)  MAYBE INTERNAL COMPUTES                  
         BAS   RE,COMPREC              OR DRIVER COMPUTES                       
         GOTO1 =V(DRIVVTST),DMCB,(RA)  DRIVVTST DOES THE WORK                   
         B     XIT                     AND SETS THE CC                          
         EJECT                                                                  
*              DEALING WITH RANK OUTPUT                                         
         SPACE 3                                                                
*                                  FOR EACH RANK RECORD ROUTINE WILL            
*                                  ESTABLISH RANK NUMBER WHICH WILL             
*                                  REPLACE VALUE IN RECORD AND SET              
*                                  RANK NUMBER (RANKEQU) AND EQUALITY           
*                                  SWITCH (EQUSW)                               
         SPACE 1                                                                
RANKOUT  NTR1                                                                   
         L     R3,ADOUT2                                           L01          
         USING DOUT2,R3                                                         
         CLI   ANYRANK,C'Y'        ANYTHING PENDING?                            
         BNE   XIT                                                              
         MVI   ANYRANK,C'N'                                                     
         MVI   EQUSW,C'N'                                                       
         XC    LASTVAL,LASTVAL                                                  
         BAS   RE,RANKGET                                                       
         XC    RANKNO,RANKNO                                                    
         MVC   RANKEQU,=F'1'                                                    
         SPACE 1                                                                
RANKOUT2 BAS   RE,RANKGET                                                       
         TM    ABCOUNT,X'80'       'EOF'?                                       
         BO    XIT                                                              
         CLC   ABVAL,LASTVAL       COMPARE THIS AGAINST LAST                    
         BE    RANKOUT4                                                         
         MVC   RANKEQU,RANKNO      DIFFERENT SO RESET RANK NO                   
         MVI   EQUSW,C'N'                                                       
         TM    BBCOUNT,X'80'       IS NEXT EOF?                                 
         BO    RANKOUT4                                                         
         MVC   LASTVAL,ABVAL                                                    
         CLC   ABVAL,BBVAL         COMPARE THIS AGAINST NEXT                    
         BNE   *+8                                                              
         MVI   EQUSW,C'Y'                                                       
         SPACE 1                                                                
RANKOUT4 LA    R5,ABDATA           POST FROM BUFFALO 'A' DATA                   
         BAS   RE,OUTPOST          POST RECORD IN CASE OF VERT %                
         BAS   RE,COMPREC          CHECK COMPUTES FOR RECORD                    
         BAS   RE,OUTPOST          NOW RE-POST                                  
         OC    GLRNKMAX,GLRNKMAX   OPTION TO SET MAX TO RANK                    
         BZ    RANKOUT6                                                         
         CLC   RANKEQU,GLRNKMAX                                                 
         BNH   RANKOUT6                                                         
         BAS   RE,BACKOUT          BACK DETAIL OUT OF TOTALS                    
         B     RANKOUT2                                                         
         SPACE 1                                                                
RANKOUT6 BAS   RE,DETAIL           AND HANDLE DETAIL LINE                       
         B     RANKOUT2                                                         
         DROP  R3                                                               
         EJECT                                                                  
*              PUTTING RECORDS OUT TO BUFFALO                                   
         SPACE 3                                                                
*                                  R4=A(BUFFALOC)                               
*                                  R5=PRESENT DATA RECORD                       
PUTRANK  NTR1                                                                   
         USING GLINTD,R2                                                        
         L     R3,ADOUT2                                           L01          
         USING DOUT2,R3                                                         
         L     R4,=A(BUFFALOC)                                                  
         USING BUFFALOD,R4                                                      
         CLI   ANYRANK,C'Y'        IF THIS IS THE FIRST PUT                     
         BE    PUTRANK2                                                         
         LH    R1,GLRECLEN         NEED TO INITIALIZE BUFFALO CSECT             
         ST    R1,BUFFLCOM                                                      
         LA    R1,8(R1)                                                         
         SR    RE,RE                                                            
         L     RF,=F'50000'        HOW MANY WILL FIT INTO 50000 BYTES?          
         DR    RE,R1                                                            
         BCTR  RF,0                                                             
         ST    RF,BUFFCRMX         TELL BUFFALO ABOUT THAT                      
         GOTO1 BUFFALO,DMCB,=C'SET',(R4)                                        
         XC    ABCOUNT,ABCOUNT                                                  
         XC    BBVAL(8),BBVAL                                                   
         SPACE 1                                                                
PUTRANK2 MVI   ANYRANK,C'Y'                                                     
         XC    ABVAL,ABVAL         BUFFALO RECORD IS THAT VALUE                 
         L     R1,GLARANK          NEED TO COMPUTE VALUE TO RANK                
         LTR   R1,R1               NOT NECESSARILY ANYTHING                     
         BZ    PUTRANK4                                                         
         USING DRIND,R1                                                         
         L     R1,DRINCOMP                                                      
         LTR   R1,R1               NOT NECESSARILY ANYTHING                     
         BZ    PUTRANK4                                                         
         ST    R1,DMCB+4                                                        
         GOTO1 =V(DRIVCOMP),DMCB,(RA),,(R5)                                     
         CP    DUB,=P'-1000000000' PROTECT AGAINST HUGE NEGATIVE                
         BL    PUTRANK4                                                         
         MVC   ABVAL,=4X'FF'                                                    
         CP    DUB,=P'1000000000'  OR POSITIVE NUMBERS                          
         BH    PUTRANK4                                                         
         CVB   R1,DUB                                                           
         A     R1,=F'1000000000'   CONVERT TO ALL POSITIVE                      
         ST    R1,ABVAL                                                         
         SPACE 1                                                                
PUTRANK4 GOTO1 =V(DRIVRANK),DMCB,(RA),=C'PUT',(R5),ABCOUNT                      
         XC    ABVAL,=4X'FF'       COMPLEMENT                                   
         L     R1,ABCOUNT                                                       
         LA    R1,1(R1)                                                         
         ST    R1,ABCOUNT          COUNT TO KEY TO STOP BUFFALO                 
*                                  COMBINING SAME VALUE RECORDS                 
         LH    R6,GLRECLEN                                                      
         MOVE  (ABDATA,(R6)),(R5)                                               
         GOTO1 BUFFALO,DMCB,=C'PUT',(R4),ABVAL                                  
         B     XIT                                                              
         DROP  R3                                                               
         EJECT                                                                  
*              ROUTINE TO SHUFFLE BUFFALO RECORDS                               
         SPACE 3                                                                
*                                  R4=A(BUFFALOC)                               
         SPACE 1                                                                
RANKGET  NTR1                                                                   
         L     R3,ADOUT2                                           L01          
         USING DOUT2,R3                                                         
         L     R4,=A(BUFFALOC)                                                  
         USING BUFFALOD,R4                                                      
         L     R1,RANKNO           ADD 1 TO RANK NUMBER                         
         LA    R1,1(R1)                                                         
         ST    R1,RANKNO                                                        
         MOVE  (ABVAL,1000),BBVAL  MOVE B TO A                                  
         TM    ABCOUNT,X'80'       IS THIS THE FINAL RECORD?                    
         BNO   RANKGET2                                                         
         XC    BBVAL(8),BBVAL      CLEAR VAL & COUNT                            
         B     XIT                                                              
         SPACE 1                                                                
RANKGET2 LA    R2,=C'HIGH'         READ HIGH FIRST TIME                         
         OC    BBCOUNT,BBCOUNT                                                  
         BZ    *+8                                                              
         LA    R2,=C'SEQ'          OR SEQUENTIAL                                
         GOTO1 BUFFALO,DMCB,(R2),(R4),BBVAL,1                                   
         TM    DMCB+8,X'80'        DID BUFFALO SET EOF                          
         BNO   RANKGET4                                                         
         OI    BBCOUNT,X'80'       YES SO MARK B RECORD                         
         MVC   BBVAL,=X'FFFFFFFF'                                               
         MVI   DMCB+8,0            AND STOP CONFUSION IN DMCB                   
         SPACE 1                                                                
RANKGET4 DS    0H                                                               
         GOTO1 =V(DRIVRANK),DMCB,(RA),=C'GET',BBDATA,BBCOUNT                    
         B     XIT                 SO WE CAN SPOT IT NEXT TIME                  
         DROP  R3                                                               
         EJECT                                                                  
*              ROUTINE MOVES RECORD TO PRESET SPOT                              
         SPACE 3                                                                
*                                  R5=A(RECORD TO BE POSTED)                    
OUTPOST  NTR1                                                                   
         MVC   GLRECNO,0(R5)       SET RECORD                                   
         MVC   GLLEVEL,1(R5)       AND LEVEL NUMBER                             
         BAS   RE,SETGLINT                                                      
         ST    R2,GLATHID                                                       
         USING GLINTD,R2                                                        
         LH    R1,GLRECLEN                                                      
         ZIC   R6,GLLEVEL          AND LEVEL NUMBER                             
         SLL   R6,2                                                             
         LA    R6,GLARECL0(R6)                                                  
         OI    0(R6),X'80'         INDICATE THERE'S A REC AT THIS LEVEL         
         L     R6,0(R6)            FIGURE OUT WHERE TO POST                     
         ST    R6,AACTREC                                                       
         ST    R6,GLATHREC                                                      
         MOVE  ((R6),(R1)),(R5)    AND DO IT                                    
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE BACKS OUT DETAIL RECORD FROM TOTALS                      
         SPACE 3                                                                
*                                  R5=A(RECORD TO BE BACKED OUT)                
BACKOUT  NTR1                                                                   
         MVC   GLRECNO,0(R5)       SET RECORD                                   
         MVC   GLLEVEL,1(R5)       AND LEVEL NUMBER                             
         BAS   RE,SETGLINT                                                      
         ST    R2,GLATHID                                                       
         USING GLINTD,R2                                                        
         MVI   THISLEV,0                                                        
         SPACE 1                                                                
BACKOUT2 CLC   THISLEV,GLLEVEL     ALL LEVELS DONE?                             
         BE    XIT                                                              
         ZIC   R6,THISLEV          PICK UP CURRENT LEVEL NUMBER                 
         SLL   R6,2                                                             
         LA    R6,GLARECL0(R6)                                                  
         TM    0(R6),X'80'         IS THERE A TOTAL AT THIS LEVEL?              
         BNO   BACKOUT4                                                         
         L     R6,0(R6)            FIGURE OUT WHERE THIS TOTAL IS               
*                                  SUBTRACT DETAIL AT (R5)                      
*                                  FROM TOTAL AT (R6)                           
         GOTO1 =V(DRIVADD),DMCB,(C'-',(RA)),(R6),(R5)                           
         SPACE 1                                                                
BACKOUT4 AI    THISLEV,1                                                        
         B     BACKOUT2                                                         
         SPACE 1                                                                
BACKTEMP NTR1                                                                   
         L     R3,=A(TEMPBUFF)     ROUTINE TO BACK OUT OF TEMPBUFF              
         LA    R3,8(R3)                                                         
         SPACE 1                                                                
BACKTMP2 OC    0(2,R3),0(R3)       ANYTHING THERE?                              
         BZ    XIT                                                              
         LA    R6,2(R3)            POINT TO A TOTAL RECORD                      
         CLC   0(1,R6),0(R5)       CHECK REC. MATCH                             
         BNE   XIT                                                              
*                                  SUBTRACT DETAIL AT (R5)                      
*                                  FROM TOTAL AT (R6)                           
         GOTO1 =V(DRIVADD),DMCB,(C'-',(RA)),(R6),(R5)                           
         AH    R3,0(R3)                                                         
         B     BACKTMP2                                                         
         EJECT                                                                  
*              ROUTINE CONTROLS COMPUTES ON RECORD                              
         SPACE 3                                                                
*                                  R5=A(RECORD TO BE COMPUTED)                  
         SPACE 1                                                                
COMPREC  NTR1                                                                   
         CLI   COMPSTAT,1                                                       
         BE    XIT                 FORGET IT - NO COMPUTES NEEDED               
         MVI   COMPSTAT,1                                                       
         BAS   RE,COMP2                                                         
         BAS   RE,COMP2            SECOND TIME TO ALLOW RIGHT TO LEFT           
         B     XIT                                                              
         SPACE 1                                                                
COMP2    NTR1                                                                   
         ZIC   R2,0(R5)            RECORD NUMBER                                
         BCTR  R2,0                                                             
         SLL   R2,2                                                             
         LA    R2,GLAINTD(R2)                                                   
         L     R2,0(R2)                                                         
*******  ST    R2,GLATHID                                                       
         USING GLINTD,R2                                                        
         L     R3,GLAFIN                                                        
         DROP  R2                                                               
         SPACE 1                                                                
COMP20   LR    R4,R3               NOTE AN INPUT FIELD                          
         B     COMPNEXT                                                         
         SPACE 1                                                                
         USING DROD,R3                                                          
COMP30   OC    DROACOMP,DROACOMP   IN ORDER TO QUALIFY FIELD                    
         BZ    COMP30X             MUST HAVE AN ATTACHED COMPUTE                
         LTR   R4,R4               AND ALSO A PREVIOUS 'IN'                     
         BZ    COMP30X                                                          
         USING DRIND,R4                                                         
         MVI   COMPSTAT,2                                                       
         MVC   DMCB+4(4),DROACOMP                                               
         GOTO1 =V(DRIVCOMP),DMCB,(RA),,(R5)                                     
         LH    R2,DRINDISP         POSITION TO INPUT FIELD                      
         AR    R2,R5                                                            
         CLI   DRINTYPE,C'P'       SELECT TYPE                                  
         BE    COMPPACK                                                         
         CLI   DRINTYPE,C'B'                                                    
         BE    COMPBIN                                                          
         B     COMP30X                                                          
         SPACE 1                                                                
COMPPACK ZIC   R1,DRINLEN          PACKED                                       
         BCTR  R1,0                                                             
         SLL   R1,4                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         ZAP   0(0,R2),DUB                                                      
         B     COMP30X                                                          
         SPACE 1                                                                
COMPBIN  CVB   R1,DUB                                                           
         CLI   DRINFLEN,2                                                       
         BL    COMPBIN1                                                         
         BE    COMPBIN2                                                         
         CLI   DRINFLEN,4                                                       
         BL    COMPBIN3                                                         
         BE    COMPBIN4                                                         
         SPACE 1                                                                
COMPBIN1 STC   R1,0(R2)                                                         
         B     COMP30X                                                          
         SPACE 1                                                                
COMPBIN2 STH   R1,0(R2)                                                         
         B     COMP30X                                                          
         SPACE 1                                                                
COMPBIN3 ST    R1,DUB                                                           
         MVC   0(3,R2),DUB+1                                                    
         B     COMP30X                                                          
         SPACE 1                                                                
COMPBIN4 ST    R1,0(R2)                                                         
         SPACE 1                                                                
COMP30X  SR    R4,R4                                                            
         SPACE 1                                                                
COMPNEXT ZIC   R1,1(R3)                                                         
         AR    R3,R1                                                            
         CLI   0(R3),X'20'                                                      
         BE    COMP20                                                           
         CLI   0(R3),X'30'                                                      
         BE    COMP30                                                           
         CLI   0(R3),X'10'                                                      
         BE    XIT                                                              
         CLI   0(R3),0                                                          
         BNE   COMPNEXT                                                         
         B     XIT                                                              
         DROP  R4                                                               
         DROP  R3                                                               
         SPACE 1                                                                
COMPSTAT DC    X'00'               CHANGED TO 1 IF NO COMPUTES                  
*                                  CHANGED TO 2 IF COMPUTES OK                  
         EJECT                                                                  
*              SET CONTROL BREAK                                                
         SPACE 3                                                                
*                                  R5=A(RECORD COMING FROM SORT)                
*                                  PREVIOUS DETAIL IS IN GLAIO                  
         SPACE 1                                                                
SETCB    NTR1                                                                   
         L     R6,GLAIO                                                         
         BAS   RE,SETGLINT                                                      
         USING GLINTD,R2                                                        
         CLI   GLRECLEV,0          UNLESS RECORD IS IMBEDDED                    
         BNE   SCB2                                                             
         MVI   CBLEV,0                                                          
         CLC   0(1,R5),0(R6)       TEST CHANGE OF RECORD                        
         BNE   XIT                                                              
         SPACE 1                                                                
SCB2     LA    R3,GLLCBS           NOW SET UP TO TEST OTHER LEVELS              
         LA    R4,1                R4=LEVEL NUMBER                              
         AH    R5,GLCONLEN                                                      
         AH    R6,GLCONLEN                                                      
         SPACE 1                                                                
SCB4     STC   R4,CBLEV                                                         
         ZIC   R1,0(R3)            PICK UP CUMULATIVE LENGTH                    
         LTR   R1,R1                                                            
         BNZ   SCB6                                                             
         BCTR  R4,0                                                             
         STC   R4,CBLEV                                                         
         B     XIT                                                              
         SPACE 1                                                                
SCB6     BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R5),0(R6)       LOOK FOR A CHANGE AT THIS LEVEL              
         BNE   XIT                 FOUND                                        
         LA    R3,1(R3)            GO AND TRY NEXT LOWER LEVEL                  
         LA    R4,1(R4)                                                         
         B     SCB4                                                             
         EJECT                                                                  
*              CONTROL MULTIPLE FIRST TIME ROUTINES                             
         SPACE 3                                                                
MULTFT   NTR1                                                                   
*                                  CBLEV IS SET TO BREAK LEVEL                  
         L     R2,GLATHID                                                       
         USING GLINTD,R2                                                        
         MVC   GLLEVEL,CBLEV       START FROM LOWEST UP TO DETAIL               
         SPACE 1                                                                
MULTFT2  CLC   GLLEVEL,GLDETLEV    DON'T NEED FT AT DETAIL LEVEL                
         BE    XIT                                                              
         BAS   RE,SINGFT           HANDLE FT ROUTINES AT THIS LEVEL             
         AI    GLLEVEL,1                                                        
         B     MULTFT2                                                          
         EJECT                                                                  
*              CONTROL MULTIPLE LAST TIME ROUTINES                              
         SPACE 3                                                                
MULTLT   NTR1                                                                   
*                                  CBLEV IS SET TO BREAK LEVEL                  
         L     R5,GLAIO            NEED GLINTD FOR PREVIOUS                     
         BAS   RE,SETGLINT                                                      
         USING GLINTD,R2                                                        
         CLC   CBLEV,GLDETLEV      DON'T NEED LT ON DETAIL                      
         BE    XIT                                                              
         ZIC   R1,GLDETLEV         SO PROCESS FROM DETAIL-1 LEVEL               
         BCTR  R1,0                TO CBLEV SETTING BACKWARDS                   
         SPACE 1                                                                
MULTLT2  STC   R1,GLLEVEL                                                       
         BAS   RE,SINGLT           HANDLE LT ROUTINES AT THIS LEVEL             
         CLC   GLLEVEL,CBLEV                                                    
         BE    XIT                                                              
         BCTR  R1,0                                                             
         B     MULTLT2                                                          
         EJECT                                                                  
*              CONTROL FIRST, LAST AND TOTAL ROUTINES                           
         SPACE 3                                                                
SINGFT   NTR1                                                                   
         MVI   BREAK,C'F'                                                       
         MVI   GLHOOK,GLFIRST                                                   
         B     SING2                                                            
         SPACE 1                                                                
SINGLT   NTR1                                                                   
         MVI   BREAK,C'L'                                                       
         MVI   GLHOOK,GLLAST                                                    
         CLC   GLLEVEL,GLRNKLEV    MAY NEED TO RELEASE RANK RECORDS             
         BNE   SING2                                                            
         CLI   GLLEVEL,0                                                        
         BE    SINGL0                                                           
         BAS   RE,RANKOUT                                                       
         SPACE 1                                                                
SING2    XC    GLARGS,GLARGS                                                    
         MVC   GLARGS(1),GLLEVEL                                                
         BAS   RE,SETAOUT          PICK UP A(THIS OUT) IF ANY                   
         MVC   GLADTENT,ALEVOUT                                                 
         XC    GLAROUT,GLAROUT                                                  
         MVI   GLAROUT,1                                                        
         BAS   RE,GOHOOK           HOOK TO APPLICATION FOR FIRST/LAST           
         OC    ALEVOUT,ALEVOUT                                                  
         BZ    XIT                 (NONE AT THIS LEVEL)                         
         USING DROD,R3                                                          
         L     R3,ALEVOUT                                                       
         CLI   DROFLFOL,0          ANY FIRST/LAST SPECIFIED?                    
         BE    SINGTOT                                                          
         MVI   ELCODE,X'44'        YES SO GO AND LOOK FOR THEM                  
         ZIC   R0,DROFLFOL                                                      
         BAS   RE,EXFLT                                                         
         SPACE 1                                                                
SINGTOT  CLI   BREAK,C'L'          IF LAST TIME ROUTINE                         
         BNE   XIT                                                              
         CLI   DROTLFOL,0          ARE THERE ANY TOTALS SPECIFIED?              
         BE    XIT                                                              
         MVI   ELCODE,X'48'                                                     
         ZIC   R0,DROTLFOL                                                      
         BAS   RE,EXFLT            THEN EXECUTE ANY CONTROLS                    
         B     XIT                                                              
         SPACE 1                                                                
SINGL0   L     R2,GLATHID          SPECIAL ROUTINE FOR LEVEL 0                  
         USING GLINTD,R2                                                        
         L     R3,GLARECEL         PICK UP ADDRESS OF RECORD ELEMENT            
         USING DRRECD,R3                                                        
         CLI   DRRECTOT,0          AND SEE IF THERE ARE ANY TOTALS              
         BE    XIT                                                              
         MVI   ELCODE,X'48'                                                     
         ZIC   R0,DRRECTOT                                                      
         BAS   RE,EXFLT                                                         
         B     XIT                                                              
         EJECT                                                                  
*              EXECUTE FIRST, LAST OR TOTAL ELEMENT                             
         SPACE 3                                                                
EXFLT    NTR1                                                                   
         MVI   AUTALLSW,C'Y'                                                    
*                                  R0=NUMBER OF ELEMENTS TO COME                
*                                  ELCODE = X'44' OR X'48'                      
*                                  BREAK = C'F' OR 'L'                          
*                                  R3=A(OUT) OR A(RECORD) FOR LEVEL 0           
         L     R2,GLATHID                                                       
         USING GLINTD,R2                                                        
         XC    GLAIFLD,GLAIFLD                                                  
         XC    GLATOUT,GLATOUT                                                  
         CLI   GLLEVEL,0                                                        
         BE    EXFLT2                                                           
         USING DROD,R3                                                          
         ST    R3,GLATOUT          SAVE A(OUT FOR TOTAL ROUTINES)               
         L     R1,DROIADD          PICK UP A(IN) IF ANY                         
         LTR   R1,R1                                                            
         BZ    EXFLT2                                                           
         USING DRIND,R1                                                         
         LH    R1,DRINDISP                                                      
         DROP  R1                                                               
         A     R1,AACTREC                                                       
         ST    R1,GLAIFLD          RECORD A(INPUT FIELD IN RECORD)              
         SPACE 1                                                                
EXFLT2   ZIC   R1,1(R3)                                                         
         LTR   R1,R1                                                            
         BZ    XIT                                                              
         AR    R3,R1                                                            
         CLC   0(1,R3),ELCODE      LOOK FOR MATCH ON ELEMENT CODE               
         BNE   EXFLT2                                                           
         USING DRFLD,R3                                                         
         CLI   ELCODE,X'48'                                                     
         BE    EXFLT4                                                           
         CLC   DRFLTYPE,BREAK      AND FIRST/LAST                               
         BNE   EXFLTEND                                                         
         SPACE 1                                                                
EXFLT4   DS    0H                  CHECK ANY CONDITIONALS                       
         GOTO1 =V(DRIVTEST),DMCB,(RA),DRFLIFS                                   
         BNE   EXFLTEND                                                         
         SPACE 1                                                                
         CLI   ELCODE,X'48'        ARE WE HANDLING TOTALS?                      
         BNE   EXFLT5                                                           
         CLI   DRFLDET,0           IS THIS DETAILED TOTALS ?                    
         BE    *+12                                                             
         TM    GLTOTIND,GLTITOT    YES - ONLY GET REGULAR TOTALS IF             
         BZ    EXFLTEND                  TOTALS AT LEVEL ABOVE DETAIL           
         L     RF,GLAIFLD          PROTECT GLAIFLD                              
         BAS   RE,TOTALS           YES - SO LOOK FOR NUMBERS                    
         BAS   RE,CLEARCUM                                                      
         ST    RF,GLAIFLD                                                       
         CLI   TOTPEND,C'Y'                                                     
         BE    EXFLT5                                                           
         TM    GLNORBOX,X'40'      IF TOTALS SUPPRESSED                         
         BNO   EXFLT9                                                           
         MVI   HORIZSW,C'Y'           DO A RULE NEXT TIME                       
         B     EXFLT9                                                           
         SPACE 1                                                                
EXFLT5   OC    DRFLRADD,DRFLRADD   ANY ROUTINE TO HANDLE?                       
         BZ    EXFLT6                                                           
         MVC   GLAROUT,DRFLRADD                                                 
         MVC   GLLABEL,DRFLROUT                                                 
         MVC   GLARGS,DRFLARGS                                                  
*                                  (GLAIFLD SET ABOVE)                          
         MVC   GLAOFLD,DRFLAPOS                                                 
         ST    R3,GLADTENT                                                      
         MVI   GLHOOK,GLROUT                                                    
         OI    GLINDS,X'40'        (X'40' INDICATES HOOK FROM TOTAL)            
         BAS   RE,GOHOOK                                                        
         NI    GLINDS,X'BF'                                                     
         CLI   GLHOOK,GLEDIT       'ALL' CAN STILL BE PUT IN                    
         BE    EXFLT6              AUTOMATICALLY                                
         MVI   AUTALLSW,C'N'                                                    
         SPACE 1                                                                
EXFLT6   CLI   DRFLELEN,60         ANY LITERALS FOR US TO HANDLE                
         BE    EXFLT8                                                           
         ZIC   R1,DRFLELEN         YES                                          
         SH    R1,=H'60'                                                        
         STC   R1,LITLEN           SET LENGTH                                   
         LA    R1,DRFLLIT                                                       
         ST    R1,ALITIN           ADDRESS                                      
         MVC   ALITOUT,DRFLAPOS    AND WHERE IT SHOULD GO                       
         BAS   RE,DOLIT                                                         
         MVI   AUTALLSW,C'N'                                                    
         SPACE 1                                                                
EXFLT8   CLI   LINETYPE,C'T'       ANY TOTALS WAITING FOR PRINT?                
         BNE   EXFLT9              LINETYPE SET IN TOTALS ROUTINE               
         BAS   RE,AUTOALL          CAN PUT 'ALL' AUTOMATICALLY                  
         BAS   RE,ROBOXOFF         MAY TURN OFF ROW BOXES                       
         MVC   NLINES,GLSPACE                                                   
******** OC    DRFLRADD,DRFLRADD   IF THERE WAS NOT A ROUTINES                  
******** BNZ   *+8                                                              
******** BAS   RE,SUPPRESS         GO AND CHECK FOR SUPPRESSION                 
         BAS   RE,SPLAT                                                         
         BAS   RE,ROBOXON          (MAY TURN ROW BOXES BACK ON)                 
         MVI   LINETYPE,0                                                       
         SPACE 1                                                                
EXFLT9   CLI   DRFLSPAC,0          ANY PRINTING TO DO                           
         BE    EXFLT10                                                          
         CLI   BREAK,C'L'          ONLY LAST GETS HANDLED HERE                  
         BNE   EXFLT10                                                          
         TM    GLINDS,GLISDONT     TEST SUPPRESS SPACING FOR REJECTED           
         BZ    *+12                                          PRNT LINES         
         CLI   DONTSW,C'Y'         YES-TEST PRINT LINE REJECTED                 
         BE    EXFLT10                 YES-DO NOT PRINT BLANK LINE(S)           
         MVC   NLINES,DRFLSPAC     SET NUMBER OF LINES                          
         BAS   RE,SPLAT            AND DO IT                                    
         SPACE 1                                                                
EXFLT10  TM    DRFLOPT,X'80'       OPTION TO SKIP NEXT TIME                     
         BNO   EXFLT11                                                          
         MVC   LASTPDET,SPACES     STOP 'CONTINUED' FEATURE                     
         MVC   LASTP2,SPACES                                                    
         MVC   SAVEP3,SPACES                                       L01          
         MVC   SAVEP4,SPACES                                       L01          
         L     R1,GLAFHEAD         YES SO SET FORCEHED                          
         LTR   R1,R1                                                            
         BZ    EXFLT12                                                          
         MVI   0(R1),C'Y'                                                       
         XC    LASTMID,LASTMID                                                  
         SPACE 1                                                                
EXFLT11  TM    DRFLOPT,X'10'       MIDHEAD OPTION                               
         BNO   EXFLT12                                                          
         MVC   LASTPDET,SPACES     STOP 'CONTINUED' FEATURE                     
         MVC   LASTP2,SPACES                                                    
         MVC   LASTP3,SPACES                                       L01          
         MVC   LASTP4,SPACES                                       L01          
         L     R1,GLAFHEAD         YES SO SET FORCEHED TO M                     
         LTR   R1,R1                                                            
         BZ    EXFLT12                                                          
         MVI   0(R1),C'M'                                                       
         XC    LASTMID,LASTMID                                                  
         SPACE 1                                                                
EXFLT12  TM    DRFLOPT,X'40'       OPTION TO RESET PAGE NUMBER                  
         BNO   EXFLT14                                                          
         L     R1,GLAPAGE          YES                                          
         LTR   R1,R1                                                            
         BZ    EXFLT14                                                          
         MVC   0(2,R1),=H'1'                                                    
         SPACE 1                                                                
EXFLT14  TM    DRFLOPT,X'20'       OPTION TO RESET SUBPAGE                      
         BNO   EXFLT16                                                          
         L     R1,GLASPAGE         YES                                          
         LTR   R1,R1                                                            
         BZ    EXFLT16                                                          
         MVC   0(2,R1),=H'1'                                                    
         SPACE 1                                                                
EXFLT16  CLI   DRFLSPAC,0          ANY PRINTING TO DO                           
         BE    EXFLT18                                                          
         CLI   BREAK,C'L'          DEALING WITH LAST HERE                       
         BE    EXFLT18                                                          
         TM    GLINDS,GLISDONT     TEST SUPPRESS SPACING FOR REJECTED           
         BZ    *+12                                          PRNT LINES         
         CLI   DONTSW,C'Y'         YES-TEST PRINT LINE REJECTED                 
         BE    EXFLT18                 YES-DO NOT PRINT BLANK LINE(S)           
         MVC   NLINES,DRFLSPAC     SET NUMBER OF LINES                          
         BAS   RE,SPLAT            AND DO IT                                    
         SPACE 1                                                                
EXFLT18  DS    0H                                                               
         SPACE 1                                                                
EXFLTEND BCT   R0,EXFLT2                                                        
         B     XIT                                                              
         EJECT                                                                  
*              SUPPORT ROW BOXES OF AND ON FOR TOTALS                           
         SPACE 3                                                                
ROBOXOFF NTR1                                                                   
         TM    GLNORBOX,X'40'      OPTION TO REMOVE ROW BOXES                   
         BNO   XIT                   WHEN WE ARE DOING TOTALS                   
         TM    GLDOWNLD,GLDLACTV                                                
         BO    XIT                 NOT INTERESTED IF DOWNLOADING                
         L     R1,GLALINE                                                       
         ZIC   RE,0(R1)                                                         
         L     R1,GLALMAX                                                       
         ZIC   RF,0(R1)                                                         
         L     R2,AMYP1                                                         
*                                  ADD ACTIVE LINES INTO RE                     
         LA    R0,20                                                            
         SPACE 1                                                                
RBOFF1   CLC   0(198,R2),SPACES                                                 
         BE    *+8                                                              
         LA    RE,1(RE)                                                         
         LA    R2,198(R2)                                                       
         BCT   R0,RBOFF1                                                        
         SPACE 1                                                                
         LA    RE,1(RE)            MAKE SURE THERE IS ENOUGH ROOM               
         CR    RE,RF                  FOR TOTALS TO PRINT                       
         BL    RBOFF2                                                           
         L     R1,GLAFHEAD                                                      
         MVI   0(R1),C'Y'          NO - SO FORCE HEADLINES                      
         L     RF,REPORT                                                        
         L     R4,AREPWRKD         POINT TO REPORT WORKING STORAGE              
         ST    R4,DMCB                                                          
         LA    R1,DMCB                                                          
         CLI   AREPWRKD,C'R'       SKIP IF NOT FAREPORT                         
         BNE   *+6                                                              
         LR    R1,R4                                                            
         GOTO1 (RF),(R1)                                                        
         SPACE 1                                                                
RBOFF2   L     R6,ABOX                                                          
         USING BOXD,R6                                                          
         MVC   SAVEBXCL,BOXCOLS    SAVE PRESENT BOX COLUMNS                     
         LA    R1,BOXCOLS                                                       
         L     R0,AFCBOX           ADDRESS OF FIRST COLUMN BOX                  
         SR    R0,R1                                                            
         BNP   XIT                                                              
         SPACE 1                                                                
RBOFF4   CLI   0(R1),C'C'          LOOK FOR 'CENTERS'                           
         BNE   *+8                                                              
         MVI   0(R1),C' '          AND REMOVE                                   
         LA    R1,1(R1)                                                         
         BCT   R0,RBOFF4                                                        
         MVC   SAVENRBX,BOXCOLS    SAVE REDUCED BOXES                           
         BAS   RE,PRHORIZ                                                       
         B     XIT                                                              
         SPACE 1                                                                
ROBOXON  NTR1                                                                   
         TM    GLNORBOX,X'40'                                                   
         BNO   XIT                                                              
         L     R6,ABOX                                                          
         CLC   SAVEBXCL,SPACES                                                  
         BE    *+10                                                             
         MVC   BOXCOLS(198),SAVEBXCL    RESTORE BOX COLUMNS                     
         MVC   SAVEBXCL,SPACES                                                  
         MVI   HORIZSW,C'Y'                                                     
         B     XIT                                                              
         SPACE 1                                                                
PRHORIZ  NTR1                                                                   
         L     R6,ABOX                                                          
         L     RF,REPORT           PRINT A HORIZONTAL LINE                      
         MVI   BOXREQ,C'B'                                                      
         L     R4,AREPWRKD         POINT TO REPORT WORKING STORAGE              
         ST    R4,DMCB                                                          
         LA    R1,DMCB                                                          
         CLI   AREPWRKD,C'R'       SKIP IF NOT FAREPORT                         
         BNE   *+6                                                              
         LR    R1,R4                                                            
         GOTO1 (RF),(R1)                                                        
         MVI   HORIZSW,C'N'                                                     
         B     XIT                                                              
         DROP  R6                                                               
         EJECT                                                                  
*              ROUTINE TO PUT IN 'ALL' AUTOMATICALLY                            
         SPACE 3                                                                
AUTOALL  NTR1                                                                   
         CLI   AUTALLSW,C'Y'       NOT IF USER HAD LITERAL OR ROUTINE           
         BNE   XIT                                                              
         SPACE 1                                                                
AUTALL2  ZIC   R1,1(R3)            LOOK FOR NEXT OUT ELEMENT                    
         AR    R3,R1                                                            
         CLI   0(R3),0                                                          
         BE    XIT                                                              
         CLI   0(R3),X'30'                                                      
         BNE   AUTALL2                                                          
         USING DROD,R3                                                          
         GOTO1 =V(DRIVTEST),DMCB,(RA),DROIFS                                    
         BNE   AUTALL2                                                          
         CLI   DROLTYP,C'P'                                                     
         BNE   AUTALL2                                                          
         CLI   DROLEN,3                                                         
         BL    XIT                 NOT ROOM FOR ANYTHING                        
         L     R4,DROAPOS                                                       
         LTR   R4,R4                                                            
         BZ    AUTALL2                                                          
         MVC   0(3,R4),=C'ALL'     ROOM FOR 'ALL'                               
         CLI   DROLEN,5                                                         
         BL    *+10                                                             
         MVC   0(5,R4),=C'*ALL*'   ROOM FOR '*ALL*'                             
         B     XIT                                                              
         EJECT                                                                  
*              SET ADDRESS OF OUT AT SPECIFIED LEVEL                            
         SPACE 3                                                                
SETAOUT  NTR1                                                                   
         L     R2,GLATHID                                                       
         USING GLINTD,R2                                                        
         L     R3,GLAFOUT                                                       
         USING DROD,R3                                                          
         SPACE 1                                                                
SAO2     CLI   0(R3),0             NO GOOD IF 0 OR X'10'                        
         BE    SAOMISS                                                          
         CLI   0(R3),X'10'                                                      
         BE    SAOMISS                                                          
         CLI   0(R3),X'30'         WE NEED AN X'30' AT REQUIRED LEVEL           
         BNE   SAONEXT                                                          
         CLC   DROLEV,GLLEVEL                                                   
         BNE   SAONEXT                                                          
         ST    R3,ALEVOUT          FOUND                                        
         B     YES                                                              
         SPACE 1                                                                
SAONEXT  ZIC   R1,1(R3)                                                         
         AR    R3,R1                                                            
         B     SAO2                                                             
         SPACE 1                                                                
SAOMISS  XC    ALEVOUT,ALEVOUT                                                  
         B     NO                                                               
         EJECT                                                                  
*              OVERALL CONTROL OF DETAIL AND TOTALS                             
         SPACE 3                                                                
DETAIL   NTR1                                                                   
         MVI   LINETYPE,C'D'                                                    
         NI    GLINDS,(X'FF'-GLTOTLIN)                                          
         TM    GLDOWNLD,GLDLACTV                                                
         BNO   DET2                                                             
         BAS   RE,DOWN                                                          
         B     XIT                                                              
         SPACE 1                                                                
TOTALS   NTR1                                                                   
         BAS   RE,CLEARCUM                                                      
         MVI   LINETYPE,C'T'                                                    
         OI    GLINDS,GLTOTLIN                                                  
         TM    GLDOWNLD,GLDLACTV                                                
         BO    XIT                                                              
         SPACE 1                                                                
DET2     NI    GLINDS,(X'FF'-GLDETFTS)   RESET FIRST TIME SWITCH                
         L     R2,GLATHID                                                       
         USING GLINTD,R2                                                        
         L     R3,GLAFOUT          PICK UP ADDRESS OF FIRST OUT                 
         USING DROD,R3                                                          
         ZIC   R1,GLLEVEL                                                       
         CLI   LINETYPE,C'D'                                                    
         BNE   *+8                                                              
         IC    R1,GLDETLEV                                                      
         ZIC   R0,GLMIDLEV                                                      
         CR    R1,R0               IF THIS LEVEL IS BELOW MID LEVEL             
         BH    DET3                                                             
         L     RF,AMYM1            CLEAR MID-LINES                              
         MVC   0(198,RF),SPACES                                                 
         MVC   198(198,RF),SPACES                                               
         SPACE 1                                                                
DET3     LR    RF,R1                                                            
         SLL   RF,2                                                             
         LA    RF,GLARECL0(RF)     RF=A(ADDRESS OF RECORD AT THIS LEV)          
         BAS   RE,TOTCON           CHECK FOR UNNECESSARY TOTALS                 
         BE    *+8                                                              
         NI    0(RF),X'7F'         YES-SUPPRESS TOTAL                           
         TM    0(RF),X'80'         TEST RECORD EXISTS AT THIS LEVEL             
         BO    *+16                                                             
         MVI   TOTPEND,C'N'        NO-SUPPRESS TOTAL                            
         MVI   LINETYPE,0                                                       
         B     XIT                                                              
         NI    0(RF),X'7F'         YES-MAKE SURE IT DOESN'T IN FUTURE           
         L     RF,0(RF)            A(RECORD AT THIS LEVEL)                      
         ST    RF,ATHISREC                                                      
         SPACE 1                                                                
*                                  CHECK THIS OUT FOR CONDITIONALS              
DET4     GOTO1 =V(DRIVTEST),DMCB,(RA),DROIFS                                    
         BNE   DETEND                                                           
         CLI   GLDETHED,C'Y'       UNLESS USER ASKED SPECIALLY                  
         BE    DET5                                                             
         CLI   DROLTYP,C'H'        DON'T BOTHER WITH HEADLINES HERE             
         BE    DETEND                                                           
         SPACE 1                                                                
DET5     ST    R3,GLADTENT                                                      
         L     R5,DROAPOS          R5=A(OUTPUT)                                 
         CLI   DROELEN,140         DO WE NEED TO HANDLE A LITERAL               
         BE    DET6                                                             
         ZIC   R1,DROELEN          YES                                          
         SH    R1,=H'140'                                                       
         STC   R1,LITLEN           PASS ITS LENGTH                              
         LA    R1,DROLIT                                                        
         ST    R1,ALITIN           ITS ADDRESS                                  
         ST    R5,ALITOUT          AND WHERE IT GOES                            
         BAS   RE,DOLIT                                                         
         L     R5,ALITOUT          R5=A(NEXT SPACE)                             
         LA    R5,1(R5)            LEAVE A BLANK                                
         SPACE 1                                                                
DET6     ST    R5,GLAOFLD                                                       
         XC    GLAIFLD,GLAIFLD                                                  
         L     R1,DROIADD                                                       
         LTR   R1,R1                                                            
         BZ    DET8                                                             
         USING DRIND,R1                                                         
         ZIC   RE,DRINLEN                                                       
         LH    RF,DRINDISP                                                      
         A     RF,ATHISREC                                                      
         ST    RF,GLAIFLD                                                       
         CLI   DRINTYPE+1,C'+'     SKIP SIGNIFICANCE TESTS                      
         BE    DET8                FOR ADDITIVE FIELDS                          
         DROP  R1                                                               
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         OC    0(0,RF),0(RF)       ANY SIGNIFICANT DATA?                        
         BZ    DETEND              NO SO NO NEED TO PROCESS                     
*                                                                               
         TM    GLTOTIND,GLTIDET    TEST ANY DETAILED TOTALS                     
         BZ    DET8                NO                                           
         CLC   DROLEV,GLDETLEV     X'FF' ALLOWED FOR DETAIL LEVEL               
         BNL   DET8                                                             
         ZIC   R4,DROLEV                                                        
         LA    R4,DETTOTS-1(R4)                                                 
         CLI   0(RF),X'FF'         TEST FOR X'FF'                               
         BNE   DET6A                                                            
         BCTR  RE,0                                                             
         LTR   RE,RE                                                            
         BM    DET6B                                                            
         EX    RE,*+8                                                           
         B     *+10                                                             
         CLC   1(0,RF),0(RF)                                                    
         BE    DET6B                                                            
         SPACE 1                                                                
DET6A    MVI   0(R4),0             INDICATE NOT DETAIL TOTAL                    
         B     DET8                                                             
         SPACE 1                                                                
DET6B    TM    GLINDS2,GLPBREAK    YES-IF PAGE BREAK REQUIRED,                  
         BZ    DET6C                                                            
         OC    DETTOTS,DETTOTS     THEN FORCE PAGE BREAK IF LAST                
         BNZ   DET6C               TIME WAS NOT A DETAIL TOTAL                  
         L     RE,GLAFHEAD                                                      
         MVI   0(RE),C'Y'                                                       
         SPACE 1                                                                
DET6C    MVI   0(R4),X'FF'         INDICATE DETAIL TOT AT THIS LEVEL            
         LTR   R5,R5               TEST PRINT=NO                                
         BZ    DETEND                                                           
         ZIC   RE,DROLEN                                                        
         BCTR  RE,0                                                             
         EX    RE,*+8              CLEAR THE OUTPUT FIELD                       
         B     *+10                                                             
         MVC   0(0,R5),SPACES                                                   
         CLI   DROLTYP,C'M'        IS IT A MIDLINE                              
         BNE   DET7                                                             
         EX    RE,*+8              YES - CLEAR THE UNDERLINE                    
         B     DET7                                                             
         MVC   198(0,R5),SPACES                                                 
*                                                                               
DET7     CLI   DROLEN,3            PUT 'ALL' IN OUTPUT FIELD                    
         BL    DETEND                                                           
         MVC   0(3,R5),=C'ALL'                                                  
         CLI   DROLEN,5                                                         
         BL    DETEND                                                           
         MVC   0(5,R5),=C'*ALL*'                                                
         B     DET12                                                            
         DROP  R2                                                               
         SPACE 1                                                                
DET8     OC    DRORADD,DRORADD     IS THERE A USER ROUTINE FOR THIS             
         BZ    DET10                                                            
         MVI   GLHOOK,GLROUT       YES SO SET UP FOR A HOOK                     
         MVC   GLLABEL,DROROUT                                                  
         MVC   GLAROUT,DRORADD                                                  
         MVC   GLARGS,DROARGS                                                   
         OC    DROACOMP,DROACOMP   DO WE NEED TO COMPUTE THIS FIELD             
         BZ    DET9                                                             
         MVC   DMCB+4(4),DROACOMP                                               
         MVC   DMCB+8(4),ATHISREC                                               
         GOTO1 =V(DRIVCOMP),DMCB,(RA)                                           
         LA    R1,DUB              NOW HAVE RESULT IN DUB                       
         ST    R1,GLAIFLD          OFFER THE ADDRESS OF THIS TO USER            
         SPACE 1                                                                
DET9     BAS   RE,GOHOOK                                                        
         CLI   GLHOOK,GLEDIT       DID USER WANT US TO EDIT                     
         BE    DET10                                                            
         BAS   RE,ALIGN                                                         
         B     DET12                                                            
         SPACE 1                                                                
DET10    BAS   RE,FORM             OTHERWISE DRIVER WILL DEAL WITH IT           
         SPACE 1                                                                
DET12    CLI   DROLTYP,C'M'        TEST MIDLINE                                 
         BNE   DETEND                                                           
         L     R1,AMYM1            YES-COMPARE TO LAST MIDLINE                  
         CLC   LASTMID,0(R1)                                                    
         BE    DETEND                                                           
         MVC   LASTPDET,SPACES     FORCE NO SUPPRESSION OF LEADING DATA         
         SPACE 1                                                                
DETEND   ZIC   R1,1(R3)                                                         
         AR    R3,R1                                                            
         CLI   0(R3),X'30'                                                      
         BE    DET4                                                             
         CLI   0(R3),0                                                          
         BE    DETEND2                                                          
         CLI   0(R3),X'10'                                                      
         BNE   DETEND                                                           
         SPACE 1                                                                
DETEND2  MVC   NLINES,GLSPACE                                                   
         TM    GLINDS2,X'80'       OPTION NOT TO SUPPRESS                       
         BNO   *+10                                                             
         MVC   LASTPDET,SPACES     FORCE NO SUPPRESSION OF LEADING DATA         
         BAS   RE,SUPPRESS         GO AND CHECK FOR SUPPRESSION                 
         CLI   LINETYPE,C'T'       TOTALS WILL PRINT AFTER WE HAVE              
         BE    XIT                 PROCESSED ANY TOTAL LITERALS ETC             
         BAS   RE,SPLAT                                                         
         MVI   LINETYPE,0                                                       
         B     XIT                                                              
         EJECT                                                                  
*              CHECK FOR UNNECESSARY TOTALS                                     
         SPACE 3                                                                
TOTCON   NTR1                                                                   
*                                  R1=LEVEL NUMBER                              
         MVI   TOTPEND,C'Y'                                                     
         LTR   R1,R1               ALWAYS ALLOW LEVEL 0                         
         BZ    YES                                                              
         USING GLINTD,R2                                                        
         ZIC   R0,GLDETLEV         R0=N'LEVELS TO CHECK                         
         DROP  R2                                                               
         SR    R0,R1                                                            
         SLL   R1,1                                                             
         LA    R1,TOTCNTS(R1)      ADDRESS TO LEVEL COUNTER                     
         LH    RE,0(R1)            AND BUMP BY 1                                
         LA    RE,1(RE)                                                         
         STH   RE,0(R1)                                                         
         CLI   LINETYPE,C'D'       ALWAYS DO DETAILS                            
         BE    YES                                                              
         SPACE 1                                                                
TOTCNT2  LA    R1,2(R1)            FIND COUNT FOR NEXT LOWEST LEVEL             
         OC    0(2,R1),0(R1)                                                    
         BNZ   TOTCNT4                                                          
         BCT   R0,TOTCNT2                                                       
         B     TOTCNT6                                                          
         SPACE 1                                                                
TOTCNT4  LH    RE,0(R1)                                                         
         XC    0(2,R1),0(R1)                                                    
         CH    RE,=H'1'            DON'T HANDLE IF ONE OR LESS                  
         BH    YES                                                              
         SPACE 1                                                                
TOTCNT6  TM    GLINDS,GLPALTOT     OPTION TO GET ALL TOTALS                     
         BO    YES                                                              
         B     NO                                                               
         EJECT                                                                  
*              POSSIBLE SUPPRESSION OF LEADING DETAIL DATA                      
         SPACE 3                                                                
SUPPRESS NTR1                                                                   
         USING GLINTD,R2                                                        
         L     R3,AMYP1                                                         
         MVI   CONTSW,C'N'                                                      
         MVC   SAVEDET,0(R3)                                                    
         MVC   SAVEP2,198(R3)                                                   
         MVC   SAVEP3,198*2(R3)   LINE 3                          L01           
         MVC   SAVEP4,198*3(R3)   LINE 4                          L01           
         LA    R4,GLPDCBS                                                       
         LA    R0,GLNUMLEV                                                      
         SPACE 1                                                                
SUP2     CLI   0(R4),0                                                          
         BE    SUP4                                                             
         ZIC   R1,0(R4)            PICK UP LENGTH OF COMPOSITE DISP.            
         BCTR  R1,0                FOR THIS LEVEL                               
         AH    R1,GLPDISP                                                       
         EX    R1,SUPCOMP          IS DATA THE SAME                             
         BNE   SUPEND                                                           
         EX    R1,SUPCOMP2         IS DATA THE SAME                             
         BNE   SUPEND                                                           
         EX    R1,SUPCOMP3         IS DATA THE SAME                L01          
         BNE   SUPEND                                              L01          
         EX    R1,SUPCOMP4         IS DATA THE SAME                L01          
         BNE   SUPEND                                              L01          
         EX    R1,SUPSPACE         AND NOT SPACES                               
         BE    SUPEND                                                           
         EX    R1,SUPCLEAR         THEN CLEAR ON THIS LINE                      
         EX    R1,NXTCLEAR              AND MAY BE ON NEXT                      
         EX    R1,NXTCLEA3              AND MAY BE ON NEXT         L01          
         EX    R1,NXTCLEA4              AND MAY BE ON NEXT         L01          
         MVI   CONTSW,C'S'         AND SET CONTINUATION SWITCH                  
*                                  S=SUSPENSE UNTIL ACTUAL PRINT                
         SPACE 1                                                                
SUP4     LA    R4,1(R4)                                                         
         BCT   R0,SUP2                                                          
         SPACE 1                                                                
SUPEND   DS    0H                                                               
         B     XIT                                                              
         SPACE 1                                                                
SUPCOMP  CLC   SAVEDET(0),LASTPDET                                              
SUPCOMP2 CLC   SAVEP2(0),LASTP2                                                 
SUPCOMP3 CLC   SAVEP3(0),LASTP3                                    L01          
SUPCOMP4 CLC   SAVEP4(0),LASTP4                                    L01          
SUPSPACE CLC   SAVEDET(0),SPACES                                                
SUPCLEAR MVC   0(0,R3),SPACES                                                   
NXTCLEAR MVC   198(0,R3),SPACES                                                 
NXTCLEA3 MVC   198*2(0,R3),SPACES                                  L01          
NXTCLEA4 MVC   198*3(0,R3),SPACES                                  L01          
         EJECT                                                                  
*              CONTROL DRIVER FORMATTING                                        
         SPACE 3                                                                
*                                  R2=A(ASSOCIATED IN)                          
*                                  R3=A(OUT)                                    
*                                  R4=A(INPUT FIELD)                            
*                                  R5=A(PRINT POSITION)                         
FORM     NTR1                                                                   
         OC    DROACOMP,DROACOMP   DO WE NEED TO COMPUTE THIS FIELD             
         BZ    FORM2                                                            
         OC    DRORADD,DRORADD     IF HAVE USER ROUTINE DONT RECOMPUTE          
         BNZ   FORMNUM2                                                         
         MVC   DMCB+4(4),DROACOMP                                               
         MVC   DMCB+8(4),ATHISREC                                               
         GOTO1 =V(DRIVCOMP),DMCB,(RA)                                           
         LTR   R5,R5               CHECK FOR P=NO                               
         BZ    XIT                                                              
         B     FORMNUM2                                                         
         SPACE 1                                                                
FORM2    LTR   R5,R5               REJECT IF P=NO                               
         BZ    XIT                                                              
         L     R2,DROIADD                                                       
         USING DRIND,R2                                                         
         LTR   R2,R2                                                            
         BZ    XIT                 (NO INPUT FIELD TO HANDLE)                   
         L     R4,GLAIFLD                                                       
         ZIC   R1,DRINLEN                                                       
         BCTR  R1,0                                                             
         BAS   RE,POSTCUM                                                       
         CLI   DRINTYPE,C'C'       PICK INPUT TYPE                              
         BE    CHAR                                                             
         CLI   DRINTYPE,C'D'                                                    
         BE    DATE                                                             
         CLI   DRINTYPE,C'X'                                                    
         BE    HEX                                                              
         B     FORMNUM                                                          
         EJECT                                                                  
*              CUME ROUTINES                                                    
         SPACE 3                                                                
CLEARCUM NTR1                                                                   
         XC    CUMEBIN,CUMEBIN                                                  
         ZAP   CUMEPACK,=P'0'                                                   
         B     XIT                                                              
         SPACE 1                                                                
POSTCUM  NTR1                                                                   
         TM    DROFORM,X'01'       MUST BE SET TO CUME                          
         BNO   XIT                                                              
         CLI   DRINTYPE,C'P'                                                    
         BE    POSTCUMP                                                         
         L     R1,CUMEBIN                                                       
         A     R1,0(R4)                                                         
         ST    R1,CUMEBIN                                                       
         ST    R1,0(R4)                                                         
         B     XIT                                                              
         SPACE 1                                                                
POSTCUMP AP    CUMEPACK,0(8,R4)                                                 
         ZAP   0(8,R4),CUMEPACK                                                 
         B     XIT                                                              
         SPACE 1                                                                
CUMEBIN  DC    F'0'                                                             
CUMEPACK DC    PL8'0'                                                           
         EJECT                                                                  
*              CHARACTER ROUTINES                                               
         SPACE 3                                                                
CHAR     CLI   DROTYPE,C'C'        PICK OUTPUT TYPE                             
         BE    CTOC                                                             
         CLI   DROTYPE,C'X'                                                     
         BE    CTOX                                                             
         B     XIT                                                              
         SPACE 1                                                                
*                                  CHARACTER TO CHARACTER ROUTINES              
CTOC     CLC   DRINLEN,DROLEN      INPUT LENGTH NOT GREATER THAN                
         BNH   ODDOUT              OUTPUT LENGTH                                
         SPACE 1                                                                
*                                  INPUT IS GREATER THAN OUTPUT                 
CTOC2    TM    DROFORM,X'10'       IS CHOPPING ALLOWED                          
         BO    CTOC4                                                            
         ZIC   R1,DROLEN           NO SO USE OUTPUT LENGTH                      
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     XIT                                                              
         MVC   0(0,R5),0(R4)       AND TRUNCATE                                 
         SPACE 1                                                                
CTOC4    ST    R4,DMCB             CHOPPING HERE                                
         MVC   DMCB(1),DRINLEN                                                  
         ST    R5,DMCB+4                                                        
         MVC   DMCB+4(1),DROLEN                                                 
         MVC   DMCB+8(4),=F'20'                                                 
         MVI   DMCB+8,198                                                       
         GOTO1 CHOPPER,DMCB                                                     
         B     XIT                                                              
         SPACE 1                                                                
CTOX     ZIC   R1,DROLEN           CHARACTER TO HEX                             
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         XC    0(0,R5),0(R5)       CLEAR OUTPUT FIRST                           
         ST    R4,DMCB                                                          
         ST    R5,DMCB+4                                                        
         LA    R1,1(R1)                                                         
         SLL   R1,1                                                             
         ZIC   R0,DRINLEN          ENSURE THAT INPUT LENGTH IS NOT              
         CR    R0,R1               MORE THAN TWICE THE OUTPUT LENGTH            
         BL    *+6                                                              
         LR    R0,R1                                                            
         ST    R0,DMCB+8                                                        
         GOTO1 HEXIN,DMCB                                                       
         B     XIT                                                              
         EJECT                                                                  
*              DATE AND HEX ROUTINES                                            
         SPACE 3                                                                
DATE     STM   R4,R5,DMCB                                                       
         MVC   DMCB(1),DRINTYPE+1                                               
         MVC   DMCB+4(1),DROTYPE+1                                              
         GOTO1 DATCON,DMCB                                                      
         B     XIT                                                              
         SPACE 1                                                                
HEX      CLI   DROTYPE,C'C'                                                     
         BE    XTOC                                                             
         CLI   DROTYPE,C'X'                                                     
         BE    XTOX                                                             
         B     XIT                                                              
         SPACE 1                                                                
*                                  HEX TO HEX ROUTINES                          
XTOX     CLC   DRINLEN,DROLEN      CHECK INPUT NOT TOO LONG                     
         BH    ODDOUT                                                           
         ZIC   R1,DROLEN                                                        
         BCTR  R1,0                                                             
         B     ODDOUT                                                           
         SPACE 1                                                                
XTOC     ZIC   R1,DROLEN           HEX TO CHARACTER                             
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R5),SPACES      CLEAR OUTPUT FIRST                           
         ST    R4,DMCB                                                          
         ST    R5,DMCB+4                                                        
         LA    R1,1(R1)                                                         
         SRL   R1,1                                                             
         ZIC   R0,DRINLEN          ENSURE THAT INPUT LENGTH IS NOT              
         CR    R0,R1               MORE THAN HALF THE OUTPUT LENGTH             
         BL    *+6                                                              
         LR    R0,R1                                                            
         ST    R0,DMCB+8                                                        
         GOTO1 HEXOUT,DMCB,,,,=C'TOG'                                           
         B     XIT                                                              
         SPACE 1                                                                
ODDOUT   EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R5),0(R4)                                                    
         BAS   RE,ALIGN                                                         
         B     XIT                                                              
         EJECT                                                                  
*              ALIGNMENT AND UNDERLINE ROUTINES                                 
         SPACE 3                                                                
ALIGN    NTR1                                                                   
         CLI   DROALIGN,C'R'       CHECK SPECIAL ALIGNMENT                      
         BE    RALIGN                                                           
         CLI   DROALIGN,C'C'                                                    
         BE    CALIGN                                                           
         B     ANYUNDER                                                         
         SPACE 1                                                                
RALIGN   L     RF,RIGHT                                                         
         B     ALIGNALL                                                         
         SPACE 1                                                                
CALIGN   L     RF,CENTER                                                        
         SPACE 1                                                                
ALIGNALL ST    R5,DMCB             A(OUTPUT)                                    
         ZIC   R1,DROLEN           L'OUTPUT                                     
         ST    R1,DMCB+4                                                        
         GOTO1 (RF),DMCB           GO TO RIGHT OR CENTER                        
         SPACE 1                                                                
ANYUNDER CLI   DROUNDER,0                                                       
         BE    XIT                                                              
         ST    R5,DMCB                                                          
         MVC   DMCB(1),DROLEN                                                   
         LA    R1,198(R5)                                                       
         ST    R1,DMCB+4                                                        
         MVC   DMCB+4(1),DROUNDER                                               
         GOTO1 UNDERLIN,DMCB                                                    
         B     XIT                                                              
         EJECT                                                                  
*              OUTPUT NUMERIC                                                   
         SPACE 3                                                                
FORMNUM  XC    EBLOCK,EBLOCK       GENERAL NUMERIC                              
         CLI   GLABCMOD,1          DONT PROCESS FOR ABC MID LINES               
         BE    XIT                                                              
         ST    R4,EBAIN            R4=A(INPUT)                                  
         MVC   EBTIN,DRINTYPE      PICK UP INPUT TYPE                           
         MVC   EBLIN,DRINFLEN      AND LENGTH                                   
         B     FORMNUM4                                                         
         SPACE 1                                                                
*                                  NUMERIC VALUE IS PACKED IN DUB               
FORMNUM2 XC    EBLOCK,EBLOCK                                                    
         LA    R1,DUB                                                           
         ST    R1,EBAIN                                                         
         MVI   EBTIN,C'P'                                                       
         MVI   EBLIN,8                                                          
         SPACE 1                                                                
FORMNUM4 ST    R5,EBAOUT           R5=A(OUTPUT)                                 
         MVC   EBLOUT,DROLEN       PASS OTHER OUTPUT VALUES                     
         MVC   EBDECS,DRODEC                                                    
         MVC   EBFILL,DROFILL                                                   
         MVC   EBFLOAT,DROFLOAT                                                 
         MVC   EBROUND,DRODIV                                                   
         MVC   EBOPT,DROEDIT                                                    
         MVC   EBTRIM,DROFORM                                                   
         MVC   EBALIGN,DROALIGN                                                 
         MVI   EBPWIDTH,198                                                     
         MVC   EBSCOUT,DROSCALE                                                 
         MVC   EBTRAIL,DROTRAIL                                                 
         BAS   RE,ADJEDIT                                                       
         GOTO1 EDITOR,DMCB,EBLOCK                                               
         B     XIT                                                              
         EJECT                                                                  
*              LAST MINUTE ADJUST BEFORE EDITOR (FUDGES!)                       
         SPACE 3                                                                
ADJEDIT  NTR1                                                                   
         TM    GLINDS,GLRNDOPT     ROUND OPTION                                 
         BNO   ADJED2                                                           
         TM    GLINDS,GLTOTLIN     ONLY APPLIES TO TOTALS                       
         BNO   ADJED2                                                           
         SPACE 1                                                                
         ZIC   R1,EBLOUT           REDUCE OUTPUT LENGTH                         
         SH    R1,=H'3'            (NO POINT OR DECIMALS)                       
         STC   R1,EBLOUT                                                        
         MVI   EBROUND,2           FORCE ROUND BY 100                           
         MVI   EBDECS,0                  AND NO DECIMAL PLACES                  
         SPACE 1                                                                
ADJED2   B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO HANDLE A LITERAL                                      
         SPACE 3                                                                
DOLIT    NTR1                                                                   
         ZIC   R0,LITLEN           LITLEN=LENGTH OF THIS ONE                    
         L     R2,ALITIN           ALITIN=ITS ADDRESS                           
         L     R3,ALITOUT          ALITOUT=WHERE IT GOES                        
         LTR   R3,R3                                                            
         BZ    XIT                                                              
         SPACE 1                                                                
DOLIT2   CLI   0(R2),C'&&'         CHECK FOR START OF SOFT EXPRESSION           
         BE    DOLIT4                                                           
         MVC   0(1,R3),0(R2)       OTHERWISE MOVE OUT THE DATA                  
         LA    R2,1(R2)                                                         
         LA    R3,1(R3)                                                         
         BCT   R0,DOLIT2                                                        
         ST    R3,ALITOUT          PASS BACK NEXT OUTPUT POSITION               
         B     XIT                                                              
         SPACE 1                                                                
DOLIT4   LR    RF,R0               LOOK FOR END OF SOFT EXPRESSION              
         LA    RE,1(R2)                                                         
         SPACE 1                                                                
DOLIT6   CLI   0(RE),C'&&'                                                      
         BE    DOLIT8                                                           
         LA    RE,1(RE)                                                         
         BCT   RF,DOLIT6                                                        
         B     DOLITEND            NOT FOUND SO TREAT FIRST AS DATA             
         SPACE 1                                                                
DOLIT8   LR    R1,RE               FIGURE OUT L'SOFT TO R1                      
         SR    R1,R2                                                            
         BCT   R1,*+8                                                           
         B     DOLITEND                                                         
         MVC   WORK,SPACES                                                      
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   WORK(0),1(R2)       PICK OFF SOFT                                
         LA    RF,WORK                                                          
         ST    RF,GLAIFLD          PASS ITS ADDRESS                             
         ST    R3,GLAOFLD          PASS ADDRESS OF OUTPUT                       
         LA    R1,1(R1)                                                         
         XC    GLARGS,GLARGS                                                    
         STC   R1,GLARGS           PASS INPUT LENGTH IN ARG(1)                  
         MVI   GLHOOK,GLRESLIT     ASK FOR LITERAL RESOLVE                      
         XC    GLAROUT,GLAROUT                                                  
         MVI   GLAROUT,1                                                        
         BAS   RE,GOHOOK           FIRST FROM APPLICATION                       
         CLI   GLARGS+1,0                                                       
         BNE   DOLIT10                                                          
         MVI   GLAROUT,2           THEN FROM SYSDRIVER                          
         BAS   RE,GOHOOK                                                        
         CLI   GLARGS+1,0                                                       
         BE    DOLITEND            NO HELP SO TREAT BOTH AS DATA                
         SPACE 1                                                                
DOLIT10  LA    R1,2(R1)            SKIP PAST WORD AND DELIMITERS                
         AR    R2,R1                                                            
         SR    R0,R1               REDUCE REMAINING LENGTH                      
         BZ    XIT                 (NONE LEFT)                                  
         ZIC   R1,GLARGS+1         L'RETURNED OUTPUT                            
         AR    R3,R1                                                            
         SPACE 1                                                                
DOLITEND MVC   0(1,R3),0(R2)       MOVE OUT THE REST OF THE DATA                
         LA    R2,1(R2)                                                         
         LA    R3,1(R3)                                                         
         BCT   R0,DOLITEND                                                      
         ST    R3,ALITOUT          PASS BACK NEXT OUTPUT POSITION               
         B     XIT                                                              
         EJECT                                                                  
*              DOWN LOADING CONTROLS                                            
         SPACE 3                                                                
DOWN     NTR1                                                                   
         CLI   DLFIRST,0           IS THIS FIRST FOR REPORT                     
         BNE   DOWN2                                                            
         MVI   DLFIRST,1                                                        
         LA    R2,DLPOOL           CLEAR POOL                                   
         LA    R0,DLMAXLIN                                                      
         SPACE 1                                                                
DOWN1    MVC   0(80,R2),SPACES                                                  
         LA    R2,80(R2)                                                        
         BCT   R0,DOWN1                                                         
         SPACE 1                                                                
*                                  THEN SKIP TO NEW PAGE                        
         GOTO1 PRINT,DMCB,PFILL,=C'BC01'                                        
         LA    R1,DLPOOL                                                        
         ST    R1,DLPADD                                                        
         MVI   DLCOUNT,0                                                        
         MVI   DLLINE,1                                                         
         BAS   RE,DOWNHEAD         DOWNLOAD HEADINGS                            
         SPACE 1                                                                
DOWN2    L     R2,GLATHID                                                       
         USING GLINTD,R2                                                        
         L     R3,GLAFOUT          PICK UP ADDRESS OF FIRST OUT                 
         USING DROD,R3                                                          
         ZIC   R1,GLLEVEL                                                       
         IC    R1,GLDETLEV                                                      
         SLL   R1,2                                                             
         LA    R1,GLARECL0(R1)     A(ADDRESS OF RECORD AT THIS LEVEL)           
         L     R1,0(R1)            A(RECORD AT THIS LEVEL)                      
         ST    R1,ATHISREC                                                      
         DROP  R2                                                               
         LA    R1,DLPOOL                                                        
         ST    R1,DLPADD                                                        
         MVI   DLCOUNT,0                                                        
         MVI   DLLINE,1                                                         
         SPACE 1                                                                
*                                  CHECK THIS OUT FOR CONDITIONALS              
DOWN4    GOTO1 =V(DRIVTEST),DMCB,(RA),DROIFS                                    
         BNE   DOWNEND                                                          
         CLI   DROLTYP,C'H'        DON'T BOTHER WITH HEADLINES HERE             
         BE    DOWNEND                                                          
         ST    R3,GLADTENT                                                      
         L     R5,DROAPOS          R5=A(OUTPUT)                                 
         CLI   DROELEN,140         DO WE NEED TO HANDLE A LITERAL               
         BE    DOWN6                                                            
         ZIC   R1,DROELEN          YES                                          
         SH    R1,=H'140'                                                       
         STC   R1,DLLEN            PASS ITS LENGTH                              
         LA    R1,DROLIT                                                        
         ST    R1,DLIADD           ITS ADDRESS                                  
         BAS   RE,DLALPHA                                                       
         SPACE 1                                                                
DOWN6    XC    GLAIFLD,GLAIFLD                                                  
         L     R1,DROIADD                                                       
         LTR   R1,R1                                                            
         BZ    DOWN8                                                            
         USING DRIND,R1                                                         
         ZIC   RE,DRINLEN                                                       
         LH    RF,DRINDISP                                                      
         A     RF,ATHISREC                                                      
         ST    RF,GLAIFLD                                                       
         DROP  R1                                                               
         SPACE 1                                                                
DOWN8    OC    DRORADD,DRORADD     IS THERE A USER ROUTINE FOR THIS             
         BZ    DOWN10                                                           
         MVI   GLHOOK,GLROUT       YES SO SET UP FOR A HOOK                     
         MVC   GLLABEL,DROROUT                                                  
         MVC   GLAROUT,DRORADD                                                  
         MVC   GLARGS,DROARGS                                                   
*                                                                               
         OC    DROACOMP,DROACOMP   DO WE NEED TO COMPUTE THIS FIELD             
         BZ    DOWN8B                                                           
         MVC   DMCB+4(4),DROACOMP                                               
         MVC   DMCB+8(4),ATHISREC                                               
         GOTO1 =V(DRIVCOMP),DMCB,(RA)                                           
         LA    R1,DUB              NOW HAVE RESULT IN DUB                       
         ST    R1,GLAIFLD          OFFER THE ADDRESS OF THIS TO USER            
         SPACE 1                                                                
DOWN8B   L     R1,AMYP1            GIVE USERS BIG BUFFER FOR OUTPUT             
         LA    R1,8(R1)            (SOMETIMES THEY PUT DATA BEFORE!)            
         ST    R1,GLAOFLD                                                       
         BAS   RE,GOHOOK                                                        
         CLI   GLHOOK,GLEDIT       DRIVOUT WILL DO EDITING SO DON'T             
         BE    DOWN8E              CHECK THE OUTPUT STACK                       
         SH    R1,=H'8'                                                         
         LR    RE,R1                                                            
         LA    RF,8                                                             
         SPACE 1                                                                
DOWN8B2  OC    0(198,RE),SPACES    CLEAN UP THE OUTPUT                          
         LA    RE,198(RE)                                                       
         BCT   RF,DOWN8B2                                                       
         SPACE 1                                                                
         LA    RE,DLOUTPUT                                                      
         LA    RF,4                                                             
         MVC   0(198,RE),SPACES                                                 
         LA    RE,198(RE)                                                       
         BCT   RF,*-10                                                          
         SPACE 1                                                                
         CLC   0(198,R1),SPACES                                                 
         BE    DOWN8E                                                           
         ST    R1,DMCB                                                          
         TM    GLDOWNLD,GLDLCUT                                                 
         BO    DOWN8B4                                                          
         GOTO1 SQUASHER,DMCB,,1584 (UP TO 8 LINES SUPPORTED)                    
         SPACE 1                                                                
DOWN8B4  L     R1,AMYP1            FIND START OF DATA                           
         SPACE 1                                                                
DOWN8D   CLI   0(R1),C' '                                                       
         BNE   *+12                                                             
         LA    R1,1(R1)                                                         
         B     DOWN8D                                                           
         MVC   DLOUTPUT(80),0(R1)  MOVE INTO FIXED AREA                         
         TM    GLDOWNLD,GLDLCUT    BUT, IF CUT OPTION IS ON                     
         BNO   DOWN8D2                                                          
         MVC   DLOUTPUT(8),SPACES                                               
         ZIC   RF,DROLEN                                                        
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   DLOUTPUT(0),0(R1)   ONLY MOVE WIDTH OF 1 LINE                    
         SPACE 1                                                                
DOWN8D2  BAS   RE,STRIP            STRIP OUT NUMERIC FIELDS                     
         SPACE 1                                                                
DOWN8E   L     R1,AMYP1                                                         
         LA    R0,20                                                            
         SPACE 1                                                                
DOWN8F   MVC   0(198,R1),SPACES    CLEAR THE BUFFER                             
         LA    R1,198(R1)                                                       
         BCT   R0,DOWN8F                                                        
         CLI   GLHOOK,GLEDIT                                                    
         BE    DOWN10                                                           
         ZIC   R0,DROLEN                                                        
         TM    GLDOWNLD,GLDLNOTR   NO TRUNCATION OF TRAILING SPACES             
         BO    DOWN9B                                                           
         LA    R1,DLOUTPUT+59      FIGURE OUT LENGTH                            
         LA    R0,60                                                            
         SPACE 1                                                                
DOWN9    CLI   0(R1),C' '                                                       
         BNE   DOWN9B                                                           
         BCTR  R1,0                                                             
         BCT   R0,DOWN9                                                         
         LA    R0,1                (MINIMUM OF 1!)                              
         SPACE 1                                                                
DOWN9B   STC   R0,DLLEN                                                         
         LA    R1,DLOUTPUT                                                      
         ST    R1,DLIADD                                                        
         BAS   RE,DLALPHA          DOWNLOAD USER EXPRESSION                     
         B     DOWNEND                                                          
         SPACE 1                                                                
DOWN10   MVC   DLLEN,DROLEN                                                     
         MVC   DLIADD,GLAIFLD                                                   
         CLI   DROTYPE,C'N'                                                     
         BE    DOWN12                                                           
         CLI   DROTYPE,C'D'                                                     
         BE    DOWN14                                                           
         OC    DLIADD,DLIADD                                                    
         BZ    DOWNEND                                                          
         BAS   RE,DLALPHA          EITHER DOWN LOAD ALPHA                       
         B     DOWNEND                                                          
         SPACE 1                                                                
DOWN12   BAS   RE,DLNUM            OR NUMERIC                                   
         B     DOWNEND                                                          
         SPACE 1                                                                
DOWN14   MVC   DLOUTPUT,SPACES     OR DATES                                     
         L     R1,GLAIFLD                                                       
         ST    R1,DMCB                                                          
         LA    R1,DLOUTPUT                                                      
         ST    R1,DMCB+4                                                        
         ST    R1,DLIADD                                                        
         L     R1,DROIADD                                                       
         USING DRIND,R1                                                         
         MVC   DMCB(1),DRINTYPE+1                                               
         DROP  R1                                                               
         MVC   DMCB+4(1),DROTYPE+1                                              
         GOTO1 DATCON,DMCB                                                      
         BAS   RE,DLALPHA          (TREAT AS ALPHA)                             
         SPACE 1                                                                
DOWNEND  ZIC   R1,1(R3)                                                         
         AR    R3,R1                                                            
         CLI   0(R3),X'30'                                                      
         BE    DOWN4                                                            
         CLI   0(R3),0                                                          
         BE    DOWNEND2                                                         
         CLI   0(R3),X'10'                                                      
         BNE   DOWNEND                                                          
         SPACE 1                                                                
DOWNEND2 MVI   GLAROUT,1           HOOK TO APPLICATION TO SAY                   
         MVI   GLHOOK,GLPRINT      WE'RE ABOUT TO 'PRINT'                       
         BAS   RE,GOHOOK                                                        
         CLI   GLHOOK,GLDONT       APPLIC. CAN THEN REJECT                      
         BE    DOWNEND6                                                         
         CLI   GLHOOK,GLBCKOUT     APPLIC. CAN THEN REJECT                      
         BE    DOWNEND6                                                         
         L     R1,DLPADD                                                        
         MVI   0(R1),C';'          SEMI-COLON IS END OF LINE                    
         LA    R2,DLPOOL                                                        
         LA    R0,DLMAXLIN                                                      
         MVC   P,SPACES                                                         
         SPACE 1                                                                
DOWNEND4 MVC   P(80),0(R2)         NOW OUTPUT THE LINES                         
         OC    P(80),SPACES                                                     
         CLC   P(80),SPACES                                                     
         BE    DOWNEND6                                                         
         GOTO1 PRINT,DMCB,PFILL,=C'BL01'                                        
         LA    R2,80(R2)                                                        
         BCT   R0,DOWNEND4                                                      
         SPACE 1                                                                
DOWNEND6 LA    R2,DLPOOL           CLEAR POOL                                   
         LA    R0,DLMAXLIN                                                      
         SPACE 1                                                                
DOWNEND8 MVC   0(80,R2),SPACES                                                  
         LA    R2,80(R2)                                                        
         BCT   R0,DOWNEND8                                                      
         MVC   P,SPACES                                                         
         LA    R1,DLPOOL                                                        
         ST    R1,DLPADD                                                        
         MVI   DLCOUNT,0                                                        
         MVI   LINETYPE,0                                                       
         MVI   DLLINE,1                                                         
         B     XIT                                                              
         EJECT                                                                  
*              DOWN LOADING HEADINGS                                            
         SPACE 3                                                                
DOWNHEAD NTR1                                                                   
         TM    GLDOWNLD,GLDLNOHD   OPTION NOT TO DOWNLOAD HEADINGS              
         BO    XIT                                                              
*        LA    R3,DOUT2                                            L01          
         L     R3,ADOUT2                                           L01          
         USING DOUT2,R3                                                         
         LA    R4,DLHDPOOL         R4=A(HEADLINE POOL)                          
         LA    R6,DLHDPOLX         R6=A(END OF HEADLINE POOL)                   
         DROP  R3                                                               
         ZIC   R0,GLLHEADL         R0=NUMBER OF HEAD LINES                      
         ZIC   R1,GLFHEADL                                                      
         SR    R0,R1                                                            
         AH    R0,=H'1'                                                         
         LA    R1,1                                                             
         SPACE 1                                                                
DH2      STC   R1,DHLINE           DEALING WITH LINES 1-4                       
         BAS   RE,DH4                                                           
         LA    R1,1(R1)                                                         
         BCT   R0,DH2                                                           
         B     XIT                                                              
         SPACE 1                                                                
DH4      NTR1                                                                   
         L     R2,GLATHID                                                       
         USING GLINTD,R2                                                        
         L     R3,GLAFOUT                                                       
         MVI   DLPEND,C'N'                                                      
         SPACE 1                                                                
DH6      CLI   0(R3),0                                                          
         BE    DHLAST                                                           
         CLI   0(R3),X'10'                                                      
         BE    DHLAST                                                           
         CLI   0(R3),X'30'         LOOK FOR OUT                                 
         BE    DH30                                                             
         CLI   0(R3),X'40'         AND HEAD ELEMENTS                            
         BE    DH40                                                             
         B     DHEND                                                            
         SPACE 1                                                                
         USING DROD,R3                                                          
DH30     ST    R3,LAST30                                                        
         CLI   DLPEND,C'Y'                                                      
         BNE   DH32                                                             
         LA    R1,DLOUTPUT                                                      
         ST    R1,DLIADD                                                        
         MVI   DLLEN,1                                                          
         MVI   DLOUTPUT,C' '                                                    
         BAS   RE,DLALPHA                                                       
         SPACE 1                                                                
DH32     MVI   DLPEND,C'N'                                                      
         CLI   DHLINE,1            TEST FIRST TIME ROUND                        
         BNE   *+10                                                             
         XC    DRODLADR(12),DRODLADR    YES-CLEAR ADDRS OF HEADS 2-4            
         CLI   DROLTYP,C'P'                                                     
         BNE   DHEND                                                            
         GOTO1 =V(DRIVTEST),DMCB,(RA),DROIFS                                    
         BNE   DHEND                                                            
         MVI   DLPEND,C'Y'         OUT IS OK SO WE NEED A HEADING               
         B     DHEND                                                            
         SPACE 1                                                                
         USING DRHDD,R3                                                         
DH40     CLI   DLPEND,C'N'         IGNORE IF NOT PENDING                        
         BE    DHEND                                                            
         GOTO1 =V(DRIVTEST),DMCB,(RA),DRHDIFS                                   
         BNE   DHEND                                                            
         TM    DRHDSTAT,X'80'      IGNORE DELETED HEADS                         
         BO    DHEND                                                            
         CLI   DHLINE,1            TEST FIRST HEADLINE                          
         BNE   *+16                                                             
         CLI   DRHDLINE,1          YES-MUST MATCH ON LINE                       
         BE    DH41                                                             
         B     DHEND                                                            
         CLC   DRHDLINE,DHLINE     NOT 1ST HEADLINE-IF MATCH,                   
         BE    DH41                                 THEN CONTINUE               
         CLI   DRHDLINE,1          TEST THIS HEAD ENTRY IS FOR HEAD 1           
         BNE   DHEND                                                            
         L     R1,LAST30           YES-                                         
         USING DROD,R1                                                          
         ZIC   RE,DHLINE                                                        
         BCTR  RE,0                                                             
         BCTR  RE,0                                                             
         SLL   RE,2                                                             
         LA    RE,DRODLADR(RE)                                                  
         ICM   RF,15,0(RE)         TEST HEAD ALREADY FORMATTED BY               
         BZ    DHEND               HEADLINE 1 ROUTINE                           
         MVI   DLPEND,C'N'         YES-SO DOWNLOAD IT NOW                       
         LA    R1,39(RF)           FIGURE OUT LENGTH                            
         LA    R0,40                                                            
         SPACE 1                                                                
DH40A    CLI   0(R1),C' '                                                       
         BNE   *+10                                                             
         BCTR  R1,0                                                             
         BCT   R0,DH40A                                                         
         STC   R0,DLLEN                                                         
         ST    RF,DLIADD                                                        
         BAS   RE,DLALPHA          DOWNLOAD USER EXPRESSION                     
         B     DHEND                                                            
         SPACE 1                                                                
DH41     MVI   DLPEND,C'N'                                                      
         CLI   DRHDELEN,56         DO WE NEED TO HANDLE A LITERAL               
         BE    DH42                                                             
         ZIC   R1,DRHDELEN         YES                                          
         SH    R1,=H'56'                                                        
         STC   R1,LITLEN           PASS ITS LENGTH                              
         LA    R1,DRHDLIT                                                       
         ST    R1,ALITIN           ITS ADDRESS                                  
         LA    R1,DLOUTPUT                                                      
         ST    R1,ALITOUT                                                       
         MVC   DLOUTPUT,SPACES                                                  
         BAS   RE,DOLIT                                                         
         B     DH44                                                             
         SPACE 1                                                                
DH42     LA    R1,DLOUTPUT                                                      
         ST    R1,GLAOFLD                                                       
         LA    RF,4                                                             
         MVC   0(198,R1),SPACES                                                 
         LA    R1,198(R1)                                                       
         BCT   RF,*-10                                                          
         XC    GLAIFLD,GLAIFLD                                                  
         L     R1,LAST30                                                        
         USING DROD,R1                                                          
         L     R1,DROIADD          PICK UP A(IN) IF ANY                         
         LTR   R1,R1                                                            
         BZ    DH43                                                             
         USING DRIND,R1                                                         
         LH    R1,DRINDISP                                                      
         DROP  R1                                                               
         A     R1,AACTREC                                                       
         ST    R1,GLAIFLD          RECORD A(INPUT FIELD IN RECORD)              
         SPACE 1                                                                
DH43     OC    DRHDRADD,DRHDRADD   IS THERE A USER ROUTINE FOR THIS             
         BZ    DH44                                                             
         MVI   GLHOOK,GLROUT       YES SO SET UP FOR A HOOK                     
         MVC   GLLABEL,DRHDROUT                                                 
         MVC   GLAROUT,DRHDRADD                                                 
         MVC   GLARGS,DRHDARGS                                                  
         ST    R3,GLADTENT                                                      
         BAS   RE,GOHOOK                                                        
         CLI   DHLINE,1            TEST FIRST HEADLINE                          
         BNE   DH44                                                             
         L     R1,LAST30           YES-TEST SUBSEQUENT HEADLINES WERE           
         USING DROD,R1                 FORMATTED BY ROUTINE FOR FIRST           
         LA    R0,3                    HEADLINE                                 
         LA    RE,DLOUTPUT+198                                                  
         LA    RF,DRODLADR                                                      
         SPACE 1                                                                
DH43A    CLC   0(40,RE),SPACES                                                  
         BE    DH43B                                                            
         CR    R4,R6               YES-CHECK WE HAVEN'T FILLED HEADLINE         
         BL    *+6                     POOL YET                                 
         DC    H'0'                                                             
         MVC   0(40,R4),0(RE)      SAVE HEADLINE IN HEAD POOL AND SAVE          
         ST    R4,0(RF)            ITS ADDRESS IN DRIVE TABLE 'OUT'             
         LA    R4,40(R4)           ENTRY                                        
         SPACE 1                                                                
DH43B    LA    RE,198(RE)                                                       
         LA    RF,4(RF)                                                         
         BCT   R0,DH43A                                                         
         SPACE 1                                                                
DH44     LA    R0,1                                                             
         CLC   DLOUTPUT,SPACES                                                  
         BE    DH48                                                             
         LA    R1,DLOUTPUT+59      FIGURE OUT LENGTH                            
         LA    R0,60                                                            
         SPACE 1                                                                
DH46     CLI   0(R1),C' '                                                       
         BNE   DH48                                                             
         BCTR  R1,0                                                             
         BCT   R0,DH46                                                          
         SPACE 1                                                                
DH48     STC   R0,DLLEN                                                         
         LA    R1,DLOUTPUT                                                      
         ST    R1,DLIADD                                                        
         BAS   RE,DLALPHA          DOWNLOAD USER EXPRESSION                     
         SPACE 1                                                                
DHEND    ZIC   R1,1(R3)                                                         
         AR    R3,R1                                                            
         B     DH6                                                              
         SPACE 1                                                                
DHLAST   CLI   DLPEND,C'Y'                                                      
         BNE   DOWNEND2                                                         
         LA    R1,DLOUTPUT                                                      
         ST    R1,DLIADD                                                        
         MVI   DLLEN,1                                                          
         MVI   DLOUTPUT,C' '                                                    
         BAS   RE,DLALPHA                                                       
         B     DOWNEND2                                                         
         EJECT                                                                  
*              DOWN LOADING FACILITIES - ALPHA                                  
         SPACE 3                                                                
DLALPHA  NTR1                                                                   
         LTR   R5,R5               TEST FOR P=NO                                
         BZ    XIT                                                              
         MVC   DLACTLEN,DLLEN                                                   
         TM    GLDOWNLD,GLDLNOTR   NO TRUNCATION OF TRAILING SPACES             
         BO    DLALPHA1                                                         
         BAS   RE,SETACTLN         FIRST FIGURE OUT ACTUAL LENGTH               
         SPACE 1                                                                
DLALPHA1 ZIC   R1,DLCOUNT          IS THERE ROOM FOR THIS                       
         ZIC   R0,DLACTLEN                                                      
         AR    R1,R0                                                            
         LA    R1,3(R1)            AND 2 QUOTES AND 1 SPACE                     
         STC   R1,DLCOUNT                                                       
         CH    R1,=H'71'                                                        
         BL    DLALPHA2                                                         
         LA    R1,DLPOOL           POINT TO NEXT LINE IN POOL                   
         ZIC   R0,DLLINE                                                        
         MH    R0,=H'80'                                                        
         AR    R1,R0                                                            
         ST    R1,DLPADD                                                        
         AI    DLLINE,1            BUMP LINE                                    
         CLI   DLLINE,DLMAXLIN     CHECK WE HAD ROOM                            
         BNH   *+6                                                              
         DC    H'0'                                                             
         MVI   DLCOUNT,0                                                        
         B     DLALPHA1            (GO BACK - WILL NOW FIT)                     
         SPACE 1                                                                
DLALPHA2 L     R2,DLIADD                                                        
         L     R3,DLPADD                                                        
         ZIC   R0,DLACTLEN                                                      
         BAS   RE,ISITNUM                                                       
         MVI   0(R3),C' '                                                       
         CLI   ITSNUM,C'Y'                                                      
         BE    *+8                                                              
         MVI   0(R3),C'"'          DOUBLE QUOTE TO START                        
         LA    R3,1(R3)                                                         
         SPACE 1                                                                
DLALPHA4 MVC   0(1,R3),0(R2)       NOW MOVE OUT THE DATA                        
         CLI   0(R3),C'"'          REPLACE A DOUBLE QUOTE                       
         BNE   *+8                                                              
         MVI   0(R3),C''''         WITH A SINGLE QUOTE                          
         CLI   ITSNUM,C'Y'         IF ITS NUMERIC                               
         BNE   DLALPHA6                                                         
         CLI   0(R3),C'$'          TAKE OUT DOLLAR SIGNS                        
         BNE   DLALPHA6                                                         
         MVI   0(R3),C' '                                                       
         SPACE 1                                                                
DLALPHA6 LA    R2,1(R2)                                                         
         LA    R3,1(R3)                                                         
         BCT   R0,DLALPHA4                                                      
         MVI   0(R3),C' '                                                       
         CLI   ITSNUM,C'Y'                                                      
         BE    *+8                                                              
         MVI   0(R3),C'"'          DOUBLE QUOTE TO END                          
         LA    R3,1(R3)                                                         
         MVI   0(R3),C' '          THEN A SPACE                                 
         LA    R3,1(R3)                                                         
         ST    R3,DLPADD                                                        
         B     XIT                                                              
         EJECT                                                                  
*              CHECK IF FIELD IS REALLY NUMERIC                                 
         SPACE 3                                                                
ISITNUM  NTR1                      CHECK IF FIELD IS NUMERIC                    
         MVI   ITSNUM,0                                                         
         CLI   GLABCSW,0                                                        
         BNE   XIT                                                              
         TM    GLDOWNLD,GLDLALPH    ALL FIELDS SHOULD BE ALPHA                  
         BO    XIT                                                              
         L     R3,GLADTENT                                                      
         LTR   R3,R3                                                            
         BZ    XIT                                                              
         CLI   0(R3),X'30'                                                      
         BNE   XIT                                                              
         USING DROD,R3                                                          
         L     R3,DROIADD                                                       
         USING DRIND,R3                                                         
         LTR   R3,R3                                                            
         BZ    ISITNUM2                                                         
         CLI   DRINTYPE+1,C'+'     CAN'T BE UNLESS FIELDS ADDITIVE              
         BNE   XIT                                                              
         SPACE 1                                                                
ISITNUM2 CLI   0(R2),C' '          SPACE ALLOWED                                
         BE    ISITNUM4                                                         
         CLI   0(R2),C'.'          SO IS PERIOD                                 
         BE    ISITNUM4                                                         
         CLI   0(R2),C','          SO IS COMMA                                  
         BE    ISITNUM4                                                         
         CLI   0(R2),C'-'          AND MINUS SIGN                               
         BE    ISITNUM4                                                         
         CLI   0(R2),C'+'          AND PLUS SIGN                                
         BE    ISITNUM4                                                         
         CLI   0(R2),C'$'          AND $ ALTHOUGH THIS WILL BE                  
         BE    ISITNUM4            STRIPPED OUT LATER                           
         CLI   0(R2),C'0'                                                       
         BNL   ISITNUM3            AND, OF COURSE, 0-9                          
         MVI   ITSNUM,C'N'                                                      
         B     XIT                                                              
         SPACE 1                                                                
ISITNUM3 MVI   ITSNUM,C'Y'         AT LEAST ONE DIGIT FOUND!                    
         SPACE 1                                                                
ISITNUM4 LA    R2,1(R2)                                                         
         BCT   R0,ISITNUM2                                                      
         B     XIT                                                              
         SPACE 1                                                                
ITSNUM   DC    C'N'                                                             
         EJECT                                                                  
*              STRIP NUMERIC FIELDS AND ZERO FILL                               
         SPACE 3                                                                
STRIP    NTR1                                                                   
         TM    GLDOWNLD,GLDLSTRP   ONLY APPLICABLE IF STRIP ON                  
         BNO   XIT                                                              
         L     R3,GLADTENT                                                      
         LTR   R3,R3                                                            
         BZ    XIT                                                              
         CLI   0(R3),X'30'                                                      
         BNE   XIT                                                              
         USING DROD,R3                                                          
         L     R3,DROIADD                                                       
         USING DRIND,R3                                                         
         LTR   R3,R3                                                            
         BZ    STRIP2                                                           
         CLI   DRINTYPE+1,C'+'     CAN'T BE UNLESS FIELDS ADDITIVE              
         BNE   XIT                                                              
         SPACE 1                                                                
*                                  DATA CURRENTLY IN DLOUTPUT                   
STRIP2   MVC   DLBORROW,DLOUTPUT   SO SAVE INTO DLBORROW                        
         MVI   ITSNEG,C'N'                                                      
         L     R3,GLADTENT                                                      
         USING DROD,R3                                                          
         MVI   DLOUTPUT,C'0'                                                    
         ZIC   R1,DROLEN           AND PRE-FILL OUTPUT WITH 0'S                 
         LTR   R1,R1                                                            
         BNP   XIT                                                              
         SH    R1,=H'2'                                                         
         BM    STRIP3                                                           
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   DLOUTPUT+1(0),DLOUTPUT                                           
         SPACE 1                                                                
STRIP3   ZIC   R1,DROLEN                                                        
         LA    RE,DLBORROW                                                      
         LA    RF,DLOUTPUT                                                      
         BCTR  R1,0                                                             
         AR    RE,R1               RE=A(END OF INPUT)                           
         AR    RF,R1               RF=A(END OF OUTPUT)                          
         LA    R1,1(R1)                                                         
         SPACE 1                                                                
STRIP4   CLI   0(RE),C'-'          CHECK FOR NEGATIVE                           
         BNE   *+8                                                              
         MVI   ITSNEG,C'Y'                                                      
         CLI   0(RE),C'0'          STRIP OUT NON NUMERIC                        
         BL    STRIP6                                                           
         CLI   0(RE),C'9'                                                       
         BH    STRIP6                                                           
         MVC   0(1,RF),0(RE)       PASS A NUMERIC CHARACTER                     
         BCTR  RF,0                                                             
         SPACE 1                                                                
STRIP6   BCTR  RE,0                                                             
         BCT   R1,STRIP4                                                        
         CLI   ITSNEG,C'Y'                                                      
         BNE   XIT                                                              
         MVI   DLOUTPUT,C'-'       RETURN NEGATIVE AS FIRST FIELD               
         B     XIT                                                              
         SPACE 1                                                                
ITSNEG   DC    C'N'                                                             
         EJECT                                                                  
*              DOWN LOADING FACILITIES - SET LENGTH OF ALPHA                    
         SPACE 3                                                                
SETACTLN NTR1                                                                   
         L     R2,DLIADD           POSITION R2 TO END OF FIELD                  
         ZIC   R3,DLLEN                                                         
         BCTR  R2,0                                                             
         AR    R2,R3                                                            
         SPACE 1                                                                
SETACT2  STC   R3,DLACTLEN                                                      
         CLI   0(R2),C' '          FIND LAST NON BLANK                          
         BH    XIT                                                              
         BCTR  R2,0                                                             
         BCT   R3,SETACT2                                                       
         MVI   DLACTLEN,1          ALWAYS SEND AT LEAST 1 SPACE                 
         B     XIT                                                              
         EJECT                                                                  
*              DOWN LOADING FACILITIES - NUMERIC                                
         SPACE 3                                                                
DLNUM    NTR1                                                                   
         LTR   R5,R5               TEST FOR P=NO                                
         BZ    XIT                                                              
         CLI   GLABCMOD,1          IF ABC IS JUST PRINTING HEADERS              
         BE    XIT                 THEN IT DOESN'T HAVE NUMBERS HERE            
         USING DROD,R3                                                          
         OC    DROACOMP,DROACOMP   DO WE NEED TO COMPUTE THIS FIELD             
         BZ    DLNUM2                                                           
         OC    DRORADD,DRORADD     IF HAVE USER ROUTINE DONT RECOMPUTE          
         BNZ   DLNUM1                                                           
         MVC   DMCB+4(4),DROACOMP                                               
         MVC   DMCB+8(4),ATHISREC                                               
         GOTO1 =V(DRIVCOMP),DMCB,(RA)                                           
*                                  NUMERIC VALUE IS PACKED IN DUB               
DLNUM1   XC    EBLOCK,EBLOCK                                                    
         LA    R1,DUB                                                           
         ST    R1,EBAIN                                                         
         MVI   EBTIN,C'P'                                                       
         MVI   EBLIN,8                                                          
         B     DLNUM4                                                           
         SPACE 1                                                                
DLNUM2   XC    EBLOCK,EBLOCK       GENERAL NUMERIC                              
         MVC   EBAIN,DLIADD                                                     
         L     R2,DROIADD          PICK UP A(IN)                                
         USING DRIND,R2                                                         
         MVC   EBTIN,DRINTYPE      PICK UP INPUT TYPE                           
         MVC   EBLIN,DRINFLEN      AND LENGTH                                   
         DROP  R2                                                               
         SPACE 1                                                                
DLNUM4   LA    R1,DLOUTPUT                                                      
         ST    R1,EBAOUT                                                        
         MVC   DLOUTPUT,SPACES                                                  
         MVI   EBLOUT,16                                                        
         MVC   EBDECS,DRODEC                                                    
         MVC   EBROUND,DRODIV                                                   
         MVI   EBOPT,X'20'         SET ZERO=NOBLANK                             
         TM    DROEDIT,X'48'                                                    
         BZ    *+8                                                              
         OI    EBOPT,X'40'                                                      
         MVI   EBALIGN,C'L'        LEFT ALIGN FIELD                             
         CLI   DROFILL,C'0'        FILL=0 IS OK                                 
         BNE   *+10                                                             
         MVC   EBFILL,DROFILL                                                   
         CLI   DROFLOAT,C'-'       SO IS FLOAT=-                                
         BNE   *+10                                                             
         MVC   EBFLOAT,DROFLOAT                                                 
***      TM    GLDOWNLD,GLDLSTRP   OPTION TO STRIP NUMBERS                      
***      BNO   DLNUM5                                                           
***      NI    EBOPT,X'FF'-EBOQCEY NO COMMAS                                    
***      NI    EBOPT,X'FF'-EBOQMEY NO MINUS SIGNS YET                           
***      MVI   EBDECS,0            NO DECIMAL POINTS                            
***      MVI   EBFILL,C'0'         FILL WITH 0                                  
***      MVC   EBLOUT,DROLEN       USE REAL OUTPUT LENGTH                       
***      MVI   EBALIGN,C'R'        AND RIGHT ALIGN                              
         SPACE 1                                                                
DLNUM5   GOTO1 EDITOR,DMCB,EBLOCK                                               
         BAS   RE,STRIP                                                         
         SPACE 1                                                                
         ZIC   R1,DROLEN                                                        
         TM    GLDOWNLD,GLDLNOTR   DO NOT TRUNCATE TRAILING SPACES              
         BO    DLNUM8                                                           
         LA    RE,DLOUTPUT+15      FIGURE OUT L'OUTPUT                          
         LA    R1,16                                                            
         SPACE 1                                                                
DLNUM6   CLI   0(RE),C' '                                                       
         BNE   DLNUM8                                                           
         BCTR  RE,0                                                             
         BCT   R1,DLNUM6                                                        
         SPACE 1                                                                
DLNUM8   ZIC   R0,DLCOUNT          IS THERE ROOM FOR THIS                       
         STC   R1,DLOLEN                                                        
         AR    R1,R0                                                            
         LA    R1,1(R1)            AND 1 SPACE?                                 
         TM    GLDOWNLD,GLDLALPH   TEST ALL FIELDS SHOULD BE ALPHA              
         BZ    *+8                                                              
         LA    R1,2(R1)            YES-ADD 2 FOR DINKS                          
         STC   R1,DLCOUNT                                                       
         CH    R1,=H'71'                                                        
         BL    DLNUM10                                                          
         LA    R1,DLPOOL           POINT TO NEXT LINE IN POOL                   
         ZIC   R0,DLLINE                                                        
         MH    R0,=H'80'                                                        
         AR    R1,R0                                                            
         ST    R1,DLPADD                                                        
         AI    DLLINE,1            BUMP LINE                                    
         CLI   DLLINE,DLMAXLIN     CHECK WE HAD ROOM                            
         BNH   *+6                                                              
         DC    H'0'                                                             
         MVI   DLCOUNT,0                                                        
         SPACE 1                                                                
DLNUM10  ZIC   R1,DLOLEN           NOW OUTPUT THE DATA                          
         L     R2,DLPADD                                                        
         TM    GLDOWNLD,GLDLALPH   DINKS IF ALL FIELDS SHOULD BE ALPHA          
         BZ    *+12                                                             
         MVI   0(R2),C'"'                                                       
         LA    R2,1(R2)                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),DLOUTPUT                                                 
         AR    R2,R1                                                            
         TM    GLDOWNLD,GLDLALPH                                                
         BZ    *+12                                                             
         MVI   0(R2),C'"'                                                       
         LA    R2,1(R2)                                                         
         MVI   0(R2),C' '          END WITH A SPACE                             
         LA    R2,1(R2)                                                         
         ST    R2,DLPADD                                                        
         B     XIT                                                              
         SPACE 1                                                                
DOWNLAST NTR1                                                                   
         TM    GLDOWNLD,GLDLACTV                                                
         BNO   XIT                                                              
         MVI   DLFIRST,0           RESET AT END OF REPORT                       
         MVI   P,C':'              SEND COLON FOR REPORT END                    
         GOTO1 PRINT,DMCB,PFILL,=C'BL01'                                        
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO CONTROL THE PRINTING OF DETAIL LINES                  
         SPACE 3                                                                
SPLAT    NTR1                                                                   
         L     R6,GLATHID                                                       
         USING GLINTD,R6                                                        
         TM    GLDOWNLD,GLDLACTV                                                
         BO    XIT                 DON'T WRITE IF DOWNLOADING                   
         MVI   GLAROUT,1           HOOK TO APPLICATION TO SAY                   
         MVI   GLHOOK,GLPRINT      WE'RE ABOUT TO PRINT                         
         BAS   RE,GOHOOK                                                        
         MVI   DONTSW,C'N'                                                      
         CLI   GLHOOK,GLDONT       APPLIC. CAN THEN REJECT                      
         BNE   SPLAT2                                                           
         MVI   DONTSW,C'Y'                                                      
         L     R2,AMYP1                                                         
         LA    R0,20                                                            
         SPACE 1                                                                
SPLAT1   MVC   0(198,R2),SPACES    SO WE'LL CLEAR PRINT LINES                   
         LA    R2,198(R2)                                                       
         BCT   R0,SPLAT1                                                        
         B     XIT                                                              
         SPACE 1                                                                
SPLAT2   MVC   LASTPDET,SAVEDET    ACTUALLY PRINTING SO SET                     
         MVC   LASTP2,SAVEP2                                                    
         MVC   LASTP3,SAVEP3                                       L01          
         MVC   LASTP4,SAVEP4                                       L01          
         CLI   CONTSW,C'S'         CONTINUATION DETAILS                         
         BNE   *+8                                                              
         MVI   CONTSW,C'Y'                                                      
         L     R2,AMYM1            SEE IF WE NEED TO HANDLE MIDS                
         MVI   NUMMIDS,0                                                        
         CLC   0(198,R2),SPACES                                                 
         BE    SPLAT2A                                                          
         MVI   MIDCONT,C'Y'        YES - SET MIDLINE CONTINUED SWITCH           
         CLC   LASTMID,0(R2)       COMPARE TO LAST MIDLINE                      
         BE    SPLAT2A                                                          
         MVI   MIDCONT,C'N'        NOT EQUAL - NOT MIDLINE CONTD                
         BAS   RE,SETMID           SET NUM MIDS                                 
         MVC   LASTMID,0(R2)                                                    
         SPACE 1                                                                
SPLAT2A  L     R2,AMYP1                                                         
         SR    R1,R1               FIRST COUNT ACTIVE LINES IN R1               
         LA    R0,20                                                            
         SPACE 1                                                                
SPLAT3   CLC   0(198,R2),SPACES                                                 
         BE    *+8                                                              
         LA    R1,1(R1)                                                         
         LA    R2,198(R2)                                                       
         BCT   R0,SPLAT3                                                        
         SPACE 1                                                                
         CLI   NUMMIDS,0           TEST ANY MIDLINES                            
         BE    SPLAT3A                                                          
         CH    R1,=H'4'            YES - MAKE SURE AT LEAST ROOM FOR            
         BNL   SPLAT3A                   4 PRINT LINES                          
         LA    R1,4                                                             
         SPACE 1                                                                
SPLAT3A  CLI   NLINES,0                                                         
         BNE   *+8                                                              
         MVI   NLINES,1                                                         
         IC    R0,NLINES                                                        
         AR    R1,R0               THEN ADD IN SPACING LINES                    
         IC    R0,NUMMIDS                                                       
         LTR   R0,R0               IF ANY MIDLINES                              
         BZ    *+8                                                              
         AH    R0,=H'2'            NEED SPACE BEFORE AND AFTER                  
         AR    R1,R0               AND ADD IN MID LINES                         
         L     R2,GLALINE                                                       
         IC    R0,0(R2)            AND ADD IN PRESENT LINE NUMBER               
         AR    R1,R0                                                            
         LA    R1,2(R1)            +2 FOR LUCK - HAD TROUBLE BEFORE             
         L     R2,GLALMAX                                                       
         IC    R0,0(R2)            R0=MAXLINES                                  
         CH    R0,=H'58'                                                        
         BH    *+8                                                              
         LA    R0,58                                                            
         CR    R1,R0               WILL THEY FIT?                               
         BNH   SPLAT4                                                           
         L     R2,GLAFHEAD         NO SO FORCE HEADLINES ON FIRST PRINT         
         MVI   0(R2),C'Y'                                                       
         SPACE 1                                                                
SPLAT4   L     R2,GLAFHEAD                                                      
         MVC   TOPSW,0(R2)         SAVE FORCEHEAD SETTING                       
         CLI   TOPSW,C'Y'          IF WE ARE ABOUT TO DO HEADLINES              
         BNE   SPLAT4B                                                          
         MVI   NUMMIDS,0              FIGURE OUT MIDLINES AGAIN                 
         L     R2,AMYM1                                                         
         CLC   0(198,R2),SPACES                                                 
         BE    SPLAT5                                                           
         BAS   RE,SETMID           SET NUM MIDS                                 
         B     SPLAT5                                                           
         SPACE 1                                                                
SPLAT4B  CLI   NUMMIDS,0           UNLESS WE ARE DOING MID LINES                
         BNE   SPLAT5                                                           
         CLI   HORIZSW,C'Y'        IF A HORIZONTAL LINE IS PENDING              
         BNE   SPLAT5                                                           
         BAS   RE,PRHORIZ          PRINT A HORIZONAL LINE                       
         SPACE 1                                                                
SPLAT5   MVI   HORIZSW,C'N'                                                     
         L     RF,REPORT                                                        
         L     R3,GLASPACE                                                      
         L     R4,AREPWRKD         POINT TO REPORT WORKING STORAGE              
         L     R5,GLAP1                                                         
         BAS   RE,SPLMIDS          DEAL WITH MIDS IF NEEDED                     
         L     R2,AMYP1                                                         
         LA    R0,20                                                            
         CLC   0(198,R2),SPACES    TEST FIRST LINE IS BLANK                     
         BNE   SPLAT7                                                           
         CLI   TOPSW,C'Y'          YES-TEST TOP OF PAGE                         
         BE    SPLAT8                                                           
         B     SPLAT7              ALWAYS PRINT LINE 1                          
         SPACE 1                                                                
SPLAT6   CLC   0(198,R2),SPACES    NOW RELEASE THE PRINT LINES                  
         BE    SPLAT8                                                           
         SPACE 1                                                                
SPLAT7   MVC   0(198,R5),0(R2)     (SUPPORT WIDE AS WELL)                       
         CLI   TOPSW,C'Y'          IF WE ARE AT 'TOP' OF PAGE                   
         BNE   *+8                                                              
         BAS   RE,CONTIN           CHECK FOR CONTINUATION                       
         MVI   TOPSW,C'N'                                                       
         MVC   0(198,R2),SPACES    CLEAR MY BUFFER                              
         MVI   0(R3),1             SINGLE SPACE                                 
         L     R4,AREPWRKD         POINT TO REPORT WORKING STORAGE              
         ST    R4,DMCB                                                          
         LA    R1,DMCB                                                          
         CLI   AREPWRKD,C'R'       SKIP IF NOT FAREPORT                         
         BNE   *+6                                                              
         LR    R1,R4                                                            
         GOTO1 (RF),(R1)                                                        
         MVC   0(198,R5),SPACES    CLEAR USERS PRINT LINES                      
         MVC   198(198,R5),SPACES                                               
         SPACE 1                                                                
SPLAT8   LA    R2,198(R2)                                                       
         BCT   R0,SPLAT6                                                        
         CLI   NLINES,1            ANY EXTRA LINES NEEDED?                      
         BE    XIT                                                              
         CLI   TOPSW,C'Y'          YES-TEST STILL AT TOP OF PAGE                
         BE    XIT                     YES-THEN DON'T BOTHER                    
         IC    R0,NLINES                                                        
         BCTR  R0,0                                                             
         STC   R0,0(R3)                                                         
         L     R4,AREPWRKD         POINT TO REPORT WORKING STORAGE              
         ST    R4,DMCB                                                          
         LA    R1,DMCB                                                          
         CLI   AREPWRKD,C'R'       SKIP IF NOT FAREPORT                         
         BNE   *+6                                                              
         LR    R1,R4                                                            
         GOTO1 (RF),(R1)                                                        
         B     XIT                                                              
         SPACE 2                                                                
SETMID   LR    R0,RE                                                            
         MVI   NUMMIDS,2           ALWAYS MIDS=2                                
         LH    RE,GLPDISP                                                       
         LA    RE,1(RE,R2)                                                      
         ST    RE,DMCB                                                          
         MVI   DMCB,49                                                          
         LA    RE,198(RE)                                                       
         ST    RE,DMCB+4                                                        
         MVI   DMCB+4,X'BF'                                                     
         CLI   0(RE),X'BF'         TEST LEFTOVER UNDERLINE ON MID2              
         BE    *+14                                                             
         CLC   198(198,R2),SPACES  OR MID2 IS SPACES                            
         BNE   SETMIDX                                                          
         MVC   198(198,R2),SPACES  YES - THEN UNDERLINE                         
         GOTO1 UNDERLIN,DMCB                                                    
SETMIDX  LR    RE,R0                                                            
         BR    RE                                                               
         DROP  R6                                                               
         EJECT                                                                  
         SPACE 1                                                                
YES      SR    R1,R1                                                            
         B     *+8                                                              
         SPACE 1                                                                
NO       LA    R1,1                                                             
         LTR   R1,R1                                                            
         SPACE 1                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
*              DEAL WITH MID LINE PRINTING                                      
         SPACE 3                                                                
*              INPUTS              R4=A(WORK AREA)                              
*                                  R5=A(P1 IN THAT AREA)                        
*                                  RF=A(REPORT)                                 
         SPACE 1                                                                
SPLMIDS  NTR1                                                                   
         CLI   NUMMIDS,0                                                        
         BE    XIT                                                              
         L     R2,AMYM1                                                         
         CLI   TOPSW,C'Y'          ARE WE ABOUT TO DO HEADLINES                 
         BE    SPLMID4                                                          
         L     R6,ABOX             NO - SO ....                                 
         USING BOXD,R6                                                          
         MVI   BOXREQ,C'C'         CLOSE EXISTING BOX                           
         MVC   0(198,R5),SPACES                                                 
         ST    R4,DMCB                                                          
         LA    R1,DMCB                                                          
         CLI   AREPWRKD,C'R'       SKIP IF NOT FAREPORT                         
         BNE   *+6                                                              
         LR    R1,R4                                                            
         GOTO1 (RF),(R1)                                                        
         MVC   0(198,R5),0(R2)     PRINT MID1                                   
         CLC   GLMIDXTR,SPACES     TEST ANY EXTRA MIDLINE DATA                  
         BNH   *+8                                                              
         BAS   RE,SPLMIDXT                                                      
         ST    R4,DMCB                                                          
         LA    R1,DMCB                                                          
         CLI   AREPWRKD,C'R'       SKIP IF NOT FAREPORT                         
         BNE   *+6                                                              
         LR    R1,R4                                                            
         GOTO1 (RF),(R1)                                                        
         LA    R2,198(R2)                                                       
         CLI   NUMMIDS,2           AND, IF NEEDED,                              
         BNE   SPLMID2                                                          
         MVC   0(198,R5),0(R2)     PRINT MID2                                   
         ST    R4,DMCB                                                          
         LA    R1,DMCB                                                          
         CLI   AREPWRKD,C'R'       SKIP IF NOT FAREPORT                         
         BNE   *+6                                                              
         LR    R1,R4                                                            
         GOTO1 (RF),(R1)                                                        
         SPACE 1                                                                
SPLMID2  MVI   BOXREQ,C'O'         OPEN NEW BOX                                 
         CLC   SAVEBXCL,SPACES                                                  
         BE    *+10                                                             
         MVC   BOXCOLS(198),SAVEBXCL    RESTORE BOX COLUMNS                     
         MVC   SAVEBXCL,SPACES                                                  
         DROP  R6                                                               
         B     SPLMID6                                                          
         SPACE 1                                                                
*                                  DOING HEADINGS - THINGS EASIER               
SPLMID4  MVC   0(198,R5),0(R2)     PRINT MID1                                   
         CLC   GLMIDXTR,SPACES     TEST ANY EXTRA MIDLINE DATA                  
         BNH   *+8                                                              
         BAS   RE,SPLMIDXT                                                      
         CLI   MIDCONT,C'Y'        IS CONTINUATION NEEDED ?                     
         BNE   SPLMID5                                                          
         LA    R1,197(R5)                                                       
         CLI   0(R1),C' '                                                       
         BH    *+10                                                             
         BCTR  R1,0                                                             
         B     *-10                                                             
         MVC   3(11,R1),=C'(CONTINUED)'                                         
         SPACE 1                                                                
SPLMID5  DS    0H                                                               
         ST    R4,DMCB                                                          
         LA    R1,DMCB                                                          
         CLI   AREPWRKD,C'R'       SKIP IF NOT FAREPORT                         
         BNE   *+6                                                              
         LR    R1,R4                                                            
         GOTO1 (RF),(R1)                                                        
         LA    R2,198(R2)                                                       
         CLI   NUMMIDS,2           AND, IF NEEDED,                              
         BNE   SPLMID6                                                          
         MVC   0(198,R5),0(R2)     PRINT MID2                                   
         ST    R4,DMCB                                                          
         LA    R1,DMCB                                                          
         CLI   AREPWRKD,C'R'       SKIP IF NOT FAREPORT                         
         BNE   *+6                                                              
         LR    R1,R4                                                            
         GOTO1 (RF),(R1)                                                        
         SPACE 1                                                                
SPLMID6  MVC   0(198,R5),SPACES    SPACE AFTER                                  
         ST    R4,DMCB                                                          
         LA    R1,DMCB                                                          
         CLI   AREPWRKD,C'R'       SKIP IF NOT FAREPORT                         
         BNE   *+6                                                              
         LR    R1,R4                                                            
         GOTO1 (RF),(R1)                                                        
         B     XIT                                                              
         SPACE 1                                                                
SPLMIDXT LA    R1,197(R5)          EXTRA MIDLINE DATA                           
         CLI   0(R1),C' '          TACK IT TO END OF MID                        
         BH    *+10                                                             
         BCTR  R1,0                                                             
         B     *-10                                                             
         MVC   3(L'GLMIDXTR,R1),GLMIDXTR                                        
         MVC   GLMIDXTR,SPACES     CLEAR EXTRA MIDLINE DATA                     
         BR    RE                                                               
         EJECT                                                                  
*              ROUTINES TO HANDLE CONTINUATION ON NEXT PAGE                     
         SPACE 3                                                                
CONTIN   NTR1                                                                   
         L     R2,GLATHID                                                       
         USING GLINTD,R2                                                        
         CLI   CONTSW,C'Y'         IS CONTINUATION RELEVANT?                    
         BNE   XIT                                                              
         MVI   CONTSW,C'N'                                                      
         CLI   LINETYPE,C'T'       NOT APPLICABLE FOR TOTAL LINES               
         BE    XIT                                                              
         L     R1,WIDTH            YES                                          
         BCTR  R1,0                                                             
         L     RE,GLAP1                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         OC    0(0,RE),LASTPDET    MAKE SURE WE GET IT ALL                      
         CLI   NUMMIDS,0           MIDLINES ALREADY DID CONTINUED               
         BNE   XIT                                                              
         SPACE 1                                                                
         L     RF,WIDTH            MOVE P1 TO P2                                
         AR    RF,RE                                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RF),0(RE)                                                    
         EX    R1,*+8              CLEAR P1                                     
         B     *+10                                                             
         MVC   0(0,RE),SPACES                                                   
         CLI   GLCNTWID,0                                                       
         BE    XIT                                                              
         ZIC   R0,GLCNTDSP         PICK UP CONTINUATION DISPLACEMENT            
         AR    RE,R0                                                            
         AH    RE,GLPDISP                                                       
         LA    RE,1(RE)                                                         
         MVC   0(4,RE),=C'CONT'    MOVE IN APPROPRIATE MESSAGE                  
         CLI   GLCNTWID,6                                                       
         BL    XIT                                                              
         MVC   0(6,RE),=C'(CONT)'                                               
         CLI   GLCNTWID,10                                                      
         BL    XIT                                                              
         MVC   0(9,RE),=C'CONTINUED'                                            
         CLI   GLCNTWID,12                                                      
         BL    XIT                                                              
         MVC   0(11,RE),=C'(CONTINUED)'                                         
         B     XIT                                                              
         EJECT                                                                  
*              HOOK TO APPLICATION OR SYSTEM DRIVER                             
         SPACE 3                                                                
GOHOOK   NTR1                                                                   
         CLI   GLAROUT,0                                                        
         BE    XIT                                                              
         CLI   GLAROUT,2           FIRST BYTE INDICATES WHER                    
*                                  1=APPLICATION 2=SYSDRIVER                    
         BE    GOH2                                                             
         BH    GOH3                                                             
         L     RF,GLAHOOK          PICK UP APPLICATION HOOK                     
         LTR   RF,RF                                                            
         BZ    XIT                                                              
         L     RE,USERRD           USERS RD                                     
         LM    R0,RC,20(RE)        RESTORE USERS R0-RC                          
         BASR  RE,RF               RF CONTAINED A(HOOK ROUTINE)                 
         XIT1                                                                   
         SPACE 1                                                                
GOH2     L     RF,GLASYSDR                                                      
         LTR   RF,RF                                                            
         BZ    XIT                                                              
         GOTO1 (RF),DMCB,(RA)                                                   
         B     XIT                                                              
         SPACE 1                                                                
GOH3     GOTO1 =V(DRIVROUT),DMCB,(RA)                                           
         B     XIT                                                              
         SPACE 1                                                                
         EJECT                                                                  
*              LTORG FOR MAIN SECTION                                           
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
*              START HEAD HOOK ROUTINES                                         
         SPACE 3                                                                
*                                  R6=POINTER TO BOXCOLS                        
         SPACE 1                                                                
         DS    0D                                                               
*                                  NEEDS WORK IN DOLIT AND GOHOOK               
*                                  BEFORE THIS COULD BE SEPARATE                
******   USING *,RF                                                             
HOOK     NTR1                                                                   
******   LA    RB,HOOK                                                          
******   LA    R9,2048(RB)                                                      
******   LA    R9,2048(R9)                                                      
******   DROP  RF                                                               
******   USING HOOK,RB,R9                                                       
         L     R5,GLAIO                                                         
         ZIC   R2,0(R5)            FROM RECORD NUMBER                           
         LTR   R2,R2               PROTECT FOR BUDGET (NO RECORDS)              
         BNZ   *+8                                                              
         LA    R2,1                                                             
         BCTR  R2,0                                                             
         SLL   R2,2                                                             
         LA    R2,GLAINTD(R2)                                                   
         L     R2,0(R2)                                                         
         USING GLINTD,R2                                                        
         L     R3,GLAFOUT          SO WE CAN GET TO THE OUTS                    
         SPACE 1                                                                
HOOK2    L     RE,AMYH1            PRECLEAR MY HEADLINES                        
         LA    R0,14                                                            
         SPACE 1                                                                
HOOK4    MVC   0(198,RE),SPACES                                                 
         LA    RE,198(RE)                                                       
         BCT   R0,HOOK4                                                         
         SPACE 1                                                                
         L     R6,ABOX             INITIALIZE BOXES                             
         USING BOXD,R6                                                          
         MVI   BOXINIT,0                                                        
         MVI   BOXWT,1                                                          
         MVI   BOXYORN,C'Y'                                                     
         MVI   BOXOFF,0                                                         
         CLI   GLBOXOPT,C'N'       OPTION NOT TO HAVE BOXES                     
         BNE   *+8                                                              
         MVI   BOXOFF,C'Y'                                                      
         MVI   BOXBLANK,C'N'                                                    
         MVC   BOXROWS,SPACES                                                   
         ZIC   R1,GLFHEADL         FIRST HEADLINE                               
         SR    RE,RE                                                            
         ICM   RE,1,GLFHLOVR       TEST FOR FIRST HEADLINE OVERRIDE             
         BZ    *+6                                                              
         LR    R1,RE                                                            
         SH    R1,=H'2'                                                         
         BM    HBOX2                                                            
         LA    R1,BOXROWS(R1)                                                   
         MVI   0(R1),C'T'                                                       
         SPACE 1                                                                
HBOX2    MVI   BOXROWS+58,C'B'                                                  
         MVC   BOXCOLS,SPACES      START OFF COLUMNS                            
         MVC   BOXCOLSR,SPACES                                                  
         LA    R6,BOXCOLS          USE R6 FOR BOXCOLS POINTER                   
         DROP  R6                                                               
         AH    R6,GLPDISP                                                       
         MVI   0(R6),C'L'          FIRST WILL BE AN L                           
         XC    AFCBOX,AFCBOX                                                    
         XC    CHUNKADD,CHUNKADD   CLEAR SWITCHES                               
         MVC   LASTHEAD,SPACES                                                  
         B     HOUT12                                                           
         EJECT                                                                  
*              OUT RELATED BOX ROUTINES                                         
         SPACE 3                                                                
HOUT12   CLI   0(R3),X'12'                                                      
         BNE   HOUT30                                                           
         TM    GLINDS2,GLEXTBOX    OPTION FOR ANOTHER BOX                       
         BNO   *+12                                                             
         MVI   0(R6),C'C'                                                       
         LA    R6,1(R6)                                                         
         ST    R6,AFCBOX           SAVE A(FIRST COLUMN BOX)                     
         B     HOOKEND                                                          
         SPACE 1                                                                
HOUT30   CLI   0(R3),X'30'                                                      
         BNE   HEAD                                                             
         ST    R3,LAST30                                                        
         USING DROD,R3                                                          
         MVC   LASTLTYP,DROLTYP    SAVE LINE TYPE FOR HEAD ROUTES               
         MVC   LASTNHED,DROHDFOL   SAVE NUMBER OF HEADS THAT FOLLOW             
         CLI   DROLTYP,C'P'                                                     
         BNE   HOUT33B             P OUTS DICTATE THE BOXES                     
         MVC   SAVOLEN,DROLEN      SAVE ODDMENTS FOR HEAD AND CHUNK             
         ST    R6,ALASTBXC                                                      
         CLI   0(R6),C'L'          (FIRST COLUMN)                               
         BE    HOUT32                                                           
         TM    DROFORM,X'02'       TEST FOLD SET ON THIS                        
         BZ    *+8                                                              
         MVI   0(R6),C'R'          YES-SET RIGHT HAND SIDE OF BOX               
         CLI   0(R6),C'R'          TEST RIGHT HAND SIDE SET YET                 
         BE    HOUT33              YES                                          
         TM    DROFORM,X'20'       OPTION TO SUPPRESS BOX TO LEFT               
         BO    HOUT32                                                           
         OC    CHUNKADD,CHUNKADD   (SEE IF CHUNK IS IN OPERATION)               
         BNZ   HOUT32                                                           
         MVI   0(R6),C'C'                                                       
         TM    GLNORBOX,X'80'      OPTION NOT TO HAVE ROW BOXES                 
         BNO   HOUT32                                                           
         OC    AFCBOX,AFCBOX                                                    
         BNZ   HOUT32                                                           
         MVI   0(R6),C' '                                                       
         SPACE 1                                                                
HOUT32   CLI   DROLINE,1           IF WE ARE ON LINE 1                          
         BH    HOUT33                                                           
         ZIC   R1,DROLEN           ADVANCE TO NEXT BOXCOLS                      
         AR    R6,R1                                                            
         ZIC   R1,GLGAP                                                         
         AR    R6,R1                                                            
         SPACE 1                                                                
HOUT33   C     R3,CHUNKADD         HAVE WE REACHED END OF CHUNK?                
         BNE   HOUT33B                                                          
         XC    CHUNKADD,CHUNKADD   YES SO CLEAR IT                              
         SPACE 1                                                                
HOUT33B  MVI   OUTYN,C'N'          CHECK FOR CONDITIONALS                       
         GOTO1 =V(DRIVTEST),DMCB,(RA),DROIFS                                    
         BNE   HOOKEND                                                          
         MVI   OUTYN,C'Y'                                                       
         CLI   DROLTYP,C'H'        ONLY INTERESTED IN HEADS                     
         BNE   HOOKEND                                                          
         EJECT                                                                  
*              DEAL WITH OUT RELATED TEXTS                                      
         SPACE 3                                                                
HOUT34   ST    R3,GLADTENT                                                      
         L     R5,DROAPOS          R5=A(OUTPUT)                                 
         CLI   DROELEN,140         DO WE NEED TO HANDLE A LITERAL               
         BE    HOUT36                                                           
         ZIC   R1,DROELEN          YES                                          
         SH    R1,=H'140'                                                       
         STC   R1,LITLEN           PASS ITS LENGTH                              
         LA    R1,DROLIT                                                        
         ST    R1,ALITIN           ITS ADDRESS                                  
         ST    R5,ALITOUT          AND WHERE IT GOES                            
         BAS   RE,DOLIT                                                         
         L     R5,ALITOUT          R5=A(NEXT SPACE)                             
         LA    R5,1(R5)            LEAVE A BLANK                                
         SPACE 1                                                                
HOUT36   ST    R5,GLAOFLD                                                       
         XC    GLAIFLD,GLAIFLD                                                  
         L     R1,DROIADD                                                       
         LTR   R1,R1                                                            
         BZ    HOUT38                                                           
         USING DRIND,R1                                                         
         LH    R1,DRINDISP                                                      
         DROP  R1                                                               
         A     R1,AACTREC                                                       
         ST    R1,GLAIFLD                                                       
         SPACE 1                                                                
HOUT38   OC    DRORADD,DRORADD     IS THERE A USER ROUTINE FOR THIS             
         BZ    HOUT40                                                           
         MVI   GLHOOK,GLROUT       YES SO SET UP FOR A HOOK                     
         MVC   GLLABEL,DROROUT                                                  
         MVC   GLAROUT,DRORADD                                                  
         MVC   GLARGS,DROARGS                                                   
         BAS   RE,GOHOOK                                                        
         B     HOOKEND                                                          
         SPACE 1                                                                
HOUT40   BAS   RE,FORM                                                          
         B     HOOKEND                                                          
         EJECT                                                                  
*              DEAL WITH HEAD ELEMENTS                                          
         SPACE 3                                                                
HEAD     CLI   0(R3),X'40'                                                      
         BNE   CHUNK                                                            
         USING DRHDD,R3                                                         
         CLI   OUTYN,C'N'          IGNORE IF PREVIOUS OUT FAILED                
         BE    HOOKEND                                                          
         CLI   LASTLTYP,C'P'       ONLY INTERESTED IN P TYPE                    
         BNE   HOOKEND                                                          
         GOTO1 =V(DRIVTEST),DMCB,(RA),DRHDIFS                                   
         BNE   HOOKEND                                                          
         TM    DRHDSTAT,X'80'      IGNORE DELETED HEADS                         
         BO    HOOKEND                                                          
         SPACE 1                                                                
         ST    R3,GLADTENT                                                      
         L     R5,DRHDAPOS         R5=A(OUTPUT)                                 
         SR    R1,R1                                                            
         ICM   R1,1,GLFHLOVR       TEST FIRST HEAD LINE OVERRIDE                
         BZ    HEAD1                                                            
         ZIC   RE,GLFHEADL         YES - ADJUST R5 TO NEW A(OUTPUT)             
         SR    R1,RE                                                            
         BNP   HEAD1                                                            
         M     R0,=F'198'                                                       
         AR    R5,R1                                                            
*                                                                               
HEAD1    ST    R5,AHEAD            SAVE A(OUTPUT)                               
         CLI   DRHDELEN,56         DO WE NEED TO HANDLE A LITERAL               
         BE    HEAD2                                                            
         ZIC   R1,DRHDELEN         YES                                          
         SH    R1,=H'56'                                                        
         STC   R1,LITLEN           PASS ITS LENGTH                              
         LA    R1,DRHDLIT                                                       
         ST    R1,ALITIN           ITS ADDRESS                                  
         ST    R5,ALITOUT          AND WHERE IT GOES                            
         BAS   RE,DOLIT                                                         
         L     R5,ALITOUT                                                       
         LA    R5,1(R5)                                                         
         SPACE 1                                                                
HEAD2    ST    R5,GLAOFLD                                                       
         XC    GLAIFLD,GLAIFLD                                                  
         L     R1,LAST30                                                        
         USING DROD,R1                                                          
         L     R1,DROIADD          PICK UP A(IN) IF ANY                         
         LTR   R1,R1                                                            
         BZ    HEAD3                                                            
         USING DRIND,R1                                                         
         LH    R1,DRINDISP                                                      
         DROP  R1                                                               
         A     R1,AACTREC                                                       
         ST    R1,GLAIFLD          RECORD A(INPUT FIELD IN RECORD)              
         SPACE 1                                                                
HEAD3    OC    DRHDRADD,DRHDRADD   IS THERE A USER ROUTINE FOR THIS             
         BZ    HEAD4                                                            
         MVI   GLHOOK,GLROUT       YES SO SET UP FOR A HOOK                     
         MVC   GLLABEL,DRHDROUT                                                 
         MVC   GLAROUT,DRHDRADD                                                 
         MVC   GLARGS,DRHDARGS                                                  
         BAS   RE,GOHOOK                                                        
         ZIC   R1,SAVOLEN          TEST SECOND HEAD LINE FORMATTED              
         BCTR  R1,0                                                             
         L     R5,GLAOFLD                                                       
         EX    R1,CHK2LNS                                                       
         BNH   HEAD4                                                            
         MVI   LASTNHED,2          YES-INDICATE AT LEAST 2 LINES                
         SPACE 1                                                                
*                                  AUTO CHUNK ROUTINES                          
HEAD4    CLI   DRHDLINE,1          ONLY ON THE FIRST HEADING LINE               
         BNE   HEAD8                                                            
         CLI   GLAUTOCH,C'N'       USER CAN TURN OFF AUTOCHUNK                  
         BE    HEAD8                                                            
         L     R5,AHEAD                                                         
         CLI   SAVOLEN,0                                                        
         BE    HEAD5                                                            
         CLI   SAVOLEN,198                                                      
         BH    HEAD5                                                            
         ZIC   R1,SAVOLEN                                                       
         BCTR  R1,0                                                             
         CLC   LASTHEAD,SPACES                                                  
         BE    HEAD6                                                            
         CLI   LASTNHED,1          CHUNK ONLY OK IF AT LEAST 2 LINES            
         BE    HEAD6                                                            
         EX    R1,CHKAUTCH         CHECK IF AUTO CHUNK IS ON                    
         BNE   HEAD6                                                            
         L     RE,ALASTBXC         YES - SO PICK UP ADDRESS LAST 'C'            
         MVI   0(RE),C' '                AND CREAM IT                           
         L     R4,ALASTH1          R4=A(BEGINNING OF CHUNK)                     
         SR    R5,R4                                                            
         LA    R5,1(R1,R5)         R5=LENGTH                                    
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),SPACES      CLEAR COMBINED SPACE                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),LASTHEAD    MOVE IN JOINT NAME TO LEFT                   
*                                  CENTER THIS INTO COMMUNAL SPACE              
         GOTO1 CENTER,DMCB,(R4),(R5)                                            
         B     HOOKEND                                                          
         SPACE 1                                                                
HEAD5    MVC   LASTHEAD,SPACES     SOMETHING WRONG                              
         XC    CHUNKADD,CHUNKADD   CLEAN UP                                     
         B     HEAD8                                                            
         SPACE 1                                                                
HEAD6    MVC   LASTHEAD,SPACES                                                  
         CLI   LASTNHED,1          CHUNK ONLY OK IF AT LEAST 2 LINES            
         BE    HEAD7                                                            
         EX    R1,SAVEHEAD         SAVE THIS HEADING FOR FUTURE                 
*                                  TEST FOR AUTO CHUNK                          
HEAD7    ST    R5,ALASTH1          ALSO SAVE ITS ADDRESS                        
         SPACE 1                                                                
HEAD8    CLI   DRHDALIN,C'L'       IF LEFT ALIGN, WE'RE DONE                    
         BE    HOOKEND                                                          
         L     R1,AHEAD                                                         
         ZIC   R0,DRHDWDTH                                                      
         LTR   R0,R0                                                            
         BZ    HOOKEND                                                          
         LR    RE,R0                                                            
         BCTR  RE,0                                                             
         SPACE 1                                                                
HOOK10   CLI   0(R1),C' '          LEFT ALIGN FIRST                             
         BNE   HOOK12                                                           
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R1),1(R1)                                                    
         BCT   R0,HOOK10                                                        
         SPACE 1                                                                
HOOK12   L     RF,CENTER           DEFAULT IS TO CENTER                         
         CLI   DRHDALIN,C'R'       OPTION TO RIGHT ALIGN                        
         BNE   *+8                                                              
         L     RF,RIGHT                                                         
         MVC   DMCB(4),AHEAD       PASS PRINT POSTION                           
         XC    DMCB+4(4),DMCB+4                                                 
         MVC   DMCB+7(1),DRHDWDTH  AND WIDTH                                    
         GOTO1 (RF),DMCB                                                        
         B     HOOKEND                                                          
         SPACE 2                                                                
CHKAUTCH CLC   0(0,R5),LASTHEAD    ** EXECUTED                                  
SAVEHEAD MVC   LASTHEAD(0),0(R5)                                                
CHK2LNS  CLC   198(0,R5),SPACES                                                 
         EJECT                                                                  
*              DEAL WITH CHUNK ELEMENTS                                         
         SPACE 3                                                                
CHUNK    CLI   0(R3),X'42'                                                      
         BNE   HOOKEND                                                          
         USING DRCHD,R3                                                         
         CLI   OUTYN,C'N'          IGNORE IF PREVIOUS OUT FAILED                
         BE    HOOKEND                                                          
         GOTO1 =V(DRIVTEST),DMCB,(RA),DRCHIFS                                   
         BNE   HOOKEND                                                          
         SPACE 1                                                                
         ST    R3,GLADTENT                                                      
         L     R5,DRCHAO1          R5=A(OUTPUT)                                 
         SR    R1,R1                                                            
         ICM   R1,1,GLFHLOVR       TEST FIRST HEAD LINE OVERRIDE                
         BZ    CHUNK1                                                           
         ZIC   RE,GLFHEADL         YES - ADJUST R5 TO NEW A(OUTPUT)             
         SR    R1,RE                                                            
         BNP   CHUNK1                                                           
         M     R0,=F'198'                                                       
         AR    R5,R1                                                            
*                                                                               
CHUNK1   ST    R5,ACHUNK           SAVE A(OUTPUT)                               
         CLI   DRCHELEN,72         DO WE NEED TO HANDLE A LITERAL               
         BE    CHUNK2                                                           
         ZIC   R1,DRCHELEN         YES                                          
         SH    R1,=H'72'                                                        
         STC   R1,LITLEN           PASS ITS LENGTH                              
         LA    R1,DRCHLIT                                                       
         ST    R1,ALITIN           ITS ADDRESS                                  
         ST    R5,ALITOUT          AND WHERE IT GOES                            
         BAS   RE,DOLIT                                                         
         L     R5,ALITOUT                                                       
         LA    R5,1(R5)                                                         
         SPACE 1                                                                
CHUNK2   ST    R5,GLAOFLD                                                       
         XC    GLAIFLD,GLAIFLD                                                  
         OC    DRCHRADD,DRCHRADD   IS THERE A USER ROUTINE FOR THIS             
         BZ    CHUNK4                                                           
         MVI   GLHOOK,GLROUT       YES SO SET UP FOR A HOOK                     
         MVC   GLLABEL,DRCHROUT                                                 
         MVC   GLAROUT,DRCHRADD                                                 
         MVC   GLARGS,DRCHARGS                                                  
         BAS   RE,GOHOOK                                                        
         SPACE 1                                                                
CHUNK4   L     R4,ACHUNK           CENTER EXPRESSION                            
         LH    R5,DRCHLEN                                                       
         GOTO1 CENTER,DMCB,(R4),(R5)                                            
         EJECT                                                                  
*              FINALLY MERGE INTO USER'S HEAD LINES                             
         SPACE 3                                                                
HOOKEND  ZIC   R1,1(R3)                                                         
         AR    R3,R1                                                            
         CLI   0(R3),X'00'                                                      
         BE    HOOKEND2                                                         
         CLI   0(R3),X'10'                                                      
         BNE   HOUT12                                                           
         SPACE 1                                                                
HOOKEND2 MVI   0(R6),C'R'          PUT IN RIGHT HAND SIDE OF BOX                
         L     R2,AMYH1                                                         
         L     R3,GLAH1                                                         
         ZIC   R0,GLNHEAD                                                       
         L     R1,WIDTH                                                         
         SPACE 1                                                                
HOOKEND4 BAS   RE,HMERGE           MERGE IN HEAD LINES                          
         LA    R2,198(R2)                                                       
         A     R3,WIDTH                                                         
         BCT   R0,HOOKEND4                                                      
         SPACE 1                                                                
         MVI   GLHOOK,GLHEAD                                                    
         MVC   GLADTENT,GLAFOUT                                                 
         XC    GLAROUT,GLAROUT                                                  
         MVI   GLAROUT,1                                                        
         BAS   RE,GOHOOK           FINALLY HOOK TO APPLICATION                  
         MVI   GLAROUT,2                                                        
         BAS   RE,GOHOOK           AND SYSTEM CONTROLLER                        
         BAS   RE,SETLAST          SET BOX M FROM LAST HEADLINE                 
         B     HXIT                                                             
         SPACE 1                                                                
HMERGE   NTR1                                                                   
         SPACE 1                                                                
HMERGE2  DS    0H                  MERGE IN SIGNIFICANT DATA                    
         CLI   0(R2),X'20'         CHECK  FOR DICTATE ESCAPE SEQUENCES          
         BL    HMERGE3                                                          
         CLI   0(R2),X'2D'                                                      
         BH    HMERGE3                                                          
         LA    RF,4                SET DEFAULT ESCAPE SEQUENCE LENGTH           
         CLI   0(R2),X'20'         CHECK FOR EXCEPTIONS                         
         BNE   *+8                                                              
         LA    RF,3                                                             
         CLI   0(R2),X'21'                                                      
         BNE   *+8                                                              
         LA    RF,3                                                             
         CLI   0(R2),X'26'                                                      
         BNE   *+8                                                              
         LA    RF,1                                                             
         CLI   0(R2),X'28'                                                      
         BNE   *+8                                                              
         LA    RF,3                                                             
         CLI   0(R2),X'29'                                                      
         BNE   *+8                                                              
         LA    RF,3                                                             
         BCTR  RF,0                DECREMENT FOR EXECUTE                        
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),0(R2)       MOVE ESCAPE SEQUENCE TO OUTPUT               
         LA    R2,1(RF,R2)         POINT TO NEXT INPUT  BYTE                    
         LA    R3,1(RF,R3)         POINT TO NEXT OUTPUT BYTE                    
         SR    R1,RF               DECREMENT INPUT COUNTER                      
         B     HMERGE4                                                          
*                                                                               
HMERGE3  CLI   0(R2),X'41'                                                      
         BL    *+10                                                             
         MVC   0(1,R3),0(R2)                                                    
         LA    R2,1(R2)                                                         
         LA    R3,1(R3)                                                         
HMERGE4  BCT   R1,HMERGE2                                                       
         B     HXIT                                                             
         EJECT                                                                  
*              SET MIDDLE BOX COMMAND FROM LAST USED HEAD LINE                  
         SPACE 3                                                                
SETLAST  NTR1                                                                   
         L     R2,GLAH1            POSITION R2 TO LAST USER HEAD LINE           
         ZIC   R1,GLNHEAD                                                       
         L     R6,ABOX                                                          
         USING BOXD,R6                                                          
         LA    R3,BOXROWS(R1)      R3=LAST+1 IN BOXROWS                         
         DROP  R6                                                               
         BCTR  R1,0                                                             
         M     R0,WIDTH                                                         
         AR    R2,R1                                                            
         L     R4,WIDTH            R4=WIDTH-1                                   
         BCTR  R4,0                                                             
         SPACE 1                                                                
SETLAST2 EX    R4,SETLEX                                                        
         BNE   SETLAST4                                                         
         SR    R2,R4                                                            
         BCTR  R2,0                                                             
         BCT   R3,SETLAST2                                                      
         DC    H'0'                                                             
         SPACE 1                                                                
SETLAST4 MVI   0(R3),C'M'          PUT AN M INTO THE ROWS                       
         CLI   NUMMIDS,0                                                        
         BE    HXIT                                                             
         MVI   0(R3),C'B'          IF MID LINES NEED TO PRINT                   
         ZIC   R1,NUMMIDS          END BOX BEFORE MIDLINES                      
         LA    R3,1(R1,R3)                                                      
         MVI   0(R3),C'T'          AND START A NEW BOX AFTER                    
         B     HXIT                                                             
         SPACE 1                                                                
SETLEX   CLC   0(0,R2),SPACES                                                   
         SPACE 1                                                                
HXIT     XIT1                                                                   
         EJECT                                                                  
*              LTORG AND BUFF FOR DRIVOUT                                       
         SPACE 3                                                                
         LTORG                                                                  
         SPACE 1                                                                
         BUFF  LINES=500,COMMENT=92,FLAVOR=DATA,KEYLIST=(8,A)                   
         SPACE 1                                                                
TEMPBUFF DC    F'0'                                                             
         DC    F'50000'                                                         
         DC    50000X'00'                                                       
         SPACE 1                                                                
TEMPREC  DC    4096X'00'                                                        
         EJECT                                                                  
*              DSECT FOR OUT STORAGE                                            
         SPACE 3                                                                
DOUTD    DSECT                                                                  
ADOUT2   DS    F                                                  L01           
CDUB     DS    PL8                                                              
PFILL    DS    CL1                                                              
P        DS    CL132                                                            
P2       DS    CL132                                                            
P3       DS    CL132                                                            
SPACES   DS    CL198                                                            
SAVEDET  DS    CL198                                                            
SAVEP2   DS    CL198                                                            
SAVEP3   DS    CL198                                               L01          
SAVEP4   DS    CL198                                               L01          
LASTPDET DS    CL198                                                            
LASTP2   DS    CL198                                                            
LASTP3   DS    CL198                                               L01          
LASTP4   DS    CL198                                               L01          
LASTMID  DS    CL198                                                            
SAVEBXCL DS    CL198                                                            
SAVENRBX DS    CL198                                                            
AFCBOX   DS    F                                                                
HORIZSW  DS    C                   (Y=HORIZONTAL LINE PENDING)                  
LASTNHED DS    XL1                                                              
CBLEV    DS    XL1                                                              
THISLEV  DS    XL1                                                              
NUMMIDS  DS    XL1                                                              
TOPSW    DS    CL1                                                              
ALEVOUT  DS    A                                                                
ATHISREC DS    A                                                                
AHEAD    DS    A                                                                
ACHUNK   DS    A                                                                
ALASTFLD DS    A                                                                
BREAK    DS    CL1                                                              
ELCODE   DS    CL1                                                              
CONTSW   DS    CL1                                                              
MIDCONT  DS    CL1                                                              
LASTLTYP DS    CL1                                                              
TOTCNTS  DS    XL50                                                             
TOTPEND  DS    CL1                                                              
DONTSW   DS    CL1                                                              
AUTALLSW DS    CL1                                                              
ANYFOLD  DS    CL1                                                              
         SPACE 1                                                                
LITLEN   DS    CL1                 PARAMETERS FOR DOLIT                         
ALITIN   DS    A                                                                
ALITOUT  DS    A                                                                
NLINES   DS    XL1                                                              
LINETYPE DS    CL1                                                              
LASTHEAD DS    CL198                                                            
CHUNKADD DS    A                                                                
OUTYN    DS    CL1                                                              
SAVOLEN  DS    CL1                                                              
ALASTBXC DS    A                                                                
ALASTH1  DS    A                                                                
         SPACE 1                                                                
*                                  RANKING SUPPORT                              
ANYRANK  DS    CL1                                                              
LASTVAL  DS    PL8                                                              
LAST30   DS    A                                                                
         SPACE 1                                                                
DETTOTS  DS    XL16                                                             
         SPACE 1                                                                
*                                  BLOCK FOR EDITOR                             
       ++INCLUDE DDEBLOCK                                                       
         SPACE 1                                                                
DLCOUNT  DS    XL1                 SUPPORT FOR DOWNLOAD                         
DLPADD   DS    A                                                                
DLIADD   DS    A                                                                
DLLEN    DS    CL1                                                              
DLACTLEN DS    CL1                                                              
DLOLEN   DS    CL1                                                              
DLPEND   DS    CL1                                                              
DHLINE   DS    XL1                                                              
DLLINE   DS    XL1                                                              
DLBORROW DS    CL60                                                             
DLFILLER DS    CL8                                                              
DLOUTPUT DS    4CL198                                                           
DLPOOL   DS    (DLMAXLIN*80)C                                                   
DLMAXLIN EQU   10                                                               
         SPACE 1                                                                
DOUT2    DS    0D                  THIS AREA NOT ADDRESSABLE                    
*                                  USE ANOTHER BASE REGISTER                    
*                                  BUFFALO AREAS                                
ABVAL    DS    F                                                                
ABCOUNT  DS    F                                                                
ABDATA   DS    992C                                                             
BBVAL    DS    F                                                                
BBCOUNT  DS    F                                                                
BBDATA   DS    992C                                                             
         SPACE 1                                                                
DLHDPOOL DS    30CL40                                                           
DLHDPOLX EQU   *                                                                
         SPACE 1                                                                
DOUTDEND EQU   *                                                                
         SPACE 1                                                                
       ++INCLUDE DRGLOBAL                                                       
       ++INCLUDE DRLOCAL                                                        
       ++INCLUDE DRINTRECD2                                                     
       ++INCLUDE DRIVETABLE                                                     
         PRINT OFF                                                              
       ++INCLUDE DDBIGBOX                                                       
       ++INCLUDE DDBUFFALOD                                                     
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'065DRIVOUT   05/01/02'                                      
         END                                                                    
