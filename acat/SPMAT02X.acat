*          DATA SET SPMAT02X   AT LEVEL 095 AS OF 05/01/02                      
*CATALP SPMAT02                                                                 
         TITLE 'T21502 - ICS MATCH OVLY 2 - ICSMATCH, ICSPOST'                  
         PRINT NOGEN                                                            
T21502   CSECT                                                                  
         NMOD1 0,T21502                                                         
         SPACE 2                                                                
         L     RC,0(R1)                                                         
         USING GENOLD,RC                                                        
         USING T215FFD,RA                                                       
*                                                                               
         RELOC RELO02                                                           
*                                                                               
         L     RE,=V(ICSADDEL)                                                  
         A     RE,RELO02                                                        
         ST    RE,VADDEL                                                        
*                                  DO MATCHING                                  
         SPACE 2                                                                
         GOTO1 =A(ICSMATCH),DMCB,(RC),RR=RELO02                                 
         CLI   PSTOPT,C'N'                                                      
         BE   STORBUF                                                           
         GOTO1 =A(ICSPOST),DMCB,(RC),RR=RELO02                                  
         CLI   ERRAREA,0                                                        
         BNE   EXT                                                              
         SPACE 2                                                                
*                                  WRITE BUFFERS TO TEMPSTR                     
STORBUF  MVI   BUFFSW,1            SET BUFFER SW                                
         LA    R3,1                                                             
         L     R4,ABUFFER                                                       
STORBUF2 DS    0H                                                               
         BAS   RE,WRTEMP                                                        
         ST    R3,NPAGES           SET NO. OF PAGES                             
         LA    R3,1(R3)                                                         
         AH    R4,TWPAGSIZ                                                      
         C     R4,ALBY                                                          
         BH    EXT                                                              
         B     STORBUF2                                                         
         SPACE 2                                                                
*                   DATA MANAGER INTERFACE FOR TEMPSTR WRITE                    
         SPACE 2                                                                
WRTEMP   ST    RE,FULL                                                          
         XC    DMCB+8(2),DMCB+8                                                 
         L     RF,VTWA                                                          
         MVC   DMCB+10(2),2(RF)    2 BYTE TERMINAL NUMBER                       
         STC   R3,DMCB+8           PAGE                                         
         MVC   DMCB+20(2),=C'L='    SET NEW LENGTH IN PARM6                     
         MVC   DMCB+22(2),TWPAGSIZ                                              
         GOTO1 VDATAMGR,DMCB,=C'DMWRT',=C'TEMPSTR',,(R4),(TERMNAL,0)            
         SPACE 2                                                                
         L     RE,FULL                                                          
         TM    DMCB+8,X'FD'                                                     
         BCR   8,RE                                                             
         DC    H'0'                TEMPSTR WRITE ERROR                          
*                                                                               
EXT      DS    0H                                                               
         XIT1                                                                   
         SPACE 2                                                                
         LTORG                                                                  
         TITLE 'ICSMATCH - ICS MATCHING MODULE'                                 
ICSMATCH CSECT                                                                  
         NMOD1 0,ICSMATCH                                                       
         SPACE 2                                                                
         L     RC,0(R1)                                                         
         USING GENOLD,RC                                                        
         USING BYELEMD,R7                                                       
         USING IELEMD,R8                                                        
         USING T215FFD,RA                                                       
*                                                                               
         BAS   RE,CHKINT       INTERVAL CHECKING - ALSO MAY SORT INV            
         SPACE 3                                                                
         L     R8,AFINV                                                         
MM2      C     R8,ALINV                                                         
         BH    MMEXT                                                            
         L     R7,AFBY                                                          
         MVI   OLDMFLD,X'FF'                                                    
         MVI   RMLST,X'FF'                                                      
         MVC   RMLST+1(L'RMLST-1),RMLST                                         
MM2A     C     R7,ALBY                                                          
         BH    MM3A                                                             
         MVI   RMCALL,C'N'         SET NOT A REMATCH CALL                       
         BAS   RE,MATCH                                                         
         TM    MSW,X'C0'                                                        
         BM    MM7                 GOOD MATCH                                   
         BO    MM8                 BUY ALREADY MATCHED                          
MM3      LA    R7,L'BYELEM(R7)                                                  
         B     MM2A                                                             
MM3A     CLI   OLDMFLD,X'FF'                                                    
         BE    MM5                                                              
*                                  GOOD MATCH                                   
MM3C     DS    0H                                                               
         MVC   IBY,OLDMBY      SET BUY REF (NOTE-ONLY 2 BYTES USED)             
         L     R7,AFBY                                                          
         MVC   HALF,OLDMBY                                                      
         AH    R7,HALF                                                          
         BCTR  R7,R0                                                            
         SR    R0,R0                                                            
         IC    R0,BYUNACH                                                       
         BCTR  R0,R0                                                            
         STC   R0,BYUNACH          POST NEW UNACHIEVED COUNTER                  
*                                  BUMP TO NEW INVOICE                          
MM4      LA    R8,IEL$MLEN(R8)                                                  
         B     MM2                                                              
*                                  REMATCHING                                   
MM5      LA    R3,RMLST                                                         
         LA    R4,L'MFLD                                                        
         LA    R5,RMLSTX                                                        
MM6      CLI   0(R3),X'FF'                                                      
         BE    MM4                                                              
         BAS   RE,RMTCH                                                         
         TM    RMSW,X'80'          TEST HAVE REMATCHED                          
         BZ    MM6A                                                             
         MVC   IBY,6(R3)           SET NEW BUY REFERENCE                        
         B     MM4                                                              
MM6A     BXLE  R3,R4,MM6                                                        
         B     MM4                                                              
*                                                                               
MM7      DS    0H                  KEEP BEST MFLD                               
         TM    MSW,X'20'           TEST FORCED TO ACCEPT THIS ONE               
         BNZ   MM7B                                                             
         CLC   OLDMFLD,MFLD                                                     
         BNH   MM3                                                              
         MVC   OLDMFLD,MFLD                                                     
         B     MM3                                                              
*                                                                               
MM7B     DS    0H                  FORCED TO ACCEPT                             
         MVC   OLDMFLD,MFLD        SET MATCH DATA                               
         B     MM3C                AND STOP SEARCH                              
*                                                                               
*                                  BUILD LIST OF ALREADY MATCHED                
*                                  'POTENTIAL' MATCHES                          
MM8      CLI   OLDMFLD,X'FF'                                                    
         BNE   MM3                 HAVE GOOD MATCH                              
         LA    R3,RMLST                                                         
         LA    R4,L'MFLD                                                        
         LA    R5,RMLSTX                                                        
*                                  KEEP LIST IN 'GOOD MATCH' ORDER              
MM9      CLC   MFLD,0(R3)                                                       
         BL    MM10                                                             
         BXLE  R3,R4,MM9                                                        
         B     MM3                                                              
MM10     SR    R5,R3                                                            
         EX    R5,MMVC1            MOVE END OF LIST TO WORK                     
         SR    R5,R4                                                            
         BNP   MM11                                                             
         EX    R5,MMVC2            MOVE WORK BACK IN                            
MM11     MVC   0(L'MFLD,R3),MFLD                                                
         B     MM3                                                              
         SPACE 2                                                                
MMEXT    XIT1                                                                   
MMVC1    MVC   X(0),0(R3)                                                       
MMVC2    MVC   L'MFLD(0,R3),X                                                   
         EJECT                                                                  
*                   MATCH ROUTINE                                               
         SPACE 3                                                                
MATCH    NTR                                                                    
         SPACE 2                                                                
         XC    MFLD,MFLD                                                        
         CLC   IDAT,BYSDT                                                       
         BL    MTEXT                                                            
         CLC   IDAT,BYEDT                                                       
         BH    MTEXT                                                            
*                                                                               
         CLI   TRAFRCLO,C'Y'       ARE RECALL/RELEASE ERRS DISCRPEANT?          
         BNE   *+12                                                             
         TM    ISTAT2,ISTRCRL      IS THIS ONE IN ERROR?                        
         BNZ   MTEXT               YES, REJECT                                  
*                                                                               
         CLC   =C'CK',AGYALPHA     ONLY FOR COKE RIGHT NOW                      
         BNE   MT01                                                             
         CLI   TRAFRCLO,C'Y'       RECALL PROFILE ALSO FOR PRD CHECK            
         BNE   *+12                NO                                           
         TM    ISTAT,ISTPRDF       YES, ALSO TEST FILM PRD ERROR?               
         BNZ   MTEXT                                                            
*                                                                               
MT01     CLI   TRAFIDSC,C'Y'       ARE INVALID FILMS DISCREPANT?                
         BNE   MT01G                                                            
*                                                                               
         CLC   =C'JW',AGYALPHA     FOR JW ONLY                                  
         BNE   MT01D                                                            
         CLI   ICSSTA+4,C'/'         LOCAL CABLE                                
         BE    MT01G               YES THEN DON'T MATCH FILMS                   
*                                                                               
MT01D    OC    TRAF9YR(2),TRAF9YR  EFFECTIVE DATE FOR FILM MATCH?               
         BZ    MT01F                                                            
         GOTO1 VDATCON,DMCB,(2,BEND),(3,WORK)                                   
         CLC   WORK(2),TRAF9YR     IS MONTH OF REQ BEFORE EFF DATE              
         BL    MT01G               YES THEN DON'T MATCH FILMS                   
*                                                                               
MT01F    TM    ISTAT,ISTINVF       YES, IS THIS ONE INVALID?                    
         BNZ   MTEXT               YES, REJECT                                  
         CLI   IFILM,0             OR MISSING?                                  
         BE    MTEXT               YES, REJECT                                  
*                                                                               
MT01G    DS    0H                                                               
         CLC   ILEN,BYLEN                                                       
         BNE   MTEXT                                                            
         CLI   BYPRD,X'FF'         NO MATCH FOR UNALLOCATED                     
         BE    MTEXT                                                            
*                                                                               
         CLI   IEST,0              IF HAVE ESTIMATE                             
         BE    MT01H                                                            
         CLC   IEST,BYEST          IT MUST MATCH                                
         BNE   MTEXT                                                            
         B     MT01J                                                            
*                                                                               
MT01H    DS    0H                  NO EST ON INV ITEM                           
         CLI   I2XPROF+2,C'Y'      TEST TO REJECT                               
         BNE   MT01J               NO                                           
         CLI   BEST,0              YES, SKIP IF NOT EST=NO                      
         BNE   MTEXT                                                            
*                                                                               
MT01J    DS    0H                                                               
         CLI   IPRD,X'FF'          POL INV SPOT                                 
         BE    MT2A                                                             
         CLC   IPRD(2),BYPRD                                                    
         BE    MT2A                                                             
         MVC   WORK(1),BYPRD2                                                   
         MVC   WORK+1(1),BYPRD                                                  
         CLC   IPRD(2),WORK        TEST REVERSE ORDER                           
         BNE   MTEXT                                                            
MT2A     DS    0H                                                               
         TM    ISTAT,X'04'         INVOICE MG                                   
         BZ    MT2AD                                                            
         TM    BYSTAT,X'04'        MUST MATCH BUY MG                            
         BZ    MTEXT                                                            
         B     MT2AH                                                            
*                                                                               
MT2AD    DS    0H                  INVOICE IS NOT A MG                          
         TM    BYSTAT,X'04'        IF BUY IS A MG                               
         BZ    MT2AH                                                            
         OI    MTIM,X'02'          GIVE MATCH A BAD SCORE                       
         CLI   I2XPROF+6,C'Y'      TEST OPTION TO NOT MATCH                     
         BE    MTEXT               YES- NO MATCH                                
*                                                                               
MT2AH    DS    0H                                                               
         TM    ISTAT2,ISTCTRQ      IF INVOICE IS CONTRACT TRADE                 
         BZ    MT2AJ                                                            
         TM    BYSTAT,X'08'        BUY MUST BE TOO                              
         BZ    MTEXT                                                            
         B     MT2AL                                                            
*                                                                               
MT2AJ    DS    0H                  INVOICE NOT CONTRACT TRADE                   
         TM    BYSTAT,X'08'        IF BUY IS                                    
         BNZ   MTEXT               NO MATCH                                     
*                                                                               
MT2AL    DS    0H                                                               
         CLI   I2XPROF+3,C'Y'      OPTION TO SKIP MATCH ON COST                 
         BE    MT2A2                                                            
         TM    BYSTAT,X'20'        TEST NTP/SYND BUY                            
         BZ    *+14                NO                                           
         OC    ICOST,ICOST         YES - MATCH ONLY TO ZERO BUY                 
         BZ    MT2A2                                                            
         TM    ISTAT,X'80'                                                      
         BNZ   MT2A2                BYPASS MATCH ON COST                        
         CLI   PSEUDOPT,C'N'        AND FOR RESPONSE/MCT REQS                   
         BNE   MT2A2                BYPASS MATCH ON COST                        
         MVC   FULL(1),ISTAT       NEG STATUS MUST BE SAME                      
         XC    FULL(1),BYSTAT                                                   
         TM    FULL,X'01'          NEG STATUS MUST MATCH                        
         BNZ   MTEXT                                                            
         CLC   ICOST,BYCOST        **NOTE-BYTE 0 OF COST NOT USED               
         BNE   MTEXT               **(ITS NEEDED ONLY FOR NETPAK)               
MT2A2    DS    0H                                                               
         IC    R5,BYDAY                                                         
         STC   R5,*+5                                                           
         TM    IDAY,0                                                           
         BZ    MTEXT                                                            
         MVC   FULL,BYSTIM         START AND END TIMES                          
*                                  START OF 600A = 3000                         
         CLC   FULL(2),=H'360'                                                  
         BNE   *+10                                                             
         MVC   FULL(2),=H'1800'                                                 
*                                                                               
         MVC   HALF,ITIM                                                        
         CLC   ITIM,FULL           START TIME                                   
         BNL   MT3                                                              
         LH    R5,FULL                                                          
         SH    R5,TIMOPT1          LEEWAY FOR BEFORE                            
         CH    R5,HALF                                                          
         BH    MT2A6                                                            
         OI    MTIM,1                                                           
         B     MT3                                                              
*                                                                               
MT2A6    DS    0H                  BEFORE STAÂ›T                                 
         CLC   FULL(2),FULL+2      START VS END - IF HIGH TIMES SPAN 6A         
         BNH   MT3D                MISS ON TIME                                 
         B     MT3A                YES - TRY END TIME                           
MT3      DS    0H                                                               
         CLC   FULL(2),FULL+2      START VS END - IF HIGH TIMES SPAN 6A         
         BH    MT3F                INVOICE MUST BE EITHER AFTER                 
*                                  START OR BEFORE END                          
MT3A     DS    0H                                                               
         CLC   ITIM,FULL+2         END TIME                                     
         BNH   MT3F                                                             
         LH    R5,FULL+2                                                        
         AH    R5,TIMOPT2          LEEWAY FOR AFTER                             
         CH    R5,HALF                                                          
         BL    MT3D                MISS ON TIME                                 
         OI    MTIM,1                                                           
         B     MT3F                                                             
*                                                                               
MT3D     DS    0H                  MIS-MATCH ON TIME                            
         TM    ISTAT,X'02'         TEST TO IGNORE TIME                          
         BZ    MTEXT               NO- NO MATCH                                 
         OI    MTIM,X'02'          YES- ALLOW MATCH BUT GIVE BAD SCORE          
*                                                                               
MT3F     DS    0H                  FILM MATCHING                                
         CLI   TRAFMBF,C'Y'        MATCH BY FILM                                
         BE    *+12                                                             
         CLI   TRAFMBF,C'B'                                                     
         BNE   MT3M                                                             
*                                                                               
         OC    TRAF9YR(2),TRAF9YR  EFFECTIVE DATE FOR FILM MATCH?               
         BZ    MT3G                                                             
         GOTO1 VDATCON,DMCB,(2,BEND),(3,WORK)                                   
         CLC   WORK(2),TRAF9YR     IS MONTH OF REQ BEFORE EFF DATE              
         BL    MT3M                YES THEN DON'T MATCH FILMS                   
*                                                                               
MT3G     CLC   =C'JW',AGYALPHA     FOR JW ONLY                                  
         BNE   MT3L                                                             
         CLI   ICSSTA+4,C'/'       LOCAL CABLE                                  
         BE    MT3M                YES THEN DON'T MATCH FILMS                   
*                                                                               
MT3L     DS    0H                  FILM MATCHING                                
         BAS   RE,MMFFILM1                                                      
         BNE   MTEXT                                                            
         BAS   RE,MMFFILM2                                                      
         BNE   MTEXT                                                            
*                                                                               
MT3M     CLI   BYCBLNET,0          IF HAVE BUY CABLE NET                        
         BE    MT3P                                                             
         CLI   ICBLNET,0           AND INVOICE                                  
         BE    MT3P                                                             
         CLC   BYCBLNET,ICBLNET    THEY MUST AGREE                              
         BNE   MTEXT                                                            
*                                                                               
MT3P     DS    0H                                                               
*                                                                               
MT4      DS    0H                  HAVE MATCH                                   
         MVC   MAMPW,BYAMPW                                                     
         MVC   MSTIM,BYSTIM                                                     
         SR    R5,R5                                                            
         IC    R5,BYUNACH                                                       
         LCR   R5,R5                                                            
         STC   R5,MUNACH           COMPLEMENT OF UNACHIEVED                     
         OI    MSW,X'80'                                                        
         LTR   R5,R5                                                            
         BNE   MT5                                                              
         OI    MSW,X'40'           BUY ALREADY MATCHED                          
MT5      S     R7,AFBY                                                          
         LA    R7,1(R7)                                                         
         STC   R7,MBY+1            RELATIVE ADDRESS OF THIS SPOT                
         SRL   R7,8                                                             
         STC   R7,MBY                                                           
*                                                                               
*                                  HANDLE ILLEGAL SEPARATIONS                   
*                                  --------------------------                   
*                                                                               
         TM    ISTAT2,X'40'        TEST FATAL INTERVAL ERROR                    
         BZ    MT8                 NO- EXIT                                     
         CLI   RMCALL,C'Y'         SKIP FOR REMATCHING                          
         BE    MT7                 NO MATCH                                     
         TM    MSW,X'40'           AND FOR ALREADY MATCHED BUYS                 
         BNZ   MT7                 NO MATCH                                     
         CLI   OLDMFLD,X'FF'       DONT BOTHER IF ALREADY HAVE                  
         BNE   MT7                 GOOD MATCH                                   
*                                                                               
*                                  FIND FIRST IN INTERVAL                       
         ST    R8,FULL             AND TRY TO REJECT IT INSTEAD                 
MT6      DS    0H                                                               
         SH    R8,=Y(IEL$MLEN)                                                  
         C     R8,AFINV                                                         
         BL    MT7                 NOT FOUND- LEAVE CURRENT REJECT              
         TM    ISTAT2,X'80'        TEST THIS IS FIRST OF SET                    
         BZ    MT6                                                              
         TM    ISTAT,X'20'         TEST FORCED TO ACCEPT                        
         BNZ   MT7                 YES- LEAVE CURRENT AS REJECT                 
         TM    ISTAT2,X'40'        TEST PREV A SEP REJECT                       
         BNZ   MT7                 YES- LEAVE CURRENT AS REJECT                 
         OC    IBY,IBY             IS IT MATCHED                                
         BNZ   MT7                 YES - LEAVE CURRENT AS REJECT                
         OI    ISTAT2,X'40'        NO- MARK PREV A SEPARATION REJECT            
         L     R8,FULL             RESTORE POINTER                              
         NI    ISTAT2,X'9F'        SET OFF CURRENT FLAGS                        
         OI    MSW,X'20'           FORCE TO ACCEPT THIS MATCH                   
         B     MT8                 (BECAUSE CANNOT UNDO RESULTS)                
*                                                                               
MT7      DS    0H                                                               
         MVI   MSW,0               RESET TO NO MATCH                            
*                                                                               
MT8      DS    0H                                                               
MTEXT    XIT1                                                                   
         EJECT                                                                  
*                   REMATCH LOGIC                                               
         SPACE 3                                                                
RMTCH    NTR                                                                    
         SPACE 2                                                                
         MVI   RMSW,0                                                           
         L     R8,AFINV                                                         
         LA    R4,IEL$MLEN                                                      
         L     R5,ALINV                                                         
*                                  FIND INV FOR BUY TO BE UNMATCHED             
RM2      CLC   6(2,R3),IBY                                                      
         BE    RM4                 HAVE INV                                     
RM3      BXLE  R8,R4,RM2                                                        
         B     RMEXT                                                            
RM4      MVI   OLDMFLD,X'FF'                                                    
         L     R7,AFBY                                                          
RM5      C     R7,ALBY                                                          
         BH    RM6A                                                             
         MVI   RMCALL,C'Y'         SET CALLED FROM REMATCH                      
         BAS   RE,MATCH                                                         
         TM    MSW,X'C0'                                                        
         BM    RM7                                                              
         TM    MSW,X'01'                                                        
         BO    RM6A                                                             
RM6      LA    R7,L'BYELEM(R7)                                                  
         B     RM5                                                              
RM6A     CLI   OLDMFLD,X'FF'                                                    
         BE    RM3                 GET NEW INV                                  
*                                  ACTUAL REMATCH                               
         L     R7,AFBY                                                          
         MVC   HALF,OLDMBY                                                      
         AH    R7,HALF                                                          
         BCTR  R7,R0                                                            
         SR    R0,R0                                                            
         IC    R0,BYUNACH                                                       
         BCTR  R0,R0                                                            
         STC   R0,BYUNACH          POST NEW UNACHEIVED COUNTER                  
         MVC   IBY,OLDMBY          SET NEW BUY REFERENCE                        
         OI    RMSW,X'80'                                                       
         B     RMEXT                                                            
*                                  KEEP BEST MFLD                               
RM7      CLC   OLDMFLD,MFLD                                                     
         BNH   RM6                                                              
         MVC   OLDMFLD,MFLD                                                     
         B     RM6                                                              
RMEXT    XIT1                                                                   
         SPACE 2                                                                
*        FIND FILM INFO                                                         
         SPACE 2                                                                
MMFFILM1 DS    0H                  FIRST FILM MATCH                             
         TM    ISTAT2,X'02'        TEST TO IGNORE FILM ERRORS                   
         BNZ   MMFFYES                                                          
         LA    R3,BYFILM                                                        
         LA    R4,IFILM                                                         
         B     MMFFILM                                                          
*                                                                               
MMFFILM2 DS    0H                  2ND FILM MATCH                               
         TM    ISTAT2,X'02'        TEST TO IGNORE FILM ERRORS                   
         BNZ   MMFFYES                                                          
         LA    R3,BYFILM2                                                       
         LA    R4,IFILM2                                                        
*                                                                               
MMFFILM  DS    0H                                                               
         CLI   TRAFMBF,C'B'        TEST BUY SIDE REQUIRED                       
         BNE   MMFF2                                                            
         CLI   0(R4),0             IF HAVE INVOICE FILM                         
         BE    MMFF2                                                            
         OC    0(2,R3),0(R3)       THEN MUST HAVE BUY SIDE                      
         BZ    MMFFNO                                                           
         B     MMFF3                                                            
*                                                                               
MMFF2    DS    0H                                                               
         OC    0(2,R3),0(R3)       IF NO FILM, OK                               
         BZR   RE                                                               
         CLI   0(R4),0             ELSE MUST HAVE 2ND                           
         BE    MMFFNO                                                           
*                                                                               
MMFF3    DS    0H                                                               
         LA    RF,WORK                                                          
         USING FLMTABD,RF                                                       
         MVC   FLMIDNO,IINVID      INVOICE                                      
         MVC   FLMSEQ,0(R3)        CMML SEQ                                     
         MVC   FLMICOD,0(R4)       INTERNAL NO.                                 
         LR    R0,RE                                                            
         GOTO1 VBINSRCH,FLMTPARS,FLMTABD                                        
         LR    RE,R0                                                            
         CLI   0(R1),1                                                          
         BE    MMFFNO              FILM NOT FOUND MEANS MISMATCH                
*                                                                               
MMFFYES  CR    RE,RE                                                            
         BR    RE                                                               
MMFFNO   LTR   RE,RE                                                            
         BR    RE                                                               
         DROP  RF                                                               
         EJECT                                                                  
*        CHKINT - DO INTERVAL CHECKING                                          
         SPACE 2                                                                
CHKINT   NTR1                                                                   
         CLC   AFINV,ALINV         TEST ANY INVOICES                            
         BNL   CKIX                                                             
         MVI   CKSORTSW,0          SORT MAY HAVE TO BE RE-DONE                  
*                                  FOR DEFFERED SPOT DATE CHANGE                
*                                  PREPARE FOR SORT                             
         LA    R4,IEL$MLEN                                                      
         L     R1,ALINV                                                         
         S     R1,AFINV                                                         
         LA    R1,1(R1)                                                         
         SR    R0,R0                                                            
         DR    R0,R4               R1= NO OF ITEMS                              
         ST    R1,DMCB+4                                                        
*                                                                               
         CLI   INTCOPT,C'N'        IF NOT REALLY DOING INTERVAL CHECK           
         BE    CKI40               JUST TEST IF NEED TO SORT                    
*                                                                               
         CLI   INTCOPT,C'B'        IF CHECKING WITHIN BRAND                     
         BNE   CKI3                                                             
*                                  SORT BY PRD/NET/DATE/TIME                    
         LA    R3,IPRD-IELEM                                                    
         LA    R2,8                                                             
         B     CKI3G                                                            
*                                                                               
CKI3     DS    0H                                                               
         TM    CBLHOPT,CBLHNHAQ    IF CBH REQ AND NEW MODE AND ALL NETS         
         BNO   CKI3B                                                            
         LA    R3,ICBLNET-IELEM    SORT ON CBLNET                               
         LA    R2,6                                                             
         B     CKI3G                                                            
*                                                                               
CKI3B    DS    0H                                                               
         MVI   CKSORTSW,1          THIS SORT SHOULD BE OK                       
*                                  FOR DEFERRED SPOT DATE CHANGE TEST           
         CLI   INOLIST+INOLSTL,C' '   IF MULTIPLE INVOICES                      
         BNH   CKI4                                                             
         LA    R3,IDAT-IELEM                                                    
         LA    R2,5                                                             
*                                                                               
CKI3G    DS    0H                                                               
         GOTO1 VXSORT,DMCB,AFINV,,(R4),(R2),(R3)                                
*                                                                               
CKI4     DS    0H                  SET PRIMARY AND SECNDRY INTERVALS            
         MVC   CIPINT,=H'0'        USE HIGH VALUE                               
         SR    RF,RF               IF NOT GIVEN                                 
         ICM   RF,1,INTCPI                                                      
         BZ    *+8                                                              
         STH   RF,CIPINT                                                        
*                                                                               
         MVC   CISINT,=H'0'        SECONDARY                                    
         SR    RF,RF                                                            
         ICM   RF,1,INTCSI                                                      
         BZ    *+8                                                              
         STH   RF,CISINT                                                        
*                                                                               
         L     R8,AFINV                                                         
         B     CKI30                                                            
*                                                                               
CKI5     DS    0H                                                               
         C     R8,ALINV                                                         
         BNL   CKI40                                                            
*                                                                               
         CLI   INTCOPT,C'B'        IF WITHIN BRAND                              
         BNE   CKI6                                                             
         CLC   IPRD,CILPRD         TEST BRAND BREAK                             
         BNE   CKI30               NEW PRD- SET LAST VALUES                     
*                                                                               
CKI6     DS    0H                                                               
         CLC   ICBLNET,CILNET      TEST CABLE NETWORK BREAK                     
         BNE   CKI30               NEW ONE- SET LAST VALUES                     
         CLC   IDAT(3),CILDAT      TEST NEW DATE                                
         BE    CKI7                                                             
*                                                                               
         LH    RF,CILTIM                                                        
         SH    RF,=H'1440'         SUBTRACT 24 HRS                              
         STH   RF,CILTIM           TO HANDLE 6AM STRADDLE                       
*                                                                               
CKI7     DS    0H                                                               
         SR    RF,RF               GET TIME INTERVAL                            
         ICM   RF,3,ITIM                                                        
         SH    RF,CILTIM                                                        
*                                                                               
         CH    RF,CIPINT           TEST VS PRIMARY                              
         BH    CKI8                                                             
         OC    CIPINT,CIPINT                                                    
         BZ    CKI8                                                             
*                                                                               
         TM    ISTAT,X'20'         TEST OK TO ACCEPT                            
         BNZ   CKI8                YES                                          
         BAS   RE,CKIDAY           DAY CHECK                                    
         BE    CKI8                OK TO ACCEPT                                 
         OI    ISTAT2,X'40'        ELSE- SET PRIMARY FLAG                       
         CH    RF,CISINT           TEST VS SECONDARY                            
         BH    *+8                 (MAY BE ZERO)                                
         OI    ISTAT2,X'20'        SET SECONDARY FLAG                           
         SH    R8,=Y(IEL$MLEN)     BACK UP TO PREV INV                          
         OI    ISTAT2,X'80'        SET FIRST OF ILLEGAL SET                     
         LA    R8,IEL$MLEN(R8)                                                  
         B     CKI32               DO NOT SET LAST TIME FROM BAD SPOT           
*                                                                               
CKI8     DS    0H                                                               
         CH    RF,CISINT           TEST VS SECONDARY                            
         BH    CKI30                                                            
         OC    CISINT,CISINT                                                    
         BZ    CKI30                                                            
         BAS   RE,CKIDAY           DAY CHECK                                    
         BE    CKI30               OK TO ACCEPT                                 
         OI    ISTAT2,X'20'        SET SECONDARY FLAG                           
*                                                                               
CKI30    DS    0H                  SET LASTS                                    
         MVC   CILDAT(3),IDAT                                                   
         MVC   CILTIM,ITIM                                                      
CKI32    DS    0H                                                               
         MVC   CILPRD,IPRD                                                      
         MVC   CILNET,ICBLNET                                                   
*                                                                               
         LA    R8,IEL$MLEN(R8)                                                  
         B     CKI5                                                             
*                                                                               
CKI40    DS    0H                                                               
         CLI   CKSORTSW,1          TEST SORT OK FOR DEFERRED SPOT               
         BE    CKI42               DATE CHANGE CODE - YES, DONE                 
         CLI   DEFDCHG,C'Y'        ARE WE DOING DEFERRAL DATE CHG?              
         BNE   CKI42               NO, OK                                       
         CLI   INOLIST+INOLSTL,C' '   ELSE IF MULTIPLE INVOICES                 
         BNH   CKI42                 MUST RESORT TO GET INV ITEMS               
*                                     IN DATE/TIME ORDER                        
         LA    R4,IEL$MLEN                                                      
         L     R1,ALINV                                                         
         S     R1,AFINV                                                         
         LA    R1,1(R1)                                                         
         SR    R0,R0                                                            
         DR    R0,R4               R1= NO OF ITEMS                              
         ST    R1,DMCB+4                                                        
         LA    R3,IDAT-IELEM                                                    
         LA    R2,5                                                             
*                                                                               
         GOTO1 VXSORT,DMCB,AFINV,,(R4),(R2),(R3)                                
*                                                                               
CKI42    DS    0H                                                               
CKIX     DS    0H                                                               
         XIT1                                                                   
         SPACE 2                                                                
*        CKIDAY - IF 2 SPOTS FALL WITHIN INTERVAL-                              
*        IF ON SAME DAY, REJECT                                                 
*        IF ONE DAY APART, REJECT (TIME ADJUSTMENT MADE ABOVE)                  
*        IF MORE THAT ONE DAY APART, ACCEPT                                     
*                                                                               
CKIDAY   NTR1                      DAY CHECK                                    
         CLC   IDAT,CILDAT         IF ON SAME DATE                              
         BE    CKIDNO              REJECT                                       
*                                  ELSE ACCEPT, UNLESS EXACLTY                  
         GOTO1 VDATCON,DMCB,(2,CILDAT),DUB   ONE DAY APART                      
         GOTO1 VADDAY,DMCB,DUB,DUB,1                                            
         GOTO1 VDATCON,DMCB,DUB,(2,FULL)                                        
         CLC   FULL(2),IDAT                                                     
         BE    CKIDNO                                                           
*                                                                               
CKIDYES  CR    RE,RE               SET CC =                                     
         B     *+6                                                              
CKIDNO   LTR   RD,RD               SET CC NOT =                                 
         XIT1                                                                   
*                                                                               
         DROP  R8,R7                                                            
         SPACE 2                                                                
         LTORG                                                                  
         TITLE 'ICSPOST - DELETE AFFELEMS AND POST NEW ONES'                    
ICSPOST  CSECT                                                                  
         NMOD1 0,ICSPOST                                                        
         SPACE 2                                                                
         L     RC,0(R1)                                                         
         USING GENOLD,RC                                                        
         USING BYELEMD,R6                                                       
         USING IELEMD,R7                                                        
         USING BUYRECD,R9                                                       
         USING T215FFD,RA                                                       
         SPACE 2                                                                
*                                  INITIALIZE ADDPARS                           
         XC    ADDPARS,ADDPARS                                                  
         L     R9,AIOABUY                                                       
         ST    R9,ADDPAR1                                                       
         LA    R0,BUYDSKS          DISK ADDRESSES                               
         ST    R0,DSKPOINT                                                      
         XC    AFEL(6),AFEL                                                     
         MVI   AFCODE,X'10'                                                     
         XC    FCMEL(7),FCMEL                                                   
         MVI   FCMCOD,X'12'                                                     
         XC    RESPEL(6),RESPEL                                                 
         MVI   RESPCOD,X'17'                                                    
         LA    R0,AFEL                                                          
         ST    R0,ADDPAR2                                                       
         XC    SAVDA,SAVDA                                                      
         EJECT                                                                  
         SPACE 3                                                                
NXTREC   L     R2,DSKPOINT                                                      
         OC    0(4,R2),0(R2)                                                    
         BZ    PSEXT                                                            
         LA    R0,BUYDSKSX                                                      
         CR    R2,R0                                                            
         BNL   PSEXT                                                            
         SPACE 2                                                                
         MVC   SAVDA,0(R2)         SAVE DISK ADDRESS                            
         MVC   KEY+14(4),0(R2)                                                  
         BAS   RE,CLRIO                                                         
         BAS   RE,GETREC                                                        
         LA    R2,4(R2)                                                         
         ST    R2,DSKPOINT                                                      
*                                                                               
         TM    BDSTAT,X'80'        SKIP ANY RADIO NPW POL BUYS                  
         BNZ   NXTREC                                                           
*                                                                               
         MVI   BYTE3,0             SET REC NOT ALTERED                          
*                                                                               
         BAS   RE,PDTF             FIX SPOT DATES IF DOING                      
*                                  DEFERRAL DATE CHANGES                        
*                                  FIND BUYS FOR THIS EST-LIN                   
         L     R6,AFBY                                                          
PS10     C     R6,ALBY                                                          
         BNL   PS40                DONE WITH RECORD                             
         CLI   BYPRD,X'FF'                                                      
         BE    PS11                BYPASS UNALLOCATED                           
         CLC   BUYKEY+9(2),BYEST   MUST BE RIGHT EST/LINE                       
         BNE   PS11                                                             
         TM    CBLHOPT,CBLHNHQ     IF CABLE NET AND NEW MODE                    
         BNO   PS12                                                             
         MVC   BYTE,BUYKEY+8       MUST BE RIGHT NET                            
         NI    BYTE,X'7F'          7 BITS ONLY                                  
         CLC   BYTE,BYCBLNET                                                    
         BE    PS12                                                             
PS11     LA    R6,L'BYELEM(R6)                                                  
         B     PS10                                                             
PS12     LA    R0,1(R6)                                                         
         S     R0,AFBY                                                          
         STH   R0,HALF             HALF = RELATIVE ADDRESS OF BUY               
         MVC   ADDPAR4(2),BYSDT    DATE                                         
         MVI   ADDPAR3+1,0         SPOT REF. NO.                                
         MVI   AFLEN,0              CLEAR LENGTH                                
         SPACE 2                                                                
         SR    R0,R0                                                            
         SR    R1,R1                                                            
         IC    R0,BYUNACH                                                       
         IC    R1,BYNOWK                                                        
         SR    R1,R0                                                            
         STC   R1,BYTE2            BYTE2 = MATCHED SPOTS FOR THIS BUY           
         BNZ   PS13                GET INVOICES                                 
         SPACE 1                                                                
*                                  NO MATCHED SPOTS                             
         CLI   BUYKEY+3,X'FF'                                                   
         BE    PS18                                                             
         B     PS21B                                                            
         EJECT                                                                  
*                                  FIND INVOICES FOR THIS BUY                   
         SPACE 3                                                                
PS13     L     R7,AFINV                                                         
PS14     C     R7,ALINV                                                         
         BL    *+6                                                              
         DC    H'0'                END OF INVOICES SHOULD NOT OCCUR             
         CLC   HALF,IBY            IS MATCHING INVOICE                          
         BE    PS16                                                             
PS15     LA    R7,IEL$MLEN(R7)                                                  
         B     PS14                                                             
PS16     MVC   AFTIME,ITIM                                                      
         SR    R1,R1                                                            
         ICM   R1,3,AFTIME                                                      
         SR    R0,R0                                                            
         D     R0,=F'60'                                                        
         MH    R1,=H'100'                                                       
         AR    R0,R1                                                            
         CH    R0,=H'2400'                                                      
         BNH   *+8                                                              
         SH    R0,=H'2400'                                                      
         STCM  R0,3,AFTIME                                                      
*                                                                               
         MVC   AFDATE,IDAT          DATE                                        
*                                                                               
         CLI   PSEUDOPT,C'N'       IF RESPONSE/PSUEDO                           
         BE    *+8                                                              
         OI    AFTIME,X'40'       SAY SO IN STATUS BITS                         
*                                                                               
         MVI   AFLEN,6                                                          
         CLI   BUYKEY+3,X'FF'                                                   
         BE    PS18                                                             
PS17     IC    R1,ADDPAR3+1        NON-POOL - BUMP REF. NO.                     
         LA    R1,1(R1)                                                         
         STC   R1,ADDPAR3+1                                                     
         B     PS20                                                             
*                                  FOR POOL BYSPT = REF. NO.                    
PS18     MVC   ADDPAR3+1(1),BYSPT                                               
PS20     DS    0H                                                               
         GOTO1 VADDEL,ADDPARS                                                   
         SPACE 1                                                                
         CLI   ADDPARS,0           TEST REC ALTERED                             
         BE    *+8                 NO                                           
         MVI   BYTE3,X'FF'                                                      
         CLI   ADDPAR3+3,0                                                      
         BE    PS21A                                                            
         CLI   ADDPAR3+2,1         REC OVERFLOW                                 
         BE    *+6                                                              
         DC    H'0'                ADDEL ERROR                                  
         B     PS50D               OVERFLOW ERROR                               
*                                                                               
PS21A    DS    0H                                                               
         CLI   TRAFPST,C'Y'      OPTION TO POST FILMS                           
         BNE   PS21A8                                                           
         CLI   BUYKEY+3,X'FF'      NO FILM POSTING FOR NON-POL                  
         BNE   PS21A8                                                           
*                                                                               
         LA    R0,FCMEL            POINT TO FILM ELEM                           
         ST    R0,ADDPAR2                                                       
         MVI   FCMLEN,0            NO FILM ELEM                                 
         XC    FCMFLM1(4),FCMFLM1  CLEAR FILM CODES                             
         CLI   AFLEN,0             IF NO AFFID                                  
         BE    PS21A4                                                           
         CLI   IFILM,0             OR NO FILM CODE                              
         BE    PS21A4                                                           
         LA    R3,IFILM                                                         
         LA    R4,FCMFLM1                                                       
         BAS   RE,PSFFILM                                                       
         OC    FCMFLM1,FCMFLM1     IF EITHER FILM UNDEFINED                     
         BZ    PS21A4              DONT POST                                    
         MVI   FCMLEN,5            ONE FILM                                     
         CLI   IFILM2,0                                                         
         BE    PS21A4                                                           
         LA    R3,IFILM2                                                        
         LA    R4,FCMFLM2                                                       
         BAS   RE,PSFFILM                                                       
         OC    FCMFLM2,FCMFLM2     IF EITHER FILM UNDEFINED                     
         BZ    PS21A4              DONT POST                                    
         MVI   FCMLEN,7            TWO FILMS                                    
*                                                                               
PS21A4   DS    0H                                                               
         GOTO1 VADDEL,ADDPARS                                                   
         SPACE 1                                                                
         CLI   ADDPARS,0           TEST REC ALTERED                             
         BE    *+8                 NO                                           
         MVI   BYTE3,X'FF'                                                      
         CLI   ADDPAR3+3,0                                                      
         BE    PS21A6                                                           
         CLI   ADDPAR3+2,1         REC OVERFLOW                                 
         BE    *+6                                                              
         DC    H'0'                ADDEL ERROR                                  
         B     PS50D               OVERFLOW ERROR                               
*                                                                               
PS21A6   DS    0H                                                               
         LA    R0,AFEL             RESTORE TO AFFELEM                           
         ST    R0,ADDPAR2                                                       
*                                                                               
PS21A8   DS    0H                                                               
*                                                                               
PSRES    DS    0H                  RESPONSE COUNT CODE                          
         CLI   PSEUDOPT,C'R'       ONLY IF RESPONSE REQ                         
         BNE   PS21A9                                                           
         CLI   BUYKEY+3,X'FF'      ONLY FOR POOL                                
         BC    0,PS21A9            **TEMP NOOP**                                
*                                                                               
         LA    R0,RESPEL           POINT TO RESPONSE ELEM                       
         ST    R0,ADDPAR2                                                       
         MVI   RESPLEN,0           NO ELEM                                      
         XC    RESPDPT(4),RESPDPT  CLEAR ELEM                                   
         CLI   AFLEN,0             IF NO AFFID                                  
         BE    PSRES4                                                           
         OC    ICOST,ICOST         OR NO RESPONSE COUNT                         
         BZ    PSRES4                                                           
         MVC   RESPNUM,ICOST+2     SET RESP COUNT                               
         MVI   RESPLEN,6           ELEM LENGTH                                  
*                                                                               
PSRES4   DS    0H                                                               
         GOTO1 VADDEL,ADDPARS                                                   
         SPACE 1                                                                
         CLI   ADDPARS,0           TEST REC ALTERED                             
         BE    *+8                 NO                                           
         MVI   BYTE3,X'FF'                                                      
         CLI   ADDPAR3+3,0                                                      
         BE    PSRES6                                                           
         CLI   ADDPAR3+2,1         REC OVERFLOW                                 
         BE    *+6                                                              
         DC    H'0'                ADDEL ERROR                                  
         B     PS50D               OVERFLOW ERROR                               
*                                                                               
PSRES6   DS    0H                                                               
         LA    R0,AFEL             RESTORE TO AFFELEM                           
         ST    R0,ADDPAR2                                                       
*                                                                               
*                                                                               
PS21A9   DS    0H                                                               
         CLI   BUYKEY+3,X'FF'                                                   
         BE    PS11                IF POOL, DONE - GO GET ANOTHER BUY           
         CLC   ADDPAR3+1(1),BYTE2  ARE ALL INVOICES FOUND                       
         BL    PS15                GET ANOTHER INVOICE                          
         SPACE 2                                                                
*                                  DELETE ANY REMAINING AFFELEMS                
         SPACE 2                                                                
PS21B    CLI   BYUNACH,0                                                        
         BE    PS11                NONE                                         
         IC    R1,ADDPAR3+1        SET REF. NO. 1 PAST                          
         LA    R1,1(R1)            LAST ELEM I ADDED                            
         STC   R1,ADDPAR3+1        AND LEAVE IT THERE                           
         SR    R0,R0                                                            
         IC    R0,BYUNACH                                                       
         MVI   AFLEN,0              SET LENGTH TO ZERO                          
         MVI   FCMLEN,0                                                         
         MVI   RESPLEN,0                                                        
PS22     GOTO1 VADDEL,ADDPARS                                                   
         SPACE 1                                                                
         CLI   ADDPARS,0           TEST REC ALTERED                             
         BE    *+8                 NO                                           
         MVI   BYTE3,X'FF'                                                      
         CLI   ADDPAR3+3,0                                                      
         BE    *+6                                                              
         DC    H'0'                ADDEL ERROR                                  
*                                                                               
         CLI   TRAFPST,C'Y'        OPTION TO POST FILMS                         
         BNE   PS23                                                             
         CLI   BUYKEY+3,X'FF'      NO FILM POSTING FOR NON-POL                  
         BNE   PS23                                                             
*                                                                               
         LA    R4,FCMEL            POINT TO FILM ELEM                           
         ST    R4,ADDPAR2                                                       
*                                                                               
         GOTO1 VADDEL,ADDPARS                                                   
         SPACE 1                                                                
         CLI   ADDPARS,0           TEST REC ALTERED                             
         BE    *+8                 NO                                           
         MVI   BYTE3,X'FF'                                                      
         CLI   ADDPAR3+3,0                                                      
         BE    *+6                                                              
         DC    H'0'                ADDEL ERROR                                  
*                                                                               
         LA    R4,AFEL             RESTORE TO AFFELEM                           
         ST    R4,ADDPAR2                                                       
*                                                                               
PS23     DS    0H                                                               
         CLI   PSEUDOPT,C'R'       RESPONSE COUNT REQ?                          
         BNE   PS24                                                             
         CLI   BUYKEY+3,X'FF'      ONLY IF POOL                                 
         BC    0,PS24              ***TEMP NOOP                                 
*                                                                               
         LA    R4,RESPEL           POINT TO RESP ELEM                           
         ST    R4,ADDPAR2                                                       
*                                                                               
         GOTO1 VADDEL,ADDPARS                                                   
         SPACE 1                                                                
         CLI   ADDPARS,0           TEST REC ALTERED                             
         BE    *+8                 NO                                           
         MVI   BYTE3,X'FF'                                                      
         CLI   ADDPAR3+3,0                                                      
         BE    *+6                                                              
         DC    H'0'                ADDEL ERROR                                  
*                                                                               
         LA    R4,AFEL             RESTORE TO AFFELEM                           
         ST    R4,ADDPAR2                                                       
PS24     BCT   R0,PS22                                                          
         B     PS11                GET ANOTHER BUY                              
PS40     EQU   *                                                                
         CLI   OTOOPT,C'Y'        MINUS OTOS?                                   
         BNE   PS52                                                             
*                             *** OTO BUG CATCHERS ***                          
         CLC   ICSHPRD(3),=C'POL'                                               
         BNE   PS42                                                             
         CLI   BPRD,0                                                           
         BE    PS46                                                             
         CLI   BPRD,X'FF'                                                       
         BE    PS46                                                             
         B     PS48                                                             
*                                                                               
PS42     DS    0H                                                               
         CLI   BPRD,0                                                           
         BE    PS48                                                             
         CLI   BPRD,X'FF'                                                       
         BE    PS48                                                             
*                                                                               
PS46     DS    0H                  CHECK OTO IN OPTIONS FIELD HERE?             
         B     PS50                                                             
*                                                                               
PS48     DS    0H                                                               
         LA    R2,ICSMEDH                                                       
         XC    ICSMSG,ICSMSG                                                    
         MVC   ICSMSG(35),=C'** OTO PROGRAM ERROR - CALL DDS! **'               
         FOUT  ICSMSGH                                                          
         B     PS52                                                             
*                                                                               
PS50     DS    0H                                                               
         L     RF,=F'0'            PATCH TO DO ONLY THIS REC                    
         LTR   RF,RF               TEST PATCH MADE                              
         BZ    PS50B                                                            
         C     RF,SAVDA                                                         
         BNE   PS52                                                             
*                                                                               
PS50B    DS    0H                                                               
         GOTO1 =V(SPMINOTO),DMCB,AIOABUY,(BPRD,BSTART),                X        
               (BPRD2,VRECUP),RR=RELO02                                         
*                                                                               
         CLC   SAVDA,=F'0'            PATCH TO BLOW UP ON THIS DA               
         BNE   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         TM    DMCB,X'40'          TEST RECORD OVERFLOW                         
         BZ    PS51                                                             
*                                                                               
PS50D    DS    0H                  OVERFLOW FINISH UP                           
         LA    R2,ICSMEDH          CURSOR                                       
         XC    ICSMSG,ICSMSG                                                    
         MVC   ICSMSG(47),=C'**RECORD OVERFLOW- NO FILE MARKING** (LINEX        
                NNN)'                                                           
         ZIC   RF,BUYKEY+10                                                     
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  ICSMSG+43(3),DUB                                                 
         FOUT  ICSMSGH                                                          
         MVI   ERRAREA,X'FF'                                                    
         DC    H'0'                                                             
         DC    C'$ABEND'                                                        
         B     EXIT                                                             
*                                                                               
PS51     DS    0H                                                               
         TM    DMCB,X'80'          TEST REC ALTERED                             
         BZ    PS52                                                             
         GOTO1 VDATCON,DMCB,(5,0),(3,BDCHG)                                     
         MVI   BDWHY3,X'20'                                                     
         MVI   BDWHY2,0                                                         
         MVI   BDWHY,0                                                          
         B     PS54                                                             
*                                                                               
PS52     DS    0H                                                               
         CLI   BYTE3,X'FF'         TEST REC ALTERED                             
         BNE   NXTREC                                                           
*                                                                               
**********************************************************************          
*        SPOT DATE CHANGE CODE FOR WESTERN 00W'S                                
**********************************************************************          
*                                                                               
         CLI   DEFDCHG,C'Y'        DEFERRAL DATE CHANGE OPTION                  
         BNE   PS54                                                             
         CLI   BUYKEY+3,X'FF'      POL BUYS ONLY                                
         BNE   PS54                                                             
*                                                                               
         LA    R3,BDELEM                                                        
         USING REGELEM,R3                                                       
*                                                                               
PS52B    DS    0H                                                               
         CLI   0(R3),0             EOR                                          
         BE    PS54                                                             
         CLI   1(R3),0             EOR                                          
         BE    PS54                                                             
         CLI   0(R3),X'0B'         FIND SPOT ELEMS                              
         BL    PS53                                                             
         CLI   0(R3),X'0F'                                                      
         BH    PS53                                                             
*                                                                               
         CLC   RDATE,PREVWKP       IN LAST WEEK OF PREV MONTH                   
         BL    PS53                                                             
         CLC   RDATE,BRDMON                                                     
         BNL   PS53                                                             
*                                                                               
         LR    RF,R3                                                            
*                                                                               
PS52D    DS    0H                                                               
         ZIC   R1,1(RF)            FIND AFFID ELEM IF ANY                       
         AR    RF,R1                                                            
         CLI   0(RF),X'10'                                                      
         BH    PS53                IF NEXT ELEM NOT AFFID, THERE                
         BL    PS53                ISN'T ONE                                    
*                                  HAVE AFFID                                   
         MVC   SVAFFDT,ADATE-AFFELEM(RF)  SAVE AFFID DATE                       
         CLC   SVAFFDT,RDATE       OK IF SAME AS SPOT DATE                      
         BE    PS53                                                             
*                                                                               
         CLC   ADATE-AFFELEM(2,RF),BRDMON  MUST BE IN CURRENT MONTH             
         BL    PS53                                                             
         MVC   RDATE,ADATE-AFFELEM(RF)  SET SPOT DATE=AFF DATE                  
         ST    R3,SVSPELM                                                       
         BAS   RE,PSQF             MAKE SURE ELEM ENDS UP IN SEQ                
         B     PS52B                                                            
*                                                                               
PS53     DS    0H                                                               
         ZIC   RF,1(R3)            NEXT SPOT ELEM                               
         AR    R3,RF                                                            
         B     PS52B                                                            
*                                                                               
PS54     DS    0H                                                               
         BAS   RE,PUTREC                                                        
         B     NXTREC                                                           
         SPACE 2                                                                
PSEXT    XIT1                                                                   
         EJECT                                                                  
**********************************************************************          
*        PSQF        ELEM SEQUENCE FIX FOR OUT-OF-WEEK DATE CHANGING            
**********************************************************************          
*                                                                               
PSQF     NTR1                                                                   
         L     R3,SVSPELM          A(SPOT ELEM)                                 
         B     PSQF16                                                           
*                                                                               
PSQF02   DS    0H                                                               
         CLI   0(R3),0             EOR                                          
         BE    PSQF44                                                           
         CLI   0(R3),X'1F'         ELEM AFTER ALL BUYS                          
         BH    PSQF44                                                           
         CLI   0(R3),X'0B'         SPOT ELEMS                                   
         BL    PSQF16                                                           
         CLI   0(R3),X'0F'                                                      
         BH    PSQF16                                                           
*                                                                               
         CLC   RDATE,SVAFFDT       TEST ELEM DATE VS SAVED AFFID                
         BE    PSQF44              IF EQUAL, NOTHING TO DO                      
         BH    PSQF44              IF HIGH, HAVE NEW INSERT POINT               
*                                                                               
PSQF16   DS    0H                                                               
         ZIC   R0,1(R3)            NEXT ELEM                                    
         AR    R3,R0                                                            
         B     PSQF02                                                           
*                                                                               
PSQF44   DS    0H                  HAVE NEW ELEM INSERT POINT                   
         L     RF,SVSPELM          A(ORIGINAL ELEM)                             
         MVC   X,0(RF)             SAVE ELEMS OUTSIDE OF REC                    
         LA    R4,X                                                             
*                                                                               
PSQF45   DS    0H                                                               
         GOTO1 VRECUP,DMCB,BUYRECD,(R4),(R3)   INSERT                           
*                                                                               
         ZIC   R0,1(R3)            NEXT INSERT POINT                            
         AR    R3,R0                                                            
*                                                                               
         ZIC   R0,1(R4)            NEXT ELEM                                    
         AR    R4,R0                                                            
*                                                                               
         CLI   0(R4),0             EOR                                          
         BE    PSQF50                                                           
         CLI   0(R4),X'10'         COPY SUBSIDIARY ELEMS                        
         BL    PSQF50                                                           
         CLI   0(R4),X'1F'                                                      
         BH    PSQF50                                                           
         B     PSQF45                                                           
*                                                                               
PSQF50   DS    0H                  NOW DELETE FROM ORIGINAL SPOT                
*                                                                               
         L     R4,SVSPELM          A(ORIGINAL ELEM)                             
*                                                                               
PSQF55   DS    0H                                                               
         GOTO1 VRECUP,DMCB,BUYRECD,(R4)     DELETE                              
*                                                                               
         CLI   0(R4),0             EOR                                          
         BE    PSQF60                                                           
         CLI   0(R4),X'10'         DELETE SUBSIDIARY ELEMS                      
         BL    PSQF60                                                           
         CLI   0(R4),X'1F'                                                      
         BH    PSQF60                                                           
         B     PSQF55                                                           
*                                                                               
PSQF60   DS    0H                                                               
*                                                                               
PSQFX    DS    0H                                                               
         XIT1                                                                   
         DROP  R3                                                               
         EJECT                                                                  
**********************************************************************          
*                                                                               
*        PDTF        SPOT DATE FIX FOR DEFERRAL DATE CHANGING                   
*                                                                               
*        MODULE RESETS THE SPOT DATE OF ANY DEFERRALL ELEMS                     
*        WHOSE DATE WAS CHANGED DURING AN EARLIER MATCH. THE IDEA IS            
*        TO RESTORE THE FILE TO ITS ORIGINAL STATUS.                            
*                                                                               
*        IT IS PRESENT IN TWO PLACES- HERE AND IN TSTREC IN SPMAT01             
*        WHERE THE RECORDS ARE RESTORED FOR BYBLD (BUT NOT WRITTEN              
*        BACK).                                                                 
*                                                                               
**********************************************************************          
*                                                                               
PDTF     NTR1                                                                   
         CLI   DEFDCHG,C'Y'        DEFERRAL DATE CHANGE?                        
         BNE   PDTFX               NO, FORGET IT.                               
         CLI   BUYKEY+3,X'FF'      POL BUYS ONLY                                
         BNE   PDTFX                                                            
         TM    BDSTAT,X'80'        SKIP IF POL NPW                              
         BNZ   PDTFX                                                            
         TM    BDSTAT2,X'80'       SKIP IF DAILY SKED (PER MEL)                 
         BNZ   PDTFX                                                            
         CLI   PROGPROF+9,C'C'     NOT CALENDAR MONTHS                          
         BE    PDTFX                                                            
         CLC   BSTART,BRDMON       START DATE MUST = START OF MONTH             
         BNE   PDTF02                                                           
*                                                                               
         ZIC   R2,BDSEDAY          IS IT AN OUT-OF-WEEK?                        
         SRDL  R2,4                (FOR OUT OF WEEK START DAY                   
         SRL   R3,28               IS HIGHER THAN END)                          
         CR    R2,R3                                                            
         BNH   PDTFX               NO, SKIP IT                                  
*                                  SET VARIOUS DATES                            
         GOTO1 VDATCON,DMCB,(2,PREVWKP),WORK                                    
         BCTR  R2,0                                                             
         GOTO1 VADDAY,DMCB,WORK,DUB,(R2)                                        
         GOTO1 VDATCON,DMCB,DUB,(2,PREVWKR)   START OF LAST ROTATOR WK          
         GOTO1 VADDAY,DMCB,DUB,DUB,6                                            
         GOTO1 VDATCON,DMCB,DUB,(2,PREVWKRX)  END OF LAST ROTATOR WEEK          
*                                                                               
         LA    R8,BDELEM                                                        
         USING REGELEM,R8                                                       
         B     PDTF04                                                           
*                                                                               
PDTF02   DS    0H                                                               
         ZIC   R0,RLEN                                                          
         AR    R8,R0                                                            
*                                                                               
PDTF04   DS    0H                                                               
         CLI   0(R8),0             EOR                                          
         BE    PDTFX                                                            
*                                                                               
         CLI   RCODE,0                                                          
         BE    PDTFX                                                            
         CLI   RCODE,X'0B'                                                      
         BL    PDTF02                                                           
         CLI   RCODE,X'0F'                                                      
         BH    PDTF02                                                           
*                                                                               
         CLC   RDATE,BSTART        SPOT DATE VS START DATE                      
         BL    PDTF06                                                           
         CLC   RDATE,PREVWKRX      MUST BE BEFORE LAST ROTATOR WK END           
         BH    PDTF02                                                           
*                                                                               
PDTF06   DS    0H                                                               
         CLC   RDATE,PREVWKP       PICK UP SPOTS IN LAST                        
         BL    PDTF02              WEEK OF PREV MONTH                           
*                                  AND IF NOT MATCHED IN PREV MONTH             
         LR    R2,R8                                                            
PDTF08   DS    0H                                                               
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         CLI   0(R2),0             EOR                                          
         BE    PDTF10              YES, SPOT IS OK                              
         CLI   0(R2),X'0B'         ANOTHER SPOT ELEM                            
         BL    PDTF08                                                           
         CLI   0(R2),X'0F'                                                      
         BNH   PDTF10              MEANS LAST SPOT NOT MATCHED                  
         CLI   0(R2),X'10'         AFFID ELEM                                   
         BNE   PDTF08                                                           
         USING AFFELEM,R2                                                       
         CLC   ADATE,BRDMON        TEST MATCHED IN PREV MONTH                   
         BL    PDTF02              YES, SKIP IT                                 
*                                                                               
*                             ELSE WILL BE MATCHED IN CURRENT MONTH             
*                                                                               
         MVC   RDATE,PREVWKR       RESET DATE TO LAST ROTATION WEEK             
         MVI   BYTE3,X'FF'         SET REC IS CHANGED                           
*                                  OF PREVIOUS MONTH                            
         DROP  R2                                                               
*                                                                               
PDTF10   DS    0H                                                               
         B     PDTF02              NEX ELEM                                     
*                                                                               
PDTFX    DS    0H                                                               
         XIT1                                                                   
         DROP  R8                                                               
         EJECT                                                                  
**********************************************************************          
*        PSFFILM     FILM ELEM SET                                              
**********************************************************************          
*                                                                               
PSFFILM  DS    0H                                                               
         XC    0(2,R4),0(R4)       CLEAR OUTPUT                                 
         ICM   R0,15,FLMTPARS+8                                                 
         BZR   RE                  NO FILMS                                     
         L     RF,AFLMTAB                                                       
         USING FLMTABD,RF                                                       
*                                                                               
PSFF4    DS    0H                                                               
         CLC   FLMIDNO,IINVID      TST FROM RIGHT INVOICE                       
         BNE   PSFF10                                                           
         CLC   FLMICOD,0(R3)       INTERNAL FILM NO.                            
         BNE   PSFF10                                                           
*                                                                               
         MVC   0(2,R4),FLMSEQ      SET CMML SEQ                                 
         BR    RE                                                               
*                                                                               
PSFF10   DS    0H                                                               
         LA    RF,FLMTABEL(RF)                                                  
         BCT   R0,PSFF4                                                         
         BR    RE                                                               
         DROP  RF                                                               
         SPACE 3                                                                
         LTORG                                                                  
         EJECT                                                                  
*                   CLEAR IOAREA                                                
         SPACE 3                                                                
CLRIO    LA    R0,16                                                            
         L     R1,AIOABUY                                                       
         XC    0(250,R1),0(R1)                                                  
         LA    R1,250(R1)                                                       
         BCT   R0,*-10                                                          
         BR    RE                                                               
         EJECT                                                                  
*                  COMMUNICATION WITH DATA MANAGER (FILE)                       
         SPACE 3                                                                
GETREC   MVC   COMMAND,=C'GETREC'                                               
         B     FILE                                                             
         SPACE 2                                                                
PUTREC   MVC   COMMAND,=C'PUTREC'                                               
         B     FILE                                                             
         SPACE 2                                                                
ADDREC   MVC   COMMAND,=C'ADDREC'                                               
         B     FILE                                                             
         SPACE 2                                                                
FILE     NTR                                                                    
         LA    R2,KEY+14                                                        
         CLI   COMMAND,C'A'                                                     
         BNE   *+8                                                              
         LA    R2,KEY                                                           
         IC    R3,TERMNAL                                                       
         IC    R4,DMINBTS                                                       
         GOTO1 VDATAMGR,DMCB,((R4),COMMAND),=C'SPTFILE',               X        
               (R2),AIOABUY,((R3),DMWORK)                                       
         EJECT                                                                  
*                  DATA MANAGER ERRORS AND EXIT                                 
         SPACE 3                                                                
DMCHECK  MVC   BYTE,DMCB+8                                                      
         NC    BYTE,DMOUTBTS                                                    
         BNZ   DMERRS                                                           
         XIT1                                                                   
         SPACE 2                                                                
DMERRS   L     RD,4(RD) .          UNWIND WITHOUT XIT                           
         LM    RE,RC,12(RD)                                                     
         SR    R3,R3 .             LET GETMSG SORT IT OUT                       
         B     ERROR                                                            
         EJECT                                                                  
*                  EXITS FROM PROGRAM                                           
         SPACE 3                                                                
ERROR    L     R4,ERRAREA                                                       
         MVI   ERRAREA,X'FF'                                                    
         MVC   DMCB+20(4),VDATAMGR                                              
         MVC   DMCB+20(1),TERMNAL                                               
         GOTO1 VGETMSG,DMCB+12,((R3),8(R4)),(2,DMCB)                            
         SPACE 2                                                                
         B     PSEXT                                                            
*                                                                               
EXIT     DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
         SPACE 3                                                                
         LTORG                                                                  
         EJECT                                                                  
BUYRECD  DSECT                                                                  
****SPGENBUY INCLUDED HERE                                                      
         PRINT OFF                                                              
       ++INCLUDE SPGENBUY                                                       
         PRINT ON                                                               
         EJECT                                                                  
*                                                                               
       ++INCLUDE SPMATWKN                                                       
