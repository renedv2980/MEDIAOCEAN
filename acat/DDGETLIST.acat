*          DATA SET DDGETLIST  AT LEVEL 003 AS OF 05/01/02                      
*CATALP GETLIST                                                                 
         SPACE 3                                                                
* PARAMETER 1 BYTE 0-3  A(9 BYTE KEY) - CL2 USER ID NUMBER                      
*                                       CL6 ADDRESSEE LIST ID                   
*                                       CL1 SPARE (NULL)                        
* PARAMETER 2 BYTE 0    OPTIONAL MAX NO OF 2-BYTE ENTRIES - DEFAULT=255         
* PARAMETER 2 BYTE 1-3  A(OUTPUT AREA)-                                         
* PARAMETER 3 BYTE 0-3  A(DATAMGR)                                              
*                                                                               
* VALUES RETURNED BY GETLIST ARE                                                
* PARAMETER 1 BYTE 0    X'00' = NO ERRORS                                       
*                       X'04' = MORE THAN ONE LEVEL OF NEST                     
*                       X'08' = MAX NO OF ENTRIES EXCEEDED                      
*                       OTHERWISE AS FOR DATAMGR ERROR BYTE BUT WITH            
*                       NEST LEVEL (0 OR 1) IN LOW ORDER NIBBLE                 
* PARAMETER 1 BYTE 1-3  A(CTFILE RECORD) - SEE CTAREC                           
* PARAMETER 2 BYTE 0    COUNT OF ENTRIES IN OUTPUT AREA                         
*             BYTE 1-3  A(OUTPUT AREA)                                          
*                       NOW CONTAINS STRING OF DESTID NOS PLUS X'FF'            
         TITLE 'GETLIST - MODULE TO READ AN ADDRESSEE LIST'                     
GETLIST  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 300,**GLIST**                                                    
         USING WORKD,RC                                                         
         LR    RA,R1                                                            
         USING PARAD,RA            RA = A(PARA LIST)                            
         L     R1,PAKEY                                                         
         MVC   INKEY,0(R1)                                                      
         MVI   ERROR,0                                                          
         MVC   MAX,=H'255'         MAX NUMBER OF IDS DEFAULTS TO 255            
         CLI   PCOUNT,0                                                         
         BE    *+10                                                             
         MVC   MAX+1(1),PCOUNT                                                  
         LA    R2,KEYA             R2 = A(CONTROL FILE RECORD)                  
         USING CTAREC,R2                                                        
         L     R8,PAOUT            R8 = A(OUTPUT STRING ENTRY)                  
         SR    R9,R9               R9 = COUNT OF OUTPUT ENTRIES                 
         SPACE 1                                                                
GL01     XC    CTAKEY,CTAKEY       BUILD KEY FOR ADDRESSEE LIST RECORD          
         MVI   CTAKTYP,C'A'                                                     
         MVC   CTAKUSER(9),INKEY                                                
         MVI   LEVEL,0                                                          
         BAS   RE,GETSTRNG                                                      
         MVI   0(R8),X'FF'         TERMINATE STRING                             
         CLI   ERROR,X'10'         LIST NOT FOUND                               
         BE    *+8                                                              
         ST    R2,PACTREC          RETURN A(RECORD)                             
         MVC   PERROR,ERROR        ERROR INDICATOR                              
         STC   R9,PCOUNT           AND COUNT OF ENTRIES                         
         XMOD1 1                                                                
         EJECT                                                                  
*              ROUTINE TO READ A LIST RECORD AND ADD TO OUTPUT STRING           
*              ON ENTRY LEVEL = 0 OR 1(NESTED LIST)                             
*                       R2    = A(KEY)                                          
*                       R8    = A(NEXT ENTRY IN OUTPUT STRING)                  
*                       R9    = COUNT OF ENTRIES                                
         SPACE 1                                                                
GETSTRNG NTR1                                                                   
         GOTO1 PDATAMGR,DMCB,=C'DMREAD',=C'CTFILE',(R2),(R2)                    
         CLI   DMCB+8,0                                                         
         BE    GS1                                                              
         MVC   ERROR,DMCB+8                                                     
         OC    ERROR,LEVEL                                                      
         B     GEXIT                                                            
         SPACE 1                                                                
GS1      LA    R3,CTADATA                                                       
         USING CTPASD,R3                                                        
         SR    R4,R4                                                            
         SPACE 1                                                                
GS2      CLI   0(R3),0                                                          
         BE    GEXIT                                                            
         CLI   0(R3),X'03'         PASSIVE POINTER                              
         BNE   GBUMP                                                            
         SPACE 1                                                                
GS3      CLI   CTPASLEN,4          IF IT'S A DEST ID NO, ADD TO STRING          
         BNE   GS4                                                              
         CH    R9,MAX              UNLESS MAXIMUM HAS BEEN REACHED              
         BNE   *+12                                                             
         MVI   ERROR,8                                                          
         B     GEXIT                                                            
         MVC   0(2,R8),CTPASDTA                                                 
         LA    R8,2(R8)                                                         
         LA    R9,1(R9)                                                         
         B     GBUMP                                                            
         SPACE 1                                                                
GS4      CLI   CTPASLEN,8          IF ITS A NESTED LIST READ IT                 
         BNE   GBUMP                                                            
         CLI   LEVEL,0             UNLESS ITS A LIST WITHIN A LIST              
         BE    *+12                WITHIN A LIST                                
         MVI   ERROR,4                                                          
         B     GEXIT                                                            
         MVC   KEYB,KEYA                                                        
         LA    R2,KEYB                                                          
         MVC   CTAKID,CTPASDTA                                                  
         MVI   LEVEL,1                                                          
         BAS   RE,GETSTRNG                                                      
         MVI   LEVEL,0                                                          
         CLI   ERROR,0                                                          
         BNE   GEXIT                                                            
         SPACE 1                                                                
GBUMP    IC    R4,1(R3)            BUMP TO NEXT ELEMENT                         
         AR    R3,R4                                                            
         B     GS2                                                              
         SPACE 1                                                                
GEXIT    XIT1  REGS=(R8,R9)        RETURN STRING POINTER AND COUNT              
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
*              DSECT TO COVER LOCAL WORKING STORAGE                             
         SPACE 1                                                                
WORKD    DSECT                                                                  
DMCB     DS    6F        V                                                      
MAX      DS    H         B         MAXIMUM NUMBER OF ENTRIES IN STRING          
LEVEL    DS    C         B         0 OR 1(=NESTED LIST)                         
ERROR    DS    C         X                                                      
INKEY    DS    CL9       V         CALLER-SUPPLIED KEY (TUSER/LISTID/0)         
KEYA     DS    CL25      V                                                      
IOA      DS    1000C     V                                                      
KEYB     DS    CL25      V                                                      
IOB      DS    1000C     V                                                      
         SPACE 3                                                                
*              DSECT TO COVER PARAMETER LIST                                    
         SPACE 1                                                                
PARAD    DSECT                                                                  
PERROR   DS    0CL1      X         ERROR INDICATOR                              
PAKEY    DS    CL4       B         A(CALLER-SUPPLIED KEY)                       
         ORG   PAKEY                                                            
PACTREC  DS    CL4       B         A(LEVEL 0 LIST RECORD)                       
PCOUNT   DS    0CL1      B         MAXIMUM ON INPUT, ACTUAL ON RETURN           
PAOUT    DS    CL4       B         A(OUTPUT AREA FOR STRING)                    
PDATAMGR DS    V         B         A(DATAMGR)                                   
         SPACE 3                                                                
*              NESTED INCLUDES                                                  
         SPACE 1                                                                
* CTGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003DDGETLIST 05/01/02'                                      
         END                                                                    
