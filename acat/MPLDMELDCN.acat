*          DATA SET MPLDMELDCN AT LEVEL 002 AS OF 11/19/85                      
*CATALP LDMELDCN                                                                
         TITLE 'LDCOUNT - MP/MEAL - DIR REC TYPE COUNT/PRINT'                   
*        PARAMS VIA R1                                                          
*        XL1   COUNT/PRINT  X'01'=COUNT  X'81'=COUNT DELETED PASSIVE            
*                           X'FF'=PRINT                                         
*        AL3   A(RECORD)                                                        
*                                                                               
         PRINT NOGEN                                                            
LDCOUNT  CSECT                                                                  
         NMOD1 WRKX-WRKD,MEALDCNT                                               
         USING WRKD,RC                                                          
*                                                                               
         CLI   0(R1),X'FF'         CALL TO PRINT TABLE                          
         BE    PRINT                                                            
         LA    RA,1                CALL TO BUMP TABLE (RA=+1)                   
         CLI   0(R1),X'81'                                                      
         BNE   COUNT                                                            
         LNR   RA,RA               CALL TO DECR TABLE (RA=-1)                   
         SPACE 2                                                                
COUNT    L     R2,0(R1)            R2=A(RECORD)                                 
         LA    R3,RECTBL           R3=A(RECORD TYPE TABLE)                      
         OC    0(3,R2),0(R2)                                                    
         BE    COUNT3              RECORD IS HEADER                             
         CLI   0(R2),X'FF'                                                      
         BE    COUNT3              RECORD IS TRAILER                            
         LA    R3,L'RECTBL(R3)                                                  
         SPACE 2                                                                
COUNT1   CLI   0(R3),X'FF'         TEST END OF TABLE                            
         BE    COUNT3                                                           
         CLC   0(1,R3),0(R2)       COMPARE AGENCY/LISTNUM                       
         BE    COUNT3                                                           
COUNT2   LA    R3,L'RECTBL(R3)                                                  
         B     COUNT1                                                           
         SPACE 2                                                                
COUNT3   L     RF,20(R3)           BUMP TOTAL RECORD COUNT                      
         AR    RF,RA                                                            
         ST    RF,20(R3)                                                        
         SPACE 2                                                                
EXIT     XMOD1 1                                                                
         EJECT                                                                  
PRINT    L     RA,=V(CPRINT)       PRINT HEADING                                
         USING DPRINT,RA                                                        
         ZAP   LINE,=P'99'                                                      
         MVC   P,HDRL1                                                          
         GOTO1 =V(PRINTER)                                                      
         MVC   P,ULINE                                                          
         GOTO1 =V(PRINTER)                                                      
         GOTO1 =V(PRINTER)                                                      
         LA    R3,RECTBL           R3=A(RECORD TABLE ENTRY)                     
         SR    R4,R4               R4=GRAND TOTAL                               
         SPACE 2                                                                
PRINT1   MVC   PLNAME,4(R3)        SET RECORD TYPE                              
         L     RF,20(R3)                                                        
         AR    R4,RF               BUMP GRAND TOTAL                             
         CVD   RF,DUB                                                           
         UNPK  DUB1,DUB                                                         
         OI    DUB1+7,C'0'                                                      
         MVC   PLRTOT,DUB1         SET RECORD COUNT                             
         LTR   RF,RF                                                            
         BNZ   *+10                                                             
         MVC   PLRTOT,DOTS         SET ZERO RECORDS                             
         MVC   P,PLINE                                                          
         GOTO1 =V(PRINTER)                                                      
         SPACE 2                                                                
PRINT2   CLI   0(R3),X'FF'         TEST IF END OF TABLE                         
         BE    PRINT3                                                           
         LA    R3,L'RECTBL(R3)                                                  
         B     PRINT1                                                           
         SPACE 2                                                                
PRINT3   GOTO1 =V(PRINTER)         PRINT GRAND TOTAL IN R4                      
         MVC   P,ULINE                                                          
         GOTO1 =V(PRINTER)                                                      
         MVC   PLNAME,=CL16'TOTAL'                                              
         CVD   R4,DUB                                                           
         UNPK  DUB1,DUB                                                         
         OI    DUB1+7,C'0'                                                      
         MVC   PLRTOT,DUB1         SET TOTAL RECORD COUNT                       
         MVC   P,PLINE                                                          
         GOTO1 =V(PRINTER)                                                      
PRINTX   B     EXIT                                                             
         EJECT                                                                  
HDRL1    DC    CL132'MEAL RECORD TYPE   NUM RECS'                               
ULINE    DC    CL132'----------------   --------'                               
         SPACE 2                                                                
PLINE    DC    CL132' '                                                         
         ORG   PLINE                                                            
PLNAME   DS    CL16                                                             
         DS    CL3                                                              
PLRTOT   DS    CL8                                                              
         ORG                                                                    
         SPACE 2                                                                
DOTS     DC    8C'.'                                                            
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*        XL1   RECORD TYPE CODE (REC TYPES ARE HEX CODES)                       
*        XL3   FLAGS                                                            
*        CL16  RECORD TYPE NAME                                                 
*        XL4   COUNT                                                            
*                                                                               
         DS    0F                                                               
RECTBL   DS    0XL24                                                            
         DC    X'00',X'000000',CL16'00-FF HDR/TRL   ',F'0'                      
RECTBLA  DC    X'20',X'000000',CL16'20-BRAND        ',F'0'                      
         DC    X'30',X'000000',CL16'30-SUPPLIER     ',F'0'                      
         DC    X'40',X'000000',CL16'40-ADVERT       ',F'0'                      
         DC    X'60',X'000000',CL16'60*BRAND.....PAS',F'0'                      
         DC    X'50',X'000000',CL16'50*ADVERT....PAS',F'0'                      
         DC    X'70',X'000000',CL16'70-AGENCY       ',F'0'                      
         DC    X'81',X'000000',CL16'81-BUCKET       ',F'0'                      
         DC    X'82',X'000000',CL16'82-BUCKET       ',F'0'                      
         DC    X'83',X'000000',CL16'83-BUCKET       ',F'0'                      
         DC    X'91',X'000000',CL16'91-BUCKET       ',F'0'                      
         DC    X'92',X'000000',CL16'92-BUCKET       ',F'0'                      
         DC    X'93',X'000000',CL16'93-BUCKET       ',F'0'                      
*                                                                               
RECTBLX  DC    X'FF',X'000000',CL16'??-UNKNOWN      ',F'0'                      
         EJECT                                                                  
*DDDPRINT                                                                       
       ++INCLUDE DDDPRINT                                                       
         SPACE 2                                                                
WRKD     DSECT                                                                  
DUB      DS    D                                                                
DUB1     DS    D                                                                
WRKX     EQU   *                                                                
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002MPLDMELDCN11/19/85'                                      
         END                                                                    
