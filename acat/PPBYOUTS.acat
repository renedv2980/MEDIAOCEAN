*          DATA SET PPBYOUTS   AT LEVEL 061 AS OF 05/01/02                      
*CATALP PPBYOUT                                                                 
         TITLE 'PPBYOUT - FORMAT PRINT BUY INFO'                                
*        CHANGE LOG                                                             
*                                                                               
*  BPLA 11/96      NEW CONTROL TO RETURN UNIT RATES, PREMIUMS,                  
*                  PBYOUNTS, PBYOLBC, PBYOSPC2                                  
*                  IN FRENCH - PBYOCLT2 - X'40'                                 
*                                                                               
*  BPLA 12/22/93   AT PB26 MOVE PBDSPACE TO WORK2 BEFORE OC                     
*                  TO PREVENT CHANGING BUY'S SPACE                              
*                                                                               
*  BPLA 12/13/93   CHANGES FOR NEWSPAPER BUYS WITH # IN PBDSPACE                
*                  (NO ASC CHECKING)                                            
*                                                                               
*  BPLA 2/12/92 NET UNIT RATE DISPLAY ALLOW FOR AC=100.00 (CARRIED              
*               AS -.001)                                                       
*  BPLA 1/20/92 FIX PREMIUM DISPLAY - CHECK IF PBDPRIN EXISTS BEFORE            
*               CHECKING IF ITS EQUAL TO PBDCOSIN                               
*  BPLA 9/3/91  FIX PREMIUM DISPLAY-WOULD DROP HIGH DIGIT IF OVER               
*               9999.99 IF I HAD A COLOR - EVEN IF PBDPRIN=PBDCOSIN             
*                                                                               
*  BPLA 8/30/91 FIX UNIT RATE DISPLAY - USE PBDACP WHEN DISPLAYING              
*               NET RATES                                                       
*                                                                               
*  BPLA 4/26/91  FIX WEEK OF COMMENT DISPLAY                                    
*                                                                               
PPBYOUT  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 52,PPBYOUT                                                       
*                                                                               
*        PARAMS  1       A(AREA COVERED BY PPBYOUTD DSECT)                      
*                        SEE DSECT FOR FUNCTION DESCRIPTION                     
*        PBYOCLT                                                                
*        X'80' = ONLY SPACE DESCRIPTION GIVEN                                   
*        X'40' = OUTDOOR SPACE AS SSSS RRRR IIII                                
*        X'20' = DO ZZZ ALLOCATION                                              
*        X'10' = SHOW ADDITIONAL DATES                                          
*        X'08' = DO REGULAR COMMENTS X'66'                                      
*        X'04' = DO INSERTION ORDER COMMENTS X'67'                              
*        X'02' = LINE NUMBER WITH DATE                                          
*        X'01' = SHOW NET                                                       
*                                                                               
*        PBYOCLT2                                                               
*        X'80' = DO POSITION INSTRUCTIONS X'68'                                 
*        X'40' = UNIT RATES AND PREMIUMS IN FRENCH                              
*                                                                               
         USING WKD,RC                                                           
         L     RA,0(R1)                                                         
         USING PPBYOUTD,RA                                                      
         L     R9,PBYOINPT                                                      
         USING PBUYREC,R9                                                       
         L     R8,PBYOVALS                                                      
         USING PVALUES,R8                                                       
         MVI   PBYOP,C' '               CLEAR OUTPUT AREA                       
         LA    R1,L'PBYOP-1                                                     
         TM    PBYOCTL,X'80'       IF ONLY SPACE DESC INPUT                     
         BZ    *+8                                                              
         LA    R1,40               ONLY CLEAR SPACE OUTPUT                      
         MOVE  (PBYOP+1,(R1)),PBYOP                                             
*                                                                               
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(19),SPACES                                              
         MVI   COMSW,C'N'                                                       
*                                                                               
         TM    PBYOCTL,X'80'       TEST SPACE DESC INPUT                        
         BZ    PB4                 NO                                           
*                                                                               
         LR    R5,R9               YES-ONLY SET OUTDOOR SPC                     
         BAS   RE,CKSPC                                                         
         B     EXIT                                                             
*                                  INSERT DATE                                  
PB4      DS    0H                                                               
         MVC   WORK,SPACES                                                      
         GOTO1 DATCON,DMCB,(3,PBUYKDAT),(5,WORK)                                
         TM    PBYOCTL,X'02'       TEST LINE NUM IN DATE                        
         BNZ   PB6                                                              
         CLI   PBUYKEY+24,1                                                     
         BNH   PB6                                                              
         MVI   WORK+8,C'-'                                                      
         ZIC   R0,PBUYKEY+24                                                    
         CVD   R0,DUB                                                           
**NEW 11/30/88               FOR BUYLINES OVER 100                              
*                            DISPLAY AS A0-N9   FOR 100-239                     
         CP    DUB,=P'100'                                                      
         BL    PB5                                                              
         DP    DUB,=P'10'                                                       
         OI    DUB+7,X'0F'                                                      
         UNPK  WORK+10(1),DUB+7(1)                                              
         ZAP   DUB,DUB(6)                                                       
         SP    DUB,=P'10'                                                       
         CVB   R7,DUB                                                           
         LA    R7,ALPHTAB(R7)                                                   
         MVC   WORK+9(1),0(R7)                                                  
         B     PB6                                                              
*                                                                               
PB5      OI    DUB+7,X'0F'                                                      
         UNPK  WORK+9(2),DUB                                                    
*                                                                               
PB6      DS    0H                                                               
         MVC   PBYOMDY,WORK                                                     
         CLI   PBDFREQ,C'M'             IF MONTHLY GET RID OF DAY               
         BNE   *+16                                                             
         MVC   PBYOMDY+3(6),WORK+5                                              
         MVC   PBYOMDY+9(2),SPACES                                              
*                                                                               
         MVC   PBYOINS(5),PBYOMDY                                               
         LA    RF,PBYOINS+3                                                     
         CLI   PBDFREQ,C'M'                                                     
         BE    *+8                                                              
         LA    RF,2(RF)                                                         
         MVC   0(3,RF),WORK+8           LINE NUM                                
*                                                                               
*                                  2ND INSERTION DATE                           
         CLI   PBDELEM+1,105       TEST NEW ELEM                                
         BL    PB8                 NO - SKIP 2ND DATE                           
         OC    PBDIDAT2,PBDIDAT2                                                
         BZ    PB8                                                              
*                                                                               
         GOTO1 DATCON,DMCB,(3,PBDIDAT2),(5,WORK)                                
         MVC   PBYOMDY2,WORK                                                    
         CLI   PBDFREQ,C'M'        TEST MONTHLY                                 
         BNE   PB7                                                              
         MVC   PBYOMDY2+3(3),WORK+5                                             
         MVC   PBYOMDY2+6(2),SPACES                                             
         MVI   PBYOINS+3,C'-'      MMM-MMM IN PBYOINS                           
         MVC   PBYOINS+4(3),WORK                                                
         B     PB7B                                                             
PB7      DS    0H                                                               
         MVC   PBYOINS2(5),WORK                                                 
*                                                                               
PB7B     DS    0H                                                               
         CLI   PBDEMIND,C' '       E/M IND                                      
         BNH   PB8                                                              
         CLI   PBYOINS+5,C' '      HAVE LINE NO                                 
         BH    PB8                                                              
*                                                                               
         MVC   PBYOINS+5(2),=C'-M'                                              
         MVC   PBYOINS2+5(2),=C'-E'                                             
         CLI   PBDEMIND,C'E'                                                    
         BE    PB7D                                                             
         MVC   PBYOINS+5(2),=C'-E'                                              
         MVC   PBYOINS2+5(2),=C'-M'                                             
PB7D     DS    0H                                                               
*                                                                               
*                                                                               
*                                  SPACE DESCRIPTIONS                           
PB8      DS    0H                                                               
         CLI   PBUYKMED,C'N'                                                    
         BE    PB20                                                             
         CLI   PBUYKMED,C'O'                                                    
         BE    PB10                                                             
*                                  FOR MAG SPACE IS EASY                        
         MVC   PBYOSPC1,PBDSPACE                                                
         B     PB30                                                             
*                                  OUTDOOR SPACE                                
PB10     DS    0H                                                               
         LA    R5,PBDSPACE                                                      
         BAS   RE,CKSPC            SET OUTDOOR SPC                              
         LA    R7,PBYOSPC1                                                      
         CLC   0(20,R7),SPACES     TEST ANYTHING DONE                           
         BE    PB12                NO - TRY COMMENT                             
         CLI   PBDSPACE,X'FF'                                                   
         BNE   PB19                                                             
         LA    R7,20(R7)                                                        
         CLC   0(20,R7),SPACES          IF 2ND LINE BLANK TRY COMMENT           
         BNE   PB19                                                             
*                                       IF 1ST COMMENT 17 OR LESS CHARS         
*                                       USE AS SPACE DESC.                      
PB12     DS    0H                                                               
         LA    R2,PBDELEM                                                       
         MVI   ELCODE,X'66'                                                     
         BAS   RE,NEXTEL                                                        
         BNE   PB19                                                             
         CLI   1(R2),19                                                         
         BH    PB19                     TOO LONG                                
         ZIC   RF,1(R2)                                                         
         SH    RF,=H'3'                                                         
         BM    PB19                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R7),2(R2)                                                    
         MVI   COMSW,C'Y'          SET COMMENT USED                             
*                                                                               
PB19     DS    0H                                                               
         B     PB30                                                             
*                                  NEWSPAPER SPACE                              
PB20     DS    0H                                                               
*                                  UNITS                                        
         OC    UNITS,UNITS                                                      
         BZ    PB21                                                             
         LA    R4,PBYOUNTS                                                      
         CLI   PBDSPACE,C'*'       TEST NO INSERTION IND                        
         BNE   *+12                                                             
         MVI   0(R4),C'*'                                                       
         LA    R4,1(R4)                                                         
         CLI   PBDSPACE,C'#'       TEST NO ASC CHECKING IND                     
         BNE   *+12                                                             
         MVI   0(R4),C'#'                                                       
         LA    R4,1(R4)                                                         
*                                                                               
         CLI   PBDUIND,X'89'       SEE IF LOWER CASE I                          
         BNE   PB20C                                                            
*                                  MEANS UNITS TO 2 DECIMALS                    
         EDIT  UNITS,(6,(R4)),2,ALIGN=LEFT                                      
         B     PB20D                                                            
*                                                                               
PB20C    EDIT  UNITS,(5,(R4)),ALIGN=LEFT                                        
*                                                                               
PB20D    AR    R4,R0               ADD EDITED LEN                               
         MVI   0(R4),C'L'                                                       
         CLI   PBDUIND,C'I'                                                     
         BE    PB20F                                                            
         CLI   PBDUIND,X'89'       LOWER CASE I                                 
         BNE   PB21                                                             
PB20F    MVI   0(R4),C'I'                                                       
*                                                                               
         TM    PBYOCLT2,X'40'      SEE IF DOING FRENCH SPACES                   
         BNO   PB21                                                             
         MVI   0(R4),C'P'          FOR POUCES                                   
*                                                                               
PB21     DS    0H                  LINES X COLS                                 
         CP    PBDCLMS,=P'0'                                                    
         BE    PB22                                                             
         ZAP   DUB,PBDUNITS                                                     
         DP    DUB,PBDCLMS                                                      
         LA    R4,PBYOLBC                                                       
         MVI   0(R4),C'('                                                       
         CLI   PBDUIND,X'89'       LOWER CASE I - 2 DECIMALS                    
         BNE   PB21B                                                            
         EDIT  (P6,DUB),(6,1(R4)),2,ALIGN=LEFT                                  
         B     PB21C                                                            
PB21B    EDIT  (P6,DUB),(6,1(R4)),ALIGN=LEFT                                    
PB21C    AR    R4,R0                                                            
         MVI   1(R4),C'L'                                                       
         CLI   PBDUIND,C'I'                                                     
         BE    PB21E                                                            
         CLI   PBDUIND,X'89'       LOWER CASE I                                 
         BNE   PB21F                                                            
PB21E    MVI   1(R4),C'I'                                                       
*                                                                               
         TM    PBYOCLT2,X'40'     SEE IF DOING FRENCH LINE BY COLUMNS           
         BNO   PB21F                                                            
         MVI   1(R4),C'P'       FOR POUCES                                      
*                                                                               
PB21F    LA    R4,1(R4)                                                         
         MVI   2(R4),C'X'                                                       
         LA    R4,4(R4)                                                         
         EDIT  PBDCLMS,(2,0(R4)),ALIGN=LEFT                                     
         AR    R4,R0                                                            
         MVC   0(5,R4),=C'COLS)'                                                
*                                                                               
*                                                                               
PB22     DS    0H                  UNIT RATE                                    
         CLI   PBDCOSTY,C'U'                                                    
         BNE   PB24                                                             
*                                                                               
         LA    R4,W                                                             
         MVC   W,SPACES                                                         
         ZAP   DUB,PBDCOS                                                       
         CVB   R1,DUB                                                           
         CLI   PBDCTYP,C'N'        TEST NET INPUT                               
         BNE   PB22B                                                            
         TM    PBYOCTL,X'01'       TEST TO SHOW AS NET                          
         BZ    PB22B                                                            
*                                                                               
*                                                                               
         ZAP   DUB,PBDACP                                                       
         CP    DUB,=P'-1'       100.000 IS CARRIED AS -.001                     
         BNE   *+10                                                             
         ZAP   DUB,=P'100000'                                                   
*                                                                               
         CVB   RF,DUB                                                           
         S     RF,=F'100000'                                                    
         LCR   RF,RF                                                            
         MR    R0,RF                                                            
         L     RF,=F'100000'                                                    
         SLDA  R0,1                                                             
         DR    R0,RF                                                            
         LTR   R1,R1                                                            
         BNP   *+8                                                              
         A     R1,=F'1'                                                         
         SRA   R1,1                                                             
*                                                                               
         B     PB22B                                                            
*                                                                               
PB22B    DS    0H                                                               
         EDIT  (R1),(10,(R4)),5,FLOAT=-                                         
         CLC   7(3,R4),=3C'000'                                                 
         BNE   *+10                                                             
         MVC   7(3,R4),SPACES                                                   
         LA    R4,W                                                             
         CLI   0(R4),C' '                                                       
         BNE   PB23                                                             
         LA    R4,1(R4)                                                         
         CLI   0(R4),C' '                                                       
         BNE   PB23                                                             
         LA    R4,1(R4)                                                         
PB23     DS    0H                                                               
         MVC   PBYOUR,0(R4)                                                     
*                                                                               
         TM    PBYOCLT2,X'40'     SEE IF DOING FRENCH UNIT RATES                
         BNO   PB24                                                             
         LA    R4,PBYOUR                                                        
         LA    R1,8                                                             
*                                                                               
PB23C    CLI   0(R4),C'.'          SEARCH FOR DECIMAL                           
         BE    PB23D                                                            
         LA    R4,1(R4)                                                         
         BCT   R1,PB23C                                                         
         B     PB24                                                             
*                                                                               
PB23D    MVI   0(R4),C','          CHANGE DECIMAL POINT TO ","                  
         SH    R4,=H'1'            BACK-UP ONE CHAR                             
         CLI   0(R4),C' '                                                       
         BH    PB24                                                             
         MVI   0(R4),C'0'         SINCE FRENCH DISPLAY 0,12345                  
*                                 IF LESS THAN 1                                
*                                                                               
PB24     DS    0H                  PREMIUM                                      
         LA    R4,PBYOPRM                                                       
         CLI   PBDCL,0                                                          
         BE    PB25                                                             
         MVC   0(1,R4),PBDCL                                                    
         OI    0(R4),C'0'                                                       
         MVI   1(R4),C'C'                                                       
         OC    PREMIUM,PREMIUM                                                  
         BZ    PB26                                                             
         MVI   2(R4),C'/'                                                       
         LA    R4,3(R4)                                                         
*                                                                               
PB25     DS    0H                                                               
         OC    PREMIUM,PREMIUM                                                  
         BZ    PB26                                                             
         CLI   PBDPRIN,C' '                                                     
         BNH   PB25A                                                            
         CLC   PBDPRIN,PBDCOSIN                                                 
         BE    *+14                                                             
         MVC   0(1,R4),PBDPRIN                                                  
         LA    R4,1(R4)                                                         
*                                                                               
PB25A    CLI   PBDCL,0               IF NO COLOR DISPLAY 10 DIGITS              
         BE    PB25F                                                            
*                                     SEE IF I'M DISPLAYING PBDPRIN             
         CLI   PBDPRIN,C' '                                                     
         BNH   PB25B                                                            
         CLC   PBDPRIN,PBDCOSIN       IF NOT I CAN DISPLAY 8 DIGITS             
         BNE   PB25C                                                            
PB25B    EDIT  PREMIUM,(8,(R4)),2,ALIGN=LEFT                                    
         LA    R5,8                                                             
         B     PB25N                                                            
*                                IF I AM,I CAN ONLY DISPLAY 7 DIGITS            
PB25C    EDIT  PREMIUM,(7,(R4)),2,ALIGN=LEFT                                    
         LA    R5,7                                                             
         B     PB25N                                                            
*                                                                               
PB25F    EDIT  PREMIUM,(10,(R4)),2,ALIGN=LEFT                                   
         LA    R5,10                                                            
*                                                                               
PB25N    TM    PBYOCLT2,X'40'        SEE IF DOING PREMIUMS IN FRENCH            
         BNO   PB26                                                             
*                                                                               
PB25O    CLI   0(R4),C'.'           SEARCH FOR DECIMAL POINT                    
         BE    PB25P                                                            
         LA    R4,1(R4)                                                         
         BCT   R5,PB25O                                                         
         B     PB26                 NONE FOUND                                  
*                                                                               
PB25P    MVI   0(R4),C','           CHANGE TO ","                               
*                                                                               
PB26     DS    0H                                                               
         MVC   WORK2(17),PBDSPACE                                               
         OC    WORK2(17),SPACES                                                 
         MVC   WORK(20),SPACES                                                  
         LA    R6,WORK2                                                         
         LA    R7,WORK                                                          
         CLI   WORK2,C'*'                                                       
         BNE   PB26C                                                            
         LA    R6,1(R6)                                                         
         MVI   0(R7),C'*'                                                       
         LA    R7,1(R7)                                                         
         B     PB27                                                             
*                                                                               
PB26C    CLI   WORK2,C'#'                                                       
         BNE   PB27              CHECK NO ASC CHECKING IND                      
         LA    R6,1(R6)                                                         
         MVI   0(R7),C'#'                                                       
         LA    R7,1(R7)                                                         
*                                                                               
PB27     DS    0H                                                               
         CLI   0(R6),C' '                                                       
         BE    PB29                NO SPACE                                     
         BAS   RE,SETNSPC                                                       
PB27D    DS    0H                  FLOAT PREM                                   
         MVC   PBYOSPC1,WORK                                                    
         CLC   PBYOUNTS,SPACES                                                  
         BE    PB29                                                             
         CLI   PBDUIND,X'89'       LOWER CASE I                                 
         BNE   PB28                                                             
         CLC   PBYOSPC1(7),PBYOUNTS    NO 2ND LINE                              
*                                      IF SPACE MATCHES PBYOUNTS                
         BE    PB29                EX 10.25I SPACE - WITH (10.25I)              
*                                                                               
*                                  (NNNNL) IN 2ND LINE                          
PB28     LA    R4,PBYOSPC2                                                      
         MVI   0(R4),C'('                                                       
         MVC   1(6,R4),PBYOUNTS+1                                               
         CLI   PBYOUNTS,C'*'                                                    
         BE    PB28C                                                            
         CLI   PBYOUNTS,C'#'                                                    
         BE    *+10                                                             
         MVC   1(7,R4),PBYOUNTS                                                 
PB28C    LA    R4,8(R4)                                                         
         CLI   0(R4),C' '                                                       
         BH    *+8                                                              
         BCT   R4,*-8                                                           
         MVI   1(R4),C')'                                                       
*                                                                               
PB29     DS    0H                                                               
         SPACE 2                                                                
*                                  GROSS                                        
PB30     DS    0H                                                               
         EDIT  GROSS,(14,PBYOGRS),2,COMMAS=YES,MINUS=YES                        
*                                                                               
         SPACE 2                                                                
*                                                                               
PB32     DS    0H                                                               
         TM    PBYOCTL,X'20'       TEST NEED ALLOCS                             
         BZ    PB34                                                             
*                                                                               
* POL ALLOCATIONS                                                               
*                                                                               
UNB20    CLC   PBUYKPRD,=C'ZZZ'                                                 
         BNE   UNB30                                                            
*                                                                               
         LA    R3,PBYOZZZ                                                       
         MVI   ELCODE,X'21'                                                     
         LA    R2,PBUYREC+33                                                    
         BAS   RE,NEXTEL                                                        
UNB24    MVC   0(3,R3),2(R2)       PRD CODE                                     
         LA    R3,2(R3)                                                         
         CLI   0(R3),C' '                                                       
         BE    *+8                                                              
         LA    R3,1(R3)                                                         
*                                                                               
         TM    PBDWTSUM,X'80'      TEST UNEQUAL SPLIT                           
         BO    UNB26                                                            
         MVI   0(R3),C','          NO                                           
         LA    R3,1(R3)                                                         
         B     UNB28                                                            
*                                                                               
UNB26    DS    0H                                                               
         MVI   0(R3),C'-'                                                       
         LA    R3,1(R3)                                                         
*                                                                               
         SR    R0,R0                                                            
         IC    R0,5(R2)            COST SHARE                                   
         BAS   RE,FMTPEDT                                                       
*                                                                               
         CLC   5(1,R2),6(R2)       TEST COST=SPACE SHARE                        
         B     UNB28                                                            
*                                                                               
         IC    R0,6(R2)            SPACE SHARE                                  
         BCTR  R3,0                                                             
         MVI   0(R3),C'-'                                                       
         LA    R3,1(R3)                                                         
         BAS   RE,FMTPEDT                                                       
UNB28    BAS   RE,NEXTEL           GET NEXT ELEM                                
         BE    UNB24               GOT ONE                                      
         BCTR  R3,0                                                             
         MVI   0(R3),C' '          OBLITERATE LAST COMMA                        
*                                                                               
         B     UNB30                                                            
         EJECT                                                                  
FMTPEDT  MVI   BYTE,0                                                           
*                                                                               
FMTPEDT2 EDIT  (R0),(3,0(R3)),ALIGN=LEFT                                        
*                                                                               
         CLI   BYTE,0                                                           
         BNE   FMTPEDT4                                                         
         AR    R3,R0                                                            
         MVI   0(R3),C'/'                                                       
         LA    R3,1(R3)                                                         
*                                                                               
         MVI   BYTE,1              NOW FORMAT TOTAL WEIGHT                      
         SR    R0,R0                                                            
         IC    R0,PBDWTSUM                                                      
         N     R0,=F'127'                                                       
         B     FMTPEDT2                                                         
*                                                                               
FMTPEDT4 AR    R3,R0               POINT TO NEXT PRINT POSITION                 
         MVI   0(R3),C','                                                       
         LA    R3,1(R3)                                                         
         BR    RE                                                               
         SPACE 3                                                                
*        COMMENTS                                                               
PB34     DS    0H                                                               
UNB30    DS    0H                                                               
         TM    PBYOCTL,X'0C'       TEST ANY COMMENTS                            
         BNZ   UNB30C                                                           
         TM    PBYOCLT2,X'80'       SEE IF DOING POSITION INS                   
         BZ    PB40                                                             
         MVI   ELCODE,X'68'                                                     
         MVI   COMSW,C'N'                                                       
         B     UNB31                                                            
*                                                                               
UNB30C   MVI   ELCODE,X'66'        REGULAR                                      
         TM    PBYOCTL,X'08'                                                    
         BNZ   UNB31                                                            
         MVI   COMSW,C'N'                                                       
         MVI   ELCODE,X'67'        MUST BE IC COMMENTS                          
UNB31    LA    R2,PBUYREC+33                                                    
         LA    R3,PBYOCOMS                                                      
*                                                                               
UNB32    DS    0H                                                               
         BAS   RE,NEXTEL                                                        
         BNE   UNB40                                                            
         CLI   COMSW,C'Y'          TEST SKIP FIRST COM                          
         BNE   *+12                                                             
         MVI   COMSW,C'N'                                                       
         B     UNB32                                                            
*                                                                               
         ZIC   RF,1(R2)                                                         
         SH    RF,=H'3'                                                         
         BM    UNB32                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),2(R2)                                                    
         LA    R3,47(R3)                                                        
         B     UNB32                                                            
*                                                                               
         SPACE 3                                                                
* BEST FOOD DAY OR WEEK OF COMMENT                                              
*                                                                               
PB40     DS    0H                                                               
UNB40    DS    0H                                                               
         CLI   PBDBFD,C'B'                                                      
         BNE   UNB44                                                            
         LA    R2,=C'BFD'                                                       
         LA    R3,3-1                                                           
         BAS   RE,SRCHCOM                                                       
         BE    UNB50                                                            
         LA    R2,=C'BEST FOOD DAY'                                             
         LA    R3,13-1                                                          
         BAS   RE,SRCHCOM                                                       
         BE    UNB50                                                            
UNB42    DS    0H                                                               
         MVC   PBYOBFD(21),=C'BEST FOOD DAY WEEK OF'                            
         MVC   PBYOBFD+22(5),PBYOINS                                            
         B     UNB50                                                            
*                                                                               
UNB44    DS    0H                                                               
         CLI   PBDBFD,C'W'                                                      
         BNE   UNB50                                                            
         LA    R2,=C'W/O '                                                      
         LA    R3,4-1                                                           
         BAS   RE,SRCHCOM                                                       
         BE    UNB50                                                            
         LA    R2,=C'WO '                                                       
         LA    R3,3-1                                                           
         BAS   RE,SRCHCOM                                                       
         BE    UNB50                                                            
         LA    R2,=C'WEEK OF'                                                   
         LA    R3,7-1                                                           
         BAS   RE,SRCHCOM                                                       
         BE    UNB50                                                            
UNB46    DS    0H                                                               
         MVC   PBYOBFD(7),=C'WEEK OF'                                           
         MVC   PBYOBFD+8(5),PBYOINS                                             
*                                  DETERMINE LINES NEEDED                       
PB50     DS    0H                                                               
UNB50    DS    0H                                                               
         LA    R4,1                                                             
         CLC   PBYOSPC2,SPACES     2ND SPACE                                    
         BNE   UNB50B                                                           
         CLI   PBYOPRM,C' '                                                     
         BNE   UNB50B                                                           
         CLI   PBYOINS2,C' '                                                    
         BE    *+8                                                              
UNB50B   DS    0H                                                               
         LA    R4,1(R4)                                                         
         CLI   PBYOZZZ,C' '        ZZZ                                          
         BE    *+8                                                              
         LA    R4,1(R4)                                                         
         CLI   PBYOBFD,C' '        BFT                                          
         BE    *+8                                                              
         LA    R4,1(R4)                                                         
         LA    R0,5                COUNT COMMENTS                               
         LA    R5,PBYOCOMS                                                      
UNB51    DS    0H                                                               
         CLI   0(R5),C' '                                                       
         BE    UNB52                                                            
         LA    R4,1(R4)                                                         
         LA    R5,47(R5)                                                        
         BCT   R0,UNB51                                                         
*                                                                               
*                                                                               
UNB52    DS    0H                                                               
         ST    R4,PBYOLNS                                                       
         B     EXIT                                                             
         SPACE 3                                                                
SRCHCOM  DS    0H                                                               
         LA    R5,PBYOCOMS                                                      
         LA    R7,47-1(R5)                                                      
         SR    R7,R3               R7= LAST PLACE TO LOOK                       
         LA    R6,1                                                             
SC2      DS    0H                                                               
         EX    R3,SCCOMP                                                        
         BER   RE                                                               
         BXLE  R5,R6,SC2                                                        
         BR    RE                                                               
SCCOMP   CLC   0(0,R5),0(R2)                                                    
         EJECT                                                                  
*        OUTDOOR SPACE FORMATTING                                               
         SPACE 2                                                                
CKSPC    NTR1                                                                   
         SPACE 2                                                                
         LA    R3,PBYOSPC                                                       
         CLI   0(R5),X'FF'                                                      
         BNE   CKSP8                                                            
         USING SPACED,R5                                                        
         CP    SPSHOW,=P'0'                                                     
         BNE   CKSP                                                             
         CP    SPREG,=P'0'                                                      
         BNE   CKSP                                                             
         CP    SPILLUM,=P'0'                                                    
         BNE   CKSP                                                             
         B     CKSPX          ALL ZERO SO EXIT                                  
CKSP     DS    0H                                                               
         TM    PBYOCTL,X'40'                                                    
         BNZ   CKSP6                                                            
         CP    SPSHOW,=P'0'                                                     
         BE    CKSP1                                                            
         LR    R6,R3                                                            
         CP    SPSHOW,=P'99999'       SPECIAL CODE                              
         BNE   CKSP0                                                            
         MVC   0(3,R3),=C'SPC'                                                  
         LA    R0,3                                                             
         B     CKSP0X                                                           
*                                                                               
CKSP0    EDIT  SPSHOW,(4,0(R3)),0,ALIGN=LEFT                                    
CKSP0X   AR    R6,R0                                                            
         MVC   1(7,R6),=C'SHOWING'                                              
*                                                                               
CKSP1    LR    R6,R3                                                            
         CLC   0(20,R6),SPACES     SEE IF 1ST LINE USED                         
         BE    *+8                                                              
         LA    R6,20(R6)                                                        
         CP    SPREG,=P'0'                                                      
         BNE   CKSP1B                                                           
         CP    SPILLUM,=P'0'                                                    
         BE    CKSPX                                                            
         LA    R0,1                                                             
         MVI   0(R6),C'0'                                                       
         B     CKSP1D                                                           
CKSP1B   DS    0H                                                               
         EDIT  SPREG,(4,0(R6)),0,ALIGN=LEFT                                     
*                                                                               
CKSP1D   DS    0H                                                               
         AR    R6,R0                                                            
         MVC   1(3,R6),=C'REG'                                                  
         MVI   4(R6),C','                                                       
         LA    R6,6(R6)                                                         
         LA    R0,1                                                             
         MVI   0(R6),C'0'                                                       
         CP    SPILLUM,=P'0'                                                    
         BE    CKSP4                                                            
         EDIT  SPILLUM,(4,0(R6)),0,ALIGN=LEFT                                   
CKSP4    DS    0H                                                               
         AR    R6,R0                                                            
         MVC   1(5,R6),=C'ILLUM'                                                
         B     CKSPX                                                            
*                                                                               
CKSP6    EDIT  SPSHOW,(4,0(R3)),0  OUTPUT DESIRED IS SSSS RRRR  IIII            
         CP    SPSHOW,=P'99999'                                                 
         BNE   *+10                                                             
         MVC   0(4,R3),=C' SPC'                                                 
         EDIT  SPREG,(4,5(R3)),0                                                
         OI    8(R3),C'0'                                                       
         EDIT  SPILLUM,(4,11(R3)),0                                             
         OI    14(R3),C'0'                                                      
         B     CKSPX                                                            
*                                                                               
CKSP8    MVC   0(17,R3),0(R5)      MOVE SPACE DESC TO FIRST LINE                
         OC    0(20,R3),SPACES                                                  
         MVC   20(20,R3),SPACES    CLEAR LINE 2                                 
         CLI   PBYOINPT,C'N'       TEST NEWS                                    
         BNE   CKSPX                                                            
         LR    R6,R5                                                            
         LR    R7,R3                                                            
         CLI   0(R6),C'S'          CHK FOR SAU                                  
         BNE   CKSPX                                                            
         CLI   1(R6),C'0'          MUST START WITH NUMBERIC                     
         BL    CKSPX                                                            
         MVC   0(17,R7),SPACES     CLEAR                                        
         MVC   0(3,R7),=C'SAU'                                                  
         MVC   4(5,R7),1(R6)       MOVE SAU NUMBER                              
         OC    4(5,R7),SPACES                                                   
         CLC   6(11,R6),SPACES     CHK FOR MOVE DATA                            
         BNH   CKSPX                                                            
         LA    R4,8(R7)            FIND LAST NON-BLANK                          
CKSP9    CLI   0(R4),C' '                                                       
         BH    CKSP10                                                           
         BCTR  R4,0                                                             
         B     CKSP9                                                            
*                                                                               
CKSP10   LA    R5,10                                                            
         LA    R6,6(R6)                                                         
CKSP11   CLI   0(R6),C' '          FIND FIRST NON BLANK                         
         BH    CKSP12              MOVE                                         
         LA    R6,1(R6)                                                         
         BCT   R5,CKSP11                                                        
         B     CKSPX                                                            
*                                                                               
CKSP12   EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   2(0,R4),0(R6)                                                    
         MVC   20(20,R3),SPACES    RECLEAR LINE 2                               
*                                                                               
CKSPX    DS    0H                                                               
         B     EXIT                                                             
         SPACE 3                                                                
NEXTEL   DS    0H                                                               
         ZIC   R0,1(R2)                                                         
         AR    R2,R0                                                            
         CLI   0(R2),0                                                          
         BE    NEXTEL2                                                          
         CLC   ELCODE,0(R2)                                                     
         BER   RE                                                               
         B     NEXTEL                                                           
NEXTEL2  DS    0H                                                               
         LTR   RE,RE                                                            
         BR    RE                                                               
         SPACE 3                                                                
SETNSPC  DS    0H                  SET NEWS SPACE                               
         CLI   0(R6),C'S'          SAU BUY                                      
         BNE   SS4                                                              
         CLI   1(R6),C'0'          2ND CHAR MUST BE NUMBER                      
         BL    SS4                                                              
         MVC   0(3,R7),=C'SAU'                                                  
         MVC   4(10,R7),1(R6)                                                   
         B     SS6                                                              
*                                                                               
SS4      DS    0H                                                               
         MVC   0(10,R7),0(R6)                                                   
SS6      DS    0H                                                               
         OC    0(20,R7),SPACES                                                  
         BR    RE                                                               
         SPACE 3                                                                
EXIT     DS    0H                                                               
         XIT1                                                                   
         SPACE 3                                                                
ALPHTAB  DS    0H                                                               
         DC    C'ABCDEFGHIJKLMNOP',X'FF'                                        
         SPACE 2                                                                
         LTORG                                                                  
         SPACE 3                                                                
WKD      DSECT                                                                  
DMCB     DS    6F                                                               
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    C                                                                
         DS    0F                                                               
WORK     DS    CL20                                                             
WORK2    DS    CL20                                                             
SPACES   DS    CL20' '                                                          
ELCODE   DS    X                                                                
COMSW    DS    C                                                                
W        DS    CL20                                                             
*                                                                               
SPACED   DSECT                                                                  
         DS    CL1                                                              
SPSHOW   DS    PL3                                                              
SPREG    DS    PL3                                                              
SPILLUM  DS    PL3                                                              
         EJECT                                                                  
*                                                                               
       ++INCLUDE PPBYOUTD                                                       
*                                                                               
         SPACE 2                                                                
*                                                                               
       ++INCLUDE PBUYREC                                                        
*                                                                               
       ++INCLUDE PBDELEM                                                        
*                                                                               
         SPACE 2                                                                
*                                                                               
       ++INCLUDE PVALUES                                                        
*                                                                               
DATCON   EQU   PBYODTCN                                                         
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'061PPBYOUTS  05/01/02'                                      
         END                                                                    
