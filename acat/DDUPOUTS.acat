*          DATA SET DDUPOUTS   AT LEVEL 009 AS OF 05/01/02                      
*CATALP UPOUT                                                                   
         TITLE 'MODULE TO EDIT UPGRADE EXPRESSION'                              
UPOUT    CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 20,*UPOUT**                                                      
         USING UPD,RC                                                           
         USING RAVLNEL,RA                                                       
         L     RA,0(R1)            A(UPGRADE ELEMENT)  PARA1 1                  
         LR    R9,R1               A(OUTPUT START)           2                  
         SPACE 2                                                                
         MVI   AREA,C' '           CLEAR A SCRATCH PAD                          
         MVC   AREA+1(79),AREA                                                  
         LA    R3,AREA                                                          
         CLI   RAVLNTYP,2          SELECT UPGRADE TYPE                          
         BE    UP2                                                              
         BL    UPEND                                                            
         CLI   RAVLNTYP,6                                                       
         BE    UP14                                                             
         CLI   RAVLNTYP,4                                                       
         BE    UP10                                                             
         BH    UPEND                                                            
         CLI   RAVLNCAT,C'P'                                                    
         BE    UP4                                                              
         OC    RAVLNOP1,RAVLNOP1                                                
         BZ    UP12                                                             
         CLI   RAVLNOP1,0                                                       
         BNE   UP6                                                              
         B     UP8                                                              
         EJECT                                                                  
*              HANDLE THE EXPRESSIONS                                           
         SPACE 3                                                                
UP2      MVC   0(30,R3),=C'DEMOS. ADJUSTED TO A RATING OF'                      
         LA    R3,31(R3)                                                        
         SPACE 1                                                                
UP3      LH    R1,RAVLNOP1                                                      
         BAS   RE,LEFT                                                          
         LH    R1,RAVLNOP2                                                      
         LTR   R1,R1                                                            
         BZ    UPEND                                                            
         CLI   RAVLNOP2,0                                                       
         BE    UP3B                                                             
         MVC   0(03,R3),=C'AND'                                                 
         LA    R3,4(R3)                                                         
         LA    R1,RAVLNOP2                                                      
         BAS   RE,BOOK                                                          
         MVC   0(5,R3),=C'SHARE'                                                
         LA    R3,6(R3)                                                         
         B     UPEND                                                            
         SPACE 1                                                                
UP3B     MVC   0(14,R3),=C'AND A SHARE OF'                                      
         LA    R3,15(R3)                                                        
         BAS   RE,LEFT                                                          
         B     UPEND                                                            
         SPACE 1                                                                
UP4      LA    R1,RAVLNOP2                                                      
         BAS   RE,BOOK                                                          
         MVC   0(38,R3),=C'HOUSEHOLD AND DEMO SHARES PROJECTED TO'              
         LA    R3,39(R3)                                                        
         LA    R1,RAVLNOP1                                                      
         BAS   RE,BOOK                                                          
         MVC   0(10,R3),=C'HPT LEVELS'                                          
         LA    R3,10(R3)                                                        
         B     UPEND                                                            
         SPACE 1                                                                
UP6      LA    R1,RAVLNOP2                                                      
         BAS   RE,BOOK                                                          
         MVC   0(28,R3),=C'HOUSEHOLD SHARE PROJECTED TO'                        
         LA    R3,29(R3)                                                        
         LA    R1,RAVLNOP1                                                      
         BAS   RE,BOOK                                                          
         MVC   0(29,R3),=C'HUT LEVEL WITH DEMOS ADJUSTED'                       
         LA    R3,30(R3)                                                        
         B     UPEND                                                            
         SPACE 1                                                                
UP8      MVC   0(33,R3),=C'DEMOS. ADJUSTED TO A HUT LEVEL OF'                   
         LA    R3,34(R3)                                                        
         B     UP3                                                              
         SPACE 1                                                                
UP10     MVC   0(30,R3),=C'DEMOS. ADJUSTED BY AN INDEX OF'                      
         LA    R3,31(R3)                                                        
         LH    R1,RAVLNOP1                                                      
         BAS   RE,LEFT                                                          
         B     UPEND                                                            
         SPACE 1                                                                
UP12     MVC   0(29,R3),=C'DEMOS. ADJUSTED TO A SHARE OF'                       
         LA    R3,30(R3)                                                        
         LH    R1,RAVLNOP2                                                      
         BAS   RE,LEFT                                                          
         B     UPEND                                                            
         SPACE 1                                                                
UP14     LA    R1,RAVLNOP2                                                      
         BAS   RE,BOOK                                                          
         MVC   0(27,R3),=C'HOUSEHOLD AND DEMO SHARES X'                         
         LA    R3,28(R3)                                                        
         LA    R1,RAVLNOP1                                                      
         BAS   RE,BOOK                                                          
         MVC   0(22,R3),=C'HPT LEVELS FACTORED BY'                              
         LA    R3,23(R3)                                                        
         LH    R1,RAVLNOP3                                                      
         BAS   RE,LEFT                                                          
         SPACE 1                                                                
UPEND    LA    R4,AREA                                                          
         SR    R3,R4                                                            
         STC   R3,4(R9)                                                         
         LTR   R3,R3                                                            
         BZ    UPEND2                                                           
         L     R4,4(R9)                                                         
         BCTR  R3,0                                                             
         EX    R3,*+8                                                           
         B     UPEND2                                                           
         MVC   0(0,R4),AREA                                                     
         SPACE 1                                                                
UPEND2   XIT1                                                                   
         EJECT                                                                  
*              SUBSIDIARY AIDS, DSECT                                           
         SPACE 3                                                                
LEFT     EDIT  (R1),(3,(R3)),ALIGN=LEFT                                         
         AR    R3,R0                                                            
         LA    R3,1(R3)                                                         
         BR    RE                                                               
         SPACE 1                                                                
BOOK     ZIC   RF,1(R1)            MONTH                                        
         CLI   0(R1),0                                                          
         BER   RE                                                               
         CLI   1(R1),0                                                          
         BER   RE                                                               
         CLI   1(R1),12                                                         
         BHR   RE                                                               
         BCTR  RF,0                                                             
         MH    RF,=H'3'                                                         
         LA    RF,MONTHS(RF)                                                    
         MVC   0(3,R3),0(RF)                                                    
         ZIC   RF,0(R1)                                                         
         EDIT  (RF),(2,3(R3))      YEAR                                         
         LA    R3,6(R3)                                                         
         BR    RE                                                               
         SPACE 1                                                                
MONTHS   DC    C'JANFEBMARAPRMAYJUN'                                            
         DC    C'JULAUGSEPOCTNOVDEC'                                            
         SPACE 1                                                                
UPD      DSECT                                                                  
AREA     DS    CL80                                                             
DUB      DS    D                                                                
WORK     DS    CL64                                                             
         EJECT                                                                  
       ++INCLUDE REGENAVL                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'009DDUPOUTS  05/01/02'                                      
         END                                                                    
