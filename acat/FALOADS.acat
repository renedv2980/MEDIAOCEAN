*          DATA SET FALOADS    AT LEVEL 001 AS OF 06/21/99                      
*CATALP FALOAD31                                                                
         TITLE 'FALOAD - LOAD OTHER FACPAK PHASES AND INITIALISE'               
         PRINT NOGEN                                                            
FALOAD   CSECT                                                                  
         ENTRY LOADPARM                                                         
         NBASE 0,**FALOAD,WORK=AWORK,SAVE=YES                                   
*                                                                               
         MVC   LOADPARM(LOADPARX-LOADPARM),0(R1)                                
         MVC   VHICORE,LOADHICO                                                 
         DATE  LOADDATE                                                         
*                                                                               
         L     R3,VSYSFAC          R3=A(SYSFAC LIST)                            
         USING SYSFACD,R3                                                       
         B     LOADPAR                                                          
*                                                                               
AWORK    DC    A(WORK)                                                          
*                                                                               
LOADPAR  LA    R0,FAPARMS          LOAD FACPAK PARM LIST                        
         GOTO1 VLOADER,DMCB,FAPARM,(R0),(C'A',(R0))                             
         OC    4(4,R1),4(R1)                                                    
         BNZ   SETNET                                                           
*                                                                               
         LA    R0,ERROR1           FAPARMS NOT FOUND                            
         GOTO1 =V(DMOD000),DMCB,V(WCTYPE),(R0),40                               
         B     LOADEOJ             EXIT BACK TO CALLER                          
*                                                                               
ERROR1   DC    CL40'*FACPAK* FAPARMS PHASE NOT FOUND'                           
         EJECT                                                                  
*=====================================================================*         
* SET UP VTAM NETWORK TABLES                                          *         
*=====================================================================*         
         SPACE 1                                                                
SETNET   L     RE,VSSB             CREATE VTAM NETWORK TABLES                   
         USING SSBD,RE                                                          
         MVC   SSBVTID,VTAMAPL     SET VTAM APPLICATION ID IN SSB               
         DROP  RE                                                               
*                                                                               
         XR    RF,RF                                                            
         ICM   RF,1,FATASKS        SET UP TASK COUNT OVERRIDE IF SET            
         BZ    *+8                                                              
         ST    RF,TSKNUM                                                        
         EJECT                                                                  
*=====================================================================*         
* SET UP UTL ENTRIES                                                  *         
*=====================================================================*         
         SPACE 1                                                                
SETUTL   CLI   FATOR,C'Y'          TOR?                                         
         BE    *+12                YES                                          
         L     RE,TSKNUM           AORS HAVE 1 UTL PER TASK ONLY                
         STCM  RE,3,VTAMUTL                                                     
                                                                                
         XR    RE,RE               GET NUMBER OF UTL ENTRIES REQUIRED           
         ICM   RE,3,VTAMUTL                                                     
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    RF,TUTLLENV         GET UTL ENTRY LENGTH                         
         L     R2,VHICORE                                                       
         LA    R2,7(R2)                                                         
         SRL   R2,3                                                             
         SLL   R2,3                MAKE MODULO 8                                
*                                                                               
         MVC   0(L'IUTL,R2),IUTL                                                
         XC    12(4,R2),12(R2)     ADR OF END OF UTL AT VUTL-10(4)              
         LA    R2,16(R2)                                                        
         XC    0(4,R2),0(R2)       ACTUAL NUM OF TRMS AT VUTL-6(4)              
         STH   RE,4(R2)            MAXIMUM NUM OF TRMS AT VUTL-2(2)             
         LA    R2,6(R2)                                                         
         ST    R2,VUTL                                                          
         STH   RF,0(R2)            ENTRY LENGTH AT VUTL+0(2)                    
         LA    R1,5(R2)                                                         
         ST    R1,2(R2)            ADR OF LAST-1 AT VUTL+2(4)                   
         MR    RE,RE                                                            
         LA    RE,6(R2)            RE=A(FIRST ENTRY),RF=TOTAL LENGTH            
*                                                                               
         LA    R1,0(RE,RF)         POINT TO END OF UTL                          
         ST    R1,VHICORE                                                       
         AHI   R2,-10                                                           
         ST    R1,0(R2)            ADR OF END OF UTL AT VUTL-10(4)              
*                                                                               
         XR    R1,R1                                                            
         MVCL  RE,R0               CLEAR UTL ENTRIES                            
         EJECT                                                                  
*=====================================================================*         
* SET UP PRQ ENTRIES                                                  *         
*=====================================================================*         
         SPACE 1                                                                
SETPRQ   CLI   FATOR,C'Y'          TOR?                                         
         BNE   SETPRQE             NO - AORS HAVE NO PRQ ENTRIES                
*                                                                               
         XR    RE,RE               GET NUMBER OF PRQ ENTRIES REQUIRED           
         ICM   RE,3,VTAMPRQ                                                     
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R0,PRQDL            GET DEFAULT ENTRY WIDTH                      
         LA    RF,PRQDL            GET AVERAGE ENTRY WIDTH                      
         L     R2,VHICORE                                                       
         LA    R2,7(R2)                                                         
         SRL   R2,3                                                             
         SLL   R2,3                                                             
         MVC   0(L'IPRQ,R2),IPRQ                                                
         XC    12(4,R2),12(R2)     ADR OF END OF PRQ AT VPRQ-10(4)              
         LA    R2,16(R2)                                                        
         XC    0(4,R2),0(R2)       ACTUAL NUM OF PRNS AT VPRQ-6(4)              
         STH   RE,4(R2)            MAXIMUM NUM OF PRNS AT VPRQ-2(2)             
         LA    R2,6(R2)                                                         
         ST    R2,VPRQ                                                          
         STH   R0,0(R2)            ENTRY LENGTH AT VPRQ+0(2) (DEFAULT)          
         LA    R1,5(R2)                                                         
         ST    R1,2(R2)            ADR OF LAST-1 AT VPRQ+2(4)                   
         MR    RE,RE                                                            
         LA    RE,6(R2)            RE=A(FIRST ENTRY),RF=TOTAL LENGTH            
         LA    R1,0(RE,RF)         POINT TO END OF PRQ                          
         ST    R1,VHICORE                                                       
         AHI   R2,-10                                                           
         ST    R1,0(R2)            ADR OF END OF PRQ AT VPRQ-10(4)              
*                                                                               
         XR    R1,R1                                                            
         MVCL  RE,R0               CLEAR PRQ ENTRIES                            
         EJECT                                                                  
*=====================================================================*         
* SET UP PRQE ENTRIES                                                 *         
*=====================================================================*         
         SPACE 1                                                                
SETPRQE  CLI   FATOR,C'Y'          TOR?                                         
         BNE   EXECSTR             NO - AORS HAVE NO PRQE ENTRIES               
*                                                                               
         XR    RE,RE               GET NUMBER OF PRQE ENTRIES PER PRQ           
         ICM   RE,3,VTAMPRQE                                                    
         BNZ   *+8                                                              
         LA    RE,16               USE 16 IF NOT DEFINED                        
*                                                                               
         XR    RF,RF                                                            
         ICM   RF,3,VTAMPRQ        MULTIPLY BY NUM OF PRQ ENTRIES               
         MR    RE,RE                                                            
         LR    RE,RF                                                            
         LA    RF,L'PNTRY          GET ENTRY WIDTH                              
         L     R2,VHICORE                                                       
         LA    R2,7(R2)                                                         
         SRL   R2,3                                                             
         SLL   R2,3                                                             
         MVC   0(L'IPQE,R2),IPQE                                                
         XC    12(4,R2),12(R2)     ADR OF END OF PRQE AT VPRQE-10(4)            
         LA    R2,16(R2)                                                        
         XC    0(4,R2),0(R2)       ACTUAL NUM OF PQES AT VPRQE-6(4)             
         STH   RE,4(R2)            MAXIMUM NUM OF PQES AT VPRQE-2(2)            
         LA    R2,6(R2)                                                         
         ST    R2,VPRQENTS                                                      
         STH   RF,0(R2)            ENTRY LENGTH AT VPRQE+0(2)                   
         MR    RE,RE                                                            
         LA    RE,6(R2)            RE=A(FIRST ENTRY),RF=TOTAL LENGTH            
         LA    R1,0(RE,RF)         POINT TO END OF PRQE                         
         ST    R1,VHICORE                                                       
         BCTR  R1,0                                                             
         ST    R1,2(R2)            ADR OF LAST-1 AT VPRQ+2(4)                   
         AHI   R2,-10                                                           
         LA    R1,1(R1)                                                         
         ST    R1,0(R2)            ADR OF END OF PRQE AT VPRQE-10(4)            
*                                                                               
         XR    R1,R1                                                            
         MVCL  RE,R0               CLEAR PQE ENTRIES                            
         EJECT                                                                  
*=====================================================================*         
* CALL FASTART TO INITIALISE FACPAK                                   *         
*=====================================================================*         
         SPACE 1                                                                
EXECSTR  GOTO1 VSTART,FAPLIST      GO TO START UP PROGRAM (FASTART)             
         OC    0(4,R1),0(R1)                                                    
*        BZ    LOADEOJ                                                          
         EJECT                                                                  
*=====================================================================*         
* BUILD LOCAL (2K) TBUFFS OVER TOP OF FASTART                         *         
*=====================================================================*         
         SPACE 1                                                                
         L     R2,VTCB             BUILD 24-BIT (2K) TBUFF AREAS                
         LH    R4,0(R2)            OVER TOP OF FASTART                          
         L     R5,2(R2)                                                         
         LA    R2,6(R2)                                                         
         USING TCBD,R2                                                          
*                                                                               
         L     R7,VSTART                                                        
         LA    R7,15(R7)                                                        
         SRL   R7,4                                                             
         SLL   R7,4                                                             
         MVC   0(L'I2KTB,R7),I2KTB                                              
         LA    R7,16(R7)                                                        
*                                                                               
FXTCB02  LR    R0,R7                                                            
         LHI   R1,32+TBUFFL                                                     
         XR    RF,RF                                                            
         MVCL  R0,RE               CLEAR TBUFF AREA                             
*                                                                               
         LA    R1,32(R7)           SET A(TBUFF)                                 
         STCM  R1,15,TCBTBUFF                                                   
         AHI   R7,32+TBUFFL        NEXT TBUFF AREA                              
         BXLE  R2,R4,FXTCB02                                                    
         DROP  R2                                                               
         EJECT                                                                  
*=====================================================================*         
* BEGIN EXECUTION OF FACPAK                                           *         
*=====================================================================*         
         SPACE 1                                                                
EXECFAC  L     RF,VADFIRST         GO TO TASK CONTROLLER                        
         XR    R1,R1                                                            
         BASR  RE,RF                                                            
         B     LOADEOJ             EXIT BACK TO CALLER                          
         EJECT                                                                  
*=====================================================================*         
* END EXECUTION                                                       *         
*=====================================================================*         
         SPACE 1                                                                
LOADEOJ  CLI   FAPROT,C'Y'         TEST STORAGE PROTECTION ENABLED              
         BE    LOADEOJ1                                                         
         XBASE                                                                  
*                                                                               
LOADEOJ1 PR                                                                     
         EJECT                                                                  
*=====================================================================*         
* LITERALS AND CONSTANTS                                              *         
*=====================================================================*         
         SPACE 1                                                                
MAXTASKS EQU   35                                                               
         LTORG                                                                  
*                                                                               
IUTL     DC    CL12'*UTL*UTL*UTL'                                               
IPRQ     DC    CL12'*PRQ*PRQ*PRQ'                                               
IPQE     DC    CL12'*PQE*PQE*PQE'                                               
I2KTB    DC    CL16'*TCBTBUFF*TCBTBUFF'                                         
*                                                                               
DMCB     DS    6F                                                               
*                                                                               
VSTART   DC    V(FASTART)                                                       
*                                                                               
FAPLIST  DC    A(LOADPARM)                                                      
         DC    A(FAPARMS)                                                       
*                                                                               
         DS    0D                  CONSTANTS AND ADCONS FOR FASTART             
         DC    CL8'LOADPARM'                                                    
*                                                                               
LOADPARM DS    0F                  PARAMS PASSED FROM FABOOT                    
LOADCMRG DC    A(0)                ADDR OF DOS COMRG/MVS PARAM LIST             
LOADUPSI DC    F'0'                UPSI VALUE                                   
LOADLOCO DC    A(0)                LOW  CORE OF FACPAK PHASE                    
LOADHICO DC    A(0)                HIGH CORE OF FACPAK PHASE                    
LOADHICX DC    A(0)                HIGH CORE OF DOS PARTN/MVS ADRSPC            
FACPAK   DC    CL8' '              MODULE NAME FACPAK                           
         DC    CL8' '              MODULE NAME FASTART                          
         DC    XL8'00'             N/D                                          
FAPARM   DC    CL8' '              MODULE NAME RUN PARAMETERS                   
FATASKS  DC    XL1'00'             OVERRIDE NUMBER OF TASKS                     
FAPROT   DC    CL1' '              FACPAK STORAGE PROTECTION FLAG               
FATOR    DC    CL1'  '             TOR STATUS                                   
         DC    XL5'00'             N/D                                          
MSGLIB   DC    CL8' '              ANSWER LOAD PROGRAM LIB                      
MSGRST   DC    CL8' '              ANSWER IS THIS A RESTART                     
MSGDAY   DC    CL8' '              ANSWER DAY VALUE                             
MSGSYSO  DC    CL8' '              ANSWER SYSTEM OVERRIDE                       
MSGLINO  DC    CL8' '              ANSWER LINE OVERRIDE                         
LOADPARX DS    0F                                                               
*                                                                               
LOADWORK DC    A(WORK)             PARAMS PASSED FROM FALOAD                    
LOADDATE DC    CL8' '                                                           
VHICORE  DC    A(0)                                                             
VBUFFERS DC    A(0)                                                             
VBUFFERX DC    A(0)                                                             
VADFIRST DC    V(ADFIRST)                                                       
VBOOT    DC    A(FALOAD)                                                        
VCTFILE  DC    V(CTFIL1)                                                        
VDATCON  DC    V(DATCON)                                                        
         DC    A(0)                                                             
VISDDS   DC    V(ISDDS)                                                         
VISREAD  DC    V(ISREAD)                                                        
VGETDAY  DC    V(GETDAY)                                                        
VLOADER  DC    V(LOADER)                                                        
         DC    A(0)                                                             
VPOWWOWS DC    V(POWWOWS)                                                       
VPQOPEN  DC    V(FAPQOPEN)                                                      
VRDTRK   DC    V(RDTRK)                                                         
VSYSFAC  DC    V(SYSFAC)                                                        
VSYSFLES DC    V(SYSFLES)                                                       
VUPDID   DC    V(UPDID)                                                         
VWORKER  DC    V(WORKER)                                                        
VWTERASE DC    V(WTERASE)                                                       
VHEXOUT  DC    V(HEXOUT)                                                        
         EJECT                                                                  
         DS    0D                                                               
         DC    CL8'LOADWORK'                                                    
WORK     DC    100D'0'                                                          
         SPACE 1                                                                
         DS    0D                                                               
         DC    CL8'LOADVALS'                                                    
*FAPARMDEF                                                                      
       ++INCLUDE FAPARMDEF                                                      
         EJECT                                                                  
*=====================================================================*         
* INCLUDED DSECTS                                                     *         
*=====================================================================*         
         SPACE 1                                                                
*FASSB                                                                          
         PRINT OFF                                                              
       ++INCLUDE FASSB                                                          
         PRINT ON                                                               
*FAUTL                                                                          
         PRINT OFF                                                              
       ++INCLUDE FAUTL                                                          
         PRINT ON                                                               
*FATCB                                                                          
         PRINT OFF                                                              
       ++INCLUDE FATCB                                                          
         PRINT ON                                                               
*FAPRQ                                                                          
         PRINT OFF                                                              
       ++INCLUDE FAPRQ                                                          
         PRINT ON                                                               
*FASYSFAC                                                                       
         PRINT OFF                                                              
       ++INCLUDE FASYSFAC                                                       
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'001FALOADS   06/21/99'                                      
         END                                                                    
