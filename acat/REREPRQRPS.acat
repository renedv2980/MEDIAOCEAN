*          DATA SET REREPRQRPS AT LEVEL 054 AS OF 05/01/02                      
*CATALP RERQRPA                                                                 
         TITLE 'REREPRQRP - RERQRP - REP VALIDATE/PRINT REQUEST'                
*******************************************************************             
*                                                                 *             
*  REREPRQRP --- PRINT OUT DETAILS OF REQUEST.                    *             
*                                                                 *             
*  CATALOG DATA SET: RERQRP(A)                                    *             
*                                                                 *             
* --------------------------------------------------------------- *             
*                                                                 *             
* >>>>>>>>>>>>>PROGRAM NOTE: IMPORTANT<<<<<<<<<<<<<<<<<<<<<<<<<<  *             
*                                                                 *             
* THIS MODULE DETERMINS THE 'REQTAB' TO USE FOR THIS REQUEST.     *             
* THE ADDRESS OF THE SELECTED TABLE IS PLACED IN VXREQTAB WHICH   *             
* IS DEFINED IN REXADDRD AND POINTED TO BY VXADDR.  REREPORT      *             
* USES THIS ADDRESS.  IF CHANGES ARE MADE TO THESE TABLES,        *             
* REREPORT WILL HAVE TO BE CHANGED AT THE SAME TIME.              *             
*                                                                 *             
* --------------------------------------------------------------- *             
*                                                                 *             
*  MOD LOG                                                        *             
*  -------                                                        *             
*  02/09/90  PJS  SUPPORT MULTIPLE REQUEST CARDS                  *             
*                                                                 *             
*  MAY24/90 (MRR) --- TURN OFF DETAILS OF REQUEST FOR A DOWNLOAD  *             
*                      REPORT                                     *             
*                                                                 *             
*  JUN29/90 (MRR) --- TURN DETAILS BACK ON                        *             
*                                                                 *             
*  AUG29/90 (MRR) --- >SWITCH PROGRAM NUMBER FOR ALL RRG RUNS     *             
*                     >2ND CARD SUPPORT: ADDITIONAL FIELDS AND    *             
*                       THE MOVE OF OPTION 6                      *             
*                     >MAKE R9 THE 2ND PROG BASE REG              *             
*                                                                 *             
*  SEP19/90 (MRR) --- CHANGE 'NETWORK CONTRACT' FROM NUMERIC TO   *             
*                      ALPHA                                      *             
*                                                                 *             
*  JAN03/91 (MRR) --- ADD DEFINITION FOR EXPANDED SPACE-END FLAG  *             
*                                                                 *             
*  JUL02/91 (MRR) --- TURN BOXES OFF FOR PRINTING DETAILS OF REQ  *             
*                                                                 *             
*  AUG26/91 (MRR) --- ADD 'R7' TO THE RRGTAB                      *             
*                     MAKE 30 (RADAR) ALSO 2 CARD NON-RRG         *             
*                                                                 *             
*  SEP20/91 (MRR) --- ADD RATING SERVICE, BOOK, DEMO AND SPACING  *             
*                      TO MULTI-CARD TABLES                       *             
*                                                                 *             
*  DEC20/91 (MRR) --- >PASS ADDRESS OF THE REQTAB VIA REXADDRD    *             
*                                                                 *             
*  JAN02/92 (MRR) --- >ADD RRG OPTIONS TO THE TABLE               *             
*                                                                 *             
*  MAR19/92 (MRR) --- >ADD MORE FIELDS TO THE TABLES              *             
*                                                                 *             
*  MAR12/93 (BU ) --- >SKIP CARD DISPLAY WHEN OTHER OUTPUT IS     *             
*                      ALSO SKIPPED.                              *             
*                                                                 *             
*******************************************************************             
*                                                                               
         PRINT NOGEN                                                            
REREQREP CSECT                                                                  
         NMOD1 128,**RQRP**,R9                                                  
         USING REQREPD,RC                                                       
         L     RA,=V(REWORKC)      PARAMETER 1 RETURNED BY MODULE               
         USING WORKD,RA                        0=REQUEST OK                     
*                                              1=REQUEST ERROR                  
         LR    R2,R1                                                            
         XC    0(4,R2),0(R2)                                                    
         MVC   PRSWITCH,RCREQREP                                                
*                                                                               
         L     RE,ABOX                                                          
         MVI   BOXOFF-BOXD(RE),C'Y'                                             
*                                                                               
         BAS   RE,HEADS                PRINT HEADLINE                           
         BAS   RE,PICKTAB              PICK THE RIGHT PROCESS TABLE             
         BAS   RE,PROCESS              PROCESS THE REQUEST CARD                 
         CLI   PRSWITCH,C'N'       OTHER OUTPUT SKIPPED?                        
         BE    MAIN50              YES - NO CARD OUTPUT EITHER                  
         CLI   MULTIC,0                                                         
         BE    MAIN50                                                           
         BAS   RE,DOMULTI                                                       
MAIN50   EQU   *                                                                
*                                                                               
         SR    R0,R0                                                            
         LTR   R0,R0                                                            
         XMOD1                                                                  
         EJECT                                                                  
*                   LOOP THROUGH TABLE AND HANDLE REQUESTS                      
         SPACE 2                                                                
PROCESS  NTR1                                                                   
*                                                                               
         L     R3,ATABLE                                                        
*                                                                               
LOOP     CLC   0(3,R3),=C'END'                                                  
         BE    PROCEXIT                                                         
         SPACE                                                                  
         PACK  RQDUB,0(3,R3)       GET ADDRESS AND LENGTH OF FIELD              
         CVB   R4,RQDUB                                                         
         A     R4,ACARDS                                                        
         BCTR  R4,0                                                             
         PACK  RQDUB,3(2,R3)                                                    
         CVB   R5,RQDUB                                                         
         BCTR  R5,R0                                                            
         CLI   5(R3),C'N'          NUMERIC TESTS                                
         BNE   LOOP4                                                            
         EX    R5,SPACOMP          SPACES OK                                    
         BE    LOOP6                                                            
         CLC   0(3,R4),=C'ALL'     AND SO IS ALL                                
         BE    LOOP6                                                            
         MVC   RQWORK(8),=8X'F0'                                                
         EX    R5,MUVZONES                                                      
         CLC   RQWORK(8),=8X'F0'                                                
         BE    LOOP6                                                            
         CLI   PRSWITCH,C'Y'                                                    
         BNE   ERRET                                                            
         MVC   P+69(17),=C'FIELD NOT NUMERIC'                                   
         MVI   3(R2),1                                                          
         B     LOOP6                                                            
         SPACE 2                                                                
ERRET    MVI   PRSWITCH,C'Y'                                                    
         B     PROCEXIT                                                         
         SPACE 2                                                                
SPACOMP  CLC   0(0,R4),SPACES                                                   
MUVZONES MVZ   RQWORK(0),0(R4)                                                  
MUVFIELD MVC   P+45(0),0(R4)                                                    
         SPACE 2                                                                
LOOP4    CLI   5(R3),C'D'          DATE TESTS                                   
         BNE   LOOP6                                                            
         CLC   0(4,R4),SPACES                                                   
         BE    LOOP6                                                            
         MVC   DATE,0(R4)                                                       
         CLC   DATE+4(2),=C'  '                                                 
         BNE   *+10                                                             
         MVC   DATE+4(2),=C'01'                                                 
         GOTO1 GETDAY,RQWORK,DATE,RQDUB                                         
         CLC   RQDUB(3),SPACES                                                  
         BNE   LOOP6                                                            
         CLI   PRSWITCH,C'Y'                                                    
         BNE   ERRET                                                            
         MVC   P+69(16),=C'NOT A VALID DATE'                                    
         MVI   3(R2),1                                                          
         SPACE 2                                                                
LOOP6    CLI   PRSWITCH,C'Y'       ONLY PRINT IF OPTION ON                      
         BNE   LOOP8                                                            
         EX    R5,SPACOMP          AND FIELD IS NON-SPACES                      
         BE    LOOP8                                                            
         MVC   P+24(20),7(R3)                                                   
         EX    R5,MUVFIELD                                                      
*                                                                               
         CLI   5(R3),C'L'          LOWER CASE FOR EXCLUDE FILTER                
         BNE   LOOP7                                                            
         CLI   0(R4),X'C0'         LOWER CASE                                   
         BH    LOOP7                                                            
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   P+46(0),P+45                                                     
         OI    P+46,X'40'         TURN ON UPPER CASE                            
         MVI   P+45,C'*'           INDICATE EXCLUDE                             
*                                                                               
LOOP7    BAS   RE,PRINT1                                                        
         SPACE 2                                                                
LOOP8    LA    R3,L'REQTAB(R3)                                                  
         B     LOOP                                                             
*                                                                               
PROCEXIT EQU   *                                                                
         SR    R0,R0                                                            
         LTR   R0,R0                                                            
         XIT1                                                                   
         EJECT                                                                  
*                   PRINT SOME MINIMAL HEADLINES                                
         SPACE 2                                                                
HEADS    NTR1                                                                   
*                                                                               
         CLI   PRSWITCH,C'Y'                                                    
         BNE   HEADEXIT                                                         
         MVC   P,SPACES                                                         
         MVC   P+24(6),=C'RUN ON'                                               
         GOTO1 DATCON,RQWORK,(4,RCDATE),(5,P+31)                                
         MVC   P+45(30),=C'DETAILS OF REQUEST      PAGE 1'                      
         GOTO1 PRINT,RQWORK,SPACES-1,=C'BC01'                                   
         BAS   RE,PRINT1                                                        
         MVC   P+45(30),=C'------------------      ------'                      
         MVC   P+28(2),=C'AT'                                                   
         TIME                                                                   
         SRL   R0,4                                                             
         LR    R1,R0                                                            
         XC    RQDUB,RQDUB                                                      
         ST    R1,RQWORK                                                        
         MVC   RQDUB+5(3),RQWORK                                                
         OI    RQDUB+7,X'0F'                                                    
         CVB   R3,RQDUB                                                         
         EDIT  (R3),(5,P+31),2,DUB=RQDUB,WRK=RQWORK                             
         BAS   RE,PRINT3                                                        
         SPACE 2                                                                
         MVC   P+24(25),=C'REQUEST FIELD        DATA'                           
         MVC   P+69(6),=C'ERRORS'                                               
         BAS   RE,PRINT1                                                        
         MVC   P+24(25),=C'-------------        ----'                           
         MVC   P+69(6),=C'------'                                               
         BAS   RE,PRINT3                                                        
*                                                                               
HEADEXIT EQU   *                                                                
         SR    R0,R0                                                            
         LTR   R0,R0                                                            
         XIT1                                                                   
         EJECT                                                                  
*                                                                               
* -----  PICKTAB - FIND THE REQUEST CARD(S) AND PICK THE RIGHT TABLE            
*                                                                               
PICKTAB  NTR1                                                                   
*                                                                               
         L     R2,=V(RQSTNUM)      HOW MANY CARDS?                              
         CLI   0(R2),2             2 OR MORE?                                   
         BL    PICK10              NO                                           
         L     R2,=V(RQSTCARD)                                                  
         MVI   MULTIC,1            SET MULTI-CARD REQUEST FLAG                  
         B     PICK20                                                           
PICK10   EQU   *                                                                
         LA    R2,QRECORD                                                       
         MVI   MULTIC,0            CLEAR MULTI-CARD REQUEST FLAG                
PICK20   EQU   *                                                                
         ST    R2,ACARDS                                                        
*                                                                               
         LA    RF,REQTAB                                                        
         CLC   0(2,R2),=C'17'      THE 17 IS SPECIAL                            
         BE    PICK100                                                          
         CLC   0(2,R2),=C'1C'      THE 1C IS ALSO SPECIAL                       
         BE    PICK100                                                          
         CLC   0(2,R2),=C'1D'      THE 1D IS ALSO SPECIAL                       
         BE    PICK100                                                          
         LA    R3,RRGTAB                                                        
PICK50   EQU   *                                                                
         CLI   0(R3),0                                                          
         BE    PICK60              NOT A RRG REPORT                             
         CLC   0(2,R2),0(R3)                                                    
         BE    PICK70              IS A RRG REPORT                              
         LA    R3,2(R3)                                                         
         B     PICK50              LOOP                                         
PICK60   EQU   *                                                                
         CLI   MULTIC,0            MULTI-CARD REGULAR REQUEST?                  
         BE    PICK100             NOPE, 1 CARD REGULAR                         
         LA    RF,REQ2TAB                                                       
         B     PICK100                                                          
PICK70   EQU   *                                                                
         LA    RF,RRGQTAB                                                       
         CLI   MULTIC,0                                                         
         BE    PICK100             SET TO 1 CARD RRG                            
         LA    RF,RRGQ2TAB         SET TO 2 CARD RRG                            
*                                                                               
PICK100  EQU   *                                                                
         ST    RF,ATABLE           STORE TABLE ADDR                             
         L     RE,VXADDR                                                        
         USING VXADDRD,RE                                                       
         ST    RF,VXREQTAB                                                      
         DROP  RE                                                               
*                                                                               
         SR    R0,R0                                                            
         LTR   R0,R0                                                            
         XIT1                                                                   
         EJECT                                                                  
*                                                                               
*- DOMULTI - FOR MULTIPLE CARD REQUESTS, PRINT OUT CARD IMAGES                  
*                                                                               
DOMULTI  NTR1                                                                   
*                                                                               
         BAS   RE,PRINT1           BLANK LINE                                   
         MVC   P(14),=C'COLUMN NUMBERS'                                         
         MVC   P+25(80),COLS                                                    
         BAS   RE,PRINT1                                                        
*                                                                               
         L     R2,=V(RQSTNUM)                                                   
         ZIC   R3,0(R2)            # REQUEST CARDS TO PRINT                     
         L     R2,ACARDS                                                        
         LA    R4,1                COUNTER FOR EDIT                             
*                                                                               
DMUL10   EQU   *                                                                
         MVC   P(21),=C'REQUEST CARD NN IMAGE'                                  
         EDIT  (R4),(2,P+13),FILL=0                                             
         MVC   P+25(80),0(R2)                                                   
         BAS   RE,PRINT1                                                        
         LA    R2,80(R2)                                                        
         LA    R4,1(R4)                                                         
         BCT   R3,DMUL10                                                        
*                                                                               
DMULEXIT EQU   *                                                                
         SR    R0,R0                                                            
         LTR   R0,R0                                                            
         XIT1                                                                   
         SPACE                                                                  
COLS     DC    CL40'1...5...10...15...20...25...30...35...40'                   
         DC    CL40'...45...50...55...60...65...70...75...80'                   
         SPACE 2                                                                
         EJECT                                                                  
*                   PRINTING AIDS                                               
         SPACE 2                                                                
PRINT1   MVC   RQCON,=C'BL01'                                                   
         B     GOPRINT                                                          
         SPACE 2                                                                
PRINT2   MVC   RQCON,=C'BL02'                                                   
         B     GOPRINT                                                          
         SPACE 2                                                                
PRINT3   MVC   RQCON,=C'BL03'                                                   
         B     GOPRINT                                                          
         SPACE 2                                                                
GOPRINT  NTR1                                                                   
         GOTO1 PRINT,RQWORK,P-1,RQCON                                           
         MVC   P,SPACES                                                         
         XIT1                                                                   
         EJECT                                                                  
*                                                                               
*                   TABLE OF RRG REPORT 'NUMBERS'                               
*                                                                               
         SPACE 2                                                                
RRGTAB   EQU   *                                                                
         DC    CL2'RG'                                                          
         DC    CL2'R1'                                                          
         DC    CL2'R2'                                                          
         DC    CL2'R3'                                                          
         DC    CL2'R4'                                                          
         DC    CL2'R5'                                                          
         DC    CL2'R6'                                                          
         DC    CL2'R7'                                                          
         DC    X'00'                                                            
         EJECT                                                                  
*                                                                               
*        REQUEST FIELD TABLES                                                   
*        --------------------                                                   
*        CL3  -  COLUMN NUMBER (RELATIVE TO 1ST POSITION, 1ST CARD)             
*        CL2  -  DATA WIDTH                                                     
*        CL1  -  DATA TYPE                                                      
*                A = ALPHA                                                      
*                D = DATE                                                       
*                N = NUMERIC                                                    
*        CL1  -  FOOTER CONTROL                                                 
*                F = PLACE IN GENERATED FOOT LINE(S)                            
*                - = DO NOT USE IN FOOT LINE(S)                                 
*        CL20 -  REQUEST FIELD TITLE                                            
*                                                                               
* ------------------------------------------------------------------            
*                                                                               
*        TABLE OF REQUEST FIELDS (NON RRG)                                      
*                                                                               
         SPACE 2                                                                
REQTAB   DS    0CL27                                                            
         DC    CL27'00102A-PROGRAM NUMBER'                                      
         DC    CL27'00302A-REPRESENTATIVE'                                      
         DC    CL27'00502A-REQUESTING OFFICE'                                   
         DC    CL27'00712A-REQUESTOR'                                           
         DC    CL27'01902AFOFFICE REGION'                                       
         DC    CL27'02102AFOFFICE'                                              
         DC    CL27'02305AFSTATION'                                             
         DC    CL27'02801AFSALES DIVISION'                                      
         DC    CL27'02901AFSALES TEAM'                                          
         DC    CL27'03003AFSALESPERSON'                                         
         DC    CL27'03304AFAGENCY'                                              
         DC    CL27'03702AFAGENCY OFFICE'                                       
         DC    CL27'03904AFADVERTISER'                                          
         DC    CL27'04303AFPRODUCT'                                             
         DC    CL27'04602AFPRODUCT CLASS'                                       
         DC    CL27'04802AFPRODUCT CATEGORY'                                    
         DC    CL27'05006D-START DATE'                                          
         DC    CL27'05606D-END DATE'                                            
         DC    CL27'06206DFAS AT DATE'                                          
         DC    CL27'06801A-MONTH TYPE (BASIS)'                                  
         DC    CL27'06901AFSTATION TYPE'                                        
         DC    CL27'07001A-SEQUENCE'                                            
         DC    CL27'07101A-OPTION1'                                             
         DC    CL27'07201A-OPTION2'                                             
         DC    CL27'07301AFACCOUNTING OPTION'                                   
         DC    CL27'07401AFSTATION GROUP'                                       
         DC    CL27'07501AFSTATION SUBGROUP'                                    
         DC    CL27'07601LFCONTRACT TYPE'                                       
         DC    CL27'07701A-OPTION3'                                             
         DC    CL27'07801A-OPTION4'                                             
         DC    CL27'07901A-OPTION5'                                             
         DC    CL27'08001A-OPTION6'                                             
         DC    C'END'                                                           
         EJECT                                                                  
*                                                                               
*        TABLE OF 2 CARD REQUEST FIELDS (NON RRG)                               
*                                                                               
         SPACE 2                                                                
REQ2TAB  DS    0CL27                                                            
         DC    CL27'00102A-PROGRAM NUMBER'                                      
         DC    CL27'00302A-REPRESENTATIVE'                                      
         DC    CL27'00502A-REQUESTING OFFICE'                                   
         DC    CL27'00712A-REQUESTOR'                                           
         DC    CL27'01902AFOFFICE REGION'                                       
         DC    CL27'02102AFOFFICE'                                              
         DC    CL27'02305AFSTATION'                                             
         DC    CL27'02801AFSALES DIVISION'                                      
         DC    CL27'02901AFSALES TEAM'                                          
         DC    CL27'03003AFSALESPERSON'                                         
         DC    CL27'03304AFAGENCY'                                              
         DC    CL27'03702AFAGENCY OFFICE'                                       
         DC    CL27'03904AFADVERTISER'                                          
         DC    CL27'04303AFPRODUCT'                                             
         DC    CL27'04602AFPRODUCT CLASS'                                       
         DC    CL27'04802AFPRODUCT CATEGORY'                                    
         DC    CL27'05006D-START DATE'                                          
         DC    CL27'05606D-END DATE'                                            
         DC    CL27'06206DFAS AT DATE'                                          
         DC    CL27'06801A-MONTH TYPE (BASIS)'                                  
         DC    CL27'06901AFSTATION TYPE'                                        
         DC    CL27'07001A-SEQUENCE'                                            
         DC    CL27'07101A-OPTION1'                                             
         DC    CL27'07201A-OPTION2'                                             
         DC    CL27'07301AFACCOUNTING OPTION'                                   
         DC    CL27'07401AFSTATION GROUP'                                       
         DC    CL27'07501AFSTATION SUBGROUP'                                    
         DC    CL27'07601LFCONTRACT TYPE'                                       
         DC    CL27'07701A-OPTION3'                                             
         DC    CL27'07801A-OPTION4'                                             
         DC    CL27'07901A-OPTION5'                                             
         DC    CL27'08101A-OPTION6'                                             
         DC    CL27'08202AFTVB REGION'                                          
         DC    CL27'08403AFOWNER'                                               
         DC    CL27'08704AFMARKET'                                              
         DC    CL27'09101NFRANK'                                                
         DC    CL27'09203AFPOINT PERSON'                                        
         DC    CL27'09508AFNETWORK CON #'                                       
         DC    CL27'10308NFCONTRACT NUMBER'                                     
         DC    CL27'11103AFAFFILIATION'                                         
         DC    CL27'11401A-EXPANDED REQ'                                        
         DC    CL27'11501AFRATING SERVICE'                                      
         DC    CL27'11606AFRATING BOOK'                                         
         DC    CL27'12205AFDEMO CATEGORY'                                       
         DC    CL27'12706DFAS-AT TO DATE'                                       
         DC    CL27'13301NFCREDIT RISK'                                         
         DC    CL27'13402NFLIABILITY POSITION'                                  
         DC    CL27'13901N-SPACING CONTROL'                                     
         DC    C'END'                                                           
         EJECT                                                                  
*                                                                               
*        TABLE OF REQUEST FIELDS (RRG)                                          
*                                                                               
         SPACE 2                                                                
RRGQTAB  DS    0CL27                                                            
         DC    CL27'07802A-REQUEST NUMBER'                                      
         DC    CL27'00302A-REPRESENTATIVE'                                      
         DC    CL27'00502A-REQUESTING OFFICE'                                   
         DC    CL27'00712A-REQUESTOR'                                           
         DC    CL27'01902AFOFFICE REGION'                                       
         DC    CL27'02102AFOFFICE'                                              
         DC    CL27'02305AFSTATION'                                             
         DC    CL27'02801AFSALES DIVISION'                                      
         DC    CL27'02901AFSALES TEAM'                                          
         DC    CL27'03003AFSALESPERSON'                                         
         DC    CL27'03304AFAGENCY'                                              
         DC    CL27'03702AFAGENCY OFFICE'                                       
         DC    CL27'03904AFADVERTISER'                                          
         DC    CL27'04303AFPRODUCT'                                             
         DC    CL27'04602AFPRODUCT CLASS'                                       
         DC    CL27'04802AFPRODUCT CATEGORY'                                    
         DC    CL27'05006D-START DATE'                                          
         DC    CL27'05606D-END DATE'                                            
         DC    CL27'06206DFAS AT DATE'                                          
         DC    CL27'06801A-MONTH TYPE (BASIS)'                                  
         DC    CL27'06901AFSTATION TYPE'                                        
         DC    CL27'07001A-SEQUENCE'                                            
         DC    CL27'07102NFRANK MAXIMUM'                                        
         DC    CL27'07301AFRECAP OPTION'                                        
         DC    CL27'07401AFSTATION GROUP'                                       
         DC    CL27'07501AFSTATION SUBGROUP'                                    
         DC    CL27'07601LFCONTRACT TYPE'                                       
         DC    CL27'07701AFRANK PERIOD'                                         
         DC    CL27'08001AFMONTH FORMAT'                                        
         DC    C'END'                                                           
         EJECT                                                                  
*                                                                               
*        TABLE OF REQUEST FIELDS (2-CARD RRG)                                   
*                                                                               
         SPACE 2                                                                
RRGQ2TAB DS    0CL27                                                            
         DC    CL27'07802A-REQUEST NUMBER'                                      
         DC    CL27'00302A-REPRESENTATIVE'                                      
         DC    CL27'00502A-REQUESTING OFFICE'                                   
         DC    CL27'00712A-REQUESTOR'                                           
         DC    CL27'01902AFOFFICE REGION'                                       
         DC    CL27'02102AFOFFICE'                                              
         DC    CL27'02305AFSTATION'                                             
         DC    CL27'02801AFSALES DIVISION'                                      
         DC    CL27'02901AFSALES TEAM'                                          
         DC    CL27'03003AFSALESPERSON'                                         
         DC    CL27'03304AFAGENCY'                                              
         DC    CL27'03702AFAGENCY OFFICE'                                       
         DC    CL27'03904AFADVERTISER'                                          
         DC    CL27'04303AFPRODUCT'                                             
         DC    CL27'04602AFPRODUCT CLASS'                                       
         DC    CL27'04802AFPRODUCT CATEGORY'                                    
         DC    CL27'05006D-START DATE'                                          
         DC    CL27'05606D-END DATE'                                            
         DC    CL27'06206DFAS AT DATE'                                          
         DC    CL27'06801A-MONTH TYPE (BASIS)'                                  
         DC    CL27'06901AFSTATION TYPE'                                        
         DC    CL27'07001A-SEQUENCE'                                            
         DC    CL27'07102NFRANK MAXIMUM'                                        
         DC    CL27'07301AFRECAP OPTION'                                        
         DC    CL27'07401AFSTATION GROUP'                                       
         DC    CL27'07501AFSTATION SUBGROUP'                                    
         DC    CL27'07601LFCONTRACT TYPE'                                       
         DC    CL27'07701AFRANK PERIOD'                                         
         DC    CL27'08101A-MONTH FORMAT'                                        
         DC    CL27'08202AFTVB REGION'                                          
         DC    CL27'08403AFOWNER'                                               
         DC    CL27'08704AFMARKET'                                              
         DC    CL27'09101NFRANK'                                                
         DC    CL27'09203AFPOINT PERSON'                                        
         DC    CL27'09508AFNETWORK CON #'                                       
         DC    CL27'10308NFCONTRACT NUMBER'                                     
         DC    CL27'11103AFAFFILIATION'                                         
         DC    CL27'11401A-EXPANDED REQ'                                        
         DC    CL27'11501AFRATING SERVICE'                                      
         DC    CL27'11606AFRATING BOOK'                                         
         DC    CL27'12205AFDEMO CATEGORY'                                       
         DC    CL27'12706DFAS-AT TO DATE'                                       
         DC    CL27'13301NFCREDIT RATING'                                       
         DC    CL27'13402NFLIABILITY POSITION'                                  
         DC    CL27'13901N-SPACING CONTROL'                                     
         DC    C'END'                                                           
         EJECT                                                                  
*                   DSECT FOR MODULE                                            
         SPACE 2                                                                
REQREPD  DSECT                                                                  
RQDUB    DS    D                                                                
ATABLE   DS    A                   A(PRINT CONTROL TABLE)                       
ACARDS   DS    A                   A(REQUEST CARD(S))                           
RQWORK   DS    CL24                                                             
PRSWITCH DS    CL1                                                              
RQCON    DS    CL4                                                              
DATE     DS    CL6                                                              
MULTIC   DS    CL1                 MULTI-CARD REQUEST FLAG                      
         PRINT OFF                                                              
         EJECT                                                                  
       ++INCLUDE REREPWORKD                                                     
         EJECT                                                                  
       ++INCLUDE DDBIGBOX                                                       
         EJECT                                                                  
       ++INCLUDE REXADDRD                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'054REREPRQRPS05/01/02'                                      
         END                                                                    
