*          DATA SET ACPROFLT   AT LEVEL 001 AS OF 05/23/05                      
*CATALP ACPROFLT                                                                
*                                                                               
***********************************************************************         
* ACPROFLT - CHECK FOR FILTER NAME/VALUE IN =PROD                     *         
* -----------------------------------------------                     *         
* ON ENTRY  P1  BYTES 0   = FILTER VALUE                              *         
*               BYTES 1-3 = A(COMPANY CODE/UNIT/LEDGER)               *         
*           P2  BYTES 0   = FILTER POSITION (1-5)                     *         
*               BYTES 1-3 = A(COMFACS)                                *         
* ON EXIT   CC  SET ON ERROR (IF INPUT INVALID)                       *         
***********************************************************************         
* LEVEL CHANGE COMMENTS                                               *         
* ---------------------                                               *         
* TKLU 23MAY05 001 <LO01-4376> - NEW VERSION                          *         
***********************************************************************         
         SPACE 1                                                                
         TITLE '=PROD FILTER NAME/VALUE VALIDATION'                             
         SPACE 1                                                                
ACPROFLT CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**PROFLT,RR=RE,CLEAR=YES                             
         USING WORKD,RC                                                         
         ST    RE,RELO                                                          
         XR    R6,R6               R6 POINTS TO C/U/L                           
         ICM   R6,15,0(R1)                                                      
         XR    R7,R7               R7 POINTS TO COMFACS                         
         ICM   R7,15,4(R1)                                                      
         USING COMFACSD,R7                                                      
         LR    R5,R1               R5 POINTS TO VALUE/POSITION                  
         SPACE 1                                                                
         USING RSFRECD,R2                                                       
         LA    R2,IOKEY            READ RECORD                                  
         MVC   RSFKEY,SPACES                                                    
         MVI   RSFKTYP,RSFKTYPQ                                                 
         MVC   RSFKCPY(3),0(R6)                                                 
         MVC   RSFKFLT#,4(R5)                                                   
         GOTO1 CDATAMGR,DMCB,DMREAD,ACCDIR,RSFRECD,RSFRECD                      
         BNE   ACPROFY             NOT ACTIVE HERE                              
         GOTO1 CDATAMGR,DMCB,GETREC,ACCMST,RSFKDA,IOAREA,IOWORK                 
         BE    *+6                                                              
         DC    H'0'                                                             
         SPACE 1                                                                
         USING RSFELD,R3                                                        
         XR    R0,R0                                                            
         LA    R2,IOAREA                                                        
         LA    R3,RSFRFST                                                       
ACPROF2  CLI   RSFEL,0             IF NOT ON RECORD THEN ERROR                  
         BE    ACPROFN                                                          
         CLI   RSFEL,RSFELQ                                                     
         BNE   ACPROF4                                                          
         CLC   RSFTYPE,0(R5)       IF MATCH THEN ALLOWED                        
         BE    ACPROFY                                                          
ACPROF4  IC    R0,RSFLN                                                         
         AR    R3,R0                                                            
         B     ACPROF2                                                          
         SPACE 1                                                                
ACPROFY  CR    RB,RB                                                            
         B     *+6                                                              
ACPROFN  LTR   RB,RB                                                            
         XIT1                                                                   
         DROP  R2,R3,R7                                                         
         EJECT                                                                  
***********************************************************************         
* CONSTANTS AND LTORG                                                 *         
***********************************************************************         
         SPACE 1                                                                
SPACES   DC    CL42' '                                                          
ACCDIR   DC    CL8'ACCDIR'                                                      
ACCMST   DC    CL8'ACCMST'                                                      
DMREAD   DC    CL8'DMREAD'                                                      
GETREC   DC    CL8'GETREC'                                                      
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* LOCAL WORKING STORAGE AND OTHER DSECTS                              *         
***********************************************************************         
         SPACE 1                                                                
WORKD    DSECT                                                                  
RELO     DS    A                                                                
DMCB     DS    6F                                                               
IOKEY    DS    XL42                                                             
         DS    XL16                                                             
IOWORK   DS    XL64                                                             
IOAREA   DS    XL2000                                                           
WORKX    EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
* INCLUDED BOOKS                                                      *         
***********************************************************************         
         SPACE 1                                                                
* ACGENFILE                                                                     
* DDCOMFACSD                                                                    
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
       ++INCLUDE DDCOMFACSD                                                     
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'001ACPROFLT  05/23/05'                                      
         END                                                                    
