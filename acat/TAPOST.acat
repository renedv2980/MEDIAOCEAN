*          DATA SET TAPOST     AT LEVEL 001 AS OF 04/03/15                      
*CATALP TAPOST                                                                  
                                                                                
*        P1 - A(TAPOST PARAMETER BLOCK)                                         
                                                                                
         TITLE 'TAPOST - TALENT POSTINGS'                                       
TAPOST   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,TAPOST,R7                                                      
         L     RA,0(R1)            TAPOST INTERFACE BLOCK                       
         USING TPD,RA                                                           
         L     RC,TPRC             GENCON                                       
         USING GEND,RC                                                          
         L     R9,ASTARTSV         A(PROGRAM SAVED STORAGE)                     
         USING SYSWORKD,R9                                                      
         L     R8,ASPOOLD          A(SPOOL)                                     
         USING SPOOLD,R8                                                        
         EJECT                                                                  
***********************************************************************         
*        MAIN CONTROL                                                 *         
***********************************************************************         
                                                                                
PROCESS  NTR1                                                                   
         BRAS  RE,INIT             INITIALIZE                                   
                                                                                
         LA    R3,KEY                                                           
PRO10    BRAS  RE,PROINV           PROCESS INVOICES                             
         JNE   PRO20                                                            
                                                                                
         BRAS  RE,PROCHKS          PROCESS CHECKS FOR EACH INVOICE              
                                                                                
         BRAS  RE,GENPOSTB         GENERATE BILLING POSTINGS                    
         J     PRO10                                                            
                                                                                
PRO20    BRAS  RE,ENDDOWN          FINISH DOWNLOADABLE REPORT                   
         BRAS  RE,ENDWORK          OR FINISH WORKER FILE                        
         BRAS  RE,SNDEMAIL         AND POSSIBLY SEND EMAIL                      
         J     XIT                                                              
                                                                                
***********************************************************************         
*        EXITS, CONSTANTS, EQUATES AND LITERALS                       *         
***********************************************************************         
                                                                                
YES      XR    RC,RC                                                            
NO       LTR   RC,RC                                                            
XIT      XIT1                                                                   
                                                                                
         GETEL R4,DATADISP,ELCODE                                               
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        INITIALIZE PROGRAM                                           *         
***********************************************************************         
                                                                                
INIT     NTR1  BASE=*,LABEL=*                                                   
         MVC   AIO,AIO1                                                         
                                                                                
         ZAP   TPDEBTOT,=P'0'      INITIALIZE DEBIT TOTAL                       
         ZAP   TPCRDTOT,=P'0'      CREDIT TOTAL                                 
         ZAP   TPPOSTRC,=P'0'      AND POSTINGS COUNTER                         
                                                                                
         LA    RE,TPD                                                           
         AHI   RE,TPPNHTAB-TPD                                                  
         ST    RE,TPAPHTAB                                                      
         LA    RE,TPD                                                           
         AHI   RE,TPPNHTBX-TPD                                                  
         MVI   0(RE),X'FF'                                                      
                                                                                
         BRAS  RE,INITDOWN         INITIALIZE FOR DOWNLOADABLE OUTPUT           
         BRAS  RE,INITWORK         OR WORKER FILE OUTPUT                        
         J     XIT                                                              
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        PROCESS NEXT INVOICE                                         *         
*        ON ENTRY ... R3=A(KEY)                                       *         
***********************************************************************         
                                                                                
         USING TLINPD,R3                                                        
PROINV   NTR1  BASE=*,LABEL=*                                                   
         OC    TPINPKEY,TPINPKEY   IF INVOICE READ SEQUENCE ALREADY             
         JZ    PI10                ESTABLISHED                                  
         MVC   KEY,TPINPKEY        RE-READ LAST INVOICE KEY                     
         GOTO1 HIGH                                                             
         CLC   KEY(L'TLINPKEY),TPINPKEY                                         
         JE    PI20                                                             
         DC    H'00'                                                            
                                                                                
PI10     XC    KEY,KEY                                                          
         BRAS  RE,INITINVB         INITIALIZE INVOICE KEY FOR BILLING           
         GOTO1 HIGH                AND READ FIRST KEY                           
         J     PI30                                                             
                                                                                
PI20     GOTO1 SEQ                                                              
                                                                                
PI30     MVI   TPSTAT,0                                                         
         ZIC   RF,TPIKCOMP                                                      
         EX    RF,*+8                                                           
         J     *+10                                                             
         CLC   KEY(0),KEYSAVE                                                   
         JE    PI50                                                             
                                                                                
         CLC   KEY(1),KEYSAVE                                                   
         JNE   NO                                                               
                                                                                
         ZIC   RF,TPIKDATE         ENSURE INVOICE FITS WITHIN                   
         AR    RF,R3               DATE RANGE                                   
         CLC   0(3,RF),TPEDATE                                                  
         JH    NO                                                               
                                                                                
PI50     CLI   TPCURR,0            IF FITERING ON CURRENCY                      
         JE    PI60                                                             
         ZIC   RF,TPIKCURR                                                      
         AR    RF,R3                                                            
         CLC   TPCURR,0(RF)        ENSURE INVOICE'S CURRENCY MATCHES            
         JNE   PI20                                                             
         DROP  R3                                                               
                                                                                
PI60     OC    TPFAGY,TPFAGY       IF FILTERING ON AGENCY                       
         BZ    PI70                                                             
         ZIC   RF,TPIKAGY                                                       
         AR    RF,R3                                                            
         CLC   TPFAGY,0(RF)                                                     
         JNE   PI20                                                             
                                                                                
PI70     OC    TPFINV,TPFINV       IF FILTERING ON INVOICE                      
         BZ    PI100                                                            
         ZIC   RF,TPIKINV                                                       
         AR    RF,R3                                                            
         CLC   TPFINV,0(RF)                                                     
         JNE   PI20                                                             
                                                                                
PI100    OC    TPOFFLST,TPOFFLST   IF FILTERING ON OFFICE                       
         JZ    PI200                                                            
         ZIC   RF,TPIKOFF                                                       
         AR    RF,R3                                                            
         LA    RE,TPOFFLST         ENSURE INVOICE'S OFFICE IS                   
PI110    CLI   0(RE),0             EOT                                          
         JE    PI20                                                             
         CLC   0(L'TGOFF,RE),0(RF) IN OFFICE LIST                               
         JE    PI200                                                            
         LA    RE,L'TGOFF(RE)                                                   
         J     PI110                                                            
                                                                                
PI200    MVC   TPINPKEY,KEY        SAVE INVOICE KEY                             
                                                                                
         BAS   RE,EXMEDADJ         EXTRACT MEDIA FOR ADJUSTMENTS                
                                                                                
         GOTO1 GETREC              GET INVOICE RECORD                           
                                                                                
         USING TAIND,R4                                                         
         L     R4,AIO1                                                          
         MVI   ELCODE,TAINELQ                                                   
         BRAS  RE,GETEL                                                         
         JE    *+6                                                              
         DC    H'00'                                                            
         TM    TAINSTA2,TAINSPRM   SKIP PRIMARY INVOICES                        
         JO    PI20                                                             
         DROP  R4                                                               
                                                                                
         USING TACOD,R4                                                         
         CLI   TPMED,0             IF MEDIA NOT ALREADY EXTRACTED               
         JNE   PI400                                                            
         L     R4,AIO1                                                          
         MVI   ELCODE,TACOELQ      GET COMMERCIAL DETAILS ELEMENT               
         BRAS  RE,GETEL                                                         
         JNE   PI500               COULD BE TRANSFERS, SKIP MEDIA               
         MVC   TPMED,TACOMED       AND SAVE MEDIA                               
         DROP  R4                                                               
                                                                                
PI400    TM    TPOPTS1,TPOPRNT     IF REPORTING PRINT INVOICES                  
         JZ    PI410                                                            
         CLI   TPMED,TACOMEDP      ENSURE INVOICE'S MEDIA IS PRINT              
         JE    PI500                                                            
         J     PI20                                                             
PI410    CLI   TPMED,TACOMEDP      ELSE ENSURE INVOICE'S MEDIA                  
         JE    PI20                IS NOT PRINT                                 
                                                                                
PI450    TM    TPOPTS1,TPOPLUS     IF REPORTING P+ INVOICES                     
         JZ    PI460                                                            
         CLI   TPMED,TACOMEDE      ENSURE INVOICE'S MEDIA IS EVENT              
         JE    PI500                                                            
         J     PI20                                                             
PI460    CLI   TPMED,TACOMEDE      ELSE ENSURE INVOICE'S MEDIA                  
         JE    PI20                IS NOT EVENT                                 
                                                                                
PI500    BAS   RE,EXTINV           EXTRACT INVOICE INFORMATION                  
         J     YES                                                              
                                                                                
***********************************************************************         
*        EXTRACT MEDIA FOR ADJUSTMENTS                                *         
*        ON ENTRY ... R3=A(INVOICE KEY)                               *         
***********************************************************************         
                                                                                
         USING TLINPD,R3                                                        
EXMEDADJ NTR1                                                                   
         MVI   TPMED,0                                                          
                                                                                
         CLC   TLINKAGY,=C'999999' IF AGENCY IS 999999                          
         JNE   XIT                 INVOICE DOES NOT HAVE TACOELQ                
         DROP  R3                                                               
                                                                                
         MVI   TPMED,C'T'          FOR NOW ALWAYS SET MEDIA AS TV               
                                                                                
         MVC   KEY,TPINPKEY        BUT REALLY WE NEED TO GO OFF                 
         GOTO1 HIGH                AND READ SOMETHING TO DETERMINE              
         J     XIT                 THIS                                         
                                                                                
***********************************************************************         
*        EXTRACT INVOICE DATA                                         *         
*        ON ENTRY ... R3=A(INVOICE PASSIVE KEY)                                 
*                     AIO1=A(INVOICE RECORD)                          *         
***********************************************************************         
                                                                                
EXTINV   NTR1                                                                   
         XC    TPINV(TPINVLNQ),TPINV                                            
         ZAP   TPICTOT,=P'0'                                                    
         ZAP   TPIDTOT,=P'0'       INITIALIZE INVOICE VARIABLES                 
         MVC   TPEST,SPACES                                                     
                                                                                
         USING TLINPD,R3                                                        
         ZIC   RF,TPIKDATE                                                      
         AR    RF,R3                                                            
         MVC   TPDATE,0(RF)        EXTRACT POSTING DATE                         
         GOTO1 DATCON,DMCB,(1,TPDATE),(X'5',TPPDATE)                            
         DROP  R3                                                               
                                                                                
         USING TLIND,R4                                                         
         L     R4,AIO1                                                          
         MVC   TGAGY,TLINAGY       SAVE AGENCY AND INVOICE NUMBER               
         XC    TLININV,=X'FFFFFFFFFFFF'                                         
         GOTO1 TPINVCON,DMCB,TLININV,TPPINV,DATCON                              
                                                                                
         CLC   TLINAGY,=CL6'999999'                                             
         JNE   EI10                                                             
         MVC   TPAINV,TLININV      SAVE ADJUSTMENT INVOICE                      
         DROP  R4                                                               
                                                                                
EI10     MVI   ELCODE,0                                                         
         BRAS  RE,GETEL                                                         
         J     *+8                                                              
EI20     BRAS  RE,NEXTEL                                                        
         JNE   EI90                                                             
                                                                                
         USING TABDD,R4                                                         
         CLI   0(R4),TABDELQ3      EXTRACT EURO BILLING DETAILS                 
         JNE   EI30                                                             
         OC    TABDCCVT,TABDCCVT                                                
         JZ    EI20                                                             
         ZAP   DUB,TABDCCVT                                                     
         CVB   R1,DUB                                                           
         ST    R1,TPEURCVT                                                      
         J     EI20                                                             
         DROP  R4                                                               
                                                                                
         USING TABDD,R4                                                         
EI30     CLI   0(R4),TABDELQ       EXTRACT BILLING DETAILS                      
         JNE   EI40                                                             
         OC    TABDCCVT,TABDCCVT                                                
         JZ    EI20                                                             
         TM    TPPDPST2,TAPDPEUR   EURO-BASED PAYMENT?                          
         JNZ   EI20                SKIP THIS                                    
         ZAP   DUB,TABDCCVT                                                     
         CVB   R1,DUB                                                           
         ST    R1,TPCANCVT                                                      
         J     EI20                                                             
         DROP  R4                                                               
                                                                                
         USING TAIND,R4                                                         
EI40     CLI   0(R4),TAINELQ       EXTRACT INVOICE OPTIONS                      
         JNE   EI50                                                             
         MVC   TPINSTAT,TAINSTAT                                                
         MVC   TPINSTA2,TAINSTA2                                                
         J     EI20                                                             
         DROP  R4                                                               
                                                                                
         USING TAPAD,R4                                                         
EI50     CLI   0(R4),TAPAELQ       EXTRACT PAY OPTIONS                          
         JNE   EI60                                                             
         CLI   TAPATYPE,TAPATHNW                                                
         JNE   EI20                                                             
         MVC   TPHNWADJ,TAPADATA                                                
         J     EI20                                                             
         DROP  R4                                                               
                                                                                
         USING TAPDD,R4                                                         
EI60     CLI   0(R4),TAPDELQ       EXTRACT PAYMENT DETAILS                      
         JNE   EI70                                                             
         MVC   TPPDSTAT,TAPDSTAT                                                
         MVC   TPPDSTA2,TAPDSTA2                                                
         MVC   TPPDPST1,TAPDPST1                                                
         MVC   TPPDPST2,TAPDPST2                                                
         MVC   TPPDHNW,TAPDHNW                                                  
         MVC   TGOFF,TAPDOFF                                                    
         MVC   TGCLI,TAPDCLI                                                    
         MVC   TGPRD,TAPDPRD                                                    
         MVC   TGUSE,TAPDUSE                                                    
         MVC   TGEMP,TAPDEMP                                                    
         MVC   TPINRTOT,TAPDINR                                                 
         J     EI20                                                             
                                                                                
         USING TACOD,R4                                                         
EI70     CLI   0(R4),TACOELQ       EXTRACT COMMERCIAL DETAILS                   
         JNE   EI80                                                             
         MVC   TGCCTEQU,TACOCTYP                                                
         J     EI20                                                             
         DROP  R4                                                               
                                                                                
         USING TANUD,R4                                                         
EI80     CLI   0(R4),TANUELQ       EXTRACT ESTIMATE NUMBER                      
         JNE   EI20                                                             
         CLI   TANUTYPE,TANUTEST                                                
         JNE   EI20                                                             
         ZIC   RF,TANULEN                                                       
         AHI   RF,-3                                                            
         EX    RF,*+8                                                           
         J     *+10                                                             
         MVC   TPEST(0),TANUMBER                                                
         J     EI20                                                             
         DROP  R4                                                               
                                                                                
***********************************************************************         
                                                                                
EI90     OC    TPCANCVT,TPCANCVT   IF CANADIAN DOLLAR INVOICE                   
         JZ    XIT                                                              
                                                                                
         USING TAPDD,R4                                                         
         L     R4,AIO1                                                          
         MVI   ELCODE,TAPDELQ      CONVERT ALL PAYMENT DETAIL AMOUNTS           
         BRAS  RE,GETEL                                                         
         JE     *+6                                                             
         DC    H'00'                                                            
                                                                                
         MVC   TPINRHND,TAPDINR    SAVE I&R AMOUNT CAN$                         
                                                                                
         LHI   R0,TAPDAMTL/L'TAPDAMTS                                           
         LA    R2,TAPDAMTS                                                      
EI100    GOTOR CVTAMT,DMCB,0(R2)                                                
         LA    R2,4(R2)                                                         
         BCT   R0,EI100                                                         
                                                                                
         CLI   TAPDLEN,TAPDLNQ                                                  
         JL    EI105                                                            
         LHI   R0,2                                                             
         LA    R2,TAPDTXNW                                                      
EI104    GOTOR CVTAMT,DMCB,0(R2)                                                
         LA    R2,4(R2)                                                         
         BCT   R0,EI104                                                         
         DROP  R4                                                               
                                                                                
         USING TABDD,R4                                                         
EI105    L     R4,AIO1                                                          
         MVI   ELCODE,TABDELQ                                                   
         BRAS  RE,GETEL                                                         
         JE     *+6                                                             
         DC    H'00'                                                            
                                                                                
         L     RF,TABDHND                                                       
         A     RF,TABDHNDC                                                      
         STCM  RF,15,TPGSTTXC      SAVE THIS TEMPORARILY                        
                                                                                
         TM    TABDSTAT,TABDSCNW   IF T&H IN CANADIAN DOLLARS                   
         JZ    EI110               CONVERT ALL BILLING DETAIL AMOUNTS           
         GOTOR CVTAMT,DMCB,TABDTAX                                              
         GOTOR CVTAMT,DMCB,TABDFICR                                             
         GOTOR CVTAMT,DMCB,TABDGST                                              
         GOTOR CVTAMT,DMCB,TABDPST                                              
         GOTOR CVTAMT,DMCB,TABDHND                                              
         GOTOR CVTAMT,DMCB,TABDHNDC                                             
         GOTOR CVTAMT,DMCB,TABDACOM                                             
         GOTOR CVTAMT,DMCB,TABDSIGN                                             
         DROP  R4                                                               
                                                                                
         USING TAPGD,R4                                                         
EI110    L     R4,AIO                                                           
         MVI   ELCODE,TAPGELQ      CONVERT ALL POSTING DETAILS                  
         BRAS  RE,GETEL                                                         
         JNE   EI130                                                            
         LA    R2,TAPGHNDI                                                      
         LA    R3,TAPGCGST         STOP AT GST TAX                              
         CLI   TAPGLEN,TAPGLNQ     IF OLD LENGTH,                               
         BNL   EI120                                                            
         LA    R3,TAPGWFEE         WIRE FEE IS THE END                          
EI120    GOTOR CVTAMT,DMCB,0(R2)                                                
         LA    R2,4(R2)                                                         
         CR    R2,R3                                                            
         JL    EI120                                                            
         DROP  R4                                                               
                                                                                
EI130    GOTOR CVTAMT,DMCB,TPHNWADJ CONVERT H&W ADJUSTMENT                      
                                                                                
         ICM   RF,15,TPINRHND                                                   
         ICM   RE,15,TPGSTTXC                                                   
         AR    RF,RE                                                            
         STCM  RF,15,TPINRHND                                                   
         J     XIT                                                              
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        PROCESS ALL CHECKS FOR INVOICE                               *         
*        ON ENTRY ... R3=A(KEY)                                       *         
***********************************************************************         
                                                                                
PROCHKS  NTR1  BASE=*,LABEL=*                                                   
         MVC   SYSDIR,=CL8'CHKDIR'                                              
         MVC   SYSFIL,=CL8'CHKFIL'                                              
         MVC   AIO,AIO2                                                         
                                                                                
         USING TLCKD,R3                                                         
         XC    TLCKKEY,TLCKKEY                                                  
         MVI   TLCKCD,TLCKCDQ                                                   
         MVC   TLCKAGY,TPINPKEY+TLINDAGY-TLINPD                                 
         MVC   TLCKINV,TPINPKEY+TLINDINV-TLINPD                                 
                                                                                
         LA    RF,TPINPKEY+TLINKADJ-TLINPD                                      
         OC    0(L'TLINKADJ,RF),0(RF)                                           
         JZ    PC10                                                             
         MVC   TLCKAGY,=C'999999'  ADJUSTMENT INVOICE                           
         MVC   TLCKINV,TPAINV                                                   
                                                                                
PC10     GOTO1 HIGH                                                             
         J     PC30                                                             
PC20     GOTO1 SEQ                                                              
PC30     CLC   KEY(TLCKSORT-TLCKD),KEYSAVE                                      
         JNE   PC40                                                             
                                                                                
         GOTO1 GETREC               GET CHECK RECORD                            
         DROP  R3                                                               
                                                                                
         BRAS  RE,FLTCHKB           FILTER CHECK KEY FOR BILLING                
         JNE   PC20                                                             
                                                                                
         BAS   RE,EXTCHK            EXTRACT CHECK INFORMATION                   
         J     PC20                                                             
                                                                                
PC40     MVC   SYSDIR,=CL8'TALDIR'                                              
         MVC   SYSFIL,=CL8'TALFIL'                                              
         MVC   AIO,AIO1                                                         
         J     XIT                                                              
                                                                                
***********************************************************************         
*        EXTRACT CHECK DATA                                           *         
*        ON ENTRY ... AIO2=A(CHECK RECORD)                            *         
***********************************************************************         
                                                                                
EXTCHK   NTR1                                                                   
         USING TAPDD,R4                                                         
         L     R4,AIO2                                                          
         MVI   ELCODE,TAPDELQ                                                   
         BRAS  RE,GETEL                                                         
         JE    *+6                                                              
         DC    H'00'                                                            
                                                                                
         USING TLCKD,RE                                                         
         MVC   TPW4TY,TAPDW4TY     SAVE W4 TYPE                                 
         CLI   TAPDW4TY,TAW4TYTR   SKIP IF TRUSTEE                              
         JNE   EC10                                                             
         L     RE,AIO2                                                          
         CLC   TLCKAGY,=C'999999'  ADJUSTMENT CHECK                             
         JE    XIT                                                              
         DROP  RE                                                               
                                                                                
EC10     DS    0H                                                               
         BAS   RE,CVTCHK                                                        
                                                                                
         LA    R2,TPAOSTOT       R2=A(APPROPRIATE W4 TYPE ACCUMULATOR)          
         BAS   RE,AOSCHK                                                        
         JE    EC20                                                             
         LA    R2,TPCATOT                                                       
         CLI   TAPDW4TY,TAW4TYCA                                                
         JE    EC20                                                             
         LA    R2,TPCOTOT                                                       
         CLI   TAPDW4TY,TAW4TYCO                                                
         JE    EC20                                                             
         LA    R2,TPESTOT                                                       
         CLI   TAPDW4TY,TAW4TYES                                                
         JE    EC20                                                             
         LA    R2,TPFOTOT                                                       
         CLI   TAPDW4TY,TAW4TYFO                                                
         JE    EC20                                                             
         LA    R2,TPINTOT                                                       
         CLI   TAPDW4TY,TAW4TYIN                                                
         JE    EC20                                                             
         LA    R2,TPTRTOT                                                       
         CLI   TAPDW4TY,TAW4TYTR                                                
         JE    EC20                                                             
         DC    H'00'                                                            
                                                                                
EC20     BRAS  RE,EXTCHKB        EXTRACT CHK INFO FOR BILLING POSTINGS          
         J     XIT                                                              
         DROP  R4                                                               
                                                                                
***********************************************************************         
*        ROUTINE RETURNS POSITIVE CONDITION CODE IF CHECK IS FOR AOS  *         
*        ON ENTRY ... AIO2=A(CHECK RECORD)                            *         
***********************************************************************         
                                                                                
AOSCHK   NTR1                                                                   
         LA    RE,AOSSSNTB                                                      
                                                                                
         USING TLCKD,R4                                                         
         L     R4,AIO                                                           
AC10     CLC   TLCKSSN,0(RE)                                                    
         JE    YES                                                              
         CLI   L'AOSSSNTB(RE),X'FF'                                             
         JE    NO                                                               
         LA    RE,L'AOSSSNTB(RE)                                                
         J     AC10                                                             
         DROP  R4                                                               
                                                                                
AOSSSNTB DS    0CL9                                                             
         DC    CL9'000112843'                                                   
         DC    CL9'000112851'                                                   
         DC    CL9'000117357'                                                   
         DC    CL9'000117358'                                                   
         DC    CL9'000117359'                                                   
         DC    CL9'000117360'                                                   
         DC    CL9'000117361'                                                   
         DC    CL9'000117362'                                                   
         DC    CL9'000117363'                                                   
         DC    CL9'000117364'                                                   
         DC    CL9'000117365'                                                   
         DC    X'FF'                                                            
                                                                                
***********************************************************************         
*        CONVERTS CANADIAN/EURO CHECK AMOUNTS TO US DOLLARS           *         
*        ON ENTRY ... R4=A(PAYMENT DETAILS ELEMENT)                   *         
***********************************************************************         
                                                                                
         USING TAPDD,R4                                                         
CVTCHK   NTR1                                                                   
*****    OC    TPCANCVT,TPCANCVT                                                
*****    JNZ   CC10                                                             
         OC    TPEURCVT,TPEURCVT        EURO CHECKS ONLY                        
         JZ    XIT                                                              
                                                                                
CC10     LHI   R0,TAPDAMTL/L'TAPDAMTS                                           
         LA    R2,TAPDAMTS                                                      
CC20     GOTOR CVTAMT,DMCB,0(R2)                                                
         LA    R2,4(R2)                                                         
         BCT   R0,CC20                                                          
                                                                                
         GOTOR CVTAMT,DMCB,TPHNWADJ                                             
         J     XIT                                                              
         DROP  R4                                                               
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        UTILITY ROUTINES                                             *         
***********************************************************************         
                                                                                
***********************************************************************         
*        ROUTINE CONVERTS AMOUNT TO US DOLLARS                        *         
*        ON ENTRY ... P1=A(AMOUNT TO CONVERT)                         *         
*                     P2=A(CONVERSION RATE)                           *         
***********************************************************************         
                                                                                
CVTAMT   NTR1  BASE=*,LABEL=*                                                   
         LA    R3,TPCANCVT                                                      
         OC    TPCANCVT,TPCANCVT                                                
         JNZ   CA10                                                             
         OC    TPEURCVT,TPEURCVT                                                
         JZ    XIT                                                              
         LA    R3,TPEURCVT                                                      
                                                                                
CA10     L     R2,0(R1)                                                         
         XR    RE,RE                                                            
         L     RF,0(R2)                                                         
         M     RE,0(R3)                                                         
         D     RE,=F'5000'                                                      
         LTR   RF,RF                                                            
         BM    *+8                                                              
         AHI   RF,1                                                             
         SRA   RF,1                                                             
         ST    RF,0(R2)                                                         
         J     XIT                                                              
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
                                                                                
***********************************************************************         
*        ROUTINE ADDS POSTING AMOUNT TO APPROPRIATE TOTAL ACCUMULATOR *         
*        ON ENTRY ... R2=A(POSTING AMOUNT)                            *         
*        RETURNS PACKED POSTING AMOUNT IN DUB                                   
***********************************************************************         
                                                                                
ADD2TOT  NTR1  BASE=*,LABEL=*                                                   
         ICM   R1,15,0(R2)                                                      
         CVD   R1,DUB                                                           
                                                                                
         TM    TPSTAT,TPCREDIT                                                  
         JZ    A2T10                                                            
         AP    TPCRDTOT,DUB                                                     
         AP    TPICTOT,DUB                                                      
         J     XIT                                                              
                                                                                
A2T10    AP    TPDEBTOT,DUB                                                     
         AP    TPIDTOT,DUB                                                      
         J     XIT                                                              
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
                                                                                
***********************************************************************         
*        ROUTINE OUTPUTS POSTING LINE                                 *         
*        ON ENTRY ... P1 BYTE 0=DEBIT/CREDIT INDICATOR                *         
*                     P1       =A(AMOUNT)                             *         
*                     P2       =A(POSTING MAPPING ENTRY)              *         
***********************************************************************         
                                                                                
OUTLINE  NTR1  BASE=*,LABEL=*                                                   
         ZICM  R2,1(R1),3                                                       
         CLC   0(4,R2),=F'0'       R2=A(POSTING AMOUNT)                         
         JE    XIT                                                              
                                                                                
         MVC   TPACCT,SPACES       INITALIZE ACCOUNT                            
         MVC   TPCNTRA,SPACES      AND CONTRA ACCOUNT                           
                                                                                
         MVC   TPSTAT,0(R1)                                                     
         CLC   TGUSE,=C'GRT'       IF GRT PAYMENT                               
         JNE   *+8                                                              
         OI    TPSTAT,TPGRT        SET GUARANTEE ATTRIBUTE STATUS               
                                                                                
         ZICM  R3,5(R1),3          R3=A(POSTING MAPPING ENTRY)                  
                                                                                
         BRAS  RE,SETACTSB         SET BILLING ACCOUNT/CONTRA ACCOUNTS          
                                                                                
         BRAS  RE,OUTDOWN          OUTPUT IN DOWNLOAD FORMAT                    
         BRAS  RE,OUTWORK          OR OUTPUT TO WORKER FILE                     
         BRAS  RE,OUTHIST          OR OUTPUT TO HIST2 SCREEN                    
                                                                                
         TM    TPSTAT,TPTOTAL      IF THIS IS A TOTAL                           
         JZ    XIT                                                              
         CP    TPIDTOT,TPICTOT     AND DEBIT DOES NOT EQUAL CREDIT              
         JZ    XIT                                                              
         OI    TPROSTAT,TPSOOB     SET OUT OF BALANCE STATUS                    
         J     XIT                                                              
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        DOWNLOAD-SPECIFIC ROUTINES                                   *         
***********************************************************************         
                                                                                
***********************************************************************         
*        INITIALIZE PROGRAM FOR DOWNLOADABLE OUTPUT                   *         
***********************************************************************         
                                                                                
INITDOWN NTR1  BASE=*,LABEL=*                                                   
         TM    TPOPTS1,TPODOWN     IF OUTPUTTING IN DOWNLOAD FORMAT             
         JZ    XIT                 INITIALIZE FOR DOWNLOADABLE OUTPUT           
                                                                                
         USING DLCBD,R5                                                         
         LA    R5,TPDLCB                                                        
         XC    DLCBD(DLCBXLX),DLCBD                                             
         MVI   DLCBACT,DLCBINIT                                                 
         LA    R1,PRTDOWN          A(HOOK ROUTINE FOR PRINTING)                 
         ST    R1,DLCBAPR                                                       
         LA    R1,P                A(PRINT LINE)                                
         MVC   P,SPACES                                                         
         ST    R1,DLCBAPL                                                       
         MVC   DLCBAED,EDITOR                                                   
         MVC   DLCXMAXL,=Y(L'P)    MAXIMUM LENGTH OF PRINT LINE                 
         MVI   DLCXDELC,C' '       DELIMITER                                    
         MVI   DLCXEOTC,C'"'       TEXT DELIMITER                               
         MVI   DLCXEOTA,C''''      TEXT DELIMITER ALTERNATE                     
         MVI   DLCXEOLC,X'5E'      SEMI-COLON FOR END OF LINE                   
         MVI   DLCXEORC,C':'       END OF REPORT                                
         OI    DLCBFLG1,DLCBFXTN                                                
         GOTO1 TPDLFLD,DLCBD                                                    
         DROP  R5                                                               
                                                                                
         GOTOR OUTPDOWN,DMCB,(C'T',DATHEAD),L'DATHEAD                           
         GOTOR OUTPDOWN,DMCB,(C'T',INVHEAD),L'INVHEAD                           
         GOTOR OUTPDOWN,DMCB,(C'T',OFFHEAD),L'OFFHEAD                           
         GOTOR OUTPDOWN,DMCB,(C'T',AGYHEAD),L'AGYHEAD                           
         GOTOR OUTPDOWN,DMCB,(C'T',CLIHEAD),L'CLIHEAD                           
         GOTOR OUTPDOWN,DMCB,(C'T',PRDHEAD),L'PRDHEAD                           
         GOTOR OUTPDOWN,DMCB,(C'T',ATRHEAD),L'ATRHEAD                           
         GOTOR OUTPDOWN,DMCB,(C'T',DSCHEAD),L'DSCHEAD                           
         GOTOR OUTPDOWN,DMCB,(C'T',ACTHEAD),L'ACTHEAD                           
         GOTOR OUTPDOWN,DMCB,(C'T',CNTHEAD),L'CNTHEAD                           
         GOTOR OUTPDOWN,DMCB,(C'T',DEBHEAD),L'DEBHEAD                           
         GOTOR OUTPDOWN,DMCB,(C'T',CRDHEAD),L'CRDHEAD                           
         BRAS  RE,EOLDOWN                                                       
         J     XIT                                                              
                                                                                
***********************************************************************         
*        USER SUPPLIED PRINT ROUTINE                                  *         
***********************************************************************         
                                                                                
PRTDOWN  NTR1                                                                   
         MVI   LINE,1              PREVENT PAGE BREAK                           
         GOTO1 SPOOL,DMCB,(R8)                                                  
         J     XIT                                                              
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
DATHEAD  DC    C'DATE'                                                          
INVHEAD  DC    C'INVOICE #'                                                     
OFFHEAD  DC    C'OFFICE'                                                        
AGYHEAD  DC    C'AGENCY'                                                        
CLIHEAD  DC    C'CLIENT'                                                        
PRDHEAD  DC    C'PRODUCT'                                                       
AMTHEAD  DC    C'AMOUNT'                                                        
ATRHEAD  DC    C'ATTRIBUTE'                                                     
DSCHEAD  DC    C'DESCRIPTION'                                                   
ACTHEAD  DC    C'ACCOUNT'                                                       
CNTHEAD  DC    C'CONTRA'                                                        
DEBHEAD  DC    C'DEBIT'                                                         
CRDHEAD  DC    C'CREDIT'                                                        
                                                                                
         LTORG                                                                  
                                                                                
***********************************************************************         
*        ROUTINE OUTPUTS LINE IN DOWNLOAD FORMAT                      *         
*        ON ENTRY ... R2=A(POSTING AMOUNT)                            *         
*                     R3=A(POSTING MAPPING ENTRY)                     *         
***********************************************************************         
                                                                                
         USING POSTD,R3                                                         
OUTDOWN  NTR1  BASE=*,LABEL=*                                                   
         TM    TPOPTS1,TPODOWN     IF OUTPUTTING IN DOWNLOAD FORMAT             
         JZ    XIT                                                              
                                                                                
         LA    RF,TPPDATE                                                       
         TM    TPINSTA2,TAINSFRC                                                
         JZ    *+8                                                              
         LA    RF,TPPLYDTE                                                      
         GOTOR OUTPDOWN,DMCB,(C'T',(RF)),L'TPPDATE                              
         GOTOR OUTPDOWN,DMCB,(C'T',TPPINV),L'TPPINV                             
         GOTOR OUTPDOWN,DMCB,(C'T',TGOFF),L'TGOFF                               
         GOTOR OUTPDOWN,DMCB,(C'T',TGAGY),L'TGAGY                               
         GOTOR OUTPDOWN,DMCB,(C'T',TGCLI),L'TGCLI                               
         GOTOR OUTPDOWN,DMCB,(C'T',TGPRD),L'TGPRD                               
                                                                                
         MVC   WORK(L'ODGRT),ODGRT                                              
         TM    TPSTAT,TPGRT                                                     
         JO    *+10                                                             
         MVC   WORK(L'ODGRT),SPACES                                             
         GOTOR OUTPDOWN,DMCB,(C'T',WORK),L'ODGRT                                
                                                                                
         GOTOR OUTPDOWN,DMCB,(C'T',POSTDOWN),L'POSTDOWN                         
         DROP  R3                                                               
                                                                                
         GOTOR OUTPDOWN,DMCB,(C'T',TPACCT),L'TPACCT                             
         GOTOR OUTPDOWN,DMCB,(C'T',TPCNTRA),L'TPCNTRA                           
                                                                                
         TM    TPSTAT,TPCREDIT                                                  
         JZ    OD10                                                             
         GOTOR OUTPDOWN,DMCB,(C'T',SPACES),L'SPACES                             
                                                                                
OD10     BRAS  RE,ADD2TOT                                                       
                                                                                
         ICM   RF,15,0(R2)                                                      
         EDIT  (RF),(12,TPAMT),2,MINUS=YES,ALIGN=LEFT                           
         GOTOR OUTPDOWN,DMCB,(C'N',TPAMT),12                                    
                                                                                
         TM    TPSTAT,TPTOTAL      IF THIS IS A TOTAL,                          
         JZ    OD20                OUTPUT ERROR IF DEBIT DOES NOT               
         CP    TPIDTOT,TPICTOT     EQUAL CREDIT                                 
         JE    OD20                                                             
         GOTOR OUTPDOWN,DMCB,(C'T',SPACES),L'SPACES                             
         GOTOR OUTPDOWN,DMCB,(C'T',ODDNEQC),L'ODDNEQC                           
                                                                                
OD20     BRAS  RE,EOLDOWN                                                       
         J     XIT                                                              
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
ODGRT    DC    CL30'GUARANTEE'                                                  
ODDNEQC  DC    CL30'DEBIT NOT EQUAL TO CREDIT'                                  
                                                                                
         LTORG                                                                  
                                                                                
***********************************************************************         
*        FINISH DOWNLOAD PROCESSING                                   *         
***********************************************************************         
                                                                                
ENDDOWN  NTR1  BASE=*,LABEL=*                                                   
         TM    TPOPTS1,TPODOWN     IF OUTPUTTING IN DOWNLOAD FORMAT             
         JZ    XIT                                                              
                                                                                
         GOTO1 OUTSPACE,DMCB,7                                                  
         GOTOR OUTPDOWN,DMCB,(C'T',FDDEBIT),L'FDDEBIT                           
         GOTO1 OUTSPACE,DMCB,2                                                  
         EDIT  (P8,TPDEBTOT),(12,TPAMT),2,MINUS=YES                             
         GOTOR OUTPDOWN,DMCB,(C'N',TPAMT),12                                    
         BRAS  RE,EOLDOWN                                                       
                                                                                
         GOTO1 OUTSPACE,DMCB,7                                                  
         GOTOR OUTPDOWN,DMCB,(C'T',FDCREDIT),L'FDCREDIT                         
         GOTO1 OUTSPACE,DMCB,3                                                  
         EDIT  (P8,TPCRDTOT),(12,TPAMT),2,MINUS=YES                             
         GOTOR OUTPDOWN,DMCB,(C'N',TPAMT),12                                    
         BRAS  RE,EOLDOWN                                                       
                                                                                
         USING DLCBD,R5                                                         
         LA    R5,TPDLCB                                                        
         MVI   DLCBACT,DLCBEOR      END OF REPORT                               
         GOTO1 TPDLFLD,DLCBD        LAST FOR REPORT                             
         J     XIT                                                              
         DROP  R5                                                               
                                                                                
***********************************************************************         
*        ROUTINE ADDS SPACES FOR BLANK COLUMNS FOR DEBITS             *         
*        ON ENTRY ... P1=# OF SPACES TO GENERATE                                
***********************************************************************         
                                                                                
OUTSPACE NTR1                                                                   
         L     R0,0(R1)                                                         
OS10     GOTOR OUTPDOWN,DMCB,(C'T',SPACES),L'SPACES                             
         BCT   R0,OS10                                                          
         J     XIT                                                              
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
FDCREDIT DC    CL30'CREDIT TOTAL'                                               
FDDEBIT  DC    CL30'DEBIT TOTAL'                                                
                                                                                
         LTORG                                                                  
                                                                                
***********************************************************************         
*        OUTPUT DOWNLOAD FIELD                                        *         
*        ON ENTRY ... P1 BYTE 0=TYPE TO PASS                          *         
*                     P1       =A(DATA)                               *         
*                     P2       =LENGTH                                *         
***********************************************************************         
                                                                                
OUTPDOWN NTR1  BASE=*,LABEL=*                                                   
         USING DLCBD,R5                                                         
         LA    R5,TPDLCB                                                        
         MVI   DLCBACT,DLCBPUT                                                  
         MVC   DLCBTYP,0(R1)                                                    
         OI    DLCBFLG1,DLCBFXFL                                                
                                                                                
         L     RF,0(R1)            RF=A(DOWNLOAD FIELD)                         
         L     RE,4(R1)            RE=L'DOWNLOAD FIELD                          
                                                                                
         LR    R1,RF                                                            
         AR    R1,RE                                                            
OPD10    SHI   R1,1                                                             
         CLI   0(R1),C' '                                                       
         JNE   OPD20                                                            
         BCT   RE,OPD10                                                         
         LHI   RE,1                                                             
OPD20    STC   RE,DLCBLEN                                                       
                                                                                
         BCTR  RE,0                                                             
         CLI   DLCBTYP,DLCBNUM     DATA TYPE IS NUMERIC                         
         JE    OPD30                                                            
                                                                                
         EX    RE,*+8                                                           
         J     *+10                                                             
         MVC   DLCBFLX(0),0(RF)                                                 
         J     OPD50                                                            
                                                                                
OPD30    EX    RE,*+8                                                           
         J     *+10                                                             
         MVC   DLCBFLD(0),0(RF)                                                 
                                                                                
OPD50    GOTO1 TPDLFLD,DLCBD                                                    
         J     XIT                                                              
         DROP  R5                                                               
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
                                                                                
***********************************************************************         
*        FINISH LINE OF DOWNLOAD OUPUT                                *         
***********************************************************************         
                                                                                
EOLDOWN  NTR1  BASE=*,LABEL=*                                                   
         USING DLCBD,R5                                                         
         LA    R5,TPDLCB                                                        
         MVI   DLCBACT,DLCBEOL      END OF LINE                                 
         GOTO1 TPDLFLD,DLCBD        (DON'T USE RF, BASR RE,RF BELOW)            
         J     XIT                                                              
         DROP  R5                                                               
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        WORKER FILE SPECIFIC ROUTINES                                *         
***********************************************************************         
                                                                                
***********************************************************************         
*        INITIALIZE PROGRAM FOR WORKER FILE OUTPUT                    *         
***********************************************************************         
                                                                                
INITWORK NTR1  BASE=*,LABEL=*                                                   
         TM    TPOPTS1,TPODOWN+TPOHIST2  IF NOT DOWNLD OR HIST2,                
         JNZ   XIT                       INITIALIZE FOR WORKER FILE             
                                                                                
         LA    R6,TPSTAREA                                                      
         MVI   0(R6),C' '                                                       
         MVC   1(249,R6),0(R6)                                                  
                                                                                
         USING UKRECD,R6                                                        
         LA    R6,TPWORKID                                                      
         XC    TPWORKID,TPWORKID   BUILD WORKER KEY                             
         MVC   UKUSRID,TGUSER                                                   
         MVI   UKSYSPRG,C'T'                                                    
         PACK  DUB(2),RCDATE+3(3)                                               
         MVC   UKDAY,DUB                                                        
         MVI   UKCLASS,C'P'                                                     
         MVI   UKFLAG,1            ALLOW MULTIPLE WORKER FILES                  
                                                                                
         BRAS  RE,INITWRKB         INITIALIZE WORKER FILE FOR BILLING           
         DROP  R6                                                               
                                                                                
         MVC   COMMAND(6),=CL6'OPEN'                                            
         BRAS  RE,WKFILE                                                        
         J     XIT                                                              
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
                                                                                
***********************************************************************         
*        ROUTINE OUTPUTS LINE TO WORKER FILE                          *         
*        ON ENTRY ... R2=A(POSTING AMOUNT)                            *         
***********************************************************************         
                                                                                
OUTWORK  NTR1  BASE=*,LABEL=*                                                   
         TM    TPOPTS1,TPODOWN+TPOHIST2  IF OUTPUT IS DOWNLD OR HIST2,          
         JNZ   XIT                       EXIT                                   
                                                                                
         XCEF  TPSTAREA,2000                                                    
                                                                                
         USING PSHEADD,R6                                                       
         LA    R6,TPSTAREA                                                      
         MVI   PSHDEL,X'50'        HEAD ELEMENT                                 
         MVI   PSHDLEN,70                                                       
         MVC   PSHDACC,TPACCT                                                   
         OC    PSHDACC+1(L'PSHDACC-1),SPACES                                    
         MVC   PSHDANAL,SPACES                                                  
         MVC   PSHDSBAC,TPCNTRA                                                 
         OC    PSHDSBAC+1(L'PSHDSBAC-1),SPACES                                  
         TM    TPSTAT,TPCREDIT                                                  
         JZ    OW10                                                             
         MVC   PSHDSBNM,SPACES                                                  
         DROP  R6                                                               
                                                                                
         USING TRANSD,R6                                                        
OW10     LA    R6,70(R6)                                                        
         MVI   TRNSEL,TRNSELQ                                                   
         MVI   TRNSLEN,TRNSLNQ                                                  
         MVC   TRNSDATE,TPDATE                                                  
         TM    TPINSTA2,TAINSFRC   FORCE TO LAST YEAR?                          
         JZ    *+10                                                             
         MVC   TRNSDATE,TPLYDATE                                                
         MVI   TRNSSBRF,0                                                       
         MVI   TRNSTYPE,50                                                      
         MVC   TRNSREF,TPPINV      INV # = REFERENCE NUMBER                     
         MVI   TRNSTYPE,9                                                       
                                                                                
         MVI   TRNSSTAT,0                                                       
         TM    TPSTAT,TPCREDIT                                                  
         JO    *+8                                                              
         MVI   TRNSSTAT,X'80'                                                   
         GOTO1 DATCON,DMCB,(1,TRNSDATE),(0,WORK)                                
         MVC   TRNSBTCH,SPACES                                                  
         MVC   TRNSBTCH(1),WORK+1        YEAR                                   
         MVC   TRNSBTCH+1(1),WORK+3      MONTH                                  
         CLI   WORK+2,C'1'               MONTH >=10                             
         JNE   OW20                                                             
         NI    TRNSBTCH+1,X'CF'          0-2 == A-C                             
         ZIC   RF,TRNSBTCH+1                                                    
         AHI   RF,1                                                             
         STC   RF,TRNSBTCH+1                                                    
                                                                                
OW20     BRAS  RE,ADD2TOT                                                       
                                                                                
         ZAP   TRNSAMNT,DUB                                                     
         MVC   TRNSANAL,SPACES                                                  
         MVC   TRNSANAL(1),TGOFF                                                
         DROP  R6                                                               
                                                                                
         AP    TPPOSTRC,=P'1'      BUMP NUMBER OF POSTING RECS                  
                                                                                
         USING ACTAD,R6                                                         
         ZIC   RE,1(R6)            BUMP TO NEXT SLOT                            
         AR    R6,RE                                                            
         TM    TPSTAT,TPDEBIT                                                   
         JZ    OW30                                                             
         XC    ACTAEL(ACTALNQ),ACTAEL                                           
         MVI   ACTAEL,ACTAELQ      ELCODE                                       
         MVI   ACTALEN,ACTALNQ     LENGTH                                       
         MVC   ACTAAGY,TGAGY       AGENCY                                       
         MVC   ACTACLI,TGCLI       CLIENT                                       
         MVC   ACTAPRD,TGPRD       PRODUCT                                      
         MVC   ACTACID,TGCID       COMMERCIAL ID                                
         MVC   ACTAEST,TPEST       ESTIMATE NUMBER                              
         DROP  R6                                                               
                                                                                
OW30     MVI   ACTALNQ(R6),0       MARK E-O-R                                   
                                                                                
         MVC   COMMAND,=CL6'ADD'                                                
         BRAS  RE,WKFILE                                                        
         J     XIT                                                              
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
                                                                                
***********************************************************************         
*        FINISH WORKER FILE PROCESSING                                *         
***********************************************************************         
                                                                                
ENDWORK  NTR1  BASE=*,LABEL=*                                                   
         TM    TPOPTS1,TPODOWN+TPOHIST2  IF NOT DOWNLD OR HIST2,                
         JNZ   XIT                       FINISH WRKR FILE PROCESSING            
                                                                                
         XCEF  TPSTAREA,2000                                                    
                                                                                
         USING PSSUBFD,R6                                                       
         LA    R6,TPSTAREA                                                      
         MVC   PSSBEL(2),=X'521D'                                               
         MVC   PSSBDESC,SPACES                                                  
         MVC   PSSBDESC(7),=C'TALENT-'                                          
         MVC   PSSBDESC+7(6),TPUSER                                             
         ZAP   PSSBRECS,TPPOSTRC                                                
         ZAP   PSSBCASH,TPDEBTOT                                                
         MVI   PSSBCASH+6,0                                                     
         MVC   COMMAND,=CL6'ADD'                                                
         BRAS  RE,WKFILE                                                        
         DROP  R6                                                               
                                                                                
         MVC   COMMAND,=CL6'CLOSE'                                              
         BRAS  RE,WKFILE                                                        
                                                                                
         TM    TPOPTS1,TPOKEEP     IF PUTTING WORKER FILE ON KEEP               
         JO    EW10                                                             
         TM    TPROSTAT,TPSOOB     OR OUT OF BALANCE                            
         JZ    XIT                                                              
EW10     MVC   COMMAND,=CL6'KEEP'  PUT WORKER FILE ON KEEP                      
         BRAS  RE,WKFILE                                                        
         J     XIT                                                              
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
                                                                                
***********************************************************************         
*        ACCESS WORKER FILE                                           *         
***********************************************************************         
                                                                                
WKFILE   NTR1  BASE=*,LABEL=*      OPEN WORKER FILE                             
         CLI   COMMAND,C'A'        IF COMMAND=ADD, COMPUTE HEADER LEN           
         JNE   WF30                                                             
         XR    R3,R3                                                            
         LA    R2,TPSTAREA                                                      
WF10     CLI   0(R2),0             FIND EOR                                     
         JE    WF20                                                             
         IC    R3,1(R2)                                                         
         AR    R2,R3                                                            
         J     WF10                                                             
                                                                                
WF20     LA    R6,TPSTHEAD         COMPUTE L'RECORD + 4                         
         XC    TPSTHEAD,TPSTHEAD                                                
         AHI   R2,1                                                             
         SR    R2,R6                                                            
         STH   R2,TPSTHEAD                                                      
                                                                                
WF30     GOTO1 WORKER,DMCB,COMMAND,TPSTBUFF,TPWORKID,TPSTHEAD                   
         TM    DMCB+8,X'C0'        ELSE CALL WORKER - PARM1 ALREADY SET         
         JZ    XIT                                                              
         DC    H'00'                                                            
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        ROUTINE OUTPUTS LINE ONTO HIST2 SCREEN                       *         
*        ON ENTRY ... R2=A(POSTING AMOUNT)                            *         
*                     R3=A(POSTING MAPPING ENTRY)                     *         
*                     TPHISTLN = CURRENT LINE ON HIST2 SCREEN         *         
***********************************************************************         
                                                                                
         USING POSTD,R3                                                         
OUTHIST  NTR1  BASE=*,LABEL=*                                                   
         TM    TPOPTS1,TPOHIST2    IF OUTPUTTING TO HIST2 SCREEN                
         JZ    XIT                                                              
                                                                                
         USING HISTD,R5                                                         
         L     R5,TPHISTLN                                                      
         CLC   8(2,R5),=C'PF'      IF AT FIRST HALF OF PFKEY LINE               
         JNE   *+8                                                              
         L     R5,TPHISLN2         GO BACK UP TO TOP OF 2ND COLUMN              
                                                                                
         MVC   HDES,POSTDOWN       DESCRIPTION                                  
         MVI   HDESH+5,L'POSTDOWN                                               
                                                                                
         BRAS  RE,ADD2TOT                                                       
                                                                                
         CP    TPIDTOT,TPICTOT     IF DEBIT DOES NOT EQUAL CREDIT               
         JE    OHIST10                                                          
         CLC   HDES,BPTOT          AND DESCRIPTION IS TOTAL                     
         JNE   OHIST10                                                          
         MVC   HDES,BPTOTNEQ       USE OUT OF BALANCE INDICATOR                 
                                                                                
OHIST10  ICM   RF,15,0(R2)                                                      
         EDIT  (RF),HAMT,2,MINUS=YES,ALIGN=LEFT                                 
         STC   R0,HAMTH+5                                                       
                                                                                
         ZIC   RF,0(R5)            BUMP TO AMOUNT                               
         AR    R5,RF                                                            
         ZIC   RF,0(R5)            BUMP TO NEXT DESCRIPTION                     
         AR    R5,RF                                                            
         CLC   8(2,R5),=C'PF'      STOP AT PFKEY LINE                           
         JE    XIT                                                              
         ZIC   RF,0(R5)            BUMP TO AMOUNT ON NEXT LINE                  
         AR    R5,RF                                                            
         ZIC   RF,0(R5)            BUMP TO NEXT DESCRIPTION                     
         AR    R5,RF                                                            
         ST    R5,TPHISTLN         SAVE A(NEXT HIST2 LINE)                      
                                                                                
         J     XIT                                                              
         DROP  R3,R5                                                            
                                                                                
***********************************************************************         
*        IF OUT OF BALANCE SEND EMAIL                                 *         
***********************************************************************         
                                                                                
SNDEMAIL NTR1  BASE=*,LABEL=*                                                   
         TM    TPROSTAT,TPSOOB     IF OUT OF BALANCE                            
         JZ    XIT                                                              
         TM    TPOPTS1,TPOEMAIL    AND SENDING EMAIL WHEN OUT OF                
         JZ    XIT                 BALANCE                                      
                                                                                
         MVC   EMDATE,TGTODAY8     SET DATE                                     
                                                                                
         CLI   TPTYPE,TPTYPEB      SET MESSAGE TO INDICATE ...                  
         JNE   SE10                                                             
         MVC   EMMSG,EMPB          PRINT BILLING                                
         TM    TPOPTS1,TPOPRNT                                                  
         JO    SE20                                                             
         MVC   EMMSG,EMVB          P+ BILLING                                   
         TM    TPOPTS1,TPOPLUS                                                  
         JO    SE20                                                             
         MVC   EMMSG,EMB           OR BILLING                                   
         J     SE20                                                             
                                                                                
SE10     MVC   EMMSG,EMEC          OR EURO CHECKS                               
         CLI   TPCURR,C'E'                                                      
         JE    SE20                                                             
         MVC   EMMSG,EMCC          OR CANADIAN CHECKS                           
         CLI   TPCURR,C'C'                                                      
         JE    SE20                                                             
         MVC   EMMSG,EMUUC         OR US URGENT CHECKS                          
         TM    TPOPTS1,TPOURG                                                   
         JO    SE20                                                             
         MVC   EMMSG,EMPK          OR US P+ CHECKS                              
         TM    TPOPTS1,TPOPLUS                                                  
         JO    SE20                                                             
         MVC   EMMSG,EMUOC         OR US OVERNIGHT CHECKS                       
                                                                                
SE20     GOTO1 DATAMGR,DMCB,=C'OPMSG',('EMLNQ',EMNOTIFY)                        
         J     XIT                                                              
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
EMNOTIFY DC    C'AUTONOTE*US-TALENT_MF_DEV_TEAM@MEDIAOCEAN.COM,'                
         DC    C'US-TALENT_PRODUCT_TEAM@MEDIAOCEAN.COM:'                        
EMDATE   DS    CL8                                                              
         DC    C' '                                                             
EMMSG    DS    CL35                                                             
EMLNQ    EQU   *-EMNOTIFY                                                       
                                                                                
EMB      DC    CL35'BILLING OUT OF BALANCE'                                     
EMPB     DC    CL35'PRINT BILLING OUT OF BALANCE'                               
EMVB     DC    CL35'P+ BILLING OUT OF BALANCE'                                  
                                                                                
EMUOC    DC    CL35'US OVERNIGHT CHECKS OUT OF BALANCE'                         
EMUUC    DC    CL35'US URGENT CHECKS OUT OF BALANCE'                            
EMCC     DC    CL35'CANADIAN CHECKS OUT OF BALANCE'                             
EMEC     DC    CL35'EURO CHECKS OUT OF BALANCE'                                 
EMPK     DC    CL35'P+ CHECKS OUT OF BALANCE'                                   
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        BILLING POSTING SPECIFIC ROUTINES                            *         
***********************************************************************         
                                                                                
***********************************************************************         
*        INITIALIZE WORKER FILE VARIABLES FOR BILLING POSTINGS        *         
*        ON ENTRY ... R6=A(WORKER FILE KEY)                           *         
***********************************************************************         
                                                                                
         USING UKRECD,R6                                                        
INITWRKB NTR1  BASE=*,LABEL=*                                                   
         CLI   TPTYPE,TPTYPEB      IF GENERATING BILLING POSTINGS               
         JNE   XIT                 SET BILLING/PRINT BILLING INDICATOR          
         MVC   UKSYSPRG+1(2),=C'PB'                                             
         TM    TPOPTS1,TPOPRNT                                                  
         JO    XIT                                                              
         MVC   UKSYSPRG+1(2),=C'VB'                                             
         TM    TPOPTS1,TPOPLUS                                                  
         JO    XIT                                                              
         MVC   UKSYSPRG+1(2),=C'BI'                                             
         J     XIT                                                              
         DROP  R6                                                               
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
                                                                                
***********************************************************************         
*        INITIALIZE INVOICE KEY FOR BILLING POSTINGS                  *         
*        ON ENTRY ... R3=A(KEY)                                       *         
***********************************************************************         
                                                                                
         USING TLINPD,R3                                                        
INITINVB NTR1  BASE=*,LABEL=*                                                   
         CLI   TPTYPE,TPTYPEB                                                   
         JNE   XIT                                                              
         MVI   TLINPCD,TLINDCDQ                                                 
         MVC   TLINDDTE,TPSDATE                                                 
         MVC   TLINDAGY,TPFAGY                                                  
         MVC   TLINDINV,TPFINV                                                  
         MVI   TPIKCOMP,TLINDAGY-TLINPD-1                                       
         MVI   TPIKDATE,TLINDDTE-TLINPD                                         
         MVI   TPIKAGY,TLINDAGY-TLINPD                                          
         MVI   TPIKINV,TLINDINV-TLINPD                                          
         MVI   TPIKCURR,TLINDCUR-TLINPD                                         
         MVI   TPIKOFF,TLINDOFF-TLINPD                                          
         J     XIT                                                              
         DROP  R3                                                               
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
                                                                                
***********************************************************************         
*        FILTER CHECK KEY FOR BILLING                                 *         
*        ON ENTRY ... R3=A(CHECK KEY)                                 *         
***********************************************************************         
                                                                                
         USING TLCKD,R3                                                         
FLTCHKB  NTR1  BASE=*,LABEL=*                                                   
         CLI   TPTYPE,TPTYPEB                                                   
         JNE   YES                                                              
                                                                                
         OC    TPPDHNW,TPPDHNW        IF WE HAVE HNW                            
         JZ    FCB10                                                            
         CLC   TLCKCAT,=CL3'ZZZ'      SKIP CATEGORY ZZZ                         
         JNE   FCB10                                                            
         CLC   TLCKSSN,=C'516120204'  EXCEPT AFM & EP FUND                      
         JE    FCB10                                                            
         TM    TPPDSTA2,TAPDSSUB      IF SUBSIDIARY INVOICE                     
         BZ    FCB05                                                            
         CLC   TLCKSSN,=C'131801294'    AND HNW CHECK                           
         JNE   FCB05                                                            
                                                                                
         USING TAPDD,R4                                                         
         L     R4,AIO2                                                          
         MVI   ELCODE,TAPDELQ         PAY DETAILS ON CHECK                      
         BRAS  RE,GETEL                                                         
         JNE   FCB05                                                            
         MVC   TPSUBHNW,TAPDPAYC        USE THAT AMOUNT                         
         J     NO                                                               
                                                                                
FCB05    OC    TPHNWADJ,TPHNWADJ      UNLESS THERE IS AN HNW ADJUSTMENT         
         JZ    NO                                                               
         TM    TPPDPST1,TAPDPCRD      AND ITS A CREDIT INVOICE                  
         JZ    NO                                                               
                                                                                
FCB10    CLC   TGUSE,=C'CPN'          IF NOT CAN PENALTY PAYMENT,               
         JE    YES                                                              
                                                                                
         LA    R1,IRCAN                                                         
FCB20    CLC   TLCKSSN,0(R1)          SKIP I&R CHECKS                           
         JE    FCB30                                                            
         CLI   L'IRCAN(R1),X'FF'                                                
         JE    YES                                                              
         LA    R1,L'IRCAN(R1)                                                   
         J     FCB20                                                            
                                                                                
         USING TACAD,R4                                                         
FCB30    L     R4,AIO2                                                          
         MVI   ELCODE,TACAELQ         CAST ELEMENT ON CHECK                     
         BRAS  RE,GETEL                                                         
         JNE   FCB50                                                            
                                                                                
FCB40    OC    TACAYEAR,TACAYEAR      HAS A CAST YEAR?                          
         JNZ   YES                    YES, TAKE IT                              
                                                                                
FCB50    CLC   TLCKCAT,=CL3'ZZZ'      SKIP CATEGORY ZZZ                         
         JE    NO                                                               
                                                                                
         J     YES                    TAKE IT OTHERWISE                         
                                                                                
         DROP  R3,R4                                                            
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
IRCAN    DS    0CL9                                                             
         DC    CL9'000000055'                                                   
         DC    CL9'000003755'                                                   
         DC    CL9'000003855'                                                   
         DC    CL9'000003876'                                                   
         DC    CL9'000007106'                                                   
         DC    CL9'000008213'                                                   
         DC    CL9'000000066'                                                   
         DC    CL9'000118211'                                                   
         DC    X'FF'                                                            
                                                                                
         LTORG                                                                  
                                                                                
***********************************************************************         
*        EXTRACT CHECK INFO FOR BILLING                               *         
*        ON ENTRY ... R2=W4 TYPE ACCUMULATOR                                    
*                     R4=A(PAYMENT DETAILS ELEMENT)                   *         
***********************************************************************         
                                                                                
         USING TAPDD,R4                                                         
EXTCHKB  NTR1  BASE=*,LABEL=*                                                   
         CLI   TPTYPE,TPTYPEB                                                   
         JNE   XIT                                                              
                                                                                
         OC    TPCANCVT,TPCANCVT   IF CANADIAN DOLLAR INVOICE                   
         JZ    ECB20                                                            
         LR    R3,R2               CONVERT ALL PAYMENT DETAIL AMOUNTS           
         LHI   R0,TAPDAMTL/L'TAPDAMTS                                           
         LA    R2,TAPDAMTS                                                      
ECB10    GOTOR CVTAMT,DMCB,0(R2)                                                
         LA    R2,4(R2)                                                         
         BCT   R0,ECB10                                                         
         LR    R2,R3                                                            
                                                                                
ECB20    L     RF,0(R2)                                                         
         A     RF,TAPDPAYI                                                      
         A     RF,TAPDPAYC                                                      
         ST    RF,0(R2)                                                         
                                                                                
         TM    TPPDPST1,TAPDPBNP   BNP SAVE UP THE P&H                          
         JZ    XIT                                                              
         TM    TPINSTAT,TAINSHLD   UNLESS IT'S PUR                              
         JO    XIT                                                              
                                                                                
         OC    TAPDPNH,TAPDPNH                                                  
         JZ    XIT                                                              
         GOTOR UPDPNHTB,DMCB,TAPDPNH                                            
         J     XIT                                                              
         DROP  R4                                                               
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        GENERATE BILLING POSTINGS                                    *         
*        ON ENTRY ... AIO1=A(INVOICE RECORD)                          *         
***********************************************************************         
                                                                                
GENPOSTB NTR1  BASE=*,LABEL=*                                                   
         CLI   TPTYPE,TPTYPEB                                                   
         JNE   XIT                                                              
                                                                                
         USING TAPGD,R4                                                         
         L     R4,AIO1                                                          
         MVI   ELCODE,TAPGELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   GPB10                                                            
         CLI   TAPGLEN,TAPGLNQ     IF OLD LENGTH,                               
         BNL   GPB05                                                            
         OC    TAPGHNDI(TAPGWFEE-TAPGHNDI),TAPGHNDI                             
         J     *+10                                                             
GPB05    OC    TAPGHNDI(TAPGEND-TAPGHNDI),TAPGHNDI                              
         JNZ   *+6                                                              
GPB10    XR    R4,R4                                                            
         DROP  R4                                                               
                                                                                
         USING TAPGD,R2                                                         
         LR    R2,R4                                                            
                                                                                
         USING TAPDD,R4                                                         
         L     R4,AIO1                                                          
         MVI   ELCODE,TAPDELQ                                                   
         BRAS  RE,GETEL                                                         
         JE    *+6                                                              
         DC    H'00'                                                            
         GOTOR OUTLINE,DMCB,('TPCREDIT',TPCATOT),BPPYA                          
         GOTOR OUTLINE,DMCB,('TPCREDIT',TPAOSTOT),BPPYAOS                       
         GOTOR OUTLINE,DMCB,('TPCREDIT',TPCOTOT),BPPYC                          
         GOTOR OUTLINE,DMCB,('TPCREDIT',TPESTOT),BPPYE                          
         GOTOR OUTLINE,DMCB,('TPCREDIT',TPFOTOT),BPPYF                          
         GOTOR OUTLINE,DMCB,('TPCREDIT',TPINTOT),BPPYI                          
                                                                                
**no-op  GOTOR OUTLINE,DMCB,('TPCREDIT',TAPDREXP),BPREXP                        
         GOTOR OUTLINE,DMCB,('TPCREDIT',TAPDNTNW),BPREXPN NON-TAX REIMB         
         GOTOR OUTLINE,DMCB,('TPCREDIT',TAPDTXNW),BPREXPT TAXABLE REIMB         
         GOTOR OUTLINE,DMCB,('TPCREDIT',TAPDPNH),BPPNH                          
                                                                                
         OC    TPSUBHNW,TPSUBHNW                                                
         JZ    GPB15                                                            
         ICM   RE,15,TPSUBHNW                                                   
         J     GPB30                                                            
                                                                                
GPB15    L     RE,TAPDHNW                                                       
         L     R0,TPHNWADJ                                                      
         TM    TPPDPST1,TAPDPCRD   IF CREDIT INVOICE                            
         JZ    GPB20                                                            
         LCR   R0,R0               COMPLIMENT H&W ADJUSTMENT                    
GPB20    AR    RE,R0               ADD ANY H&W ADJUSTMENTS                      
         XR    R0,R0                                                            
         LTR   RE,RE                                                            
         JNM   GPB30                                                            
         OC    TPHNWADJ,TPHNWADJ   IF THERE IS AN HNWADJ,                       
         JZ    GPB30                                                            
         XR    RE,RE               AND RESULT < 0, MAKE HNW AMT 0               
GPB30    ST    RE,TGFULL                                                        
         GOTOR OUTLINE,DMCB,('TPCREDIT',TGFULL),BPHNW                           
                                                                                
         GOTOR OUTLINE,DMCB,('TPCREDIT',TAPDINR),BPINR                          
         GOTOR OUTLINE,DMCB,('TPCREDIT',TAPDDUES),BPDUES                        
                                                                                
         USING TABDD,R4                                                         
         L     R4,AIO1                                                          
         MVI   ELCODE,TABDELQ                                                   
         BRAS  RE,GETEL                                                         
         JE    *+6                                                              
         DC    H'00'                                                            
                                                                                
         LA    RF,TABDHND                                                       
         LTR   R2,R2                  IND. HANDLING                             
         JZ    GPB40                                                            
********************************************************************            
* TAKE THIS OUT WHEN BILLING TAKES CARE OF HANDLING OVERRIDES      *            
********************************************************************            
**NO-OP  OC    TAPGHNDI(TAPGWCN-TAPGHNDI),TAPGHNDI    NEW HANDLING?             
**04/14  JZ    GPB40                                                            
***************                                                                 
         LA    RF,TAPGHNDI                                                      
                                                                                
         OC    TPCANCVT,TPCANCVT   IF CANADIAN DOLLAR INVOICE                   
         BZ    GPB35                                                            
         GOTOR OUTLINE,DMCB,('TPCREDIT',0(RF)),BPHANDCN                         
         B     GPB45                                                            
                                                                                
GPB35    OC    TPEURCVT,TPEURCVT   IF EURO INVOICE                              
         BZ    GPB40                                                            
         GOTOR OUTLINE,DMCB,('TPCREDIT',0(RF)),BPHANDEU                         
         B     GPB45                                                            
                                                                                
GPB40    GOTOR OUTLINE,DMCB,('TPCREDIT',0(RF)),BPHAND                           
***************                                                                 
GPB45    LTR   R2,R2                                                            
         JZ    GPB50                                                            
         OC    TAPGHNDI(TAPGWCN-TAPGHNDI),TAPGHNDI    NEW HANDLING?             
         JZ    GPB69                                  OVERRIDE, SKIP            
***************                                                                 
                                                                                
         LTR   R2,R2               PREMIUM IND HANDLING                         
         JZ    GPB50                                                            
                                                                                
         OC    TPCANCVT,TPCANCVT   IF CANADIAN DOLLAR INVOICE                   
         BZ    GPB47                                                            
         GOTOR OUTLINE,DMCB,('TPCREDIT',TAPGHPRI),BPHNDCNP                      
         B     GPB50                                                            
                                                                                
GPB47    OC    TPEURCVT,TPEURCVT   IF EURO INVOICE                              
         BZ    GPB49                                                            
         GOTOR OUTLINE,DMCB,('TPCREDIT',TAPGHPRI),BPHNDEUP                      
         B     GPB50                                                            
                                                                                
GPB49    GOTOR OUTLINE,DMCB,('TPCREDIT',TAPGHPRI),BPHANDP                       
                                                                                
GPB50    LA    RF,TABDHNDC         CORP HANDLING                                
         LTR   R2,R2                                                            
         JZ    GPB58                                                            
         LA    RF,TAPGHNDC                                                      
                                                                                
         OC    TPCANCVT,TPCANCVT   IF CANADIAN DOLLAR INVOICE                   
         BZ    GPB55                                                            
         GOTOR OUTLINE,DMCB,('TPCREDIT',0(RF)),BPHANDCN                         
         B     GPB59                                                            
                                                                                
GPB55    OC    TPEURCVT,TPEURCVT   IF EURO INVOICE                              
         BZ    GPB58                                                            
         GOTOR OUTLINE,DMCB,('TPCREDIT',0(RF)),BPHANDEU                         
         B     GPB59                                                            
                                                                                
GPB58    GOTOR OUTLINE,DMCB,('TPCREDIT',0(RF)),BPHANDC                          
                                                                                
GPB59    LTR   R2,R2              PREMIUM CORP HANDLING                         
         JZ    GPB69                                                            
                                                                                
         OC    TPCANCVT,TPCANCVT   IF CANADIAN DOLLAR INVOICE                   
         BZ    GPB65                                                            
         GOTOR OUTLINE,DMCB,('TPCREDIT',TAPGHPRC),BPHNDCNP                      
         B     GPB68                                                            
                                                                                
GPB65    OC    TPEURCVT,TPEURCVT   IF EURO INVOICE                              
         BZ    GPB67                                                            
         GOTOR OUTLINE,DMCB,('TPCREDIT',TAPGHPRC),BPHNDEUP                      
         B     GPB68                                                            
                                                                                
GPB67    GOTOR OUTLINE,DMCB,('TPCREDIT',TAPGHPRC),BPHANDCP                      
                                                                                
GPB68    GOTOR OUTLINE,DMCB,('TPCREDIT',TAPGHIAG),BPHNDGRI                      
         GOTOR OUTLINE,DMCB,('TPCREDIT',TAPGHCAG),BPHNDGRC                      
         GOTOR OUTLINE,DMCB,('TPCREDIT',TAPGHIAP),BPHNDGIP                      
         GOTOR OUTLINE,DMCB,('TPCREDIT',TAPGHCAP),BPHNDGCP                      
                                                                                
*        GOTOR OUTLINE,DMCB,('TPCREDIT',TAPGHISU),BPHIS                         
                                                                                
GPB69    BRAS  RE,GETWIRE               GET WIRE FEE                            
                                                                                
         OC    TABDCSF,TABDCSF                                                  
         JZ    GPB80                                                            
         OC    TABDCCVT,TABDCCVT        CANADIAN/EUROS?                         
         JZ    GPB70                                                            
         GOTOR OUTLINE,DMCB,('TPCREDIT',TABDCSF),BPCONSF                        
         J     GPB80                                                            
                                                                                
GPB70    L     RE,TABDCSF                                                       
         SHI   RE,300              DEDUCT $3                                    
         ST    RE,TGFULL                                                        
         GOTOR OUTLINE,DMCB,('TPCREDIT',TGFULL),BPCSF                           
                                                                                
         MVC   TGFULL,=F'300'      POST $3                                      
         GOTOR OUTLINE,DMCB,('TPCREDIT',TGFULL),BPCSFMR                         
                                                                                
GPB80    GOTOR OUTLINE,DMCB,('TPCREDIT',TABDGST),BPGST                          
                                                                                
         CLI   TABDTYPE,TABRTY20  TALENT MASTERS FEE                            
         JNE   GPB100                                                           
         CLC   TGAGY,=CL6'0585'                                                 
         JNE   GPB90                                                            
         GOTOR OUTLINE,DMCB,('TPCREDIT',TABDACOM),BPLUNA                        
         J     GPB120                                                           
GPB90    GOTOR OUTLINE,DMCB,('TPCREDIT',TABDACOM),BPTALM                        
         J     GPB120                                                           
                                                                                
GPB100   CLI   TABDTYPE,TABRTYE    EMS FEE                                      
         JNE   GPB110                                                           
         GOTOR OUTLINE,DMCB,('TPCREDIT',TABDACOM),BPEMS                         
         J     GPB120                                                           
                                                                                
GPB110   CLC   TGAGY,=CL6'1094'    BROADCAST BUSINESS FEE                       
         JNE   GPB115                                                           
         GOTOR OUTLINE,DMCB,('TPCREDIT',TABDACOM),BPBRBF                        
         J     GPB120                                                           
                                                                                
GPB115   CLI   TPMED,TACOMEDE      IF EMPLOYER P+                               
         JNE   GPB120              AGENCY COMMISSION                            
         GOTOR OUTLINE,DMCB,('TPCREDIT',TABDACOM),BPACPP                        
                                                                                
GPB120   GOTOR OUTLINE,DMCB,('TPCREDIT',TABDSIGN),BPSIGN                        
         GOTOR OUTLINE,DMCB,('TPCREDIT',TABDPST),BPPST                          
                                                                                
         LTR   R2,R2               PAYROLL TAXES                                
         JNZ   GPB130              AND FICA CREDITS                             
         L     R1,TABDFICR                                                      
         LCR   R1,R1               COMPLEMENT OVER FICA AMOUNT                  
         ST    R1,TABDFICR                                                      
         BRAS  RE,EXTRATAX         EXTRA TAXES (WA, HI)                         
                                                                                
         L     R5,TABDTAX                                                       
         SR    R5,R6                                                            
         ST    R5,TABDTAX                                                       
         GOTOR OUTLINE,DMCB,('TPCREDIT',TABDTAX),BPTAX                          
         J     GPB140                                                           
                                                                                
GPB130   DS    0H                                                               
         GOTOR OUTLINE,DMCB,('TPCREDIT',TAPGFUTA),BPFUTA                        
         GOTOR OUTLINE,DMCB,('TPCREDIT',TAPGSUTA),BPSUTA                        
         GOTOR OUTLINE,DMCB,('TPCREDIT',TAPGFICA),BPFICA                        
         GOTOR OUTLINE,DMCB,('TPCREDIT',TAPGMED),BPMED                          
         GOTOR OUTLINE,DMCB,('TPCREDIT',TAPGWCN),BPWCN                          
         GOTOR OUTLINE,DMCB,('TPCREDIT',TAPGWCC),BPWCC                          
         BRAS  RE,EXTRATAX         EXTRA TAXES (WA, HI)  FOR BRATE              
         DROP  R2                                                               
                                                                                
GPB140   BRAS  RE,CANRODB          FIX CANADIAN ROUNDING ISSUES                 
                                                                                
         GOTOR OUTLINE,DMCB,('TPDEBIT+TPTOTAL',TABDTOT),BPTOT                   
         BRAS  RE,OUTPHTBB                                                      
                                                                                
         OC    TPINRHND,TPINRHND                                                
         JZ    GPB500                                                           
                                                                                
         BRAS  RE,GETGSTTX         GET GST TAX                                  
                                                                                
GPB500   XC    TPINRHND,TPINRHND                                                
         J     XIT                                                              
         DROP  R4                                                               
         LTORG                                                                  
         EJECT                                                                  
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
BPPYA    DC    CL20'CANADIAN PAYMENT'                                           
         DC    CL15'/0SE41009'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPPYAOS  DC    CL20'AOS'                                                        
         DC    CL15'/0SE41003'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPPYC    DC    CL20'CORPORATION PAYMENT'                                        
         DC    CL15'/0SE41003'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPPYE    DC    CL20'ESTATE PAYMENT'                                             
         DC    CL15'/0SE41003'                                                  
         DC    CL15'/0SR/o'     '                                               
                                                                                
BPPYF    DC    CL20'FOREIGNER PAYMENT'                                          
         DC    CL15'/0SE41003'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPPYI    DC    CL20'INDIVIDUAL PAYMENT'                                         
         DC    CL15'/0SE41001'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPREXPN  DC    CL20'REIMB EXP'                                                  
         DC    CL15'/0SE41003'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPREXPT  DC    CL20'REIMB EXP TAXABLE'                                          
         DC    CL15'/0SE41013'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPPNH    DC    CL20'PNH'                                                        
         DC    CL15'/0SE41007'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPHNW    DC    CL20'HNW'                                                        
         DC    CL15'/0SE41007'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPINR    DC    CL20'INR'                                                        
         DC    CL15'/0SE41007'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPDUES   DC    CL20'UNION DUES'                                                 
         DC    CL15' '                                                          
         DC    CL15' '                                                          
                                                                                
BPTOT    DC    CL20'TOTAL'                                                      
         DC    CL15'/0SR/r'                                                     
         DC    CL15'   /c'                                                      
                                                                                
BPTOTNEQ DC    CL20'TOTAL*****'                                                 
         DC    CL15'/0SR/r'                                                     
         DC    CL15'   /c'                                                      
                                                                                
BPTAX    DC    CL20'PAYROLL TAX'                                                
         DC    CL15'/0SE41004'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPHAND   DC    CL20'HANDLING'                                                   
         DC    CL15'/0SE41006'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPHANDP  DC    CL20'PREMIUM HANDLING'                                           
         DC    CL15'/0SE41006'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPHANDC  DC    CL20'CORP HANDLING'                                              
         DC    CL15'/0SE41006'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPHANDCP DC    CL20'PREMIUM CORP HAND'                                          
         DC    CL15'/0SE41006'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPHANDCN DC    CL20'CANADIAN HANDLING'                                          
         DC    CL15'/0SE41010'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPHNDCNP DC    CL20'PREMIUM CAN HANDLING'                                       
         DC    CL15'/0SE41010'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPHANDEU DC    CL20'EURO HANDLING'                                              
         DC    CL15'/0SE41010'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPHNDEUP DC    CL20'PREMIUM EURO HANDLING'                                      
         DC    CL15'/0SE41010'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPHNDGRI DC    CL20'IND HAND APPL GRT'                                          
         DC    CL15'/0SE41006'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPHNDGRC DC    CL20'CORP HAND APPL GRT'                                         
         DC    CL15'/0SE41006'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPHNDGIP DC    CL20'PIND HAND APPL GRT'                                         
         DC    CL15'/0SE41006'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPHNDGCP DC    CL20'PCRP HAND APPL GRT'                                         
         DC    CL15'/0SE41006'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPCSF    DC    CL20'CSF PAYABLE'                                                
         DC    CL15'/0SE41050'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPCSFMR  DC    CL20'CSF MASTER REVENUE'                                         
         DC    CL15'/0SE41050'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPCONSF  DC    CL20'CONTRACT SERVICE FEE'                                       
         DC    CL15'/0SE41050'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPFICR   DC    CL20'FICA CREDIT'                                                
         DC    CL15'/0SE41005'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPGST    DC    CL20'GST'                                                        
         DC    CL15'/0SE41011'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPTALM   DC    CL20'TALENT MASTERS'                                             
         DC    CL15'/0SE41096'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPEMS    DC    CL20'EMS FEE'                                                    
         DC    CL15' '                                                          
         DC    CL15' '                                                          
                                                                                
BPLUNA   DC    CL20'LUNA PROD && TALENT'                                        
         DC    CL15' '                                                          
         DC    CL15' '                                                          
                                                                                
BPACPP   DC    CL20'P+ AGY COMMISSION'                                          
         DC    CL15'/0SE21210'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPBRBF   DC    CL20'BROADCAST BUS FEE'                                          
         DC    CL15'/0SE41096'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPSIGN   DC    CL20'SIG FEE'                                                    
         DC    CL15'/0SE41096'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPPST    DC    CL20'PST'                                                        
         DC    CL15'/0SE41011'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPFUTA   DC    CL20'FUTA'                                                       
         DC    CL15' '                                                          
         DC    CL15' '                                                          
                                                                                
BPSUTA   DC    CL20'SUTA'                                                       
         DC    CL15' '                                                          
         DC    CL15' '                                                          
                                                                                
*PHIS    DC    CL20'HAWAIIAN SURCHARGE'                                         
*        DC    CL15' '                                                          
*        DC    CL15' '                                                          
                                                                                
BPFICA   DC    CL20'FICA'                                                       
         DC    CL15' '                                                          
         DC    CL15' '                                                          
                                                                                
BPWCN    DC    CL20'WC NON-CORP'                                                
         DC    CL15' '                                                          
         DC    CL15' '                                                          
                                                                                
BPMED    DC    CL20'MEDICARE'                                                   
         DC    CL15' '                                                          
         DC    CL15' '                                                          
                                                                                
BPWCC    DC    CL20'WC CORP'                                                    
         DC    CL15' '                                                          
         DC    CL15' '                                                          
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        GET GST TAX                                                  *         
***********************************************************************         
         USING TAPGD,R2                                                         
GETGSTTX NTR1  BASE=*,LABEL=*                                                   
         LTR   R2,R2                                                            
         JZ    GGST10                                                           
         GOTOR OUTLINE,DMCB,('TPCREDIT',TAPGCGST),BPGST2                        
                                                                                
         L     R1,TAPGCGST         CANADIAN GST TAX                             
         S     R1,TAPGUGST         - US GST TAX = DIFFERENCE                    
         STCM  R1,15,TPGSTTXD                                                   
         GOTOR OUTLINE,DMCB,('TPDEBIT',TPGSTTXD),BPGST4                         
         GOTOR OUTLINE,DMCB,('TPDEBIT+TPTOTAL',TAPGUGST),BPGST3                 
         J     GGSTX                                                            
         DROP  R2                                                               
*                                                                               
GGST10   XR    R0,R0                                                            
*&&DO                                                                           
         ICM   R1,15,TPINRHND                                                   
         ICM   RE,15,TPGSTRAT                                                   
         MR    R0,RE                                                            
         LH    RF,=H'5000'                                                      
         DR    R0,RF                                                            
         LTR   R1,R1               QUOTIENT                                     
         BM    *+8                                                              
         AHI   R1,1                                                             
         SRA   R1,1                                                             
         STCM  R1,15,TPGSTTXC                                                   
*&&                                                                             
                                                                                
         GOTOR OUTLINE,DMCB,('TPCREDIT',TPGSTTXC),BPGST2                        
                                                                                
         ICM   R1,15,TPGSTTXC                                                   
         ICM   RE,15,TPCANCVT                                                   
         XR    R0,R0                                                            
         MR    R0,RE                                                            
         LH    RF,=H'5000'                                                      
         DR    R0,RF                                                            
         LTR   R1,R1               QUOTIENT                                     
         BM    *+8                                                              
         AHI   R1,1                                                             
         SRA   R1,1                                                             
         STCM  R1,15,TPGSTTXU                                                   
                                                                                
         ICM   RF,15,TPGSTTXC                                                   
         SR    RF,R1                                                            
         STCM  RF,15,TPGSTTXD                                                   
                                                                                
         GOTOR OUTLINE,DMCB,('TPDEBIT',TPGSTTXD),BPGST4                         
         GOTOR OUTLINE,DMCB,('TPDEBIT+TPTOTAL',TPGSTTXU),BPGST3                 
GGSTX    J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
                                                                                
BPGST2   DC    CL20'GST TAX'            CAN$                                    
         DC    CL15'/0SYTXCN G'                                                 
         DC    CL15'/0SE42011'                                                  
                                                                                
BPGST3   DC    CL20'GST TAX CONVERTED'  US$                                     
         DC    CL15'/0SE42011'                                                  
         DC    CL15'/0SYTXCN G'                                                 
                                                                                
BPGST4   DC    CL20'GST TAX CONV DIFF'  US$                                     
         DC    CL15'/0SK2CDNGST'                                                
         DC    CL15'/0SE42011'                                                  
         LTORG                                                                  
         EJECT                                                                  
                                                                                
***********************************************************************         
*        GET WIRE FEE                                                 *         
***********************************************************************         
         USING TAPGD,R2                                                         
GETWIRE  NTR1  BASE=*,LABEL=*                                                   
         LTR   R2,R2                                                            
         JZ    GWIREX                                                           
         CLI   TAPGLEN,TAPGLNQ                                                  
         JL    GWIREX                                                           
         OC    TAPGWFEE,TAPGWFEE        IF WE HAVE WIRE FEE,                    
         JZ    GWIREX                                                           
                                                                                
         OC    TPCANCVT,TPCANCVT        IF CANADIAN DOLLAR INVOICE              
         JZ    GWIRE10                                                          
         GOTOR OUTLINE,DMCB,('TPCREDIT',TAPGWFEE),BPWIRECN                      
         J     GWIREX                                                           
                                                                                
GWIRE10  OC    TPEURCVT,TPEURCVT        IF EURO INVOICE                         
         JZ    GWIRE20                                                          
         GOTOR OUTLINE,DMCB,('TPCREDIT',TAPGWFEE),BPWIREEU                      
         J     GWIREX                                                           
                                                                                
GWIRE20  GOTOR OUTLINE,DMCB,('TPCREDIT',TAPGWFEE),BPWIRE                        
                                                                                
GWIREX   J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
                                                                                
BPWIRECN DC    CL20'CANADIAN WIRE FEE'                                          
         DC    CL15'/0SE41010'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPWIRE   DC    CL20'WIRE FEE'                                                   
         DC    CL15'/0SE41006'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPWIREEU DC    CL20'EURO WIRE FEE'                                              
         DC    CL15'/0SE41010'                                                  
         DC    CL15'/0SR/o'                                                     
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        OUTPUT P&H TABLE                                             *         
***********************************************************************         
                                                                                
OUTPHTBB NTR1  BASE=*,LABEL=*                                                   
         USING PNHD,R2                                                          
         L     R2,TPAPHTAB                                                      
                                                                                
OPHTB10  CLI   0(R2),X'FF'                                                      
         JE    OPHTB20                                                          
         CLC   PNHUN,=3X'00'                                                    
         JE    OPHTB20                                                          
                                                                                
         MVC   TGUNI,PNHUN                                                      
         MVC   TGLCL,PNHLCL                                                     
                                                                                
         ICM   RF,15,PNHAMT                                                     
         A     RF,TPPNHTOT                                                      
         ST    RF,TPPNHTOT                                                      
                                                                                
         GOTOR OUTLINE,DMCB,('TPCREDIT',PNHAMT),BPPNH2                          
                                                                                
         XC    0(PNHLNQ,R2),0(R2)  CLEAR IT OUT FOR NEXT INVOICE                
         LA    R2,PNHLNQ(R2)       BUMP TO NEXT ENTRY                           
         J     OPHTB10                                                          
         DROP  R2                                                               
                                                                                
OPHTB20  GOTOR OUTLINE,DMCB,('TPDEBIT',TPPNHTOT),BPPNHT2                        
         J     XIT                                                              
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
BPPNH2   DC    CL20'BNP PNH'                                                    
         DC    CL15'/0SXPW/u'                                                   
         DC    CL15'   /l P&&H'                                                 
                                                                                
BPPNHT2  DC    CL20'BNP P&&H TOTAL'                                             
         DC    CL15'/0SE42005'                                                  
         DC    CL15'/0SXPW'                                                     
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        FIX SMALL CANADIAN ROUNDING ISSUES BY ADJUSTING CREDITS      *         
*        ON ENTRY ... DUB=(PACKED VALUE OF DEBIT TOTAL)               *         
***********************************************************************         
                                                                                
         USING TABDD,R4                                                         
CANRODB  NTR1  BASE=*,LABEL=*                                                   
         OC    TPCANCVT,TPCANCVT   IF THIS IS A CANADIAN PAYMENT                
         JZ    XIT                                                              
         L     R1,TABDTOT                                                       
         CVD   R1,DUB                                                           
         CP    DUB,TPICTOT         AND THE DEBIT TOTAL DOES NOT MATCH           
         JE    XIT                 THE CREDIT TOTAL                             
         CVB   R1,DUB                                                           
         ZAP   DUB,TPICTOT                                                      
         CVB   R2,DUB                                                           
         SR    R1,R2               SUBTRACT DEBITS FROM CREDITS                 
         JZ    XIT                                                              
         LPR   RE,R1                                                            
         CHI   RE,10               IF DIFFERENCE IS 10 CENTS OR LESS            
         JH    XIT                                                              
         ST    R1,FULL             POST AS CANADIAN ROUNDING                    
         GOTOR OUTLINE,DMCB,('TPCREDIT',FULL),BPCRO                             
         J     XIT                                                              
         DROP  R4                                                               
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
BPCRO    DC    CL20'CANADIAN ROUNDING'                                          
         DC    CL15' '                                                          
         DC    CL15' '                                                          
                                                                                
         LTORG                                                                  
         EJECT                                                                  
                                                                                
***********************************************************************         
*        EXTRA TAXES                                                  *         
*           RETURNS R6, TOTAL EXTRA TAXES                             *         
***********************************************************************         
         USING TAXTD,R4                                                         
EXTRATAX NTR1  BASE=*,LABEL=*                                                   
                                                                                
         XR    R6,R6                                                            
         L     R4,AIO1                                                          
         MVI   ELCODE,TAXTELQ      FIND ALL EXTRA TAXES                         
         BRAS  RE,GETEL                                                         
         J     EXTX200                                                          
EXTX100  BRAS  RE,NEXTEL                                                        
EXTX200  JNE   EXTX900                                                          
                                                                                
         ICM   R5,15,TAXTTAXS                                                   
         LA    R3,BPCABO                                                        
         CLC   =C'CA ',TAXTUNIT    ONLY WASH AND HAWAII NOW                     
         JE    EXTX300                                                          
         LA    R3,BPHIEX                                                        
         CLC   =C'HI ',TAXTUNIT                                                 
         JE    EXTX300                                                          
         LA    R3,BPNYEX                                                        
         CLC   =C'NY ',TAXTUNIT                                                 
         JE    EXTX300                                                          
         LA    R3,BPWABO                                                        
         CLC   =C'WA ',TAXTUNIT    ONLY WASH AND HAWAII NOW                     
         JE    EXTX300                                                          
         J     EXTX100                                                          
                                                                                
EXTX300  AR    R6,R5               ACCUMULATE THEM                              
         GOTOR OUTLINE,DMCB,('TPCREDIT',TAXTTAXS),(R3)                          
         J     EXTX100                                                          
                                                                                
EXTX900  XIT1  REGS=(R6)                                                        
                                                                                
         DROP  R4                                                               
                                                                                
BPCABO   DC    CL20'CALIF SUI SURCHARGE'                                        
         DC    CL15'/0SE41014'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPHIEX   DC    CL20'HAWAII EXCISE TAX'                                          
         DC    CL15'/0SE41008'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPNYEX   DC    CL20'NEW YORK SUI SURCHRG'                                       
         DC    CL15'/0SE41014'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
BPWABO   DC    CL20'WASHINGTON BO TAX'                                          
         DC    CL15'/0SE41012'                                                  
         DC    CL15'/0SR/o'                                                     
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        SET ACCOUNT AND CONTRA ACCOUNT FOR BILLING POSTING           *         
*        ON ENTRY ... R3=A(POSTING MAPPING ENTRY)                     *         
***********************************************************************         
                                                                                
         USING POSTD,R3                                                         
SETACTSB NTR1  BASE=*,LABEL=*                                                   
         CLI   TPTYPE,TPTYPEB      IF GENERATING BILLING POSTINGS               
         JNE   XIT                                                              
         TM    TPOPTS1,TPOHIST2    AND NOT HIST2                                
         JO    XIT                                                              
                                                                                
         GOTOR CDYNACCT,DMCB,POSTACCT,TPACCT                                    
         GOTOR CDYNACCT,DMCB,POSTCTRA,TPCNTRA                                   
         J     XIT                                                              
         DROP  R3                                                               
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        ROUTINE UPDATES P&H TABLE                                    *         
*        ON ENTRY ... P1=A(P&H AMOUNT)                                *         
***********************************************************************         
                                                                                
UPDPNHTB NTR1  BASE=*,LABEL=*                                                   
         USING TACAD,R4                                                         
         L     R4,AIO                                                           
         MVI   ELCODE,TACAELQ    GET CAST DETAILS ELEMENT                       
         BRAS  RE,GETEL                                                         
         JNE   XIT                                                              
                                                                                
         L     RF,0(R1)          RF=A(P&H AMOUNT)                               
                                                                                
         USING PNHD,R2                                                          
         L     R2,TPAPHTAB                                                      
UPNHT10  CLC   PNHUN,TACAUN        FIND UNION/LOCAL IN P&H TABLE                
         JNE   UPNHT20                                                          
         CLC   PNHLCL,TACALOCL                                                  
         JE    UPNHT40                                                          
UPNHT20  OC    PNHUN,PNHUN                                                      
         JZ    UPNHT30             ELSE, ADD IT TO FIRST FREE ENTRY             
         LA    R2,PNHLNQ(R2)                                                    
         CLI   0(R2),X'FF'                                                      
         JNE   UPNHT10                                                          
         DC    H'0'                HAVE TO INCREASE TABLE                       
                                                                                
UPNHT30  MVC   PNHUN,TACAUN                                                     
         MVC   PNHLCL,TACALOCL                                                  
         DROP  R4                                                               
                                                                                
UPNHT40  ICM   RE,15,PNHAMT                                                     
         A     RE,0(RF)                                                         
         STCM  RE,15,PNHAMT                                                     
         J     XIT                                                   **         
         DROP  R2                                                               
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
*        CONVERT DYNAMIC ACCOUNT                                      *         
*          P1 = A(ACCOUNT)                                            *         
*          P2 = A(CONVERTED ACCOUNT) - 14 CHAR LIMIT                  *         
*                                                                     *         
*               /a = Agency Code                                      *         
*               /c = Client Name                                      *         
*               /l = Union Local                                      *         
*               /o = Office                                           *         
*               /r = Receivable Account                               *         
*               /t = Tax Unit                                         *         
*               /u = Union                                            *         
*               /0 = ACCPAK Company Code                              *         
***********************************************************************         
                                                                                
CDYNACCT NTR1  BASE=*,LABEL=*                                                   
         L     R5,0(R1)            ACCOUNT TO CONVERT                           
         L     R6,4(R1)            WORK                                         
         LA    R7,CVTDACCT                                                      
         MVC   CVTDACCT,SPACES                                                  
                                                                                
         LA    R3,15               MAX LENGTH FOR ACCOUNT                       
CDA100   CLI   0(R5),C'/'          LOOK FOR DYNAMIC CHARACTER                   
         JNE   CDA500                                                           
                                                                                
         SHI   R3,1                SUBTRACT FROM LENGTH                         
         AHI   R5,1                BUMP TO WHICH DYNAMIC DEFINITION             
                                                                                
         LA    RE,DYNROUT          DYNAMIC ROUTINES                             
         XR    R1,R1                                                            
CDA200   CLI   0(RE),X'FF'         EOT                                          
         JE    CDA700              NOT IN TABLE, SKIP IT                        
         CLC   0(1,R5),0(RE)       MATCH TO CHARACTER                           
         JE    CDA400                                                           
         AHI   RE,1                                                             
         AHI   R1,4                                                             
         J     CDA200              CONTINUE CONVERTING REST OF ACCOUNT          
                                                                                
CDA400   LA    RE,CDAROUT                                                       
         AR    RE,R1                                                            
                                                                                
         L     RF,0(RE)            LOAD THE ROUTINE                             
         BASR  RE,RF                                                            
         J     CDA700                                                           
                                                                                
CDA500   MVC   0(1,R7),0(R5)       COPY ONE TO ONE WHEN NOT DYNAMIC             
         AHI   R7,1                                                             
CDA700   AHI   R5,1                BUMP TO NEXT CHAR                            
         BCT   R3,CDA100                                                        
                                                                                
         MVC   0(15,R6),CVTDACCT                                                
         J     XIT                                                              
                                                                                
CVTDACCT DC    CL32' '                                                          
                                                                                
DYNROUT  DC    C'a'                AGENCY CODE                                  
         DC    C'c'                CLIENT NAME                                  
         DC    C'e'                EMPLOYER                                     
         DC    C'l'                UNION LOCAL                                  
         DC    C'o'                OFFICE                                       
         DC    C'r'                RECEIVABLE ACCT                              
         DC    C't'                TAX UNIT                                     
         DC    C'u'                UNION                                        
         DC    C'0'                ACC COMPANY CODE                             
         DC    X'FF'                                                            
                                                                                
CDAROUT  DS    0A                                                               
         DC    AL4(DYNAGY)         AGENCY CODE                                  
         DC    AL4(DYNCLIN)        CLIENT NAME                                  
         DC    AL4(DYNEMP)         EMPLOYER                                     
         DC    AL4(DYNULCL)        UNION LOCAL                                  
         DC    AL4(DYNOFF)         OFFICE                                       
         DC    AL4(DYNRCVA)        RECEIVABLE ACCT                              
         DC    AL4(DYNUNIT)        TAX UNIT                                     
         DC    AL4(DYNUNI)         UNION                                        
         DC    AL4(DYNCC)          ACC COMPANY CODE                             
         DC    X'FFFFFFFF'                                                      
         EJECT                                                                  
*======================================================================         
DYNAGY   NTR1                                                                   
         MVC   0(L'TGAGY,R7),TGAGY       AGENCY CODE                            
         J     DYNCLEAN                                                         
                                                                                
DYNCLIN  NTR1                                                                   
         MVC   AIO,AIO2                                                         
         GOTO1 TPRCVAL,DMCB,TLCLCDQ,(X'A0',TGCLI)                               
         USING TANAD,R4                                                         
         L     R4,AIO2                                                          
         MVI   ELCODE,TANAELQ      GET CLIENT NAME                              
         BRAS  RE,GETEL                                                         
         JE    *+6                                                              
         DC    H'00'                                                            
         ZIC   RF,TANALEN                                                       
         CHI   RF,14               ONLY NEED MAX 12 CHARS                       
         JNH   DYNC100                                                          
         LHI   RF,14                                                            
DYNC100  SHI   RF,3                                                             
         EX    RF,*+8                                                           
         J     *+10                                                             
         MVC   0(0,R7),TANANAME                                                 
         AR    R7,RF                                                            
         AHI   R7,1                                                             
         J     DYNCLN20                                                         
                                                                                
DYNEMP   NTR1                                                                   
         TM    TPOPTS1,TPOPRNT+TPOPLUS                                          
         BZ    DYNCLEAN                                                         
         MVC   0(2,R7),=C'PP'            PRINT EMPLOYER                         
         TM    TPOPTS1,TPOPRNT                                                  
         BO    DYNCLEAN                                                         
         MVC   0(2,R7),=C'P+'            P+ EMPLOYER                            
         J     DYNCLEAN                                                         
                                                                                
DYNULCL  NTR1                                                                   
         MVC   0(L'TGUNILCL,R7),TGUNILCL                                        
         AHI   R7,L'TGUNILCL                                                    
         J     DYNCLN20                                                         
                                                                                
DYNOFF   NTR1                                                                   
         MVC   0(L'TGOFF,R7),TGOFF       OFFICE                                 
         J     DYNCLEAN                                                         
                                                                                
DYNRCVA  NTR1                                                                   
         MVC   AIO,AIO2            GET RECEIVABLE ACCT                          
         GOTO1 TPRCVAL,DMCB,TLAYCDQ,(X'A0',TGAGY)                               
         USING TABRD,R4                                                         
         L     R4,AIO2                                                          
         MVI   ELCODE,TABRELQ                                                   
         BRAS  RE,GETEL                                                         
         JE    *+6                                                              
         DC    H'00'                                                            
         MVC   0(L'TABRRECV,R7),TABRRECV                                        
         J     DYNCLEAN                                                         
                                                                                
DYNUNIT  NTR1                                                                   
         MVC   0(L'TPSVUNIT,R7),TPSVUNIT                                        
         AHI   R7,L'TPSVUNIT                                                    
         J     DYNCLN20                                                         
                                                                                
DYNUNI   NTR1                                                                   
         MVC   0(L'TGUNI,R7),TGUNI                                              
         AHI   R7,L'TGUNI                                                       
         J     DYNCLN20                                                         
                                                                                
DYNCC    NTR1                                                                   
         MVC   0(L'TPHEX,R7),TPHEX                                              
         AHI   R7,1                                                             
         J     DYNCLN20                                                         
*---------------------------------------------------------------------          
DYNCLEAN DS    0H                                                               
DYNCLN10 CLI   0(R7),C' '          FIND FIRST SPACE                             
         BNH   DYNCLN20                                                         
         AHI   R7,1                                                             
         J     DYNCLN10                                                         
                                                                                
DYNCLN20 MVC   AIO,AIO1                                                         
         XIT1  REGS=(R7)                                                        
                                                                                
         LTORG                                                                  
         EJECT                                                                  
                                                                                
       ++INCLUDE TAPOSTD                                                        
         EJECT                                                                  
       ++INCLUDE TAATREFD                                                       
         EJECT                                                                  
*DDSPLWORKD                                                                     
*DDSPOOLD                                                                       
*DDDLCB                                                                         
*TAGENFILE                                                                      
*ACGENBOTH                                                                      
*ACGENPOST                                                                      
*TASYSDSECT                                                                     
*TASYSEQUS                                                                      
*DMWRKRK                                                                        
*TASYSWORKD                                                                     
*TASYSVALD                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDSPLWORKD                                                     
       ++INCLUDE DDSPOOLD                                                       
       ++INCLUDE DDDLCB                                                         
       ++INCLUDE DDMASTD                                                        
       ++INCLUDE TAREPFFD                                                       
       ++INCLUDE DDGENTWA                                                       
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE DDGETRETD                                                      
       ++INCLUDE TAGENFILE                                                      
       ++INCLUDE ACGENBOTH                                                      
       ++INCLUDE ACGENPOST                                                      
       ++INCLUDE TASYSDSECT                                                     
       ++INCLUDE TASYSEQUS                                                      
       ++INCLUDE DMWRKRK                                                        
SYSWORKD DSECT                                                                  
       ++INCLUDE TASYSWORKD                                                     
         EJECT                                                                  
                                                                                
SYSCOMMD DSECT                                                                  
       ++INCLUDE TASYSVALD                                                      
         PRINT ON                                                               
***********************************************************************         
*        DSECT COVERS HIST2 SCREEN LINE                               *         
***********************************************************************         
                                                                                
HISTD    DSECT                                                                  
HDESH    DS    XL8                 DESCRIPTION HEADER                           
HDES     DS    CL20                DESCRIPTION                                  
HAMTH    DS    XL8                 AMOUNT HEADER                                
HAMT     DS    CL11                AMOUNT                                       
HISTLNQ  EQU   *-HISTD                                                          
                                                                                
***********************************************************************         
*        DSECT COVERS POSTING MAPPING ENTRY                           *         
***********************************************************************         
                                                                                
POSTD    DSECT                                                                  
POSTDOWN DS    CL20                                                             
POSTACCT DS    XL15                                                             
POSTCTRA DS    XL15                                                             
POSTLNQ  EQU   *-POSTD                                                          
                                                                                
***********************************************************************         
*        DSECT COVERS CHECK WITHHOLDINGS TABLE                        *         
***********************************************************************         
                                                                                
CWTABD   DSECT                                                                  
CWTCODE  DS    CL3                                                              
         DS    X                                                                
CWTTOTS  DS    0XL24                                                            
CWTTTAX  DS    F                   UNIT TAX                                     
CWTTNCRP DS    F                   UNIT NON-RES CORP TAX                        
CWTTNFGN DS    F                   UNIT NON-RES FOREIGN TAX                     
CWTTSDI  DS    F                   UNIT FICA / SDI                              
CWTTSUI  DS    F                   UNIT SUI                                     
CWTTFLI  DS    F                   UNIT FLI                                     
CWTTOLNQ EQU   *-CWTTOTS                                                        
                                                                                
         ORG   CWTTOTS                                                          
CWCNTAX  DS    F                   CANADIAN FD TAX                              
CWPRVTX  DS    F                   PROVINCIAL TAX                               
CWPP     DS    F                   PROVINCIAL PENSION PLAN                      
CWEI     DS    F                   PROVINCIAL EMPLOYMENT INSURANCE              
CWPIP    DS    F                   PROVINCIAL PARENTAL INS PLAN                 
         DS    F                   SPARE                                        
                                                                                
CWTPOST1 DS    CL(POSTLNQ)                                                      
CWTPOST2 DS    CL(POSTLNQ)                                                      
CWTPOST3 DS    CL(POSTLNQ)                                                      
CWTPOST4 DS    CL(POSTLNQ)                                                      
CWTPOST5 DS    CL(POSTLNQ)                                                      
CWTPOST6 DS    CL(POSTLNQ)                                                      
CWTLNQ   EQU   *-CWTABD                                                         
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'001TAPOST    04/03/15'                                      
         END                                                                    
