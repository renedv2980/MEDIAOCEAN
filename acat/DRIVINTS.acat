*          DATA SET DRIVINTS   AT LEVEL 003 AS OF 05/01/02                      
*CATALP DRIVINIT                                                                
         TITLE 'DRIVINIT - INITIALIZATION FOR DRIVER'                           
DRIVINIT CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 400,**INIT**,R9,R7                                               
         USING INITD,RC                                                         
         L     RA,0(R1)                                                         
         USING GLOBALD,RA                                                       
         SPACE 1                                                                
         BAS   RE,PRELIMS          ODDMENTS                                     
         BAS   RE,USERADDS         RESOLVE ADDRESSES FROM USER                  
         BAS   RE,LINKADDS         RESOLVE OUR LINKED ADDRESSES                 
         BAS   RE,SKELETON         BUILD A SKELETON DRIVE TABLE                 
         BAS   RE,FILLADDS         FILL IN ADDRESSES                            
         BAS   RE,DINTRECS         GET DETAILS OF INTERNAL RECORDS              
         BAS   RE,PRNTADDS         FILL IN PRINT POSITIONS                      
         BAS   RE,CHNKADDS         RESOLVE CHUNK ADDRESSES                      
         BAS   RE,SORTINIT         INITIALIZE THE SORT                          
         BAS   RE,FINALS           ODDMENTS AT THE END                          
         B     XIT                                                              
         SPACE 1                                                                
YES      SR    R1,R1                                                            
         B     *+8                                                              
         SPACE 1                                                                
NO       LA    R1,1                                                             
         LTR   R1,R1                                                            
         SPACE 1                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
*              ROUTINES TO SET UP SOME ODDMENTS                                 
         SPACE 3                                                                
PRELIMS  NTR1                                                                   
         LR    R0,RA                                                            
         A     R0,GLSIZE                                                        
         ST    R0,GLAEND           NOTE END OF OUR GLOBALS                      
         SPACE 1                                                                
         MVI   GLANYERR,C'N'                                                    
         MVI   TRACOPT,C'N'                                                     
         CLI   GLTRACE,C'Y'                                                     
         BNE   *+8                                                              
         MVI   TRACOPT,C'Y'                                                     
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(197),SPACES                                             
         MVC   P,SPACES                                                         
         L     R2,=A(MYPRINT)                                                   
         ST    R2,AMYH1            A(HEADLINES)                                 
         LA    R2,2772(R2)         (14 X 198)                                   
         ST    R2,AMYM1                                                         
         LA    R2,396(R2)          (2 X 198)                                    
         ST    R2,AMYP1                                                         
         LA    R2,3960(R2)         (20 X 198)                                   
         ST    R2,AMYF1                                                         
         L     R2,AMYH1                                                         
         LA    R0,40                                                            
         SPACE 1                                                                
PREL2    MVC   0(198,R2),SPACES    CLEAR MY PRINT AREAS                         
         LA    R2,198(R2)                                                       
         BCT   R0,PREL2                                                         
         LA    R2,LOCALEND                                                      
         SPACE 1                                                                
         MVC   0(8,R2),=C'*DRIVIO*'                                             
         LA    R2,8(R2)                                                         
         ST    R2,GLAIO                                                         
         LA    R2,2000(R2)                                                      
*                                  SET UP A(DRIVE TABLE)                        
         MVC   0(8,R2),=C'*DRIVE**'                                             
         LA    R2,8(R2)                                                         
         ST    R2,GLADTAB                                                       
         SPACE 1                                                                
         LR    R2,RA               SET UP NEED TABLE                            
         A     R2,GLSIZE                                                        
         LA    R1,400                                                           
         ST    R1,NEEDMAX                                                       
         MH    R1,NEEDWIDE                                                      
         SR    R2,R1                                                            
         ST    R2,ANEEDTAB                                                      
         SH    R2,=H'8'                                                         
         MVC   0(8,R2),=C'**NEED**'                                             
         SPACE 1                                                                
         MVC   GLAPROG-8(8),=C'*GLOBAL*'                                        
         MVC   DUB-8(8),=C'*LOCAL**'                                            
         SPACE 1                                                                
         CLI   GLFHEADL,0          SET HEADINGS TO 9 AND 10                     
         BNE   *+8                 IF NOT PROVIDED                              
         MVI   GLFHEADL,9                                                       
         CLI   GLLHEADL,0                                                       
         BNE   *+8                                                              
         MVI   GLLHEADL,10                                                      
         CLI   GLGAP,0             GAP TO 1                                     
         BNE   *+8                                                              
         MVI   GLGAP,1                                                          
         CLI   GLSPACE,0           DETAIL SPACING TO 1                          
         BNE   *+8                                                              
         MVI   GLSPACE,1                                                        
         CLI   GLNHEAD,0           NUMBER OF HEADLINES TO 14                    
         BNE   *+8                                                              
         MVI   GLNHEAD,14                                                       
         CLI   GLNMID,0            NUMBER OF MIDLINES TO 2                      
         BNE   *+8                                                              
         MVI   GLNMID,2                                                         
         SPACE 1                                                                
         MVI   GLAROUT,2           GIVE FIRST HOOK TO SYSDRIVER                 
         MVI   GLHOOK,GLINIT                                                    
         BAS   RE,GOHOOK                                                        
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINES TO FILL IN ADDRESSES FROM USER                          
         SPACE 3                                                                
USERADDS NTR1                                                                   
         L     R5,GLAWORKD         PICK UP A(USERS WORKD)                       
         LTR   R5,R5                                                            
         BZ    FATAL1                                                           
         L     R2,=V(DRIVADDS)                                                  
         SPACE 1                                                                
US2      CLI   0(R2),X'FF'                                                      
         BE    FATAL2                                                           
         CLC   GLTWORKD,0(R2)      LOOK FOR A MATCH ON TYPE                     
         BE    US4                                                              
         LA    R2,4(R2)                                                         
         B     US2                                                              
         SPACE 1                                                                
US4      L     R2,0(R2)            USER ADDRESSES INTO DRIVER DSECTS            
         USING UADDS,R2                                                         
         LA    R3,UAHHOOK                                                       
         LA    R4,GLAHEDHK                                                      
         BAS   RE,RES                                                           
         LA    R3,UAHEAD1                                                       
         LA    R4,GLAH1                                                         
         BAS   RE,RES                                                           
         LA    R3,UAMID1                                                        
         LA    R4,GLAM1                                                         
         BAS   RE,RES                                                           
         LA    R3,UAP                                                           
         LA    R4,GLAP1                                                         
         BAS   RE,RES                                                           
         LA    R3,UAFOOT1                                                       
         LA    R4,GLAF1                                                         
         BAS   RE,RES                                                           
         LA    R3,UALINE                                                        
         LA    R4,GLALINE                                                       
         BAS   RE,RES                                                           
         LA    R3,UAMAXL                                                        
         LA    R4,GLALMAX                                                       
         BAS   RE,RES                                                           
         LA    R3,UASPACE                                                       
         LA    R4,GLASPACE                                                      
         BAS   RE,RES                                                           
         LA    R3,UAFHED                                                        
         LA    R4,GLAFHEAD                                                      
         BAS   RE,RES                                                           
         LA    R3,UAFMID                                                        
         LA    R4,GLAFMID                                                       
         BAS   RE,RES                                                           
         LA    R3,UAFFUT                                                        
         LA    R4,GLAFFOOT                                                      
         BAS   RE,RES                                                           
         LA    R3,UAPAGE                                                        
         LA    R4,GLAPAGE                                                       
         BAS   RE,RES                                                           
         LA    R3,UASPAGE                                                       
         LA    R4,GLASPAGE                                                      
         BAS   RE,RES                                                           
         LA    R3,UASUBPRG                                                      
         LA    R4,GLASPROG                                                      
         BAS   RE,RES                                                           
         LA    R3,UASPECS                                                       
         LA    R4,GLASPECS                                                      
         BAS   RE,RES                                                           
         LA    R3,UARQCARD                                                      
         LA    R4,GLAREQ                                                        
         BAS   RE,RES                                                           
         LA    R3,UAAGENCY                                                      
         LA    R4,GLAAGNCY                                                      
         BAS   RE,RES                                                           
         L     R1,GLAAGNCY                                                      
         LTR   R1,R1                                                            
         BZ    *+10                                                             
         MVC   GLAGENCY,0(R1)      FILL AGENCY CODE IF ADDRESS OK               
         LA    R3,UASYSP                                                        
         LA    R4,GLASPROF                                                      
         BAS   RE,RES                                                           
         LA    R3,UAPROGP                                                       
         LA    R4,GLAPPROF                                                      
         BAS   RE,RES                                                           
         EJECT                                                                  
*              NOW PICK OUT ADDRESSES OF ADDRESSES                              
         SPACE 3                                                                
         LA    R3,UAADDAY                                                       
         LA    R4,ADDAY                                                         
         BAS   RE,ARES                                                          
         LA    R3,UAABOX                                                        
         LA    R4,ABOX                                                          
         BAS   RE,ARES                                                          
         LA    R3,UABUFF                                                        
         LA    R4,BUFFALO                                                       
         BAS   RE,ARES                                                          
         LA    R3,UACOMFAC                                                      
         LA    R4,GLCOMFAC                                                      
         BAS   RE,ARES                                                          
         LA    R3,UADM                                                          
         LA    R4,DATAMGR                                                       
         BAS   RE,ARES                                                          
         LA    R3,UADATCON                                                      
         LA    R4,DATCON                                                        
         BAS   RE,ARES                                                          
         LA    R3,UAGETDAY                                                      
         LA    R4,GETDAY                                                        
         BAS   RE,ARES                                                          
         LA    R3,UAHEXOUT                                                      
         LA    R4,HEXOUT                                                        
         BAS   RE,ARES                                                          
         LA    R3,UAPRINT                                                       
         LA    R4,PRINT                                                         
         BAS   RE,ARES                                                          
         LA    R3,UAREPORT                                                      
         LA    R4,REPORT                                                        
         BAS   RE,ARES                                                          
         LA    R3,UAUNDAY                                                       
         LA    R4,UNDAY                                                         
         BAS   RE,ARES                                                          
         LA    R3,UAUNTIME                                                      
         LA    R4,UNTIME                                                        
         BAS   RE,ARES                                                          
         LA    R3,UAUTL                                                         
         LA    R4,UTL                                                           
         BAS   RE,ARES                                                          
         LA    R3,UAWORKER                                                      
         LA    R4,WORKER                                                        
         BAS   RE,ARES                                                          
         LA    R3,UAXSORT                                                       
         LA    R4,XSORT                                                         
         BAS   RE,ARES                                                          
         EJECT                                                                  
*              DEAL WITH WIDE PRINTING                                          
         SPACE 3                                                                
         L     R1,ABOX                                                          
         USING BOXD,R1                                                          
         MVC   WIDTH,=F'132'                                                    
         CLC   BOXWIDTH,=F'132'    IF WIDTH GREATER THAN 132                    
         BNH   XIT                                                              
         MVC   WIDTH,=F'198'       THEN SWITCH TO 198 MODE                      
         SPACE 1                                                                
         L     R1,BOXAWIDE         RELOCATE PRINT LINE ADDRESSES                
         USING WIDED,R1                                                         
         LA    RE,XHEAD1                                                        
         ST    RE,GLAH1                                                         
         LA    RE,XMID1                                                         
         ST    RE,GLAM1                                                         
         LA    RE,XP1                                                           
         ST    RE,GLAP1                                                         
         B     XIT                                                              
         EJECT                                                                  
*              FACILITIES TO HELP RESOLVE USER ADDRESSES                        
         SPACE 3                                                                
RES      NTR1                                                                   
         MVI   RESTYPE,C'M'                                                     
         B     RES2                                                             
         SPACE 1                                                                
ARES     NTR1                                                                   
         MVI   RESTYPE,C'A'                                                     
         SPACE 1                                                                
RES2     L     R6,0(R3)                                                         
         SLL   R6,8                                                             
         SRL   R6,8                                                             
         CLI   0(R3),1             CHECK FOR ANY SPECIAL VALUES                 
         BL    RESNORM                                                          
         BE    RESODD1                                                          
         CLI   0(R3),3                                                          
         BL    RESODD2                                                          
         BE    RESODD3                                                          
         CLI   0(R3),5                                                          
         BL    RESODD4                                                          
         BE    RESODD5                                                          
         CLI   0(R3),7                                                          
         BL    RESODD6                                                          
         BE    RESODD7                                                          
         DC    H'0'                                                             
         SPACE 1                                                                
RESODD1  L     R5,X'FD4'(R5)       A(EXTRAD)                                    
         L     R5,X'1B0'(R5)       A(MASTD)                                     
         L     R5,X'254'(R5)       A(BOX)                                       
         ST    R5,0(R4)                                                         
         B     XIT                                                              
         SPACE 1                                                                
RESODD2  L     R3,X'230'(R5)       A(SPOOLD)                                    
         AR    R3,R6                                                            
         B     RESALL                                                           
         SPACE 1                                                                
RESODD3  L     R3,X'200'(R5)       A(TWA)                                       
         AR    R3,R6                                                            
         B     RESALL                                                           
         SPACE 1                                                                
RESODD4  L     R3,X'E0C'(R5)       A(2ND DSECT FOR PPG)                         
         AR    R3,R6                                                            
         B     RESALL                                                           
         SPACE 1                                                                
RESODD5  L     R5,X'FD4'(R5)       A(EXTRAD)                                    
         L     R5,X'154'(R5)       A(UTL)                                       
         ST    R5,0(R4)                                                         
         B     XIT                                                              
         SPACE 1                                                                
RESODD6  L     R3,X'E48'(R5)       A(REP EXTENSION)                             
         AR    R3,R6                                                            
         B     RESALL                                                           
         SPACE 1                                                                
RESODD7  L     R5,X'200'(R5)       A(TWA)                                       
         L     R5,X'03C'(R5)       A(MASTC)                                     
         L     R5,X'288'(R5)       A(UTL)                                       
         ST    R5,0(R4)                                                         
         B     XIT                                                              
         SPACE 1                                                                
RESNORM  L     R3,0(R3)            NORMALLY RELATIVE TO WORKD                   
         AR    R3,R5                                                            
         SPACE 1                                                                
RESALL   ST    R3,0(R4)            RETURN THIS ADDRESS                          
         CLI   RESTYPE,C'A'                                                     
         BNE   XIT                                                              
         MVC   0(4,R4),0(R3)       OR OPTIONALLY THE CONTENTS                   
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINES TO FILL IN OUR LINKED ADDRESSES                         
         SPACE 3                                                                
LINKADDS NTR1                                                                   
         LA    R1,GLOPTS           INTERNAL FILTER ADDRESSES                    
         ST    R1,GLAOPTS                                                       
         LA    R1,GLDATENO                                                      
         ST    R1,GLADTNO                                                       
         LA    R1,GLRECNO                                                       
         ST    R1,GLARECNO                                                      
         LA    R1,GLLEVEL                                                       
         ST    R1,GLALEVEL                                                      
         SPACE 1                                                                
         MVC   CENTER,=V(CENTER)   PICK UP LINKED VTYPES                        
         MVC   CHOPPER,=V(CHOPPER)                                              
         MVC   DEMOCON,=V(DEMOCON)                                              
         MVC   EDITOR,=V(EDITOR)                                                
         MVC   GLAEDITR,EDITOR                                                  
         MVC   HEXIN,=V(HEXIN)                                                  
         MVC   LOADER,=V(LOADER)                                                
         MVC   RIGHT,=V(RIGHT)                                                  
         MVC   SQUASHER,=V(SQUASHER)                                            
         MVC   SORTER,=V(SORTER)                                                
         MVC   UNDERLIN,=V(UNDERLIN)                                            
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINES TO BUILD A SKELETON DRIVE TABLE                         
         SPACE 3                                                                
SKELETON NTR1                                                                   
         L     R2,GLAPROG          R2 = A(MAIN ELEMENTS)                        
         L     R3,GLADTAB          R3 = A(DRIVE TABLE ENTRY)                    
         XC    SETAREA,SETAREA                                                  
         MVI   FILATOT,C'N'        INIT FIRST/LAST/TOTAL INDICATOR              
         SPACE 1                                                                
SKEL2    CLI   0(R2),X'00'         CHECK END OF PROGRAM                         
         BE    XIT                                                              
         CLI   0(R2),X'FF'                                                      
         BE    XIT                                                              
         LA    R1,MAINLIST                                                      
         SPACE 1                                                                
SKEL4    CLC   0(1,R1),0(R2)       LOOK UP ELEMENT CODE                         
         BE    SKEL6                                                            
         CLI   0(R1),X'FF'                                                      
         BE    SKEL8                                                            
         LA    R1,4(R1)                                                         
         B     SKEL4                                                            
         SPACE 1                                                                
SKEL6    ST    R2,AMAINEL          MAIN ELEMENT FOUND - NOTE ADDRESS            
         ST    R3,ADRIVEL                                                       
         XC    0(255,R3),0(R3)     PRECLEAR ELEMENT                             
         L     RF,0(R1)            PICK UP ADDRESS OF ROUTINE                   
         BASR  RE,RF               GO AND FILL IN DRIVE ETC                     
         CLI   1(R3),0             ANY LENGTH SET?                              
         BE    SKEL8                                                            
         MVC   0(1,R3),0(R2)       YES - SO COPY ELEMENT CODE                   
         SPACE 1                                                                
SKEL7    ZIC   R1,1(R3)                                                         
         AR    R3,R1                                                            
         ST    R3,ENDDRIVE         NOTE END OF DRIVE TABLE                      
         CLI   0(R3),0             MAY HAVE ADDED OTHER ELEMENTS                
         BNE   SKEL7                                                            
         SPACE 1                                                                
SKEL8    ZIC   R1,1(R2)                                                         
         LTR   R1,R1                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R2,R1                                                            
         B     SKEL2                                                            
         EJECT                                                                  
*              MAIN ELEMENTS - REC AND DATA                                     
         SPACE 3                                                                
REC      NTR1                                                                   
         USING DERECD,R2           RECORD START                                 
         USING DRRECD,R3                                                        
         MVI   RECYN,C'N'                                                       
         LA    R1,DRRECIFS                                                      
         BAS   RE,GETIFS                                                        
         BNE   XIT                 CAN BE REJECTED                              
         MVI   RECYN,C'Y'                                                       
         MVC   INDISP,LENCON                                                    
         MVI   DRRECLEN,24                                                      
         AI    GLRECNO,1                                                        
         MVC   DRRECNUM,GLRECNO                                                 
         MVI   GLLEVEL,0                                                        
         ST    R3,ALASTREC                                                      
         XC    ALASTIN,ALASTIN                                                  
         XC    ALASTOUT,ALASTOUT                                                
         XC    ADICTENT,ADICTENT                                                
         MVI   KEYORDAT,C'K'       SET IN KEY                                   
         B     XIT                                                              
         SPACE 1                                                                
DATA     NTR1                                                                   
         USING DEDATD,R2           DATA START                                   
         USING DRDATD,R3                                                        
         CLI   RECYN,C'N'                                                       
         BE    XIT                                                              
         MVI   DRDATLEN,2                                                       
         MVI   KEYORDAT,C'D'       SET IN DATA                                  
         B     XIT                                                              
         EJECT                                                                  
*              MAIN ELEMENTS - SET (AND UNSET)                                  
         SPACE 3                                                                
SET      NTR1                                                                   
         USING DESETD,R2           SET OR UNSET                                 
         MVI   ELCODE,0                                                         
         BAS   RE,GETSUB                                                        
         BE    SET2                                                             
         MVI   ERRNUM,2            MUST HAVE A SUB ELEMENT                      
         BAS   RE,ERROR                                                         
         B     XIT                                                              
         SPACE 1                                                                
SET2     LA    R1,SETAREA                                                       
         L     R4,ASUBEL                                                        
         SPACE 1                                                                
SET4     CLI   0(R1),0             FIND A SPOT IN SETAREA                       
         BE    SET6                                                             
         CLC   0(1,R1),0(R4)       OR A PREVIOUS VERSION                        
         BE    SET6                                                             
         ZIC   R0,1(R1)                                                         
         AR    R1,R0                                                            
         B     SET4                                                             
         SPACE 1                                                                
SET6     CLI   DESETSW,0           SET                                          
         BE    SET8                                                             
         ZIC   RE,1(R4)            MOVE IN NEW ELEMENT                          
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R1),0(R4)                                                    
         B     XIT                                                              
         SPACE 1                                                                
SET8     CLI   0(R1),0             UNSET                                        
         BNE   SET10                                                            
         MVI   ERRNUM,3            MUST BE PRECEDED BY SET                      
         BAS   RE,ERROR                                                         
         B     XIT                                                              
         SPACE 1                                                                
SET10    ZIC   RE,1(R4)            MOVE IN NEW ELEMENT                          
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R1),0(R4)                                                    
         B     XIT                                                              
         EJECT                                                                  
*              MAIN ELEMENTS - IN                                               
         SPACE 3                                                                
IN       NTR1                                                                   
         USING DEISD,R2            INPUT STATEMENT                              
         USING DRIND,R3                                                         
         MVI   INOROUT,C'Y'                                                     
         MVI   INYN,C'N'                                                        
         CLI   RECYN,C'N'          DONT NEED IN IF REC IS OFF                   
         BE    XIT                                                              
         LA    R1,DRINIFS          CHECK FOR CONDITIONS                         
         BAS   RE,GETIFS                                                        
         BNE   XIT                                                              
         MVI   INYN,C'Y'                                                        
         ST    R3,ALASTIN                                                       
         XC    ALASTOUT,ALASTOUT                                                
         ST    R3,NEEDADD                                                       
         MVC   NEEDLABL,DEISLAB                                                 
         MVC   NEEDREC,GLRECNO                                                  
         CLI   DEISLEN,2                                                        
         BE    *+8                                                              
         BAS   RE,PUTLABEL                                                      
         AI    GLLEVEL,1           UPDATE LEVEL NUMBER                          
         MVC   DRINLEV,GLLEVEL     AND SAVE                                     
         XC    ADICTENT,ADICTENT                                                
         MVC   DICNAME,SPACES                                                   
         MVC   ENTNAME,SPACES                                                   
         BAS   RE,DICENT           CHECK FOR ANY ENTRY                          
         MVC   DRINDICN,DICNAME                                                 
         MVC   DRINENTN,ENTNAME                                                 
         MVI   ELCODE,X'21'        CHECK FOR OUTPUT LABEL                       
         BAS   RE,GETSUB                                                        
         BNE   IN2                                                              
         L     R4,ASUBEL                                                        
         USING DEOSLD,R4                                                        
         MVC   DRINOLAB,DEOSLABL                                                
         MVC   NEEDLABL,DEOSLABL                                                
         MVC   NEEDREC,GLRECNO                                                  
         LA    R1,DRINOADD                                                      
         ST    R1,NEEDADD                                                       
         BAS   RE,PUTNEED                                                       
         SPACE 1                                                                
IN2      MVI   ELCODE,X'22'        PICK UP INPUT TYPE                           
         BAS   RE,GETBEST                                                       
         BE    IN4                                                              
         MVI   ERRNUM,4                                                         
         BAS   RE,ERROR                                                         
         B     IN6                                                              
         SPACE 1                                                                
IN4      L     R4,ASUBEL                                                        
         USING DEITD,R4                                                         
         MVC   DRINTYPE,DEITYPE                                                 
         CLI   KEYORDAT,C'K'       IF WE ARE IN THE KEY                         
         BNE   IN5                                                              
         CLI   DRINTYPE+1,C'+'     PROTECT AGAINST ADDITIVE FIELDS              
         BNE   *+8                                                              
         MVI   DRINTYPE+1,C'-'                                                  
         SPACE 1                                                                
IN5      MVI   DRINREP,1           REPITITION IS IN EXTENDED DATA               
         CLI   DEITLEN,4                                                        
         BE    IN6                                                              
         MVC   DRINREP,DEITREP                                                  
         SPACE 1                                                                
IN6      MVI   ELCODE,X'23'        AND LENGTH                                   
         BAS   RE,GETBEST                                                       
         BE    IN8                                                              
         MVI   ERRNUM,5                                                         
         BAS   RE,ERROR                                                         
         B     IN10                                                             
         SPACE 1                                                                
IN8      L     R4,ASUBEL                                                        
         USING DEILD,R4                                                         
         MVC   DRINFLEN,DEILEN                                                  
         ZIC   R1,DRINFLEN         FIELD LENGTH                                 
         ZIC   R0,DRINREP                                                       
         MR    R0,R0               TIMES REPITITION                             
         STC   R1,DRINLEN          =TOTAL INPUT LENGTH                          
         MVC   DRINDISP,INDISP                                                  
         ZIC   R1,DRINLEN                                                       
         AH    R1,INDISP           SET DISPLACEMENT FOR NEXT IN                 
         STH   R1,INDISP                                                        
         SPACE 1                                                                
IN10     MVI   ELCODE,X'24'        ANY INPUT ROUTINE?                           
         MVI   INPUTRTN,C'N'                                                    
         BAS   RE,GETBEST                                                       
         BNE   IN12                                                             
         MVI   INPUTRTN,C'Y'                                                    
         L     R4,ASUBEL                                                        
         USING DEIRD,R4                                                         
         MVC   DRINROUT,DEIROUT                                                 
         MVC   GLLABEL,DEIROUT                                                  
         XC    GLARGS,GLARGS                                                    
         SPACE 1                                                                
IN12     MVI   ELCODE,X'25'        ANY INPUT ARGUMENTS?                         
         BAS   RE,GETBEST                                                       
         BNE   IN13                                                             
         L     R4,ASUBEL                                                        
         USING DEIAD,R4                                                         
         ZIC   R1,DEIALEN                                                       
         SH    R1,=H'3'                                                         
         EX    R1,IN12EX1                                                       
         EX    R1,IN12EX2          SET GLARGS FOR RESOLVE HOOK                  
         B     IN13                                                             
IN12EX1  MVC   DRINARGS(0),DEIARGS                                              
IN12EX2  MVC   GLARGS(0),DEIARGS                                                
         SPACE 1                                                                
IN13     CLI   INPUTRTN,C'Y'                                                    
         BNE   *+14                                                             
         BAS   RE,RESOLVE                                                       
         MVC   DRINRADD,GLAROUT                                                 
         XC    GLARGS,GLARGS                                                    
         SPACE 1                                                                
IN14     MVI   ELCODE,X'26'        LOOK FOR OPTIONS                             
         BAS   RE,GETBEST                                                       
         B     IN18                                                             
         SPACE 1                                                                
IN16     BAS   RE,NEXTSUB                                                       
         SPACE 1                                                                
IN18     BNE   IN20                                                             
         L     R4,ASUBEL                                                        
         USING DEIOD,R4                                                         
         CLI   DEIONUM,1           DECIMAL PLACES                               
         BNE   *+10                                                             
         MVC   DRINDEC,DEIOVAL                                                  
         CLI   DEIONUM,2           COMPLEMENT FIELD                             
         BNE   *+8                                                              
         OI    DRINOPT,X'80'                                                    
         CLI   DEIONUM,3           SCALE                                        
         BNE   *+10                                                             
         MVC   DRINSCAL,DEIOVAL                                                 
         B     IN16                                                             
         SPACE 1                                                                
IN20     MVI   ELCODE,X'87'        ANY LITERALS                                 
         SR    R1,R1                                                            
         BAS   RE,GETSUB                                                        
         BNE   INEND                                                            
         L     R4,ASUBEL                                                        
         USING DELITD,R4                                                        
         ZIC   R1,DELITLEN                                                      
         SH    R1,=H'4'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   DRINLIT(0),DELITRAL                                              
         LA    R1,1(R1)                                                         
         SPACE 1                                                                
INEND    LA    R1,96(R1)                                                        
         STC   R1,DRINELEN                                                      
         B     XIT                                                              
         EJECT                                                                  
*              MAIN ELEMENTS - OUT                                              
         SPACE 3                                                                
OUT      NTR1                                                                   
         USING DEOSD,R2            OUTPUT STATEMENT                             
         USING DROD,R3                                                          
         MVI   INOROUT,C'O'                                                     
         MVI   OUTYN,C'N'                                                       
         CLI   RECYN,C'N'                                                       
         BE    XIT                                                              
         CLI   INYN,C'N'           REJECT OUT IF PRECEDING IN FAILED            
         MVI   INYN,C'Y'                                                        
         BE    XIT                                                              
         LA    R1,DROIFS           CHECK FOR OUT CONDITIONS                     
         BAS   RE,GETIFS                                                        
         BNE   XIT                                                              
         ST    R3,ALASTOUT                                                      
         ST    R3,NEEDADD                                                       
         MVC   NEEDLABL,DEOSLAB                                                 
         MVC   NEEDREC,GLRECNO                                                  
         CLI   DEOSLEN,2                                                        
         BE    *+8                                                              
         BAS   RE,PUTLABEL                                                      
         MVC   DROIADD,ALASTIN     DEFAULT IS PREVIOUS IN                       
         MVI   ELCODE,X'31'        CHECK FOR INPUT LABEL                        
         BAS   RE,GETSUB                                                        
         BNE   OUT2                                                             
         L     R4,ASUBEL                                                        
         USING DEISLD,R4                                                        
         MVC   DROILAB,DEISLABL                                                 
         MVC   NEEDLABL,DEISLABL                                                
         MVC   NEEDREC,GLRECNO                                                  
         BAS   RE,GETADD                                                        
         MVC   DROIADD,NEEDADD                                                  
         BNE   XIT                                                              
         L     R1,DROIADD          PICK UP ENTRY NAME FROM IN                   
         USING DRIND,R1                                                         
         MVC   ENTNAME,DRINENTN                                                 
         DROP  R1                                                               
         SPACE 1                                                                
OUT2     BAS   RE,DICENT           CHECK FOR ANY ENTRY                          
         MVC   DRODICN,DICNAME                                                  
         MVC   DROENTN,ENTNAME                                                  
         MVC   ENTNAME,SPACES      (DON'T USE FOR NEXT OUT)                     
         MVI   OUTYN,C'Y'                                                       
         L     R1,DROIADD                                                       
         LTR   R1,R1                                                            
         BZ    OUT4                                                             
         USING DRIND,R1                                                         
         MVC   DROLEV,DRINLEV                                                   
         ST    R3,DRINOADD                                                      
         DROP  R1                                                               
         SPACE 1                                                                
OUT4     XC    ALASTIN,ALASTIN                                                  
         MVI   ELCODE,X'32'        PICK UP OUTPUT TYPE                          
         BAS   RE,GETBEST                                                       
         BNE   OUT6                                                             
         L     R4,ASUBEL                                                        
         USING DEOTD,R4                                                         
         MVC   DROTYPE,DEOTYPE                                                  
         SPACE 1                                                                
OUT6     MVI   ELCODE,X'33'        AND LENGTH                                   
         BAS   RE,GETBEST                                                       
         BNE   OUT10                                                            
         L     R4,ASUBEL                                                        
         USING DEOLD,R4                                                         
         MVC   DROLEN,DEOLEN                                                    
         SPACE 1                                                                
OUT10    MVI   ELCODE,X'34'        ANY OUTPUT ROUTINE?                          
         BAS   RE,GETBEST                                                       
         BNE   OUT12                                                            
         L     R4,ASUBEL                                                        
         USING DEORD,R4                                                         
         MVC   DROROUT,DEOROUT                                                  
         MVC   GLLABEL,DEOROUT                                                  
         BAS   RE,RESOLVE                                                       
         MVC   DRORADD,GLAROUT                                                  
         SPACE 1                                                                
OUT12    MVI   ELCODE,X'35'        ANY OUTPUT ARGUMENTS?                        
         BAS   RE,GETBEST                                                       
         BNE   OUT14                                                            
         L     R4,ASUBEL                                                        
         USING DEOAD,R4                                                         
         ZIC   R1,DEOALEN                                                       
         SH    R1,=H'3'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   DROARGS(0),DEOARGS                                               
         SPACE 1                                                                
OUT14    MVI   ELCODE,X'36'        LOOK FOR OPTIONS                             
         BAS   RE,GETBEST                                                       
         B     OUT18                                                            
         SPACE 1                                                                
OUT16    BAS   RE,NEXTSUB                                                       
         SPACE 1                                                                
OUT18    BNE   OUT30                                                            
         L     R4,ASUBEL                                                        
         USING DEOOD,R4                                                         
         CLI   DEOONUM,1           DECIMAL PLACES                               
         BNE   *+10                                                             
         MVC   DRODEC,DEOOVAL                                                   
         CLI   DEOONUM,2           COMMAS=YES                                   
         BNE   *+8                                                              
         OI    DROEDIT,X'80'                                                    
         CLI   DEOONUM,3           MINUS=YES                                    
         BNE   *+8                                                              
         OI    DROEDIT,X'40'                                                    
         CLI   DEOONUM,4           ZERO=NOBLANK                                 
         BNE   *+8                                                              
         OI    DROEDIT,X'20'                                                    
         CLI   DEOONUM,5           FILL=X                                       
         BNE   *+10                                                             
         MVC   DROFILL,DEOOVAL                                                  
         CLI   DEOONUM,6           FLOAT=X                                      
         BNE   *+10                                                             
         MVC   DROFLOAT,DEOOVAL                                                 
         CLI   DEOONUM,7           BRACKET(=M)                                  
         BNE   OUT20                                                            
         OI    DROEDIT,X'10'                                                    
         CLI   DEOOVAL,C'M'                                                     
         BNE   OUT20                                                            
         OI    DROEDIT,X'08'                                                    
         SPACE 1                                                                
OUT20    CLI   DEOONUM,8           DIV=                                         
         BNE   *+10                                                             
         MVC   DRODIV,DEOOVAL                                                   
         CLI   DEOONUM,9           TRIM                                         
         BNE   *+8                                                              
         OI    DROFORM,X'80'                                                    
         CLI   DEOONUM,10          BIG=BELOW                                    
         BNE   *+8                                                              
         OI    DROFORM,X'40'                                                    
         CLI   DEOONUM,X'20'       ALIGN=                                       
         BNE   *+10                                                             
         MVC   DROALIGN,DEOOVAL                                                 
         CLI   DEOONUM,X'21'       CENTER                                       
         BNE   *+8                                                              
         MVI   DROALIGN,C'C'                                                    
         CLI   DEOONUM,X'22'       UNDER=X                                      
         BNE   OUT20B                                                           
         MVC   DROUNDER,DEOOVAL                                                 
         CLI   DROUNDER,0                                                       
         BNE   OUT20B                                                           
         MVI   DROUNDER,X'BF'      DEFAULT IS HORIZONTAL                        
         SPACE 1                                                                
OUT20B   CLI   DEOONUM,X'23'       NOBOX                                        
         BNE   *+8                                                              
         OI    DROFORM,X'20'                                                    
         CLI   DEOONUM,X'24'       CHOP                                         
         BNE   *+8                                                              
         OI    DROFORM,X'10'                                                    
         CLI   DEOONUM,X'25'       MARK                                         
         BNE   *+8                                                              
         OI    DROFORM,X'04'                                                    
         CLI   DEOONUM,X'26'       FOLD                                         
         BNE   *+8                                                              
         OI    DROFORM,X'02'                                                    
         CLI   DEOONUM,X'27'       SCALE                                        
         BNE   *+10                                                             
         MVC   DROSCALE,DEOOVAL                                                 
         CLI   DEOONUM,X'28'       TRAIL                                        
         BNE   *+10                                                             
         MVC   DROTRAIL,DEOOVAL                                                 
         B     OUT16                                                            
         SPACE 1                                                                
OUT30    MVI   DROLTYP,C'P'        PRESET OUTPUT                                
         MVI   DROLINE,0           TO P*                                        
         MVI   DROCOL,0                                                         
         MVI   ELCODE,X'86'        LOOK FOR OUTPUT POSITION                     
         BAS   RE,GETSUB                                                        
         BNE   OUT32                                                            
         L     R4,ASUBEL                                                        
         USING DEOPPD,R4                                                        
         MVC   DROPOS,DEOPPLT                                                   
         SPACE 1                                                                
OUT32    CLI   DROLINE,1           SET RELATIVE LINE FOR HEADINGS               
         BH    OUT34                                                            
         MVI   RELHEAD,1                                                        
         B     OUTLIT                                                           
         SPACE 1                                                                
OUT34    CLI   DROLINE,C'+'                                                     
         BE    OUT36                                                            
         MVC   RELHEAD,DROLINE                                                  
         B     OUTLIT                                                           
         SPACE 1                                                                
OUT36    AI    RELHEAD,1                                                        
         SPACE 1                                                                
OUTLIT   MVI   ELCODE,X'87'        ANY LITERALS                                 
         SR    R1,R1                                                            
         BAS   RE,GETSUB                                                        
         BNE   OUTEND                                                           
         L     R4,ASUBEL                                                        
         USING DELITD,R4                                                        
         ZIC   R1,DELITLEN                                                      
         SH    R1,=H'3'                                                         
         CLI   DROLEN,0            IF LENGTH HAS NOT BEEN SPECIFIED             
         BNE   *+8                                                              
         STC   R1,DROLEN           USE LITERAL LENGTH                           
         CLI   DROTYPE,0           IF TYPE HAS NOT BEEN SPECIFIED               
         BNE   *+8                                                              
         MVI   DROTYPE,C'C'        USE TYPE C                                   
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   DROLIT(0),DELITRAL                                               
         LA    R1,1(R1)                                                         
         SPACE 1                                                                
OUTEND   LA    R1,120(R1)                                                       
         STC   R1,DROELEN                                                       
         BAS   RE,DICHEAD          CHECK FOR HEADINGS IN ENTRY                  
         CLI   DROTYPE,0           TYPE SHOULD NOW BE ESTABLISHED               
         BNE   OUTEND2                                                          
         MVI   ERRNUM,4                                                         
         BAS   RE,ERROR                                                         
         SPACE 1                                                                
OUTEND2  CLI   DROLEN,0            AND SO SHOULD LENGTH                         
         BNE   XIT                                                              
         MVI   ERRNUM,5                                                         
         BAS   RE,ERROR                                                         
         SPACE 1                                                                
         B     XIT                                                              
         EJECT                                                                  
*              IF HEADINGS ARE IN THE DICTIONARY ENTRY                          
*              PRESET HEAD ELEMENTS IN DRIVE TABLE                              
         SPACE 3                                                                
DICHEAD  NTR1                                                                   
         L     R2,ADICTENT                                                      
         LTR   R2,R2                                                            
         BZ    XIT                 NONE FOUND                                   
         CLI   DROLTYP,C'P'        ONLY INTERESTED IF OUT IS IN P               
         BNE   XIT                                                              
         LR    R4,R3                                                            
         ZIC   R1,DROELEN                                                       
         AR    R4,R1                                                            
         USING DRHDD,R4                                                         
         USING DELITD,R2                                                        
         SPACE 1                                                                
DICH2    CLI   0(R2),0                                                          
         BE    XIT                                                              
         CLI   0(R2),X'87'                                                      
         BNE   DICH4                                                            
         AI    DROHDFOL,1                                                       
         MVI   DRHDEL,X'40'                                                     
         MVC   DRHDWDTH,DROLEN     PICK UP WIDTH FROM OUT                       
         ZIC   R1,DELITLIN                                                      
         ZIC   R0,RELHEAD                                                       
         AR    R1,R0               DISPLACED HEADING                            
         BCTR  R1,0                                                             
         STC   R1,DRHDLINE                                                      
         ZIC   R1,DELITLEN                                                      
         SH    R1,=H'4'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   DRHDLIT(0),DELITRAL                                              
         LA    R1,56+1(R1)                                                      
         STC   R1,DRHDELEN                                                      
         AR    R4,R1                                                            
         SPACE 1                                                                
DICH4    ZIC   R1,1(R2)                                                         
         AR    R2,R1                                                            
         B     DICH2                                                            
         DROP  R4                                                               
         DROP  R2                                                               
         EJECT                                                                  
*              MAIN ELEMENTS - HEAD                                             
         SPACE 3                                                                
HEAD     NTR1                                                                   
         USING DEHDD,R2            HEAD STATEMENT                               
         USING DRHDD,R3                                                         
         CLI   OUTYN,C'N'                                                       
         BE    XIT                 IGNORE IF PREVIOUS OUT FAILED                
         LA    R1,DRHDIFS          CHECK FOR CONDITIONS                         
         BAS   RE,GETIFS                                                        
         BNE   XIT                                                              
         SPACE 1                                                                
         MVI   ELCODE,X'82'        ANY HEAD ROUTINE?                            
         BAS   RE,GETSUB                                                        
         BNE   HEAD2                                                            
         L     R4,ASUBEL                                                        
         USING DESRD,R4                                                         
         MVC   DRHDROUT,DESROUT                                                 
         MVC   GLLABEL,DESROUT                                                  
         BAS   RE,RESOLVE                                                       
         MVC   DRHDRADD,GLAROUT                                                 
         SPACE 1                                                                
HEAD2    MVI   ELCODE,X'83'        ANY HEAD ARGUMENTS?                          
         BAS   RE,GETSUB                                                        
         BNE   HEAD4                                                            
         L     R4,ASUBEL                                                        
         USING DESAD,R4                                                         
         ZIC   R1,DESALEN                                                       
         SH    R1,=H'3'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   DRHDARGS(0),DESARGS                                              
         SPACE 1                                                                
HEAD4    MVC   DRHDLINE,DEHDLIN                                                 
         MVC   THISHLIN,DEHDLIN                                                 
         MVI   ELCODE,X'36'        LOOK FOR OPTIONS                             
         BAS   RE,GETBEST                                                       
         SPACE 1                                                                
HEAD5    BNE   HEAD6                                                            
         L     R4,ASUBEL                                                        
         USING DEOOD,R4                                                         
         CLI   DEOONUM,X'20'       ALIGNMENT OPTION                             
         BNE   *+10                                                             
         MVC   DRHDALIN,DEOOVAL                                                 
         BAS   RE,NEXTSUB                                                       
         B     HEAD5                                                            
         SPACE 1                                                                
HEAD6    MVI   ELCODE,X'87'        ANY LITERALS                                 
         SR    R1,R1                                                            
         BAS   RE,GETSUB                                                        
         BNE   HEAD8                                                            
         L     R4,ASUBEL                                                        
         USING DELITD,R4                                                        
         ZIC   R1,DELITLEN                                                      
         SH    R1,=H'4'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   DRHDLIT(0),DELITRAL                                              
         LA    R1,1(R1)                                                         
         SPACE 1                                                                
HEAD8    LA    R1,56(R1)                                                        
         STC   R1,DRHDELEN                                                      
         L     R4,ALASTOUT         FIND WHAT THIS RELATES TO                    
         LTR   R4,R4                                                            
         BZ    HEADEND                                                          
         USING DROD,R4                                                          
         AI    DROHDFOL,1          UPDATE CONNECTED OUT ENTRY                   
         MVC   DRHDWDTH,DROLEN     PICK UP WIDTH FROM OUT                       
         SPACE 1                                                                
*                                  R4=A(RELATED OUT ENTRY)                      
HEAD10   ZIC   R1,1(R4)            SEE IF THERE IS A PREVIOUS HEAD              
         AR    R4,R1               FOR THE SAME LINE                            
         CR    R4,R3                                                            
         BE    HEADEND                                                          
         CLI   0(R4),X'40'                                                      
         BNE   HEAD10                                                           
         DROP  R3                                                               
         USING DRHDD,R4                                                         
         CLC   DRHDLINE,THISHLIN                                                
         BNE   HEAD10                                                           
         OI    DRHDSTAT,X'80'      FOUND - SO DELETE                            
         DROP  R4                                                               
         SPACE 1                                                                
HEADEND  B     XIT                                                              
         EJECT                                                                  
*              MAIN ELEMENTS - CHUNK                                            
         SPACE 3                                                                
CHUNK    NTR1                                                                   
         USING DECHD,R2            CHUNK STATEMENT                              
         USING DRCHD,R3                                                         
         SPACE 1                                                                
         CLI   OUTYN,C'N'                                                       
         BE    XIT                 IGNORE IF PREVIOUS OUT FAILED                
         LA    R1,DRCHIFS          CHECK FOR CONDITIONS                         
         BAS   RE,GETIFS                                                        
         BNE   XIT                                                              
         MVC   DRCHADD1,ALASTOUT                                                
         MVI   ELCODE,X'82'        ANY CHUNK ROUTINE?                           
         BAS   RE,GETSUB                                                        
         BNE   CHUNK2                                                           
         L     R4,ASUBEL                                                        
         USING DESRD,R4                                                         
         MVC   DRCHROUT,DESROUT                                                 
         MVC   GLLABEL,DESROUT                                                  
         BAS   RE,RESOLVE                                                       
         MVC   DRCHRADD,GLAROUT                                                 
         SPACE 1                                                                
CHUNK2   MVI   ELCODE,X'83'        ANY CHUNK ARGUMENTS?                         
         BAS   RE,GETSUB                                                        
         BNE   CHUNK4                                                           
         L     R4,ASUBEL                                                        
         USING DESAD,R4                                                         
         ZIC   R1,DESALEN                                                       
         SH    R1,=H'3'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   DRCHARGS(0),DESARGS                                              
         SPACE 1                                                                
CHUNK4   MVC   DRCHLAB2,DECHENDL   LABEL OF SECOND OUTPUT                       
         MVC   NEEDLABL,DECHENDL                                                
         MVC   NEEDREC,GLRECNO                                                  
         LA    R1,DRCHADD2         NEED ITS ADDRESS LATER                       
         ST    R1,NEEDADD                                                       
         BAS   RE,PUTNEED                                                       
         SPACE 1                                                                
CHUNK6   MVI   ELCODE,X'87'        ANY LITERALS                                 
         SR    R1,R1                                                            
         BAS   RE,GETSUB                                                        
         BNE   CHUNK8                                                           
         L     R4,ASUBEL                                                        
         USING DELITD,R4                                                        
         ZIC   R1,DELITLEN                                                      
         SH    R1,=H'4'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   DRCHLIT(0),DELITRAL                                              
         LA    R1,1(R1)                                                         
         SPACE 1                                                                
CHUNK8   LA    R1,72(R1)                                                        
         STC   R1,DRCHELEN                                                      
         L     R4,ALASTOUT         FIND WHAT THIS RELATES TO                    
         LTR   R4,R4                                                            
         BZ    CHUNKEND                                                         
         USING DROD,R4                                                          
         AI    DROCHFOL,1          UPDATE CONNECTED OUT ENTRY                   
         SPACE 1                                                                
CHUNKEND B     XIT                                                              
         EJECT                                                                  
*              MAIN ELEMENTS - FIRST LAST AND TOTAL                             
         SPACE 3                                                                
TOTAL    DS    0H                                                               
FIRST    NTR1                                                                   
         USING DEFLD,R2                                                         
         USING DRFLD,R3                                                         
         CLI   RECYN,C'N'                                                       
         BE    XIT                 IGNORE IF PREVIOUS REC FAILED                
         CLI   OUTYN,C'N'                                                       
         BE    XIT                 IGNORE IF PREVIOUS OUT FAILED                
         LA    R1,DRFLIFS          CHECK FOR CONDITIONS                         
         BAS   RE,GETIFS                                                        
         BNE   XIT                                                              
         MVI   FILATOT,C'Y'        INDICATE FIRST/LAST/TOTAL                    
         CLI   DEFLEL,X'48'                                                     
         BE    *+10                                                             
         MVC   DRFLTYPE,DEFLIND    F OR L                                       
         MVI   ELCODE,X'82'        ANY SYSTEM ROUTINE?                          
         BAS   RE,GETBEST                                                       
         BNE   FIRST2                                                           
         L     R4,ASUBEL                                                        
         USING DESRD,R4                                                         
         MVC   DRFLROUT,DESROUT                                                 
         MVC   GLLABEL,DESROUT                                                  
         BAS   RE,RESOLVE                                                       
         MVC   DRFLRADD,GLAROUT                                                 
         SPACE 1                                                                
FIRST2   MVI   ELCODE,X'83'        ANY ARGUMENTS?                               
         BAS   RE,GETSUB                                                        
         BNE   FIRST4                                                           
         L     R4,ASUBEL                                                        
         USING DESAD,R4                                                         
         ZIC   R1,DESALEN                                                       
         SH    R1,=H'3'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   DRFLARGS(0),DESARGS                                              
         SPACE 1                                                                
FIRST4   MVI   ELCODE,X'86'        LOOK FOR  POSITION                           
         BAS   RE,GETSUB                                                        
         BNE   FIRST5                                                           
         L     R4,ASUBEL                                                        
         USING DEOPPD,R4                                                        
         MVC   DRFLPOS,DEOPPLT                                                  
         SPACE 1                                                                
FIRST5   MVI   ELCODE,X'49'        LOOK FOR TOTAL DETAIL ELEMENT                
         BAS   RE,GETSUB                                                        
         BNE   FIRST6                                                           
         L     R4,ASUBEL                                                        
         USING DEDETD,R4                                                        
         MVC   DRFLDET,DEDETAIL                                                 
         SPACE 1                                                                
FIRST6   MVI   ELCODE,X'87'        ANY LITERALS                                 
         SR    R1,R1                                                            
         BAS   RE,GETSUB                                                        
         BNE   FIRST8                                                           
         L     R4,ASUBEL                                                        
         USING DELITD,R4                                                        
         ZIC   R1,DELITLEN                                                      
         SH    R1,=H'4'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   DRFLLIT(0),DELITRAL                                              
         LA    R1,1(R1)                                                         
         SPACE 1                                                                
FIRST8   LA    R1,60(R1)                                                        
         STC   R1,DRFLELEN                                                      
         MVI   ELCODE,X'46'        LOOK FOR INSTRUCTIONS                        
         BAS   RE,GETSUB                                                        
         B     FIRST12                                                          
         SPACE 1                                                                
FIRST10  BAS   RE,NEXTSUB                                                       
         SPACE 1                                                                
FIRST12  BNE   FIRST22                                                          
         L     R4,ASUBEL                                                        
         USING DECBD,R4                                                         
         CLI   DECBINS,2                                                        
         BL    FIRST14                                                          
         BE    FIRST16                                                          
         CLI   DECBINS,4                                                        
         BL    FIRST18                                                          
         BE    FIRST20                                                          
         CLI   DECBINS,6                                                        
         BL    FIRST21                                                          
         DC    H'0'                                                             
         SPACE 1                                                                
FIRST14  OI    DRFLOPT,X'80'       SKIP TO NEW PAGE                             
         B     FIRST10                                                          
         SPACE 1                                                                
FIRST16  MVC   DRFLSPAC,DECBVAL    SPACE N LINES                                
         B     FIRST10                                                          
         SPACE 1                                                                
FIRST18  OI    DRFLOPT,X'40'       RESET PAGE NUMBER                            
         B     FIRST10                                                          
         SPACE 1                                                                
FIRST20  OI    DRFLOPT,X'20'       RESET SUB PAGE NUMBER                        
         B     FIRST10                                                          
         SPACE 1                                                                
FIRST21  OI    DRFLOPT,X'10'       MIDHEAD                                      
         B     FIRST10                                                          
         SPACE 1                                                                
FIRST22  L     R4,ALASTOUT         FIND WHAT THIS RELATES TO                    
         LTR   R4,R4                                                            
         BZ    FIRST26                                                          
         USING DROD,R4                                                          
         CLI   DEFLEL,X'48'                                                     
         BE    FIRST24                                                          
         AI    DROFLFOL,1          UPDATE CONNECTED OUT ENTRY                   
         B     FIRSTEND                                                         
         SPACE 1                                                                
FIRST24  AI    DROTLFOL,1                                                       
         B     FIRSTEND                                                         
         SPACE 1                                                                
FIRST26  L     R4,ALASTREC                                                      
         LTR   R4,R4                                                            
         BZ    FIRSTEND                                                         
         USING DRRECD,R4                                                        
         AI    DRRECTOT,1                                                       
         B     FIRSTEND                                                         
         SPACE 1                                                                
FIRSTEND MVI   FILATOT,C'N'        RESET FIRST/LAST/TOTAL INDICATOR             
         B     XIT                                                              
         EJECT                                                                  
*              MAIN ELEMENTS - COMPUTATIONAL                                    
         SPACE 3                                                                
COMP     NTR1                                                                   
         USING DECMPD,R2           COMP STATEMENT                               
         USING DRCMD,R3                                                         
         CLI   INOROUT,C'O'        CHECK WHETHER IN OR OUT CAME BEFORE          
         BE    COMP1                                                            
         CLI   INYN,C'N'                                                        
         BE    XIT                 IGNORE IF PREVIOUS IN FAILED                 
         L     R1,ALASTIN                                                       
         LTR   R1,R1                                                            
         BZ    COMPERR                                                          
         USING DRIND,R1                                                         
         SPACE 1                                                                
COMP0    ST    R3,DRINCOMP                                                      
         DROP  R1                                                               
         B     COMP1B                                                           
         SPACE 1                                                                
COMP1    CLI   OUTYN,C'N'                                                       
         BE    XIT                 IGNORE IF PREVIOUS OUT FAILED                
         USING DROD,R1                                                          
         L     R1,ALASTOUT                                                      
         LTR   R1,R1                                                            
         BZ    COMPERR                                                          
         CLC   DROROUT,=CL8'RANK'  RANK ALWAYS APPLIES TO IN                    
         BNE   COMP1A                                                           
         L     R1,DROIADD                                                       
         B     COMP0                                                            
         SPACE 1                                                                
COMP1A   ST    R3,DROACOMP         SAVE A(COMP DRIVE ENTRY)                     
         XC    DROIADD,DROIADD     OUT DOESN'T NEED IN NOW                      
         SPACE 1                                                                
         DROP  R1                                                               
COMP1B   LA    R5,DRCMEXP          R5=A(EXPRESSION IN DRIVE TABLE)              
         MVI   ELCODE,X'52'        GO AND LOOK FOR FORMULA ELS.                 
         BAS   RE,GETSUB                                                        
         B     COMP4                                                            
         SPACE 1                                                                
COMP2    BAS   RE,NEXTSUB                                                       
         SPACE 1                                                                
COMP4    BNE   COMPEND                                                          
         AI    DRCMNEXP,1                                                       
         L     R4,ASUBEL                                                        
         USING DEFRMD,R4                                                        
         DROP  R3                                                               
         USING DRCMEXP,R5                                                       
         MVC   DRCMOP,DEFRMOP                                                   
         MVC   DRCMSUB,DEFRMLEV                                                 
         MVC   DRCMTYPE,DEFRMON                                                 
         MVC   DRCMLIT,DEFRMLIT                                                 
         DROP  R5                                                               
         USING DRCMD,R3                                                         
         CLI   DEFRMON,1           IF LITERAL, WE'RE DONE                       
         BE    COMP6                                                            
         MVC   NEEDLABL,DEFRMLAB   OTHERWISE WE WILL NEED AN ADDRESS            
         MVC   NEEDREC,GLRECNO                                                  
         LA    R1,4(R5)                                                         
         XC    4(4,R5),4(R5)                                                    
         ST    R1,NEEDADD                                                       
         BAS   RE,PUTNEED                                                       
         SPACE 1                                                                
COMP6    LA    R5,8(R5)                                                         
         B     COMP2                                                            
         SPACE 1                                                                
COMPEND  ZIC   R1,DRCMNEXP         LENGTH = N'EXPRESSIONS                       
         SLL   R1,3                *8                                           
         LA    R1,4(R1)            +4                                           
         STC   R1,DRCMELEN                                                      
         B     XIT                                                              
         SPACE 1                                                                
COMPERR  MVI   ERRNUM,6                                                         
         BAS   RE,ERROR                                                         
         B     XIT                                                              
         EJECT                                                                  
*              MAIN ELEMENTS - CONDITIONS                                       
         SPACE 3                                                                
CON      NTR1                                                                   
         USING DECOND,R2           CONDITION STATEMENT                          
         USING DRCOND,R3                                                        
         ST    R3,NEEDADD          NOTE ADDRESS                                 
         MVC   NEEDLABL,DECONLAB   AND LABEL                                    
         MVI   NEEDREC,0                                                        
         BAS   RE,PUTLABEL                                                      
         MVC   DRCONLAB,DECONLAB                                                
         MVI   ELCODE,X'62'        LOOK FOR SUB-CONDITIONS                      
         LA    R5,DRCONCON                                                      
         BAS   RE,GETSUB                                                        
         B     CON4                                                             
         SPACE 1                                                                
CON2     BAS   RE,NEXTSUB                                                       
         SPACE 1                                                                
CON4     BNE   CONEND                                                           
         AI    DRCONNCN,1          NOTE N'CONDITIONS HERE                       
         L     R4,ASUBEL                                                        
         USING DESCND,R4                                                        
         DROP  R3                                                               
         USING DRCONCON,R5                                                      
         ZIC   R1,DESCNFLD         CHANGE FLD# TO ADDRESS                       
         BCTR  R1,0                                                             
         SLL   R1,2                                                             
         LA    R1,GLAOPTS(R1)                                                   
         L     R1,0(R1)                                                         
         ZIC   R0,DESCNSUB         (+DISPLACEMENT)                              
         LTR   R0,R0                                                            
         BZ    *+6                                                              
         BCTR  R0,0                                                             
         AR    R1,R0                                                            
         ST    R1,DRCONAF1                                                      
         MVC   DRCONIOA,DESCNCON                                                
         LA    R1,COPLIST                                                       
         SR    RE,RE                                                            
         SPACE 1                                                                
CON6     CLC   DESCNOP,0(R1)       LOOK UP OP CODE                              
         BE    CON8                                                             
         LA    R1,2(R1)                                                         
         LA    RE,1(RE)                                                         
         CLI   0(R1),X'FF'                                                      
         BNE   CON6                                                             
         DC    H'0'                                                             
         SPACE 1                                                                
COPLIST  DC    C'EQGTLTNENLNH'                                                  
         DC    X'FF'                                                            
         SPACE 1                                                                
CON8     STC   RE,DRCONOP                                                       
         MVI   DRCONTYP,1          1 BYTE VALUE                                 
         MVC   DRCONVAL,DESCNVAL+1                                              
         CLI   DESCNVAL,0                                                       
         BE    CON10                                                            
         MVI   DRCONTYP,2          2 BYTE VALUE                                 
         MVC   DRCONVAL,DESCNVAL                                                
         CLI   DESCNVAL,X'40'                                                   
         BH    CON10                                                            
         MVI   DRCONTYP,0          OTHERWISE ITS ANOTHER FIELD                  
         ZIC   R1,DESCNF2                                                       
         BCTR  R1,0                                                             
         SLL   R1,2                                                             
         LA    R1,GLAOPTS(R1)                                                   
         L     R1,0(R1)                                                         
         ZIC   R0,DESCNF2+1        (+DISPLACEMENT)                              
         LTR   R0,R0                                                            
         BZ    *+6                                                              
         BCTR  R0,0                                                             
         AR    R1,R0                                                            
         ST    R1,DRCONAF2                                                      
         DROP  R5                                                               
         USING DRCOND,R3                                                        
         SPACE 1                                                                
CON10    LA    R5,12(R5)                                                        
         B     CON2                                                             
         SPACE 1                                                                
CONEND   ZIC   R1,DRCONNCN         ELEMENT LENGTH = N'CONDITIONS                
         MH    R1,=H'12'           *12                                          
         LA    R1,12(R1)           +12                                          
         STC   R1,DRCONELN                                                      
         B     XIT                                                              
         EJECT                                                                  
*              DEAL WITH POSSIBLE DICTIONARY AND ENTRY ELEMENTS                 
         SPACE 3                                                                
DICENT   NTR1                                                                   
*                                  R2=A(PRESENT MAIN ELEMENT)                   
*                                  RETURNING ADICTENT IF FOUND                  
         SPACE 1                                                                
         MVI   ELCODE,X'80'        FIRST LOOK FOR A DICTIONARY NAME             
         BAS   RE,GETBEST                                                       
         BNE   DICENT2                                                          
         L     R4,ASUBEL                                                        
         USING DEDICD,R4                                                        
         MVC   DICNAME,DEDICNAM                                                 
         SPACE 1                                                                
DICENT2  MVI   ELCODE,X'81'        NOW TRY FOR ENTRY                            
         BAS   RE,GETBEST                                                       
         BNE   DICENT4                                                          
         L     R4,ASUBEL                                                        
         USING DEENTD,R4                                                        
         MVC   ENTNAME,DEENTNAM                                                 
         SPACE 1                                                                
DICENT4  CLC   ENTNAME,SPACES      GOT SOMETHING?                               
         BE    DICMISS2                                                         
         LA    R4,KEY                                                           
         XC    KEY,KEY                                                          
         USING DICKEY,R4                                                        
         MVI   DICSYS,DICSYSQ                                                   
         MVI   DICKTYP,DICKTYPQ                                                 
         MVC   DICCODE,DICNAME                                                  
         MVC   DICENTRY,ENTNAME                                                 
         BAS   RE,DICREAD                                                       
         BNE   DICMISS                                                          
         LA    R4,IO                                                            
         LA    R1,DICFIRST                                                      
         ST    R1,ADICTENT                                                      
         B     XIT                                                              
         SPACE 1                                                                
DICMISS  MVC   P(31),=C'ERROR TRYING TO READ DICTIONARY'                        
         BAS   RE,PRINTERR                                                      
         MVC   P(8),=C'DICNAME='                                                
         MVC   P+8(8),DICNAME                                                   
         MVC   P+20(8),=C'ENTNAME='                                             
         MVC   P+28(8),ENTNAME                                                  
         BAS   RE,PRINTERR                                                      
         BAS   RE,PRINTOUT                                                      
         SPACE 1                                                                
DICMISS2 XC    ADICTENT,ADICTENT                                                
         B     XIT                                                              
         EJECT                                                                  
*              I/O FOR DICTIONARY FILE                                          
         SPACE 3                                                                
DICREAD  NTR1                                                                   
         L     R1,UTL                                                           
         MVC   SAVUTL,4(R1)        SAVE USER'S UTL                              
         MVI   4(R1),X'0A'         CONTROL SYSTEM                               
         CLI   DICOPEN,C'Y'                                                     
         BE    DICREAD2                                                         
         MVI   DICOPEN,C'Y'                                                     
         GOTO1 DATAMGR,DMCB,=C'OPEN',=C'CONTROL',GENLIST,IO                     
         SPACE 1                                                                
DICREAD2 GOTO1 DATAMGR,DMCB,=C'DMREAD',=CL8'GENDIR',KEY,KEY,0                   
         CLI   DMCB+8,0                                                         
         BNE   DMNO                                                             
         GOTO1 DATAMGR,DMCB,=C'GETREC',=CL8'GENFIL',DICDA,IO,DMWORK             
         CLI   DMCB+8,0                                                         
         BNE   DMNO                                                             
         L     R1,UTL                                                           
         MVC   4(1,R1),SAVUTL                                                   
         B     YES                                                              
         SPACE 1                                                                
DMNO     L     R1,UTL                                                           
         MVC   4(1,R1),SAVUTL                                                   
         B     NO                                                               
         SPACE 1                                                                
SAVUTL   DC    X'00'                                                            
DICOPEN  DC    C'N'                                                             
GENLIST  DC    CL8'NGENDIR'                                                     
         DC    CL8'NGENFIL'                                                     
         DC    C'X'                                                             
         EJECT                                                                  
*              RESOLVE ROUTINE LABELS INTO ROUTINE ADDRESSES                    
         SPACE 3                                                                
RESOLVE  NTR1                                                                   
*                                  GLLABEL HAS LABEL OF ROUTINE                 
*                                  RETURNING ADDRESS IN GLAROUT                 
         XC    GLAROUT,GLAROUT                                                  
         MVI   GLHOOK,GLRESOLV                                                  
         SPACE 1                                                                
         MVI   GLAROUT,1           GO TO APPLICATION FIRST                      
         BAS   RE,GOHOOK                                                        
         MVI   GLAROUT,1                                                        
         OC    GLAROUT+1(3),GLAROUT+1     ANY HELP?                             
         BNZ   YES                                                              
         SPACE 1                                                                
         MVI   GLAROUT,2           NOW TRY SYSTEM DRIVER                        
         BAS   RE,GOHOOK                                                        
         MVI   GLAROUT,2                                                        
         OC    GLAROUT+1(3),GLAROUT+1     ANY HELP?                             
         BNZ   YES                                                              
         SPACE 1                                                                
         MVI   GLAROUT,3           FINALLY DRIVER ITSELF                        
         BAS   RE,GOHOOK                                                        
         MVI   GLAROUT,3                                                        
         OC    GLAROUT+1(3),GLAROUT+1     ANY HELP?                             
         BNZ   YES                                                              
         SPACE 1                                                                
*                                  NO GOOD - WE HAVE A PROBLEM                  
         SPACE 1                                                                
         MVC   P(24),=C'UNRESOLVED ROUTINE NAME '                               
         MVC   P+24(8),GLLABEL                                                  
         BAS   RE,PRINTERR                                                      
         BAS   RE,PRINTOUT                                                      
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINES TO FLESH OUT DRIVE TABLE WITH ADDRESSES                 
         SPACE 3                                                                
FILLADDS NTR1                                                                   
         L     R2,ANEEDTAB                                                      
         L     R0,NEEDSOFA                                                      
         LTR   R0,R0                                                            
         BZ    XIT                                                              
         SPACE 1                                                                
FILLADD2 L     R1,0(R2)            PICK UP ADDRESS WHERE NEED IS                
         LTR   R1,R1                                                            
         BZ    FILLADD4                                                         
         MVC   NEEDLABL,4(R2)                                                   
         MVC   NEEDREC,16(R2)                                                   
         BAS   RE,GETADD                                                        
         MVC   1(3,R1),NEEDADD+1   PRESERVE FIRST BYTE                          
         BE    FILLADD4                                                         
         MVC   P(17),=C'UNRESOLVED LABEL='                                      
         MVC   P+17(8),NEEDLABL                                                 
         BAS   RE,PRINTERR                                                      
         SPACE 1                                                                
FILLADD4 AH    R2,NEEDWIDE                                                      
         BCT   R0,FILLADD2                                                      
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINES TO GENERATE DETAILS OF INTERNAL RECORDS                 
         SPACE 3                                                                
DINTRECS NTR1                                                                   
         LA    R2,GLAINTD                                                       
         L     R3,ENDDRIVE                                                      
         LA    R3,8(R3)                                                         
         SRL   R3,3                                                             
         SLL   R3,3                                                             
         MVC   0(8,R3),=C'*INTDET*'                                             
         LA    R3,8(R3)                                                         
         ZIC   R0,GLRECNO          PICK UP NUMBER OF ACTIVE RECORDS             
         LA    R1,L'GLINTDET                                                    
         MR    R0,R0                                                            
         ZIC   R0,GLRECNO                                                       
         AR    R1,R3                                                            
         ST    R1,ARECPOOL                                                      
         MVI   GLRECNO,1                                                        
         CH    R0,=H'12'                                                        
         BNH   DINT2                                                            
         LA    R0,12                                                            
         MVC   P(30),=C'MORE THAN 12 RECORDS SPECIFIED'                         
         BAS   RE,PRINTERR                                                      
         SPACE 1                                                                
DINT2    ST    R3,0(R2)            SAVE THE ADDRESS OF DETAILS                  
         BAS   RE,ONEREC           GO AND GET DETAILS FOR 1 RECORD              
         USING GLINTD,R3                                                        
         AI    GLRECNO,1                                                        
         LA    R2,4(R2)                                                         
         LA    R3,L'GLINTDET(R3)                                                
         BCT   R0,DINT2                                                         
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO PICK UP DETAILS FOR ONE RECORD                        
         SPACE 3                                                                
ONEREC   NTR1                                                                   
         XC    0(L'GLINTDET,R3),0(R3)                                           
         L     R2,GLADTAB                                                       
         SPACE 1                                                                
ONER2    CLI   0(R2),X'10'         LOOK FOR RECORD ELEMENT                      
         BNE   ONER4               IN DRIVE TABLE                               
         USING DRRECD,R2                                                        
         CLC   GLRECNO,DRRECNUM    THAT MATCHES                                 
         BE    ONER6                                                            
         SPACE 1                                                                
ONER4    CLI   0(R2),0                                                          
         BNE   *+6                                                              
         DC    H'0'                                                             
         ZIC   R1,1(R2)                                                         
         AR    R2,R1                                                            
         B     ONER2                                                            
         SPACE 1                                                                
ONER6    BAS   RE,GETLENS          FIGURE OUT INPUT/OUTPUT LENGTHS              
         BAS   RE,RECADDS          SET UP RECORD ADDRESSES                      
         B     XIT                                                              
         EJECT                                                                  
*              FIGURE OUT LENGTHS OF KEY, DATA RECORDS AND PRINT                
         SPACE 3                                                                
GETLENS  NTR1                                                                   
*                                  R2=A(DRIVE TABLE ENTRY FOR RECORD)           
*                                  R3=A(INTERNAL RECORD DETAILS)                
         ST    R2,GLARECEL         SAVE ADDRESS OF RECORD ELEMENT               
         MVI   LENTYPE,C'K'                                                     
         MVI   RANKFND,C'N'                                                     
         USING DRRECD,R2                                                        
         CLI   DRRECTOT,0          ANY LEVEL 0 TOTALS                           
         BE    *+8                                                              
         MVI   GLTOTLV0,1          YES - NOTE                                   
         MVI   ANYFOLD,C'N'                                                     
         SPACE 1                                                                
GTL2     ZIC   R1,1(R2)            GET AN ELEMENT                               
         AR    R2,R1                                                            
         CLI   0(R2),X'10'         HAVE WE GOT TO THE END                       
         BE    GTLEND                                                           
         CLI   0(R2),X'00'                                                      
         BE    GTLEND                                                           
         CLI   0(R2),X'FF'                                                      
         BE    GTLEND                                                           
         CLI   0(R2),X'12'                                                      
         BE    GTL12                                                            
         CLI   0(R2),X'20'                                                      
         BE    GTL20                                                            
         CLI   0(R2),X'30'                                                      
         BE    GTL30                                                            
         CLI   0(R2),X'48'                                                      
         BE    GTL48                                                            
         CLI   0(R2),X'50'                                                      
         BE    GTL50                                                            
         B     GTL2                                                             
         SPACE 1                                                                
GTL12    MVI   LENTYPE,C'D'        DATA ELEMENT - RESET TYPE                    
         ZIC   R1,LASTILEV                                                      
         LA    R1,GLTOTLEV-1(R1)   SET LEVEL FOR DETAIL                         
         MVI   0(R1),2                                                          
         TM    GLINDS2,GLEXTBOX    OPTION TO GET ANOTHER BOX HERE               
         BNO   GTL2                                                             
         LH    R1,GLPWIDTH                                                      
         LA    R1,1(R1)                                                         
         STH   R1,GLPWIDTH                                                      
         B     GTL2                                                             
         SPACE 1                                                                
         USING DRIND,R2                                                         
GTL20    CLI   DRINTYPE+1,C'+'     CHECK FOR ADDITIVE FIELDS                    
         BNE   *+8                                                              
         OI    GLRECIND,GLADINRC   AND SET BIT ON                               
         CLC   DRINROUT(7),=C'COLRANK'   CHECK FOR COLUMN RANKING               
         BNE   GTL21                                                            
         OI    GLRECIND,GLCLRANK   AND SET BIT ON                               
         CLI   GLRNKLEV,0          ESTABLISH LEVEL 1 IF NOT SPECIFIED           
         BNE   GTL21                                                            
         MVI   GLRNKLEV,1                                                       
         SPACE 1                                                                
GTL21    ZIC   R1,DRINLEN          INPUT FIELD - PICK UP LENGTH                 
         LH    RE,GLRECLEN         ADD TO RECORD LENGTH                         
         AR    RE,R1                                                            
         STH   RE,GLRECLEN                                                      
         CLI   LENTYPE,C'K'        AND, IF WE'RE STILL IN THE KEY,              
         BNE   GTL2                                                             
         MVC   GLDETLEV,DRINLEV    NOTE LEVEL NUMBER                            
         STC   R1,GLDETLEN         NOTE LEVEL LENGTH                            
         LH    RE,GLKEYLEN         ADD TO KEY LENGTH                            
         AR    RE,R1                                                            
         STH   RE,GLKEYLEN                                                      
         CLI   DRINLEV,1           AT LEVEL 1                                   
         BNE   GTL22                                                            
         STC   R1,GLLCBS           LENGTH=L'C/B 1                               
         SPACE 1                                                                
GTL22    ZIC   RE,DRINLEV          PICK UP LEVEL NUMBER                         
         STC   RE,LASTILEV                                                      
         LA    RE,GLLCBS-2(RE)                                                  
         ZIC   R0,0(RE)            PICK UP LENGTH OF PREVIOUS LEVEL             
         AR    R1,R0               ADD LENGTH OF THIS LEVEL                     
         STC   R1,1(RE)            AND SAVE                                     
         STC   R0,GLDETDSP         NOTE DISPLACEMENT TO LEVEL                   
         SPACE 1                                                                
GTL23    OC    GLAFIN,GLAFIN       NOTE ADDRESS OF FIRST IN                     
         BNZ   *+8                                                              
         ST    R2,GLAFIN                                                        
         CLC   DRINROUT,=CL8'RANK' NOTE IN RELATED TO RANK                      
         BNE   GTL24                                                            
         ST    R2,GLARANK                                                       
         MVC   GLRNKLEV,DRINLEV                                                 
         OC    DRINCOMP,DRINCOMP                                                
         BZ    GTL24                                                            
         MVI   RANKFND,C'Y'                                                     
         SPACE 1                                                                
GTL24    CLC   DRINROUT,=CL8'RECORD'                                            
         BNE   *+10                                                             
         MVC   GLRECLEV,DRINLEV                                                 
         B     GTL2                                                             
         SPACE 1                                                                
         USING DROD,R2                                                          
GTL30    CLI   DROLTYP,C'N'                                                     
         BE    GTL31B                                                           
         CLI   DROLTYP,C'P'        THIS STUFF IS ONLY FOR P LINES               
         BNE   GTL32                                                            
         TM    DROFORM,X'02'       IS FOLD SET ON THIS                          
         BNO   *+8                                                              
         MVI   ANYFOLD,C'Y'                                                     
         CLI   ANYFOLD,C'Y'        IF PREVIOUS FOLD                             
         BE    GTL32               DON'T COUNT IN LENGTH                        
         CLI   DROLINE,1           ONLY INTERESTED IN FIRST PRINT LINE          
         BH    GTL32               (ALSO CUTS OUT +)                            
         CLI   GLCNTWID,0          HAS CONTINUATION BEEN SET YET                
         BNE   GTL31                                                            
         CLI   DROLEN,6            IS THIS WIDE ENOUGH                          
         BL    GTL31                                                            
         MVC   GLCNTDSP,GLPWIDTH+1 YES - SO SAVE DISPLACEMENT                   
         MVC   GLCNTWID,DROLEN     AND WIDTH UP TO NOW                          
         SPACE 1                                                                
GTL31    ZIC   R1,DROLEN           OUTPUT ELEMENT - PICK UP LENGTH              
         AH    R1,GLPWIDTH                                                      
         ZIC   R0,GLGAP                                                         
         AR    R1,R0               ADD THIS +GAP TO PRINT WIDTH                 
         STH   R1,GLPWIDTH                                                      
         SPACE 1                                                                
GTL31B   CLI   LENTYPE,C'K'        IF WE'RE STILL IN THE KEY                    
         BNE   GTL32                                                            
         CLI   RANKFND,C'Y'        UNLESS RANK IS ACTIVE                        
         BE    GTL32                                                            
         SR    R1,R1               NOTE PRINT DISP AT THIS LEVEL                
         ICM   R1,1,DROLEV                                                      
         BZ    GTL32                                                            
         LA    R1,GLPDCBS-1(R1)                                                 
         MVC   0(1,R1),GLPWIDTH+1                                               
         SPACE 1                                                                
GTL32    CLI   DROTLFOL,0          ANY TOTALS FOR THIS OUT?                     
         BE    GTL34                                                            
         ZIC   R1,DROLEV           THEN NOTE LEVEL                              
         STC   R1,LASTOLEV                                                      
         LA    R1,GLTOTLEV-1(R1)   WHERE TOTALS ARE NEEDED                      
         MVI   0(R1),1                                                          
         SPACE 1                                                                
GTL34    OC    GLAFOUT,GLAFOUT     NOTE ADDRESS OF FIRST OUT                    
         BNZ   *+8                                                              
         ST    R2,GLAFOUT                                                       
         CLI   DROLTYP,C'M'        NOTE LEVEL OF FIRST MID-LINE                 
         BNE   GTL2                                                             
         CLI   GLMIDLEV,0                                                       
         BNE   GTL2                                                             
         MVC   GLMIDLEV,DROLEV                                                  
         B     GTL2                                                             
         SPACE 1                                                                
         USING DRFLD,R2                                                         
GTL48    CLI   DRFLDET,0           WAS DETAIL TOTAL LEVELS SPECIFIED            
         BE    GTL2                                                             
         ZIC   R1,LASTOLEV                                                      
         LA    R1,GLTOTLEV-1(R1)                                                
         ZIC   RE,DRFLDET                                                       
         LA    RE,16(RE)           SAVE N'LEVELS + 16                           
         STC   RE,0(R1)                                                         
         OI    GLTOTIND,GLTIDET    TAKE A NOTE IN GLINT                         
         B     GTL2                                                             
         SPACE 1                                                                
         USING DRCMD,R2                                                         
GTL50    LA    R1,DRCMEXP          FOR COMPUTATIONAL ELEMENTS                   
         ZIC   R0,DRCMNEXP                                                      
         SPACE 1                                                                
GTL502   CLI   0(R1),C'V'          CHECK FOR VERTICAL PERCENTS                  
         BNE   *+8                                                              
         MVI   GLANYVRT,C'Y'       TAKE A NOTE IN GLINT IF FOUND                
         LA    R1,8(R1)                                                         
         BCT   R0,GTL502                                                        
         B     GTL2                                                             
         SPACE 1                                                                
GTLEND   ZIC   RE,GLDETLEV         CHECK THERE IS A TOTAL FOR THE LEVEL         
         BCTR  RE,0                ABOVE DETAIL LEVEL                           
         CLM   RE,1,LASTOLEV                                                    
         BNE   *+8                                                              
         OI    GLTOTIND,GLTITOT    YES - TAKE A NOTE                            
         LH    R1,GLRECLEN         WORK OUT L'DATA                              
         SH    R1,GLKEYLEN                                                      
         STH   R1,GLDATLEN                                                      
         LH    R1,GLRECLEN                                                      
         MVC   GLCONLEN,LENCON     SET CONTROL LENGTH HERE                      
         AH    R1,GLCONLEN         ADD IN CONTROL LENGTH                        
         STH   R1,GLRECLEN         TO TOTAL RECORD LENGTH                       
         CLC   GLKEYLEN,BGKEYLEN                                                
         BL    *+10                                                             
         MVC   BGKEYLEN,GLKEYLEN   UPDATE BIGGEST KEY FIELD                     
         CLC   GLRECLEN,BGRECLEN                                                
         BL    *+10                                                             
         MVC   BGRECLEN,GLRECLEN   UPDATE BIGGEST RECORD SIZE                   
         SPACE 1                                                                
         MVI   GLPDISP,0                                                        
         CLI   GLDOWNLD,0          NO LIMIT TO WIDTH WHEN DOWNLOADING           
         BNE   XIT                                                              
         L     R1,ABOX             FIGURE OUT DISPLACEMENT                      
         USING BOXD,R1                                                          
         L     R1,BOXWIDTH         PICK UP ACTUAL WIDTH                         
         DROP  R1                                                               
         LTR   R1,R1                                                            
         BNZ   *+8                                                              
         LA    R1,132                                                           
         OC    GLWPAPER,GLWPAPER   OPTIONAL OVERRIDE PAPER WIDTH                
         BZ    *+8                                                              
         L     R1,GLWPAPER                                                      
         SH    R1,GLPWIDTH                                                      
         BM    GTLEND2                                                          
         SRL   R1,1                                                             
         CLI   GLLFTOPT,C'Y'       OPTION TO LEFT ALIGN                         
         BNE   *+6                                                              
         SR    R1,R1                                                            
         STH   R1,GLPDISP                                                       
         B     XIT                                                              
         SPACE 1                                                                
GTLEND2  MVC   P(18),=C'REPORT IS TOO WIDE'                                     
         BAS   RE,PRINTERR                                                      
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINES TO GENERATE RECORD ADDRESSES                            
         SPACE 3                                                                
RECADDS  NTR1                                                                   
         MVI   GLLEVEL,0                                                        
         LA    R2,GLARECL0                                                      
         LA    R4,GLTOTLV0                                                      
         LA    R5,GLNUMLEV+1                                                    
         SPACE 1                                                                
RA2      CLI   0(R4),0             DO WE NEED A RECORD AT THIS LEVEL            
         BE    RA4                                                              
         L     RE,ARECNOW          YES SO PICK UP A CORE ADDRESS                
         LTR   RE,RE                                                            
         BNZ   *+8                                                              
         L     RE,ARECPOOL                                                      
         MVC   0(8,RE),=C'*R01*L01' SEED CORE WITH RECORD/LEVEL                 
         EDIT  (1,GLRECNO),(2,2(RE)),FILL=0                                     
         EDIT  (1,GLLEVEL),(2,6(RE)),FILL=0                                     
         LA    RE,8(RE)                                                         
         ST    RE,0(R2)            SAVE ADDRESS IN GLINT                        
         AH    RE,GLRECLEN                                                      
         LA    RE,8(RE)            ROUND TO NEXT DOUBLE                         
         SRL   RE,3                                                             
         SLL   RE,3                                                             
         ST    RE,ARECNOW          AND SAVE NEXT AVAILABLE ADDRESS              
         C     RE,GLAEND           IS THERE ANY ROOM?                           
         BL    RA4                                                              
         MVC   P(27),=C'NOT ENOUGH CORE FOR INTRECS'                            
         BAS   RE,PRINTERR                                                      
         B     XIT                                                              
         SPACE 1                                                                
RA4      AI    GLLEVEL,1                                                        
         LA    R2,4(R2)                                                         
         LA    R4,1(R4)                                                         
         BCT   R5,RA2                                                           
         B     XIT                                                              
         DROP  R3                                                               
         EJECT                                                                  
*              ROUTINES TO GENERATE PRINT ADDRESSES IN DRIVE TABLE              
         SPACE 3                                                                
PRNTADDS NTR1                                                                   
         L     R3,GLADTAB                                                       
         MVI   GLRECNO,0                                                        
         SPACE 1                                                                
PAD2     CLI   0(R3),0                                                          
         BE    XIT                                                              
         CLI   0(R3),X'10'         LOOK FOR REC                                 
         BE    PAD10                                                            
         CLI   0(R3),X'12'         DATA                                         
         BE    PAD12                                                            
         CLI   0(R3),X'30'         OUT                                          
         BE    PAD30                                                            
         CLI   0(R3),X'40'         HEAD                                         
         BE    PAD40                                                            
         CLI   0(R3),X'44'         FIRST/LAST                                   
         BE    PAD44                                                            
         CLI   0(R3),X'48'         TOTAL                                        
         BE    PAD44                                                            
         B     PADEND                                                           
         SPACE 1                                                                
PAD10    ZIC   R1,GLRECNO          PICK UP RECORD DETAILS                       
         SLL   R1,2                                                             
         LA    R1,GLAINTD(R1)                                                   
         L     R2,0(R1)                                                         
         USING GLINTD,R2                                                        
         AI    GLRECNO,1                                                        
         LH    R1,GLPDISP          SET UP SOFT COLUMN NUMBER FROM               
         LA    R1,1(R1)            DISPLACEMENT                                 
         STC   R1,SOFTMID                                                       
         LA    R1,1(R1)                                                         
         STC   R1,SOFTCOL                                                       
         MVI   SOFTLINE,1                                                       
         MVI   WLASTOUT,0                                                       
         B     PADEND                                                           
         SPACE 1                                                                
PAD12    TM    GLINDS2,GLEXTBOX    OPTION TO GET AN EXTRA BOX                   
         BNO   PADEND                                                           
         AI    SOFTCOL,1                  SO LEAVE AN EXTRA SPACES              
         B     PADEND                                                           
         SPACE 1                                                                
         USING DROD,R3                                                          
PAD30    LA    R5,DROAPOS                                                       
         MVI   SOFTHLIN,0          RESET HEADING NUMBER                         
         TM    DROFORM,X'04'       WAS MARK SET FOR THIS OUT                    
         BNO   *+10                                                             
         MVC   MARKCOL,SOFTCOL                                                  
         TM    DROFORM,X'02'       WAS FOLD SET FOR THIS OUT                    
         BNO   PAD30B                                                           
         MVC   SOFTCOL,MARKCOL     THEN BACK UP TO MARK COLUMN                  
         AI    SOFTLINE,1          AND ADD 1 TO THE LINE                        
         SPACE 1                                                                
PAD30B   GOTO1 FIGPADD,DMCB,DROPOS,(R5)                                         
         CLI   DROLTYP,C'P'        FOR PRINT OUTS...                            
         BNE   PADEND                                                           
         MVC   WLASTOUT,DROLEN     SAVE WIDTH OF THIS FOR LATER                 
         B     PADEND                                                           
         SPACE 1                                                                
         USING DRHDD,R3                                                         
PAD40    CLI   DRHDLINE,0          WAS SOFT HEADING INDICATED                   
         BNE   PAD40B                                                           
         AI    SOFTHLIN,1                                                       
         MVC   DRHDLINE,SOFTHLIN                                                
         SPACE 1                                                                
PAD40B   ZIC   R1,GLFHEADL         NUMBER OF HEAD LINE                          
         ZIC   R0,DRHDLINE         PLUS RELATIVE HEADING NO                     
         AR    R1,R0                                                            
         ZIC   R0,SOFTLINE         PLUS ADJUSTMENT FOR FOLD ETC                 
         AR    R1,R0                                                            
         SH    R1,=H'3'                                                         
         CH    R1,=H'13'                                                        
         BL    *+8                                                              
         LA    R1,13                                                            
         M     R0,=F'198'                                                       
         A     R1,AMYH1                                                         
         ZIC   R0,SOFTCOL          PLUS SOFT COLUMN                             
         BCTR  R0,0                                                             
         AR    R1,R0                                                            
         ST    R1,DRHDAPOS                                                      
         B     PADEND                                                           
         SPACE 1                                                                
         USING DRFLD,R3                                                         
PAD44    LA    R5,DRFLAPOS                                                      
         GOTO1 FIGPADD,DMCB,DRFLPOS,(R5)                                        
         SPACE 1                                                                
PADEND   ZIC   R1,1(R3)                                                         
         LTR   R1,R1                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R3,R1                                                            
         B     PAD2                                                             
         EJECT                                                                  
*              GENERATE PRINT ADDRESS FROM WHERE EXPRESSION                     
         SPACE 3                                                                
FIGPADD  NTR1                                                                   
         L     R2,0(R1)                                                         
         L     R5,4(R1)                                                         
*                                  R2=A(PRINT EXPRESSION)                       
*                                  R3=A(DRIVE TABLE ENTRY)                      
*                                  R5=A(PRINT ADDRESS RETURNED)                 
*                                  SOFTLINE SOFTCOL SET TO PRESENT              
         L     R4,AMYH1                                                         
         CLI   0(R2),C'H'          POSITION R4 TO HEAD                          
         BE    FIGP2                                                            
         L     R4,AMYM1                                                         
         CLI   0(R2),C'M'          MID                                          
         BE    FIGP2                                                            
         L     R4,AMYP1                                                         
         CLI   0(R2),C'P'          PRINT                                        
         BE    FIGP10                                                           
         CLI   0(R2),0                                                          
         BE    FIGP10                                                           
         L     R4,AMYF1                                                         
         CLI   0(R2),C'F'          OR FOOT                                      
         BE    FIGP2                                                            
         SR    R4,R4                                                            
         CLI   0(R2),C'N'          OR NOT AT ALL                                
         BE    FIGPEND                                                          
         MVI   ERRNUM,7                                                         
         BAS   RE,ERROR                                                         
         B     XIT                                                              
         SPACE 1                                                                
FIGP2    ZIC   R1,1(R2)            NOW PICK UP LINE NUMBER                      
         LTR   R1,R1                                                            
         BNZ   *+8                                                              
         LA    R1,1                                                             
         BCTR  R1,0                                                             
         M     R0,=F'198'                                                       
         AR    R4,R1                                                            
         ZIC   R1,2(R2)            AND COLUMN NUMBER                            
         AR    R4,R1                                                            
         BCTR  R4,0                                                             
         CLI   0(R2),C'M'          FOR MID-LINES                                
         BNE   FIGPEND                                                          
         ZIC   R1,SOFTMID          ADD IN SOFT DISPLACEMENT                     
         AR    R4,R1                                                            
         B     FIGPEND                                                          
         EJECT                                                                  
*              HANDLE THE SPECIAL RULES FOR PRINT LINES                         
         SPACE 3                                                                
FIGP10   CLI   1(R2),C'+'          TEST LINE FOR +                              
         BE    FIGP12                                                           
         CLI   1(R2),0             TEST LINE SOFT                               
         BE    FIGP14                                                           
         B     FIGP16                                                           
         SPACE 1                                                                
FIGP12   AI    SOFTLINE,1          BUMP SOFTLINE 1 FOR +                        
         B     FIGP20                                                           
         SPACE 1                                                                
FIGP14   ZIC   R1,SOFTCOL          FOR SOFT ADD WIDTH OF PREVIOUS               
         ZIC   R0,WLASTOUT                                                      
         LTR   R0,R0               DON'T ADD WIDTH IF FIRST OUT                 
         BZ    FIGP20                                                           
         CLI   0(R3),X'30'         AND ONLY IF IT IS AN OUT                     
         BNE   FIGP20                                                           
         AR    R1,R0                                                            
         ZIC   R0,GLGAP            +GAP                                         
         AR    R1,R0                                                            
         STC   R1,SOFTCOL                                                       
         B     FIGP20                                                           
         SPACE 1                                                                
FIGP16   MVC   SOFTLINE,1(R2)      USE SPECIFIED LINE NUMBER                    
         CLI   2(R2),0             MAY USE SOFT COLUMN THOUGH                   
         BE    FIGP14                                                           
         SPACE 1                                                                
FIGP20   CLI   2(R2),0             WAS SPECIFIC ROW SPECIFIED?                  
         BE    *+10                                                             
         MVC   SOFTCOL,2(R2)       THEN USE IT                                  
         ZIC   R1,SOFTLINE         NOW COMPUTE ADDRESS                          
         BCTR  R1,0                                                             
         MH    R1,=H'198'                                                       
         ZIC   R0,SOFTCOL                                                       
         BCTR  R0,0                                                             
         AR    R1,R0                                                            
         AR    R4,R1                                                            
         SPACE 1                                                                
FIGPEND  ST    R4,0(R5)            RETURN COMPUTED ADDRESS                      
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINES TO GENERATE ADDRESSES IN CHUNK ENTRIES                  
         SPACE 3                                                                
CHNKADDS NTR1                                                                   
         L     R3,GLADTAB                                                       
         MVI   GLRECNO,0                                                        
         SPACE 1                                                                
CHAD2    CLI   0(R3),0                                                          
         BE    XIT                                                              
         CLI   0(R3),X'42'         LOOK FOR CHUNK                               
         BE    CHAD42                                                           
         B     CHADEND                                                          
         SPACE 1                                                                
         USING DRCHD,R3                                                         
CHAD42   L     R4,DRCHADD1         A(FIRST OUT)                                 
         USING DROD,R4                                                          
         L     R1,DROAPOS          PICK UP FIRST DISPLACEMENT                   
         L     R0,AMYP1                                                         
         SR    R1,R0                                                            
         STH   R1,CHUD1                                                         
         L     R4,DRCHADD2         A(SECOND OUT)                                
         L     R1,DROAPOS          PICK UP SECOND DISPLACEMENT                  
         L     R0,AMYP1                                                         
         SR    R1,R0                                                            
         STH   R1,CHUD2                                                         
         SPACE 1                                                                
         ZIC   R1,GLFHEADL         A(HEADING 1) TO R1                           
         BCTR  R1,0                                                             
         M     R0,=F'198'                                                       
         A     R1,AMYH1                                                         
         LR    RE,R1                                                            
         AH    RE,CHUD1                                                         
         ST    RE,DRCHAO1          FIGURE OUT ACTUAL COL ADDRESSES              
         LR    RE,R1                                                            
         AH    RE,CHUD2                                                         
         ST    RE,DRCHAO2                                                       
         SPACE 1                                                                
         S     RE,DRCHAO1          COMPUTE WIDTH OF CHUNK                       
         ZIC   R1,DROLEN           (LENGTH OF SECOND)                           
         DROP  R4                                                               
         AR    RE,R1                                                            
         ZIC   R1,GLGAP                                                         
         AR    RE,R1                                                            
         STH   RE,DRCHLEN                                                       
         SPACE 1                                                                
CHADEND  ZIC   R1,1(R3)                                                         
         LTR   R1,R1                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R3,R1                                                            
         B     CHAD2                                                            
         SPACE 1                                                                
CHUD1    DS    H                                                                
CHUD2    DS    H                                                                
         EJECT                                                                  
*              ROUTINE TO INITIALIZE THE SORT                                   
         SPACE 3                                                                
SORTINIT NTR1                                                                   
         CLI   GLABCSW,0           ABC DOES NOT NEED SORTING                    
         BNE   XIT                                                              
         LH    R1,BGKEYLEN         MODIFY SORT CARD                             
         LA    R2,SORTCARD+15                                                   
         EDIT  (R1),(3,(R2)),FILL=0                                             
         SPACE 1                                                                
         LH    R1,BGRECLEN         MODIFY RECORD CARD                           
         LA    R2,RECCARD+21                                                    
         EDIT  (R1),(3,(R2)),FILL=0                                             
         SPACE 1                                                                
******** GOTO1 SORTER,DMCB,SORTCARD,RECCARD,0                                   
*                                  THIS IS HELD UP UNTIL WE                     
*                                  HAVE AN ACTUAL RECORD IN DRIVIN              
         MVI   GLANYSRT,C'N'                                                    
         B     XIT                                                              
         SPACE 1                                                                
         ENTRY SORTCARD                                                         
         ENTRY RECCARD                                                          
SORTCARD DC    CL80'SORT FIELDS=(3,NNN,A,1,2,A),FORMAT=BI,WORK=1'               
RECCARD  DC    CL80'RECORD TYPE=F,LENGTH=NNN'                                   
         EJECT                                                                  
*              ROUTINES TO HANDLE THE NEED TABLE                                
         SPACE 3                                                                
PUTNEED  NTR1                                                                   
         MVI   PUTTYPE,C'N'                                                     
         B     PUTALL                                                           
         SPACE 1                                                                
PUTLABEL NTR1                                                                   
         CLI   NEEDLABL,X'41'                                                   
         BL    XIT                                                              
         MVI   PUTTYPE,C'L'                                                     
         SPACE 1                                                                
PUTALL   L     R2,ANEEDTAB         GET ADDRESS OF NEXT ENTRY                    
         L     R1,NEEDSOFA                                                      
         MH    R1,NEEDWIDE                                                      
         AR    R2,R1                                                            
         LR    R1,R2                                                            
         CLI   PUTTYPE,C'L'                                                     
         BNE   *+8                                                              
         LA    R1,12(R2)                                                        
         MVC   0(4,R1),NEEDADD     SAVE ADDRESS                                 
         MVC   4(8,R2),NEEDLABL    AND LABEL                                    
         MVC   16(1,R2),NEEDREC    AND RECORD NUMBER                            
         SPACE 1                                                                
         L     R1,NEEDSOFA                                                      
         LA    R1,1(R1)                                                         
         ST    R1,NEEDSOFA                                                      
         C     R1,NEEDMAX                                                       
         BL    XIT                                                              
         DC    H'0'                                                             
         DC    C'EXPAND NEED TABLE'                                             
         SPACE 1                                                                
GETADD   NTR1                                                                   
*                                  NEEDLABL TO NEEDADD IF FOUND                 
         XC    NEEDADD,NEEDADD                                                  
         L     R2,ANEEDTAB                                                      
         L     R0,NEEDSOFA                                                      
         LTR   R0,R0                                                            
         BZ    NO                                                               
         SPACE 1                                                                
GETADD2  OC    12(4,R2),12(R2)                                                  
         BZ    GETADD3                                                          
         CLC   4(8,R2),NEEDLABL                                                 
         BNE   GETADD3                                                          
         CLC   16(1,R2),NEEDREC                                                 
         BE    GETADD4                                                          
         CLI   NEEDREC,0                                                        
         BE    GETADD4                                                          
         CLI   16(R2),0                                                         
         BE    GETADD4                                                          
         SPACE 1                                                                
GETADD3  AH    R2,NEEDWIDE                                                      
         BCT   R0,GETADD2                                                       
         B     NO                                                               
         SPACE 1                                                                
GETADD4  MVC   NEEDADD,12(R2)                                                   
         B     YES                                                              
         EJECT                                                                  
*              ROUTINE TO GET BEST SUBSIDIARY ELEMENT                           
         SPACE 3                                                                
GETBEST  NTR1                                                                   
         MVI   SUBWHERE,C'P'       PROGRAM IS PREFERRED                         
         BAS   RE,GETSUB                                                        
         BE    YES                                                              
         CLI   FILATOT,C'Y'        TEST FIRST/LAST/TOTAL                        
         BE    NO                  YES-STOP HERE                                
         SPACE 1                                                                
         MVI   SUBWHERE,C'E'       MAYBE THERE WAS AN ENTRY                     
         L     R2,ADICTENT                                                      
         BAS   RE,GB2                                                           
         BE    YES                                                              
         SPACE 1                                                                
         MVI   SUBWHERE,C'S'       OR FROM SET DEFAULTS                         
         LA    R2,SETAREA                                                       
         BAS   RE,GB2                                                           
         BNE   GB1                                                              
         ZIC   R1,1(R2)            WAS THERE ANY SIGNIFICANT DATA               
         SH    R1,=H'3'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         OC    2(0,R2),2(R2)                                                    
         BZ    NO                                                               
         B     YES                                                              
         SPACE 1                                                                
GB1      MVI   SUBWHERE,C'D'       FINALLY FROM DRIVER DEFAULTS                 
         LA    R2,DDEFAREA                                                      
         BAS   RE,GB2                                                           
         BNE   NO                                                               
         B     YES                                                              
         SPACE 1                                                                
GB2      NTR1                                                                   
         LTR   R2,R2                                                            
         BZ    NO                                                               
         SPACE 1                                                                
GB4      CLI   0(R2),0                                                          
         BE    GB8                                                              
         CLI   0(R2),X'FF'                                                      
         BE    GB8                                                              
         CLC   ELCODE,0(R2)                                                     
         BE    GB6                                                              
         ZIC   R1,1(R2)                                                         
         LTR   R1,R1                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R2,R1                                                            
         B     GB4                                                              
         SPACE 1                                                                
GB6      ST    R2,ASUBEL                                                        
         B     YES                                                              
         SPACE 1                                                                
GB8      XC    ASUBEL,ASUBEL                                                    
         B     NO                                                               
         EJECT                                                                  
*              ROUTINE TO LOOK FOR UP TO 4 CONDITIONAL ELEMENTS                 
*              AND TO SAVE ADDRESSES IN DRIVE TABLE                             
         SPACE 3                                                                
GETIFS   NTR1                                                                   
*                                  IT IS ASSUMED THAT                           
*                                  R2 = A(MAIN ELEMENT)                         
         LR    R3,R1               R1 = A(POSITION OF IF ADDRESSES)             
         ST    R3,ATHISIF                                                       
*                                  WE WILL USE R3 IN THIS ROUTINE               
         LA    R0,4                PRESET FOR UP TO 4 CONDITIONS                
         MVI   ELCODE,0                                                         
         BAS   RE,GETSUB                                                        
         B     GIF4                                                             
         SPACE 1                                                                
GIF2     BAS   RE,NEXTSUB                                                       
         SPACE 1                                                                
GIF4     BNE   GIFEND                                                           
         L     R2,ASUBEL                                                        
         CLI   0(R2),X'84'         DID WE FIND AN IF                            
         BE    GIF10                                                            
         CLI   0(R2),X'85'         OR A NOT                                     
         BE    GIF8                                                             
         B     GIF2                                                             
         SPACE 1                                                                
GIF8     MVI   0(R3),X'80'         NOT FOUND                                    
         SPACE 1                                                                
         USING DEIFD,R2                                                         
GIF10    MVC   NEEDLABL,DEIFLABL   CAN WE RESOLVE ADDRESS NOW                   
         MVI   NEEDREC,0                                                        
         BAS   RE,GETADD                                                        
         MVC   1(3,R3),NEEDADD+1                                                
         BE    GIF12                                                            
         OI    0(R3),X'40'         NO SO ITS RUN TIME                           
         ST    R3,NEEDADD                                                       
         BAS   RE,PUTNEED                                                       
         SPACE 1                                                                
GIF12    LA    R3,4(R3)                                                         
         BCT   R0,GIF2                                                          
         MVI   ERRNUM,1            TOO MANY CONDITIONS                          
         BAS   RE,ERROR                                                         
         B     XIT                                                              
         SPACE 1                                                                
GIFEND   L     R3,ATHISIF                                                       
         GOTO1 =V(DRIVTEST),DMCB,(RA),(R3)                                      
         BE    YES                                                              
         B     NO                                                               
         EJECT                                                                  
*              ROUTINE TO GET SUBSIDIARY ELEMENTS FROM PROGRAM                  
         SPACE 3                                                                
GETSUB   NTR1                                                                   
*                                  IT IS ASSUMED THAT R2 HAS ADDRESS            
*                                  OF MAIN ELEMENT                              
         XC    ASUBEL,ASUBEL                                                    
         B     GSUB2                                                            
         SPACE 1                                                                
NEXTSUB  NTR1                                                                   
         L     R2,ASUBEL                                                        
         XC    ASUBEL,ASUBEL                                                    
         SPACE 1                                                                
GSUB2    ZIC   R1,1(R2)                                                         
         LTR   R1,R1                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R2,R1                                                            
         CLI   0(R2),0             CHECK FOR END OF PROGRAM                     
         BE    NO                                                               
         CLI   0(R2),X'FF'                                                      
         BE    NO                                                               
         LA    R1,MAINLIST                                                      
         SPACE 1                                                                
GSUB4    CLC   0(1,R1),0(R2)       OR FOR A MAIN ELEMENT                        
         BE    NO                                                               
         LA    R1,4(R1)                                                         
         CLI   0(R1),X'FF'                                                      
         BNE   GSUB4                                                            
         SPACE 1                                                                
         CLI   ELCODE,0            CHECK ELEMENT CODE IF NEEDED                 
         BE    GSUB6                                                            
         CLC   ELCODE,0(R2)                                                     
         BNE   GSUB2                                                            
         SPACE 1                                                                
GSUB6    ST    R2,ASUBEL                                                        
         B     YES                                                              
         EJECT                                                                  
*              FINAL INITIALIZATION ROUTINES                                    
         SPACE 3                                                                
FINALS   NTR1                                                                   
         CLI   TRACOPT,C'Y'                                                     
         BNE   XIT                                                              
         L     R2,GLADTAB          FIRST WE WILL TRACE DRIVE TABLE              
         SPACE 1                                                                
FIN2     CLI   0(R2),0                                                          
         BE    FIN10                                                            
         LA    R1,DRIVLIST         LOOK UP FOR NAME OF ELEMENT                  
         SPACE 1                                                                
FIN4     CLC   0(1,R1),0(R2)                                                    
         BE    FIN6                                                             
         CLI   0(R1),X'FF'                                                      
         BE    FIN6                                                             
         LA    R1,8(R1)                                                         
         B     FIN4                                                             
         SPACE 1                                                                
FIN6     MVC   P(7),1(R1)          SHOW TYPE OF ELEMENT                         
         ST    R2,DUB              AND ADDRESS                                  
         MVC   P+10(8),=C'ADDRESS='                                             
         GOTO1 HEXOUT,DMCB,DUB,P+18,4,=C'TOG'                                   
         BAS   RE,PRINTOUT                                                      
         ZIC   R3,1(R2)                                                         
         BAS   RE,TRASOME                                                       
         AR    R2,R3                                                            
         B     FIN2                                                             
         SPACE 1                                                                
FIN10    LA    R4,GLAINTD          THEN SHOW GLINTS                             
         LA    R6,12                                                            
         LA    R5,1                                                             
         SPACE 1                                                                
FIN12    L     R2,0(R4)                                                         
         LTR   R2,R2                                                            
         BZ    FIN14                                                            
         MVC   P(17),=C'DETAILS OF RECORD'                                      
         EDIT  (R5),(2,P+17)                                                    
         BAS   RE,PRINTOUT                                                      
         LA    R3,L'GLINTDET                                                    
         BAS   RE,TRASOME                                                       
         SPACE 1                                                                
FIN14    LA    R4,4(R4)                                                         
         LA    R5,1(R5)                                                         
         BCT   R6,FIN12                                                         
         B     XIT                                                              
         SPACE 1                                                                
DRIVLIST DC    X'10',C'RECORD '                                                 
         DC    X'12',C'END KEY'                                                 
         DC    X'20',C'INPUT  '                                                 
         DC    X'30',C'OUTPUT '                                                 
         DC    X'40',C'HEAD   '                                                 
         DC    X'42',C'CHUNK  '                                                 
         DC    X'44',C'F/LAST '                                                 
         DC    X'48',C'TOTAL  '                                                 
         DC    X'50',C'COMPUTE'                                                 
         DC    X'60',C'COND.  '                                                 
         DC    X'FF',C'OTHERS '                                                 
         EJECT                                                                  
*              ERROR ROUTINES                                                   
         SPACE 3                                                                
FATAL1   DC    H'0'                                                             
         DC    C'NO WORKING STORAGE PROVIDED'                                   
         SPACE 1                                                                
FATAL2   DC    H'0'                                                             
         DC    C'DONT RECOGNIZE WORK TYPE'                                      
         SPACE 1                                                                
ERROR    NTR1                                                                   
         LA    R1,ERRLIST                                                       
         SPACE 1                                                                
ERROR2   CLC   ERRNUM,0(R1)        LOOK UP FOR ERROR                            
         BE    ERROR4                                                           
         ZIC   R0,1(R1)                                                         
         AR    R1,R0                                                            
         CLI   0(R1),X'FF'                                                      
         BNE   ERROR2                                                           
         DC    H'0'                                                             
         SPACE 1                                                                
ERROR4   ZIC   RE,1(R1)            PICK IT OFF                                  
         SH    RE,=H'3'                                                         
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   P(0),2(R1)                                                       
         BAS   RE,PRINTERR                                                      
         BAS   RE,PRINTOUT                                                      
         BAS   RE,TRAMAIN                                                       
         B     XIT                                                              
         SPACE 1                                                                
ERRLIST  DS    0H                                                               
         DC    AL1(1,24),CL22'MORE THAN 4 CONDITIONS'                           
         DC    AL1(2,22),CL20'SET NEEDS AN OPERAND'                             
         DC    AL1(3,18),CL16'UNSET BEFORE SET'                                 
         DC    AL1(4,14),CL12'MISSING TYPE'                                     
         DC    AL1(5,16),CL14'MISSING LENGTH'                                   
         DC    AL1(6,17),CL15'NO OUT FOR COMP'                                  
         DC    AL1(7,19),CL17'PRINT NOT H/M/P/T'                                
         DC    X'FF'                                                            
         EJECT                                                                  
*              HOOK TO APPLICATION OR SYSTEM DRIVER                             
         SPACE 3                                                                
GOHOOK   NTR1                                                                   
         CLI   GLAROUT,2           FIRST BYTE INDICATES WHERE                   
*                                  1=APPLICATION 2=SYSDRIVER                    
*                                  3=DRIVER ITSELF (DRIVROUT)                   
         BE    GOH2                                                             
         BH    GOH3                                                             
         CLI   GLAROUT,0                                                        
         BE    XIT                                                              
         L     RF,GLAHOOK          PICK UP APPLICATION HOOK                     
         LTR   RF,RF                                                            
         BZ    XIT                                                              
         L     RE,USERRD           USERS RD                                     
         LM    R0,RC,20(RE)        RESTORE USERS R0-RC                          
         BASR  RE,RF               RF CONTAINED A(HOOK ROUTINE)                 
         XIT1                                                                   
         SPACE 1                                                                
GOH2     L     RF,GLASYSDR                                                      
         LTR   RF,RF                                                            
         BZ    XIT                                                              
         GOTO1 GLASYSDR,DMCB,(RA)                                               
         B     XIT                                                              
         SPACE 1                                                                
GOH3     GOTO1 =V(DRIVROUT),DMCB,(RA)                                           
         B     XIT                                                              
         SPACE 1                                                                
         EJECT                                                                  
*              TRACE AND PRINTING FACILITIES                                    
         SPACE 3                                                                
TRAMAIN  NTR1                                                                   
         LR    R2,R3               TRACE DRIVETABLE ELEMENT                     
         BAS   RE,TRAEL                                                         
         L     R2,AMAINEL          TRACE MAIN ELEMENT                           
         SPACE 1                                                                
TRAMAIN2 BAS   RE,TRAEL            AND ANY SUBSIDIARY                           
         ZIC   R1,1(R2)                                                         
         AR    R2,R1                                                            
         CLI   0(R2),0             (END OF PROGRAM)                             
         BE    XIT                                                              
         LA    R1,DRIVLIST                                                      
         SPACE 1                                                                
TRAMAIN4 CLI   0(R1),X'FF'                                                      
         BE    TRAMAIN2                                                         
         CLC   0(1,R1),0(R2)       UNTIL NEXT MAIN ELEMENT                      
         BE    XIT                                                              
         LA    R1,8(R1)                                                         
         B     TRAMAIN4                                                         
         SPACE 1                                                                
TRAEL    NTR1                                                                   
         B     TRAALL                                                           
         SPACE 1                                                                
TRASOME  NTR1                                                                   
         CLI   TRACOPT,C'Y'        SELECTIVE - R2=A(ELEMENT)                    
*                                              R3=LENGTH                        
         BE    TRAALL2                                                          
         B     XIT                                                              
         SPACE 1                                                                
TRAALL   ZIC   R3,1(R2)            R3=L'ELEMENT                                 
         SPACE 1                                                                
TRAALL2  BCTR  R3,0                -1                                           
         EX    R3,TRAM1                                                         
         BAS   RE,PRINTOUT                                                      
         GOTO1 HEXOUT,DMCB,(R2),P2,132,=C'SEP'                                  
         EX    R3,TRAM2                                                         
         BAS   RE,PRINTOUT                                                      
         EX    R3,TRAM3                                                         
         BAS   RE,PRINTOUT                                                      
         BAS   RE,PRINTOUT                                                      
         B     XIT                                                              
         SPACE 1                                                                
TRAM1    MVC   P(0),0(R2)                                                       
TRAM2    MVC   P(0),P2                                                          
TRAM3    MVC   P(0),P3                                                          
         SPACE 1                                                                
PRINTOUT NTR1                                                                   
         B     PRINTALL                                                         
         SPACE 1                                                                
PRINTERR NTR1                                                                   
         MVI   GLANYERR,C'Y'                                                    
         SPACE 1                                                                
PRINTALL GOTO1 PRINT,DMCB,PFILL,=C'BL01'                                        
         MVC   P,SPACES                                                         
         B     XIT                                                              
         EJECT                                                                  
*              TABLE OF MAIN ELEMENTS AND DRIVER DEFAULTS                       
         SPACE 3                                                                
MAINLIST DS    0H                                                               
         DC    X'10',AL3(REC)                                                   
         DC    X'12',AL3(DATA)                                                  
         DC    X'14',AL3(SET)                                                   
         DC    X'20',AL3(IN)                                                    
         DC    X'30',AL3(OUT)                                                   
         DC    X'40',AL3(HEAD)                                                  
         DC    X'42',AL3(CHUNK)                                                 
         DC    X'44',AL3(FIRST)                                                 
         DC    X'48',AL3(TOTAL)                                                 
         DC    X'50',AL3(COMP)                                                  
         DC    X'60',AL3(CON)                                                   
         DC    X'FF'                                                            
         SPACE 1                                                                
DDEFAREA DS    0H                  DEFAULT VALUES                               
         DC    X'2204',C'B+'       INPUT TYPE BINARY ADD                        
         DC    X'230304'           INPUT LENGTH 4                               
*******  DC    X'3204',C'N',X'00'  OUTPUT TYPE NUMERIC                          
*******  DC    X'330308'           OUTPUT LENGTH 8                              
         DC    X'FF'                                                            
         SPACE 1                                                                
LENCON   DC    H'2'                                                             
         SPACE 1                                                                
NEEDWIDE DC    H'20'                                                            
         EJECT                                                                  
*              LTORG AND BUFFERS                                                
         SPACE 3                                                                
         LTORG                                                                  
         SPACE 1                                                                
MYPRINT  DS    0D                                                               
         DC    40CL198' '                                                       
         EJECT                                                                  
*              DSECT FOR INITIALIZATION                                         
         SPACE 3                                                                
INITD    DSECT                                                                  
         SPACE 1                                                                
ASUBEL   DS    A                                                                
AMAINEL  DS    A                                                                
ADRIVEL  DS    A                                                                
ADICTENT DS    A                                                                
ALASTIN  DS    A                                                                
ALASTOUT DS    A                                                                
ALASTREC DS    A                                                                
ATHISIF  DS    A                                                                
INDISP   DS    H                                                                
RESTYPE  DS    CL1                                                              
LENTYPE  DS    CL1                                                              
RELHEAD  DS    AL1                                                              
THISHLIN DS    AL1                                                              
PUTTYPE  DS    CL1                                                              
SUBWHERE DS    CL1                                                              
KEYORDAT DS    CL1                                                              
LASTILEV DS    CL1                                                              
LASTOLEV DS    CL1                                                              
SOFTLINE DS    XL1                                                              
SOFTCOL  DS    XL1                                                              
SOFTMID  DS    XL1                                                              
WLASTOUT DS    XL1                                                              
MARKCOL  DS    XL1                                                              
SOFTHLIN DS    XL1                                                              
RECYN    DS    CL1                                                              
INYN     DS    CL1                                                              
INOROUT  DS    CL1                                                              
OUTYN    DS    CL1                                                              
FILATOT  DS    CL1                                                              
         DS    0C                                                               
RANKFND  DS    CL1                                                              
SETAREA  DS    CL255                                                            
PFILL    DS    CL1                                                              
P        DS    CL133                                                            
P2       DS    CL132                                                            
P3       DS    CL132                                                            
SPACES   DS    CL198                                                            
ELCODE   DS    CL1                                                              
ERRNUM   DS    CL1                                                              
NEEDADD  DS    A                                                                
NEEDLABL DS    CL8                                                              
NEEDREC  DS    XL1                                                              
DICNAME  DS    CL8                                                              
ENTNAME  DS    CL8                                                              
ANYFOLD  DS    CL1                                                              
INPUTRTN DS    CL1                                                              
         SPACE 1                                                                
DMWORK   DS    12D                                                              
KEY      DS    CL48                                                             
IO       DS    2000C                                                            
         EJECT                                                                  
       ++INCLUDE DRGLOBAL                                                       
         EJECT                                                                  
       ++INCLUDE DRLOCAL                                                        
         EJECT                                                                  
       ++INCLUDE DRINTRECD2                                                     
         EJECT                                                                  
       ++INCLUDE DRIVADDSD                                                      
         EJECT                                                                  
       ++INCLUDE CTGENDIC                                                       
         EJECT                                                                  
       ++INCLUDE DRIVETABLE                                                     
         EJECT                                                                  
       ++INCLUDE DDBIGBOX                                                       
         EJECT                                                                  
       ++INCLUDE DDWIDED                                                        
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003DRIVINTS  05/01/02'                                      
         END                                                                    
