*          DATA SET DDEIOSQL   AT LEVEL 005 AS OF 06/07/19                      
*CATALP EIOSQL                                                                  
         TITLE 'EIOSQL - ESS IO SQL EXTRACT SUB-SYSTEM PROCESSOR'               
**********************************************************************          
* EIOSQL -                                                           *          
*   ESS IO SQL EXTRACT SUB SYSTEM INTERFACE                          *          
* PARAMETERS:                                                        *          
*           P1 = A(ESSATC - ESSIO ATTACHED TASK CONTROL BLOCK)       *          
**********************************************************************          
         PRINT NOGEN                                                            
EIOSQL   CSECT                                                                  
         NMOD1 WORKX-WORKD,*EIOSQL*,CLEAR=YES,RA,R9,R8,RR=RE                    
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RE,RELO                                                          
         ST    RD,SAVERD                                                        
         MVC   PARM,0(R1)          SAVE INPUT PARAMETER LIST                    
         L     RF,0(R1)                                                         
         USING ESSATCD,RF          EXTRACT VALUES FORM ESSATCD                  
         ST    RF,AESSATCB         SAVE ADDRESS ATCB                            
         L     R2,EATDATA          R2=A(GENERAL MESSAGE DATA SAVE AREA)         
         USING ESSDATAD,R2                                                      
         LA    R3,ESSDBUFF         R3=A(SQL MESSAGE DATA AREA)                  
         USING ESQLDAT,R3                                                       
         L     R4,EATWORK                                                       
         USING EIOSQLD,R4          R4=A(SQL MESSAGE DATA SAVE AREA)             
         L     R5,EATFACS                                                       
         USING ESSFACSD,R5         R5=A(COMMON FACILITIES)                      
         MVC   ESSNAME,EATNAME     SAVE ESS ID NAME                             
         MVC   EXFDDNM,ESSNAME                                                  
         MVC   ESSNAME(3),=CL3'ESS'                                             
         MVC   ESSIDNUM,EATENUM    SAVE ESS ID NUMBER                           
         MVC   AEXFDCB,EATBLDCB                                                 
* ??     MVC   EXFDDNM(2),=CL2'BL'                                              
         ICM   RE,15,EATESSB                                                    
         USING ESSBD,RE                                                         
*                                                                               
         MVI   FTPFLAG,C'N'                                                     
         TM    ESSBSTA1,ESSBSFTP   FTP=YES FOR THIS HOSTESS?                    
         BZ    SETFTPX             NO                                           
         TM    EATHFLG1,GESSFNFQ   FTP ENABLED FOR ESS ID?                      
         BZ    SETFTPX             YES                                          
         MVI   FTPFLAG,C'Y'                                                     
SETFTPX  EQU   *                                                                
*                                                                               
         MVI   RECOMFLG,C'N'                                                    
         TM    EATHFLG1,GESSFRCQ                                                
         BNZ   *+12                                                             
         TM    ESSBSTA1,ESSBSRCO                                                
         BZ    *+8                                                              
         MVI   RECOMFLG,C'Y'       SAVE RECOMMIT FLAG                           
         MVI   SIMFLG,C'N'                                                      
         TM    EATHFLG2,GESSFSIQ                                                
         BZ    *+8                                                              
         MVI   SIMFLG,C'Y'         SAVE SEND IMMEDIATE FLAG                     
*                                                                               
         MVC   P,SPACES                                                         
         MVC   WTOMSG,SPACES                                                    
         MVC   WTOMSG(8),ESSBJNAM                                               
         MVI   WTOMSG+8,C'.'                                                    
         MVC   WTOMSG+9(8),EATNAME                                              
         MVI   WTOMSG+17,C'.'                                                   
*                                                                               
         MVC   ERRMSG,=CL80'UNDEFINED MESSAGE'                                  
*                                                                               
         MVC   AESSESSD,EATAESS                                                 
         ICM   R6,15,EATAESS                                                    
         BZ    ESQL010                                                          
         DROP  RE,RF                                                            
*                                                                               
         USING ESSESSD,R6                                                       
         MVC   WTOMSG+18(8),ESEELU                                              
         MVC   FTPSIZE,ESEFTPS                                                  
         MVC   SESSNUM,ESESNUM                                                  
         DROP  R6                                                               
*                                                                               
ESQL010  EQU   *                                                                
*                                                                               
*YYUN, 1/24/2013, NOT REALLY NEEDED                                             
*        MVC   P(8),=C'FTPFLAG='                                                
*        MVC   P+9(1),FTPFLAG                                                   
*        MVC   P+19(6),=C'EIOSQL'                                               
*        GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
*                                                                               
         L     R7,AMAJORNM         MAJOR RESCOURCE NAME                         
         ENQ   ((7),EQESSIO,E,8)   NQUEUE ESSIO                                 
         LA    RF,20                                                            
         STCM  RF,15,LOOPCNT                                                    
*                                                                               
         CLI   ESSDMODE,ESSDMRCQ   TEST ESSIO MESSAGE MODE CODE                 
         BE    PROCRCV             PROCESS RECEIVE MODE                         
         CLI   ESSDMODE,ESSDMSNQ                                                
         BE    PROCSND             PROCESS SEND MODE                            
         BAS   RE,DCH0                                                          
*                                                                               
ESQLOK   EQU   *                   RETURN OK                                    
         MVI   ESSDRETC,ESSDROKQ                                                
         B     EXIT                                                             
*                                                                               
ESQLNF   EQU   *                   RETURN NO SQL FILES FOR TRANSFER             
         MVI   ESSDRETC,ESSDRNFQ                                                
         B     EXIT                                                             
*                                                                               
EREX0001 EQU   *                   ERROR FROM EXTRACT SYSTEM PROCESS            
         MVC   ESSDERRC,=AL4(ERNSQLEX)  (SEE DDEXSERVD)                         
         CLI   BYTE,EXSERT1Q       TEST IF WARNING ERRORS                       
         BE    PSNDFST             RETURN TO PROCESS FILES                      
         CLI   BYTE,EXSERO1Q                                                    
         BE    PSNDFST             RETURN TO PROCESS FILES                      
         CLI   BYTE,EXSECO2Q                                                    
         BE    ESQLWARN                                                         
         CLI   BYTE,EXSERC1Q       RECEIVE MESSAGE ERROR                        
         BE    ESQLW010            IGNORE ANY RECEIVE ERROR FOR NOW             
         CLI   BYTE,EXSEUPXQ+X'01'                                              
         BE    ESQLWARN                                                         
         B     ESQLERR             ELSE SERIOUS ERROR                           
EREX0002 EQU   *                   INVALID MESSAGE RECEIVED FROM ESS            
         MVC   P(40),=CL40'INVALID SQL MESSAGE RECEIVED'                        
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         MVC   ESSDERRC,=AL4(ERNSQLMV)                                          
         B     ESQLERR                                                          
EREX0003 EQU   *                   INVALID COMMIT MESSAGE RECEIVED              
         MVC   P(40),=CL40'INVALID COMMIT MESSAGE RECEIVED'                     
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         MVC   ESSDERRC,=AL4(ERNSQLMV)                                          
         B     ESQLERR                                                          
EREX0004 EQU   *                   INVALID MESSAGE AFTER SEND REQUEST           
         MVC   P(40),=CL40'INVALID MESSAGE RETURNED TO SENDER'                  
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         MVC   ESSDERRC,=AL4(ERNSQLMV)                                          
         B     ESQLERR                                                          
EREX0005 EQU   *                   GENERAL PROGRAM LOGIC EXCEPTION              
         MVC   ESSDERRC,=AL4(ERNSQLPE)                                          
         B     ESQLERR                                                          
EREX0006 EQU   *                   ERROR READING EXTRACT MVS DATA SET           
         MVC   ESSDERRC,=AL4(ERNSQLRX)                                          
         B     ESQLERR                                                          
EREX0007 EQU   *                   ERROR CONDITION CODE RETURNED                
         MVC   ESSDERRC,=AL4(ERNSQLNM)  IN NOTIFICATION MESSAGE                 
         B     ESQLERR                                                          
EREX0008 EQU   *                                                                
         MVC   ERRMSG,=CL80'SQL SUBSYSTEM FILE SEND ERROR COUNT MAX'            
         EDIT  ESSDERRC,(4,ERRMSG+40),ZERO=NOBLANK,FILL=0                       
         MVC   P(L'ERRMSG),ERRMSG                                               
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         MVC   ESSDERRC,=AL4(ERNSQLSL)                                          
         B     ESQLERR                                                          
EREX0009 EQU   *                   INVALID MESSAGE BEFORE SEND REQUEST          
         MVC   P(40),=CL40'INVALID MESSAGE RETURNED TO SENDER'                  
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         MVC   ESSDERRC,=AL4(ERNSQLMV)                                          
         B     ESQLERR                                                          
EREX0010 EQU   *                   ERROR BULK DOWNLOAD RECORD COUNT             
         MVC   ESSDERRC,=AL4(ERNSQLRC)                                          
         B     ESQLERR                                                          
EREX0011 EQU   *                   ERROR OPENING EXTRACT MVS DATA SET           
         MVC   ESSDERRC,=AL4(ERNSQLOX)                                          
         B     ESQLERR                                                          
*                                                                               
ESQLWARN EQU   *                   RETURN WARNING ERROR CODE                    
         MVC   P(11),=CL11'<WARN ????>'                                         
         EDIT  ESSDERRC,(4,P+6),ZERO=NOBLANK,FILL=0                             
         MVC   P+12(L'ERRMSG),ERRMSG                                            
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         ICM   RE,15,AESSESSD                                                   
         BZ    ESQLW010                                                         
         USING ESSESSD,RE                                                       
         ICM   RF,15,ESESWCNT                                                   
         LA    RF,1(RF)                                                         
         STCM  RF,15,ESESWCNT                                                   
         CLC   ESESWCNT,=AL4(4)                                                 
         BNH   ESQLW010                                                         
         XC    ESESWCNT,ESESWCNT                                                
         B     ESQLERR                                                          
         DROP  RE                                                               
ESQLW010 MVI   ESSDRETC,ESSDROKQ                                                
         B     EXIT                                                             
*                                                                               
ESQLERR  EQU   *                   RETURN SERIOUS ERROR CODE                    
         MVC   P(11),=CL11'<ERROR????>'                                         
         EDIT  ESSDERRC,(4,P+6),ZERO=NOBLANK,FILL=0                             
         MVC   P+12(L'ERRMSG),ERRMSG                                            
         MVC   WTOMSGD(L'WTOMSGD),P                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         GOTO1 AESSWTO,DMCB,X'FE000001',(L'WTOMSG,WTOMSG)                       
         MVI   ESSDRETC,ESSDRERQ                                                
         B     EXIT                                                             
*                                                                               
EXIT     EQU   *                                                                
         L     R7,AMAJORNM         MAJOR RESCOURCE NAME                         
         DEQ   ((7),EQESSIO,8)     DEQUEUE ESSIO PROCESS                        
*                                                                               
         L     RD,SAVERD                                                        
         L     R1,APARM            EXIT WITH UPDATED PARAMETER LIST             
         MVC   0(L'PARM,R1),PARM                                                
         CLI   ESSDRETC,ESSDRERQ   SET CC .NE. IF RETURN CODE ERROR             
         BE    EXITERR                                                          
         B     EXITOK                                                           
EXITOK   SR    RC,RC                                                            
EXITERR  LTR   RC,RC                                                            
         XMOD1                                                                  
         EJECT                                                                  
***********************************************************************         
* PROCESS SQL SUB-SYSTEM REQUEST RECEIVED FROM ESS                    *         
***********************************************************************         
PROCRCV  EQU   *                                                                
         BAS   RE,VALSQLM          VALIDATE SQL SUB-SYSTEM MESSAGE              
         BNE   EREX0002                                                         
         CLC   EIOSMID,=AL4(ESQLCOMQ)                                           
         BNE   PRCV010                                                          
         BAS   RE,VALCOMM          VALIDATE COMMIT MESSAGE PARAMETERS           
         BNE   EREX0003                                                         
*                                                                               
PRCV010  EQU   *                   TEST IF INFO MESSAGE TYPE                    
         MVI   ESSDHTYP,0                                                       
         CLI   ESSDMTYP,ESSHINFQ   TEST IF INFO MESSAGE TYPE                    
         BE    PRCVINF                                                          
         CLI   ESSDMTYP,ESSHERRQ   TEST IF ERROR MESSAGE TYPE                   
         BE    PROCERR                                                          
         BAS   RE,DCH0                                                          
*                                                                               
PRCVINF  EQU   *                   PROCESS INFO MESSAGE TYPE                    
         CLC   EIOSMID,=AL4(ESQLCOMQ)                                           
         BE    PCOMMIT             PROCESS COMMIT FILE MESSAGE                  
         CLC   EIOSMID,=AL4(ESQLRCVQ)                                           
         BE    PRECEIVE            PROCESS RECEIVED FILE MESSAGE                
         BAS   RE,DCH0                                                          
         EJECT                                                                  
***********************************************************************         
* PROCESS SQL SUB-SYSTEM SEND REQUEST MESSAGE                         *         
***********************************************************************         
PROCSND  EQU   *                                                                
*                                                                               
         CLI   ESSDSEQN,ESSDSFSQ   TEST MESSAGE FIRST IN SEQUENCE               
         BE    PSNDFST             GET FIRST SQL MESSAGE TO SEND                
         CLI   ESSDSEQN,ESSDSNXQ   TEST MESSAGE NEXT IN SEQUENCE                
         BE    PSNDNXT             GET NEXT SQL MESSAGE IN SEQUENCE             
         CLI   ESSDSEQN,ESSDSLSQ   TEST AFTER MESSAGE LAST IN SEQUENCE          
         BE    PSNDLST             PROCESS RESULT OF SQL MESSAGE SEND           
         BAS   RE,DCH0                                                          
***********************************************************************         
* PROCESS FIRST MESSAGE IN SEND REQUEST SEQUENCE                      *         
* SEARCH SQL EXTRACT TRANSFER CONTROL TABLE TO BUILD SEND REQUEST     *         
***********************************************************************         
PSNDFST  EQU   *                                                                
         ICM   RF,15,LOOPCNT                                                    
         BCT   RF,*+8                                                           
         B     EREX0008                                                         
         STCM  RF,15,LOOPCNT                                                    
         MVI   ESSDMTYP,ESSHINFQ   INTIALISE MESSAGE HEADER PARAMETERS          
         MVC   ESSDMID,=AL3(ESSHSQLQ)                                           
         OI    ESSDMFF,ESSHLSTQ                                                 
         MVI   ESSDHTYP,ESSHDATQ                                                
*                                                                               
         LA    R6,WORK             BUILD EXSERV PARAMETER BLOCK                 
         XC    WORK,WORK                                                        
         USING EXSERVD,R6                                                       
         MVC   EXSEID,ESSNAME                                                   
         MVC   EXSSNUM,SESSNUM                                                  
*                                  RETURN NEXT FILE READY TO SEND               
PSNF010  MVC   EXSACTN,=AL4(EXSARTSQ)                                           
         MVC   EXSSIM,SIMFLG       SAVE SEND IMMEDIATE FLAG                     
         GOTO1 AEXSERV,DMCB,WORK,BYTE,0                                         
         CLI   BYTE,EXSROKQ        CHECK RETURN CODE ENTRY FOUND                
         BE    PSNFNOT                                                          
         CLI   BYTE,EXSRNOQ        CHECK RETURN CODE ENTRY NOT FOUND            
         BE    PSNF020                                                          
         BAS   RE,EXSERROR         ELSE EXIT WITH EXSERV ERROR                  
*                                  RETURN NEXT FILE RECOMMIT MESSAGE            
PSNF020  MVC   EXSACTN,=AL4(EXSARCOQ)                                           
         GOTO1 AEXSERV,DMCB,WORK,BYTE,0                                         
         CLI   BYTE,EXSROKQ        CHECK RETURN CODE ENTRY FOUND                
         BE    PSNFRCO                                                          
         CLI   BYTE,EXSRNOQ        CHECK RETURN CODE ENTRY NOT FOUND            
         BE    ESQLNF                                                           
         BAS   RE,EXSERROR         ELSE EXIT WITH EXSERV ERROR                  
*                                                                               
PSNFNOT  CLI   FTPFLAG,C'N'                                                     
         BE    PSFN010                                                          
         ZAP   DUBG,EXSBNM         NUMBER OF BYTES OF FILE                      
         CVBG  GR1,DUBG            CONVERT TO BINARY TO COMPARE                 
         CLM   R1,15,FTPSIZE       FTPSIZE IS MINIMUM SIZE                      
         BNL   *+8        <----AH3 THIS BRANCH DON'T MAKE SENSE TO ME           
PSFN010  OI    ESSDMFF,ESSHBDLQ                                                 
         MVC   ESSDRNUM,EXSRNM     SAVE FILE RECORD NUMBER                      
         ZAP   ESSDRCNT,=PL8'0'    ZERO FILE RECORD COUNT                       
         ZAP   ESSDCHKP,=PL8'0'    ZERO FILE RECORD RESTART CHECKPOINT          
*                                                                               
         MVC   EIOSMID,=AL4(ESQLNOTQ)                                           
         MVC   EIOSCON,=CL6'000000'                                             
         MVC   EIOSMAID,EXSAGY                                                  
         MVC   EIOSMSYS,EXSSYS                                                  
         BAS   RE,GETSYSC                                                       
         MVC   EIOSMSUB,EXSSUB                                                  
         BAS   RE,GETSUBC                                                       
         MVC   EIOSMFNM,EXSFNUM                                                 
         MVC   EIOSLOAD,EXSLOAD                                                 
         CLI   FTPFLAG,C'Y'        DON'T SET LOAD CONTROL WHEN IT'S ON          
         BNE   *+8                                                              
         MVI   EIOSLOAD,C'3'                                                    
*                                                                               
         MVC   EIOSMDSN,EXSDSN                                                  
         MVC   EIOSMRNM,EXSRNM                                                  
         MVC   EIOSMBNM,EXSBNM                                                  
         MVC   EIOSMMTO,EXSMTO                                                  
         MVC   EIOSMGNM,EXSGNUM                                                 
         BAS   RE,BLDSQLM          BUILD SQL SUB-SYSTEM MESSAGE                 
         MVC   P,SPACES                                                         
         MVC   P(24),=CL24'SQL FILE READY FOR BDN: '                            
         CLI   FTPFLAG,C'Y'                                                     
         BNE   *+10                                                             
         MVC   P(24),=CL24'SQL FILE READY FOR FTP: '                            
*                                                                               
         BAS   RE,FORMOPM                                                       
         MVC   WTOMSGD(L'WTOMSGD),P                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         GOTO1 AESSWTO,DMCB,X'FE000001',(L'WTOMSG,WTOMSG)                       
         B     ESQLOK                                                           
*                                                                               
PSNFRCO  MVC   EIOSMID,=AL4(ESQLRCOQ)                                           
         MVC   EIOSCON,=CL6'000000'                                             
         MVC   EIOSMAID,EXSAGY                                                  
         MVC   EIOSMSYS,EXSSYS                                                  
         BAS   RE,GETSYSC                                                       
         MVC   EIOSMSUB,EXSSUB                                                  
         BAS   RE,GETSUBC                                                       
         MVC   EIOSMFNM,EXSFNUM                                                 
         MVC   EIOSMDSN,EXSDSN                                                  
         MVC   EIOSMRNM,EXSRNM                                                  
         MVC   EIOSMBNM,EXSBNM                                                  
         MVC   EIOSMMTO,EXSMTO                                                  
         MVC   EIOSMGNM,EXSGNUM                                                 
         CLI   FTPFLAG,C'Y'        DON'T SET LOAD CONTROL WHEN IT'S ON          
         BNE   *+8                                                              
         MVI   EIOSLOAD,C'3'                                                    
         BAS   RE,BLDSQLM          BUILD SQL SUB-SYSTEM MESSAGE                 
         B     ESQLOK                                                           
         DROP  R6                                                               
***********************************************************************         
* PROCESS NEXT MESSAGE IN SEND REQUEST SEQUENCE                       *         
***********************************************************************         
PSNDNXT  EQU   *                                                                
         CLI   FTPFLAG,C'Y'        NFS TRANSFER?                                
         BE    ESQLOK              YES - EXIT                                   
*                                                                               
         CLI   ESSDBLKD,C'Y'       TEST FOR BULK DATA DOWNLOAD                  
         BE    *+8                                                              
         BAS   RE,DCH0                                                          
         CLI   ESSDBLKO,C'Y'       TEST BULK DATA FILE OPENED                   
         BE    PBLKDNL                                                          
*                                                                               
         BAS   RE,VALSQLM          VALIDATE SQL SUB-SYSTEM MESSAGE              
         BNE   EREX0009                                                         
         MVC   P(40),=CL40'SQL SUBSYSTEM BULK DOWNLOAD VALID TO GO'             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
*                                                                               
         CLI   ESSDMTYP,ESSHINFQ   TEST IF INFO MESSAGE TYPE                    
         BE    PSNXINF                                                          
         CLI   ESSDMTYP,ESSHERRQ   TEST IF ERROR MESSAGE TYPE                   
         BE    PSNXERR                                                          
         BAS   RE,DCH0                                                          
*                                                                               
PSNXERR  EQU   *                   PROCESS ERROR MESSAGE TYPE                   
         MVC   ESSDCON,EIOSCON                                                  
         MVC   P(34),=CL34'SQL SUBSYSTEM ERROR MESSAGE CODE: '                  
         MVC   P+34(6),ESSDCON                                                  
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     ESQLOK                                                           
*                                                                               
PSNXINF  EQU   *                   PROCESS INFO MESSAGE TYPE                    
         CLC   EIOSMID,=AL4(ESQLNOTQ)                                           
         BE    PBLKDNL             CONTINUE WITH NORMAL BULKDOWNLOAD            
         CLC   EIOSMID,=AL4(ESQLCHKQ)                                           
         BE    PSNXCHK             PROCESS CHECKPOINT MESSAGE                   
         BAS   RE,DCH0                                                          
*                                                                               
*                                  CHECK BULK DOWNLOAD OK                       
PSNXCHK  EQU   *                                                                
*                                                                               
         BAS   RE,VALCHKP          VALIDATE CHECKPOINT FIELD                    
         BNE   EREX0009                                                         
         MVC   ESSDCHKP,EIOSCHKP                                                
         MVC   P(44),=CL44'CHECKPOINT RECORD NUMBER: '                          
         EDIT  ESSDCHKP,(8,P+26),ZERO=NOBLANK,FILL=0                            
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     PBLKDNL                                                          
***********************************************************************         
* PROCESS BULK DATA DOWNLOAD OF SQL EXTRACT FILE                      *         
***********************************************************************         
PBLKDNL  EQU   *                                                                
         XC    ESSDDLN,ESSDDLN                                                  
         NI    ESSDMFF,X'FF'-ESSHLSTQ                                           
         CLI   ESSDBLKO,C'Y'       TEST BULK DATA FILE OPENED                   
         BE    PBLK100                                                          
         MVI   ESSDBLKO,C'Y'       SET BULK DATA FILE OPENED                    
*                                  DYNAMICALLY ALLOCATE EXTRACT FILE            
PBLK010  EQU   *                                                                
         MVC   TXTDSN+6(44),EIOSMDSN                                            
         MVC   TXTDD+6(8),EXFDDNM                                               
         MVI   TXTDD+5,L'EXFDDNM                                                
         ICM   R6,15,AEXFDCB       RF=A(DCB) BLFILE                             
* ??     MVC   DCBDDNAM-IHADCB(8,R6),EXFDDNM                                    
         MVC   P(20),=CL20'CLOSE DCB:'                                          
         MVC   P+20(8),EXFDDNM                                                  
         MVC   P+30(44),EIOSMDSN                                                
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         CLOSE ((R6))              MAKE SURE LAST EXTRACT FILE CLOSED           
         FREEPOOL (R6)                                                          
         MVC   TXTUNDD+6(8),EXFDDNM                                             
         MVI   TXTUNDD+5,8                                                      
         MVC   TXTDSN+6(44),EIOSMDSN                                            
         LA    R1,ARBLKUN2                                                      
         DYNALLOC                                                               
*                                  TEST TRACE MESSAGE                           
         MVC   P,SPACES                                                         
         MVC   P(20),=CL20'CLEARED DCB:'                                        
         MVC   P+20(8),EXFDDNM                                                  
         MVC   P+30(44),EIOSMDSN                                                
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
*                                                                               
         LA    R1,ARBLKEXA                                                      
         DYNALLOC                                                               
         LTR   RF,RF                                                            
         BZ    PBLK040             DATASET WAS ALLOCATED OK                     
*                                                                               
         MVC   P+11(13),=C'<<< ERROR >>>'                                       
         MVC   P+30(22),=C'DYNALLOC ERROR CODE = '                              
         GOTO1 AHEXOUT,DMCB,RBLKEXTA+4,P+52,2,=C'TOG'                           
         CLC   =F'4',DMCB+16                                                    
         BNE   PBLK022                                                          
         MVC   P+56(15),=C',  INFO CODE = '                                     
         GOTO1 AHEXOUT,DMCB,RBLKEXTA+6,P+71,2,=C'TOG'                           
         CLC   =F'4',DMCB+16                                                    
         BNE   PBLK022                                                          
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         CLC   RBLKEXTA+4(2),=XL2'0410'                                         
         BNE   PBLK022                                                          
*                                                                               
         MVC   TXTUNDD+6(8),EXFDDNM                                             
         MVI   TXTUNDD+5,8                                                      
         MVC   TXTDSN+6(44),EIOSMDSN                                            
         LA    R1,ARBLKUNA         TRY DEALLOCATION BY DDNAME                   
         DYNALLOC                                                               
*                                  TEST TRACE MESSAGE                           
         MVC   P,SPACES                                                         
         MVC   P(20),=CL20'CLEARED DCB:'                                        
         MVC   P+20(8),EXFDDNM                                                  
         MVC   P+30(44),EIOSMDSN                                                
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
*                                                                               
         LA    R1,ARBLKEXA                                                      
         DYNALLOC                                                               
         LTR   RF,RF                                                            
         BZ    PBLK040             DATASET WAS ALLOCATED OK                     
*                                                                               
         MVC   P+11(13),=C'<<< ERROR >>>'                                       
         MVC   P+30(22),=C'DYNALLOC ERROR CODE = '                              
         GOTO1 AHEXOUT,DMCB,RBLKEXTA+4,P+52,2,=C'TOG'                           
         CLC   =F'4',DMCB+16                                                    
         BNE   PBLK022                                                          
         MVC   P+56(15),=C',  INFO CODE = '                                     
         GOTO1 AHEXOUT,DMCB,RBLKEXTA+6,P+71,2,=C'TOG'                           
         CLC   =F'4',DMCB+16                                                    
         BNE   PBLK022                                                          
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
*        CLC   RBLKEXTA+4(2),=XL2'0410'                                         
*        BNE   PBLK022                                                          
*        B     PBLK040                                                          
*                                                                               
PBLK022  EQU   *                                                                
         MVC   ERRMSG,=CL80'READING EXTRACT MVS DATA SET: '                     
         MVC   ERRMSG+30(44),EIOSMDSN                                           
         B     EREX0006                                                         
*                                                                               
PBLK040  EQU   *                                                                
         LA    R1,PBLKEOD          EODAD                                        
         ICM   R6,15,AEXFDCB       R6=A(DCB) EXFILE                             
         ST    R1,DCBEODAD-IHADCB(R6)                                           
         OPEN  ((R6),INPUT)        OPEN EXTRACT FILE                            
         LTR   RF,RF                                                            
         BZ    PBLK042                                                          
         MVC   P,SPACES                                                         
         MVC   P(12),=CL12'OPEN ERROR: '                                        
         EDIT  (RF),(8,P+12),ZERO=NOBLANK,FILL=0                                
         MVC   P+22(8),EXFDDNM                                                  
         MVC   P+32(44),EIOSMDSN                                                
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         MVC   ERRMSG,=CL80'OPENING EXTRACT MVS DATA SET: '                     
         MVC   ERRMSG+30(44),EIOSMDSN                                           
         B     EREX0011                                                         
*                                  TEST TRACE MESSAGE                           
PBLK042  MVC   P,SPACES                                                         
         MVC   P(12),=CL12'OPENED DCB:  '                                       
         MVC   P+20(8),EXFDDNM                                                  
         MVC   P+30(44),EIOSMDSN                                                
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
*                                                                               
         MVC   P,SPACES                                                         
         MVC   P(24),=CL24'START BULK DOWNLOAD:'                                
         BAS   RE,FORMOPM                                                       
         MVC   WTOMSGD(L'WTOMSGD),P                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         GOTO1 AESSWTO,DMCB,X'FE000001',(L'WTOMSG,WTOMSG)                       
         B     PBLK100                                                          
*                                                                               
PBLK100  EQU   *                   GET NEXT RECORD FROM EXTRACT FILE            
         ICM   R6,15,AEXFDCB                                                    
         GET   (R6),EXFIOL                                                      
*                                                                               
         AP    ESSDRCNT,=PL8'1'    BUMP FILE RECORD COUNTER                     
         CP    ESSDRCNT,ESSDCHKP   COMPARE TO RESTART CHECKPOINT NUMBER         
         BL    PBLK100                                                          
*                                  MOVE DATA TO MESSAGE BUFFER                  
         LA    RE,ESSDBUFF         CLEAR BUFFER                                 
         ICM   RF,15,=AL4(ESSDATAL-ESSDHDRL)                                    
         XCEF                                                                   
         SR    R1,R1                                                            
         ICM   R1,3,EXFIOL         GET VARIABLE RECORD LENGTH                   
         SH    R1,=H'4'                                                         
         LTR   R1,R1                                                            
         BNZ   *+8                                                              
         BAS   RE,DCH0                                                          
         LA    RE,ESSDBUFF                                                      
         LA    R0,EXFIO                                                         
         LR    RF,R1                                                            
         MVCL  RE,R0               MOVE DATA                                    
*                                                                               
         MVC   0(2,RE),=XL2'0D15'                                               
         ICM   R1,3,EXFIOL         APPEND DELIMITER FOR PC FILE                 
         SH    R1,=H'4'                                                         
         LA    R1,2(R1)                                                         
         STCM  R1,3,ESSDDLN                                                     
         B     PBLKOK                                                           
*                                                                               
PBLKEOD  EQU   *                                                                
         OI    ESSDMFF,ESSHLSTQ                                                 
         NI    ESSDMFF,X'FF'-ESSHBDLQ                                           
         MVC   ESSDBUFF(25),=C'END OF BULK DATA TRANSFER'                       
         MVC   ESSDBUFF+25(2),=XL2'0D15'                                        
         LA    R1,27                                                            
         STCM  R1,3,ESSDDLN                                                     
         ICM   R6,15,AEXFDCB       RF=A(DCB) EXFILE                             
*                                                                               
         CLOSE ((R6))              CLOSE EXTRACT FILE                           
         FREEPOOL (R6)                                                          
         MVC   TXTUNDD+6(8),EXFDDNM                                             
         MVI   TXTUNDD+5,8                                                      
         MVC   TXTDSN+6(44),EIOSMDSN                                            
         LA    R1,ARBLKUN2                                                      
         DYNALLOC                                                               
*                                  TEST TRACE MESSAGE                           
         MVC   P,SPACES                                                         
         MVC   P(12),=CL12'CLOSED DCB:  '                                       
         MVC   P+20(8),EXFDDNM                                                  
         MVC   P+30(44),EIOSMDSN                                                
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         MVC   P,SPACES                                                         
         MVC   P(24),=CL24'END BULK DOWNLOAD:'                                  
         BAS   RE,FORMOPM                                                       
         MVC   WTOMSGD(L'WTOMSGD),P                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         GOTO1 AESSWTO,DMCB,X'FE000001',(L'WTOMSG,WTOMSG)                       
         B     PBLKOK                                                           
*                                                                               
PBLKOK   B     ESQLOK                                                           
         EJECT                                                                  
***********************************************************************         
* PROCESS AFTER LAST MESSAGE IN SEND REQUEST SEQUENCE                 *         
***********************************************************************         
PSNDLST  EQU   *                                                                
         BAS   RE,VALSQLM          VALIDATE SQL SUB-SYSTEM MESSAGE              
         BNE   EREX0004                                                         
*                                                                               
         CLI   ESSDMTYP,ESSHINFQ   TEST IF INFO MESSAGE TYPE                    
         BE    PSLSINF                                                          
         CLI   ESSDMTYP,ESSHERRQ   TEST IF ERROR MESSAGE TYPE                   
         BE    PROCERR                                                          
         BAS   RE,DCH0                                                          
*                                                                               
PSLSERR  EQU   *                   PROCESS ERROR MESSAGE TYPE                   
         MVC   ESSDCON,EIOSCON                                                  
         MVC   P(34),=CL34'SQL SUBSYSTEM ERROR MESSAGE CODE: '                  
         MVC   P+34(6),ESSDCON                                                  
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     ESQLOK                                                           
*                                                                               
PSLSINF  EQU   *                   PROCESS INFO MESSAGE TYPE                    
         CLC   EIOSMID,=AL4(ESQLNOTQ)                                           
         BE    PSLSCBD             PROCESS NOTIFY FILE READY MESSAGE            
         CLC   EIOSMID,=AL4(ESQLRCOQ)                                           
         BE    PRECOM              PROCESS RECOMMIT FILE MESSAGE                
         CLC   EIOSMID,=AL4(ESQLCHKQ)                                           
         BE    PSLSCBD             PROCESS CHECK POINTED DOWNLOAD               
         BAS   RE,DCH0                                                          
*                                                                               
*                                  CHECK BULK DOWNLOAD OK                       
PSLSCBD  EQU   *                                                                
         CLI   FTPFLAG,C'Y'                                                     
         BE    PNOTIFY             PROCESS NOTIFY FILE READY MESSAGE            
*                                  TEST 'FILE ALREADY RECEIVED'                 
         CLC   EIOSCON,=CL6'000999'  MESSAGE RETURNED (NO DOWNLOAD)             
         BE    PNOTIFY             PROCESS NOTIFY FILE READY MESSAGE            
*                                                                               
         MVC   P(44),=CL44'RECORD NUMBER:          DOWNLOADED: '                
         MVC   P+45(30),=CL30' CHECK POINT: '                                   
         EDIT  ESSDRNUM,(8,P+15),ZERO=NOBLANK,FILL=0                            
         EDIT  ESSDRCNT,(8,P+36),ZERO=NOBLANK,FILL=0                            
         EDIT  ESSDCHKP,(8,P+59),ZERO=NOBLANK,FILL=0                            
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
*                                                                               
         CP    ESSDRNUM,=PL8'0'    IF FILE RECORD IS 0, SKIP CHECKING           
         BE    PNOTIFY             YES, PROCESS NOTIFICATION                    
*                                  COMPARE FILE RECORD NUMBER WITH              
         CP    ESSDRCNT,ESSDRNUM   DOWNLOADED RECORD COUNT                      
         BE    PNOTIFY             IF MATCH PROCESS NOTIFICATION                
         MVC   ERRMSG,=CL80'EXTRACT FILE BULK DOWNLOAD'                         
         MVC   ERRMSG+26(40),=CL40' RECORD COUNT MISSMATCH'                     
         B     EREX0010                                                         
         EJECT                                                                  
***********************************************************************         
* PROCESS SQL SUB-SYSTEM ERROR MESSAGE                                *         
***********************************************************************         
PROCERR  EQU   *                                                                
         MVC   ESSDCON,EIOSCON                                                  
         CLC   ESSDCON,=CL6'000000'                                             
         BNE   PERR010                                                          
*                                  IF SPURIOUS CODE FROM ESS THEN               
*                                  OVERRIDE WITH HOST DEFAULT CODE              
         MVC   ESSDCON,=CL6'099999'                                             
         MVC   EIOSCON,ESSDCON                                                  
*                                                                               
PERR010  EQU   *                                                                
         MVC   P(14),=CL14'<<<WARNING>>> '                                      
         MVC   P+14(40),=CL40'SQL SUBSYSTEM MESSAGE CONDITION CODE: '           
         MVC   P+52(6),ESSDCON                                                  
         MVC   WTOMSGD(L'WTOMSGD),P                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         GOTO1 AESSWTO,DMCB,X'FE000001',(L'WTOMSG,WTOMSG)                       
*                                                                               
         CLC   EIOSMID,=AL4(ESQLNOTQ)                                           
         BNE   PERR020                                                          
         CLI   SIMFLG,C'Y'         TEST SEND IMMEDIATE FLAG                     
         BNE   PNOTIFY             PROCESS NOTIFY FILE ERROR MESSAGE            
         MVI   ESSDCON,C'E'        FORCE SERIOUS ERROR                          
         MVC   EIOSCON,ESSDCON                                                  
         B     PNOTIFY             PROCESS NOTIFY FILE ERROR MESSAGE            
*                                                                               
PERR020  CLC   EIOSMID,=AL4(ESQLCOMQ)                                           
         BE    PCOMMIT             PROCESS COMMIT FILE ERROR MESSAGE            
         CLC   EIOSMID,=AL4(ESQLRESQ)                                           
         BE    PRESEND             PROCESS RETRY SEND OF FILE MESSAGE           
         CLC   EIOSMID,=AL4(ESQLRCVQ)                                           
         BE    PRECEIVE            PROCESS RECEIVE FILE ERROR MESSAGE           
         CLC   EIOSMID,=AL4(ESQLRCOQ)                                           
         BE    *+8                                                              
         BAS   RE,DCH0             UNKNOWN ERROR MESSAGE                        
*                                                                               
*                                  PROCESS RECOMMIT ERROR MESSAGE               
         MVC   P,SPACES                                                         
         MVC   P(24),=CL24'SQL FILE RECOMMIT ERR.: '                            
         BAS   RE,FORMOPM                                                       
         MVC   WTOMSGD(L'WTOMSGD),P                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         GOTO1 AESSWTO,DMCB,X'FE000001',(L'WTOMSG,WTOMSG)                       
         B     PRECOM                                                           
         EJECT                                                                  
***********************************************************************         
* PROCESS SQL SUB-SYSTEM COMMIT FILE MESSAGE ACKNOWLEDGE              *         
***********************************************************************         
PCOMMIT  EQU   *                                                                
         LA    R6,WORK             BUILD EXSERV PARAMETER BLOCK                 
         XC    WORK,WORK                                                        
         USING EXSERVD,R6                                                       
         MVC   EXSACTN,=AL4(EXSACOMQ)                                           
         MVC   EXSCON,EIOSCON                                                   
         MVC   EXSEID,ESSDSNAM                                                  
         MVC   EXSAGY,EIOSMAID                                                  
         MVC   EXSSYS,EIOSMSYS                                                  
         MVC   EXSSUB,EIOSMSUB                                                  
         MVC   EXSFNUM,EIOSMFNM                                                 
         MVC   EXSSKEY,EIOSMSKY                                                 
*                                  INSERT VALUES IN EXTRACT TABLE               
         GOTO1 AEXSERV,DMCB,WORK,BYTE,0                                         
         CLI   BYTE,0              CHECK RETURN CODE OK                         
         BE    *+8                                                              
         BAS   RE,EXSERROR         ELSE EXIT WITH EXSERV ERROR                  
         MVC   EIOSMGNM,EXSGNUM    SAVE FILE GENERATION NUMBER                  
         MVC   EIOSFLG1,EXSFLG1    SAVE EXTRACT CONTROL FLAGS 1                 
         DROP  R6                                                               
*                                  DYNAMICALLY ALLOCATE MVS FILE                
*                                                                               
         CLI   BYTE,0              IF ANY ERROR FOUND                           
         JE    *+8                                                              
         BAS   RE,WRTLOG           WRITE COMMIT LOG TO MVS DATASET              
*                                                                               
         MVC   P,SPACES                                                         
         MVC   P(24),=CL24'SQL FILE COMMITTED: '                                
         BAS   RE,FORMOPM                                                       
         MVC   WTOMSGD(L'WTOMSGD),P                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         TM    EIOSFLG1,GXSDFMLQ                                                
         BZ    PCOM100                                                          
         GOTO1 AESSWTO,DMCB,X'FF000001',(L'WTOMSG,WTOMSG)                       
         B     PCOM110                                                          
PCOM100  EQU   *                                                                
         GOTO1 AESSWTO,DMCB,X'FE000001',(L'WTOMSG,WTOMSG)                       
PCOM110  EQU   *                                                                
*                                                                               
         B     ESQLOK                                                           
         EJECT                                                                  
***********************************************************************         
* PROCESS SQL SUB-SYSTEM RETRY SEND OF FILE MESSAGE                   *         
***********************************************************************         
PRESEND  EQU   *                                                                
         LA    R6,WORK             BUILD EXSERV PARAMETER BLOCK                 
         XC    WORK,WORK                                                        
         USING EXSERVD,R6                                                       
         MVC   EXSACTN,=AL4(EXSARESQ)                                           
         MVC   EXSCON,EIOSCON                                                   
         MVC   EXSEID,ESSDSNAM                                                  
         MVC   EXSAGY,EIOSMAID                                                  
         MVC   EXSSYS,EIOSMSYS                                                  
         MVC   EXSSUB,EIOSMSUB                                                  
         MVC   EXSFNUM,EIOSMFNM                                                 
*                                  INSERT VALUES IN EXTRACT TABLE               
         GOTO1 AEXSERV,DMCB,WORK,BYTE,0                                         
         CLI   BYTE,0              CHECK RETURN CODE OK                         
         BE    *+8                                                              
         BAS   RE,EXSERROR         ELSE EXIT WITH EXSERV ERROR                  
         DROP  R6                                                               
*                                                                               
         B     ESQLOK                                                           
         EJECT                                                                  
***********************************************************************         
* PROCESS SQL SUB-SYSTEM RECEIVED FILE MESSAGE ACKNOWLEDGE            *         
***********************************************************************         
PRECEIVE EQU   *                                                                
         LA    R6,WORK             BUILD EXSERV PARAMETER BLOCK                 
         XC    WORK,WORK                                                        
         USING EXSERVD,R6                                                       
         MVC   EXSACTN,=AL4(EXSARCVQ)                                           
         MVC   EXSCON,EIOSCON                                                   
         MVC   EXSEID,ESSDSNAM                                                  
         MVC   EXSAGY,EIOSMAID                                                  
         MVC   EXSSYS,EIOSMSYS                                                  
         MVC   EXSSUB,EIOSMSUB                                                  
         MVC   EXSFNUM,EIOSMFNM                                                 
*                                  INSERT VALUES IN EXTRACT TABLE               
         GOTO1 AEXSERV,DMCB,WORK,BYTE,0                                         
         CLI   BYTE,0              CHECK RETURN CODE OK                         
         BE    *+8                                                              
         BAS   RE,EXSERROR         ELSE EXIT WITH EXSERV ERROR                  
         MVC   EIOSMGNM,EXSGNUM    SAVE FILE GENERATION NUMBER                  
         MVC   EIOSFLG1,EXSFLG1    SAVE EXTRACT CONTROL FLAGS 1                 
         DROP  R6                                                               
*                                                                               
         MVC   P,SPACES                                                         
         MVC   P(22),=CL22'SQL FILE RECEIVED : '                                
         BAS   RE,FORMOPM                                                       
         MVC   WTOMSGD(L'WTOMSGD),P                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         TM    EIOSFLG1,GXSDFMLQ                                                
         BZ    PREC100                                                          
         GOTO1 AESSWTO,DMCB,X'FF000001',(L'WTOMSG,WTOMSG)                       
         B     PREC110                                                          
PREC100  EQU   *                                                                
         GOTO1 AESSWTO,DMCB,X'FE000001',(L'WTOMSG,WTOMSG)                       
PREC110  EQU   *                                                                
*                                                                               
         CLC   EIOSCON,=CL6'000135'                                             
         BE    PCOMMIT             COMMIT IF NULL FILE RECEIVED                 
         B     ESQLOK                                                           
         EJECT                                                                  
***********************************************************************         
* PROCESS SQL SUB-SYSTEM SEND MESSAGE FILE READY NOTIFICATION ACKN.   *         
***********************************************************************         
PNOTIFY  EQU   *                                                                
         LA    R6,WORK             BUILD EXSERV PARAMETER BLOCK                 
         XC    WORK,WORK                                                        
         USING EXSERVD,R6                                                       
         MVC   EXSACTN,=AL4(EXSANOTQ)                                           
         MVC   EXSCON,EIOSCON                                                   
         MVC   EXSEID,ESSDSNAM                                                  
         MVC   EXSAGY,EIOSMAID                                                  
         MVC   EXSSYS,EIOSMSYS                                                  
         MVC   EXSSUB,EIOSMSUB                                                  
         MVC   EXSFNUM,EIOSMFNM                                                 
*                                  INSERT VALUES IN EXTRACT TABLE               
         GOTO1 AEXSERV,DMCB,WORK,BYTE,0                                         
         CLI   BYTE,0              CHECK RETURN CODE OK                         
         BE    *+8                                                              
         BAS   RE,EXSERROR         ELSE EXIT WITH EXSERV ERROR                  
         MVC   EIOSMGNM,EXSGNUM    SAVE FILE GENERATION NUMBER                  
         MVC   EIOSFLG1,EXSFLG1    SAVE EXTRACT CONTROL FLAGS 1                 
         DROP  R6                                                               
*                                                                               
         MVC   P,SPACES                                                         
         MVC   P(24),=CL24'SQL FILE NOTIFICATION : '                            
         BAS   RE,FORMOPM                                                       
         MVC   WTOMSGD(L'WTOMSGD),P                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         TM    EIOSFLG1,GXSDFMLQ                                                
         BZ    PNOT100                                                          
         GOTO1 AESSWTO,DMCB,X'FF000001',(L'WTOMSG,WTOMSG)                       
         B     PNOT110                                                          
PNOT100  EQU   *                                                                
         GOTO1 AESSWTO,DMCB,X'FE000001',(L'WTOMSG,WTOMSG)                       
PNOT110  EQU   *                                                                
*                                  TEST NOTIY RETURN CODE                       
         CLC   EIOSCON,=CL6'000000'  NO ERROR                                   
         BE    ESQLOK                                                           
*                                                                               
         CLC   EIOSCON,=CL6'000999'  MESSAGE RETURNED (NO DOWNLOAD)             
         BE    ESQLOK                                                           
*                                                                               
         MVC   ERRMSG,=CL80'SQL SUBSYSTEM NOTIFICATION MESSAGE'                 
         MVC   ERRMSG+34(40),=CL40' CONDITION CODE: '                           
         MVC   ERRMSG+51(L'EIOSCON),EIOSCON                                     
         B     EREX0007                                                         
         EJECT                                                                  
***********************************************************************         
* PROCESS SQL SUB-SYSTEM SEND MESSAGE FILE RECOMMIT ACKNOWLEDGE       *         
***********************************************************************         
PRECOM   EQU   *                                                                
         LA    R6,WORK             BUILD EXSERV PARAMETER BLOCK                 
         XC    WORK,WORK                                                        
         USING EXSERVD,R6                                                       
         MVC   EXSACTN,=AL4(EXSARCAQ)                                           
         MVC   EXSCON,EIOSCON                                                   
         MVC   EXSEID,ESSDSNAM                                                  
         MVC   EXSAGY,EIOSMAID                                                  
         MVC   EXSSYS,EIOSMSYS                                                  
         MVC   EXSSUB,EIOSMSUB                                                  
         MVC   EXSFNUM,EIOSMFNM                                                 
*                                  INSERT VALUES IN EXTRACT TABLE               
         GOTO1 AEXSERV,DMCB,WORK,BYTE,0                                         
         CLI   BYTE,0              CHECK RETURN CODE OK                         
         BE    *+8                                                              
         BAS   RE,EXSERROR         ELSE EXIT WITH EXSERV ERROR                  
         MVC   EIOSMGNM,EXSGNUM    SAVE FILE GENERATION NUMBER                  
         MVC   EIOSFLG1,EXSFLG1    SAVE EXTRACT CONTROL FLAGS 1                 
         DROP  R6                                                               
*                                                                               
         MVC   P,SPACES                                                         
         MVC   P(24),=CL24'SQL FILE RECOMMIT : '                                
         BAS   RE,FORMOPM                                                       
         MVC   WTOMSGD(L'WTOMSGD),P                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         TM    EIOSFLG1,GXSDFMLQ                                                
         BZ    PRCO100                                                          
         GOTO1 AESSWTO,DMCB,X'FF000001',(L'WTOMSG,WTOMSG)                       
         B     PRCO110                                                          
PRCO100  EQU   *                                                                
         GOTO1 AESSWTO,DMCB,X'FE000001',(L'WTOMSG,WTOMSG)                       
PRCO110  EQU   *                                                                
*                                                                               
         B     ESQLOK                                                           
         EJECT                                                                  
***********************************************************************         
* PRINT OUT COMMIT FILE LOG DATA TO SYSOUT LOG                        *         
***********************************************************************         
PRNLOG   NTR1                                                                   
         BAS   RE,BDSNAME          BUILD DATA SET NAME                          
         MVC   LOGDSN,SPACES                                                    
         MVC   P(21),=C'LOG SQL COMMIT FILE: '                                  
         MVC   P+23(L'LOGDSN),LOGDSN                                            
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         BAS   RE,BHEADER          BUILD EXTRACT HEADER                         
         MVC   P(L'LOGLINE),LOGLINE                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         ICM   RF,15,EIOSMLOG                                                   
         STCM  RF,15,LOGLAST                                                    
         ICM   RF,15,EIOSMLOX                                                   
         STCM  RF,15,ENDLOG                                                     
*                                                                               
PLOG020  EQU   *                                                                
         BAS   RE,GETLOGLN                                                      
         CLI   LOGEOF,C'Y'                                                      
         BE    PLOG040                                                          
         MVC   P(L'LOGLINE),LOGLINE                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     PLOG020                                                          
*                                                                               
PLOG040  EQU   *                                                                
         BAS   RE,BTRAIL           BUILD EXTRACT TRAILER                        
         MVC   P(L'LOGLINE),LOGLINE                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
*                                                                               
PLOG100  EQU   *                                                                
         B     PLOGOK                                                           
*                                                                               
PLOGNO   B     NO                                                               
PLOGOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* WRITE OUT COMMIT FILE LOG DATA TO MVS DATA SET                      *         
***********************************************************************         
WRTLOG   NTR1                                                                   
*                                                                               
         BAS   RE,BDSNAME          BUILD DATA SET NAME                          
*                                  DYNAMICALLY ALLOCATE MVS FILE                
         MVC   TXTDD+6(7),=CL7'LOGFILE'                                         
         MVI   TXTDD+5,7                                                        
         LA    R1,ARBLKCTA         DYNAMIC ALLOCATION PARAMETER BLOCK           
         DYNALLOC                                                               
         LTR   RF,RF                                                            
         BZ    WLOG010             DATASET WAS CREATED OK                       
*                                                                               
         LA    R1,ARBLKMOD         TRY DYNAMIC ALLOCATION OF EXISTING           
         DYNALLOC                  LOGFILE DATA SET WITH DISP=MOD               
         LTR   RF,RF                                                            
         BZ    WLOG010                                                          
*                                                                               
         MVC   P+11(13),=C'<<< ERROR >>>'                                       
         MVC   P+30(22),=C'DYNALLOC ERROR CODE = '                              
         GOTO1 AHEXOUT,DMCB,RBLKCATA+4,P+52,2,=C'TOG'                           
         CLC   =F'4',DMCB+16                                                    
         BNE   WLOGNO                                                           
         MVC   P+56(15),=C',  INFO CODE = '                                     
         GOTO1 AHEXOUT,DMCB,RBLKCATA+6,P+71,2,=C'TOG'                           
         CLC   =F'4',DMCB+16                                                    
         BNE   WLOGNO                                                           
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     WLOGNO                                                           
*                                                                               
WLOG010  OPEN  (LOGFILE,OUTPUT)                                                 
         LTR   RF,RF                                                            
         BZ    WLOG012                                                          
*                                                                               
         MVC   P+11(13),=C'<<< ERROR >>>'                                       
         MVC   P+30(22),=C'LOG FILE OPEN ERROR'                                 
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     WLOGNO                                                           
*                                                                               
WLOG012  BAS   RE,BHEADER          BUILD EXTRACT HEADER                         
         PUT   LOGFILE,LOGLINE                                                  
         ICM   RF,15,EIOSMLOG                                                   
         STCM  RF,15,LOGLAST                                                    
         ICM   RF,15,EIOSMLOX                                                   
         STCM  RF,15,ENDLOG                                                     
*                                                                               
WLOG020  EQU   *                                                                
         BAS   RE,GETLOGLN                                                      
         CLI   LOGEOF,C'Y'                                                      
         BE    WLOG040                                                          
         B     WLOG030                                                          
*                                                                               
WLOG030  PUT   LOGFILE,LOGLINE                                                  
         CLI   LOGEOF,C'Y'                                                      
         BNE   WLOG020                                                          
*                                                                               
WLOG040  EQU   *                                                                
         BAS   RE,BTRAIL           BUILD EXTRACT TRAILER                        
         PUT   LOGFILE,LOGLINE                                                  
*                                                                               
WLOG100  EQU   *                                                                
         CLOSE (LOGFILE)                                                        
         LTR   RF,RF                                                            
         BZ    WLOG102                                                          
*                                                                               
         MVC   P+11(13),=C'<<< ERROR >>>'                                       
         MVC   P+30(22),=C'LOG FILE CLOSE ERROR'                                
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     WLOGNO                                                           
*                                                                               
WLOG102  MVC   TXTUNDD+6(7),=CL7'LOGFILE'                                       
         MVI   TXTUNDD+5,7                                                      
         MVC   TXTDSN+6(44),LOGDSN                                              
         LA    R1,ARBLKUNA         DYNAMIC UNALLOCATION PARAMETER BLOCK         
         DYNALLOC                                                               
         LTR   RF,RF                                                            
         BZ    WLOG110                                                          
*                                                                               
         MVC   P+11(13),=C'<<< ERROR >>>'                                       
         MVC   P+30(22),=C'DYNALLOC ERROR CODE = '                              
         GOTO1 AHEXOUT,DMCB,RBLKUNA+4,P+52,2,=C'TOG'                            
         CLC   =F'4',DMCB+16                                                    
         BNE   WLOGNO                                                           
         MVC   P+56(15),=C',  INFO CODE = '                                     
         GOTO1 AHEXOUT,DMCB,RBLKUNA+6,P+71,2,=C'TOG'                            
         CLC   =F'4',DMCB+16                                                    
         BNE   WLOGNO                                                           
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     WLOGNO                                                           
*                                                                               
WLOG110  EQU   *                                                                
         B     WLOGOK                                                           
*                                                                               
WLOGNO   B     NO                                                               
WLOGOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* BUILD PQ REPORT EXTRACT HEADER                                      *         
***********************************************************************         
BHEADER  NTR1                                                                   
         MVC   LOGLINE,SPACES                                                   
         MVC   LOGLINE(80),=CL80'SQL EXTRACT FILE TRANSFER LOG HEADER'          
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* BUILD PQ REPORT EXTRACT TRAILER                                     *         
***********************************************************************         
BTRAIL   NTR1                                                                   
         MVC   LOGLINE,SPACES                                                   
         MVC   LOGLINE(80),=CL80'SQL EXTRACT FILE TRANSFER LOG TRAILER'         
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* BUILD PQ REPORT EXTRACT MVS DATA SET NAME                           *         
***********************************************************************         
BDSNAME  NTR1                                                                   
         MVC   LOGDSN,SPACES                                                    
         LA    R6,LOGDSN                                                        
*&&UK*&& MVC   0(8,R6),=CL8'ESS.XLG.'                                           
*&&US*&& MVC   0(8,R6),=CL8'DDS.XLG.'                                           
         LA    R6,8(R6)                                                         
         MVC   0(8,R6),ESSNAME                                                  
         LA    R6,8(R6)                                                         
         MVI   0(R6),C'.'                                                       
         LA    R6,1(R6)                                                         
         CLI   EIOSMAID,C'A'                                                    
         BL    BDSN020                                                          
         CLI   EIOSMAID,C'Z'                                                    
         BH    BDSN020                                                          
         CLI   EIOSMAID,C'A'                                                    
         BL    BDSN010                                                          
         CLI   EIOSMAID,C'Z'                                                    
         BH    BDSN010                                                          
         B     BDSN030                                                          
BDSN010  CLI   EIOSMAID,C'0'                                                    
         BL    BDSN020                                                          
         CLI   EIOSMAID,C'9'                                                    
         BH    BDSN020                                                          
         B     BDSN030                                                          
BDSN020  MVI   0(R6),C'H'                                                       
         LA    R6,1(R6)                                                         
         GOTO1 AHEXOUT,DMCB,EIOSMAID,(R6),2,=C'TOG'                             
         LA    R6,4(R6)                                                         
         B     BDSN040                                                          
BDSN030  MVC   0(2,R6),EIOSMAID                                                 
         LA    R6,2(R6)                                                         
BDSN040  MVC   0(1,R6),ESQLSYS                                                  
         LA    R6,1(R6)                                                         
         MVC   0(1,R6),ESQLSUB                                                  
         LA    R6,1(R6)                                                         
         MVI   0(R6),C'.'                                                       
         LA    R6,1(R6)                                                         
         SR    RF,RF                                                            
         ICM   RF,3,EIOSMGNM                                                    
         EDIT  (RF),(6,0(R6)),ZERO=NOBLANK,FILL=0                               
         MVI   0(R6),C'G'                                                       
         LA    R6,6(R6)                                                         
         LA    R1,EIOSMSKY                                                      
         LA    R0,8                                                             
         CLI   0(R1),C' '                                                       
         BE    BDSN060                                                          
         MVI   0(R6),C'.'                                                       
         LA    R6,1(R6)                                                         
         MVI   0(R6),C'X'                                                       
         CLI   0(R1),C'A'                                                       
         BL    BDSN060                                                          
         CLI   0(R1),C'Z'                                                       
         BH    BDSN060                                                          
*                                                                               
BDSN050  CLI   0(R1),C' '                                                       
         BE    BDSN060                                                          
         CLI   0(R1),C'A'                                                       
         BL    BDSN054                                                          
         CLI   0(R1),C'Z'                                                       
         BNH   BDSN052                                                          
         CLI   0(R1),C'0'                                                       
         BL    BDSN054                                                          
         CLI   0(R1),C'9'                                                       
         BH    BDSN054                                                          
BDSN052  MVC   0(1,R6),0(R1)                                                    
         LA    R6,1(R6)                                                         
BDSN054  LA    R1,1(R1)                                                         
         BCT   R0,BDSN050                                                       
*                                                                               
BDSN060  EQU   *                                                                
*                                                                               
         MVC   TXTDSN+6(44),LOGDSN LOAD DYNALLOC BLOCK WITH DSN                 
         MVC   P(8),=C'LOGDSN: '                                                
         MVC   P+10(44),LOGDSN                                                  
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     BDSNOK                                                           
*                                                                               
BDSNNO   B     NO                                                               
BDSNOK   B     YES                                                              
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* GET LOG FILE LINE                                                   *         
***********************************************************************         
GETLOGLN NTR1                                                                   
         MVI   LOGEOF,C'N'                                                      
         L     RF,LOGLAST                                                       
         MVC   LOGLINE,SPACES                                                   
         LA    R1,LOGLINE                                                       
         LA    R0,L'LOGLINE                                                     
GLIN010  EQU   *                                                                
         CLM   RF,15,ENDLOG                                                     
         BNL   GLINEND                                                          
         CLI   0(RF),X'0D'                                                      
         BE    GLINEND                                                          
         CLI   0(RF),X'15'                                                      
         BE    GLIN020                                                          
         MVC   0(1,R1),0(RF)                                                    
         LA    R1,1(R1)                                                         
         LA    RF,1(RF)                                                         
         BCT   R0,GLIN010                                                       
         B     GLINX                                                            
GLIN020  EQU   *                                                                
         LA    RF,1(RF)                                                         
         CLI   0(RF),X'0D'                                                      
         BE    GLINEND                                                          
         B     GLINX                                                            
*                                                                               
GLINEND  MVI   LOGEOF,C'Y'                                                      
GLINX    ST    RF,LOGLAST                                                       
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* FORMAT FILE TRANSFER OPERATOR MESSAGE                               *         
***********************************************************************         
FORMOPM  NTR1                                                                   
         LA    R6,P+24                                                          
         MVC   0(8,R6),ESSNAME                                                  
         LA    R6,8(R6)                                                         
         MVI   0(R6),C','                                                       
         LA    R6,1(R6)                                                         
         CLI   EIOSMAID,C'A'                                                    
         BL    FOPM020                                                          
         CLI   EIOSMAID,C'Z'                                                    
         BH    FOPM020                                                          
         CLI   EIOSMAID,C'A'                                                    
         BL    FOPM010                                                          
         CLI   EIOSMAID,C'Z'                                                    
         BH    FOPM010                                                          
         B     FOPM030                                                          
FOPM010  CLI   EIOSMAID,C'0'                                                    
         BL    FOPM020                                                          
         CLI   EIOSMAID,C'9'                                                    
         BH    FOPM020                                                          
         B     FOPM030                                                          
FOPM020  MVI   0(R6),C'H'                                                       
         LA    R6,1(R6)                                                         
         GOTO1 AHEXOUT,DMCB,EIOSMAID,(R6),2,=C'TOG'                             
         LA    R6,4(R6)                                                         
         B     FOPM040                                                          
FOPM030  MVC   0(2,R6),EIOSMAID                                                 
         LA    R6,2(R6)                                                         
FOPM040  MVC   0(1,R6),EIOSSYSC                                                 
         LA    R6,1(R6)                                                         
         MVC   0(1,R6),EIOSSUBC                                                 
         LA    R6,1(R6)                                                         
         MVI   0(R6),C','                                                       
         LA    R6,1(R6)                                                         
         SR    RF,RF                                                            
         ICM   RF,3,EIOSMGNM                                                    
         EDIT  (RF),(6,0(R6)),ZERO=NOBLANK,FILL=0                               
         MVI   0(R6),C'G'                                                       
         LA    R6,6(R6)                                                         
         MVI   0(R6),C','                                                       
         LA    R6,1(R6)                                                         
FOPM050  SR    RF,RF                                                            
         ICM   RF,3,EIOSMFNM                                                    
         EDIT  (RF),(6,0(R6)),ZERO=NOBLANK,FILL=0                               
         MVI   0(R6),C'T'                                                       
         LA    R6,6(R6)                                                         
         CLC   EIOSMID,=AL4(ESQLCOMQ)                                           
         BNE   FOPM070                                                          
         MVI   0(R6),C','                                                       
         LA    R6,1(R6)                                                         
         LA    R1,EIOSMSKY                                                      
         LA    R0,8                                                             
         CLI   0(R1),C' '                                                       
         BE    FOPM050                                                          
FOPM060  CLI   0(R1),C' '                                                       
         BE    FOPM070                                                          
         CLI   0(R1),0                                                          
         BE    *+10                                                             
         MVC   0(1,R6),0(R1)                                                    
         LA    R6,1(R6)                                                         
         LA    R1,1(R1)                                                         
         BCT   R0,FOPM060                                                       
FOPM070  MVI   0(R6),C','                                                       
         LA    R6,1(R6)                                                         
         MVC   0(5,R6),=CL5'RC = '                                              
         LA    R6,5(R6)                                                         
         MVC   0(L'EIOSCON,R6),EIOSCON                                          
*                                                                               
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* VALIDATE SQL SUB-SYSTEM MESSAGE DATA                                *         
***********************************************************************         
VALSQLM  NTR1                                                                   
         MVI   ERRFLAG,C'N'                                                     
*                                  VALIDATE MESSAGE ID                          
         BAS   RE,VALSMID                                                       
         BNE   VSQMER                                                           
*                                  VALIDATE CONDITION CODE                      
         BAS   RE,VALSCON                                                       
         BNE   VSQMER                                                           
*                                  VALIDATE AGENCY ALPHA ID                     
         GOTO1 VALALNUM,DMCB,ESQLAID,2                                          
         BNE   VSQMERAA                                                         
         CLI   ESQLAID,C'@'                                                     
         BNE   *+8                                                              
         MVI   ESQLAID,C'*'                                                     
         MVC   EIOSMAID,ESQLAID                                                 
*                                  VALIDATE SYSTEM                              
         BAS   RE,VALXSYS                                                       
         BNE   VSQMER                                                           
*                                  VALIDATE SUB SYSTEM                          
         BAS   RE,VALXSUB                                                       
         BNE   VSQMER                                                           
*                                  VALIDATE FILE SEQUENCE NUMBER                
         GOTO1 ANUMVAL,DMCB,ESQLFNUM+3,(X'00',5)                                
         CLI   0(R1),0                                                          
         BNE   VSQMERFN                                                         
         L     R1,4(R1)                                                         
         STCM  R1,3,EIOSMFNM                                                    
         B     VSQMOK                                                           
*                                  AGENCY ALPHA ERROR                           
VSQMERAA MVI   ERRFLAG,C'Y'                                                     
         MVC   ERRMSG,=CL80'NON ALPHA-NUMERIC AGENCY ALPHA ID'                  
         B     VSQMER                                                           
*                                  FILE SEQUENCE NUMBER ERROR                   
VSQMERFN MVI   ERRFLAG,C'Y'                                                     
         MVC   ERRMSG,=CL80'NON NUMERIC FILE SEQUENCE NUMBER'                   
         B     VSQMER                                                           
*                                  SQL MESSAGE ERROR                            
VSQMER   EQU   *                                                                
         B     VSQMNO                                                           
*                                                                               
VSQMNO   B     NO                                                               
VSQMOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* VALIDATE SQL SUB-SYSTEM MESSAGE CHECKPOINT RECORD NUMBER            *         
***********************************************************************         
VALCHKP  NTR1                                                                   
*                                  VALIDATE CHECKPOINT RECORD NUMBER            
         GOTO1 ANUMVAL,DMCB,ESQLRNM,(X'00',8)                                   
         CLI   0(R1),0                                                          
         BNE   VCHKER                                                           
         L     R1,4(R1)                                                         
         CVD   R1,DUB                                                           
         MVC   EIOSCHKP,DUB                                                     
         B     VCHKOK                                                           
*                                  CHECKPOINT RECORD NUMBER ERROR               
VCHKER   EQU   *                                                                
         MVI   ERRFLAG,C'Y'                                                     
         MVC   ERRMSG,=CL80'NON NUMERIC CHECKPOINT RECORD NUMBER'               
         B     VCHKNO                                                           
*                                                                               
VCHKNO   B     NO                                                               
VCHKOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* VALIDATE EXTRA PARAMETERS IN COMMIT FILE MESSAGE                    *         
***********************************************************************         
VALCOMM  NTR1                                                                   
         MVI   ERRFLAG,C'N'                                                     
*                                  VALIDATE SERVER APPLICATION KEY              
         BAS   RE,VALSKEY                                                       
         BNE   VCOMER                                                           
*                                  VALIDATE LOG DATA                            
         BAS   RE,VALSLOG                                                       
         BNE   VCOMER                                                           
         B     VCOMOK                                                           
*                                  SQL MESSAGE ERROR                            
VCOMER   EQU   *                                                                
         B     VCOMNO                                                           
*                                                                               
VCOMNO   B     NO                                                               
VCOMOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* VALIDATE SQL SUB-SYSTEM MESSAGE ID                                  *         
***********************************************************************         
VALSMID  NTR1                                                                   
*                                                                               
         GOTO1 VALALNUM,DMCB,ESQLMID,4                                          
         BNE   VSMIER                                                           
         CLC   ESQLMID,=AL4(ESQLCOMQ)                                           
         BE    VSMI010                                                          
         CLC   ESQLMID,=AL4(ESQLRCVQ)                                           
         BE    VSMI010                                                          
         CLC   ESQLMID,=AL4(ESQLNOTQ)                                           
         BE    VSMI010                                                          
         CLC   ESQLMID,=AL4(ESQLRESQ)                                           
         BE    VSMI010                                                          
         CLC   ESQLMID,=AL4(ESQLRCOQ)                                           
         BE    VSMI010                                                          
         CLC   ESQLMID,=AL4(ESQLCHKQ)                                           
         BE    VSMI010                                                          
         B     VSMIER                                                           
*                                                                               
VSMI010  MVC   EIOSMID,ESQLMID                                                  
         B     VSMIOK                                                           
*                                                                               
VSMIER   EQU   *                                                                
         MVI   ERRFLAG,C'Y'                                                     
         MVC   ERRMSG,=CL80'INVALID SQL MESSAGE ID'                             
         B     VSMINO                                                           
*                                                                               
VSMINO   B     NO                                                               
VSMIOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* VALIDATE SQL SUB-SYSTEM CONDITION CODE                              *         
***********************************************************************         
VALSCON  NTR1                                                                   
*                                                                               
         MVC   EIOSCON,ESQLCON                                                  
         CLI   EIOSCON,EXSCOKQ    OVERRIDE ANY ERROR ??                         
         BE    *+8                                                              
         MVI   EIOSCON,EXSCOKQ    WITH WARNIONG STATUS                          
         B     VSCOOK                                                           
*                                                                               
VSCOER   EQU   *                                                                
         MVI   ERRFLAG,C'Y'                                                     
         MVC   ERRMSG,=CL80'INVALID SQL CONDITION CODE'                         
         B     VSCONO                                                           
*                                                                               
VSCONO   B     NO                                                               
VSCOOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* VALIDATE SYSTEM CHARACTER CODE                                      *         
***********************************************************************         
VALXSYS  NTR1                                                                   
         L     R6,=A(SYSLEX)                                                    
         USING SYSLSTD,R6                                                       
         LA    R6,6(R6)            R3=A(SYSTEM LIST)                            
*                                                                               
VXSY010  CLI   0(R6),0             TEST E-O-T                                   
         BE    VXSY022                                                          
         CLC   SYSLRPLT,ESQLSYS    MATCH ON SYSTEM CHARACTER CODE               
         BE    VXSY030                                                          
VXSY020  LA    R6,SYSLLEN(R6)      NO - BUMP TO NEXT TABLE ENTRY                
         B     VXSY010                                                          
*                                                                               
VXSY022  L     R6,=A(SYSLST)                                                    
         LA    R6,6(R6)            R3=A(EXTENDED SYSTEM LIST)                   
*                                                                               
VXSY024  CLI   0(R6),0             TEST E-O-T                                   
         BE    VXSYER                                                           
         CLC   SYSLRPLT,ESQLSYS    MATCH ON SYSTEM CHARACTER CODE               
         BE    VXSY030                                                          
VXSY026  LA    R6,SYSLLEN(R6)      NO - BUMP TO NEXT TABLE ENTRY                
         B     VXSY024                                                          
*                                                                               
VXSY030  MVC   EIOSMSYS,SYSLNUM    RETURN SYSTEM NUMBER                         
         MVC   EIOSSYSC,ESQLSYS                                                 
         B     VXSYOK                                                           
*                                                                               
VXSYER   EQU   *                                                                
         MVI   ERRFLAG,C'Y'                                                     
         MVC   ERRMSG,=CL80'INVALID SYSTEM'                                     
         B     VXSYNO                                                           
*                                                                               
VXSYNO   B     NO                                                               
VXSYOK   B     YES                                                              
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
* VALIDATE SUB-SYSTEM CHARACTER CODE                                  *         
***********************************************************************         
VALXSUB  NTR1                                                                   
         L     R6,=A(SUBLST)                                                    
         USING SUBLSTD,R6                                                       
         LA    R6,6(R6)            R3=A(SYSTEM LIST)                            
*                                                                               
VXSU010  CLI   0(R6),0             TEST E-O-T                                   
         BE    VXSUER                                                           
         CLC   SUBLKEY1,ESQLSUB    MATCH ON SUB-SYSTEM CHARACTER CODE           
         BE    VXSU030                                                          
VXSU020  LA    R6,SYSLLEN(R6)      NO - BUMP TO NEXT TABLE ENTRY                
         B     VXSU010                                                          
*                                                                               
VXSU030  MVC   EIOSMSUB,SUBLNUM    RETURN SUB-SYSTEM NUMBER                     
         MVC   EIOSSUBC,ESQLSUB                                                 
         B     VXSUOK                                                           
*                                                                               
VXSUER   EQU   *                                                                
         MVI   ERRFLAG,C'Y'                                                     
         MVC   ERRMSG,=CL80'INVALID SUB-SYSTEM'                                 
         B     VXSUNO                                                           
*                                                                               
VXSUNO   B     NO                                                               
VXSUOK   B     YES                                                              
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
* VALIDATE SQL SUB-SYSTEM SERVER APPLICATION KEY                      *         
***********************************************************************         
VALSKEY  NTR1                                                                   
*                                                                               
         MVC   EIOSMSKY,ESQLSKEY                                                
         OC    EIOSMSKY,EIOSMSKY                                                
         BZ    VSKEER                                                           
         CLC   EIOSMSKY,SPACES                                                  
         BE    VSKEER                                                           
         MVC   P(8),=C'SERVER: '                                                
         MVC   P+12(16),EIOSMSKY                                                
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     VSKEOK                                                           
*                                                                               
VSKEER   EQU   *                                                                
         MVI   ERRFLAG,C'Y'                                                     
         MVC   ERRMSG,=CL80'INVALID SQL SERVER NAME'                            
         B     VSKENO                                                           
*                                                                               
VSKENO   B     NO                                                               
VSKEOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* VALIDATE SQL SUB-SYSTEM COMMIT LOG DATA                             *         
***********************************************************************         
VALSLOG  NTR1                                                                   
*                                                                               
         LA    RE,ESQLLOG                                                       
         STCM  RE,15,EIOSMLOG                                                   
         SR    RF,RF                                                            
         ICM   RF,3,ESSDDLN                                                     
         AR    RE,RF                                                            
         SH    RE,=Y(ESQLLOG-ESQLDAT)                                           
         STCM  RE,15,EIOSMLOX                                                   
         SH    RE,=Y(3)                                                         
         MVI   0(RE),X'0D'                                                      
         B     VSLOOK                                                           
*                                                                               
VSLOER   EQU   *                                                                
         MVI   ERRFLAG,C'Y'                                                     
         MVC   ERRMSG,=CL80'INVALID SQL COMMIT LOG DATA'                        
         B     VSLONO                                                           
*                                                                               
VSLONO   B     NO                                                               
VSLOOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* VALIDATE ALPHA NUMERIC FIELD                                        *         
***********************************************************************         
VALALNUM NTR1                                                                   
         LM    R2,R3,0(R1)                                                      
         LTR   R3,R3                                                            
         BZ    VALNOK                                                           
*                                                                               
VALN010  CLI   0(R2),C'@'                                                       
         BE    VALN030                                                          
         CLI   0(R2),C'#'                                                       
         BE    VALN030                                                          
         CLI   0(R2),C'$'                                                       
         BE    VALN030                                                          
         CLI   0(R2),X'81'                                                      
         BL    VALNNO                                                           
         CLI   0(R2),X'A9'                                                      
         BNH   VALN030                                                          
         CLI   0(R2),C'A'                                                       
         BL    VALNNO                                                           
         CLI   0(R2),C'Z'                                                       
         BNH   VALN030                                                          
         CLI   0(R2),C'0'                                                       
         BL    VALNNO                                                           
         CLI   0(R2),C'9'                                                       
         BH    VALNNO                                                           
VALN030  LA    R2,1(R2)                                                         
         BCT   R3,VALN010                                                       
*                                                                               
VALNOK   B     YES                                                              
VALNNO   B     NO                                                               
VALNX    XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* BUILD SQL SUB-SYSTEM RETURN MESSAGE                                 *         
***********************************************************************         
BLDSQLM  NTR1                                                                   
*                                  BUILD SQL MESSAGE ID                         
         BAS   RE,BLDSMID                                                       
         BNE   BSQLER                                                           
*                                  BUILD CONDITION CODE                         
         BAS   RE,BLDSCON                                                       
         BNE   BSQLER                                                           
*                                  BUILD AGENCY ID                              
         BAS   RE,BLDSAID                                                       
         BNE   BSQLER                                                           
*                                  BUILD SYSTEM CODE                            
         BAS   RE,BLDSSYS                                                       
         BNE   BSQLER                                                           
*                                  BUILD SUB SYSTEM CODE                        
         BAS   RE,BLDSSUB                                                       
         BNE   BSQLER                                                           
*                                  BUILD FILE GENERATION NUMBER                 
         BAS   RE,BLDSFNUM                                                      
         BNE   BSQLER                                                           
*                                  BUILD LOAD REFRESH LEVEL CODE                
         BAS   RE,BLDSLREF                                                      
         BNE   BSQLER                                                           
*                                  BUILD DATA SET NAME                          
         BAS   RE,BLDSDSN                                                       
         BNE   BSQLER                                                           
*                                  BUILD FILE RECORD NUMBER                     
         BAS   RE,BLDSRNM                                                       
         BNE   BSQLER                                                           
*                                  BUILD FILE RECORD NUMBER                     
         BAS   RE,BLDSBNM                                                       
         BNE   BSQLER                                                           
*                                  BUILD FILE MONEY CHECK SUM                   
         BAS   RE,BLDSMTO                                                       
         BNE   BSQLER                                                           
*                                                                               
         MVC   ESQLDATX(2),=CL2'DD'                                             
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,3,=Y(ESQLDATX-ESQLDAT)                                        
         STCM  RF,3,ESSDDLN                                                     
         B     BSQLOK                                                           
*                                  EXCHANGE MESSAGE ERROR                       
BSQLER   EQU   *                                                                
         BAS   RE,DCH0                                                          
         B     BSQLNO                                                           
*                                                                               
BSQLNO   B     NO                                                               
BSQLOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* BUILD SQL MESSAGE ID                                                *         
***********************************************************************         
BLDSMID  NTR1                                                                   
*                                                                               
         MVC   ESQLMID,EIOSMID                                                  
         B     BSMIOK                                                           
*                                                                               
BSMIER   EQU   *                                                                
         BAS   RE,DCH0                                                          
         B     BSMINO                                                           
*                                                                               
BSMINO   B     NO                                                               
BSMIOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* BUILD CONDTION CODE                                                 *         
***********************************************************************         
BLDSCON  NTR1                                                                   
*                                                                               
         MVC   ESQLCON,EIOSCON                                                  
         B     BSCOOK                                                           
*                                                                               
BSCOER   EQU   *                                                                
         BAS   RE,DCH0                                                          
         B     BSCONO                                                           
*                                                                               
BSCONO   B     NO                                                               
BSCOOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* BUILD AGENCY ID                                                     *         
***********************************************************************         
BLDSAID  NTR1                                                                   
*                                                                               
         MVC   ESQLAID,EIOSMAID                                                 
         CLI   ESQLAID,C'*'                                                     
         BNE   *+8                                                              
         MVI   ESQLAID,C'@'                                                     
         B     BSAIOK                                                           
*                                                                               
BSAIER   EQU   *                                                                
         BAS   RE,DCH0                                                          
         B     BSAINO                                                           
*                                                                               
BSAINO   B     NO                                                               
BSAIOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* BUILD SYSTEM CODE                                                   *         
***********************************************************************         
BLDSSYS  NTR1                                                                   
         L     R6,=A(SYSLEX)                                                    
         USING SYSLSTD,R6                                                       
         LA    R6,6(R6)            R3=A(SYSTEM LIST)                            
*                                                                               
BSSY010  CLI   0(R6),0             TEST E-O-T                                   
         BE    BSSY022                                                          
         CLC   SYSLNUM,EIOSMSYS    MATCH ON SYSTEM NUMBER                       
         BE    BSSY030                                                          
BSSY020  LA    R6,SYSLLEN(R6)      NO - BUMP TO NEXT TABLE ENTRY                
         B     BSSY010                                                          
*                                                                               
BSSY022  L     R6,=A(SYSLST)                                                    
         LA    R6,6(R6)            R3=A(EXTENDED SYSTEM LIST)                   
*                                                                               
BSSY024  CLI   0(R6),0             TEST E-O-T                                   
         BE    BSSYER                                                           
         CLC   SYSLNUM,EIOSMSYS    MATCH ON SYSTEM NUMBER                       
         BE    BSSY030                                                          
BSSY026  LA    R6,SYSLLEN(R6)      NO - BUMP TO NEXT TABLE ENTRY                
         B     BSSY024                                                          
*                                                                               
BSSY030  MVC   ESQLSYS,SYSLRPLT    RETURN SYSTEM CHARACTER CODE                 
         B     BSSYOK                                                           
*                                                                               
BSSYER   EQU   *                                                                
         BAS   RE,DCH0                                                          
         B     BSSYNO                                                           
*                                                                               
BSSYNO   B     NO                                                               
BSSYOK   B     YES                                                              
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
* BUILD SUB SYSTEM CODE                                               *         
***********************************************************************         
BLDSSUB  NTR1                                                                   
         L     R6,=A(SUBLST)                                                    
         USING SUBLSTD,R6                                                       
         LA    R6,6(R6)            R3=A(SYSTEM LIST)                            
*                                                                               
BSSU010  CLI   0(R6),0             TEST E-O-T                                   
         BE    BSSUER                                                           
         CLC   SUBLNUM,EIOSMSUB    MATCH ON SUB-SYSTEM NUMBER                   
         BE    BSSU030                                                          
BSSU020  LA    R6,SYSLLEN(R6)      NO - BUMP TO NEXT TABLE ENTRY                
         B     BSSU010                                                          
*                                                                               
BSSU030  MVC   ESQLSUB,SUBLKEY1    RETURN SUB-SYSTEM CHARACTER CODE             
         B     BSSUOK                                                           
*                                                                               
BSSUER   EQU   *                                                                
         BAS   RE,DCH0                                                          
         B     BSSUNO                                                           
*                                                                               
BSSUNO   B     NO                                                               
BSSUOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* BUILD FILE GENERATION NUMBER                                        *         
***********************************************************************         
BLDSFNUM NTR1                                                                   
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,3,EIOSMFNM                                                    
         EDIT  (RF),(8,ESQLFNUM),ZERO=NOBLANK,FILL=0                            
         OC    EIOSLOAD,EIOSLOAD                                                
         BZ    BSFNOK                                                           
         MVC   ESQLLOAD,EIOSLOAD                                                
         B     BSFNOK                                                           
*                                                                               
BSFNER   EQU   *                                                                
         BAS   RE,DCH0                                                          
         B     BSFNNO                                                           
*                                                                               
BSFNNO   B     NO                                                               
BSFNOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* BUILDLOAD REFRESH CODE                                              *         
***********************************************************************         
BLDSLREF NTR1                                                                   
*                                                                               
         OC    EIOSLOAD,EIOSLOAD                                                
         BZ    BSLROK                                                           
         MVC   ESQLLOAD,EIOSLOAD                                                
         B     BSLROK                                                           
*                                                                               
BSLRER   EQU   *                                                                
         BAS   RE,DCH0                                                          
         B     BSLRNO                                                           
*                                                                               
BSLRNO   B     NO                                                               
BSLROK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* BUILD FILE MVS DATA SET NAME                                        *         
***********************************************************************         
BLDSDSN  NTR1                                                                   
*                                                                               
         MVC   ESQLDSN,EIOSMDSN                                                 
         B     BSDSOK                                                           
*                                                                               
BSDSER   EQU   *                                                                
         BAS   RE,DCH0                                                          
         B     BSDSNO                                                           
*                                                                               
BSDSNO   B     NO                                                               
BSDSOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* BUILD FILE RECORD NUMBER                                            *         
***********************************************************************         
BLDSRNM  NTR1                                                                   
*                                                                               
         EDIT  EIOSMRNM,(8,ESQLRNM),ZERO=NOBLANK,FILL=0                         
         B     BSRNOK                                                           
*                                                                               
BSRNER   EQU   *                                                                
         BAS   RE,DCH0                                                          
         B     BSRNNO                                                           
*                                                                               
BSRNNO   B     NO                                                               
BSRNOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* BUILD FILE BYTE NUMBER                                              *         
***********************************************************************         
BLDSBNM  NTR1                                                                   
*                                                                               
         EDIT  EIOSMBNM,(10,ESQLBNM),ZERO=NOBLANK,FILL=0                        
         B     BSBNOK                                                           
*                                                                               
BSBNER   EQU   *                                                                
         BAS   RE,DCH0                                                          
         B     BSBNNO                                                           
*                                                                               
BSBNNO   B     NO                                                               
BSBNOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* BUILD MONEY CHECKSUM                                                *         
***********************************************************************         
BLDSMTO  NTR1                                                                   
*                                                                               
         EDIT  EIOSMMTO,(12,ESQLMTO),ZERO=NOBLANK,FILL=0                        
         B     BSMTOK                                                           
*                                                                               
BSMTER   EQU   *                                                                
         BAS   RE,DCH0                                                          
         B     BSMTNO                                                           
*                                                                               
BSMTNO   B     NO                                                               
BSMTOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* GET SYSTEM CHARACTER CODE                                           *         
***********************************************************************         
GETSYSC  NTR1                                                                   
         L     R6,=A(SYSLEX)                                                    
         USING SYSLSTD,R6                                                       
         LA    R6,6(R6)            R3=A(SYSTEM LIST)                            
*                                                                               
GSYC010  CLI   0(R6),0             TEST E-O-T                                   
         BE    GSYC022                                                          
         CLC   SYSLNUM,EIOSMSYS    MATCH ON SYSTEM NUMBER                       
         BE    GSYC030                                                          
GSYC020  LA    R6,SYSLLEN(R6)      NO - BUMP TO NEXT TABLE ENTRY                
         B     GSYC010                                                          
*                                                                               
GSYC022  L     R6,=A(SYSLST)                                                    
         LA    R6,6(R6)            R3=A(EXTENDED SYSTEM LIST)                   
*                                                                               
GSYC024  CLI   0(R6),0             TEST E-O-T                                   
         BE    GSYCER                                                           
         CLC   SYSLNUM,EIOSMSYS    MATCH ON SYSTEM NUMBER                       
         BE    GSYC030                                                          
GSYC026  LA    R6,SYSLLEN(R6)      NO - BUMP TO NEXT TABLE ENTRY                
         B     GSYC024                                                          
*                                                                               
GSYC030  MVC   EIOSSYSC,SYSLRPLT   RETURN SYSTEM CHARACATER CODE                
         B     GSYCOK                                                           
*                                                                               
GSYCER   EQU   *                                                                
         BAS   RE,DCH0                                                          
         B     GSYCNO                                                           
*                                                                               
GSYCNO   B     NO                                                               
GSYCOK   B     YES                                                              
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
* GET SUB SYSTEM CHARACTER CODE                                       *         
***********************************************************************         
GETSUBC  NTR1                                                                   
         L     R6,=A(SUBLST)                                                    
         USING SUBLSTD,R6                                                       
         LA    R6,6(R6)            R3=A(SYSTEM LIST)                            
*                                                                               
GSUC010  CLI   0(R6),0             TEST E-O-T                                   
         BE    GSUCER                                                           
         CLC   SUBLNUM,EIOSMSUB    MATCH ON SUB-SYSTEM NUMBER                   
         BE    GSUC030                                                          
GSUC020  LA    R6,SYSLLEN(R6)      NO - BUMP TO NEXT TABLE ENTRY                
         B     GSUC010                                                          
*                                                                               
GSUC030  MVC   EIOSSUBC,SUBLKEY1   RETURN SUB-SYSTEM CHARACTER CODE             
         B     GSUCOK                                                           
*                                                                               
GSUCER   EQU   *                                                                
         BAS   RE,DCH0                                                          
         B     GSUCNO                                                           
*                                                                               
GSUCNO   B     NO                                                               
GSUCOK   B     YES                                                              
         DROP  R2,R3,R4                                                         
         EJECT                                                                  
***********************************************************************         
* PROCESS ERROR FROM EXSERV - ERROR CODE IN BYTE                      *         
***********************************************************************         
EXSERROR DS    0H                                                               
         SR    RE,RB                                                            
         STCM  RE,15,FULL                                                       
         MVC   P(40),=CL40'EXSERV ERROR CODE: '                                 
         GOTO1 AHEXOUT,DMCB,BYTE,P+19,1,=C'TOG'                                 
         GOTO1 AHEXOUT,DMCB,FULL,P+25,4,=C'TOG'                                 
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
*                                                                               
         MVC   ERRMSG,=CL80'X-'                                                 
         GOTO1 AHEXOUT,DMCB,BYTE,ERRMSG+2,1,=C'TOG'                             
         GOTO1 AHEXOUT,DMCB,FULL,ERRMSG+5,4,=C'TOG'                             
         LA    R6,WORK                                                          
         USING EXSERVD,R6                                                       
         MVC   ERRMSG+15(4),EXSACTN                                             
         MVC   ERRMSG+20(8),EXSEID                                              
         MVC   ERRMSG+29(2),EXSAGY                                              
*                                                                               
         USING SYSLSTD,RE                                                       
         L     RE,=A(SYSLEX)       CHECK IN TABLE OF VALID SYSTEMS              
         LA    RE,6(RE)                                                         
         LA    RE,SYSLLEN(RE)      GO PAST SERVICE SYSTEM ENTRY                 
EXSE010  CLI   SYSLNUM,0                                                        
         BE    EXSE012                                                          
         CLC   EXSSYS,SYSLNUM                                                   
         BE    EXSE020                                                          
         LA    RE,SYSLLEN(RE)                                                   
         B     EXSE010                                                          
*                                                                               
EXSE012  L     RE,=A(SYSLST)       CHECK IN EXTENDED SYSTEM TABLE               
         LA    RE,6(RE)                                                         
         LA    RE,SYSLLEN(RE)      GO PAST SERVICE SYSTEM ENTRY                 
EXSE014  CLI   SYSLNUM,0                                                        
         BE    EXSE030                                                          
         CLC   EXSSYS,SYSLNUM                                                   
         BE    EXSE020                                                          
         LA    RE,SYSLLEN(RE)                                                   
         B     EXSE014                                                          
*                                                                               
EXSE020  MVC   ERRMSG+32(1),SYSLRPLT                                            
EXSE030  EQU   *                                                                
         USING SUBLSTD,RE                                                       
         L     RE,=A(SUBLST)       CHECK IN TABLE OF VALID SYSTEMS              
         LA    RE,6(RE)                                                         
EXSE040  CLI   SUBLNUM,0                                                        
         BE    EXSE060                                                          
         CLC   EXSSUB,SUBLNUM                                                   
         BE    EXSE050                                                          
         LA    RE,SUBLLEN(RE)                                                   
         B     EXSE040                                                          
*                                                                               
EXSE050  MVC   ERRMSG+33(1),SUBLKEY1                                            
EXSE060  OC    EXSFNUM,EXSFNUM                                                  
         BZ    EXSE070                                                          
         SR    RF,RF                                                            
         ICM   RF,3,EXSFNUM                                                     
         EDIT  (RF),(5,ERRMSG+35),ZERO=NOBLANK,FILL=0                           
*                                                                               
EXSE070  OC    EXSSKEY,EXSSKEY                                                  
         BZ    EXSE080                                                          
         MVC   ERRMSG+41(8),EXSSKEY                                             
*                                                                               
EXSE080  EQU   *                                                                
         MVC   P(L'ERRMSG),ERRMSG                                               
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     EREX0001                                                         
         DROP  R6,RE                                                            
***********************************************************************         
* PROCESS DC H'0' ERROR                                               *         
***********************************************************************         
DCH0     EQU   *                                                                
         SR    RE,RB                                                            
         STCM  RE,15,FULL                                                       
         MVC   ERRMSG,=CL80'PROGRAM EXCEPTION AT OFFSET: '                      
         GOTO1 AHEXOUT,DMCB,FULL,ERRMSG+29,4,=C'TOG'                            
         B     EREX0005                                                         
*                                                                               
YES      SR    RC,RC                                                            
NO       LTR   RC,RC                                                            
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
         DS    0D                                                               
SPACES   DC    132C' '                                                          
OPMSG    DC    CL80' '                                                          
WTOALARM DC    CL2'*A'             WTO ALARM MESSAGE                            
EQESSIO  DC    CL8'EQESSIO '       MINOR RESCOURCE NAME FOR ESSIO ENQ           
         DS    0D                                                               
LOGFILE  DCB   DDNAME=LOGFILE,MACRF=(GM,PM),DSORG=PS,RECFM=FB,         +        
               LRECL=132,BLKSIZE=16500                                          
         SPACE 2                                                                
* CREATE LOGFILE DATASETS -- DYNAMIC ALLOCATION                                 
*                                                                               
         DS    0F                                                               
ARBLKCTA DC    X'80',AL3(RBLKCATA) R1 POINTS TO THIS BEFORE DYNALLOC            
*                                                                               
RBLKCATA DC    X'1401000000000000',A(ACATALLT),X'0000000000000000'              
*                                                                               
ACATALLT DC    X'00',AL3(TXTDD)                                                 
         DC    X'00',AL3(TXTDSN)                                                
         DC    X'00',AL3(TXTDISP)                                               
         DC    X'00',AL3(TXTNDISP)                                              
         DC    X'00',AL3(TXTBLKLN)                                              
         DC    X'00',AL3(TXTPRI)                                                
         DC    X'00',AL3(TXTSEC)                                                
         DC    X'00',AL3(TXTRLSE)                                               
         DC    X'80',AL3(TXTUNIT)                                               
         SPACE 2                                                                
* EXISTING LOGFILE DATASET -- DYNAMIC ALLOCATION WITH DISP MOD                  
*                                                                               
         DS    0F                                                               
ARBLKMOD DC    X'80',AL3(RBLKMOD) R1 POINTS TO THIS BEFORE DYNALLOC             
*                                                                               
RBLKMOD  DC    X'1401000000000000',A(AMODALLT),X'0000000000000000'              
*                                                                               
AMODALLT DC    X'00',AL3(TXTDD)                                                 
         DC    X'00',AL3(TXTDSN)                                                
         DC    X'80',AL3(TXTDISPM)                                              
         SPACE 2                                                                
* EXISTING EXFILE DATASET -- DYNAMIC ALLOCATION                                 
*                                                                               
         DS    0F                                                               
ARBLKEXA DC    X'80',AL3(RBLKEXTA) R1 POINTS TO THIS BEFORE DYNALLOC            
*                                                                               
RBLKEXTA DC    X'1401000000000000',A(AEXTALLT),X'0000000000000000'              
*                                                                               
AEXTALLT DC    X'00',AL3(TXTDD)                                                 
         DC    X'00',AL3(TXTDSN)                                                
         DC    X'80',AL3(TXTDISPS)                                              
         SPACE 2                                                                
* LOGFILE DATASETS -- DYNAMIC UNALLOCATION BY DDNAME                            
*                                                                               
         DS    0F                                                               
ARBLKUNA DC    X'80',AL3(RBLKUNA)  R1 POINTS TO THIS BEFORE DYNALLOC            
*                                                                               
RBLKUNA  DC    X'1402000000000000',A(ACATUNT),X'0000000000000000'               
*                                                                               
ACATUNT  DC    X'80',AL3(TXTUNDD)                                               
         SPACE 2                                                                
* DELETE LOGFILE DATASETS -- DYNAMIC ALLOCATION                                 
*                                                                               
         DS    0F                                                               
ARBLKDLA DC    X'80',AL3(RBLKDELA) R1 POINTS TO THIS BEFORE DYNALLOC            
*                                                                               
RBLKDELA DC    X'1401000000000000',A(ADELALLT),X'0000000000000000'              
*                                                                               
ADELALLT DC    X'00',AL3(TXTDSN)                                                
         DC    X'00',AL3(TXTDISPO)                                              
         DC    X'80',AL3(TXTNDISD)                                              
         SPACE 2                                                                
* LOGFILE DATASETS -- DYNAMIC UNALLOCATION BY DSN                               
*                                                                               
         DS    0F                                                               
ARBLKUN2 DC    X'80',AL3(RBLKUNA2) R1 POINTS TO THIS BEFORE DYNALLOC            
*                                                                               
RBLKUNA2 DC    X'1402000000000000',A(ACATUNT2),X'0000000000000000'              
*                                                                               
*ACATUNT2 DC    X'00',AL3(TXTDSN)                                               
*         DC    X'80',AL3(TXTREMOV)                                             
ACATUNT2 DC    X'80',AL3(TXTDSN)                                                
         SPACE 3                                                                
TXTDD    DC    AL2(DALDDNAM),X'00010008',CL8' '        DDNAME=........          
TXTDSN   DC    AL2(DALDSNAM),X'0001002C',CL44' '       DSN=............         
TXTDISPS DC    AL2(DALSTATS),X'0001000108'             DISP=(SHR,.....)         
TXTDISPM DC    AL2(DALSTATS),X'0001000102'             DISP=(MOD,.....)         
TXTDISP  DC    AL2(DALSTATS),X'0001000104'             DISP=(NEW,.....)         
TXTNDISP DC    AL2(DALNDISP),X'0001000102'                 =(...,CATLG)         
TXTDISPO DC    AL2(DALSTATS),X'0001000101'             DISP=(OLD,...)           
TXTNDISD DC    AL2(DALNDISP),X'0001000104'                 =(...,DEL)           
TXTBLKLN DC    AL2(DALBLKLN),X'00010003',AL3(16500)    SPACE=(16500,...         
TXTPRI   DC    AL2(DALPRIME),X'00010003',AL3(25)            =(..(25,...         
TXTSEC   DC    AL2(DALSECND),X'00010003',AL3(40)            =(...,40),.         
TXTRLSE  DC    AL2(DALRLSE),X'0000'                         =(...,RLSE)         
TXTUNIT  DC    AL2(DALUNIT),X'00010005',C'SYSDA'       UNIT=SYSDA               
TXTUNDD  DC    AL2(DUNDDNAM),X'00010008',CL8' '        DDNAME=........          
TXTREMOV DC    AL2(DUNREMOV),X'0000'                   REMOVE INUSE ATR         
         SPACE 2                                                                
LOGDSN   DC    CL44'XLOG.ESSNNNNN.SERVERNM.DATABASE.AAASS.FNNNNN'               
         EJECT                                                                  
         SPACE 1                                                                
       ++INCLUDE DXSUBLST                                                       
         SPACE 1                                                                
       ++INCLUDE FASYSLST                                                       
         SPACE 1                                                                
       ++INCLUDE DXSYSLEX                                                       
         SPACE 1                                                                
         EJECT                                                                  
**********************************************************************          
* WORKING STORAGE DSECT                                                         
**********************************************************************          
WORKD    DSECT                                                                  
DMCB     DS    6F                                                               
PARAS    DS    10F                                                              
DUB      DS    D                                                                
DUBG     DS    PL16                                                             
SAVERD   DS    F                                                                
FULL     DS    F                                                                
APARM    DS    A                                                                
RELO     DS    A                                                                
AESSATCB DS    A                                                                
PARM     DS    0XL(8*4)                                                         
         DS    XL(7*4)                                                          
LOGLAST  DS    A                                                                
ENDLOG   DS    A                                                                
AEXFDCB  DS    A                                                                
AESSESSD DS    A                                                                
LOOPCNT  DS    XL4                                                              
EXFDDNM  DS    CL8                                                              
ERRFLAG  DS    CL1                                                              
ESSNAME  DS    CL8                                                              
SESSNUM  DS    XL1                                                              
ESSIDNUM DS    XL2                                                              
FTPFLAG  DS    CL1                                                              
FTPSIZE  DS    XL4                                                              
RECOMFLG DS    CL1                                                              
SIMFLG   DS    CL1                                                              
HALF     DS    H                                                                
BYTE     DS    XL1                                                              
OPENRTRY DS    CL1                                                              
LOGEOF   DS    CL1                                                              
ERRMSG   DS    CL80                                                             
LOGLINE  DS    CL132                                                            
P        DS    CL132                                                            
WORK     DS    CL256                                                            
*                                                                               
WTOMSG   DS    0CL128              WTO MESSAGE AREA                             
WTOMSGK  DS    CL28                KEY PART                                     
WTOMSGD  DS    CL100               DETAIL PART                                  
*                                                                               
EXFIOL   DS    XL4                                                              
EXFIO    DS    CL10000                                                          
WORKX    EQU   *                                                                
         EJECT                                                                  
         PRINT ON                                                               
       ++INCLUDE DDEXSERVD                                                      
         EJECT                                                                  
       ++INCLUDE DDESSD                                                         
         EJECT                                                                  
       ++INCLUDE DDEIOSQLD                                                      
         EJECT                                                                  
         PRINT OFF                                                              
       ++INCLUDE FASYSLSTD                                                      
         EJECT                                                                  
       ++INCLUDE DXSUBLSTD                                                      
         EJECT                                                                  
       ++INCLUDE GEGENESS                                                       
         EJECT                                                                  
       ++INCLUDE GEGENXTR                                                       
         EJECT                                                                  
         DCBD  DSORG=PS,DEVD=DA                                                 
         EJECT                                                                  
         IEFZB4D2                                                               
         EJECT                                                                  
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005DDEIOSQL  06/07/19'                                      
         END                                                                    
