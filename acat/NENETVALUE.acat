*          DATA SET NENETVALUE AT LEVEL 032 AS OF 02/28/20                      
*CATALP NETVALUE                                                                
NETVALUE TITLE '- CONVERT UNIT REC TO NETBLOCK'                                 
         SPACE 5                                                                
***********************************************************************         
*  #20 For storage protection had to move BLDCHGD table of billed     *         
*  #20 prods no longer on the unit into Netvalue working storage -    *         
*  #20 this gets cleared on entries into Netvalue - new logic keeps   *         
*  #20 an index of product in Netblock and this is used to find the   *         
*  #20 next billed and then removed product -                         *         
*                                                                               
*  *13 MXM DEMO LOGIC                                                           
*  #12 TURNS OFF HUNOPT IF NBSURVEY=H(NETWORK PRECISION)              *         
*  #12 CHANGES LOGIC FOR CABLE IF NO AFFID -CHKS EST=ACT -            *         
*     (ABOVE LOGIC #12 FOR CABLE -EVEN BEFORE CHANGE- SEEMS           *         
*     (CONVOLUTED AND/OR WRONG BUT WE LIVE WITH IT FOR NOW)           *         
***********************************************************************         
NETVALUE RSECT                                                                  
         DS    14000C              36B0 HEX                                     
         ORG   *-14000                                                          
         NMOD1 CONVWRKX-CONVWORK,**NETVALU,R9,RR=R2,CLEAR=YES                   
         L     RA,0(R1)            ADDRESS OF NETBLOCK.                         
         USING NETBLOCK,RA                                                      
         USING CONVWORK,RC                                                      
         MVC   NLIVEXT(4),=C'NLIV'                                              
         MVC   CAVGEXT(4),=C'CAVG'                                              
         MVC   DBXPODT(4),=C'PDMS'                                              
         ST    R2,RELO                                                          
         L     RE,=A(SUBR)                                                      
         A     RE,RELO                                                          
         ST    RE,ASUBR                                                         
*                                 ESTABLISH ADDRESSABILITY                      
         LA    R1,DUMMYREC                                                      
         LA    R1,L'DUMMYREC(R1)                                                
         LA    R1,1000(R1)          XPANDED WORKSPACE AFTER DREC                
         ST    R1,AWORKREC                                                      
         LA    R1,3000(R1)                                                      
         ST    R1,ACLIREC                                                       
         EJECT                                                                  
************************************************                                
* MAIN SECTION - LOOKS AT INFO IN NETBLOCK AND CALLS APPROPRIATE                
*                  ROUTINE.                                                     
************************************************                                
*                                                                               
         L     R1,NBAIO            POINT TO RECORD                              
         CLI   0(R1),X'02'         TEST FOR PACKAGE                             
         BNE   UN2                                                              
         BAS   RE,PACKGREC                                                      
         B     XITCONV                                                          
UN2      CLI   0(R1),X'04'         TEST FOR UNIT                                
         BE    *+6                                                              
         DC    H'0'                TAKE A HIT IF UNIDENTIFIED                   
*                                                                               
         BRAS  RE,DECODEM          DECODE DEMO LIST                             
*                                                                               
         OC    NBAPROD,NBAPROD     USER PASSED ADDRESS                          
         BNZ   *+8                                                              
         MVI   APRODLCL,C'Y'       NO-SET LOCAL STORAGE SWITCH                  
*                                                                               
UN10     BAS   RE,UNITREC          PROCESS UNIT                                 
*                                                                               
         CLI   APRODLCL,C'Y'       USING LOCAL STORAGE                          
         BNE   *+14                                                             
         XC    NBAPROD,NBAPROD     YES/CLEAR ADDRESS                            
         MVI   APRODLCL,0               CLEAR SWITCH                            
*                                                                               
         BRAS  RE,ENCODEM          ENCODE DEMO LIST                             
****************************************************                            
*        SET NETBLOCK FIELDS TO ZERO IF OVERFLOW PROD                           
         TM    NBINDS6,NBI6XOVP    NBSPLPRN A FUDGE ?                           
         BZ    *+8                                                              
         MVI   NBSPLPRN,0          YES                                          
         TM    NBINDS6,NBI6X1OV    NBPRD NOT REAL ?                             
         BZ    *+8                                                              
         MVI   NBPRD,0                                                          
         TM    NBINDS6,NBI6X2OV    NBPRD2 NOT REAL ?                            
         BZ    *+8                                                              
         MVI   NBPRD2,0                                                         
*****************************************************                           
         B     XITCONV                                                          
*                                                                               
XITCONV  XMOD1 1                                                                
         SPACE 5                                                                
*****************************************************                           
* PACKGREC - PROCESS A PACKAGE RECORD.                                          
*                                                                               
* CALLED FROM - MAIN PROGRAM                                                    
*                                                                               
* LOCALS: R8 - USED (AND UPDATED) BY NEXTEL. CONTAINS THE ADDRESS OF            
*                THE CURRENT ELEMENT.                                           
*         R7 - THE ADDRESS OF THE PACKAGE RECORD. USED FOR DSECTS.              
***************************************************                             
         USING NPRECD,R7                                                        
PACKGREC NTR1                                                                   
         L     R7,NBAIO            LOCATION OF PACKAGE RECORD                   
*                                                                               
*                                  **** SET MODES AND ACTUALS                   
GVPKG    XC    NBSUBMSK,NBSUBMSK   RESET SUB MASK                               
         CLC   NBACTNET,NPKNET                                                  
         BE    GV2                                                              
         MVC   NBACTNET,NPKNET     NETWORK                                      
         OI    NBSUBMSK,NBSBMNET                                                
GV2      CLC   NBACTEST,NPKEST                                                  
         BE    GV4                                                              
         OI    NBSUBMSK,NBSBMEST                                                
         MVC   NBACTEST,NPKEST     ESTIMATE                                     
GV4      CLC   NBACTPAK,NPKPACK                                                 
         BE    GV6                                                              
         MVC   NBACTPAK,NPKPACK                                                 
         OI    NBSUBMSK,NBSBMPAK                                                
GV6      MVC   NBACTDP,NPAKDP                                                   
*                                                                               
         MVC   NBPAKNAM,NPAKNAME        PACKAGE NAME                            
         MVC   NBPAKCST,NPAKCOST        PACKAGE COST                            
         MVC   NBPAKCPM,NPAKGCPM        PACKAGE GUARANTEED CPM                  
         MVC   NBPAKINT,NPAKINT         PACKAGE INTEGRATION                     
         XC    NBDPNAM,NBDPNAM                                                  
         XC    NBDPNAM2,NBDPNAM2                                                
         EXPDP NBDPNAM,NPAKDP           EXPANDED DAYPART                        
         MVI   NBACTNDP+1,X'40'                                                 
         MVC   NBACTNDP(1),NPAKDP                                               
         CLC   =C'UNKNOWN',NBDPNAM       DID WE FIND IT ?                       
         BNE   ENDDPT                                                           
         MVC   BYTE,NPAKDP                                                      
         BAS   RE,NDPTVAL          NEW DAYPART VAL                              
         MVC   NBACTNDP,WORK       NEW 2 CHAR CODE                              
         MVC   NBDPNAM,WORK+2      SET FIRST PART OF NAME                       
         MVC   NBDPNAM2,WORK+10    SET NEXT                                     
ENDDPT   DS    0H                                                               
*                                                                               
         MVC   NBUNCODE,NPAKUNCD        UNIV CODE                               
*                                                                               
         L     R6,NBAIO                                                         
         MVI   SRCHEL,X'02'                                                     
         BAS   RE,GETEL2                                                        
         USING NPK2D,R6                                                         
*                                                                               
         USING NBXTNDD,RF                                                       
         OC    NBEXTEND,NBEXTEND                                                
         JZ    *+14                                                             
         L     RF,NBEXTEND                                                      
         MVC   NBXCVT,NPKCVTYP     COMSCORE VIEWING TYPE                        
*                                                                               
         B     MXIT                                                             
         DROP  R7,RF                                                            
         EJECT                                                                  
*                                                                               
NDPTVAL  NTR1                                                                   
         TM    NBSBKEND,NBNODPT2   READ FOR 2 CHAR DAYPART?                     
         BO    NDPTXX              NO                                           
*                                                                               
         XC    WORK(20),WORK    CHECK STANDARD AGY DPTS                         
         XC    KEYSAVE,KEYSAVE                                                  
*********************************************************                       
* IF APPLICATION PASSED ADDRESS OF DAYPART SAVE AREA                            
* READ AGY/CLIENT DAYPART RECORDS AND SET TO TABLE                              
* TABLE -> 4 BYTE AGY/MED/CLIENT/DPT MAP CODE,16 BYTE CODE/NAME                 
         ICM   R3,15,NBADPTSV   ADDRESS OF DPT SV AREA ?                        
         BZ    NDPT20           NO ADDRES PASSED                                
         XC    KEY,KEY          YES                                             
         LA    R2,KEY                                                           
         USING NDPTHDR,R2                                                       
         MVC   NDPTKTYP,=X'0D07'                                                
         MVC   NDPTAGM,NBACTAM                                                  
         MVC   NDPTCLT,NBACTCLI                                                 
         CLC   0(3,R3),KEY+2       SAME AGY/MED/CLI?                            
         BE    NDPT10              YES                                          
         LR    RE,R3            NO/CLEAR AREA AND RELOAD                        
         L     RF,=F'5200'      CLEAR FOR MAX OF 255 (X20) RECORDS              
         XCEF                                                                   
         MVC   0(3,R3),KEY+2    SET AGY/MEDIA/CLI FOR ID                        
         LA    R3,3(R3)                BUMP OVER ID                             
         LA    R4,255                  MAX NUMBER OF DPT RECS                   
         XC    KEY+3(2),KEY+3   CLEAR CLIENT SO WE START AT AGY                 
         MVC   KEYSAVE,KEY                                                      
         GOTO1 NBDM,DMCB,=C'DMRDHI',=C'UNTDIR',KEY,KEY                          
         B     NDPT05                                                           
NDPT04   GOTO1 NBDM,DMCB,=C'DMRSEQ',=C'UNTDIR',KEY,KEY                          
*                                                                               
NDPT05   CLC   KEY(3),KEYSAVE       AGY/MED/ID                                  
         BNE   NDPT10                                                           
         CLC   KEY+3(2),=2X'00'     AGENCY LEVEL DAYPART?                       
         BE    NDPT07               YES                                         
         LA    R1,NBKEY+2                                                       
         CLI   NBKEY,X'04'         UNIT?                                        
         BE    NDPT06                                                           
         CLI   NBKEY,X'84'         UNIT?                                        
         BE    NDPT06                                                           
         CLI   NBKEY,X'94'         UNIT?                                        
         BE    NDPT06                                                           
         CLI   NBKEY,X'02'         PKG                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         LA    R1,NBKEY+12         PKG                                          
NDPT06   CLC   KEY+3(2),0(R1)       NO/MAKE SURE IT'S SAME CLIENT               
         BNE   NDPT04               NO - GET NEXT                               
NDPT07   MVC   0(4,R3),NDPTAGM      AGY/MED/CLIENT/DPT MAP CODE                 
         MVC   4(2,R3),NDPTDPTA     ALPHA DAYPOART CODE                         
         MVC   6(14,R3),NDPTDES     DESCRIPTION                                 
         LA    R3,20(R3)                                                        
         BCT   R4,NDPT04                                                        
         DC    H'0'             TABLE FULL                                      
*                                                                               
* - TABLE -> 4 BYTE AGY/MED/CLIENT,2 BYTE DPT ALPHA,14 BYTE NAME                
NDPT10   DS    0H                                                               
         XC    KEY,KEY                                                          
         MVC   KEY(1),NBACTAM                                                   
         CLI   BYTE,127            CLIENT LEVEL DPT?                            
         BH    *+10                                                             
         MVC   KEY+1(2),NBACTCLI                                                
         MVC   KEY+3(1),BYTE                                                    
         L     R3,NBADPTSV                                                      
         LA    R3,3(R3)                BUMP OVER AGY/MED/CLI ID                 
NDPT12   CLC   0(4,R3),KEY                                                      
         BE    NDPT15                                                           
         LA    R3,20(R3)                                                        
         CLI   0(R3),0                                                          
         BNE   NDPT12                                                           
         DC    H'0'             MUST BE HERE!                                   
NDPT15   MVC   WORK(8),4(R3)                                                    
         MVC   WORK+8(8),12(R3)                                                 
         OC    KEYSAVE,KEYSAVE  HOW DID WE GET HERE?                            
         BZ    NDPTXX           DID NOT FILL TBL                                
         B     NDPTX            FILLED TBL/RESET UNITS                          
******************************************************                          
*                                                                               
NDPT20   DS    0H                                                               
         XC    KEY,KEY                                                          
         LA    R2,KEY                                                           
         USING NDPTHDR,R2                                                       
         MVC   NDPTKTYP,=X'0D07'                                                
         MVC   NDPTAGM,NBACTAM                                                  
         CLI   BYTE,127            CLIENT LEVEL DPT?                            
         BH    *+10                                                             
         MVC   NDPTCLT,NBACTCLI                                                 
         MVC   NDPTDPTE,BYTE                                                    
****     CLC   NBDPKSV,KEY+2       HAVE WE DONE THIS?                           
****     BNE   NDPT25                                                           
****     MVC   WORK(8),NBDPTSV1    YES-MOVE IN CODE/DESCRIPTION                 
****     MVC   WORK+8(8),NBDPTSV2  YES-MOVE IN CODE/DESCRIPTION                 
****     B     NDPTXX                                                           
NDPT25   MVC   KEYSAVE,KEY                                                      
***      MVC   NBDPKSV,KEY+2       SAVE KEY FOR CHECK                           
         GOTO1 NBDM,DMCB,=C'DMRDHI',=C'UNTDIR',KEY,KEY                          
         CLC   KEY(6),KEYSAVE                                                   
         BNE   NDPTX                                                            
         MVC   WORK(2),NDPTDPTA                                                 
         MVC   WORK+2(14),NDPTDES                                               
****     MVC   NBDPTSV1,WORK    SAVE CODE/DESCRIPTION                           
****     MVC   NBDPTSV2,WORK+8  SAVE CODE/DESCRIPTION                           
NDPTX    DS    0H                  RESET DIRECTORY POINTER                      
         XC    KEY,KEY                                                          
         MVC   KEY(20),NBKEY                                                    
         MVI   BYTE,0                                                           
         TM    NBDMGOPT,X'08'      DO THEY NEED DELETED RECORDS?                
         BNO   *+8                                                              
         OI    BYTE,X'08'                                                       
         GOTO1 NBDM,DMCB,(BYTE,=C'DMRDHI'),=C'UNTDIR',KEY,KEY                   
NDPTXX   B     MXIT                                                             
*                                                                               
****************************************************                            
         EJECT                                                                  
***************************************************                             
* UNITREC - READS UNIT RECORD IN THE I/O AREA AND FILL IN APPROPRIATE           
*             FIELDS IN THE NETBLOCK DSECT. SUMS UP ELEMENTS.                   
*                                                                               
* CALLED FROM: MAIN PROGRAM.                                                    
*                                                                               
* CALLS TO: NEXTEL - MACRO WHICH PROCESSES ELEMENT RECORDS.                     
*           DODEMS - GETS REQUESTED DEMOS INTO NETBLOCK                         
*                                                                               
* LOCALS: R8 - USED (AND UPDATED) BY NEXTEL. CONTAINS THE ADDRESS OF            
*               THE RECORD OR THE CURRENT ELEMENT.                              
*         R7-  THE ADDRESS OF THE UNIT RECORD. USED FOR DSECTS.                 
*         SRCHEL - THE ELEMENT TYPE WHICH NEXTEL WILL SEARCH FOR.               
*                   X'00' MEANS SEARCH ALL.                                     
**********************************************************                      
*                                                                               
         USING NURECD,R7                                                        
UNITREC  NTR1                                                                   
*                                                                               
         MVC   NBDTADSP,=H'27'     IN CASE RESET                                
*                                                                               
         BRAS  RE,CHKCS            CHECK IF UNIT IS COMSCORE                    
*                                                                               
         BRAS  RE,UNVTYP           CHECK IF UNIT HAS VTYPE                      
*                                                                               
         TM    NBINDS3,NBI3A2DC           2 DECIMAL AGENCY?                     
         BNO   NOT2DC                      NO                                   
         TM    NBSBKEND,NBEXPDM6+NBEXPDEM  YES/ ALSO EXPDM REQUEST?             
         BZ    *+12                             NO                              
         NI    NBINDS3,X'FF'-NBI3A2DC           YES-TURN OF 2 DEC AGY           
         B     NOT2DC                                                           
*                                                                               
         NI    NBINDS3,X'FF'-NBI3B2DC     YES/CLEAR NEW BOOK BITS               
         L     R6,NBAIO                                                         
         MVI   SRCHEL,X'5D'                                                     
         BAS   RE,GETEL2                                                        
         BNE   NOT2DC                                                           
         USING NUBKD,R6                                                         
         CLC   NUBKBOOK,=X'5901'                                                
         BL    NOT2DC                                                           
         OI    NBINDS3,NBI3B2DC    SET NEW 2 DEC BOOK                           
         DROP  R6                                                               
*                                                                               
NOT2DC   DS    0H                                                               
         NI    NBINDS4,X'FF'-NBI4BRK                                            
         L     R6,NBAIO                                                         
         MVI   SRCHEL,X'60'        CHECK BREAKOUT INDICATOR                     
         BAS   RE,GETEL2                                                        
         BNE   NOTBRK                                                           
BRK10    CLI   2(R6),C'X'          BREAKOUT?                                    
         BE    BRKOUT                                                           
         BAS   RE,NEXTEL2                                                       
         BE    BRK10                                                            
         B     NOTBRK                                                           
BRKOUT   CLI   3(R6),C'Y'          BREAKOUT=Y                                   
         BNE   NOTBRK                                                           
         OI    NBINDS4,NBI4BRK                                                  
NOTBRK   DS    0H                                                               
*                                                                               
*                                                                               
* CHECK PROFILE IF LATEST REQUESTED                                             
         TM    NBINDS3,NBI3REVO    REVOPT SET BY APPLICATION?                   
         BO    GV11                YES/DO NOT TOUCH                             
         MVI   NBREVOPT,0                                                       
         LA    R7,NBUSER1+6        SYNDICATION PROFILE                          
         CLI   NBSURVEY,C'S'                                                    
         BE    GV10                                                             
         LA    R7,1(R7)            NETWORK PROFILE                              
         CLI   NBSURVEY,C'N'                                                    
         BE    GV10                                                             
         CLI   NBSURVEY,C'H'                                                    
         BE    GV10                                                             
         LA    R7,1(R7)            CABLE PROFILE                                
GV10     CLI   0(R7),C'L'                                                       
         BNE   *+8                                                              
         MVI   NBREVOPT,C'A'                                                    
*                                                                               
GV11     MVI   ACTSEXST,C'F'       SET DEMO FLAGS FLASE                         
         MVI   ESTSEXST,C'F'       SET TRUE IF DEMO ELEMENTS ON RECORD          
         L     R7,NBAIO            I/O AREA CONTAINING UNIT RECORD              
*                                                                               
*                                  *** SET MODES AND ACTUALS                    
         CLI   0(R7),X'04'         CHECK FOR NORMAL (X'04') OR                  
         BNE   GVUNPASV              PASSIVE (X'84') KEY                        
*                                                                               
         XC    NBSUBMSK,NBSUBMSK                                                
         MVC   NBACTDAT,NUKDATE                                                 
         MVC   NBACTSQH,NUKTIME                                                 
         MVC   NBACTDP,NUKDP                                                    
         MVC   NBACTSUB,NUKSUB                                                  
*                                                                               
         CLC   NBACTNET,NUKNET                                                  
         BE    GV12                                                             
         OI    NBSUBMSK,NBSBMNET                                                
         MVC   NBACTNET,NUKNET                                                  
GV12     CLC   NBACTPRG,NUKPROG                                                 
         BE    GV14                                                             
         OI    NBSUBMSK,NBSBMPRG                                                
         MVC   NBACTPRG,NUKPROG                                                 
GV14     CLC   NBACTEST,NUKEST                                                  
         BE    GV16                                                             
         MVC   NBACTEST,NUKEST                                                  
         OI    NBSUBMSK,NBSBMEST                                                
GV16     CLC   NBACTCLI,NUKCLT                                                  
         BE    GV18                                                             
         MVC   NBACTCLI,NUKCLT                                                  
         OI    NBSUBMSK,NBSBMCLI                                                
GV18     DS    0H                                                               
* SET BILLED INTEGRATION PRODUCT                                                
         XC    INTPROD,INTPROD                                                  
         CLI   NBINTPRD,X'FF'      ALREADY DONE                                 
         BE    GV18AC                  AND LOOK NO FURTHER                      
         MVI   NBINTPRD,0                                                       
         L     R6,NBAIO                SPECIAL BILLED INTEGRATION?              
         MVI   SRCHEL,X'60'                                                     
         BAS   RE,GETEL2                                                        
         B     *+8                                                              
GV18A    BAS   RE,NEXTEL2                                                       
         BNE   GV18AC                                                           
         CLI   2(R6),C'N'          INTEGRATION ?                                
         BNE   GV18A                                                            
         MVC   NBINTPRD,6(R6)      SET CODE                                     
         MVC   INTPROD,3(R6)       SET 3 CHAR CODE                              
         NI    NBINDS6,X'FF'-NBI6XINT                                           
         CLI   NBINTPRD,0          IS IT OVERFLOW PROD?                         
         BNE   *+8                                                              
         OI    NBINDS6,NBI6XINT                                                 
*                                                                               
GV18AC   B     GVEND                                                            
*                                                                               
         EJECT                                                                  
*                                                                               
GVUNPASV DC    H'0'                HOW DOES IT EVER GET HERE?                   
         XC    NBSUBMSK,NBSUBMSK   PASSIVE KEY                                  
         MVC   NBACTDAT,NUKPDATE                                                
         XC    NBACTSQH,NBACTSQH   NO START QUARTER FOR PROGS                   
         MVC   NBACTDP,NUKPDP                                                   
         MVC   NBACTSUB,NUKPSUB                                                 
*                                                                               
         CLC   NBACTNET,NUKPNET                                                 
         BE    GV40                                                             
         OI    NBSUBMSK,NBSBMNET                                                
         MVC   NBACTNET,NUKPNET                                                 
GV40     CLC   NBACTPRG,NUKPPROG                                                
         BE    GV44                                                             
         OI    NBSUBMSK,NBSBMPRG                                                
         MVC   NBACTPRG,NUKPPROG                                                
GV44     CLC   NBACTEST,NUKPEST                                                 
         BE    GV46                                                             
         MVC   NBACTEST,NUKPEST                                                 
         OI    NBSUBMSK,NBSBMEST                                                
GV46     DS    0H                                                               
*                                                                               
GVEND    DS    0H                                                               
                                                                                
*    SET UP NBMAINEL IN NETBLOCK                                                
         XC    NBSPLPR3,NBSPLPR3                                                
         XC    NBPR1CL3,NBPR1CL3                                                
         XC    NBPR2CL3,NBPR2CL3                                                
         CLI   NBREROPT,0          CHECK FOR RERATE OPTION                      
         BE    *+10                                                             
         MVC   NUPOSTDT,NBREROPT   FORCE LOOK UP                                
         ZIC   R3,NUMAINLN         GET LENGTH OF MAIN ELEMENT                   
         BCTR  R3,0                DECREMENT LENGTH FOR EX                      
         EXMVC R3,NBMAINEL,NUMAINEL   MOVE MAIN ELEMENT TO NETBLOCK             
         CLI   NBPRD,0                                                          
         BE    ENDMAIN                                                          
         MVC   BYTE,NBPRD                                                       
         BRAS  RE,GETPRD                                                        
         MVC   NBPR1CL3,WORK       SET 3 CHAR PROD                              
         CLI   NBPRD2,0                                                         
         BE    ENDMAIN                                                          
         MVC   BYTE,NBPRD2                                                      
         BRAS  RE,GETPRD                                                        
         MVC   NBPR2CL3,WORK                                                    
ENDMAIN  DS    0H                                                               
****     CLC   0(20,R1),=X'04E3999CDC7404C6D6D6C4F8F0C7E9F0F03401C3'            
****     BNE   *+8                                                              
****     MVI   BYTE,1                                                           
*                                                                               
* IF X'19' ELEM PRESENT, THAT TAKES PRECEDENCE OVER ALL ELSE.  USE              
* THE X'19' AND LOOK NO FURTHER -                                               
*                                                                               
         XC    NBPRDLST,NBPRDLST                                                
         XC    NBPRDNO,NBPRDNO                                                  
         ICM   R1,15,NBAPROD          CLEAR MUTLI PROD AREA                     
         BZ    NOCLEAR                                                          
         XC    0(30,R1),0(R1)                                                   
*                                                                               
NOCLEAR  NI    NBINDS6,X'FF'-(NBI6X1OV+NBI6X2OV)                                
         L     R6,NBAIO                                                         
         MVI   SRCHEL,X'19'                                                     
         BAS   RE,GETEL2                                                        
         BNE   GV70                                                             
         USING NUPDEEL,R6                                                       
         ZIC   R1,NUPDELEN         HOW MANY PRODS?                              
         S     R1,=F'3'                                                         
         SR    R0,R0                                                            
         D     R0,=F'7'                                                         
         C     R1,=F'2'                                                         
***      BH    GV50                                                             
         BNH   GV47                                                             
         CLC   NBSELPRD,=C'POL'  IF POL FILLIN NBPR1CL3 ETC ALSO                
         BNE   GV50              SPECIAL FOR NPAY                               
*                                                                               
*                             AT LEAST 1                                        
GV47     MVI   NBPRD,1             FAKE 1 CHAR PROD                             
         MVC   NBPR1CL3,NUPDEPR     PROD ALPHA    NETBLOCKD                     
         MVC   WORK(3),NBPR1CL3                                                 
         BRAS  RE,GETPRD2          GET 1 CHAR PROD CODE                         
         CLI   BYTE,0                                                           
         BE    GV47B                                                            
         MVC   NBPRD,BYTE          IT'S NOT PROD OVERFLOW                       
         B     *+8                 JUMP OVE FLAG                                
GV47B    OI    NBINDS6,NBI6X1OV    SET NBPRD FALSE                              
*                                                                               
         TM    NUPDEIND,X'C0'      * IF COPYSPLIT/TRIGBK USE PCT/FEED           
         BZ    GV47C               * FROM X'01' SET EARLIER                     
         MVC   NBP1SHR,NUPDEPCT    * ELSE DOLLAR PCT FROM X'19'                 
         MVC   NBFEED,NUPDEFD      * ELSE FEED XYZ FORM X'19'                   
*                                                                               
GV47C    MVI   NBPRD2,0                                                         
         NI    NBINDS6,X'FF'-NBI6X2OV                                           
         XC    NBPR2CL3,NBPR2CL3                                                
         C     R1,=F'2'            ANY MORE PRODS?                              
         BL    GV60                                                             
         MVI   NBPRD2,2            FAKE 2ND PROD                                
         MVC   NBPR2CL3,NUPDEPR+7   SET 2ND PROD   NETBLOCKD                    
         MVC   WORK(3),NBPR2CL3                                                 
         BRAS  RE,GETPRD2                                                       
         CLI   BYTE,0              OVERFLOW PROD?                               
         BE    GV49                YES                                          
         MVC   NBPRD2,BYTE         NO                                           
         B     *+8                                                              
GV49     OI    NBINDS6,NBI6X2OV    SET NBPRD2 FALSE                             
         B     GV60                                                             
*                                                                               
GV50     DS    0H                                                               
         STC   R1,NBPRDNO          SET NUMBER OF MULTIPLE PRODUCTS              
*                                                                               
         ICM   R3,15,NBAPROD          IF USER SUPPLIED                          
         BNZ   *+8                 USE IT                                       
         LA    R3,PRODTBL          NBAPROD                                      
         ST    R3,NBAPROD          FOR LOCAL USE                                
         USING NXBLKD,R3                                                        
         MVC   NXBIND,NUPDEIND    SET INDICATOR                                 
         LA    R6,NUPDEPR                                                       
         USING NUPDEPR,R6                                                       
         LA    R3,NXPRDS                                                        
         USING NXPRDS,R3                                                        
GV55     MVC   NXPCL3,NUPDEPR    3 CHAR PROD                                    
         MVC   NXPFD,NUPDEFD     PROD FEED                                      
         MVC   NXPPCT,NUPDEPCT   PROD PCT                                       
         MVC   WORK(3),NXPCL3                                                   
         MVI   BYTE,0                                                           
         BRAS  RE,GETPRD2                                                       
         CLI   BYTE,0              NO MATCH                                     
         BE    *+10                                                             
         MVC   NXPCL1,BYTE         SET 1 CARH PROD CODE                         
         LA    R3,NXPPLENE(R3)     BUMP PRODTABLE                               
         LA    R6,7(R6)            HARD CODE                                    
         BCT   R1,GV55                                                          
         DROP  R6,R3                                                            
*    NOW CHECK PRODTABLE AND SEE IF ALL PRODS HAVE BOTH                         
*    1 AND 3 CHARACTER PRODS - THOSE THAT DON'T, GIVE THEM                      
*    'FUDGED' 1 CHAR PRODS AND SET NXPTYP=Y TO SAY THAT                         
*    THIS PARTICULAR 1 CHAR IS A FUDGE FOR INTERNAL NETVAL USE                  
         ZIC   R2,NBPRDNO          GET NUMBER OF MULTI PRODS                    
         L     R3,NBAPROD                                                       
         USING NXBLK,R3                                                         
         LA    R3,NXPRDS                                                        
         USING NXPRDS,R3                                                        
GV57     CLI   NXPCL1,0                                                         
         BNE   GV58                                                             
         BRAS  RE,PRDFUDGE                                                      
         MVC   NXPCL1,BYTE                                                      
         MVI   NXPTYP,C'Y'         SET FUDGE FLAG                               
GV58     LA    R3,NXPPLENE(R3)                                                  
         BCT   R2,GV57                                                          
* MOVE 1 BYTE PRODS TO NBPRDLST                                                 
         L     R3,NBAPROD                                                       
         USING NXBLK,R3                                                         
         LA    R3,NXPRDS                                                        
         USING NXPRDS,R3                                                        
         ZIC   R1,NBPRDNO                                                       
         LA    R2,NBPRDLST                                                      
GV59     MVC   0(1,R2),NXPCL1                                                   
         LA    R2,1(R2)                                                         
         LA    R3,NXPPLENE(R3)                                                  
         BCT   R1,GV59                                                          
         B     GV90                                                             
*                                                                               
GV60     DS    0H                                                               
         B     GV90                                                             
************************************************************                    
* - CHECK IF THERE ARE X'14' MULTIPROD ELEMS -                                  
* - FILL NBPRDLST WITH UNITS PRODUCTS                                           
GV70     XC    NBPRDLST,NBPRDLST                                                
         XC    NBPRDNO,NBPRDNO                                                  
****8    XC    NBAPROD,NBAPROD                                                  
         L     R6,NBAIO                                                         
         LA    R6,NUMAINEL                                                      
GV75     ZIC   R1,1(R6)                                                         
         LTR   R1,R1                                                            
         BZ    GV90                                                             
         AR    R6,R1                                                            
         CLI   0(R6),X'14'         PRODUCT ELEMENT                              
         BH    GV90                CAN'T FIND/SKIP                              
         BNE   GV75                                                             
         ICM   R1,15,NBAPROD       IF USER SUPPLIED                             
         BNZ   *+8                                                              
         LA    R1,PRODTBL          ADDR OF PRODTBL                              
         ST    R1,NBAPROD          NEW PRODUCT TABLE                            
         BAS   RE,FRMTPTBL         FORMAT NEW PROD TABLE                        
         B     GV90                                                             
         DROP  R2                                                               
*                                                                               
* SETS NBPRDNO AND NBPRDLST                                                     
* SETS NEW PRODTBL                                                              
* R6 POINTS TO 1ST  X'14' ELEMENT                                               
FRMTPTBL NTR1                                                                   
*                                                                               
         USING NUPRDD,R6                                                        
         LA    R2,NBPRDLST                                                      
         ZIC   R1,NUPRDLEN         GET NUMBER OF PRODUCTS IN ELEM               
         S     R1,=F'3'            ELEM ID/LEN/INDICATOR                        
         SR    R0,R0                                                            
         D     R0,=F'6'            PROD DATA LEN = 6                            
         STC   R1,NBPRDNO          SET NUMBER OF MULTIPLE PRODUCTS              
*                                                                               
***      LA    R3,PRODTBL          NEW PROD TABLE                               
         L     R3,NBAPROD          NEW PROD TABLE                               
         USING NXBLKD,R3                                                        
         MVC   NXBIND,NUPRDIND    SET INDICATOR                                 
         LA    R3,NXPRDS                                                        
         USING NXPRDS,R3                                                        
FRMT20   MVC   NXPCL1,NUPRDPR    1 CHAR PROD                                    
         MVC   NXPFD,NUPRDFD    PROD FEED                                       
         MVC   NXPPCT,NUPRDPCT   PRODPCT                                        
         MVC   0(1,R2),NUPRDPR     SET ONE BYTE PRDCODE -> NBPRDLST             
         MVC   BYTE,0(R2)        BYTE=1 CHAR PROD, WORK RETURNS 3 CHAR          
         BRAS  RE,GETPRD                                                        
         MVC   NXPCL3,WORK                                                      
         LA    R3,NXPPLENE(R3)     BUMP PRODTABLE                               
         LA    R6,6(R6)            BUMP X'14' TO NXT PROD                       
         LA    R2,1(R2)            BUMP NBPRDLST (OLD 1 CHAR PRODLST)           
         BCT   R1,FRMT20                                                        
*                                                                               
         XIT1                                                                   
         DROP  R6,R3                                                            
*                                                                               
GV90     DS    0H                                                               
*                                                                               
* IF NBADPLST PASSED - THEN IF MULTIPRODS, FILL WITH                            
* 3 CHAR PROD CODE                                                              
*                                                                               
         CLI   NBPRDNO,0           MULTIPRODS?                                  
         BE    GV91                                                             
         ICM   R4,15,NBADPLST                                                   
         BZ    GV91                                                             
* MOVE 31 BYTE PRODS TO NBADPLST                                                
         L     R3,NBAPROD                                                       
         USING NXBLK,R3                                                         
         LA    R3,NXPRDS                                                        
         USING NXPRDS,R3                                                        
         ZIC   R1,NBPRDNO                                                       
GV90A    MVC   0(3,R4),NXPCL3                                                   
         LA    R4,3(R4)                                                         
         LA    R3,NXPPLENE(R3)                                                  
         BCT   R1,GV90A                                                         
         DROP  R3                                                               
                                                                                
GV91     DS    0H                                                               
* LOAD INFORMATION FROM 02 ELEMENT                                              
         XC    NBUNST3(18),NBUNST3                                              
****     LA    R3,NUMAINEL+80      DUMB!                                        
         LA    R3,NUMAINEL                                                      
         ZIC   R1,1(R3)                                                         
         LTR   R1,R1                                                            
         BNZ   *+6                                                              
         DC    H'0'                NUMAINEL MUST EXIST                          
         AR    R3,R1                                                            
         CLI   0(R3),X'02'         DOES 02 ELEMENT EXIST                        
         BNE   *+10                NO BYPASS MOVE                               
         MVC   NBUNST3(18),2(R3)   MOVE INFO OUT                                
                                                                                
* FOR TRAFFIC, DON'T SET NBUNST3                                                
         CLI   NBESTOPT,X'40'      DO WE NEED DEMOS?                            
         BH    *+12                                                             
         CLI   NBACTOPT,X'40'      DO WE NEED DEMOS?                            
         BNH   NOTRIGX                                                          
         L     R1,NBAPROD                                                       
***      TM    2(R1),X'80'         IS IT TRIGIBACK                              
         TM    1(R1),X'80'         WITH PRODTBL DSECT                           
         BNO   *+8                                                              
         OI    NBUNST3,X'41'       YES/COPYSPLIT+DO GRPS                        
NOTRIGX  EQU   *                                                                
* LOAD ALL SPECIAL CHARGES                                                      
         SR    R1,R1                                                            
         LA    R6,NUMAINEL                                                      
         USING NUSPRD,R6                                                        
SPNEXT   ZIC   R3,1(R6)                                                         
         CLI   1(R6),0                                                          
         BE    SPEND                                                            
         AR    R6,R3                                                            
         CLI   0(R6),0                                                          
         BE    SPEND                                                            
         CLI   0(R6),X'03'                                                      
         BNE   SPNEXT                                                           
         MVC   FULL,NUSPRAMT                                                    
         A     R1,FULL                                                          
         B     SPNEXT                                                           
SPEND    STCM  R1,15,NBSPCHRG                                                   
         DROP  R6                                                               
* LOAD EARNED COST                                                              
         XC    NBERNCST,NBERNCST                                                
         XC    NBMAXCST,NBMAXCST                                                
         LA    R6,NUMAINEL                                                      
         USING NUMCSTD,R6                                                       
ECNEXT   ZIC   R3,1(R6)                                                         
         CLI   1(R6),0                                                          
         BE    ECEND                                                            
         AR    R6,R3                                                            
         CLI   0(R6),0                                                          
         BE    ECEND                                                            
         CLI   0(R6),X'15'                                                      
         BNE   ECNEXT                                                           
         MVC   NBERNCST,NUMCEIC    EARNED COST                                  
         MVC   NBMAXCST,NUMCSMCS   MAX    COST                                  
         NI    NBINDS2,X'FF'-NBERNCOV    TURN OFF FLAG                          
         TM    NUMCSTAT,X'80'      EARNED COST OVERRIDE?                        
         BNO   ECEND                                                            
         OI    NBINDS2,NBERNCOV                                                 
ECEND    EQU   *                                                                
         DROP  R6                                                               
*                                                                               
         LA    R6,NUMAINEL          ICOS2                                       
         USING NUCOS2D,R6                                                       
ICOSNXT  ZIC   R3,1(R6)                                                         
         CLI   1(R6),0                                                          
         BE    ICOSEND                                                          
         AR    R6,R3                                                            
         CLI   0(R6),0                                                          
         BE    ICOSEND                                                          
         CLI   0(R6),X'74'                                                      
         BNE   ICOSNXT                                                          
         MVC   NBICOS2,NUC2AINT    ICOS2 AMOUNT                                 
ICOSEND  EQU   *                                                                
         DROP  R6                                                               
*                                                                               
         LA    R6,NUMAINEL          VENDOOR/AIRING STATION                      
         USING NUCOS2D,R6                                                       
         NI    NBINDS3,X'FF'-NBI3AIRV                                           
         TM    NBINDS7,NBI7AIRD    IGNORE AIRED VENDOR?                         
         BO    VNDSEND             YES                                          
VNDSNXT  ZIC   R3,1(R6)                                                         
         CLI   1(R6),0                                                          
         BE    VNDSEND                                                          
         AR    R6,R3                                                            
         CLI   0(R6),0                                                          
         BE    VNDSEND                                                          
         CLI   0(R6),X'60'                                                      
         BNE   VNDSNXT                                                          
         CLI   2(R6),C'V'              VENDING ?                                
         BNE   VNDSNXT                                                          
         OI    NBINDS3,NBI3AIRV        VENDOR STATION INDICATOR                 
         MVC   NBNTISTA,7(R6)          VENDOR STATION                           
         MVC   NBSTATYP,11(R6)         STATION TYPE                             
         MVC   NBPOSTYP,12(R6)         POSTING TYPE                             
         MVC   NBSURVEY,12(R6)         POSTING TYPE ***                         
VNDSEND  EQU   *                                                                
         DROP  R6                                                               
*                                                                               
         TM    NBUNITST,X'80'      FOR MINUS UNIT                               
         BZ    UR1                                                              
         L     R1,NBACTUAL         NEGATIVE ACT, ASS AND INTEG COSTS            
         LNR   R1,R1               (*** NOTE: DONT COMPLEMENT BECAUSE           
         ST    R1,NBACTUAL          WE WANT TO HANDLE IT PROPERLY EVEN          
         L     R1,NBASSIGN          IF THE COSTS ARE ALREADY NEGATIVE)          
         LNR   R1,R1                                                            
         ST    R1,NBASSIGN                                                      
***********************************                                             
* IF INTEGRATION HAS SPECIAL PRODUCT                                            
* NBSPLPRN MUST = SPECIAL INT PROD ELSE SKIP INTEG $$$                          
**       CLI   NBINTPRD,0                  XYZ CHECK                            
**       BE    UR0                                                              
***      CLC   NBSPLPRN,NBINTPRD                                                
***      BNE   UR1                                                              
         CLI   INTPROD,0                   XYZ CHECK                            
         BE    UR0                                                              
         CLC   NBSPLPR3,INTPROD     LET'S USE CL3S                              
         BNE   UR1                                                              
**********************************                                              
UR0      L     R1,NBINTEG                                                       
         LNR   R1,R1                                                            
         ST    R1,NBINTEG                                                       
         SPACE 1                                                                
UR1      MVI   SPLITLEN,0          PRECLEAR SPLIT PERCENTAGES                   
         XC    SPLITCST,SPLITCST                                                
         XC    SPLITPRE,SPLITPRE                                                
         CLI   NBPRD2,0            IF 2 PRODUCTS                                
         BE    UR1PT2                                                           
         OC    NBP1SHR,NBP1SHR     AND NO PRODUCT SHARE ASSIGNED                
         BNZ   UR1PT2                                                           
*                                                                               
UR1PT1   TM    NBUNST2,X'04'       AND 1ST IS NOT ZERO                          
         BO    UR1PT2                                                           
         MVC   NBP1SHR,=H'5000'    THEN SET P1SHR TO 50%  (2 DEC)               
         SPACE 1                                                                
UR1PT2   DS    0H                                                               
         TM    NBSPLOPT,X'80'      IS SPLIT OPTION SET                          
         BNO   UR1PT5                                                           
*                                                                               
UR1PT2B  BAS   RE,SPLIT                                                         
         CLI   NBSPLPRN,0          MAY BE NO MORE                               
         BE    MXIT                                                             
         SPACE 1                                                                
*                                                                               
UR1PT5   EQU   *                                                                
         XC    NBDPNAM,NBDPNAM                                                  
         XC    NBDPNAM2,NBDPNAM2                                                
         EXPDP NBDPNAM,NUKDP       MOVE EXPANDED DAYPART TO NETBLOCK            
         MVI   NBACTNDP+1,X'40'                                                 
         MVC   NBACTNDP(1),NUKDP                                                
         CLC   =C'UNKNOWN',NBDPNAM DID WE FIND IT?                              
         BNE   ENDDPT2                                                          
         MVC   BYTE,NUKDP                                                       
         BAS   RE,NDPTVAL          NEW DAYPART VAL                              
         MVC   NBACTNDP,WORK       NEW 2 CHAR CODE                              
         MVC   NBDPNAM,WORK+2      SET FIRST PART OF NAME                       
         MVC   NBDPNAM2,WORK+10    SET NEXT                                     
ENDDPT2  DS    0H                                                               
*                                                                               
         BCDAY NBDAYNAM,NUDAY      MOVE CHARACTER DAY TO NETBLOCK               
         XC    NBSUBOUT,NBSUBOUT                                                
         CLI   NBACTSUB,1          SUBPRT IN PRINTABLE FORM                     
         BE    UR2                                                              
         MVI   NBSUBOUT,C'-'                                                    
         LA    R2,NBSUBOUT+1                                                    
         EDIT  NBACTSUB,(2,0(R2)),ALIGN=LEFT                                    
UR2      MVC   NBCALCOS,NBACTUAL    IF NBUSER+8=Y AND NBASSIGN<>0               
         CLI   NBUSER+8,C'Y'          NBCALCOS=NBASSIGN                         
         BNE   UR4                  ELSE                                        
         OC    NBASSIGN,NBASSIGN      NBCALCOS=NBACTUAL                         
         BNZ   UR3                                                              
         TM    NBUNITST,X'08'      WAS ASS COST INPUT                           
         BNO   UR4                                                              
UR3      MVC   NBCALCOS,NBASSIGN                                                
UR4      DS    0H                   IF PRE-EMPTED OR MISSED                     
         TM    NBUNITST,X'42'                                                   
         BZ    UR6                                                              
         XC    NBCALCOS,NBCALCOS       CALCOS                                   
         XC    NBINTEG,NBINTEG         AND INTEGRATION ARE ZERO                 
UR6      DS    0H                                                               
*                                                                               
***** SINCE THIS DATA IS NOT ALWAYS FILLED IN, IT MUST BE CLEARED.              
         XC    NBBILTGR(NBSUMLEN),NBBILTGR   CLEAR THE SUM AREAS                
         XC    NBMGFPCD(NBMGFLEN),NBMGFPCD   CLEAR MAKE GOOD FOR AREA           
         XC    NBMGBPCD(NBMGBLEN),NBMGBPCD   CLEAR MAKE GOOD BY AREA            
         XC    NBESTUN(16),NBESTUN     CLEAR DEMO AREA                          
         XC    NBACTUN(16),NBACTUN     CLEAR DEMO AREA                          
         XC    NBGUAFAC,NBGUAFAC             CLEAR GUARANTEE                    
         XC    NBNGUFAC,NBNGUFAC             CLEAR GUARANTEE                    
         OC    NBADEM,NBADEM       IF DEMO BLOCK GIVEN                          
         BZ    UR10                                                             
         TM    NBINDS6,NBI6XDEM    EXPANDED DEMO BLOCK?                         
         BO    UR8                                                              
         L     R2,NBADEM          NO -                                          
         USING NDDEMBLK,R2                                                      
         LA    RF,NDDEMLEN                                                      
         XCEF  NDESTDEM,(RF)                                                    
         DROP  R2                                                               
         B     UR10                                                             
UR8      L     R2,NBADEM          YES -                                         
         USING XDDEMBLK,R2                                                      
         LA    RF,XDDEMLEN                                                      
         XCEF  XDESTDEM,(RF)                                                    
         DROP  R2                                                               
*-GET NAD DEMO INFORMATION                                                      
UR10     XC    DINLIST(DINEND-DINLIST),DINLIST                                  
         B     UR15                                                             
***-WAITING FOR FLAG TO BE SET BY NETW TEAM -                                   
         MVI   NADFLG,C'Y'         !!! PATCH FOR NOW                            
         LA    R4,NDMAXDEM         MAX NO OF DEMOS(LENGTH 3 EA)                 
         TM    NBINDS6,NBI6XDEM    EXPANDED DEMO BLOCK?                         
         BNO   *+8                                                              
         LA    R4,XDMAXDEM         YES-50 MAX DEMOS                             
         L     R3,NBADEM                                                        
         GOTO1 =A(XTROUTIN),DMCB,(3,(RC)),RR=RELO   CABNCAT                     
UR15     CLI   NBESTOPT,0                                                       
         BNE   *+12                                                             
         CLI   NBACTOPT,0                                                       
         BE    *+8                                                              
         BAS   RE,BLDNADDM                                                      
*                                                                               
         TM    NBINDS5,X'02'       COMING FROM AGENCY SUMMARY?                  
         BO    XITUNIT             THEN GET OUT HERE                            
*                                                                               
         LA    R8,NUMAINEL               GET FIRST ELEMENT                      
         ST    R8,ELEM1            SAVE 1ST ELEMENT FOR DEMAND                  
*                                                                               
*                                                                               
URLOOP   CLI   0(R8),X'10'         BILLING ELEMENT                              
         BE    UNITBILL                                                         
         CLI   0(R8),X'12'         PAY ELEMENT                                  
         BE    UNITPAY                                                          
         CLI   0(R8),X'06'         MAKE-GOOD-FOR ELEMENT                        
         BE    UNITMGF                                                          
         CLI   0(R8),X'07'         MAKE-GOOD-BY ELEMENT                         
         BE    UNITMGB                                                          
         CLI   0(R8),X'33'         ESTIMATE VPH DEMO ELEM                       
         BE    UNITEST                                                          
         CLI   0(R8),X'35'         ESTIMATE HOME DATA ELEM                      
         BE    UNITHOME                                                         
         CLI   0(R8),X'43'         ACTUAL VPH DEMO ELEM                         
         BE    UNITACT                                                          
         CLI   0(R8),X'B1'         PACKAGE GUARANTEE ELEMENT                    
         BE    UNITGUAR                                                         
         CLI   0(R8),X'B3'         NEW PACKAGE GUARANTEE ELEMENT                
         BE    UNITNGUA                                                         
         CLI   0(R8),X'DE'         DEMO OVERRIDE (ACT) ELEM                     
         BE    UNITOVA                                                          
*                                                                               
         B     NEXTELEM            DONT CARE ABOUT THIS ELEMENT                 
*                                                                               
***                                                                             
*                                                                               
         USING NUBILD,R8                                                        
UNITBILL TM    NUBILST,X'20'       TEST FOR UNBILLED                            
         BO    UBXIT                                                            
         CLI   NBSPLPR3,0          DO WE HAVE 3 CHAR PROD ?                     
         BNE   UB01                YES-USE THAT                                 
         CLI   NBSPLPRN,0          IF SPLIT PRODUCT IN ACTION                   
         BE    UB2                                                              
         CLI   NBSPLPRN,X'FF'                                                   
         BE    UB2                                                              
*                                  MATCH PRODUCT                                
UB01     CLI   NUBILLEN,29         CARRIES 3 CHAR PROD ?                        
         BL    UB05                                                             
         CLC   NBSPLPR3,NUBILPRC                                                
         BNE   UBXIT                                                            
         CLI   NBPRDNO,0           IF MULTIPRODS?                               
         BE    UB1                 NO                                           
         BRAS  RE,CHKDUPR3         CHECK AGAINST 3 CHAR                         
         BE    UBXIT               ALREADY DONE                                 
         B     UB2                                                              
*                                                                               
UB05     DS    0H                  USE OLD BILLING ELEMENT                      
         TM    NBINDS6,NBI6XOVP    NBSPLPRN INTERNAL FUDGE?                     
         BO    UBXIT               YES - SKIP ELEM                              
         CLC   NBSPLPRN,NUBILPRD   THEN WE NEED TO MATCH PRODUCT                
         BNE   UBXIT                                                            
UB07     CLI   NBPRDNO,0           IS IT MULTI PRODS ?                          
         BE    UB1                 NO                                           
         CLI   NBSPLPR3,0                                                       
         BRAS  RE,CHKDUPRD           YES/CHECK PRODS                            
         BE    UBXIT                     ALREADY DONE                           
         B     UB2                                                              
*                                                                               
UB1      CLC   NBPRD,NBPRD2        IF BOOKEND (BOTH BRANDS THE SAME)            
         BNE   UB2                                                              
         CLI   NBSPLTYP,C'F'          ONLY COUNT ON FIRST TIME                  
         BNE   UBXIT                                                            
         SPACE 1                                                                
UB2      CLI   NBBILSTR,0          MAY BE FILTERING ON BILLED DATES             
         BE    UB3                                                              
         CLC   NUBILDAT,NBBILSTR                                                
         BL    UBXIT                                                            
         SPACE 1                                                                
UB3      CLI   NBBILEND,0                                                       
         BE    UB3C                                                             
         CLC   NUBILDAT,NBBILEND                                                
         BH    UBXIT                                                            
                                                                                
************************************************                                
UB3C     ICM   R3,15,NBINVFLT        INVOICE FILTER?                            
         BZ    UB4                                                              
         OC    0(12,R3),0(R3)                                                   
         BZ    UB4                                                              
         GOTO1 NBDATCON,DMCB,(2,NUBILDAT),DUB                                   
         MVC   DUB(2),DUB+2               SET UP MM                             
         MVC   DUB+2(4),NUBILNUM            GET NNNN                            
         CLI   0(R3),C'*'          START INV NUMBER                             
         BE    *+14                                                             
         CLC   DUB(6),0(R3)        INV LESS THAN START INV                      
         BL    UBXIT               REJECT                                       
         CLI   6(R3),C'*'          END INV NUMBER                               
         BE    UB4                 OK                                           
         CLC   DUB(6),6(R3)        INV GREATER THAN END INV                     
         BH    UBXIT               REJECT                                       
****************************************************                            
                                                                                
UB4      DS    0H                                                               
* - SPBVAL EXPECTS BILL FORMULA IN 3D PARAMETER TO CALCULATE ACTUAL             
*   AMOUNT / ELSE GROSS=ACTUAL AMOUNT                                           
         GOTO1 =V(SPBVAL),DMCB,(C'U',(R8)),WORK,0,RR=RELO                       
         LA    R1,WORK                                                          
         USING SPBVALD,R1                                                       
         MVC   DUB(4),SPBVEGRS                                                  
         MVC   DUB+4(4),SPBVENET                                                
*                                                                               
         LA    R3,NBBILTGR         DEFAULT SUM LOCATION TO TIME TYPE            
         CLI   NUBILTYP,C'I'       IF INTEGRATION TYPE                          
         BNE   UBCONT                 SET SUM LOCATION TO INTEG TYPE            
         LA    R3,NBBILIGR         *** NOTE BAD OR NEW TYPE IS TREATED          
         B     UB5                                                              
*                                         AS TIME TYPE.                         
UBCONT   CLI   NUBILTYP,C'T'       IF NOT TIME TYPE                             
         BNE   UBXIT                  BYPASS THE ELEMENT                        
UB5      L     R4,0(R3)            GET CURRENT GROSS SUM VALUE                  
*        A     R4,NUBILGRS         ADD GROSS VALUE FROM ELEMENT                 
         A     R4,DUB              ADD GROSS VALUE FROM ELEMENT                 
         ST    R4,0(R3)            RE-STORE IT                                  
*                                                                               
         L     R4,4(R3)            GET CURRENT NET SUM VALUE                    
*        A     R4,NUBILNET         ADD NET VALUE FROM ELEMENT                   
         A     R4,DUB+4            ADD NET VALUE FROM ELEMENT                   
         ST    R4,4(R3)            RE-STORE IT                                  
*                                  *** ASSUMES SUMS ARE 1 WORD                  
UBXIT    B     NEXTELEM                                                         
         DROP  R8                                                               
*****                                                                           
*                                                                               
         USING NUPAYD,R8                                                        
UNITPAY  CLI   NBPAYSTR,0          MAY BE FILTERING ON PAID DATES               
         BE    UP2                                                              
         CLC   NUPAYDAT,NBPAYSTR                                                
         BL    UPXIT                                                            
         SPACE 1                                                                
UP2      CLI   NBPAYEND,0                                                       
         BE    UP3                                                              
         CLC   NUPAYDAT,NBPAYEND                                                
         BH    UPXIT                                                            
         SPACE 1                                                                
**UP3      CLI   NBSPLPRN,0          IF SPLIT PRODUCT IN ACTION                 
**         BE    UP4                                                            
**         LA    R1,NBPRDLST         ..IF MULTPRODS                             
**         CLI   0(R1),0                                                        
**         BNE   UP3A                ..GO THERE                                 
**         CLC   NBSPLPRN,NBPRD      ..ELSE 2 PROD PROCESSING                   
**         BE    UP4                                                            
**         CLC   NBSPLPRN,NBPRD2                                                
**         BE    UP4                                                            
**         B     UPXIT                                                          
UP3      CLI   NBSPLPR3,0          IF SPLIT PRODUCT IN ACTION                   
         BE    UP4                                                              
         CLI   NBPRDNO,0           ..IF MULTPRODS                               
         BNE   UP3A                ..GO THERE                                   
         CLC   NBSPLPR3,NBPR1CL3   ..ELSE 2 PROD PROCESSING                     
         BE    UP4                                                              
         CLC   NBSPLPR3,NBPR2CL3                                                
         BE    UP4                                                              
         B     UPXIT                                                            
*                                                                               
UP3A     DS    0H                                                               
         ZIC   R3,NBPRDNO          NUMBER OF MULT PRODUCTS                      
UP3B     ICM   R1,15,NBADPLST         3 CHAR PROD LIST                          
         LTR   R1,R1               (IF PASSED)                                  
         BZ    UP3C                                                             
UP3BB    CLC   NBSPLPR3,0(R1)      THEN ONLY COUNT CURRENT PRODUCTS             
         BE    UP4                                                              
         LA    R1,3(R1)            NOT OLD BILL PROD.                           
         BCT   R3,UP3BB                                                         
         B     UPXIT                                                            
*                                                                               
* THIS LOOKS AT OLD 1 CHARACTER PROD -                                          
UP3C     LA    R1,NBPRDLST         THEN ONLY COUNT CURRENT PRODUCTS             
         ZIC   R3,NBPRDNO                                                       
UP3D     CLC   NBSPLPRN,0(R1)      THEN ONLY COUNT CURRENT PRODUCTS             
         BE    UP4                                                              
         LA    R1,1(R1)            NOT OLD BILL PROD.                           
         BCT   R3,UP3D                                                          
         B     UPXIT                                                            
                                                                                
*                                                                               
UP4      LA    R3,NBPAYTGR         DEFAULT SUM LOCATION TO TIME TYPE            
         CLI   NUPAYTYP,C'I'       IF INTEGRATION TYPE                          
         BNE   UPCONT                 SET SUM LOCATION TO INTEG TYPE            
         LA    R3,NBPAYIGR         *** NOTE BAD OR NEW TYPE IS TREATED          
         B     UP5                                                              
*                                         AS TIME TYPE.                         
UPCONT   CLI   NUPAYTYP,C'T'       IF NOT TIME TYPE                             
         BNE   UPXIT                                                            
UP5      L     R4,0(R3)            GET CURRENT GROSS SUM VALUE                  
         A     R4,NUPAYGRS         ADD GROSS VALUE FROM ELEMENT                 
         ST    R4,0(R3)            RE-STORE IT                                  
*                                                                               
         L     R4,4(R3)            GET CURRENT NET SUM VALUE                    
         A     R4,NUPAYNET         ADD NET VALUE FROM ELEMENT                   
         ST    R4,4(R3)            RE-STORE IT                                  
*                                  *** ASSUMES SUMS ARE 1 WORD                  
UPXIT    B     NEXTELEM                                                         
         DROP  R8                                                               
*                                                                               
*****                                                                           
*                                                                               
         USING NUMGD,R8                                                         
UNITMGF  LA    R3,NBMGFLEN         LENGTH OF MAKE-GOOD-FOR ELEMENT              
         BCTR  R3,0                DECREMENT FOR EX                             
         EXMVC R3,NBMGFPCD,NUMGPCOD                                             
         XC    NBMGFSBP,NBMGFSBP   CLEAR SUBPRT                                 
         CLI   NBMGFSUB,0          SUBPRT IN PRINTABLE FORM                     
         BE    MGF2                                                             
         MVI   NBMGFSBP,C'-'                                                    
         LA    R2,NBMGFSBP+1                                                    
         EDIT  NBMGFSUB,(2,0(R2)),ALIGN=LEFT                                    
MGF2     MVC   NBMGFSTA,NUMGSTAT   STATUS OF MISSED UNIT                        
         MVC   NBMGFST2,NUMGST3    STATUS2 OF MISSED UNIT                       
MGFX     B     NEXTELEM                                                         
         DROP  R8                                                               
*                                                                               
****                                                                            
*                                                                               
         USING NUMGD,R8                                                         
UNITMGB  LA    R3,NBMGBLEN         LENGTH OF MAKE-GOOD-BY ELEMENT               
         BCTR  R3,0                DECREMENT FOR EX                             
         EXMVC R3,NBMGBPCD,NUMGPCOD                                             
         XC    NBMGBSBP,NBMGBSBP   CLEAR SUBPRT                                 
         CLI   NBMGBSUB,0          SUBPRT IN PRINTABLE FORM                     
         BE    MGB2                                                             
         MVI   NBMGBSBP,C'-'                                                    
         LA    R2,NBMGBSBP+1                                                    
         EDIT  NBMGBSUB,(2,0(R2)),ALIGN=LEFT                                    
MGB2     MVC   NBMGBSTA,NUMGSTAT   STATUS OF MAKEGOOD UNIT                      
MGBX     B     NEXTELEM                                                         
         DROP  R8                                                               
*                                                                               
****                                                                            
*                                                                               
UNITEST  MVI   ESTSEXST,C'T'       ESTIMATE DEMO ELEMENT FOUND                  
         B     NEXTELEM                                                         
*                                                                               
****                                                                            
*                                                                               
         USING NUEHD,R8                                                         
UNITHOME EQU   *                   ADJUST EST HUT & RTG BY HUTPCT               
*                                                                               
         XC    SAVE35AD,SAVE35AD   SET SAVED 35 ELEM ADDRESS TO ZERO            
*                                                                               
         CLI   NBESTOPT,0          IF EST DEMOS NOT NEEDED, DON'T ADJ           
         BE    UHX                                                              
         OC    NBHUTPCT,NBHUTPCT   IF 0, DON'T ADJUST                           
         BZ    UHX                                                              
         CLC   NBHUTPCT,=H'10000'  IF 100.00, DON'T ADJUST                      
         BE    UHX                                                              
*                                  (CHECKING FOR HOME RTG O/R)                  
         XC    WORK(5),WORK                                                     
         MVI   WORK+2,C'R'         RATING HOMES MODIFIER                        
         MVI   WORK+3,X'01'                                                     
         GOTO1 NBHELLO,DMCB,(C'G',UNITFIL),(X'DD',NBAIO),(5,WORK)               
         CLI   DMCB+12,0           IF OVERRIDDEN, DON'T ADJUST                  
         BE    UHX                                                              
*                                                                               
         ST    R8,SAVE35AD         SAVE ADDRESS OF ELEMENT                      
         MVC   SAVE35EL,NUEHRTG    SAVE RTG AND HUT                             
*                                                                               
         SR    R6,R6                                                            
         ICM   R6,3,NBHUTPCT       MULTIPLIER                                   
         ST    R6,MULTER                                                        
         MVC   DIVSOR,=F'10000'    DIVISOR                                      
         MVC   ADDER,=F'5000'      ROUNDING FACTOR                              
*                                                                               
         SR    R5,R5                                                            
         ICM   R5,3,NUEHHUT        ADJUST HUT                                   
         M     R4,MULTER                                                        
         A     R5,ADDER                                                         
         D     R4,DIVSOR                                                        
         STCM  R5,3,NUEHHUT        NEW HUT = OLD HUT * ADJUSTMENT               
*                                                                               
         ST    R5,DUB              RECALCULATE RTG                              
         SR    R5,R5                                                            
         ICM   R5,3,NUEHSHR                                                     
         M     R4,DUB                                                           
         A     R5,ADDER                                                         
         D     R4,DIVSOR                                                        
         STCM  R5,3,NUEHRTG        NEW RTG = SHR * NEW HUT                      
*                                                                               
UHX      B     NEXTELEM                                                         
         DROP  R8                                                               
         SPACE 1                                                                
UNITFIL  DC    CL8'UNTFIL'         HELLO FILE NAME                              
*                                                                               
****                                                                            
*                                                                               
UNITACT  MVI   ACTSEXST,C'T'       ACTUAL DEMO ELEMENT FOUND                    
         B     NEXTELEM                                                         
*                                                                               
****                                                                            
*                                                                               
         USING NUOVD,R8                                                         
UNITOVA  CLI   NBREROPT,0          RERATE...HIDE THE OVERRIDE ELEMENTS          
         BE    NEXTELEM                                                         
         TM    NUOVFLG,X'80'       NAD DEMO FROM FILE                           
         BO    NEXTELEM             IT AIN'T AN OVERRIDE                        
         MVI   NUOVEL,X'EE'        REPLACE 'DE' WITH 'EE' (ACT)                 
         B     NEXTELEM                                                         
         DROP  R8                                                               
*                                                                               
****                                                                            
*                                                                               
         USING NUGUAD,R8                                                        
UNITGUAR CLI   NBDEMRAW,C'Y'                                                    
         BE    *+10                                                             
         MVC   NBGUAFAC,NUGUAFAC   MOVE IN FACTOR                               
         B     NEXTELEM                                                         
         DROP  R8                                                               
*                                                                               
         USING NUNGUD,R8                                                        
UNITNGUA CLI   NBDEMRAW,C'Y'                                                    
         BE    *+10                                                             
         MVC   NBNGUFAC,NUNGUFAC   MOVE IN NEW PKG GUARANTEE FACTOR             
         B     NEXTELEM                                                         
         DROP  R8                                                               
*                                                                               
         SPACE 3                                                                
NEXTELEM ZIC   R1,1(R8)            GET NEXT ELEMENT LENGTH (ADDR IN R8)         
         CLI   1(R8),0                                                          
         BE    XITUNIT                                                          
         AR    R8,R1                                                            
         CLI   0(R8),0             TEST FOR EOR                                 
         BE    XITUNIT                                                          
         B     URLOOP                                                           
*                                                                               
         EJECT                                                                  
XITUNIT  CLI   NBSPLPRN,0                                                       
         BE    XITUNIT2                                                         
         TM    NBSPLOPT,X'10'      TEST IGNORE COPY SPLIT                       
         BZ    *+12                                                             
         TM    NBUNST3,X'40'       IF COPY SPLIT, BYPASS                        
         BO    XITUNIT2                                                         
         CLI   NBPRD2,0            IF 2ND PROD                                  
         BNE   XITUNT1             SPLIT                                        
         CLI   NBPRDLST,0          IF MULTIPLE PRODS                            
         BE    XITUNIT2            NO SPLIT                                     
*                                                                               
XITUNT1  LA    R1,NBPAYTGR                                                      
         BAS   RE,SPLITOH                                                       
         LA    R1,NBPAYTNT                                                      
         BAS   RE,SPLITOH                                                       
*                               INTEGRATION PROD                                
***      CLI   NBINTPRD,0                                                       
***      BE    XITUNT1A                                                         
***      CLC   NBINTPRD,NBSPLPRN    INT PRD MATCHES ?                           
***      BE    XITUNIT2             YES/GIVE IT ALL$/DONT SPLIT                 
         CLI   INTPROD,0                                                        
         BE    XITUNT1A                                                         
         CLC   INTPROD,NBSPLPR3    INT PRD MATCHES ?                            
         BE    XITUNIT2             YES/GIVE IT ALL$/DONT SPLIT                 
*                                                                               
         XC    NBPAYIGR,NBPAYIGR    NO MATCH-GIVE NOTHING                       
         XC    NBPAYINT,NBPAYINT                                                
         B     XITUNIT2                                                         
XITUNT1A LA    R1,NBPAYIGR                                                      
         BAS   RE,SPLITOH                                                       
         LA    R1,NBPAYINT                                                      
         BAS   RE,SPLITOH                                                       
         B     XITUNIT2                                                         
SPLITOH  NTR1                                                                   
         CLI   NBPRDLST,0          IF PRODUCT ELEMENT                           
         BE    *+12                                                             
         BAS   RE,SPLITEM0         GO TO SPLITEM0                               
         B     *+8                                                              
         BAS   RE,SPLITEM          ELSE DO NORMAL 2 PROD PROCESS                
         XIT1                                                                   
         SPACE 1                                                                
XITUNIT2 GOTO1 =A(DODEMS),DMCB,(RC),RR=RELO                                     
*                                                                               
         USING NUOVD,R8                                                         
*                                                                               
* IF UNIT IS AN EST/ACT OVERRIDE UNIT, RESET DC OVERRIDE ELEM                   
* THAT MAY HAVE BEEN CHANGED TO DE TO FUDGE MANOVERRIDE ROUTINES                
         LA    R8,NUMAINEL                                                      
XITUNIT3 CLI   0(R8),0                                                          
         BE    XITUNT4B                                                         
         CLI   0(R8),X'DE'         LOOK FOR DE ELEMS                            
         BNE   XITUNIT4                                                         
         TM    NUOVFLG,X'10'       ARE THESE REALLY EST/ACT ELEMS?              
         BNO   XITUNIT4            NO                                           
         MVI   0(R8),X'DC'         YES/RESET                                    
         NI    NUOVFLG,X'FF'-X'10' CLEAR FLAG                                   
XITUNIT4 ZIC   R1,1(R8)                                                         
         CLI   1(R8),0                                                          
         BE    XITUNT4B                                                         
         AR    R8,R1                                                            
         B     XITUNIT3                                                         
*                                                                               
XITUNT4B CLI   NBREROPT,0          RERATE...UNHIDE THE OVERRIDE ELEMNTS         
         BE    XITUNIT8                                                         
         LA    R8,NUMAINEL                                                      
*                                                                               
XITUNIT5 CLI   0(R8),0                                                          
         BE    XITUNIT8                                                         
         CLI   0(R8),X'ED'                                                      
         BNE   *+12                                                             
         MVI   NUOVEL,X'DD'        'ED' RESTORED TO 'DD'                        
         B     XITUNIT6                                                         
         CLI   0(R8),X'EE'                                                      
         BNE   XITUNIT6                                                         
         MVI   NUOVEL,X'DE'        'EE' RESTORED TO 'DE'                        
*                                                                               
XITUNIT6 ZIC   R1,1(R8)                                                         
         AR    R8,R1                                                            
         B     XITUNIT5                                                         
*                                                                               
         USING NUEHD,R8                                                         
XITUNIT8 OC    SAVE35AD,SAVE35AD   WAS HUT ADJUSTMENT APPLIED                   
         BZ    MXIT                                                             
         GOTO1 NBHELLO,DMCB,(C'G',UNITFIL),(X'35',NBAIO),0                      
         CLI   DMCB+12,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         L     R8,DMCB+12          ADDRESS OF EST HOMES (X'35') ELEMENT         
         MVC   NUEHRTG(4),SAVE35EL RESTORE ESTIMATED RTG AND HUT                
         DROP  R8                                                               
*                                                                               
MXIT     XIT1                                                                   
*                                                                               
         EJECT                                                                  
*              ROUTINE TO CHECK FOR SPLITS                                      
         SPACE 3                                                                
*                                  NBSPLPNM IS SET AS FOLLOWS                   
*              INPUT               0=GET FIRST PRODUCT                          
*                                  N=GET NEXT PRODUCT                           
*                                  X'FF'=LAST WAS ALLOCATED                     
*                                  NBSPLCNT = NEXT PROD NUMBER                  
*              OUTPUT              0=NO MORE PRODUCTS                           
*                                  N=PRODUCT FOUND                              
*                                  X'FF'=UNIT IS UNALLOCATED                    
*                                                                               
*              NBLEN NBACTUAL NBASSIGN NBINTEG NBSPCHRG                         
*              NBMAXCST AND NBERNCST NBICOS2                                    
*              WILL BE ADJUSTED                                                 
*              ACCORDING TO THE SPLIT PERCENTAGE                                
*              SPLITLEN SET TO SECONDS LENGTH FOR SUBSEQUENT                    
*              IMPRESSION ADJUSTMENT                                            
         EJECT                                                                  
SPLIT    NTR1                                                                   
         CLI   NBSPLTYP,C'B'       BILLED/CHANGED MODE?                         
         BE    SPLLAST0                                                         
*                                                                               
         CLI   NBPRDLST,0          ..IF PRODUCT ELEMENT                         
         BNE   SPLIT0                 GO THERE                                  
*                                  ..ELSE NORMAL 2 PROD PROCESSING              
* IF ONLY ONE PROD ON UNIT, CLEAR INTEGRATION                                   
* IF NBINTPRD NOT = NBPRD                                                       
         CLI   NBPRD2,0            IF ONLY 1 PROD ON UNIT                       
         BNE   SPIT00                                                           
***      CLI   NBINTPRD,0          AND SPECIAL INTEGRATION PRODUCT              
***      BE    SPIT00                                                           
***      CLC   NBPRD,NBINTPRD      IF INT PROD NOT = NBPRD                      
***      BE    SPIT00                                                           
         CLI   INTPROD,0          AND SPECIAL INTEGRATION PRODUCT               
         BE    SPIT00                                                           
         CLC   INTPROD,NBPR1CL3    IF INT PROD NOT = NBPRD                      
         BE    SPIT00                                                           
         XC    NBINTEG,NBINTEG     CLEAR INTEGRATION                            
*                                                                               
SPIT00   CLI   NBWHERE,C'I'        IF WE DID NOT COME FROM NETIO                
         BE    SPLIT2                                                           
         CLC   NBPRD,NBPRD2        IF IDENTICAL PIGGYBACK PRODS                 
         BNE   SPLIT1                                                           
         CLI   NBSPLTYP,C'F'       CHECK TYPE                                   
         BE    SPLFIRST                                                         
         CLI   NBSPLTYP,C'S'                                                    
         BE    SPLSECND                                                         
SPLIT1   CLC   NBSPLPRN,NBPRD      REESTABLISH WHICH PRODUCT WE                 
         BE    SPLFIRST            PREVIOUSLY DEALT WITH TO FIND                
         CLC   NBSPLPRN,NBPRD2     THE SAME SPLIT FACTORS                       
         BE    SPLSECND                                                         
         B     SPLITXIT                                                         
         SPACE 1                                                                
SPLIT2   CLI   NBINTPRD,X'FF'   IF INT PROD DONE                                
         BE    SPLBLSTX         GET OUT NOW                                     
         CLI   NBSPLPRN,X'FF'                                                   
         BE    SPLLAST                                                          
         CLI   NBEFFPNM,0          WAS A SPECIFIC PRODUCT SELECTED              
         BE    SPLALL                                                           
         TM    NBSBKEND,X'20'      ALWAYS SPLIT BOOKENDS                        
         BO    SPLALL                                                           
         CLI   NBSPLPRN,0           HAVE WE HANDLED BEFORE                      
         BNE   SPLBLAST                  THEN WE ARE DONE                       
         CLC   NBPRD,NBEFFPNM      MATCH ON FIRST?                              
         BE    SPLFIRST                                                         
         CLC   NBPRD2,NBEFFPNM     MATCH ON SECOND?                             
         BE    SPLSECND                                                         
         B     SPLLAST             NO  - SO GO AND TRY FOR BILLING              
         SPACE 1                                                                
SPLALL   CLI   NBPRD,0             IS UNIT ALLOCATED?                           
         BNE   SPLALL2                                                          
         MVI   NBSPLPRN,X'FF'      NO -  SO SET                                 
***      MVC   NBSPLPR3,=C'POL'     CAUSED PROBLEMS !!!                         
         B     SPLITXIT                                                         
         SPACE 1                                                                
SPLALL2  CLI   NBSPLPRN,0          ALLOCATED - IS THIS THE FIRST                
         BE    SPLFIRST                                                         
         CLI   NBPRD2,0            IS THERE A SECOND?                           
         BE    SPLLAST             NO -  SO WE'RE THROUGH                       
         CLC   NBPRD,NBPRD2        PXZ                                          
         BNE   SPLALL2A            PXZ                                          
         CLI   NBSPLTYP,C'S'       PXZ                                          
         BE    SPLLAST             PXZ                                          
SPLALL2A CLC   NBSPLPRN,NBPRD      IF PREVIOUS PRODUCT WAS THE FIRST            
         BE    SPLSECND               DEAL WITH SECOND PRODUCT NOW              
         B     SPLLAST                                                          
         SPACE 1                                                                
SPLFIRST MVC   NBSPLPRN,NBPRD      RETURN FIRST PRODUCT CODE                    
         MVC   NBSPLPR3,NBPR1CL3   YES                                          
                                                                                
***********************************************                                 
* NOW THAT NBSPLBPN IS NOT 6 BYTES IN NETVALUE BUT PASSED                       
* BY APPLICATION PROGRAM NEED TO BE CARFUL WHEN/WHERE IT'S                      
* CALLED - DIDN'T MATTER EARLIER SINCE NETVALUE ALWAY HAD                       
* ADDRESSABILITY AND APPS HAD TO DO NOTHING -                                   
****     XC    NBSPLBPN,NBSPLBPN   CLEAR BILLING PRODUCT LIST                   
         TM    NBSPLOPT,X'20'      GET BILLED PRODS?                            
         BNO   SKIPIT              NO-                                          
         ICM   R1,15,NBSPLBPN                                                   
         BNZ   *+6                                                              
         DC    H'0'                                                             
         XC    0(18,R1),0(R1)      3X6 PRODUCTS                                 
SKIPIT   DS    0H                                                               
***********************************************                                 
                                                                                
         MVI   NBSPLTYP,C'F'                                                    
         MVC   SPLITCST,=H'10000'  PRESET COST SPLIT TO 100%                    
         XC    SPLITPRE,SPLITPRE                                                
         CLI   NBPRD2,0            IS THERE A SECOND PRODUCT                    
         BE    SPLITXIT            NO - SO NO NEED TO SPLIT                     
*************************************************************                   
* (PXZ) DON'T KNOW WHAT THIS GARBAGE IS BELOW  6/4/98                           
* NBFEED HAS FEED % - BUT THIS IS % FOR 2ND PRODUCT                             
*                                                                               
         OC    NBFEED,NBFEED       IF FEED=100 OR ZERO                          
         BZ    SPLF1               SKIP THIS                                    
         CLC   =H'10000',NBFEED                                                 
         BE    SPLF1                                                            
                                                                                
**       XC    FULL,FULL                                                        
*8       MVC   FULL+2(2),NBFEED       2ND PROD                                  
**       L     RE,=F'10000'           FEED = NBFEED MINUS 100.00                
**       L     RF,FULL                                                          
*8       SR    RE,RF                                                            
**       STCM  RE,3,NBFEED                                                      
************************************************************                    
SPLF1    CLC   NBPRD2,NBPRD        11/13                                        
         BNE   SPLF5                                                            
         TM    NBSBKEND,X'20'      ALWAYS SPLIT BOOKENDS                        
         BO    SPLF5                                                            
         CLI   NBEFFPNM,0                                                       
         BNE   SPLITXIT            11/13                                        
SPLF5    MVC   SPLITCST,NBP1SHR    YES - PICK UP COST SPLIT                     
         TM    NBSPLOPT,X'10'      TEST IGNORE COPY SPLIT                       
         BO    *+12                                                             
         TM    NBUNST3,X'40'       TEST COPY SPLIT                              
         BO    SPLADJ                                                           
         ZIC   R1,NBLEN            ASSUME HALF THE LENGTH                       
         SRL   R1,1                                                             
         STC   R1,NBLEN                                                         
         CLI   NBLEN1,0            UNLESS LENGTH WAS INPUT                      
         BE    SPLADJ                                                           
         MVC   NBLEN,NBLEN1                                                     
         B     SPLADJ                                                           
         SPACE 1                                                                
SPLSECND MVC   NBSPLPRN,NBPRD2     RETURN SECOND PRODUCT CODE                   
         MVC   NBSPLPR3,NBPR2CL3                                                
*                                                                               
* PXZ FEB18/97 - SPLIT FEED FOR PIGGY/BACKS                                     
* SPLIT FEED FOR PIGGBACKS - NEVER DONE BEFORE? WHY?                            
*************************************************************                   
* (PXZ) WORKING ON SECOND PROD - WHY WAS IT SKIPPED????? 6/4/98                 
*                                                                               
***      B     SPLL00                                                           
*                                                                               
         OC    NBFEED,NBFEED       IF FEED=100 OR ZERO                          
         BZ    SPLL00              SKIP THIS                                    
***      CLC   =H'10000',NBFEED                                                 
***      BE    SPLL00                                                           
                                                                                
         XC    FULL,FULL                                                        
         MVC   FULL+2(2),NBFEED       2ND PROD                                  
         L     RE,=F'10000'           FEED = NBFEED MINUS 100.00                
         L     RF,FULL                                                          
         SR    RE,RF                                                            
         STCM  RE,3,NBFEED                                                      
************************************************************                    
SPLL00   MVI   NBSPLTYP,C'S'                                                    
         MVC   SPLITPRE,NBP1SHR    PICK UP PREVIOUS SPLIT                       
         MVC   SPLITCST,=H'10000'                                               
         TM    NBSPLOPT,X'10'      TEST IGNORE COPY SPLIT                       
         BO    *+12                                                             
         TM    NBUNST3,X'40'       TEST COPY SPLIT                              
         BO    SPLADJ                                                           
         CLI   NBLEN1,0            IF LENGTH WAS INPUT                          
         BE    SPLL0                                                            
         ZIC   R1,NBLEN            SUBTRACT FIRST PARTNER FROM TOTAL            
         ZIC   R0,NBLEN1                                                        
         SR    R1,R0                                                            
         STC   R1,NBLEN            RETURN 2ND PARTNERS TIME                     
         B     SPLADJ                                                           
SPLL0    ZIC   R1,NBLEN            ELSE/ASSUME HALF THE LENGTH                  
         SRL   R1,1                                                             
         STC   R1,NBLEN                                                         
         SPACE 1                                                                
SPLADJ   CLC   NBPRD,NBPRD2        IF IDENTICAL PIGGYBACK PRODS                 
         BNE   SPLADJ2                                                          
         TM    NBSBKEND,X'20'      ALWAYS SPLIT BOOKENDS                        
         BO    SPLADJ2                                                          
         CLI   NBEFFPNM,0          DON'T SPLIT IF SINGLE PRODUCT                
         BNE   SPLITXIT               (ONLY HERE FOR BOOKENDS)                  
         SPACE 1                                                                
SPLADJ2  LA    R1,NBACTUAL         NOW SPLIT ACTUAL,                            
         BAS   RE,SPLITEM                                                       
         LA    R1,NBASSIGN         ASSIGNED                                     
         BAS   RE,SPLITEM                                                       
***********************************                                             
* IF INTEGRATION HAS SPECIAL PRODUCT                                            
* NBSPLPRN MUST = SPECIAL INT PROD ELSE SKIP INTEG $$$                          
***      CLI   NBINTPRD,0                                                       
***      BE    SPLADJ3                                                          
***      CLC   NBSPLPRN,NBINTPRD   IF SPECIAL INTEG PROD                        
***      BE    SPLADJ3A            GET 100%                                     
         CLI   INTPROD,0                                                        
         BE    SPLADJ3                                                          
         CLC   NBSPLPR3,INTPROD    IF SPECIAL INTEG PROD                        
         BE    SPLADJ3A            GET 100%                                     
         XC    NBINTEG,NBINTEG                                                  
**********************************                                              
SPLADJ3  LA    R1,NBINTEG          AND INTEGRATION                              
         BAS   RE,SPLITEM                                                       
SPLADJ3A LA    R1,NBSPCHRG         AND SPECIAL CHARGES                          
         BAS   RE,SPLITEM                                                       
         LA    R1,NBERNCST         AND EARNED COST                              
         BAS   RE,SPLITEM                                                       
         LA    R1,NBMAXCST         AND MAX    COST                              
         BAS   RE,SPLITEM                                                       
         LA    R1,NBICOS2          AND ICOS2                                    
         BAS   RE,SPLITEM                                                       
         MVC   SPLITLEN,NBLEN                                                   
         B     SPLITXIT                                                         
         SPACE 1                                                                
* SPLLAST WAS FOR 'REGULAR' PRODS AND SPLLAST0 FOR MULTI PRODS                  
* BUT FOR FINDING BILLED/CHANGED PRODS WE CAN USE SAME ROUTINE (?)              
SPLLAST  TM    NBINDS3,NBI3BPRD    IF BLDPRD                                    
         BNO   SPLBLAST                                                         
         TM    NBSPLOPT,X'20'      IF X'20' BIT IS SET AS WELL                  
         BNO   SPLBLAST                                                         
         B     SPLLAST0            GO TO COMMON CODE FOR BOTH                   
*                                  REGULAR AND COPYSPLIT UNITS                  
                                                                                
         LA    R8,NUMAINEL         CHECK FOR BILLING ELEMENT                    
         SPACE 1                                                                
SPLB2    ZIC   R1,1(R8)                                                         
         CLI   1(R8),0                                                          
         BE    SPLBLAST                                                         
         AR    R8,R1                                                            
         CLI   0(R8),0                                                          
         BE    SPLBLAST                                                         
         CLI   0(R8),X'10'                                                      
         BNE   SPLB2                                                            
         USING NUBILD,R8                                                        
*                                                                               
         CLI   NUBILLEN,29         NEW 3 CHAR PROD LENGTH?                      
         BL    SPLB3                                                            
******** CLI   NUBILPRD,0          IF HAS 1 BYTE CODE,USE THAT                  
******** BNE   SPLB3                                                            
         CLI   NUBILPRC,0          MAKE SURE IT'S THERE -                       
         BE    SPLB3               ELSE USE 1                                   
         CLC   NUBILPRC,NBPR1CL3   ELSE USE 3                                   
         BE    SPLB2                                                            
         CLC   NUBILPRC,NBPR2CL3                                                
         BE    SPLB2                                                            
         B     SPLB3B                                                           
*                                                                               
SPLB3    CLC   NUBILPRD,NBPRD      THAT IS FOR UNALLOCATED BRAND                
         BE    SPLB2                                                            
         CLC   NUBILPRD,NBPRD2                                                  
         BE    SPLB2                                                            
SPLB3B   TM    NUBILST,X'20'       (UNBILLED NOT USEFUL)                        
         BO    SPLB2                                                            
         CLI   NBEFFPNM,0          IF SPECIFIC PRODUCT SELECTED                 
*        BE    SPLB4                                                            
         BNE   SPLB3F              IF THERE USE 1 BYTE CODE                     
         CLI   NBEFFPN3,0                                                       
         BE    SPLB4                                                            
         CLC   NBEFFPN3,NUBILPRC   ELSE CHECK 3 CHAR CODES                      
         BNE   SPLB2                                                            
         B     SPLB4                                                            
*                                                                               
SPLB3F   CLC   NBEFFPNM,NUBILPRD   CHECK IT MATCHES                             
         BNE   SPLB2                                                            
         SPACE 1                                                                
SPLB4    ICM   R5,15,NBSPLBPN         SEE IF WE'VE DONE THIS ONE YET            
         CLI   NUBILLEN,29                                                      
         BL    SPLB4D                                                           
         CLI   NUBILPRC,0          DO WE HAE 3 CHAR CODE?                       
         BE    SPLB4D                                                           
         MVC   WORK(3),NUBILPRC    YES-USE IT                                   
         B     SPLB4E                                                           
SPLB4D   MVC   BYTE,NUBILPRD    ** SHOULD NEVER HAVE TO GO TO GETPRD3           
         BRAS  RE,GETPRD        * NUBILPRD HAS GOT TO BE ON CLIENT REC          
         CLC   =C'UNA',WORK     * ELSE IT WILL BE OVERFLOW AND HAVE             
         BNE   SPLB4E          **HAVE NUBILPRC                                  
         CLC   =C'TH',NBSELAGY     OK FOR ZENITH                                
         BE    SPLB4E              THEY HAVE UNA AS PROD CODE                   
         DC    H'0'                                                             
***      BAS   RE,GETPRD3                                                       
SPLB4E   LA    R0,4                                                             
         SPACE 1                                                                
SPLB6    CLI   0(R5),0                                                          
         BE    SPLB8                                                            
         CLC   0(3,R5),WORK                                                     
         BE    SPLB2               DONE THIS PRODUCT                            
         LA    R5,3(R5)                                                         
         BCT   R0,SPLB6                                                         
         B     SPLBLAST                                                         
         SPACE 1                                                                
**SPLB8    CLI   NUBILLEN,29         3 CHARACTER ?                              
***      BL    SPLB9                                                            
***      CLI   NUBILPRC,0          3 CHAR CODE ?                                
***      BE    SPLB9                                                            
***      MVC   WORK(3),NUBILPRC    USE IT                                       
***      B     SPLB10                                                           
***SPLB9    MVC   BYTE,NUBILPRD                                                 
***         BAS   RE,GETPRD3                                                    
SPLB8    DS    0H                                                               
SPLB10   MVC   NBSPLPRN,NUBILPRD   AND PASS IT BACK                             
         MVC   NBSPLPR3,WORK                                                    
         MVC   0(3,R5),NBSPLPR3    SAVE THIS PRODUCT                            
         MVI   NBSPLTYP,C'B'                                                    
         XC    NBACTUAL,NBACTUAL   AND TREAT THINGS AS ZERO                     
         XC    NBASSIGN,NBASSIGN                                                
         XC    NBINTEG,NBINTEG                                                  
         XC    SPLITCST,SPLITCST                                                
         MVI   SPLITLEN,0                                                       
         B     SPLITXIT                                                         
                                                                                
*****************************************************                           
* INPUT - BYTE = 1 BYTE PROD CODE                                               
* OUTPUT- WORK = 3 CHAR PROD                                                    
GETPRD3  NTR1                                                                   
         XC    WORK,WORK                                                        
         LA    R4,WORK                                                          
         USING PLSTPSSV,R4                                                      
         MVC   WORK(2),=X'0DF1'                                                 
         MVC   WORK+2(3),NBACTAM                                                
         MVC   PLSTBPRD+1(1),BYTE                                               
         MVC   WORK+20(13),WORK                                                 
         GOTO1 NBDM,DMCB,=C'DMRDHI',=C'SPTDIR',WORK,WORK                        
         B     GETPRD3H                                                         
*                                                                               
GTPRSEQ  GOTO1 NBDM,DMCB,=C'DMRSEQ',=C'SPTDIR',WORK,WORK                        
*                                                                               
GETPRD3H CLC   WORK+20(5),WORK                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   PLSTBPRD+1(1),BYTE                                               
         BNE   GTPRSEQ                                                          
         MVC   WORK(3),PLSTPRD                                                  
         XIT1                                                                   
         DROP  R4                                                               
****************************************************                            
         SPACE 1                                                                
* INTEGRATION ASSIGNED TO ONE PRODUCT                                           
* IF THAT PRODUCT IS ONE OF SCHEDULED PRODUCTS PROGRAM                          
* HAS HANDLED IT WHEN IT DEALT WITH THAT PRODUCT                                
*IF NOT A REGULARLY SCHEDULED PRODUCT - DEAL WITH IT HERE                       
*                                                                               
SPLBLAST DS    0H                      INTEGRATION PRODUCT?                     
**       CLI   NBINTPRD,0              INTEGRATION PRODUCT?                     
**       BE    SPLBLSTX                NO                                       
**       CLI   NBINTPRD,X'FF'          DONE?                                    
**       BE    SPLBLSTX                                                         
**       CLC   NBINTPRD,NBPRD      IS IT ALSO ONE OF REGUALR PRODS?             
**       BE    SPLBLSTX                                                         
**       CLC   NBINTPRD,NBPRD2                                                  
**       BE    SPLBLSTX                                                         
*                                                                               
         CLI   INTPROD,0              INTEGRATION PRODUCT?                      
         BE    SPLBLSTX                NO                                       
         CLI   NBINTPRD,X'FF'          DONE?                                    
         BE    SPLBLSTX                                                         
         CLC   INTPROD,NBPR1CL3        IS IT ALSO ONE OF REGUALR PRODS?         
         BE    SPLBLSTX                                                         
         CLC   INTPROD,NBPR2CL3                                                 
         BE    SPLBLSTX                                                         
*                                   NO SO WE NEED TO HANDLE IT HERE             
         MVC   NBSPLPRN,NBINTPRD   SET CODE                                     
         MVC   NBSPLPR3,INTPROD    SET CODE                                     
         L     R7,NBAIO            GET AND SET INTEGRATION                      
         USING NURECD,R7                                                        
         LA    R1,NUMAINEL                                                      
         USING NUMAINEL,R1                                                      
         MVC   NBINTEG,NUINTEG      SET INTEGRATION                             
         XC    NBACTUAL,NBACTUAL                                                
         XC    NBASSIGN,NBASSIGN                                                
         MVI   SPLITLEN,0                                                       
         MVI   NBINTPRD,X'FF'      SET AS LAST                                  
         XC    SPLITCST,SPLITCST                                                
         B     SPLITXIT                                                         
         DROP  R1,R7                                                            
*                                                                               
SPLBLSTX MVI   NBSPLPRN,0          RETURN NO FURTHER                            
         MVI   NBSPLTYP,0                                                       
         MVI   NBINTPRD,0          CLEAR THIS                                   
         XC    INTPROD,INTPROD     CLEAR THIS                                   
         SPACE 1                                                                
SPLITXIT XIT1                                                                   
         EJECT                                                                  
*              ROUTINE TO SPLIT THE COSTS                                       
         SPACE 3                                                                
*              INPUTS              R1=A(FULL WORD TO BE SPLIT)                  
*                                  SPLITCST=H'PERCENT SPLIT NOW'                
*                                  SPLITPRE=H'PREVIOUS PERCENTAGE'              
         SPACE 1                                                                
SPLITEM  NTR1                                                                   
         L     RF,0(R1)            FIRST COMPUTE TOTAL SPLIT                    
         LH    RE,SPLITCST                                                      
         MR    RE,RE                                                            
         D     RE,=F'5000'                                                      
         LTR   RF,RF                                                            
         BM    *+8                                                              
         AH    RF,=H'1'                                                         
         SRA   RF,1                                                             
         LR    R2,RF               (SAVE RESULT IN R2)                          
         SPACE 1                                                                
         L     RF,0(R1)            THEN COMPUTE PREVIOUS SPLIT                  
         LH    RE,SPLITPRE                                                      
         MR    RE,RE                                                            
         D     RE,=F'5000'                                                      
         LTR   RF,RF                                                            
         BM    *+8                                                              
         AH    RF,=H'1'                                                         
         SRA   RF,1                                                             
         SR    R2,RF               AND SUBTRACT THIS FROM PREVIOUS              
         SPACE 1                                                                
         ST    R2,0(R1)            RETURN THIS VALUE TO CALLER                  
         XIT1                                                                   
         EJECT                                                                  
                                                                                
* SPLITS UNITS WITH PRODUCT ELEMENT                                             
SPLIT0   DS    0H                                                               
         SPACE 1                                                                
         CLI   NBINTPRD,X'FF'      IF INT PROD IS DONE                          
         BE    SPLBLASX            DONT COME BACK                               
         CLI   NBSPLPRN,X'FF'      IF WE'RE FINISHED                            
         BE    SPLLAST0            CHECK BILLING ELEMENT PRODUCTS               
*                                                                               
         CLI   NBPRDLST,0          IS UNIT ALLOCATED?                           
         BNE   SPL100                                                           
         MVI   NBSPLPRN,X'FF'      NO -  SO SET UNALLOCATED                     
         B     SPLITX0                   AND GET OUT                            
*                                                                               
SPL100   DS    0H                  YES - ALLOCATED                              
         L     R6,NBAPROD                GET PRODUCT ELEMENT                    
*******  USING NUPRDD,R6                                                        
         USING NXBLKD,R6                                                        
         LA    R6,NXPRDS                                                        
         USING NXPRDS,R6                                                        
*                                  SO R6->START OF PRODSECT                     
         LA    R2,1                SET PRODUCT COUNTER                          
         ZIC   RE,NBPRDNO          TOTAL NUMBER OF PRODUCTS                     
         CLI   NBSPLCNT,0        ..ARE WE IN MIDDLE                             
         BE    SPL180                                                           
         CLI   NBWHERE,C'I'            ARE WE FROM NETIO?                       
         BNE   SPL185                  NO                                       
         ZIC   R1,NBSPLCNT       ..YES/BUMP TO NEXT PROD NO                     
         LA    R1,1(R1)                                                         
         STC   R1,NBSPLCNT                                                      
         CLC   NBSPLCNT,NBPRDNO  ..IF NEXT PRODUCT NO > TOTAL PROD NO           
         BH    SPLLAST0                DONE/NO MORE PRODS                       
         MVI   NBSPLTYP,0                                                       
         B     *+12                                                             
SPL180   MVI   NBSPLCNT,1        ,,NO/FIRST TIME                                
         MVI   NBSPLTYP,C'F'                                                    
*                                                                               
SPL185   ZIC   R3,NBSPLCNT                                                      
SPL200   CR    R3,R2               IS THIS PRODUCT NUMBER WE WANT               
         BE    SPL250              YES                                          
         LA    R2,1(R2)            NO/BUMP PRODUCT COUNTER                      
***      LA    R6,6(R6)               BUMP TO NEXT PRODUCT                      
         LA    R6,NXPPLENE(R6)        BUMP TO NEXT PRODUCT                      
         BCT   RE,SPL200                                                        
         DC    H'0'                SHOULD NEVER GET HERE                        
*                                                                               
SPL250   MVC   NBSPLPRN,NXPCL1            RETURN PRODUCT CODE                   
         MVC   NBSPLPR3,NXPCL3            PROD ALPHA                            
         MVC   NBP1SHR,NXPPCT             PRODUCT SHARE (2 DEC)                 
         MVC   SPLITCST,NXPPCT            PRODUCT SHARE (2 DEC)                 
         MVC   NBFEED,NXPFD               PRODUCT FEED                          
***      XC    NBSPLBPN,NBSPLBPN   CLEAR BILLING PRODUCT LIST                   
         ICM   R1,15,NBSPLBPN                                                   
         XC    0(18,R1),0(R1)                                                   
*                                                                               
* IF TROGLADYTE (TRIGBCK) MUST SET NBLEN AND SPLITLEN                           
* ***NOTE *** SETTING SPLITLEN SCREWS UP DEMO VALUES FORGET IT                  
*****    TM    NUPRDIND,X'80'      IS IT TROGLADYTE                             
*******  I DON'T THINK THIS CODE WORKS EVEN NOW SINCE                           
******* NUPRDIND IS AT START OF DSECT - ON 2ND PASS WE                          
******* ARE NOT POINTING TO CORRECT FIELD                                       
**       LA    R1,PRODTBL                                                       
         L     R1,NBAPROD                                                       
         TM    1(R1),X'80'      IS NBXIND TRIGBCK                               
         BNO   SPL300              NO                                           
         L     R1,NBAIO            YES                                          
         USING NURECD,R1                                                        
         ZIC   R5,NULEN            GET TOTAL LENGTH                             
         DROP  R1                                                               
         SR    R0,R0                                                            
         LR    R1,R5                                                            
         D     R0,=F'3'           TROGS ALWAYS = 3                              
         STC   R1,NBLEN                                                         
*                                                                               
SPL300   L     R6,NBAPROD          GET PRODUCT ELEMENT                          
         LTR   R6,R6                                                            
         BNZ   *+6                                                              
         DC    H'0'                MUST BE THERE                                
* - PICK UP COST SPLIT                                                          
         LA    R3,NBACTUAL                                                      
         ICM   R2,15,NBACTUAL         NOW SPLIT ACTUAL                          
         BAS   R5,GETPACC                                                       
         LA    R3,NBASSIGN                                                      
         ICM   R2,15,NBASSIGN         ASSIGNED                                  
         BAS   R5,GETPACC                                                       
***********************************                                             
* IF INTEGRATION HAS SPECIAL PRODUCT                                            
* NBSPLPRN MUST = SPECIAL INT PROD ELSE SKIP INTEG $$$                          
***      CLI   NBINTPRD,0                                                       
***      BE    SPL302                                                           
***      CLC   NBSPLPRN,NBINTPRD       IF SPECIAL INTG PROG                     
***      BE    SPL303                  GET 100% OF INTEG                        
         CLI   INTPROD,0                                                        
         BE    SPL302                                                           
         CLC   NBSPLPR3,INTPROD        IF SPECIAL INTG PROG                     
         BE    SPL303                  GET 100% OF INTEG                        
         XC    NBINTEG,NBINTEG                                                  
**************************************                                          
SPL302   LA    R3,NBINTEG                                                       
         ICM   R2,15,NBINTEG          AND INTEGRATION                           
         BAS   R5,GETPACC                                                       
SPL303   LA    R3,NBSPCHRG                                                      
         ICM   R2,15,NBSPCHRG         AND SPECIAL CHARGES                       
         BAS   R5,GETPACC                                                       
         LA    R3,NBERNCST                                                      
         ICM   R2,15,NBERNCST         AND EARNED COST                           
         BAS   R5,GETPACC                                                       
         LA    R3,NBMAXCST                                                      
         ICM   R2,15,NBMAXCST         AND EARNED COST                           
         BAS   R5,GETPACC                                                       
         LA    R3,NBICOS2                                                       
         ICM   R2,15,NBICOS2          AND ICOS2                                 
         BAS   R5,GETPACC                                                       
         B     SPLITX0                                                          
*                                                                               
GETPACC  XC    WORK,WORK                                                        
         GOTO1 =V(NEPACC),DMCB,(R6),(R2),(1,WORK),NETBLOCK,RR=RELO              
         ZIC   R1,NBSPLCNT         CURRENT PROD NO                              
         LA    R2,WORK                                                          
         MVC   0(4,R3),0(R2)       RETURN SPLIT AMOUNT                          
         LA    R2,4(R2)            BUMP SPLIT AMOUNT TABLE                      
         BCT   R1,*-10                                                          
         BR    R5                                                               
         SPACE 1                                                                
SPLLAST0 TM    NBINDS3,NBI3BPRD    IF BLDPRD MODE                               
         BNO   SPLBLAS0                                                         
         TM    NBSPLOPT,X'20'      IF X'20' BIT IS SET AS WELL                  
         BNO   SPLBLAS0                                                         
******   CLI   NBSPLTYP,C'B'       BILLED/CHANGED PROD MODE?                    
******   BE    *+8                 YES-TABLE ALREADY FILLED                     
******                             Now rebuild table everytime since            
******                             it can not be saved between                  
******                             netvalue calls                               
         BRAS  RE,FUDGEL           RETURNS BILLED/CHANGED PRODS                 
*                                  IN BLDCHGD                                   
         CLI   BLDCHGD,0          ANY BILLED/CHANGED PRODS?                     
         BNE   *+12                                                             
         MVI   NBPRDIDX,0         NO,CLEAR INDEX INTO TABLE                     
         B     SPLBLAS0                                                         
*                                                                               
         LA    RE,BLDCHGD         YES                                           
         ZIC   RF,NBPRDIDX        INDEX                                         
         MHI   RF,3                LENGTH OF PRODS IN TABLE                     
         AR    RE,RF               POINT TO PROD                                
         CLI   0(RE),0             NO MORE PRODS IN TABLE?                      
         BNE   *+12                                                             
         MVI   NBPRDIDX,0          CLEAR INDEX                                  
         B     SPLBLAS0                                                         
         ZIC   RF,NBPRDIDX         SET INDEX TO NEXT PROD                       
         LA    RF,1(RF)                                                         
         STC   RF,NBPRDIDX                                                      
*                                  AND DEAL WITH THIS PROD NOW                  
         MVC   NBSPLPR3,0(RE)      3 CHARACTER                                  
         MVC   WORK(3),NBSPLPR3                                                 
         BRAS  RE,GETPRD2                                                       
         MVC   NBSPLPRN,BYTE       1 CHARACTER (WHAT IF ZERO?)                  
         CLI   BYTE,0              IF OVERFLOW PROD                             
         BNE   SKPSPL                                                           
         MVI   NBSPLPRN,1          SET A 1                                      
SKPSPL   DS    0H                                                               
**                                                                              
         MVI   NBSPLTYP,C'B'                                                    
         XC    NBACTUAL,NBACTUAL   AND TREAT THINGS AS ZERO                     
         XC    NBASSIGN,NBASSIGN                                                
         XC    NBINTEG,NBINTEG                                                  
         XC    SPLITCST,SPLITCST                                                
         MVI   SPLITLEN,0                                                       
         MVI   NBSPLCNT,1          ELSE SCREWS UP IN SRRET (NENETACCN)          
*                                                                               
         B     SPLITX0                                                          
**********************************************************                      
SPLBLAS0 DS    0H                                                               
         CLI   INTPROD,0          SPECIAL INTEG PROD?                           
         BE    SPLBLASX            NO                                           
*****************************************************************               
* SINCE PROGRAM NOW SHARES SPLLAST0 (BLDPRD CHECK)                              
* IN BOTH MULTI PROD AND REGULAR PROD MODES                                     
* IN CASE WE ARE NOT IN MULTI - NEED TO FINISH INTPROD                          
* IN ITS OWN ROUTINE IN MULTI - NEED TO FINISH INTPROD                          
         CLI   NBPRDLST,0          IF NOT MULTI                                 
         BE    SPLBLAST            GO HERE TO FINISH INTPROD                    
******************************************************************              
         CLC   INTPROD,NBSPLPR3   HAVE WE ALREADY HANDLED IT                    
         BE    SPLBLASX            YES                                          
         LA    R1,6                WAS IT ONE OF MULTI PRODS?                   
         ICM   RE,15,NBADPLST      LIST OF 3 CHAR PRODS                         
SPLBLX5  CLC   INTPROD,0(RE)                                                    
         BE    SPLBLASX            YES/MUST HAVE ALREADY HANDLED IT             
         CLI   0(RE),0                                                          
         BE    SPLBLX7                                                          
         LA    RE,3(RE)                                                         
         BCT   R1,SPLBLX5                                                       
*                                                                               
SPLBLX7  XC    NBACTUAL,NBACTUAL   NO-DO IT HERE                                
         XC    NBASSIGN,NBASSIGN                                                
         MVI   SPLITLEN,0                                                       
         MVI   NBINTPRD,X'FF'      SET AS LAST                                  
         XC    SPLITCST,SPLITCST                                                
         B     SPLITX0                                                          
*                                                                               
SPLBLASX MVI   NBSPLPRN,0          RETURN NO FURTHER                            
         MVI   NBSPLTYP,0                                                       
         MVI   NBSPLCNT,0                                                       
         MVI   NBINTPRD,0                                                       
         XC    INTPROD,INTPROD                                                  
         SPACE 1                                                                
SPLITX0  XIT1                                                                   
         DROP  R8                                                               
         EJECT                                                                  
*              ROUTINE TO SPLIT THE COSTS                                       
         SPACE 3                                                                
*              INPUTS              R1=A(FULL WORD TO BE SPLIT)                  
*                                  SPLITCST=H'PERCENT SPLIT NOW'                
         SPACE 1                                                                
SPLITEM0 NTR1                                                                   
*                                                                               
         TM    NBSPLOPT,X'80'      MAKE SURE WE'RE SPLITTING RE GRANT           
         BNO   SPLITEMX                                                         
         L     R6,NBAPROD          GET PRODUCT ELEMENT                          
         LTR   R6,R6                                                            
         BZ    SPLITEMX                                                         
* - PICK UP COST SPLIT                                                          
         XC    WORK,WORK                                                        
         LR    R4,R1                                                            
         L     R2,0(R4)                                                         
         GOTO1 =V(NEPACC),DMCB,(R6),(R2),(1,WORK),NETBLOCK,RR=RELO              
         ZIC   R1,NBSPLCNT         CURRENT PROD NO                              
         LA    R2,WORK                                                          
         MVC   0(4,R4),0(R2)       RETURN SPLIT AMOUNT                          
         LA    R2,4(R2)            BUMP SPLIT AMOUNT TABLE                      
         BCT   R1,*-10                                                          
         B     SPLITEMX                                                         
SPLITEMX XIT1                                                                   
         EJECT                                                                  
*                                                                               
*                                                                               
***********************************************************************         
* TITLE BLDNADDM BUILD NAD DEMO LIST OFF NAD DEFINITION RECORD                  
*                                                                               
**************************************                                          
*                                                                               
* BLDNADDM NTR1                                                                 
*         OC    NBDM,NBDM           DO WE HAVE ADDRESSABILITY                   
*         BZ    BLDNDEX                                                         
*         USING NUNAD,R8                                                        
*         GOTO1 NBHELLO,DMCB,(C'G',UNITFIL),(X'62',NBAIO),0                     
*         CLI   DMCB+12,0                                                       
*         BE    BLDND50                                                         
*                                                                               
* BLDND20  MVI   DINDMSET,X'FF'      INITIALIZE NAD DEMOS                       
*         XC    DINNDCOD,DINNDCOD   CLEAR NAD HOLD AREA                         
*         B     BLDNDEX                                                         
*                                                                               
* BLDND50  L     R8,DMCB+12                                                     
*         CLC   NUNADCD,DINNDCOD                                                
*         BE    BLDNDEX                                                         
*                                                                               
*         MVC   DINNDCOD,NUNADCD                                                
*         GOTO1 =A(XTROUTIN),DMCB,(1,(RC)),RR=RELO  READ NAD RECORD             
*         BNE   BLDND20             RECORD NOT FOUND                            
*         DROP  R8                                                              
*                                                                               
*         USING NNDRECD,R8                                                      
*         LA    R8,WORKREC                                                      
*         GOTO1 NBDATCON,DMCB,(3,NNDACTD),(2,NBNADDEF) LAST ACTIVE DT.          
*                                                                               
*         LA    R8,NNDMAINL                                                     
*         LA    RE,DINDMSET                                                     
*                                                                               
* BLDND100 ZIC   R3,1(R8)                                                       
*         AR    R8,R3                                                           
*         CLI   0(R8),0                                                         
*         BE    BLDND150                                                        
*         CLI   0(R8),X'DD'                                                     
*         BNE   BLDND100                                                        
*         MVC   0(3,RE),3(R8)                                                   
*         LA    RE,3(RE)                                                        
*         B     BLDND100                                                        
*                                                                               
* BLDND150 MVI   0(RE),X'FF'                                                    
*                                                                               
* BLDNDEX  XIT1                                                                 
*         DROP  R8                                                              
*                                                                               
         GETELN R6,NBDTADSP,SRCHEL,2                                            
*         LTORG                                                                 
*                                                                               
*                                                                               
**********************************************************************          
* TITLE BLDNADDM BUILD NAD DEMO LIST OFF NAD DEFINITION RECORD                  
*                                                                               
* *** NOTE ***  DEMO LIST IS NOW BUILT OFF OF NDDEMOS ( WHICH WILL              
*               EITHER BE DEMOS OFF OF DEMO FIELD AT REWQUEST TIME              
*               OR DEMOS FROM EST HEADER READ AT REQUEST TIME)                  
***********************************************************************         
*                                                                               
BLDNADDM NTR1                                                                   
         OC    NBDM,NBDM           DO WE HAVE ADDRESSABILITY                    
         BZ    BLDNDEX                                                          
         USING NUNAD,R8                                                         
**       GOTO1 NBHELLO,DMCB,(C'G',UNITFIL),(X'62',NBAIO),0                      
**       CLI   DMCB+12,0                                                        
**       BE    BLDND50                                                          
         B     BLDND50             ALWAYS TRY TO BUILD LIST                     
*                                                                               
BLDND20  MVI   DINDMSET,X'FF'      INITIALIZE NAD DEMOS                         
         XC    DINNDCOD,DINNDCOD   CLEAR NAD HOLD AREA                          
         B     BLDNDEX                                                          
*                                                                               
BLDND50  DS    0H                                                               
         TM    NBVARIND,X'40'      USE NDDEMOS FOR NAD ?                        
         BNO   BLDND60             NO                                           
         L     R8,NBADEM           YES/USE NDDEMOS, NOT ESTHEADER               
         LTR   R8,R8                                                            
         BZ    BLDND60             NOT AROUND/TRY EST HEADER                    
         USING NDDEMBLK,R8                                                      
         LA    R8,NDDEMOS                                                       
         TM    NBINDS6,NBI6XDEM                                                 
         BNO   *+8                                                              
         USING XDDEMBLK,R8                                                      
         LA    R8,XDDEMOS                                                       
         B     BLDND62                                                          
                                                                                
BLDND60  GOTO1 =A(XTROUTIN),DMCB,(2,(RC)),RR=RELO  READ EST RECORD              
         BNE   BLDND20             RECORD NOT FOUND                             
         DROP  R8                                                               
         USING ESTHDR,R8                                                        
*                                                                               
         TM    NBINDS6,NBI6XDEM                                                 
         BO    BLDX                                                             
         L     R8,AWORKREC        POINT TO ESTIMATE RECORD                      
         MVC   WORK(60),EDEMLST    GET THE STANDARD 20                          
         MVC   WORK+60(3),EDEM21   AND GET 21ST                                 
         LA    R8,WORK            R8 ->                                         
BLDND62  LA    RE,DINDMSET                                                      
         MVI   0(RE),X'FF'                                                      
         LA    RF,21              FOR 21 DEMOS                                  
         TM    NBINDS6,NBI6XDEM                                                 
         BNO   *+8                                                              
         LA    RF,50                                                            
*                                                                               
BLDND100 CLI   0(R8),0                                                          
         BE    BLDND120                                                         
         CLI   0(R8),X'FF'         NDDEMOS WILL HAVE FF AT END                  
         BE    BLDNDEX                                                          
         MVC   0(3,RE),0(R8)                                                    
         MVI   3(RE),X'FF'                                                      
         LA    RE,3(RE)                                                         
BLDND120 LA    R8,3(R8)                                                         
         BCT   RF,BLDND100                                                      
         B     BLDNDEX                                                          
*                                                                               
BLDX     DS    0H                  EXPANDED TO 50 DEMOS                         
         L     R8,AWORKREC        POINT TO ESTIMATE RECORD                      
         MVC   WORK(60),EDEMLST    GET THE STANDARD 20                          
         MVC   WORK+60(3),EDEM21   AND GET 21ST                                 
         OC    EDEM21,EDEM21       IF ZERO                                      
         BNZ   BLDX20                                                           
         MVC   WORK+60(60),EDEMLST1                                             
         MVC   WORK+120(30),EDEMLST2                                            
         LA    RF,50                                                            
         B     *+8                                                              
BLDX20   LA    RF,21                                                            
         LA    RE,DINDMSET                                                      
         LA    R8,WORK                                                          
         MVI   0(RE),X'FF'                                                      
BLDNX100 DS    0H                                                               
         CLI   0(R8),0                                                          
         BE    BLDNX120                                                         
         CLI   0(R8),X'FF'         NDDEMOS WILL HAVE FF AT END                  
         BE    BLDNDEX                                                          
         MVC   0(3,RE),0(R8)                                                    
         MVI   3(RE),X'FF'                                                      
         LA    RE,3(RE)                                                         
BLDNX120 LA    R8,3(R8)                                                         
         BCT   RF,BLDNX100                                                      
         B     BLDNDEX                                                          
*                                                                               
BLDNDEX  XIT1                                                                   
         DROP  R8                                                               
         LTORG                                                                  
                                                                                
*                                                                               
         EJECT                                                                  
DECODEM  NTR1  BASE=*,LABEL=*                                                   
         CLI   NBACTOPT,0                                                       
         BNE   *+12                                                             
         CLI   NBESTOPT,0                                                       
         BE    DECDX                                                            
*                                                                               
         L     R3,NBADEM                                                        
         USING NDDEMBLK,R3                                                      
         LTR   R3,R3                                                            
         BZ    DECDX                                                            
*                                                                               
         OC    NDDEMOS,NDDEMOS             DOING DEMOS?                         
         BZ    DECDX                                                            
         OC    NBDEMCON,NBDEMCON           DO WE HAVE DEMOCON                   
         BNZ   DECD10                                                           
         GOTO1 NBCALLOV,DMCB,0,X'D9000AE0'                                      
         MVC   NBDEMCON,DMCB                                                    
*                                                                               
DECD10   LA    R4,NDDEMOS                                                       
         LA    R5,XDEMTBL2         EXPANDED DEMO TABLE                          
         TM    NBINDS6,NBI6XDEM    WAS THIS PASSED ?                            
         BO    *+8                                                              
         LA    R5,DEMOXTBL         NO,USE WORKING STORAGE TABLE                 
         L     RF,NBDEMCON                                                      
         XC    WORK,WORK                                                        
*                                                                               
         SR    R1,R1               GET NUMBER OF DEMOS PASSED                   
         LR    RE,R4                                                            
DECD20   CLI   0(RE),X'FF'         END OF DEMO LIST                             
         BE    DECD22              YES                                          
         OC    0(2,RE),0(RE)       SOME APPS DO NOT HAVE FF AT END              
         BZ    DECD22                                                           
         LA    RE,3(RE)            NO                                           
         LA    R1,1(R1)                                                         
         B     DECD20                                                           
*                                                                               
DECD22   ST    R4,DMCB             DEMO LIST                                    
         STCM  R1,1,DMCB           PASS NUMER OF DEMOS IN LIST                  
*                                                                               
         PRINT GEN                                                              
         GOTO1 (RF),DMCB,,('DEMOCON_16',(R4)),(R3),(R5)                         
         PRINT NOGEN                                                            
DECDX    XIT1                                                                   
         DROP  R3                                                               
         LTORG                                                                  
*                                                                               
ENCODEM  NTR1  BASE=*,LABEL=*                                                   
         CLI   NBACTOPT,0                                                       
         BNE   *+12                                                             
         CLI   NBESTOPT,0                                                       
         BE    ENCDX                                                            
*                                                                               
         L     R3,NBADEM                                                        
         USING NDDEMBLK,R3                                                      
         LTR   R3,R3                                                            
         BZ    ENCDX                                                            
*                                                                               
         OC    NDDEMOS,NDDEMOS             DOING DEMOS?                         
         BZ    ENCDX                                                            
*                                                                               
         OC    NBDEMCON,NBDEMCON           DO WE HAVE DEMOCON                   
         BNZ   ENCD10                                                           
         GOTO1 NBCALLOV,DMCB,0,X'D9000AE0'                                      
         MVC   NBDEMCON,DMCB                                                    
*                                                                               
ENCD10   LA    R4,NDDEMOS                                                       
         LA    R5,XDEMTBL2                                                      
         TM    NBINDS6,NBI6XDEM    WAS THIS PASSED ?                            
         BO    *+8                                                              
         LA    R5,DEMOXTBL         NO,USE WORKING STORAGE TABLE                 
         L     RF,NBDEMCON                                                      
         XC    WORK,WORK                                                        
*                                                                               
         SR    R1,R1               GET NUMBER OF DEMOS PASSED                   
         LR    RE,R4                                                            
ENCD20   CLI   0(RE),X'FF'         END OF DEMO LIST                             
         BE    ENCD22              YES                                          
         OC    0(2,RE),0(RE)       SOME APPS DO NOT HAVE FF AT END              
         BZ    ENCD22                                                           
         LA    RE,3(RE)            NO                                           
         LA    R1,1(R1)                                                         
         B     ENCD20                                                           
*                                                                               
ENCD22   ST    R4,DMCB             DEMO LIST                                    
         STCM  R1,1,DMCB           PASS NUMER OF DEMOS IN LIST                  
*                                                                               
         GOTO1 (RF),DMCB,,('DEMOCON_17',(R4)),(R3),(R5)                         
ENCDX    XIT1                                                                   
         DROP  R3                                                               
         LTORG                                                                  
*                                                                               
         EJECT                                                                  
*          DATA SET NENETVALAC AT LEVEL 002 AS OF 05/23/07                      
***************************************************                             
** ROUTINE FINDS PRODUCTS ON BILL ELEMENTS THAT ARE                             
** NOT ON THE UNIT -                                                            
***************************************************                             
FUDGEL   NTR1 BASE=*,LABEL=*                                                    
*                                                                               
* CREATE LIST OF BILLED PRODS FROM BILL ELEMENTS                                
         XC    WORK,WORK                                                        
         L     R6,NBAIO                                                         
         MVI   SRCHEL,X'10'                                                     
         BAS   RE,GETEL2                                                        
         BNE   FUDGELX                                                          
         B     FUDG10                                                           
         USING NUBILD,R6                                                        
FUDGNXT  BAS   RE,NEXTEL2                                                       
         BNE   FUDG30                                                           
*                                                                               
FUDG10   DS    0H                  IF UNBILLED                                  
******   TM    NUBILST,X'20'       IF UNBILLED                                  
******   BO    FUDGNXT             SKIP                                         
         CLI   NUBILLEN,29          ELEM WITH 3 CHAR PROD CODE                  
         BL    FUDG20               NO                                          
         MVC   FULL(3),NUBILPRC     YES-SET 3 CHAR PROD                         
         BAS   R5,ADDTOTBL          ADD TO TABEL OF BILLED PRODS                
         B     FUDGNXT                                                          
FUDG20   MVC   BYTE,NUBILPRD        1 BYTE PROD                                 
         BRAS  RE,GETPRD                                                        
         MVC   FULL(3),WORK         3 CHAR PROD                                 
         BAS   R5,ADDTOTBL                                                      
         B     FUDGNXT                                                          
*                                                                               
******************************                                                  
* FULL HAS 3 CHAR PROD CODE - ADD TO TABLE OF                                   
* BILLED PRODS IN WORK+10                                                       
******************************                                                  
ADDTOTBL DS    0H                                                               
         LA    R1,WORK+10                                                       
ADDTO10  CLI   0(R1),0                                                          
         BE    ADDTO20                                                          
         CLC   0(3,R1),FULL        DO WE ALREADY HAVE CODE                      
         BER   R5                                                               
         LA    R1,3(R1)            BUMP PROD SAVE AREA                          
         B     ADDTO10                                                          
ADDTO20  MVC   0(3,R1),FULL                                                     
         BR    R5                                                               
*******************************                                                 
                                                                                
*                                                                               
************************************************                                
* WORK+10 HAS TABLE OF BILLED PRODS                                             
* CHECK AGAINST X'19',X'14',AND NUPRD,NUPRD2                                    
* SET X'FF' IN 1ST BYTE OF BILLED PRODS THAT MATCH                              
*************************************************                               
FUDG30   DS    0H                                                               
         L     R6,NBAIO                                                         
         MVI   SRCHEL,X'19'                                                     
         BAS   RE,GETEL2                                                        
         BE    FUDG50              YES- X'19' ELEM                              
         L     R6,NBAIO                                                         
         MVI   SRCHEL,X'14'                                                     
         BAS   RE,GETEL2                                                        
         BE    FUDG70              YES- X'14' ELEM                              
         B     FUDG35                                                           
                                                                                
*********************************************************************           
*                                  NO X'19' NO X'14' - USE X'01' ELEM           
                                                                                
         DROP  R6                                                               
FUDG35   L     R6,NBAIO                                                         
         MVI   SRCHEL,1            POINT TO MAIN ELEMENT                        
         BAS   RE,GETEL2                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         USING NUMAINEL,R6                                                      
         CLI   NUPRD,0             UNALLOCATED?                                 
         BE    FUDGELX                                                          
         MVC   BYTE,NUPRD          1 BYTE PROD                                  
         BRAS  RE,GETPRD           RETURNS 3 BYTE PROD IN WORK                  
         BAS   R5,CHKBPRD          MATCH AGAINST BILLED PRODS                   
         CLI   NUPRD2,0            PIGGYBACK PROD?                              
         BE    FUDG80                                                           
         MVC   BYTE,NUPRD2         1 BYTE PROD                                  
         BRAS  RE,GETPRD           RETURNS 3 BYTE PROD IN WORK                  
         BAS   R5,CHKBPRD          MATCH AGAINST BILLED PRODS                   
         B     FUDG80                                                           
************************************************************                    
* UNIT  HAS AN X'19' ELEMENT - MATCH AGAINST BILLED PRODS                       
FUDG50   DS    0H                                                               
         USING NUPDED,R6                                                        
         ZIC   R1,NUPDELEN   HOW MANY PRODUCTS?                                 
         S     R1,=F'3'                                                         
         SR    R0,R0                                                            
         D     R0,=F'7'      R1=NUMBER OF PRODS                                 
         LR    R2,R1         USE R2 FOR PROD COUNTER                            
         LA    R6,NUPDEPR                                                       
FUDG55   MVC   WORK(3),0(R6)       PASS PROD IN WORK                            
         BAS   R5,CHKBPRD          MATCH AGAISNT BILLED PRODS                   
         LA    R6,NUPDTLEN(R6)     BUMP TO NEXT PROD IN ELEM                    
         BCT   R2,FUDG55                                                        
         B     FUDG80              NO MORE PRODS ON X'19'                       
                                                                                
****************************************************************                
* UNIT HAS X'14' ELEM - MATCH ITS PRODUCTS AGAINT BILLED PRODS                  
         DROP  R6                                                               
FUDG70   DS    0H                                                               
         USING NUPRDEL,R6                                                       
         ZIC   R1,NUPRDLEN                                                      
         S     R1,=F'3'                                                         
         SR    R0,R0                                                            
         D     R0,=F'6'            R1=NUMBER PF PRODS                           
         LR    R2,R1               USE R2 FOR NO OF PROD COUNT                  
         LA    R6,NUPRDPR                                                       
FUDG75   MVC   BYTE,0(R6)          1 BYTE PROD                                  
         BRAS  RE,GETPRD           RETURNS 3 BYTE IN WORK                       
         BAS   R5,CHKBPRD          MATCH AGAINST BILLED PRODS                   
         LA    R6,6(R6)            BUMP TO NEXT PROD IN ELEM                    
         BCT   R2,FUDG75                                                        
         B     FUDG80              NO MORE PRODS ON X'14'                       
*                                                                               
                                                                                
FUDG80   DS    0H                                                               
*  NOW CHECK AGAINST INTPROD - INTEGRATION PROD                                 
         CLI   INTPROD,0                                                        
         BE    *+14                                                             
         MVC   WORK(3),INTPROD                                                  
         BAS   R5,CHKBPRD                                                       
*  NOW CHECK AGAINST X'03' SPECIAL CHARGE ELEMENT                               
         L     R6,NBAIO                                                         
         MVI   SRCHEL,3                                                         
         BAS   RE,GETEL2                                                        
         BNE   FUDG82X                                                          
         USING NUSPRD,R6                                                        
FUDG80A  CLI   NUSPRLEN,NUSPRLN1   IF OLD STYLE                                 
         BE    FUDG80D             GET NEXT ELEMENT                             
         CLI   NUSPRLEN,NUSPRLN2   1 BYTE STYLE                                 
         BNE   FUDG80B                                                          
         MVC   BYTE,NUSPRBPR       PASS 1 BYTE                                  
         BRAS  RE,GETPRD           RETURNS 3 BYTE IN WORK                       
         BAS   R5,CHKBPRD          MATCH AGAINST BILLED PRODS                   
         B     FUDG80D                                                          
FUDG80B  CLI   NUSPRLEN,NUSPRLN4                                                
         BNE   FUDG80D                                                          
         MVC   WORK(3),NUSPRBPC                                                 
         BAS   R5,CHKBPRD                                                       
FUDG80D  BAS   RE,NEXTEL2                                                       
         BE    FUDG80A                                                          
         BNE   FUDG82X                                                          
*                                                                               
*                                                                               
FUDG82X  LA    R1,WORK+10        SEE IF ANY UNAMTCHED BILLED PRODS              
         LA    R2,WORK+70        SAVE UNMTCHED PRODS HERE                       
         SR    RE,RE               COUNTER OF UNMATCHED PRODS                   
FUDG83   MVI   0(R2),0             SET UNMATCHED TO 0                           
         MVI   BLDCHGD,0           SET UNMATCHED TO 0                           
         CLI   0(R1),0             EOF                                          
         BE    FUDG90                                                           
         CLI   0(R1),X'FF'         PROD ALREADY MATCHED?                        
         BE    FUDG84                                                           
         MVC   0(3,R2),0(R1)         SAVE UNMATCHED PROD                        
         LA    RE,1(RE)            BUMP COUNTER                                 
         LA    R2,3(R2)            BUMP TO NXT SAVE AREA                        
FUDG84   LA    R1,3(R1)            BUMP PROD TABLE                              
         B     FUDG83                                                           
*                                                                               
FUDG90   CLI   WORK+70,0           ANY UNMATCHED BILLED PRODS ?                 
         BE    FUDGELX                NO-THAT'S ALL                             
         C     RE,=F'15'                                                        
         BNH   *+6                                                              
         DC    H'0'                                                             
* DON'T KNOW WHY I MOVED THEM TO WORK+100                                       
**       XC    WORK+100(50),WORK+100                                            
**       MVC   WORK+100(45),WORK+70  HAD TO INCREASE -                          
         B     FUDG91                 GO HANDLE UNMATCHED BILLED PRDS           
*                                                                               
                                                                                
*************************************************                               
* MOVE UNMATCHED BILLED PRODS TO BLDCHGD                                        
*************************************************                               
FUDG91   DS    0H                                                               
***      L     R1,=A(BLDCHGD)                                                   
         LA    R1,BLDCHGD                                                       
         MVC   0(45,R1),WORK+70                                                 
*                                                                               
FUDGELX  XIT1                                                                   
         SPACE                                                                  
******************************************************                          
* MATCH PROD IN WORK AGAINST TABLE OF                                           
* BILLED PRODS AT WORK+10                                                       
* IF BILLED PROD DOES NOT MATCH A PROD ON THE UNIT                              
* WE LATER CREATE A X'19' WITH ALL THE UNIT PRODS + BILLED PROD                 
*********************************************************                       
CHKBPRD  DS    0H                                                               
         LA    R1,WORK+10                                                       
CHKPB5   CLC   WORK(3),0(R1)       UNIT PROD = BILLED PROD ?                    
         BNE   CHKPB7              MATCH -                                      
         MVI   0(R1),X'FF'         SET MATCHED FLAG                             
         BR    R5                                                               
CHKPB7   LA    R1,3(R1)            BUMP BILLED PROD TABLE                       
         CLI   0(R1),0                                                          
         BNE   CHKPB5                                                           
         BR    R5                                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*  THERE MAY BE DUPLICATE PRODS IN PRODLIST FOR MULTIS                          
*  MAKE SURE WE ONLY DEAL WITH IT ONCE FOR BILL AND PAY                         
*                                                                               
CHKDUPRD NTR1  BASE=*,LABEL=*                                                   
         CLI   NBSPLCNT,1          ARE WE ON FIRST PROD?                        
         BNH   CHKDOK                                                           
         ZIC   R1,NBSPLCNT         R1=CURRENT PROD IN LIST                      
         BCTR  R1,0                FOR BCT                                      
         LA    R2,NBPRDLST         LIST OF PRODS                                
CHKD02   CLC   0(1,R2),NBSPLPRN    HAVE WE DONE THIS ALREADY?                   
         BE    CHKD                                                             
         LA    R2,1(R2)                                                         
         BCT   R1,CHKD02                                                        
CHKDOK   LTR   RE,RE                                                            
CHKD     XIT1                                                                   
         LTORG                                                                  
*                                                                               
CHKDUPR3 NTR1  BASE=*,LABEL=*                                                   
         CLI   NBSPLCNT,1          ARE WE ON FIRST PROD?                        
         BNH   CHK3OK                                                           
         ZIC   R1,NBSPLCNT         R1=CURRENT PROD IN LIST                      
         BCTR  R1,0                FOR BCT                                      
         ICM   R2,15,NBADPLST      LIST OF PRODS                                
         BNZ   *+6                                                              
         DC    H'0'                                                             
CHKD32   CLC   0(3,R2),NBSPLPR3    HAVE WE DONE THIS ALREADY?                   
         BE    CHK3D                                                            
         LA    R2,3(R2)                                                         
         BCT   R1,CHKD32                                                        
CHK3OK   LTR   RE,RE                                                            
CHK3D    XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
* CHECK IF UNIT HAS A DEFAULT VTYP                                              
UNVTYP   NTR1  BASE=*,LABEL=*                                                   
         TM    NBVTYPS3,X'01'      USING VTYPS OVERRIDE?                        
         BO    VTYPX               YES-DON'T USE UNIT'S VTYP                    
         MVI   NBVTYPS,0            CLEAR THE FIELD                             
         MVI   NBVTYPS2,0           CLEAR THE FIELD                             
         MVI   NBVTYPS3,0           CLEAR THE FIELD                             
         L     R6,NBAIO                                                         
         MVI   SRCHEL,X'18'                                                     
         BAS   RE,GETEL3                                                        
         BNE   VTYPX                                                            
         USING NUDTAD,R6                                                        
         OC    NUDVTYPE,NUDVTYPE   UNIT'S VTYP                                  
         BZ    VTYPX                                                            
*                                                                               
         MVC   NBVTYPS(2),NUDVTYPE                                              
         TM    NUUNST5,X'20'       POD LOOKUP?                                  
         BNO   *+8                                                              
         OI    NBVTYPS3,X'08'      TURN ON PODCAST                              
         DROP  R6                                                               
*                                                                               
*  MAKE SURE ELEMENT HAS A VALID VTYPE - COULD BE OLD JUNK                      
         LA    RE,VTYPTBL                                                       
VTYP10   CLC   NBVTYPS(2),0(RE)                                                 
         BE    VTYPX                                                            
         LA    RE,2(RE)                                                         
         CLI   0(RE),0                                                          
         BE    VTYP12              NOT IN TABLE                                 
         B     VTYP10                                                           
                                                                                
*                                                                               
VTYPTBL  DC    C'PLPSP7'                                                        
         DC    C'P1P2P3'                                                        
         DC    C'CLCSC7'                                                        
         DC    C'C1C2C3'                                                        
         DC    C'MLMSM7'                                                        
         DC    C'M1M2M3'                                                        
         DC    C'ALASA7'                                                        
         DC    C'N3'                                                            
         DC    X'00'                                                            
*                                                                               
VTYP12   XC    NBVTYPS(2),NBVTYPS   JUNK - SEND NO VTYP                         
VTYPX    XIT1                                                                   
*                                                                               
* CHECK IF UNIT IS COMSCORE                                                     
CHKCS    NTR1  BASE=*,LABEL=*                                                   
         OC    NBEXTEND,NBEXTEND                                                
         JZ    CHKCSX                                                           
         L     RF,NBEXTEND                                                      
         USING NBXTNDD,RF                                                       
*                                                                               
         XC    NBXCSN,NBXCSN       COMSCORE SERIES NUMBER                       
         XC    NBXCVT,NBXCVT       COMSCORE VIEWING TYPE                        
*                                                                               
         L     R6,NBAIO                                                         
         MVI   SRCHEL,NUCSELQ                                                   
         BAS   RE,GETEL3                                                        
         BNE   CHKCSX                                                           
         USING NUCSELD,R6                                                       
*                                                                               
         L     RF,NBEXTEND                                                      
         USING NBXTNDD,RF                                                       
         MVC   NBXCSN,NUCSSN       SERIES #                                     
         MVC   NBXCVT,NUCSVTYP     VIEWING TYPE                                 
         DROP  RF                                                               
*                                                                               
CHKCSX   XIT1                                                                   
         GETELN R6,NBDTADSP,SRCHEL,3                                            
         LTORG                                                                  
*                                                                               
*****************************************************                           
         EJECT                                                                  
************************* HIPO ****************************************         
*  NUMBER:NV1                                                                   
*  TITLE: DODEMS - DEMO LOGIC FOR NETVALUE                                      
*                                                                               
* THIS IS IN THE FORM OF A FINITE STATE MACHINE. THE STATES ARE:                
*  TEST FOR ESTIMATE      (INITIAL) (READ)    STTSTEST    (NV1.1)               
*  TEST FOR ACTUAL        (READ)              STTSTACT    (NV1.2)               
*  DO ESTIMATE            (ACTION)            STDOEST     (NV1.3)               
*  PRE-ACTUALS            (READ)              STPREACT    (NV1.10)              
*  ASSIGN RESULT          (READ)              STASSRES    (NV1.4)               
*  CHECK IF DEMOS EXIST   (ACTION)            STCKEX      (NV1.5)               
*  CHECK FOR BLACKWEEK    (READ)              STCKBW      (NV1.6)               
*  CHECK IF VPHS EXIST    (ACTION)            STCKVPEX    (NV1.7)               
*  PUT EM OUT             (ACTION)            STPUTEM     (NV1.8)               
*  FINISHED               (FINAL)             STEXIT      (NV1.9)               
*                                                                               
* READ STATES DO MINIMAL PROCESSING (NO CALLS TO THE DEMO SYSTEM) AND           
* DETERMINE THE NEXT STATE, WHILE SETTING UP VALUES TO BE USED BY               
* ACTION STATES.                                                                
*                                                                               
* ACTION STATES DO THE BULK OF PROCESSING. THEY MAY ALSO ACT AS READ            
* STATES.                                                                       
*                                                                               
* THE VARIABLE RESULT IS INTERNAL AND IS USED TO GUIDE FLOW.                    
*                                                                               
* A FLOW DIAGRAM SHOWING STATES (BASED ON RESULT) EXISTS AND SHOULD             
* BE MAINTAINED WHEN CHANGES ARE MADE.                                          
*                                                                               
*                                                                               
*  CALLED FROM: UNITREC - PROCESSES A UNIT RECORD                               
*                                                                               
*  CALLS TO:                                                                    
*  BUILD DEMOUT INPUT LIST FOR ALL DEMOS   (NVU.1)    BLDDEMIN                  
*  SET DBLOCK FOR AFFADAVIT DEMOS          (NV1.4.1)  SETATPDM                  
*  SET DBLOCK FOR PROGRAM DEMOS            (NV1.4.2)  SETPRDEM                  
*  SET DBLOCK FOR TIME PERIOD DEMOS        (NV1.4.3)  SETTPDEM                  
*  SET DBLOCK FOR 2 DEC PLACE PRECISSION   (NV1.4.4)  SETDMPRE                  
*  WIPES OUT VPH ELEMENT FROM DBAREC       (NV1.4.5)  SETDMSET                  
*  RESETS DBAREC AFTER DEMOUT CALL         (NV1.4.6)  SETDMPST                  
*  GET DEMOS FROM ESTIMATE ELEMENTS        (NV1.3.1)  ESTOUTS                   
*  GET DEMOS FROM ACTUAL ELEMENTS          (NV1.8.1)  ACTOUTS                   
*  GET DEMOS FROM DEMAND INTO OUTPUT LIST  (NV1.5.1)  GETDEMOS                  
*  COPY ESTIMATE ELEMENTS TO ACTUAL ELEMS  (NV1.8.D.2)ESTTOACT                  
*  GET VPHS FROM DEMAND INTO RECORD        (NV1.7.1)  GETVDEMS                  
*  INITIALIZE DBLOCK FOR GENERAL USE       (NV1.7.2)  INITDB                    
*  INITIALIZE MATHBLOCK FOR GENERAL USE    (NV1.7.3)  INITMATH                  
*  INITIALIZE THE DUMMY RECORD FOR DEMOMATH(NV1.7.4)  INITDUM                   
*  DO ANY WEIGHTED DEMOS                   (NV1.8.4)  WEIGHDEM                  
*  ADJUST IMPRESSIONS FOR ESTIMATED DEMOS  (NV1.3.2)  ADJEST                    
*  ADJUST IMPRESSIONS FOR ACTUAL DEMOS     (NV1.8.5)  ADJACT                    
*  FILL EST DEMOS INTO NEBLOCK             (NV1.8.6)  MOVESTDM                  
*  FILL ACT DEMOS INTO NEBLOCK             (NV1.8.10) MOVACTDM                  
*  CREATE OVERRIDE ELEMENT FOR HOME DATA   (NV1.8.7)  OVERHOME                  
*  MODIFY UNIT RECORD WITH SAVED ELEMENTS  (NV1.8.8)  MERGEDUM                  
*  GET PROGRAM RECORD                      (NV1.8.L.2)GETPRGRC                  
*  GET DEMOS FROM PROGRAM RECORD           (NV1.8.L.3)PRGTOEST                  
*  CREATE A NEW NETVALUE ELEMENT           (NV1.8.9)  NEWNVEL                   
*  SEE IF INFO IN NETVALUE ELEMENT CHANGED (NV1.10.1) CHEKNVEL                  
*                                                                               
*                                                                     *         
*  GLOBAL OUTPUTS:                                                              
*     NETBLOCK - DEMOS RETRIEVED.                                               
*     UNIT RECORD - ACTUAL VPH AND HOMES ELEMENT IF DOESN'T EXIST               
*     UNIT RECORD - NET VALUE ELEMENT.                                          
*                                                                               
*                                                                     *         
*  LOCALS:                                                                      
*   DOALIST- LIST OF DEMO VALUES RETURNED FROM DEMOUT.                          
*            THIS LIST IS MODIFIED BY THE APPROPRIATE ROUTINES AND              
*            IS EVENTUALLY TRANSLATED INTO THE VALUES RETURNED IN               
*            NETBLOCK.                                                          
*     RESULT - USED BY THE STATE MACHINE.                                       
*                                                                               
****************************************************                            
*                                                                               
DODEMS   DS    0D                                                               
         NMOD1 0,**DODEMS,R9,R8                                                 
         L     RC,0(R1)            REESTABLISH WORKING STORAGE                  
         EJECT                                                                  
************************* HIPO ****************************************         
*  NUMBER:NV1.1                                                                 
*  TITLE: STTSTEST - STATE TO TEST IF ESTIMATE DEMOS ARE REQUESTED              
*                                                                               
*  COMMENTS: THIS IS ALSO THE INITIAL STATE.                                    
*            THIS IS A READ STATE WHOSE PURPOSE IS ONLY TO SEE IF               
*     ESTIMATE DEMOS SHOULD BE GIVEN.                                           
*                                                                               
*  INPUTS: NETBLOCK                                                             
*          NBESTOPT - Y - RETURN EST DEMOS FOR ALL BUT MAKEGOODS, PFBS          
*                     M - RETURN EST DEMOS FOR ALL                              
*                     0 - RETURN NO DEMOS                                       
*                     A - NOT FOR MISSED UNITS...                               
*                         YES FOR STATUS OF ESTIMATED                           
*                         YES FOR MAKEGOODS IF ORIGINAL UNIT                    
*                             UNIT HAD A STATUS OF ESTIMATED                    
*                     P - AS ABOVE BUT PFB OK AS WELL                           
*                         OTHERS AS FOR A                                       
*                                                                               
*  OUTPUTS: NBESTUN - SET TO 1 IF ESTIMATES ARE GIVEN, ELSE 0                   
*                                                                               
***********************************************************************         
STTSTEST EQU   *                                                                
*                                                                               
***** INITIALIZATION                                                            
         CLI   NBVTYPS,C'M'        ASKING MINUTE BY MINUTE?                     
         BNE   MINUTX                                                           
         OC    NBAFFTIM,NBAFFTIM   MUST HAVE AFFID TIME                         
         BNZ   MINUTX                                                           
MINUTX   DS    0H                                                               
*                                                                               
         MVC   RESULT(2),=X'0000'  SET RESULT TO 0'S                            
         MVI   NBRESULT,X'00'                                                   
         MVI   NBRESULN,C'U'       NADS FROM UNIT EST                           
         NI    NBUNTSW,X'7F'                                                    
*                                                                               
         CLI   NBPOSTYP,C'C'       CABLE                                        
         BE    *+12                                                             
         CLI   NBSURVEY,C'O'       OTHER                                        
         BNE   *+14                                                             
         MVC   DEMPREC(14),COPREC                                               
         B     *+10                                                             
         MVC   DEMPREC(14),NSPREC                                               
*                                                                               
*-- SET POST AFFIDS PROFILE                                                     
*                                                                               
         TM    NBINDS8,NBI8RNTK   RENTRAK OPTION ?                              
         BNO   NORENTRK                                                         
         MVI   NBUSER2+5,C'Y'      POST AFFID (CABLE)                           
         MVI   NBUSER2+7,C'Y'      POST TIME PERIOD (CABLE)                     
         MVI   SVPSTAFD,C'Y'       FORCE POST AFFID                             
         OC    NBSDAFDT,NBSDAFDT   . YES - DO WE HAVE AFFID DATE?               
         BNZ   TEDZ                                                             
         OC    NBAFFTIM,NBAFFTIM   . YES - DO WE HAVE AFFID TIME?               
         BNZ   TEDZ                                                             
         MVI   SVPSTAFD,C'N'       NO-POST TIME PERIOD                          
         B     TEDZ                                                             
NORENTRK DS    0H                                                               
*                                                                               
         MVC   SVPSTAFD,NBUSER2+3  DEFAULT NET PROFILE                          
         CLI   NBVTYPS,C'M'           IF MINUTE BY MINUTE                       
         BNE   *+8                                                              
         MVI   SVPSTAFD,C'Y'       FORCE POST ON AFFID                          
         BAS   RE,CKCABLE                                                       
         BP    TEDZ                NETWORK                                      
         BM    SETSYND             SYNDICATION                                  
*                                                                               
         BRAS  RE,CHKACP           .SPECIAL VTYPES ?                            
         BNE   SETIT2              .NO                                          
SETIT    CLI   NBUSER2+5,C'Y'      .YES-IF POST ON AFFID ?                      
         BNE   SETIT2              . NO - DO REGULAR PROCESSING                 
         OC    NBSDAFDT,NBSDAFDT   . YES - DO WE HAVE AFFID DATE?               
         BNZ   SETIT1                                                           
         OC    NBAFFTIM,NBAFFTIM   . YES - DO WE HAVE AFFID TIME?               
         BNZ   SETIT1                                                           
         MVI   SVPSTAFD,C'N'       NO-POST TIME PERIOD                          
         B     TEDZ                                                             
*                                                                               
SETIT1   MVI   SVPSTAFD,C'D'       .FORCE POSTAFFID PROFILE SV TO D             
         B     TEDZ                                                             
SETIT2   MVC   SVPSTAFD,NBUSER2+5  CABLE PROFLIE                                
         B     TEDZ                                                             
*                                                                               
SETSYND  MVC   SVPSTAFD,NBUSER2+6  SYNDICATION PROFILE                          
*                                                                               
TEDZ     CLI   NBSPLTYP,C'B'       IF SPLIT TYPE IS BILLED,                     
         BE    ESTNOT                 IGNORE                                    
         CLI   NBESTOPT,C'Y'       CHECK OPTION SETTING                         
         BE    TEDY                                                             
         CLI   NBESTOPT,C'P'       OK FOR PFB BUT NOT FOR MG                    
         BE    TEDP                                                             
         CLI   NBESTOPT,C'M'       ALWAYS RETURN FOR M                          
         BE    ESTOK                                                            
         CLI   NBESTOPT,0          DON'T RETURN FOR ZERO                        
         BE    ESTNOT                                                           
         B     TEDA                                                             
         SPACE 1                                                                
*                                  FOR 'ESTIMATED' SCHEDULES                    
*                                  WHEN NBESTOPT=Y                              
TEDY     TM    NBUNITST,X'01'      RETURN ALL BUT MAKEGOODS                     
         BO    ESTNOT                                                           
         TM    NBUNITST,X'04'                 AND PFBS                          
         BO    ESTNOT                                                           
         TM    NBUNST3,X'02'                  AND ADUS                          
         BO    ESTNOT                                                           
         B     ESTOK                                                            
         SPACE 1                                                                
*                                  WHEN NBESTOPT=P                              
*                                  FOR 'ACTUAL' SCHEDULES                       
TEDP     TM    NBUNITST,X'02'      DO NOT RETURN MISSED                         
         BO    ESTNOT                                                           
         TM    NBUNITST,X'04'      DO RETURN ESTIMATED UNITS (PFB)              
         BO    TEDPA                                                            
         TM    NBUNST3,X'02'       DO RETURN ESTIMATED UNITS (ADU)              
         BNO   ESTOK                                                            
TEDPA    TM    NBUNITST,X'01'      RETURN MAKEGOODS                             
         BNO   ESTOK                                                            
**       TM    NBMGFSTA,X'04'          WHEN THE ORIGINAL WAS NOT PFB            
**       BO    ESTNOT                                                           
         TM    NBMGFSTA,X'02'          BUT WAS MISSED                           
         BO    ESTOK                                                            
         B     ESTNOT                                                           
*                                  FOR 'ACTUAL' SCHEDULES                       
*                                  WHEN NBESTOPT IS SOMETHING ELSE              
TEDA     TM    NBUNITST,X'02'      DO NOT RETURN MISSED                         
         BO    ESTNOT                                                           
         TM    NBUNITST,X'04'      DO RETURN ESTIMATED UNITS (PFB)              
         BO    TEDAA                                                            
         TM    NBUNST3,X'02'       DO RETURN ESTIMATED UNITS (ADU)              
         BNO   ESTOK                                                            
TEDAA    TM    NBUNITST,X'01'      AND RETURN MAKEGOODS                         
         BNO   ESTNOT                                                           
         TM    NBMGFSTA,X'04'          WHEN THE ORIGINAL WAS NOT PFB            
         BO    ESTNOT                                                           
         TM    NBMGFST2,X'02'          WHEN THE ORIGINAL WAS NOT ADU            
         BO    ESTNOT                                                           
         TM    NBMGFSTA,X'02'          BUT WAS MISSED                           
         BO    ESTOK                                                            
         B     ESTNOT                                                           
*                                                                               
**** ACCEPTED. PROCESS ESTIMATE.                                                
ESTOK    OI    NBUNTSW,X'80'       INDICATE UNIT IN ESTIMATED SCHEDULE          
         CLI   NBESTOPT,0          DON'T RETURN FOR ZERO                        
         BE    ESTNOT                                                           
         MVC   NBESTUN,=H'1'       NBESTUN=1                                    
         B     STDOEST             STATE=DO ESTIMATE                            
*                                                                               
**** REJECTED. DONT PROCESS ESTIMATE.                                           
ESTNOT   MVC   NBESTUN,=H'0'       NBESTUN=0                                    
         B     STTSTACT            STATE=TEST FOR ACTUAL                        
*                                                                               
*--DEMO OVERRIDE PRECISION TABLES                                               
COPREC   DC    C'R',X'82',C'S',X'81',C'P',X'82',C'T',X'42',C'H',X'42'           
         DC    C'U',X'44',C'V',X'40'                                            
NSPREC   DC    C'R',X'81',C'S',X'81',C'P',X'81',C'T',X'43',C'H',X'43'           
         DC    C'U',X'44',C'V',X'40'                                            
******************************************************                          
         EJECT                                                                  
************************* HIPO ****************************************         
*  NUMBER:NV1.2                                                                 
*  TITLE: STTSTACT - STATE TO TEST IF ACTUAL DEMOS ARE REQUESTED                
*                                                                               
*  COMMENTS  THIS IS A READ STATE WHOSE PURPOSE IS ONLY TO SEE IF               
*     ACTUAL DEMOS SHOULD BE GIVEN.                                             
*                                                                               
*  INPUTS: NETBLOCK                                                             
*                                                                               
*  OUTPUTS: NBACTUN - SET TO 1 IF ACTUAL DEMOS ARE GIVEN, ELSE 0                
*                                                                               
***********************************************************************         
STTSTACT EQU   *                                                                
         MVC   NBACTUN,=H'0'                                                    
         NI    NBUNTSW,X'BF'                                                    
         CLI   NBSPLTYP,C'B'       IF SPLIT TYPE IS BILLED,                     
         BE    STEXIT                 IGNORE                                    
         TM    NBUNITST,X'42'      IF MISSED OR PREEMPTED                       
         BNZ   STEXIT                 IGNORE                                    
         OI    NBUNTSW,X'40'       INDICATE UNIT IN ACTUAL SCHEDULE             
         CLI   NBACTOPT,C'Y'                                                    
         BNE   STEXIT                                                           
         MVC   NBACTUN,=H'1'                                                    
         B     STPREACT                                                         
*                                                                               
***********************************************************************         
         EJECT                                                                  
************************* HIPO ****************************************         
*  NUMBER:NV1.3                                                                 
*  TITLE: STDOEST  - STATE TO HANDLE ESTIMATE DEMOS                             
*                                                                               
*  COMMENTS  THIS IS AN ACTION STATE WHOSE PURPOSE IS TO HANDLE ALL             
*     REQUESTS FOR ESTIMATE DEMOS. THERE IS ONLY ONE WAY TO RETRIEVE            
*     ESTIMATES SO THIS STATE CONTAINS NO LOGIC. THIS IS THE ESTIMATE           
*     EQUIVALENT OF THE STATE PUT-EM-OUT (USED FOR ACTUALS).                    
*                                                                               
*  OUTPUTS: NB ESTIMATE DEMOS.  (NBESTUN - NBESTDEM)                            
*           UDEORA - SET TO 'E' FOR ESTIMATED DEMOS                             
*                                                                               
*                                                                               
***********************************************************************         
STDOEST  EQU   *                                                                
         MVI   UDEORA,C'E'         .TELL USERDEM WE WANT ESTIMATED DEMS         
         BAS   RE,BLDDEMIN         .BUILD INPUT LIST FOR DEMOUT                 
*                                   .GET ESTIMATED DEMOS INTO DOALIST           
         GOTO1 ASUBR,DMCB,('ESTOUTSE',(RC))                                     
         BAS   RE,USERDEM          .HANDLE USER DEMO                            
         BAS   RE,WEIGHDEM         .HANDLE WEIGHTED DEMO                        
         BAS   RE,ADJEST           .ADJUST DEMOS IN DOALIST (EST)               
         BAS   RE,MOVESTDM         .MOVE DOALIST DEMOS TO NETBLOCK              
*                                                                               
         B     STTSTACT            .STATE=FINISHED                              
         EJECT                                                                  
************************* HIPO ****************************************         
*  NUMBER:NV1.10                                                                
*  TITLE: STPREACT - PRE-ACTUAL STATE                                           
*                                                                               
*  COMMENTS: THIS IS A READ STATE WHOSE PURPOSE IS TO INITIALIZE                
*     VARIABLES NEEDED BY SUBSEQUENT STATES, AND TO CHECK                       
*     IF THE NETVALUE ELEMENT EXISTS OR NOT. IF IT DOES NOT, IT ASSIGNS         
*     RESULT TO THE PROPER VALUE.                                               
*     ALSO CHECKS IF AGENCY IS ALLOWED TO READ FILE.                            
*                                                                               
*     NOTE: THIS STATE IS ACTUALLY TWO READ STATES. THIS SPLIT OCCURRED         
*     2/26/85 TO CORRECT NETVALUE ELEMENT LOGIC. THE SECOND STATE               
*     IS TITLED STPRE2.                                                         
*                                                                               
*  CALLS TO: NETBROWN - GETS BLACKWEEK VALUES                         *         
*            DATCON - CONVERT COMPRESSED DATE TO EBCDIC                         
*            NETWEEK - GETS A NETWORK WEEK FROM A DATE                          
*            BLDDEMIN - BUILD INPUT LIST FOR DEMOUT                             
*            NEXTEL - MACROS WHICH PROCESS ELEMENT RECORDS.                     
*            CHEKNVEL - SETS NVELSTAT TO 'Y' IF ELEMENT HAS CHANGED             
*                                                                     *         
*  INPUTS: NETBLOCK                                                             
*  LOCAL OUTPUTS: SAVEYEAR,SAVEWEEK - DAY,WEEK OF UNIT                          
*                 BLAKLIST -LIST OF WEEKS IF A BLACKWEEK                        
*                 BLAKMASK - BITMASK TELLS IF POCKET PIECE EXISTS FOR           
*                          BLACKWEEKS. BIT SET IF DOESN'T EXIST.                
*                                                                     *         
*  STATE OUTPUTS:                                                     *         
*  RESULT:                                                                      
*     B - AGENCY NOT AUTHORIZED FOR DEMOS                                       
*     E - POCKET PIECE NOT FOUND. USE ESTIMATED                                 
*     L - USE PROGRAM RECORD                                                    
*     T - NTI CABLE. USE TIME PERIOD DEMOS (NO PROG DEMOS EXIST YET)            
*     W - POCKET PIECE NOT FOUND. USE ZEROES.                                   
*     X - USE VALUES IN ACTUAL                                                  
*     Y - CABLE. USE VALUES IN ACTUAL (OVERRIDES)                               
*                                                                               
* LOCALS: R6 - USED (AND UPDATED) BY NEXTEL. CONTAINS THE ADDRESS OF            
*               THE RECORD OR THE CURRENT ELEMENT.                              
*         R7-  THE ADDRESS OF THE UNIT RECORD. USED FOR DSECTS.                 
*         SRCHEL - THE ELEMENT TYPE WHICH NEXTEL WILL SEARCH FOR.               
*                   X'00' MEANS SEARCH ALL.                                     
*                                                                               
**********************************************************************          
         USING NURECD,R7                                                        
STPREACT DS    0H                                                               
*                                                                               
         LA    RF,DOALEN                                                        
         XCEF  DOALIST,(RF)        CLEAR DOALIST FOR ACTUAL DEMOS               
                                                                                
*                                                                               
         CLI   NBVTYPS,C'M'        IF MINUTE X MINUTE                           
         BNE   STPRMXM                                                          
         CLI   NBSTATYP,C'N'      ONLY FOR NETWORK                              
         BE    STPRMXM                                                          
         CLI   NBSTATYP,C'C'      AND CABLE                                     
         BE    STPRMXM                                                          
         XIT1                      ELSE EXIT ACUTAL DEMOS                       
*                                                                               
STPRMXM  DS    0H                                                               
*                                                                               
         L     R7,NBAIO            ADDRESS OF UNIT RECORD                       
*                                                                               
         MVC   KEYSAV2(2),NBACTDAT    SET NBACTDAT IN WORK AREA                 
         OC    NBSDAFDT,NBSDAFDT      IS THERE AFFID DATE                       
         BZ    ST010                                                            
         CLI   NBAFFOPT,C'N'       USE AFFID TIME                               
         BE    ST010                                                            
         CLI   NBAFFOPT,C'Y'                                                    
         BE    ST005                                                            
         CLI   SVPSTAFD,C'N'       POST ON AFFID TIME                           
         BE    ST010                                                            
ST005    MVC   KEYSAV2(2),NBSDAFDT                                              
*                                                                               
ST010    BAS   RE,CHKWNDW          CHECK IF WINDOW BUY                          
*                                   UNCOMPRESS DATE                             
         GOTO1 NBDATCON,DMCB,(2,KEYSAV2),(0,EBCDDAT)                            
*                                                                               
*                                                                               
         L     RF,=V(NETWEEK)      RELOCATE AND GET WEEK                        
         A     RF,RELO                                                          
         GOTO1 (RF),DMCB,EBCDDAT,NBGETDAY,NBADDAY                               
         MVC   SAVEWEEK,DMCB+8     SAVE WEEK (DEMO WK AT DMCB+8)                
*                                                                               
         GOTO1 NBDATCON,DMCB,(2,KEYSAV2),(3,DUB)                                
         MVC   SAVEYEAR,DUB        SAVE YEAR                                    
*                                                                               
*                                                                               
         CLI   SAVEWEEK,1          IF WEEK=1 AND MONTH =12 THEN                 
         BNE   GETHISP                 NEED TO INCREMENT YEAR.                  
         CLI   DUB+1,12                E.G. 12/30/81 IS IN 1ST WEEK             
         BNE   GETHISP                    OF 1982                               
         ZIC   R1,SAVEYEAR                                                      
         LA    R1,1(R1)                                                         
         STC   R1,SAVEYEAR                                                      
*                                                                               
*** PXZ   GETHISP  CLI   NBPOSTYP,C'H'       DOING NHTI DEMOS                   
GETHISP  CLI   NBSURVEY,C'H'       DOING NHTI DEMOS                             
         BNE   GB2                                                              
         CLI   NBUSER1+3,C'H'      WKLY HISP                                    
         BE    GB2                                                              
         CLI   NBUSER1+3,C'B'      WKLY HISP (AND WKLY NAD)                     
         BE    GB2                                                              
*                                                                               
         GOTO1 NBDATCON,DMCB,(0,EBCDDAT),(3,DUB)                                
         MVC   SAVEYEAR(2),DUB     SAVE YEAR/MONTH                              
*    SCAN TABLE FOR ACTUAL NHT DATES                                            
**       L     RE,=A(NADMON)                                                    
**       A     RE,RELO                                                          
*          DATA SET NENTVLDEMO AT LEVEL 013 AS OF 02/25/08                      
**       A     RE,RELO                                                          
         L     R2,NBACOM                                                        
         USING COMFACSD,R2                                                      
         GOTO1 CDEMTABS,DMCB,MTHDATES                                           
         ICM   R2,15,0(R1)   R2->MONTH/DATES TABLE                              
         USING MTHDATD,R2                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         L     R1,4(R1)            LENGTH OF TABLE ENTRY                        
*                                                                               
         CLC   DUB(3),MDEDATE                                                   
         BNH   *+10                                                             
         AR    R2,R1                                                            
         B     *-12                                                             
         CLC   DUB(3),MDSDATE                                                   
         BL    *+10                                                             
         MVC   SAVEYEAR(2),MDNLBOOK   SET BOOK FROM TABLE                       
         DROP  R2                                                               
*                                                                               
GB2      BAS   RE,BLDDEMIN         BUILD LIST OF REQUESTED DEMOS                
         MVI   BLAKMASK,X'00'      INITIALIZE BLAKWEEK MASK                     
*                                                                               
         BAS   RE,CKCABLE          FOR CABLE EST=ACT IF NO AFFID                
         BNZ   IPA05                                                            
         CLI   SVPSTAFD,C'N'      TEST IF POST ON AFFID                         
         BE    STPRE2A                 CHECK TIME PERIOD                        
         OC    NBAFFTIM,NBAFFTIM                                                
         BNZ   IPA05                                                            
         OC    NBSDAFDT,NBSDAFDT                                                
         BZ    STPRE2A                 CHECK TIME PERIOD                        
*                                   SET UP DBLOCK FOR DEMAND                    
IPA05    GOTO1 ASUBR,DMCB,('INITDBE',(RC))                                      
         MVC   DBSELAGY,NBSELAGY                                                
         MVI   DBFUNCT,DBTSTACS                                                 
         GOTO1 NBDEMAND,DMCB,DBLOCK,0,0   (ERROR=0 IF AUTHORIZED)               
IPA10    CLI   DBERROR,0           .IF AGENCY AUTHORIZED FOR DEMOS              
         BE    EPA10                                                            
         OC    DBACTRMK,DBACTRMK                                                
         BZ    IPA20                                                            
         BAS   RE,CKCABLE          CHECK IF CABLE                               
         BNZ   EPA10               NO                                           
IPA20    CLI   NBREVOPT,C'B'       .THIS AREA FOR CABLE ONLY                    
         BE    STPRE2                                                           
         CLI   NBREVOPT,C'C'                                                    
         BE    STPRE2                                                           
IPA30    CLI   NBREVOPT,C'E'         USE PROGRAM REC FOR EST DEMOS              
         BE    EPA1001               YES                                        
         MVI   RESULT,C'B'         ..RESULT=UNAUTHORIZED AGCY. USE EST          
         CLI   NBUSER1+8,C'Y'      ..IF EST = ACT PROFILE SET                   
         BE    STCKMAN                                                          
         CLI   NBUSER1+8,C'L'      ..IF EST = ACT PROFILE SET                   
         BE    STCKMAN                                                          
         MVI   RESULT,C'W'         ..RESULT = ESTIMATED                         
         B     STCKMAN                                                          
*                                                                               
EPA10    LA    R6,NUMAINEL         .ELSEIF NV ELEMENT EXISTS                    
         MVC   SRCHEL,NVELCODE                                                  
         BAS   RE,NEXTEL           ..(SETS COND CODE IF ELEMENT EXISTS)         
         BNE   EPA11                                                            
         BAS   RE,CHEKNVEL         ..IF NO CHANGE IN NV ELEMENT                 
         CLI   NVELSTAT,C'Y'       ..(R6 CONTAINS LOC OF NVEL)                  
         BE    EPA111                                                           
*--CHECK PROFILES TO SEE IF AFFIDAVIT TIME SHOULD BE USED                       
         CLI   NBAFFOPT,C'Y'       USE AFFIDAVIT TIME                           
         BE    EPA10A                                                           
         CLI   NBAFFOPT,C'N'       DONT USE AFFIDAVIT TIME                      
         BE    EPA111                                                           
         CLI   SVPSTAFD,C'N'      TEST IF POST ON AFFID                         
         BE    EPA111                                                           
*                                                                               
EPA10A   CLI   NBUSER2+4,C'Y'      GAA=YES FORCES A LOOKUP                      
         BE    EPA111              SO SAYS ZEN.                                 
         MVI   RESULT,C'X'         ..RESULT=USE VALUES IN ACTUAL                
         B     STPUTEM             ..STATE=PUT EM OUT                           
*                                  ..ELSE                                       
EPA111   BAS   RE,DELACTEL         ...DELETE NV AND ACTUAL ELEMENTS             
         B     STPRE2              ...STATE=PRE-ACTUAL STATE 2                  
*                                  ..FI                                         
*                                  .ELSE                                        
EPA11    B     STPRE2              ..STATE=PRE-ACTUAL STATE 2                   
*                                  .FI                                          
*                                                                               
*                                                                               
****** THIS WAS SPLIT INTO TWO READ STATES TO CORRECT NETVALUE                  
****** ELEMENT LOGIC ON 2/26/85. TECHNICALLY, THIS SHOULD BE                    
****** ANOTHER STATE BECAUSE SOME CONDITIONS IN THE ABOVE LOGIC                 
****** GO TO THIS STATE AND OTHERS DONT.                                        
*                                                                               
STPRE2A  DS    0H                                                               
***********************************************************************         
* CHECK FOR C, A, AND P1,P2,P3 V TYPES ********************************         
*                                                                               
         BRAS  RE,CHKACP           SPECIAL A,C,P1,P2,P3 VTYPES?                 
         BNE   STPRE1              NO                                           
         CLI   SVPSTAFD,C'N'       YES - POST AFFID?                            
         BNE   STPRE1A                   YES                                    
*                                        NO                                     
*                                                                               
STPRE1   CLI   NBUSER2+7,C'N'      IS TIME PERIOD POST OK FOR CABLE             
         BNE   STPRE2                                                           
STPRE1A  CLI   NBUSER1+8,C'N'      EST AS ACT                                   
         BE    ECE12AAA            NO-RETURN Z                                  
         MVI   RESULT,C'E'                                                      
         B     STPUTEM                                                          
*                                                                               
*                                   ..VALIDATE BOOK                             
STPRE2   DS    0H                                                               
         CLI   NBACTOPT,0          LET'S DO IT LIKE THIS                        
         BE    EPA1000A            (NO ACTUAL-SKIP)                             
*                                                                               
STPRE2B  GOTO1 ASUBR,DMCB,('INITDBE',(RC))                                      
*                                                                               
**************************************************                              
         CLI   NBVTYPS,C'M'          IF ASKING FOR MXM                          
         BNE   STPRE2C                                                          
         MVC   DBFILE,=C'RLD'        USE RLD FILE                               
         MVI   DBFUNCT,DBVLNBK                                                  
         B     STPRE2F                                                          
**************************************************                              
*                                                                               
STPRE2C  MVI   DBFUNCT,DBVLNBK                                                  
         CLI   DBSELSTA+4,C'S'     DEFAULT FOR SYND                             
         BNE   STPRE2G                                                          
         CLC   =C'SJ',NBSELAGY      ,,, IF M2                                   
         BE    *+14                                                             
         CLC   =C'M2',NBSELAGY      ,,, IF M2                                   
         BNE   STPRE2D                                                          
         CLC   =C'SIN',DBSELSTA     ,,, AND SIN, USE SIN                        
         BE    STPRE2G                                                          
         CLC   =C'ITN',DBSELSTA     ,,, AND ITN, USE ITN                        
         BE    STPRE2G                                                          
STPRE2D  MVC   DBSELSTA(4),=C'WB  ' ,,, ALL OTHER SYND USE WB                   
*                                                                               
STPRE2G  CLI   NBSURVEY,C'H'            >>>HISPANIC                             
         BNE   *+16                                                             
         MVC   DBSELSTA+4(1),NBSURVEY                                           
         MVC   DBFILE(3),=C'NAD'        >>>USES NAD                             
*                                                                               
         CLI   NBTCARLV,0          TCAR LOOK UP?                                
         BE    *+8                                                              
         MVI   DBBTYPE,C'V'        FORCE BKTYPE V                               
*                                                                               
         CLI   NBTCARLV,C'5'       LESS THAN THIS NEEDS TO BE WB1 FILE          
         BL    STPRE2F                                                          
         CLC   DBSELSTA(4),=C'WB  ' WB NEEDS TO BE WB1 FILE                     
         BE    STPRE2F                                                          
         MVI   DBBTYPE,C'T'                                                     
*                                                                               
STPRE2F  GOTO1 NBDEMAND,DMCB,DBLOCK,0                                           
         CLI   DBACTSTA+4,C'D'         ,,,DAILY BOOK                            
         BNE   *+8                                                              
         MVI   STATUS,C'D'             ,,,BUT NOTHING FOUND                     
                                                                                
***********************************************************                     
***********************************************************                     
IPA100   CLC   =C'RLD',DBFILE      RLD FILE ?                                   
         BNE   IPA101              NO                                           
         CLI   DBERROR,0           YES-POCKET PIECE EXISTS?                     
         BNE   IPA100X             NO                                           
                                                                                
*                                  YES                                          
         OC    NBAFFTIM,NBAFFTIM   AFFID TIME ?                                 
         BZ    IPA100A                                                          
         B     EAR122                                                           
*                                                                               
IPA100A  OI    NBVTYPS3,X'04'      IGNORE VTYP SETTING                          
*                                  CLEARING NBVTYPS DISABLES NEWRIGEN           
*                                  OVERRIDE OPTION FOR VTYP=NNN                 
**IPA100A  XC    NBVTYPS(2),NBVTYPS  CLEAR VTYPS                                
*                                                                               
         CLI   NBSTATYP,C'N'       NETWORK STATION ?                            
         BNE   IPA100B                                                          
         CLI   NBUSER+12,C'Y'      YES-DEFAULT TO TP                            
*        BE    TIMEPERD                TIME PERIOD                              
         BE    EAR130C                 TIME PERIOD                              
         B     IPA100Z                                                          
*                                                                               
IPA100B  CLI   NBUSER2+7,C'Y'       ASSUME CABLE                                
*        BE    TIMEPERD                                                         
         BE    EAR130C                 TIME PERIOD                              
         B     IPA100Z                                                          
*                                                                               
                                                                                
*                                                                               
IPA100X  DS    0H                **NO POCKET PIECE**                            
         CLI   NBSTATYP,C'N'       NETWORK                                      
         BNE   IPA100Y                                                          
         CLI   NBUSER1+7,C'Y'      EST AS ACT                                   
         BE    IPA100ZZ                                                         
         B     IPA100Z                                                          
*                                                                               
IPA100Y  CLI   NBUSER1+8,C'Y'      EST AS ACT (CABLE)                           
         BE    IPA100ZZ                                                         
         B     IPA100Z                                                          
*                                                                               
IPA100Z  MVI   RESULT,C'Z'                                                      
         B     STCKMAN                                                          
IPA100ZZ MVI   RESULT,C'E'                                                      
         B     STPUTEM                                                          
***********************************************************                     
***********************************************************                     
*                                                                               
IPA101   CLI   DBERROR,0           ..IF POCKET PIECE DOESN'T EXIST              
         BE    EPA101                 POCKET PIECE EXISTS                       
*                                                                               
         GOTO1 ASUBR,DMCB,('INITDBE',(RC))                                      
         MVI   DBSELSTA+4,C'N'                                                  
         MVI   DBFUNCT,DBVLNBK                                                  
         CLI   NBSURVEY,C'H'                                                    
         BNE   *+16                                                             
         MVC   DBSELSTA+4(1),NBSURVEY                                           
         MVC   DBFILE(3),=C'NAD'                                                
         GOTO1 NBDEMAND,DMCB,DBLOCK,0                                           
         CLI   DBERROR,0                                                        
         BE    IPA105                                                           
*                                                                               
         BRAS  RE,CHKACP           SPECIAL V TYPE READ                          
         BNE   EPA1000A                                                         
         OC    NBAFFTIM,NBAFFTIM   YES - AFFID TIME ON UNIT ?                   
         BNZ   EPA1000                                                          
         OC    NBSDAFDT,NBSDAFDT         AFFID DATE ON UNIT ?                   
         BNZ   EPA1000                                                          
         CLI   NBUSER2+5,C'Y'      POST ON AFFID?                               
         BNE   *+8                                                              
         OI    NBVTYPS3,X'06'     SET X'02' AND X'04'                           
*                                                                               
*************************************************************                   
IPA105   CLI   NBREVOPT,C'B'                                                    
         BE    EPA1000                                                          
         CLI   NBREVOPT,C'C'                                                    
         BE    EPA1000                                                          
*                                   .....SET UP FOR TIME PERIOD DEMOS           
TIMEPERD GOTO1 ASUBR,DMCB,('STTPDEME',(RC))                                     
         MVI   DBSELSTA+4,C'N'                                                  
         MVI   RESULT,C'T'         .....RESULT=USE TIME PERIOD                  
         B     STCKEX              .....STATE=CK IF EXIST                       
EPA1000A BAS   RE,CKCABLE          ...ELSEIF CABLE TV STATION                   
         BP    EPA1000                                                          
         BZ    EPA1000B                                                         
         LA    R6,NUMAINEL         ....IF DEMO OVERRIDE ELEMS EXIST             
         MVI   SRCHEL,X'47'                                                     
         BAS   RE,NEXTEL                                                        
         BE    EPA1000                                                          
EPA1000B CLI   NBREVOPT,0          ** IF REVOPT SET                             
         BNE   EPA1000                                                          
         LA    R6,NUMAINEL         ....IF DEMO OVERRIDE ELEMS EXIST             
EPA1000C MVI   SRCHEL,X'DE'                                                     
         BAS   RE,NEXTEL                                                        
         BNE   EPA10000                                                         
         TM    NUOVFLG-NUOVEL(R6),X'80'   MIGHT BE NAD FROM FILE                
         BO    EPA1000C                    TRY NEXT ELEMENT                     
         MVI   RESULT,C'Y'         .....RESULT=CABLE. USE MANUALS               
         B     STPUTEM             .....STATE=PUT EM OUT                        
EPA10000 CLI   NBSURVEY,C'S'       ....CHECK SYNDICATION PROFILE                
         BNE   *+16                                                             
         CLI   NBUSER1+6,C'N'                                                   
         BE    EPA10001                                                         
         B     *+12                                                             
         CLI   NBUSER1+8,C'N'      ....ELSEIF REPORT CABLES EST AS ACT          
         BE    EPA10001                                                         
         MVI   RESULT,C'E'         .....RESULT=USE ESTIMATED                    
         B     STPUTEM             .....STATE=PUT EM OUT                        
*                                  ....ELSE                                     
EPA10001 MVI   RESULT,C'W'         .....RESULT=REPORT AS 0S                     
         B     STPUTEM             .....STATE=PUT EM OUT                        
*                                  ....FI                                       
EPA1000  CLI   NBREVOPT,C'E'       ...ELSEIF NBREVOPT NE E                      
         BE    EPA1001                                                          
         CLI   NBREVOPT,C'A'       ...AND NBREVOPT NE A                         
         BE    EPA1002                                                          
         CLI   NBREVOPT,C'B'       ...AND NBREVOPT NE B                         
         BE    EPA1002                                                          
         CLI   NBREVOPT,C'C'       ...AND NBREVOPT NE C                         
         BE    EPA1001                                                          
*                                                                               
         CLI   NBSURVEY,C'S'       ....CHECK SYNDICATION PROFILE                
         BNE   *+16                                                             
         CLI   NBUSER1+6,C'N'      ....EST AS ACT FOR SYND?                     
         BE    EPA10021            NO                                           
         B     EPA10011            YES                                          
*                                                                               
         CLI   NBSURVEY,C'N'       ....CHECK NETWORK PROFILE                    
         BNE   *+16                                                             
         CLI   NBUSER1+7,C'N'      ....IF REPORT EST AS ACT                     
         BE    EPA10021                                                         
         B     EPA10011                                                         
*                                                                               
         CLI   NBUSER1+8,C'N'      ALL ELSE CHECK CABLE PROFILE                 
         BE    EPA10021            NO - EST AS ACT                              
         B     EPA10011            YES                                          
*                                                                               
EPA10011 MVI   RESULT,C'E'         .....RESULT=USE ESTIMATED                    
         B     STPUTEM             .....STATE=PUT EM OUT                        
*                                  ....ELSE                                     
EPA10021 MVI   RESULT,C'W'         .....RESULT=NO POCKET PIECE YET              
         B     STPUTEM             .....STATE=PUT EM OUT                        
*                                  ....FI                                       
*                                  ...ELSE                                      
EPA1001  MVI   RESULT,C'N'         ....RESULT=USE PROGRAM RECORD                
         B     STPUTEM             ....STATE=PUTEMOUT                           
*                                  ...FI                                        
EPA1002  MVI   RESULT,C'L'         ...RESULT=LATEST ESTIMATE AS ACTUALS         
         B     STPUTEM                                                          
*                                                                               
EPA101   EQU   *                   ..ELSE                                       
         CLI   NBREVOPT,C'B'            IF = B                                  
         BE    EPA1002                  OVERRIDE POCKETPIECE                    
         CLI   NBREVOPT,C'C'            IF = C                                  
         BE    EPA1001                                                          
         B     STASSRES            ...STATE=ASSIGN ACTUALS                      
*                                  ..FI                                         
*                                  .FI                                          
**                                                                              
CHKWNDW  NTR1                                                                   
         TM    NBUNST3,X'08'                                                    
         BZ    CHKWNDEX                                                         
         MVC   KEYSAV2(2),=XL2'F198'  DEFAULT BOOK DEC24/2020                   
         MVI   DUB,C'W'                                                         
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNTFIL  '),(X'60',NBAIO),(1,DUB)           
         CLI   DMCB+12,0           DONT ADJUST IF OVERRIDDEN                    
         BNE   CHKWNDEX                                                         
         L     R5,DMCB+12                                                       
         MVC   KEYSAV2(2),3(R5)                                                 
CHKWNDEX B     XIT                                                              
******************************************************                          
**** CHECK IF STATION IS CABLE (I.E NON-NETWORK)                                
**** CTL BIT SET TO 0 IF CABLE.                                                 
CKCABLE  NTR1                                                                   
** PXZ   CLI   NBPOSTYP,C'N'       IS STATION NETWORK                           
**       BE    ISNET                                                            
**       CLI   NBPOSTYP,C'H'       IS STATION HISPANIC                          
**       BE    ISNET                                                            
**       CLI   NBPOSTYP,C'S'       IS STATION SYNDICATION                       
**       BE    ISSYN                                                            
**       CLI   NBPOSTYP,C'C'       IS STATION CABLE                             
**       BE    ISCABLE                                                          
**       CLI   NBPOSTYP,C'O'       IS STATION OTHERS TREAT AS CABLE             
**       BE    ISCABLE                                                          
         CLI   NBSURVEY,C'N'       IS STATION NETWORK                           
         BE    ISNET                                                            
         CLI   NBSURVEY,C'H'       IS STATION HISPANIC                          
         BE    ISNET                                                            
         CLI   NBSURVEY,C'S'       IS STATION SYNDICATION                       
         BE    ISSYN                                                            
         CLI   NBSURVEY,C'C'       IS STATION CABLE                             
         BE    ISCABLE                                                          
         CLI   NBSURVEY,C'O'       IS STATION OTHERS TREAT AS CABLE             
         BE    ISCABLE                                                          
         B     ISNET                                                            
ISCABLE  SR    R1,R1               CABLE. SET TO 0.                             
         B     XITCKC                                                           
ISNET    LA    R1,1                                                             
         B     XITCKC                                                           
ISSYN    SR    R1,R1                                                            
         BCTR  R1,0                                                             
XITCKC   LTR   R1,R1                                                            
         B     XIT                                                              
         EJECT                                                                  
************************* HIPO ****************************************         
*  NUMBER:NV1.4                                                                 
*  TITLE: STASSRES - ASSIGN RESULT STATE                              *         
*                                                                     *         
*  COMMENTS: THIS IS A READ STATE WHOSE PURPOSE IS TO GIVE RESULT               
*     AN INITIAL VALUE BASED ON THE CONTENTS OF NETBLOCK.,                      
*                                                                     *         
*  INPUTS: NETBLOCK                                                             
*                                                                     *         
*  STATE OUTPUTS:                                                     *         
*  RESULT:                                                                      
*     A - AFFADAVIT                                                             
*     M - MANUAL OVERRIDE                                                       
*     P - PROGRAM DEMOS                                                         
*     PT - PROGRAM, USE TIME PERIOD IF NONE EXIST                               
*     T - TIME PERIOD DEMOS                                                     
*     Z - ZEROES                                                                
*                                                                               
* INPUTS: R7-  THE ADDRESS OF THE UNIT RECORD. USED FOR DSECTS.                 
*                                                                     *         
* LOCALS: R6 - USED (AND UPDATED) BY NEXTEL. CONTAINS THE ADDRESS OF            
*               THE RECORD OR THE CURRENT ELEMENT.                              
*         SRCHEL - THE ELEMENT TYPE WHICH NEXTEL WILL SEARCH FOR.               
*                   X'00' MEANS SEARCH ALL.                                     
***********************************************************************         
STASSRES EQU   *                                                                
*                                  ******* REMOVED FROM LOGIC*****              
EAR11    CLC   =C'ITN',NBACTNET    IF ITN                                       
         BE    IAR120              USE PROGRAM                                  
         CLC   =C'SIN',NBACTNET    IF SIN                                       
         BE    IAR120              USE PROGRAM                                  
         CLC   =C'WB',NBACTNET     OR WB                                        
         BE    IAR120              USE PROGRAM                                  
         CLI   NBAFFOPT,C'Y'       USE AFFIDAVIT TIME                           
         BE    EAR11A                                                           
         CLI   NBAFFOPT,C'N'       DONT USE AFFIDAVIT TIME                      
         BE    EAR12                                                            
         CLI   SVPSTAFD,C'N'      TEST IF POST ON AFFID                         
         BE    EAR12                                                            
EAR11A   OC    NBAFFTIM,NBAFFTIM   .ELSEIF AFFTIM NE 0                          
         BZ    EAR12                                                            
** PXZ   CLI   NBPOSTYP,C'H'       NO TP FOR HISPANIC                           
***      BE    EAR12                                                            
***      CLI   NBPOSTYP,C'C'       CABLE NEEDS TO SELECT LEVEL                  
***      BNE   EAR11B                                                           
         CLI   NBSURVEY,C'H'       NO TP FOR HISPANIC                           
         BE    EAR12                                                            
         CLI   NBSURVEY,C'C'       CABLE NEEDS TO SELECT LEVEL                  
         BNE   EAR11B                                                           
         CLI   SVPSTAFD,C'P'       PROGRAM LEVEL                                
         BE    EAR122                                                           
         CLI   SVPSTAFD,C'T'       TRACK LEVEL                                  
         BE    EAR122                                                           
         CLI   SVPSTAFD,C'D'       DAILY TELECAST                               
         BE    EAR122                                                           
*                                   ..SET UP DBLOCK FOR AFF TIME PER            
EAR11B   GOTO1 ASUBR,DMCB,('STATPDME',(RC))                                     
         MVI   RESULT,C'A'         ..RESULT=AFFADAVIT                           
*                                                                               
         CLI   NBVTYPS,C'M'          BUT IF MINUTE X MINUTE                     
         BNE   EAR11F                                                           
         MVI   RESULT,C'U'           SET MXM FLAG                               
         TM    NBVTYPS3,X'08'      MINUTE BY MINUTE POD                         
         BZ    *+8                                                              
         MVI   RESULT+1,C'P'         YES                                        
*                                                                               
EAR11F   B     STCKEX              ..STATE=CHECK IF EXIST                       
*                                                                               
EAR12    DS    0H                                                               
         CLI   NBSURVEY,C'N'       IF NETWORK                                   
         BE    EAR12C              IGNORE NBI3AIRV HERE                         
         CLI   NBSURVEY,C'S'       IF SYNDICAtion                               
         BE    EAR12C              IGNORE NBI3AIRV HERE                         
         TM    NBINDS3,NBI3AIRV    ELSE IF VENDING STATION                      
         BO    EAR130B             NTI NOT ALLOWED                              
EAR12C   CLI   NBSURVEY,C'C'       (CABLE DOESN'T HAVE PROGRAM DATA             
         BE    EAR130B             SO SKIP ROUND PROFILE TEST)                  
         CLI   NBSURVEY,C'O'       (OTHER DOESN'T HAVE PROGRAM DATA             
         BE    EAR130B             SO SKIP ROUND PROFILE TEST)                  
         CLI   NBSURVEY,C'S'       (SYND. WILL NOT DEFAULT TO TIME              
         BE    IAR120              PERIOD IF NO NTI CODE)                       
         CLI   NBSURVEY,C'H'       (HISP. WILL NOT DEFAULT TO TIME              
         BE    IAR120              PERIOD IF NO NTI CODE)                       
         CLI   NBUSER+12,C'N'      .ELSEIF NBUSER+12 EQ N                       
         BNE   EAR13                                                            
IAR120   OC    NBNTI,NBNTI         ..IF NTI EQ 0                                
         BNZ   EAR121                                                           
         CLI   NBSURVEY,C'H'       HISPANIC - USE PROGRAMS/FOR TP               
         BE    EAR121                                                           
         MVI   RESULT,C'Z'         ...RESULT=ZEROES                             
         CLI   STATUS,C'D'    DAILY DATA IN BUT NOT YET                         
         BE    RESULTE        DO ESTIMATE                                       
         CLC   =C'H9',NBSELAGY  IF STARCOM                                      
         BE    CHKSURV          STAR NEVER WANT A Z                             
         CLC   =C'DU',NBSELAGY  OR MEDIAVEST                                    
         BE    CHKRESE                                                          
         B     RESULTPT          NO-RESULT Z                                    
CHKRESE  OC    NBNTI,NBNTI      AND NO NTI                                      
         BNZ   RESULTPT         NO-RESULT Z                                     
CHKSURV  CLI   NBSURVEY,C'S'    AND SYNDICATION                                 
         BNE   RESULTPT         NO-RESULT Z                                     
RESULTE  MVI   RESULT,C'E'      DO ESTIMATE                                     
RESULTPT B     STCKMAN          WILL CHECK FOR MANUAL OVERRIDES AND             
*                               IF THEY EXIST RESULT-> M. ELSE GOES TO          
*                               STPUTEM NOT TOUCHING RESULT.                    
*                                  ..ELSE                                       
*                                   ..SET DBLOCK FOR PROGRAM DEMOS              
EAR121   GOTO1 ASUBR,DMCB,('STPRDEME',(RC))                                     
         MVI   RESULT,C'P'         ..RESULT=PROGRAM DEMOS                       
         B     STCKEX              ..STATE=CHECK IF EXIST                       
*                                                                               
* FOR CABLE AFFIDAVITS **************                                           
*                                   ..SET DBLOCK FOR PROGRAM DEMOS              
EAR122   MVC   RESULT(1),NBMIRTYP                                               
         MVI   NBMIRTYP,0                                                       
         GOTO1 ASUBR,DMCB,('STPRDEME',(RC))                                     
         MVC   NBMIRTYP,RESULT                                                  
         GOTO1 ASUBR,DMCB,('SETATPDE',(RC))                                     
         MVI   RESULT,C'A'               ..RESULT=A=PROGRAM DEMOS               
         CLI   NBVTYPS,C'M'                                                     
         BNE   EAR122X                                                          
         MVI   RESULT,C'U'               ..RESULT=U=PROGRAM DEMOS               
         TM    NBVTYPS3,X'08'                                                   
         BZ    *+8                                                              
         MVI   RESULT+1,C'P'                                                    
EAR122X  B     STCKEX                    ..STATE=CHECK IF EXIST                 
**************************************                                          
*                                  ..FI                                         
EAR13    EQU   *                   .ELSE                                        
EAR130   OC    NBNTI,NBNTI         ..IF NTI EQ 0                                
         BNZ   EAR131                                                           
*                                   ...SET DBLOCK FOR TIME PERIOD DEMOS         
EAR130B  DS    0H                                                               
         BRAS  RE,CHKACP           SPECIAL V TYPE READ                          
         BNE   EAR130C                                                          
         CLI   NBUSER2+5,C'Y'      POST ON AFFID?                               
         BNE   *+8                                                              
         OI    NBVTYPS3,X'04'      NENTVLDEMO IGNORES NBVTYPS                   
*                                                                               
EAR130C  GOTO1 ASUBR,DMCB,('STTPDEME',(RC))                                     
         MVI   RESULT,C'T'         ...RESULT=TIME PERIOD DEMOS                  
         B     STCKEX              ...STATE=CHECK IF EXIST                      
*                                  ..ELSE                                       
*                                   ...SET DBLOCK FOR PROGRAM DEMOS             
EAR131   GOTO1 ASUBR,DMCB,('STPRDEME',(RC))                                     
         MVC   RESULT(2),=C'PT'    ...RESULT=PROG,TP IF NONE                    
         B     STCKEX              ...STATE=CHECK IF EXIST                      
*                                  ..FI                                         
*                                  .FI                                          
         DROP  R7                                                               
***********************************************************************         
         EJECT                                                                  
************************* HIPO ****************************************         
*  NUMBER:NV1.5                                                                 
*  TITLE: STCKEX   - STATE TO CHECK IF DEMOS EXIST                              
*                                                                               
*  COMMENTS  THIS IS AN ACTION STATE WHOSE PURPOSE IS TO GET THE                
*     DEMO VALUES FROM DEMAND. IF DEMOS EXIST, IT GOES TO THE CHECK             
*     FOR BLACK WEEK STATE, OTHERWISE IT CHECKS PARAMETERS AND                  
*     GOES TO THE APPROPRIATE STATE. THIS STATE COULDNT CARE LESS               
*     ABOUT BLACK WEEKS.                                                        
*                                                                               
*  OUTPUTS: DOALIST - GETS FILLED IF DEMOS ARE GOTTEN.                          
*                                                                               
*  INPUT RESULT:                                                                
*       A - AFFADAVIT                                                           
*       P - PROGRAM DEMOS                                                       
*       PT - PROGRAM,TRY TIME PERIOD IF NONE EXIST                              
*       T - TIME PERIOD DEMOS                                                   
*                                                                               
*  OUTPUT RESULT:                                                               
*       A-   (FOUND DEMOS)                                                      
*       P -  (FOUND DEMOS)                                                      
*       PT - (FOUND DEMOS)                                                      
*       T - IF INPUT T THEN FOUND DEMOS                                         
*           IF INPUT PT THEN PROG DEMOS NOT FOUND                               
*       Z - ZEROES  (DEMOS NOT FOUND)                                           
***********************************************************************         
STCKEX   EQU   *                                                                
*                                   .GET DEMOS FROM DEMAND INTO DOALIST         
         GOTO1 ASUBR,DMCB,('GETDEMOE',(RC))                                     
ICE10    CLI   STATUS,C'Y'         .IF DEMOS WERE GOTTEN                        
         BNE   ECE11                                                            
         B     STPUTEM                                             *            
********************************************************************            
*                                                                               
ECE11    CLI   RESULT+1,C'T'       .ELSEIF RESULT=PT                            
         BNE   ECE12                                                            
         MVC   RESULT(2),=C'T '    ...CHANGE RESULT TO T                        
*                                   ...SET DBLOCK FOR TIME PERIOD               
         GOTO1 ASUBR,DMCB,('STTPDEME',(RC))                                     
         B     STCKEX              ...STATE=CKECK IF EXIST                      
*                                  .ELSE                                        
ECE12    TM    NBVTYPS3,X'02'        BOOK ALSO NOT IN ?                         
         BNO   *+12                                                             
         NI    NBVTYPS3,X'FF'-X'02'  CLEAR C3 READ FLAG                         
         B     ECE12AA                                                          
ECE12AAA MVI   RESULT,C'Z'         ...RESULT=ZEROES                             
         CLC   =C'H9',NBSELAGY                                                  
         BNE   ECE12A                                                           
         CLI   NBSURVEY,C'S'                                                    
         BNE   ECE12A                                                           
ECE12AA  MVI   RESULT,C'E'                                                      
         B     ECE12D                                                           
ECE12A   CLC   =C'DU',NBSELAGY     MEDIAVEST (GOOD OLD)                         
         BE    *+10                                                             
         CLC   =C'SJ',NBSELAGY     MEDIAVEST (GOOD OLD)                         
         BE    *+10                                                             
         CLC   =C'M2',NBSELAGY     MENYN                                        
         BNE   ECE12B                                                           
         CLC   =C'SIN',NBACTNET                                                 
         BE    *+10                                                             
         CLC   =C'ITN',NBACTNET                                                 
         BE    *+14                                                             
         CLC   =C'WB',NBACTNET                                                  
         BNE   ECE12B                                                           
         MVI   RESULT,C'E'                                                      
ECE12B   CLI   STATUS,C'D'         DAILY DATA NOT FOUND                         
         BNE   ECE12D                                                           
         MVI   RESULT,C'E'                                                      
         CLI   NBREVOPT,C'A'       USE LATEST FOR EST DEMOS                     
         BNE   *+8                                                              
         MVI   RESULT,C'L'         YES                                          
         B     STPUTEM                                                          
*                                  .ELSE                                        
ECE12D   B     STCKMAN             ...STATE=CHECK FOR MANUAL OVERRIDES          
*                                  .FI                                          
******************************************************                          
         EJECT                                                                  
************************* HIPO ****************************************         
*  TITLE: STCKMAN  - STATE TO CHECK IF MANUAL OVERRIDES EXIST                   
*                                                                               
*  COMMENTS  THIS IS AN ACTION STATE WHOSE PURPOSE IS TO SEE IF                 
*     MANUAL OVERRIDES EXIST.                                                   
*                                                                               
*  INPUT RESULT:                                                                
*       Z - REPORT ZERO DEMOS. NO DEMOS FOUND.                                  
*       B - REPORT ESTIMATED DEMOS. UNAUTORIZED AGENCY.                         
*                                                                               
*  OUTPUT RESULT:                                                               
*       SAME AS INPUT - NO MANUAL DEMOS                                         
*       M -  MANUAL DEMOS                                                       
***********************************************************************         
STCKMAN  EQU   *                                                                
         USING NURECD,R7                                                        
         L     R7,NBAIO                                                         
         LA    R6,NUMAINEL         .IF DEMO OVERRIDE ELEMS EXIST                
         USING NUOVD,R6                                                         
         MVI   SRCHEL,X'DE'                                                     
STCKMAN1 BAS   RE,NEXTEL                                                        
         BNE   ECM11                                                            
         TM    NUOVFLG,X'80'       NAD DEMO FROM FILE                           
         BO    STCKMAN1            TRY NEXT OVERRIDE ELEMENT                    
         MVI   RESULT,C'M'         ..RESULT=MANUAL OVERRIDE                     
         B     STPUTEM             ..STATE=PUT EM OUT                           
*                                  .ELSE                                        
ECM11    B     STPUTEM             ..LEAVE RESULT AS IT IS                      
*                                  ..STATE=PUT EM OUT                           
*                                  .FI                                          
         DROP  R7,R6                                                            
************************* HIPO ****************************************         
         EJECT                                                                  
************************* HIPO ****************************************         
*  NUMBER:NV1.8                                                                 
*  TITLE: STPUTEM  - STATE TO PUT ACTUAL DEMOS IN NETBLOCK                      
*                                                                               
*  COMMENTS  THIS IS AN ACTION STATE WHOSE PURPOSE IS TO PUT THE                
*     ACTUAL DEMO VALUES IN THE FIELDS NBACTSHR-NBACTDEM. IT ALSO               
*     MAINTAINS THE ACTUAL DEMO ELEMENTS IN THE UNIT RECORD.                    
*  ACTIONS ARE DEPENDENT ON THE VALUE OF RESULT. A HASH TABLE IS                
*     USED TO ACCESS THE APPROPRIATE ROUTINE DEPENDING ON THE A-Z VALUE         
*     OF RESULT.                                                                
*                                                                               
*  OUTPUTS: NETBLOCK- FILLS IN VALUES FOR ACTUAL DEMOS.                         
*           UNIT RECORD - MAY ADD OR MODIFY DEMO ELEMENTS                       
*           NBRESULT - SAME AS RESULT EXCEPT THAT IF RESULT=X THEN              
*                      NBRESULT IS FROM THE NETVALUE ELEMENT                    
*           UDEORA - C'A' TO GET ACTUAL USER DEMOS                              
*                                                                               
*  INPUT RESULT:                                                                
*       SEE HASH TABLE                                                          
*                                                                               
***********************************************************************         
STPUTEM  EQU   *                                                                
         MVI   UDEORA,C'A'         WANT ACTUAL DEMOS                            
         MVC   NBRESULT,RESULT     SET NBRESULT                                 
         MVC   NBRES2,RESULT       SET SECONDARY RESULT (MAY BE 'X')            
*                                                                               
         CLI   NBVTYPS,C'M'        MXM?                                         
         BNE   STPUTM2                                                          
         TM    NBVTYPS3,X'08'      POD REPORTING                                
         BZ    STPUTM2                                                          
         MVC   NBRES2,RESULT+1     SET RESULT 'UP'                              
STPUTM2  DS    0H                                                               
*                                                                               
         L     R1,NBAIO                                                         
*                                                                               
         MVC   BYTE,RESULT         SAVE RESULT IN BYTE                          
         TR    BYTE,ORDTBL-193     CHANGE BYTE TO THE TRANSLATED VALU           
         ZIC   R4,BYTE                                                          
         SLL   R4,2                MULT BY 4 (BRANCHES ARE 4 BYTES)             
         B     HASHTBL(R4)         ADDRESS OF PROPER BRANCH                     
*                                                                               
HASHTBL  DS    0F                  TABLE OF ROUTINES,BASED ON RESULT            
*                                  RESULT     DESCRIPTION          ORD          
         DC    F'0'                0     ALLOWS FOR ORD(0)          0           
         B     DOACTNB             A     AFFADAVIT                  1           
         B     DOESTACT            B     UNAUTHORIZED AGENCY        2           
*        B     DOBWBVPH            C     TIME VPH AVGS,BACK VPH     3           
         DC    F'0'                                                             
*        B     DOESTVPH            D     VPHS FROM ESTIMATE         4           
         DC    F'0'                                                             
         B     DOESTACT            E     ALL FROM ESTIMATE          5           
         B     DOFORMUL            F     USE FACTOR ON ESTS         6           
*        B     DOBWAVVP            G     TIME VPH AVGS, FORWARD VPH 7           
         DC    F'0'                                                             
*        B     DOESTVPH            H     VPHS FROM EST. FORWARD VPH 8           
         DC    F'0'                                                             
         B     DOFORMUL            I     USE FACTORS ON ESTS,       9           
*                                          FORWARD VPHS                         
         B     DOACTNB             J     GAA LINE                   1           
         DC    F'0'                K     **UNUSED**                 11          
         B     DOLATEST            L     LATEST ESTIMATES AS ACTUAL 12          
         B     DOUSEMAN            M     MANUAL OVERRIDE            13          
         B     DOPRGREC            N     PROGRAM RECORD             14          
         DC    F'0'                O     **UNUSED**                 15          
         B     DOACTNB             P     PROGRAM                    16          
         B     DOACTNB             Q     **UNUSED**                 17          
*        B     DOBWBVPH            R     PROG, VPH AVG, BAKWARD VPH 18          
         DC    F'0'                Q     **UNUSED**                 17          
*        B     DOBWAVVP            S     PROG, VPH AVG, FORWARD VPH 19          
         DC    F'0'                Q     **UNUSED**                 17          
         B     DOACTNB             T     TIME PERIOD                20          
         B     DOACTNB             U     MXM DATA                   21          
         DC    F'0'                V     **UNUSED**                 22          
         B     DOZERO              W     NO POCKET PIECE. USE 0S    23          
         B     DOUSEACT            X     ACTUAL NETVALUE ELEM       24          
         B     DOUSEMAN            Y     CABLE. USE MANUAL OVERS    25          
         B     DOZERO              Z     ZEROESED                   26          
*                                                                               
******************************************************                          
         EJECT                                                                  
*********** ACTUAL DEMOS NON-BLACK WEEK ***********************                 
* FOR RESULT= T,P   DEMOS ARE ALREADY IN DOALIST                                
DOACTNB  EQU   *                                                                
         BAS   RE,NEWNVEL          MODIFY NETVALUE ELEMENT                      
         BAS   RE,MERGEDUM         ADD NEW ACTUAL DEMO ELEMENTS TO UNIT         
*                                   GET DEMOS FROM THE NEW UNIT                 
         GOTO1 ASUBR,DMCB,('ACTOUTSE',(RC))                                     
         BAS   RE,USERDEM          FILL IN USER DEMO VALUES                     
         BAS   RE,WEIGHDEM         DO REQUESTED WEIGHTED DEMOS                  
         BAS   RE,ADJACT           ADJUST REQUESTED IMPRESSIONS                 
         BRAS  RE,ADJ2RNDA         ADJUST REQUESTED IMPRESSIONS                 
         BAS   RE,MOVACTDM         FILL DEMOS IN NETBLOCK                       
         B     STEXIT              STATE=FINISHED                               
*                                                                               
***************************************************************                 
***************************************************************                 
*                                                                               
***********     USE ESTIMATED DEMOS FOR ACTUALS ***************                 
* FOR RESULT=E,B                                                                
DOESTACT EQU   *                                                                
*                                  GET ALL DEMOS FROM ESTIMATED ELEMS           
*                                                                               
         TM    NBINDS2,NBNOEACT    NOESTASACT ?                                 
         BO    DOEAX               EXIT                                         
*                                                                               
         GOTO1 ASUBR,DMCB,('ESTOUTSE',(RC))                                     
         BAS   RE,USERDEM          FILL IN USER DEMO VALUES                     
         BAS   RE,WEIGHDEM         DO REQUESTED WEIGHTED DEMOS                  
         BAS   RE,ADJEST           ADJUST REQUESTED IMPRESSIONS                 
         BRAS  RE,ADJ2RNDA          ROUND ALL FOR 2 DEC AGY                     
         BAS   RE,MOVACTDM         FILL DEMOS IN NETBLOCK                       
* CHECK FOR ESTIMATED ACTUAL DEMOS                                              
* TRICK HERE IS THAT DC ELEMS ARE EITHER R OR T WHILE                           
* DEMOS IN NDDEMOS CAN BE R,T,I AND OTHERS.                                     
         GOTO1 NBHELLO,DMCB,(C'G',UNTFIL),(X'DC',NBAIO),0                       
         CLI   DMCB+12,0           DC OVERRIDE ELEMS?                           
         BNE   DOEAX               NO                                           
         L     R1,DMCB+12          YES                                          
         USING NUOVD,R1                                                         
         ICM   R7,15,NBADEM        NET DEMO BLOCK AROUND?                       
         BZ    DOEAX                                                            
         USING NDDEMBLK,R7                                                      
DOEA3    TM    NBINDS6,NBI6XDEM    EXPANDED DEMO LIST                           
         BO    DOEA3B                                                           
         LA    R3,NDDEMOS          GET DEMOS LIST                               
         LA    R4,NDACTDEM         ACT DEMO VALUES                              
         ZIC   R5,NDNDEMOS                                                      
         LTR   R5,R5                                                            
         BZ    DOEAX                                                            
         B     DOEA5                                                            
         USING XDDEMBLK,R7                                                      
DOEA3B   DS    0H                  EXPANDED DEMO LIST                           
         LA    R3,XDDEMOS          GET DEMOS LIST                               
         LA    R4,XDACTDEM         ACT DEMO VALUES                              
         ZIC   R5,XDNDEMOS                                                      
         LTR   R5,R5                                                            
         BZ    DOEAX                                                            
DOEA5    CLC   NUOVCAT,0(R3)       MATCH DEMO CATEGORY?                         
         BNE   DOEA10                                                           
         CLC   NUOVNUM,2(R3)       MATCH DEMO NUMBER?                           
         BNE   DOEA10                                                           
         CLI   NUOVMOD,C'R'        RTG ?                                        
         BNE   DOEA7                                                            
         MVC   2(2,R4),NUOVVAL+2   SET RATING                                   
         B     *+10                                                             
DOEA7    MVC   4(4,R4),NUOVVAL     ASSUME IMPRESSION                            
                                                                                
** ADJUST IMPS/RTGS                                                             
** CABLE IMPS NOT ADJUSTED                                                      
** CABLE RTGS ADJUSTED DOWN IF NOT REQUESTING CABLE PREC                        
** NET/SYN IMPS/RTGS X 10 IF HUNDREDS OPTION IS ON                              
         CLI   NUOVPRE,X'42'       CABLE PRECISION IMPS?                        
         BE    DOEA10              DON'T ADJUST                                 
         CLI   NUOVPRE,X'82'       ..IF CABLE PRECISION RTG                     
         BNE   DOEA7B                                                           
         CLI   NBPREOPT,C'Y'       ..IF REQUESTING CABLE PRE                    
         BE    DOEA10              DO NOTHING                                   
         TM    NBINDS3,NBI3A2DC   ..TWO DECIAL AGENCY                           
         BO    DOEA10              DO NOTHING                                   
*                                  ELSE ROUND UP BY 10                          
         SR    RE,RE                                                            
         ICM   RE,3,NUOVVAL+2                                                   
         SR    RF,RF                                                            
         SRDA  RE,31                                                            
         D     RE,=F'10'                                                        
         LTR   RF,RF                                                            
         BM    *+8                                                              
         AHI   RF,1                                                             
         SRA   RF,1                                                             
         STCM  RF,3,2(R4)                                                       
         B     DOEA10                                                           
* NETWORK/SYNDICATION COMES HERE                                                
DOEA7B   CLI   NBHUNOPT,C'Y'       ADJUST DC ELEMS ?                            
         BNE   DOEA10                                                           
         ICM   RF,15,NUOVVAL       BUMP BY 10                                   
         SR    RE,RE                                                            
         M     RE,=F'10'                                                        
         STCM  RF,15,4(R4)                                                      
*                                                                               
DOEA10   LA    R4,8(R4)            BUMP DEMOVALUES                              
         LA    R3,3(R3)            BUMP LIST OF DEMOS                           
         BCT   R5,DOEA5                                                         
                                                                                
DOEA8    ZIC   RE,1(R1)            GET NEXT DC ELEM                             
         AR    R1,RE                                                            
         CLI   0(R1),X'DC'                                                      
         BE    DOEA3               DO NEXT DC ELEM                              
         B     DOEAX               THAT'S ALL                                   
*                                                                               
DOEAX    EQU   *                                                                
         B     STEXIT              STATE=FINISHED                               
         DROP  R1,R7                                                            
*                                                                               
***************************************************************                 
*   USE ACT HH/EST HH FORMULA ON ESTIMATED RATINGS                              
*                                                                               
* FOR RESULT=F,I                                                                
DOFORMUL EQU   *                                                                
*                                   CREATE ACTUAL DEMO ELEMS FROM EST.          
         GOTO1 ASUBR,DMCB,('ESTOACTE',(RC)),0                                   
*                                   ADJUST BY FORMULA                           
         GOTO1 ASUBR,DMCB,('USEFORME',(RC))                                     
         BAS   RE,NEWNVEL          MODIFY NETVALUE ELEMENT                      
*                                   OVERRIDE HOME DATA FROM DOALIST             
         GOTO1 ASUBR,DMCB,('OVRHOMEE',(RC))                                     
         BAS   RE,MERGEDUM         MODIFY ACTUAL DEMO ELEMENTS IN UNIT          
*                                   GET DEMOS FROM THE NEW UNIT                 
         GOTO1 ASUBR,DMCB,('ACTOUTSE',(RC))                                     
         BAS   RE,USERDEM          FILL IN USER DEMO VALUES                     
         BAS   RE,WEIGHDEM         DO REQUESTED WEIGHTED DEMOS                  
         BAS   RE,ADJEST           ADJUST REQUESTED IMPRESSIONS                 
         BRAS  RE,ADJ2RNDA                                                      
         BAS   RE,MOVACTDM         MOVE THE REQUESTED DEMOS TO NETBLOCK         
         B     STEXIT                                                           
*********** DEMOS ARE FROM THE PROGRAM RECORD ************************          
* LATEST ESTIMATE FROM PROGRAM RECORD - OUTPUT IS ESTIMATED DEMOS               
* FOR RESULT = N                                                                
DOPRGREC EQU   *                                                                
         MVI   UDEORA,C'E'         RESET USER DEMO TYPE                         
         BAS   RE,GETPRGRC         LOOKUP PROGRAM RECORD                        
         BNE   STEXIT              PROGRAM RECORD NOT FOUND                     
*                                   MERGE UNIT EVN DEMOS WITH VPH ELEM          
         GOTO1 ASUBR,DMCB,('PRGTOESE',(RC))                                     
         BAS   RE,MERGEEST         MODIFY ESTIMATED DEMO ELEMENTS               
*                                  GET ALL DEMOS FROM ESTIMATED ELEMS           
         GOTO1 ASUBR,DMCB,('ESTOUTSE',(RC))                                     
         BAS   RE,USERDEM          FILL IN USER DEMO VALUES                     
         BAS   RE,WEIGHDEM         DO REQUSETED WEIGHTED DEMOS                  
         BAS   RE,ADJEST           ADJUST REQUESTED IMPRESSIONS                 
         BAS   RE,MOVESTDM         MOVE THE REQUESTED DEMOS TO NETBLOCK         
         B     STEXIT              STATE=FINISHED                               
***************************************************************                 
*                                                                               
***************************************************************                 
* LATEST ESTIMATES FROM PROGRAM RECORD USED AS ACTUALS                          
* FOR RESULT = L                                                                
DOLATEST EQU   *                                                                
         BAS   RE,GETPRGRC         LOOKUP PROGRAM RECORD                        
         BNE   STEXIT              PROGRAM RECORD NOT FOUND                     
*                                   MERGE UNIT EVN DEMOS WITH VPH ELEM          
         GOTO1 ASUBR,DMCB,('PRGTOESE',(RC))                                     
*                                   CONVERT EST DEMOS TO ACTUALS                
         GOTO1 ASUBR,DMCB,('ESTOACTE',(RC)),1                                   
         BAS   RE,NEWNVEL          MODIFY NETVALUE ELEMENT                      
         BAS   RE,MERGEDUM         MODIFY ACTUAL DEMO ELEMENTS IN UNIT          
*                                   GET DEMOS FROM THE NEW UNIT                 
         GOTO1 ASUBR,DMCB,('ACTOUTSE',(RC))                                     
         BAS   RE,USERDEM          FILL IN USER DEMO VALUES                     
         BAS   RE,WEIGHDEM         DO REQUESTED WEIGHTED DEMOS                  
         BAS   RE,ADJEST           ADJUST REQUESTED IMPRESSIONS                 
         BRAS  RE,ADJ2RNDA          ROUND ALL IN 2 DEC AGY                      
         BAS   RE,MOVACTDM         MOVE THE REQUESTED DEMOS TO NETBLOCK         
         B     STEXIT              STATE=FINISHED                               
***************************************************************                 
*                                                                               
***************************************************************                 
******* DEMOS ARE DIRECTLY FROM EXISTING ACTUAL ELEMENTS ************           
*******  NETVALUE ELEMENT DOESNT EXIST. MANUAL OVERRIDES                        
* FOR RESULT = M,Y                                                              
DOUSEMAN EQU   *                                                                
*                                   GET ALL DEMOS FROM ACTUAL ELEMS             
         GOTO1 ASUBR,DMCB,('MANOUTSE',(RC))                                     
         BAS   RE,USERDEM          FILL IN USER DEMO VALUES                     
         BAS   RE,WEIGHDEM         DO REQUESTED WEIGHTED DEMOS                  
         BAS   RE,ADJACT           ADJUST REQUESTED IMPRESSIONS                 
         BRAS  RE,ADJ2RNDA          ROUND ALL IN 2 DEC AGY                      
         BAS   RE,MOVACTDM         FILL DEMOS IN NETBLOCK                       
         B     STEXIT              STATE=FINISHED                               
***************************************************************                 
*                                                                               
******* DEMOS ARE DIRECTLY FROM EXISTING ACTUAL ELEMENTS ************           
*******  NETVALUE ELEMENT EXISTS                                                
* FOR RESULT = X                                                                
DOUSEACT EQU   *                                                                
         MVC   NBRESULT,SAVRESLT   PUT WHERE DEMOS CAME FROM ORIGINALLY         
*                                    INTO NBRESULT                              
         GOTO1 ASUBR,DMCB,('ACTOUTSE',(RC))                                     
         BAS   RE,USERDEM          FILL IN USER DEMO VALUES                     
         BAS   RE,WEIGHDEM         DO REQUESTED WEIGHTED DEMOS                  
         BAS   RE,ADJACT           ADJUST REQUESTED IMPRESSIONS                 
         BRAS  RE,ADJ2RNDA         ADJUST REQUESTED IMPRESSIONS                 
         BAS   RE,MOVACTDM         FILL DEMOS IN NETBLOCK                       
         B     STEXIT              STATE=FINISHED                               
***************************************************************                 
*                                                                               
***** ALL DEMOS ARE ZEROES  *******************                                 
*  FOR RESULT = Z,W                                                             
DOZERO   EQU   *                                                                
*                          '       EST/ACT OVERRIDE UNIT?                       
**       GOTO1 NBHELLO,DMCB,(C'G',UNTFIL),(X'DC',NBAIO),0                       
**       CLI   DMCB+12,0           DC OVERRIDE ELEMS?                           
**       BNE   DOZEROX                                                          
**       L     R1,DMCB+12                                                       
**       USING NUOVD,R1                                                         
**DC10     CLI   0(R1),X'DC'         CHANGE DC -> DE                            
**         BNE   DCX                                                            
**         MVI   0(R1),X'DE'         CHANGE TO DE                               
**         OI    NUOVFLG,X'10'       SET INDICATOR OF CHANGE                    
**         ZIC   RE,1(R1)                                                       
**         AR    R1,RE                                                          
**         B     DC10                                                           
**DCX      EQU   *                                                              
**         B     DOUSEMAN                                                       
**         DROP  R1                                                             
* CHECK FOR ESTIMATED ACTUAL DEMOS                                              
* TRICK HERE IS THAT DC ELEMS ARE EITHER R OR T WHILE                           
* DEMOS IN NDDEMOS CAN BE R,T,I AND OTHERS.                                     
         GOTO1 NBHELLO,DMCB,(C'G',UNTFIL),(X'DC',NBAIO),0                       
         CLI   DMCB+12,0           DC OVERRIDE ELEMS?                           
         BNE   DOZAX               NO                                           
         L     R1,DMCB+12          YES                                          
         USING NUOVD,R1                                                         
         ICM   R7,15,NBADEM        NET DEMO BLOCK AROUND?                       
         BZ    DOZAX                                                            
         USING NDDEMBLK,R7                                                      
DOZA3    TM    NBINDS6,NBI6XDEM    EXPANDED DEMO BLOCK?                         
         BO    DOZA3B                                                           
         LA    R3,NDDEMOS          GET DEMOS LIST                               
         LA    R4,NDACTDEM         ACT DEMO VALUES                              
         ZIC   R5,NDNDEMOS                                                      
         LTR   R5,R5                                                            
         BZ    DOZAX                                                            
         B     DOZA5                                                            
         USING XDDEMBLK,R7                                                      
DOZA3B   LA    R3,XDDEMOS          YES/EXPANDED DEMO BLOCK                      
         LA    R4,XDACTDEM                                                      
         ZIC   R5,XDNDEMOS                                                      
         LTR   R5,R5                                                            
         BZ    DOZAX                                                            
DOZA5    CLC   NUOVCAT,0(R3)       MATCH DEMO CATEGORY?                         
         BNE   DOZA10                                                           
         CLC   NUOVNUM,2(R3)       MATCH DEMO NUMBER?                           
         BNE   DOZA10                                                           
         CLI   NUOVMOD,C'R'        RTG ?                                        
         BNE   DOZA7                                                            
         MVC   2(2,R4),NUOVVAL+2   SET RATING                                   
         B     *+10                                                             
DOZA7    MVC   4(4,R4),NUOVVAL     ASSUME IMPRESSION                            
                                                                                
** ADJUST IMPS/RTGS                                                             
** CABLE IMPS NOT ADJUSTED                                                      
** CABLE RTGS ADJUSTED DOWN IF NOT REQUESTING CABLE PREC                        
** NET/SYN IMPS/RTGS X 10 IF HUNDREDS OPTION IS ON                              
         CLI   NUOVPRE,X'42'       CABLE PRECISION IMPS?                        
         BE    DOZA10              DON'T ADJUST                                 
         CLI   NUOVPRE,X'82'       ..IF CABLE PRECISION RTG                     
         BNE   DOZA7B                                                           
         CLI   NBPREOPT,C'Y'       ..IF REQUESTING CABLE PRE                    
         BE    DOZA10              DO NOTHING                                   
         TM    NBINDS3,NBI3A2DC    ..IF 2 DECIMAL AGENCY                        
         BO    DOZA10              DO NOTHING                                   
*                                  ELSE ROUND UP BY 10                          
         SR    RE,RE                                                            
         ICM   RE,3,NUOVVAL+2                                                   
         SR    RF,RF                                                            
         SRDA  RE,31                                                            
         D     RE,=F'10'                                                        
         LTR   RF,RF                                                            
         BM    *+8                                                              
         AHI   RF,1                                                             
         SRA   RF,1                                                             
         STCM  RF,3,2(R4)                                                       
         B     DOZA10                                                           
* NETWORK/SYNDICATION COMES HERE                                                
DOZA7B   CLI   NBHUNOPT,C'Y'       ADJUST DC ELEMS ?                            
         BNE   DOZA10                                                           
         ICM   RF,15,NUOVVAL       BUMP BY 10                                   
         SR    RE,RE                                                            
         M     RE,=F'10'                                                        
         STCM  RF,15,4(R4)                                                      
*                                                                               
DOZA10   LA    R4,8(R4)            BUMP DEMOVALUES                              
         LA    R3,3(R3)            BUMP LIST OF DEMOS                           
         BCT   R5,DOZA5                                                         
                                                                                
DOZA8    ZIC   RE,1(R1)            GET NEXT DC ELEM                             
         AR    R1,RE                                                            
         CLI   0(R1),X'DC'                                                      
         BE    DOZA3               DO NEXT DC ELEM                              
         B     DOZAX               THAT'S ALL                                   
*                                                                               
DOZAX    EQU   *                                                                
         DROP  R1,R7                                                            
DOZEROX  B     STEXIT              STATE=FINISHED                               
***************************************************************                 
         EJECT                                                                  
********************************************************                        
*  STEXIT - END STATE FOR DODEMS                                                
****************************************************                            
*                                                                               
STEXIT   B     XIT                                                              
******************************************************                          
         EJECT                                                                  
******************************************************                          
* NVU.1                                                                         
* SUBROUTINE BLDDEMIN - BUILD INPUT LIST FOR DEMOUT.                            
*   TAKES 3-CHAR DEMO CODE REQUESTS FROM NETBLOCK AND INSERTS                   
*   THE APPROPRIATE CHARACTER TO REQUEST VPHS, GRPS AND IMPS.                   
*                                                                               
*  CALLED FROM: DODEMS - FILL IN ALL DEMO STUFF IN NETBLOCK           *         
*                                                                     *         
*  INPUTS: NDDEMOS - CONTAINS THE 3-CHAR DEMOS REQUESTED              *         
*                                                                     *         
*  OUTPUTS: DIALIST - LIST USED AS INPUT TO DEMOUT.                   *         
*           DIULIST - LIST OF UNIVS ALSO USED AS DEMOUT INPUT         *         
*           NDNDEMOS -THE NUMBER OF 3-CHAR DEMOS                      *         
*           NDWGTPOS - POSITION (OFFSET IN NDDEMOS) OF WEIGHTED DEMO  *         
*                                                                     *         
*  LOCALS:  R2 - LOCATION IN DIALIST TO PUT 3-BYTE DEMO REQUEST       *         
*           R3 - COUNTER OF CURRENT OFFSET IN NDDEMOS                 *         
*           R4 - LOCATION OF 3-BYTE DEMO REQUEST IN NETBLOCK          *         
*           R6 - LOCATION IN DIULIST TO PUT 3-BYTE UNIV REQUEST       *         
*           R7 - ADDRESS OF DEMO BLOCK                                          
*                                                                     *         
***********************************************************************         
*                                                                               
BLDDEMIN NTR1                                                                   
         LA    RF,DIALEN                                                        
         XCEF  DIALIST,(RF)                                                     
         MVI   DIASHARE+1,C'S'     DEMO CODE FOR SHARE                          
         MVI   DIASHARE+2,X'01'                                                 
         MVI   DIAHUT+1,C'P'       DEMO CODE FOR PUT                            
         MVI   DIAHUT+2,X'01'                                                   
         MVI   DIAVHOME+1,C'V'     DEMO CODE FOR TOT HOMES                      
         MVI   DIAVHOME+2,X'01'    DEMO CODE FOR TOT HOMES                      
         MVI   DIAGHOME+1,C'R'     DEMO CODE FOR HOMES GRP                      
         MVI   DIAGHOME+2,X'01'                                                 
         MVI   DIAIHOME+1,C'T'     DEMO CODE FOR HOMES IMPRESSION               
*                                                                               
         CLI   NBHUNOPT,C'Y'       MAY CHANGE TO H FOR HUNDREDS                 
         BNE   BLDDEM2                                                          
         CLI   NBSURVEY,C'N'       EXCEPT FOR MAJOR NETWORK                     
         BE    BLDDEM2                                                          
         CLI   NBSURVEY,C'S'       EXCEPT FOR SYNDICATION                       
         BE    BLDDEM2                                                          
         CLI   NBSURVEY,C'H'       EXCEPT FOR HISPANIC                          
         BE    BLDDEM2                                                          
*                                                                               
* PROBLEM IF BOOKTYP (NBSURVEY=C) IS CABLE PRECISION                            
* BUT NBPOSTYP=H (NETWORK PRECISION)                                            
         CLI   NBPREOPT,C'Y'       IS WRITER FORCING HUNDREDS                   
         BE    BLDDEM1             YES                                          
         CLI   NBPOSTYP,C'H'       NO-HISPANIC(NETWORK)PRECISION ?              
         BNE   BLDDEM1                NO-DO REGULAR PROCESSING                  
         MVI   NBHUNOPT,0             YES-TURN OFF HUNOPT                       
         B     BLDDEM2                                                          
*                                                                               
BLDDEM1  MVI   DIAIHOME+1,C'H'                                                  
         SPACE 1                                                                
BLDDEM2  MVI   DIAIHOME+2,X'01'                                                 
         MVI   DIUHOME+1,C'U'      DEMO CODE FOR HOMES UNIVERSE                 
         MVI   DIUHOME+2,X'01'                                                  
*                                                                               
         MVI   DIADMSET,X'FF'      START WITH EMPTY DIADMSET                    
         MVI   DIUDMSET,X'FF'      START WITH EMPTY DIUDMSET                    
         OC    NBADEM,NBADEM       DEMO BLOCK SPECIFIED?                        
         BZ    XITBLD                                                           
*                                                                               
         LA    R2,DIADMSET                                                      
         LA    R6,DIUDMSET                                                      
         L     R7,NBADEM                                                        
         USING NDDEMBLK,R7                                                      
         LA    R5,XDEMTBL2         2ND LIST (PERSONAL LANG DEMOS)               
         TM    NBINDS6,NBI6XDEM    WAS THIS PASSED ?                            
         BO    *+8                                                              
         LA    R5,DEMOXTBL         NO,USE WORKING STORAGE TABLE                 
         SR    R3,R3                                                            
         TM    NBINDS6,NBI6XDEM    EXPANDED DEMO BLOCK?                         
         BO    BLDDEM2A            YES                                          
         LA    R4,NDDEMOS          NO                                           
         MVI   NDWGTPOS,0          INITIALIZE IN CASE NO WEIGHTS                
         B     BLDDEM2B                                                         
         USING XDDEMBLK,R7                                                      
*                                                                               
                                                                                
BLDDEM2A LA    R4,XDDEMOS          YES                                          
         MVI   XDWGTPOS,0          INITIALIZE IN CASE NO WEIGHTS                
*                                                                               
BLDDEM2B MVI   HAVNAD,C'N'         RESET NAD DEMOS SWITCH                       
         MVI   HAVBTU,C'N'         RESET USER FILE SWITCH                       
*                                                                               
*                                  DO FOR EACH 3-BYTE DEMO CODE                 
BLDLOOP  OC    0(3,R4),0(R4)       TEST FOR END OF LIST                         
         BZ    ENDBLOOP                                                         
         CLI   0(R4),X'FF'                                                      
         BE    ENDBLOOP                                                         
*                                                                               
         CLI   0(R4),171           CHECK FOR TVQ ADJUST                         
         BNE   *+12                                                             
         MVI   HAVBTU,C'Y'                                                      
         B     BLDD1                                                            
*                                                                               
         CLI   0(R4),172           CHECK FOR OPI ADJUST                         
         BNE   *+12                                                             
         MVI   HAVBTU,C'O'                                                      
         B     BLDD1                                                            
*                                                                               
         CLI   0(R4),0             CHECK FOR NAD CATAGORY                       
         BE    *+8                                                              
         MVI   HAVNAD,C'Y'         SET HAVE NAD DEMOS                           
*                                                                               
BLDD1    CLI   1(R4),X'21'         IF A USER DEMO                               
         BE    BLDD2               DON'T ADD PREFIXES                           
         CLI   1(R4),63            IF A WEIGHTED DEMO                           
         BNE   FILLTBL             DON'T ADD PREFIXES AND                       
         USING NDDEMBLK,R7                                                      
         TM    NBINDS6,NBI6XDEM                                                 
         BO    BLDD1C                                                           
         STC   R3,NDWGTPOS         STORE OFFSET IN NDWGTPOS                     
         B     BLDD2                                                            
         USING XDDEMBLK,R7                                                      
BLDD1C   STC   R3,XDWGTPOS                                                      
         SPACE 1                                                                
BLDD2    MVC   0(3,R2),0(R4)       PUT UNPREFIXED DEMO IN DIADMSET              
         MVC   3(3,R2),0(R4)       DEMOUT WILL RETURN 0S FOR THESE              
         MVC   6(3,R2),0(R4)       DEMOS                                        
         MVC   0(3,R6),0(R4)       UNIVERSE                                     
         B     NEXTDEM                                                          
*                                                                               
FILLTBL  MVC   0(3,R2),0(R4)       PUT 3-BYTE DEMO CODE                         
         MVI   1(R2),C'V'          VPH PREFIX                                   
         XC    DMCB,DMCB                                                        
         L     RF,NBDEMCON                                                      
         PRINT GEN                                                              
         GOTO1 (RF),DMCB,(R2),('DEMOCON_17',(R2)),NBADEM,(R5)                   
*                                                                               
         MVC   3(3,R2),0(R4)                                                    
         MVI   4(R2),C'R'          GRP  PREFIX                                  
         LA    R1,3(R2)                                                         
         ST    R1,DMCB                                                          
         ST    R1,DMCB+4                                                        
         MVI   DMCB+4,DEMOCON_17                                                
         GOTO1 (RF),DMCB,,,NBADEM,(R5)                                          
         PRINT NOGEN                                                            
*                                                                               
         MVC   6(3,R2),0(R4)                                                    
         MVI   7(R2),C'T'          IMPRESSION PREFIX(THOUSANDS)                 
         LA    R1,6(R2)                                                         
         ST    R1,DMCB                                                          
         ST    R1,DMCB+4                                                        
         MVI   DMCB+4,DEMOCON_17                                                
         GOTO1 (RF),DMCB,,,NBADEM,(R5)                                          
*                                                                               
* CHANGE DEMO LIST IN NENTVLDEMO                                                
******   TM    NBSBKEND,NBEXPDEM   EXPANDED DEMO VALUES?                        
******   BNO   *+8                                                              
******   MVI   7(R2),C'Y'          YES/SET IMP PREFIX TO Y                      
*                                                                               
         CLI   NBHUNOPT,C'Y'                                                    
         BNE   FILLTBL2                                                         
         CLI   NBSURVEY,C'N'                                                    
         BE    FILLTBL2                                                         
         CLI   NBSURVEY,C'S'                                                    
         BE    FILLTBL2                                                         
         CLI   NBSURVEY,C'H'                                                    
         BE    FILLTBL2                                                         
         MVI   7(R2),C'H'          OR HUNDREDS FOR CABLE                        
         SPACE 1                                                                
FILLTBL2 MVC   0(3,R6),0(R4)                                                    
         MVI   1(R6),C'U'          UNIVERSE PREFIX                              
         L     RF,NBDEMCON                                                      
         GOTO1 (RF),DMCB,(R6),('DEMOCON_17',(R6)),NBADEM,(R5)                   
*                                                                               
NEXTDEM  LA    R2,9(R2)            GET READY FOR NEXT 3-BYTE DEMO CODE          
         LA    R6,3(R6)                                                         
         LA    R3,1(R3)                                                         
         LA    R5,1(R5)            BUMP 2ND PERSONAL LANG DEMO LIST             
         USING NDDEMBLK,R7                                                      
         LA    R1,NDMAXDEM         CHECK IF LIST FULL                           
         TM    NBINDS6,NBI6XDEM    EXPANDED DEMO BLOCK                          
         BNO   *+8                                                              
         USING XDDEMBLK,R7                                                      
         LA    R1,XDMAXDEM         YES                                          
         CR    R3,R1                                                            
         BNL   ENDBLOOP                                                         
         LA    R4,3(R4)            GET NEXT                                     
         B     BLDLOOP                                                          
*                                                                               
ENDBLOOP MVI   0(R2),X'FF'         MARK END OF INPUT LIST                       
         MVI   0(R6),X'FF'                                                      
         TM    NBINDS6,NBI6XDEM                                                 
         BNO   ENDBLPX                                                          
         USING XDDEMBLK,R7                                                      
         STC   R3,XDNDEMOS                                                      
         B     XITBLD                                                           
         USING NDDEMBLK,R7                                                      
ENDBLPX  STC   R3,NDNDEMOS                                                      
XITBLD   B     XIT                                                              
         DROP  R7                                                               
************************************************************                    
         EJECT                                                                  
*                                                                               
*                                                                               
***********************************************************************         
* NUMBER : 1.3.2                                                                
* TITLE ADJEST -ADJUST IMPRESSIONS FOR ESTIMATE DATA                            
*                                                                               
* CALLED FROM : ANYTIME THE RETRIEVED DEMOS HAVE ESTIMATE ELEMENT ROOTS         
*   LOCALS: R7 - ADDRESS OF DEMOBLOCK                                           
**************************************                                          
*                                                                               
         PRINT GEN                                                              
ADJEST   NTR1  WORK=(R5,150)                                                    
         PRINT NOGEN                                                            
         USING MULTIPD,R5                                                       
*                                                                               
         TM    NBSPLOPT,X'80'      .IF SPLITTING                                
         BZ    *+12                                                             
         CLI   NBPRDNO,0           .IF MULT PRODS                               
         BNE   SETIMPS             .GOTO SPECIAL IMPS PROCESSING                
         B     ADJE1                                                            
*                                                                               
ADJMLT   NTR1                   MULTI PRODS COME IN HERE FROM GETORGS           
*                                                                               
ADJE1    MVI   NUMDEMS,0           DEFAULT                                      
         MVI   OVELCODE,X'DD'      DONT ADJUST IF ESTIMATE OVERRIDE             
*                                    ELEMENT EXISTS FOR THIS DEMO               
         OC    NBADEM,NBADEM       CK IF DEMO BLOCK GIVEN                       
         BZ    ADJE2                                                            
         L     R7,NBADEM                                                        
         USING NDDEMBLK,R7                                                      
         TM    NBINDS6,NBI6XDEM    EXPANDED DEMO BLOCK?                         
         BO    *+14                                                             
         MVC   NUMDEMS,NDNDEMOS    NO                                           
         B     ADJE2                                                            
         USING XDDEMBLK,R7                                                      
         MVC   NUMDEMS,XDNDEMOS                                                 
ADJE2    BAS   RE,ADJEQUIV                                                      
         BAS   RE,ADJUNIV                                                       
         BAS   RE,ADJFEED                                                       
         BAS   RE,ADJCOPY          COPY SPLITS                                  
         CLI   NBN2B11,C'A'        IMP PCT FOR ACTUALS ONLY (ALL)               
         BE    *+24                                                             
** PXZ   CLI   NBPOSTYP,C'C'       IS STATION CABLE                             
         CLI   NBSURVEY,C'C'       IS STATION CABLE                             
         BNE   *+12                                                             
         CLI   NBN2B11,C'Y'        IMP PCT FOR ACTUALS ONLY (CABLE)             
         BE    *+8                                                              
         BAS   RE,ADJIMP                                                        
         CLC   NBACTDAT,=X'B32B'   FROM SEP11/89 PKG/DEMO                       
         BL    *+8                 GUARANTEES AFFECT OVERRIDES                  
         MVI   OVELCODE,0                                                       
         BAS   RE,ADJPG            PACKAGE GUARANTEE                            
         BAS   RE,ADJDG            DEMO GUARANTEE                               
         BAS   RE,ADJRND                                                        
         BRAS  RE,ADJ2DEC                                                       
XITAEST  B     XIT                                                              
                                                                                
         EJECT                                                                  
* - GET IMPS FOR ALL PRODUCTS AT ONE SHOT                                       
* - AND RETURN IMPS FOR REQUESTED PRODUCT                                       
SETIMPS  DS    0H                                                               
*                                                                               
         LA    R1,ORIGIMPS         SET ADDRS OF EST IMP SAVEAREA                
         ST    R1,ADROIMP                                                       
         LA    R1,SUBTIMPS                                                      
         ST    R1,ADRTIMP                                                       
         LA    R1,RAWIMPSE                                                      
         ST    R1,ARAWIMPS                                                      
         LA    R1,ORIGGRPS         SET ADDRS OF EST GRP SAVEAREA                
         ST    R1,ADROGRP                                                       
         LA    R1,SUBTGRPS                                                      
         ST    R1,ADRTGRP                                                       
         LA    R1,RAWGRPSE                                                      
         ST    R1,ARAWGRPS                                                      
         LA    R1,ADJMLT                                                        
         ST    R1,ADDRADJ                                                       
         XC    MBPCTSV,MBPCTSV     FEED PCT SUBTOT AREA                         
         MVI   NUMDEMS,0           DEFAULT                                      
         MVI   OVELCODE,X'DD'      DONT ADJUST IF ESTIMATE OVERRIDE             
*                                    ELEMENT EXISTS FOR THIS DEMO               
         OC    NBADEM,NBADEM       CK IF DEMO BLOCK GIVEN                       
         BZ    SIMP2                                                            
         L     R7,NBADEM                                                        
         USING NDDEMBLK,R7                                                      
         TM    NBINDS6,NBI6XDEM    EXPANDED DEMO BLOCK?                         
         BO    *+14                                                             
         MVC   NUMDEMS,NDNDEMOS    NO                                           
         B     SIMP2                                                            
         USING XDDEMBLK,R7                                                      
         MVC   NUMDEMS,XDNDEMOS    YES                                          
*                                                                               
SIMP2    BAS   RE,GETORGS          GET IMPS BEFORE ADJUSTMENT                   
         LA    R2,1                COUNTER FOR PROD NUMBER                      
******** L     R7,NBAPROD          ADDR OF PRODUCT ELEMENT                      
*********USING NUPRDD,R7                                                        
         L     RE,NBAPROD                                                       
         USING NXBLKD,RE                                                        
         LA    R7,NXPRDS           BUMP OVER NXBIND (TYPE INDICATOR)            
         USING NXPRDS,R7                                                        
         DROP  RE                                                               
*****************************************************************               
SIMP5    TM    NBUNST3,X'40'       IF COPY SPLIT                                
         BNO   SIMP5A                                                           
         BAS   RE,RESETGRP         RESET GRPS TO RAW                            
****************************************************************                
***SIMP5A   MVC   NBFEED,NUPRDFD      SET FEED PCT                              
SIMP5A   MVC   NBFEED,NXPFD        SET FEED PCT                                 
         L     R1,MBPCTSV          AND TOTAL IT                                 
         SR    R0,R0                                                            
         ICM   R0,3,NBFEED                                                      
         AR    R1,R0                                                            
         ST    R1,MBPCTSV                                                       
         C     R1,=F'10000'       ,IF REACHED 100 PCT                           
         BNE   SIMP6                                                            
         CLI   NBSPLCNT,1        ,AND IT'S NOT 1ST PRODUCT                      
         BE    SIMP6                                                            
         BAS   RE,LASTPRD        ,PROCESS AS LAST PROD(TOTAL-SUBTOTAL)          
         B     SIMP10                                                           
*                                                                               
SIMP6    BAS   RE,ADJEQUIV                                                      
         BAS   RE,ADJUNIV                                                       
         BAS   RE,ADJFEED                                                       
         BAS   RE,ADJCOPY          COPY SPLITS                                  
         CLI   NBN2B11,C'A'        IMP PCT FOR ACTUALS ONLY (ALL)               
         BE    *+24                                                             
** PXZ   CLI   NBPOSTYP,C'C'       IS STATION CABLE                             
         CLI   NBSURVEY,C'C'       IS STATION CABLE                             
         BNE   *+12                                                             
         CLI   NBN2B11,C'Y'        IMP PCT FOR ACTUALS ONLY (CABLE)             
         BE    *+8                                                              
         BAS   RE,ADJIMP                                                        
         CLC   NBACTDAT,=X'B32B'   FROM SEP11/89 PKG/DEMO                       
         BL    *+8                 GUARANTEES AFFECT OVERRIDES                  
         MVI   OVELCODE,0                                                       
         BAS   RE,ADJPG            PACKAGE GUARANTEE                            
         BAS   RE,ADJDG            DEMO GUARANTEE                               
         BAS   RE,ADJRND                                                        
         BRAS  RE,ADJ2DEC                                                       
*                                                                               
SIMP10   STC   R2,BYTE                                                          
********************************************************                        
         CLC   NBSPLCNT,BYTE       ...IF REQUESTED PROD                         
         BNE   SIMP10B                                                          
         CLC   MBPCTSV,=F'10000'   ...AND IF FEED = 100%                        
         BNE   SIMPX                                                            
         CLI   NBSPLCNT,1          ...AND IF NOT 1ST PROD                       
         BE    SIMPX                                                            
         BAS   RE,ADJPG            ...ADJUST BEFORE EXITING                     
         BAS   RE,ADJDG                                                         
         BAS   RE,ADJRND                                                        
         BRAS  RE,ADJ2DEC                                                       
         B     SIMPX                                                            
**********************************************************                      
SIMP10B  BAS   RE,ADDIMPT          ...ELSE ADD TO IMP SUBTOTAL                  
         LA    R2,1(R2)            ...BUMP PROD NO COUNT                        
         STC   R2,BYTE                                                          
         CLC   BYTE,NBPRDNO        IF NEXT PROD IS LAST                         
         BE    SIMPLAST            DO PROCESSING FOR LAST PROD                  
         BNH   *+6                                                              
         DC    H'0'                         SHOULD NEVER GET HERE               
***      LA    R7,6(R7)            BUMP TO NEXT PROD IN PROD ELEM               
         LA    R7,9(R7)            BUMP TO NEXT PROD IN PROD ELEM               
         BAS   RE,RESETIMP         RESET ORIGINAL IMP VALUES                    
         B     SIMP5               AND PROCESS                                  
*                                                                               
SIMPLAST DS    0H             SUBTRACT SUBTOTAL IMPS FROM ORIGINAL              
****     LA    R7,6(R7)            BUMP TO LAST PROD IN ELEMENT                 
         LA    R7,9(R7)            BUMP TO LAST PROD IN ELEMENT                 
***      OC    NUPRDFD,NUPRDFD     CHECK IN CASE LAST PROD FEED = 0             
         OC    NXPFD,NXPFD     CHECK IN CASE LAST PROD FEED = 0                 
         BNZ   SIMP20                                                           
         XC    NBFEED,NBFEED       YES SET FEED TO ZERO                         
         L     RF,ADDRADJ          GET ADJUSTMENT ADDRESS                       
         BASR  RE,RF               AND SEND THROUGH DEMO ROUTINES               
         B     SIMPX                                                            
*                                                                               
SIMP20   BAS   RE,LASTPRD     FOR IMPS FOR FINAL PRODECT                        
*                                                                               
SIMPX    B     XIT                                                              
         DROP  R7                                                               
                                                                                
         EJECT                                                                  
* - SUBROUTINES FOR MULTIPLE PRODUCT ADJUSTMENT                                 
                                                                                
*                                                                               
GETORGS  NTR1                                                                   
                                                                                
* SAVE RAW NUMBERS BEFORE ANY ADJUSTMENTS                                       
         BAS   RE,SAVIMPS                                                       
                                                                                
* - SET FEED PCT TO 100 AND SEND THROUGH DEMO ROUTINES                          
* - THIS NUMBER WILL BE USED AS BASE FOR CALCULATING LAST PROD DEMO             
                                                                                
         MVC   NBFEED,=X'2710'     100%                                         
         L     RF,ADDRADJ          GET EST OR ACT ADJUSTMENT ADDR               
         BASR  RE,RF               SEND THROUGH NETVALUE                        
         L     RE,ADRTIMP                AND SAVE                               
         XC    0(84,RE),0(RE)                                                   
         L     RE,ADROIMP                                                       
         XC    0(84,RE),0(RE)                                                   
         SR    R0,R0                                                            
         L     R1,DOAIHOME         ...DO HOMES FIRST                            
         ST    R1,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    R2,DOADMSET                                                      
         SR    R7,R7                                                            
         ICM   R7,1,NUMDEMS                                                     
         BZ    GETO30                                                           
GETO10   L     R1,ALIMPOFF(R2)      ...GET REST OF DEMOS                        
         SR    R0,R0                                                            
         ST    R1,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    R2,ALLENGTH(R2)                                                  
         BCT   R7,GETO10                                                        
GETO30   DS    0H                  DO I NEED TO HANDLE GRPS                     
         TM    NBUNST3,X'40'       DO IT IF COPYSPLIT                           
         BO    GETO32                                                           
         TM    NBUNST3,X'01'                                                    
         BNO   GETOX                                                            
         DS    0H                  YES                                          
GETO32   L     RE,ADRTGRP                                                       
         XC    0(84,RE),0(RE)                                                   
         L     RE,ADROGRP          ...SAVE GRPS BEFORE ANY ADJUSTMENTS          
         XC    0(84,RE),0(RE)                                                   
         SR    R0,R0                                                            
         L     R1,DOAGHOME         ...DO HOMES FIRST                            
         ST    R1,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    R2,DOADMSET                                                      
         SR    R7,R7                                                            
         ICM   R7,1,NUMDEMS                                                     
         BZ    GETOX                                                            
GETO50   L     R1,ALGRPOFF(R2)      ...GET REST OF DEMOS                        
         SR    R0,R0                                                            
         ST    R1,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    R2,ALLENGTH(R2)                                                  
         BCT   R7,GETO50                                                        
GETOX    BAS   RE,RESETIMP         RESET RAW NUMBERS                            
         L     R2,ADRTIMP                                                       
         L     R2,ADROIMP                                                       
         B     XIT                                                              
                                                                                
* - SAVE RAW IMPS AS PASSED BY NETVALUE                                         
SAVIMPS  NTR1                                                                   
         L     RE,ARAWIMPS                                                      
         L     R1,DOAIHOME                                                      
         ST    R1,0(RE)            DO HOMES FIRST                               
         LA    RE,4(RE)                                                         
         LA    R2,DOADMSET                                                      
         SR    R7,R7                                                            
         ICM   R7,1,NUMDEMS                                                     
         BZ    SAVIX                                                            
SAVI10   L     R1,ALIMPOFF(R2)    ...GET REST OF DEMOS                          
         ST    R1,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    R2,ALLENGTH(R2)                                                  
         BCT   R7,SAVI10                                                        
         L     R2,ARAWIMPS                                                      
SAVI20   DS    0H                  DO I NEED TO HANDLE GRPS                     
         TM    NBUNST3,X'40'       IF COPY SPLIT                                
         BO    SAVI21              YES                                          
         TM    NBUNST3,X'01'                                                    
         BNO   SAVIX                                                            
SAVI21   L     RE,ARAWGRPS         YES                                          
         L     R1,DOAGHOME                                                      
         ST    R1,0(RE)            DO HOMES FIRST                               
         LA    RE,4(RE)                                                         
         LA    R2,DOADMSET                                                      
         SR    R7,R7                                                            
         ICM   R7,1,NUMDEMS                                                     
         BZ    SAVIX                                                            
SAVI30   L     R1,ALGRPOFF(R2)    ...GET REST OF DEMOS                          
         ST    R1,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    R2,ALLENGTH(R2)                                                  
         BCT   R7,SAVI30                                                        
*                                                                               
SAVIX    B     XIT                                                              
*                                                                               
                                                                                
* - PUT IMPS BACK TO RAW                                                        
RESETIMP NTR1                                                                   
         L     RE,ARAWIMPS                                                      
         MVC   DOAIHOME,0(RE)                                                   
         LA    RE,4(RE)                                                         
         LA    R2,DOADMSET                                                      
         SR    R7,R7                                                            
         ICM   R7,1,NUMDEMS                                                     
         BZ    REST20                                                           
REST10   L     R1,0(RE)                                                         
         ST    R1,ALIMPOFF(R2)                                                  
         LA    RE,4(RE)                                                         
         LA    R2,ALLENGTH(R2)                                                  
         BCT   R7,REST10                                                        
REST20   TM    NBUNST3,X'01'       DO I NEED TO HANDLE GRPS                     
         BNO   RESTX                                                            
         B     REST21                                                           
*                                                                               
RESETGRP NTR1                    COPY SPLIT COMES IN HERE                       
REST21   L     RE,ARAWGRPS                                                      
         MVC   DOAGHOME,0(RE)                                                   
         LA    RE,4(RE)                                                         
         LA    R2,DOADMSET                                                      
         SR    R7,R7                                                            
         ICM   R7,1,NUMDEMS                                                     
         BZ    RESTX                                                            
REST30   L     R1,0(RE)                                                         
         ST    R1,ALGRPOFF(R2)                                                  
         LA    RE,4(RE)                                                         
         LA    R2,ALLENGTH(R2)                                                  
         BCT   R7,REST30                                                        
*                                                                               
         L     R2,ARAWGRPS                                                      
RESTX    B     XIT                                                              
*                                                                               
* - ADD TO IMPS SUBTOTAL                                                        
ADDIMPT  NTR1                                                                   
         L     RE,ADRTIMP          ..ADD TO SUBTOTL IMPS                        
         L     RF,DOAIHOME         ..DO HOMES FIRST                             
         L     R1,0(RE)                                                         
         AR    RF,R1                                                            
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    R2,DOADMSET                                                      
         SR    R7,R7                                                            
         ICM   R7,1,NUMDEMS                                                     
         BZ    ADDI20                                                           
ADDI10   L     RF,ALIMPOFF(R2)      ..GET REST OF DEMOS                         
         L     R1,0(RE)                                                         
         AR    RF,R1                                                            
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    R2,ALLENGTH(R2)                                                  
         BCT   R7,ADDI10                                                        
         L     R7,ADRTIMP                                                       
ADDI20   DS    0H                  DO I NEED TO HANDLE GRPS                     
         TM    NBUNST3,X'01'                                                    
         BNO   ADDIX                                                            
         L     RE,ADRTGRP          ..ADD TO SUBTOTL IMPS                        
         L     RF,DOAGHOME         ..DO HOMES FIRST                             
         L     R1,0(RE)                                                         
         AR    RF,R1                                                            
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    R2,DOADMSET                                                      
         SR    R7,R7                                                            
         ICM   R7,1,NUMDEMS                                                     
         BZ    ADDIX                                                            
ADDI30   L     RF,ALGRPOFF(R2)      ..GET REST OF DEMOS                         
         L     R1,0(RE)                                                         
         AR    RF,R1                                                            
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    R2,ALLENGTH(R2)                                                  
         BCT   R7,ADDI30                                                        
ADDIX    B     XIT                                                              
                                                                                
* - GET IMPS FOR LAST PROD IN MULTIPLE PROD UNIT                                
LASTPRD  NTR1                                                                   
         L     RE,ADROIMP     TO GET IMPS FOR LAST PRODUCT                      
         L     RF,ADRTIMP                                                       
         L     R1,0(RE)                                                         
         L     R0,0(RF)                                                         
         SR    R1,R0                                                            
         ST    R1,DOAIHOME                                                      
*                                                                               
         LA    RE,4(RE)                                                         
         LA    RF,4(RF)                                                         
         LA    R2,DOADMSET                                                      
         SR    R7,R7                                                            
         ICM   R7,1,NUMDEMS                                                     
         BZ    LPRD20                                                           
LPRD15   DS    0H                   ..GET REST OF DEMOS                         
         L     R1,0(RE)                                                         
         L     R0,0(RF)                                                         
         SR    R1,R0                                                            
         ST    R1,ALIMPOFF(R2)                                                  
         LA    RE,4(RE)                                                         
         LA    RF,4(RF)                                                         
         LA    R2,ALLENGTH(R2)                                                  
         BCT   R7,LPRD15                                                        
LPRD20   DS    0H                  GRPS?                                        
         TM    NBUNST3,X'01'                                                    
         BNO   LPRDX                                                            
         L     RE,ADROGRP                                                       
         L     RF,ADRTGRP                                                       
         L     R1,0(RE)                                                         
         L     R0,0(RF)                                                         
         SR    R1,R0                                                            
         ST    R1,DOAGHOME                                                      
*                                                                               
         LA    RE,4(RE)                                                         
         LA    RF,4(RF)                                                         
         LA    R2,DOADMSET                                                      
         SR    R7,R7                                                            
         ICM   R7,1,NUMDEMS                                                     
         BZ    LPRDX                                                            
LPRD30   DS    0H                   ..GET REST OF DEMOS                         
         L     R1,0(RE)                                                         
         L     R0,0(RF)                                                         
         SR    R1,R0                                                            
         ST    R1,ALGRPOFF(R2)                                                  
         LA    RE,4(RE)                                                         
         LA    RF,4(RF)                                                         
         LA    R2,ALLENGTH(R2)                                                  
         BCT   R7,LPRD30                                                        
LPRDX    B     XIT                                                              
         DROP  R5                                                               
***********************************************************************         
         EJECT                                                                  
*                                                                               
************************* HIPO ****************************************         
*  NUMBER: NV1.8.4                                                              
*  TITLE: WEIGHDEM -WEIGHTS ANY WEIGHTED DEMOS IN LIST                          
*                                                                     *         
*  COMMENTS: USES THE FORMULA IN NBESTWTS TO GET A WEIGHTED DEMO.               
*  *** WHERE SUM(WEIGHTS) APPEARS: IF THE WEIGHTED DEMO HAS A WEIGHT            
*  ***  THEN USE THIS NUMBER INSTEAD OF SUM(WEIGHTS)                            
*                                                                               
*        WEIGHTED VPH= SUM(VPH*WEIGHT)/SUM(WEIGHTS)                             
*        WEIGHTEDIMPRESSION=SUM(IMPRESSIONS*WEIGHT)/SUM(WEIGHTS)                
*        WEIGHTEDGRP=(SUM(IMPRESSIONS*WEIGHT))/                                 
*                                    SUM(IMPRESSIONS*WEIGHT/GRP)                
*        WEIGHTEDUNIV=(SUM(IMPRESSIONS*WEIGHT/GRP))/SUM(WEIGHTS)                
*                                                                               
*  CALLED FROM: STPUTEM - CALLED BY ANY CASE USING DEMOS                        
*                                                                               
*  INPUTS: NBWGTLST - THE LIST OF DEMOS TO WEIGHT.                              
*          NBWGTPOS - THE POSITION (OFFSET IN NDDEMOS) OF WEIGHTED              
*                           DEMO.                                               
*                                                                               
*  I/O S-  DOALIST-THE LIST OF DEMO VALUES WHERE WEIGHTED DEMOS                 
*                 SHOULD BE INSERTED.                                           
*          DOAWUNIV - WEIGHTED UNIVERSE                                         
*                                                                               
*  LOCALS: SUMWVPH - SUM(VPH *WEIGHT)                                           
*          SUMWIMP - SUM(IMPRESSIONS*WEIGHT)                                    
*          SUMWUNIV - SUM(IMPRESSIONS*WEIGHT/GRP)                               
*          SUMWGT - SUM(WEIGHTS)                                                
*          SAVWGT - HALFWORD USED TO STORE WEIGHT.                              
*          SVBYTE2 -IS SECOND BYTE OF THIS HALFWORD                             
*          R2 - POINTS TO DEMO IN NDWGTLST                                      
*          R5 - POINTS TO ASSOCIATED DEMO IN DOALIST                            
*          R6 - CURRENT OFFSET INTO NDWGTLST                                    
*          R7 - ADDRESS OF DEMO BLOCK                                           
*****************************                                                   
WEIGHDEM NTR1                                                                   
         OC    NBADEM,NBADEM       DOES DEMO BLOCK EXIST?                       
         BZ    XITWD                                                            
         XC    DOAWUNIV,DOAWUNIV   CLEAR WEIGHTED UNIVERSE FIELD                
         L     R7,NBADEM                                                        
         USING NDDEMBLK,R7                                                      
         TM    NBINDS6,NBI6XDEM    EXPANDED DEMO BLOCK?                         
         BO    WD05                YES                                          
         OC    NDWGTLST,NDWGTLST   NO/IF 0 THEN NO WEIGHTING                    
         BZ    XITWD                                                            
         B     WD10                                                             
WD05     DS    0H                                                               
         USING XDDEMBLK,R7                                                      
         OC    XDWGTLST,XDWGTLST   EXPANDED DEMO                                
         BZ    XITWD                                                            
WD10     XC    SUMWVPH,SUMWVPH     INITIALIZE SUMS                              
         XC    SUMWIMP,SUMWIMP                                                  
         XC    SUMWUNIV,SUMWUNIV                                                
         XC    SUMWGT,SUMWGT                                                    
         XC    SAVWGT,SAVWGT       CLEAR HALFWORD TO SAVE WEIGHT                
*                                                                               
         LA    R5,DOADMSET                                                      
         USING XDDEMBLK,R7                                                      
         LA    R2,XDWGTLST         POINT TO FIRST PARTNER                       
         LA    R6,XDMAXDEM                                                      
         TM    NBINDS6,NBI6XDEM    EXPANDED DEM BLOCK                           
         BO    WDLOOP              YES                                          
         USING NDDEMBLK,R7                                                      
         LA    R6,NDMAXDEM         NO                                           
         LA    R2,NDWGTLST         POINT TO FIRST PARTNER                       
*                                  DO FOR EACH PARTNER                          
WDLOOP   CLI   0(R2),0               CK FOR LAST PARTNER                        
         BE    WDNEXT                                                           
         ZIC   R4,0(R2)            MOVE WEIGHT INTO SAVWGT                      
         STH   R4,SAVWGT                                                        
         L     R1,SUMWGT           NEW TOTAL SUM OF WEIGHTS                     
         AR    R4,R1                                                            
         ST    R4,SUMWGT                                                        
*                                                                               
         L     R4,ALVPHOFF(R5)     GET THIS VPH VALUE                           
         MH    R4,SAVWGT                                                        
         L     R3,SUMWVPH                                                       
         AR    R3,R4               NEW TOTAL VPH                                
         ST    R3,SUMWVPH                                                       
*                                                                               
         L     R4,ALIMPOFF(R5)     GET THIS IMPRESSION VALUE                    
         MH    R4,SAVWGT                                                        
         L     R3,SUMWIMP                                                       
         AR    R3,R4               NEW TOTAL IMP                                
         ST    R3,SUMWIMP                                                       
*                                                                               
         SR    R0,R0               CLEAR R0 FOR DIVISION                        
         LR    R1,R4                                                            
         OC   ALGRPOFF(4,R5),ALGRPOFF(R5)  CK FOR 0 DIVISOR                     
         BZ    WDNEXT                                                           
         D     R0,ALGRPOFF(R5)     GET THIS GRP VALUE                           
         L     R3,SUMWUNIV                                                      
         AR    R3,R1                                                            
         ST    R3,SUMWUNIV         NEW TOTAL UNIV                               
*                                                                               
WDNEXT   LA    R2,1(R2)            NEXT DEMO IN LIST                            
         LA    R5,ALLENGTH(R5)     NEXT DEMO SET IN DOALIST                     
         BCT   R6,WDLOOP                                                        
*                                                                               
         USING NDDEMBLK,R7                                                      
         ZIC   R3,NDWGTPOS         POSITION OF WEIGHTED DEMO                    
         ZIC   R1,NDWGTLST(R3)     GET WEIGHT OF WEIGHTED DEMO                  
         TM    NBINDS6,NBI6XDEM    EXPANDED DEMO BLOKC?                         
         BNO   WD20                                                             
         USING XDDEMBLK,R7                                                      
         ZIC   R3,XDWGTPOS                                                      
         ZIC   R1,XDWGTLST(R3)                                                  
WD20     LTR   R1,R1               IF WEIGHT=0 THEN USE SUM OF WEIGHTS          
         BZ    FILLWD                                                           
         ST    R1,SUMWGT            ELSE REPLACE SUMWGTS                        
FILLWD   LA    R1,ALLENGTH         OFFSET IN DOADMSET PER POSITION              
         MR    R2,R1                                                            
         LA    R3,DOADMSET(R3)     LOCATION OF DEMO VALS                        
*                                                                               
         SR    R4,R4               CLEAR R4 FOR DIVISION                        
         L     R5,SUMWVPH                                                       
         OC    SUMWGT,SUMWGT       CK FOR 0 DIVSOR                              
         BZ    FW2                                                              
         D     R4,SUMWGT                                                        
         ST    R5,ALVPHOFF(R3)     SAVE WEIGHTED VPH                            
*                                                                               
FW2      SR    R4,R4               CLEAR R4 FOR DIVISION                        
         L     R5,SUMWIMP                                                       
         OC    SUMWGT,SUMWGT       CK FOR 0 DIVSOR                              
         BZ    FW4                                                              
         D     R4,SUMWGT                                                        
         ST    R5,ALIMPOFF(R3)     SAVE WEIGHTED IMPRESSION                     
*                                                                               
FW4      SR    R4,R4               CLEAR R4 FOR DIVISION                        
         L     R5,SUMWIMP                                                       
         L     R2,SUMWUNIV                                                      
         LTR   R2,R2               CK FOR ZERO DIVSOR                           
         BZ    FW6                                                              
         DR    R4,R2                                                            
         ST    R5,ALGRPOFF(R3)     SAVE WEIGHTED GRP                            
*                                                                               
FW6      SR    R4,R4               CLEAR R4 FOR DIVISION                        
         L     R5,SUMWUNIV                                                      
         OC    SUMWGT,SUMWGT       CK FOR 0 DIVSOR                              
         BZ    XITWD                                                            
         D     R4,SUMWGT                                                        
         ST    R5,DOAWUNIV         SAVE WEIGHTED UNIV                           
*                                                                               
         USING NDDEMBLK,R7                                                      
         ZIC   R3,NDWGTPOS                                                      
         TM    NBINDS6,NBI6XDEM    EXPANDED DEMO BLOCK?                         
         BNO   FW12                                                             
         USING XDDEMBLK,R7                                                      
         ZIC   R3,XDWGTPOS                                                      
FW12     SLL   R3,2                 EACH UNIV IS STORED AS 4 BYTES              
         LA    R3,DOUDMSET(R3)      PROPER SPOT IN LIST                         
         ST    R5,0(R3)                                                         
*                                                                               
XITWD    B     XIT                                                              
         DROP  R7                                                               
***********************************************************************         
         EJECT                                                                  
************************* HIPO ****************************************         
*  TITLE: USERDEM - LOOKS FOR ANY USER DEMOS IN LIST                            
*                                                                     *         
*                                                                               
*  CALLED FROM: STPUTEM - CALLED BY ANY CASE USING DEMOS                        
*  CALLS TO: GETUSERD - ACTUALLY CALCULATES USER DEMOS                          
*                                                                               
*          NDUSRNMS - NAMES OF THE USER DEMOS. NOTE: THESE NAMES ARE            
*                      MATCHED TO THE USER DEMO ELEMENTS, NOT THE               
*                      RELATIVE USER DEMO NUMBER (3RD BYTE OF DEMO              
*                      DECRIPTOR).                                              
*                                                                               
*  OUTPUTS -  DOALIST-THE LIST OF DEMO VALUES WHERE USER DEMO VALUES            
*                 SHOULD BE INSERTED.                                           
*             DOULIST- LIST OF UNIVERSE VALUES                                  
*             NDUSRUNV - USER DEMO UNIVERSE VALUES                              
*                                                                               
* LOCALS:  R2 - ADDRESS OF DEMO LIST                                            
*          R4 - POINTS TO ASSOCIATED UNIV IN DOULIST                            
*          R5 - POINTS TO ASSOCIATED DEMO IN DOALIST                            
*          R6 - COUNTER FOR NUMBER OF DEMOS IN LIST                             
*          R7 - ADDRESS OF DEMO BLOCK                                           
*                                                                               
* ARGS TO GETUSERD: UDEORA - C'A' FOR ACTUALS, C'E' FOR ESTS                    
*                   UD7NAME - NAME OF THIS USER DEMO                            
* ARGS FROM GETUSERD: USERUNV - CALCULATED UNIVERSE FOR THIS DEMO               
*                     USERVPH                                                   
*                     USERGRP                                                   
*                     USERIMP                                                   
*****************************                                         *         
USERDEM  NTR1                                                                   
         OC    NBADEM,NBADEM       DOES DEMO BLOCK EXIST?                       
         BZ    XITUD                                                            
         L     R7,NBADEM                                                        
         TM    NBINDS6,NBI6XDEM                                                 
         BO    USD05                                                            
         USING NDDEMBLK,R7                                                      
         XC    NDUSRUNV,NDUSRUNV   CLEAR USER DEMO UNIVERSE FIELDS              
         B     USD10                                                            
         USING XDDEMBLK,R7                                                      
USD05    XC    XDUSRUNV,XDUSRUNV   EXPANDED DEMO BLOCK                          
USD10    LA    R5,DOADMSET                                                      
         LA    R4,DOUDMSET                                                      
         USING NDDEMBLK,R7                                                      
         LA    R2,NDDEMOS                                                       
         LA    R6,NDMAXDEM                                                      
         TM    NBINDS6,NBI6XDEM    EXPANED DEM BLOCK?                           
         BNO   UDLOOP                                                           
         USING XDDEMBLK,R7                                                      
         LA    R2,XDDEMOS                                                       
         LA    R6,XDMAXDEM                                                      
*                                                                               
*                                  DO FOR EACH 3-BYTE DEMO CODE                 
UDLOOP   CLI   1(R2),X'21'         IF A USER DEMO                               
         BNE   NEXTUD                                                           
         ZIC   R3,2(R2)            GET RELATIVE USER DEMO NUMBER                
         BCTR  R3,0                DECREMENT TO CALC OFFSET                     
         MH    R3,=H'7'            USER DEMO NAMES ARE 7 BYTES                  
         TM    NBINDS6,NBI6XDEM                                                 
         BO    UD12                                                             
         USING NDDEMBLK,R7                                                      
         LA    R3,NDUSRNMS(R3)     GET NAME OF THIS DEMO IN UD7NAME             
         B     UD15                                                             
         USING XDDEMBLK,R7                                                      
UD12     LA    R3,XDUSRNMS(R3)                                                  
UD15     MVC   UD7NAME,0(R3)                                                    
         BAS   RE,GETUSERD                                                      
*                                                                               
         MVC   ALVPHOFF(4,R5),USERVPH       FILL IN DOALIST                     
         MVC   ALGRPOFF(4,R5),USERGRP                                           
         MVC   ALIMPOFF(4,R5),USERIMP                                           
         MVC   0(4,R4),USERUNV              FILL IN DOULIST                     
*                                                                               
         ZIC   R3,2(R2)            GET RELATIVE DEMO NUMBER                     
         BCTR  R3,0                DECREMENT TO CALC OFFSET                     
         SLL   R3,2                MULT BY 4 (L' UNIVERSE VALUE)                
         TM    NBINDS6,NBI6XDEM                                                 
         BNO   UD20                                                             
         USING XDDEMBLK,R7                                                      
         LA    R3,XDUSRUNV(R3)                                                  
         B     UD21                                                             
         USING NDDEMBLK,R7                                                      
UD20     LA    R3,NDUSRUNV(R3)                                                  
UD21     SR    R0,R0                                                            
         L     R1,USERUNV                                                       
         D     R0,=F'100'                                                       
         ST    R1,0(R3)            RETURN EDITED UNIVERSE VALUE                 
*                                                                               
NEXTUD   LA    R2,3(R2)            GET NEXT DEMO                                
         LA    R4,4(R4)            GET NEXT UNIV IN DOULIST                     
         LA    R5,ALLENGTH(R5)     NEXT DEMO SET IN DOALIST                     
         BCT   R6,UDLOOP                                                        
*                                                                               
XITUD    B     XIT                                                              
         DROP  R7                                                               
***********************************************************************         
         EJECT                                                                  
***********************************************************************         
*  TITLE: GETUSERD- CALCULATES A USER DEMO BY READING UNIT RECORD               
*                                                                     *         
*  COMMENTS: UNIV AND VPH ELEMENTS OR AN OVERRIDE ELEMENT MUST EXIST            
*            FOR EACH USER DEMO. ELSE ZERO IS RETURNED.                         
*                                                                               
*        VPH - FROM USER DEMO VPH ELEMENT OR USER OVERRIDE ELEMENT              
*        UNIV - FROM USER DEMO UNIV ELEMENT OR USER OVERRIDE ELEMENT            
*        IMP - (HOME IMP)*VPH                                                   
*        GRP - IMP/UNIV                                                         
*                                                                               
*  CALLED FROM: USERDEM - PROCESSES DEMO LIST                                   
*                                                                               
*  INPUTS: UD7NAME- NAME OF THE USER DEMO: THIS NAME IS                         
*                      MATCHED TO THE USER DEMO ELEMENTS, NOT THE               
*                      RELATIVE USER DEMO NUMBER (3RD BYTE OF DEMO              
*                      DECRIPTOR).                                              
*          UDEORA - C'A' FOR ACTUALS, C'E' FOR ESTS                             
*                                                                               
* OUTPUTS:   USERUNV - CALCULATED UNIVERSE FOR THIS DEMO                        
*            USERVPH                                                            
*            USERGRP                                                            
*            USERIMP                                                            
*                                                                               
*                                                                               
* LOCALS:  R4 - A(UNIT RECORD)                                                  
*          R6 - A(CURRENT ELEMENT) USED BY NEXTEL                               
*                                                                               
* PROCESS: GET UNIVERSE VALUE IN USERUNV (USE OVERRIDE IF EXISTS)               
*          GET UNIVERSE VALUE IN USERVPH  (USE OVERRIDE IF EXISTS)              
*          CALC IMP, GRP FROM ABOVE                                             
*          LOOK FOR IMP,GRP OVERRIDES AND REPLACE IF NECESSARY                  
*                                                                               
************************************************************                    
*                                                                               
GETUSERD NTR1                                                                   
*                                                                               
         USING NURECD,R4                                                        
         L     R4,NBAIO                                                         
*                                                                               
         XC    USERUNV,USERUNV     INITIALIZE VALUES                            
         XC    USERVPH,USERVPH                                                  
         XC    USERGRP,USERGRP                                                  
         XC    USERIMP,USERIMP                                                  
*                                                                               
         LA    R6,NUMAINEL                                                      
         MVI   SRCHEL,X'C1'        LOOK FOR USER UNIVERSE ELEMENT               
UDNXTUNV BAS   RE,NEXTEL                                                        
         BNE   UDLKVPH                                                          
         USING UDEUND,R6           GOT ONE, SEE IF IT MATCHES                   
         CLC   UDEUNNAM,UD7NAME    IF NO MATCH GET NEXT EL                      
         BNE   UDNXTUNV                                                         
         MVC   USERUNV,UDEUNIV     MATCH. SAVE IT AND GO ON.                    
*                                                                               
UDLKVPH  MVI   SRCHEL,X'C3'        LOOK FOR USER VPH ELEMENT                    
         LA    R6,NUMAINEL                                                      
UDNXTVPH BAS   RE,NEXTEL                                                        
         BNE   UDLKOVER                                                         
         USING UDEVPD,R6           GOT ONE, SEE IF IT MATCHES                   
         CLC   UDEVPNAM,UD7NAME    IF NO MATCH GET NEXT EL                      
         BNE   UDNXTVPH                                                         
         SR    R1,R1                                                            
         ICM   R1,3,UDEVPH                                                      
         CLI   UDEVPFMT,C'T'       TEST IF NUMBER ALREADY IN THOU'S             
         BE    UDLKVPH1                                                         
         MH    R1,=H'10'                                                        
UDLKVPH1 ST    R1,USERVPH                                                       
*                                                                               
UDLKOVER LA    R6,NUMAINEL                                                      
         MVI   SRCHEL,X'CD'        LOOK FOR USER OVERRIDE ELEMS                 
         CLI   UDEORA,C'E'         GET PROPER ELEMENT CODE IN SRCHEL            
         BE    UDNXTOV             (CD FOR ESTS, CE FOR ACTS)                   
         MVI   SRCHEL,X'CE'                                                     
UDNXTOV  BAS   RE,NEXTEL                                                        
         BNE   UDDOCALC                                                         
         USING UDOVD,R6            GOT ONE, SEE IF IT MATCHES                   
         CLC   UDOVNAM,UD7NAME     IF NO MATCH GET NEXT EL                      
         BNE   UDNXTOV                                                          
         CLI   UDOVMOD,C'V'        IF VPH OVERRIDE                              
         BNE   UDNO2                                                            
         MVC   USERVPH,UDOVVAL     MATCH. SAVE IT AND GO ON.                    
UDNO2    CLI   UDOVMOD,C'U'        IF UNIV OVERRIDE                             
         BNE   UDNXTOV                                                          
         MVC   USERUNV,UDOVVAL     MATCH. SAVE IT AND GO ON.                    
         B     UDNXTOV                                                          
*                                                                               
UDDOCALC L     R1,USERVPH                                                       
         M     R0,DOAIHOME         VPH*(HOME IMP)                               
         AH    R1,=H'500'                                                       
         D     R0,=F'1000'         PUT IN SCALE                                 
         ST    R1,USERIMP          =USER IMPRESSION                             
         OC    USERUNV,USERUNV     CK FOR DIVIDE BY ZERO                        
         BZ    UDDO4                                                            
         CLI   NBHUNOPT,C'Y'       HUNDREDS OPTION                              
         BNE   UDDO2                                                            
****************************************** PXZ                                  
***      CLI   NBSURVEY,C'N'       MAIN NETWORK                                 
***      BE    UDDO2                                                            
***      CLI   NBSURVEY,C'S'       SYNDICATION                                  
***      BE    UDDO2                                                            
***      CLI   NBSURVEY,C'H'       HISPANIC                                     
***      BE    UDDO2                                                            
         CLI   NBPOSTYP,C'N'       MAIN NETWORK                                 
         BE    UDDO2                                                            
         CLI   NBPOSTYP,C'S'       SYNDICATION                                  
         BE    UDDO2                                                            
         CLI   NBPOSTYP,C'H'       HISPANIC                                     
         BE    UDDO2                                                            
****************************************** PXZ                                  
         M     R0,=F'10000'        GET IMP IN SCALE FOR DIVIDE (HUNS)           
         B     *+8                                                              
UDDO2    M     R0,=F'100000'       GET IMP IN SCALE FOR DIVIDE (THOUS)          
         D     R0,USERUNV          (USER IMP)/(USER UNIV)                       
         SR    R0,R0               CLEAR R0 FOR DIVIDE                          
         CLI   NBPREOPT,C'Y'                                                    
         BNE   UDD02A                                                           
         CLI   NBPOSTYP,C'C'       IF CABLE NO ROUND                            
         BE    UDD03                                                            
         CLI   NBPOSTYP,C'O'       IF OTHER NO ROUND                            
         BE    UDD03                                                            
UDD02A   AH    R1,=H'5'            ROUND IT                                     
         D     R0,=F'10'                                                        
         CLI   NBPREOPT,C'Y'                                                    
         BNE   UDD03                                                            
         SR    R0,R0                                                            
         M     R0,=F'10'           IF CABLE PRECISION                           
UDD03    ST    R1,USERGRP          =USER GRP                                    
*                                                                               
UDDO4    LA    R6,NUMAINEL                                                      
         MVI   SRCHEL,X'CD'        LOOK FOR USER OVERRIDE ELEMS                 
         CLI   UDEORA,C'E'         GET PROPER ELEMENT CODE IN SRCHEL            
         BE    UENXTOV             (CD FOR ESTS, CE FOR ACTS)                   
         MVI   SRCHEL,X'CE'                                                     
UENXTOV  BAS   RE,NEXTEL                                                        
         BNE   UEN04                                                            
         USING UDOVD,R6            GOT ONE, SEE IF IT MATCHES                   
         CLC   UDOVNAM,UD7NAME     IF NO MATCH GET NEXT EL                      
         BNE   UENXTOV                                                          
         CLI   UDOVMOD,C'T'        IF IMP OVERRIDE                              
         BNE   UENO2                                                            
         MVC   USERIMP,UDOVVAL     MATCH. SAVE IT AND GO ON.                    
UENO2    CLI   UDOVMOD,C'R'        IF GRP OVERRIDE                              
         BNE   UENXTOV                                                          
         MVC   USERGRP,UDOVVAL                                                  
         B     UENXTOV                                                          
UEN04    ICM   RE,15,USERIMP       IF USER IMPRESSION CALCULATED                
         BNZ   UEN05               DONE                                         
         ICM   R0,15,USERGRP                                                    
         BZ    UDXIT                                                            
         ICM   R1,15,USERUNV                                                    
         BZ    UDXIT                                                            
         MR    R0,R0                                                            
         A     R1,=F'5000'                                                      
         D     R0,=F'10000'                                                     
         ST    R1,USERIMP                                                       
*                                                                               
UEN05    TM    NBUNST3,X'40'       IF COPY SPLIT THEN DON'T ADJUST              
         BO    UDXIT                                                            
         CLC   NBFEED,=H'0'        IF 0 THEN DON'T ADJUST                       
         BE    UDXIT                                                            
         CLC   NBFEED,=H'10000'    IF 100%, DONT ADJUST                         
         BE    UDXIT                                                            
*                                                                               
         SR    R6,R6                                                            
         ICM   R6,3,NBFEED         MULTIPLIER                                   
         L     R1,USERIMP          MAKE SURE WE HAVE VALUE                      
         MR    R0,R6               THEN ADJUST FOR FEED PERCENT                 
         A     R1,=F'5000'                                                      
         D     R0,=F'10000'                                                     
         ST    R1,USERIMP                                                       
*                                                                               
UDXIT    B     XIT                                                              
         DROP  R4                                                               
         DROP  R6                                                               
***********************************************************************         
         EJECT                                                                  
***********************************************************************         
* NUMBER : NV1.8.5                                                              
* TITLE ADJACT -ADJUST IMPRESSIONS FOR ACTUAL  DATA                             
*                                                                               
* CALLED FROM: ANYTIME THE DEMOS WERE NOT FROM THE ESTIMATE ELEMENT             
*   LOCAL OUTPUT: NUMDEMS - THE NUMBER OF DEMOS REQUESTED. SET TO 0             
*                            IF DEMO BLOCK NOT GIVEN.                           
*   LOCALS: R7 - ADDRESS OF DEMOBLOCK                                           
**************************************                                          
*                                                                               
ADJACT   NTR1  WORK=(R5,150)                                                    
         USING MULTIPD,R5                                                       
         TM    NBSPLOPT,X'80'      IF SPLITTING                                 
         BZ    *+12                                                             
         CLI   NBPRDNO,0           IF MULTIPLE PRODS                            
         BNE   SETIMPA             GO DO SPECIAL ROUTINE                        
         B     ADJA1                                                            
*                                                                               
ADJMULT  NTR1                      MULTI PROD COMES IN HERE                     
*                                                                               
ADJA1    MVI   NUMDEMS,0           DEFAULT                                      
         MVI   OVELCODE,X'DE'      DONT ADJUST IF ACTUAL OVERRIDE               
*                                    ELEMENT EXISTS FOR THIS DEMO               
         OC    NBADEM,NBADEM       CK IF DEMO BLOCK GIVEN                       
         BZ    ADJA2                                                            
         L     R7,NBADEM                                                        
         TM    NBINDS6,NBI6XDEM                                                 
         BO    ADJAA1                                                           
         USING NDDEMBLK,R7                                                      
         MVC   NUMDEMS,NDNDEMOS                                                 
         B     ADJA2                                                            
ADJAA1   DS    0H                                                               
         USING XDDEMBLK,R7                                                      
         MVC   NUMDEMS,XDNDEMOS                                                 
*                                                                               
ADJA2    BRAS  RE,ADJEXPDM         ADJUST EXPANDED DEMO VALUE                   
         BAS   RE,ADJEQUIV                                                      
         BAS   RE,ADJFEED                                                       
         BAS   RE,ADJCOPY          COPY SPLITS                                  
         BAS   RE,ADJIMP                                                        
         BAS   RE,ADJRND                                                        
         BRAS  RE,ADJ2DEC                                                       
*                                                                               
XITAACT  B     XIT                                                              
*                                                                               
* - DO ALL PROD IMPS AT ONE SHOT AND RETURN REQUESTED PROD IMP VALUES           
SETIMPA  DS    0H                                                               
*                                                                               
         LA    R1,ACTGIMPS         SET ADDRS OF ACT IMP SAVEAREA                
         ST    R1,ADROIMP                                                       
         LA    R1,ACTTIMPS                                                      
         ST    R1,ADRTIMP                                                       
         LA    R1,RAWIMPSA                                                      
         ST    R1,ARAWIMPS                                                      
         LA    R1,ACTGGRPS         SET ADDRS OF ACT GRP SAVEAREA                
         ST    R1,ADROGRP                                                       
         LA    R1,ACTTGRPS                                                      
         ST    R1,ADRTGRP                                                       
         LA    R1,RAWGRPSA                                                      
         ST    R1,ARAWGRPS                                                      
         LA    R1,ADJMULT                                                       
         ST    R1,ADDRADJ                                                       
*                                                                               
         MVI   NUMDEMS,0           DEFAULT                                      
         MVI   OVELCODE,X'DE'      DONT ADJUST IF ACTUAL OVERRIDE               
*                                    ELEMENT EXISTS FOR THIS DEMO               
         OC    NBADEM,NBADEM       CK IF DEMO BLOCK GIVEN                       
         BZ    SETA2                                                            
         L     R7,NBADEM                                                        
         TM    NBINDS6,NBI6XDEM                                                 
         BO    SETA1                                                            
         USING NDDEMBLK,R7                                                      
         MVC   NUMDEMS,NDNDEMOS                                                 
         B     SETAA1                                                           
         USING XDDEMBLK,R7                                                      
SETA1    MVC   NUMDEMS,XDNDEMOS                                                 
*                                                                               
SETAA1   BRAS  RE,ADJEXPDM         ADJUST EXPANDED DEMO VALUE                   
*                                                                               
SETA2    BAS   RE,GETORGS          GET RAW IMPS/ROUND/SAVE                      
         LA    R2,1                                                             
******** L     R7,NBAPROD          R7=MULT PROD ELEMENT                         
******** USING NUPRDD,R7                                                        
         L     RE,NBAPROD                                                       
         USING NXBLKD,RE                                                        
         LA    R7,NXPRDS                                                        
         USING NXPRDS,R7                                                        
         DROP  RE                                                               
*SETA5    MVC   NBFEED,NUPRDFD      SET FEED PERCENT                            
SETA5    MVC   NBFEED,NXPFD        SET FEED PERCENT                             
         L     R1,MBPCTSV          AND ADD TO PCT SAVE AREA                     
         SR    R0,R0                                                            
         ICM   R0,3,NBFEED                                                      
         AR    R1,R0                                                            
         ST    R1,MBPCTSV                                                       
         C     R1,=F'10000'        IF REACHED 100 PCT                           
         BNE   SETA6                                                            
         CLI   NBSPLCNT,1          AND ITS NOT 1ST PRODUCT                      
         BE    SETA6                                                            
         BAS   RE,LASTPRD          PROCESS AS LAST PROD(TOTAL-SUBTOTAL)         
         B     SETA10                                                           
*                                                                               
SETA6    BAS   RE,ADJEQUIV                                                      
         BAS   RE,ADJFEED                                                       
         BAS   RE,ADJCOPY          COPY SPLITS                                  
         BAS   RE,ADJIMP                                                        
         BAS   RE,ADJRND                                                        
         BRAS  RE,ADJ2DEC                                                       
*                                                                               
SETA10   STC   R2,BYTE                                                          
         CLC   NBSPLCNT,BYTE       IF REQUESTED PROD                            
         BE    ADJAX               THAT'S ALL FOLKS                             
         BAS   RE,ADDIMPT          ELSE ADD TO IMP SUBTOTAL                     
         LA    R2,1(R2)            BUMP PROD COUNT                              
         STC   R2,BYTE                                                          
         CLC   BYTE,NBPRDNO        IF NEXT PROD IS LAST                         
         BE    SETALAST            GO PROCESS LAST                              
         BNH   *+6                                                              
         DC    H'0'                SHOULD NEVER GET HERE                        
******   LA    R7,6(R7)            BUMP TO NEXT PROD IN ELEM                    
         LA    R7,9(R7)            BUMP TO NEXT PROD IN ELEM                    
         BAS   RE,RESETIMP         RESET ORIGINAL IMP VALUES                    
         B     SETA5               AND PROCESS                                  
*                                                                               
SETALAST DS    0H                                                               
******   LA    R7,6(R7)            BUMP TO LAST PROD ELEM                       
         LA    R7,9(R7)            BUMP TO LAST PROD ELEM                       
******   OC    NUPRDFD,NUPRDFD     IF LAST FEED = 0                             
         OC    NXPFD,NXPFD         IF LAST FEED = 0                             
         BNZ   SETA20                                                           
         XC    NBFEED,NBFEED       CEAR FEED ADJ PCT                            
         L     RF,ADDRADJ          GET ADJUSTMENT ADDRESS                       
         BASR  RE,RF               AND SEND THROUGH DEMO ROUTINES               
         B     ADJAX                                                            
*                                                                               
SETA20   DS    0H             SUBTRACT SUBTOTAL IMPS FROM ORIGINAL              
         BAS   RE,LASTPRD                                                       
*                                                                               
ADJAX    B     XIT                                                              
         DROP  R7,R5                                                            
**************************************************************                  
         EJECT                                                                  
*              EQUIVALENCY OF IMPRESSIONS AND RATINGS                           
         SPACE 3                                                                
*              INPUTS              NBN0B2 IMPRESSION ADJUSTMENT                 
*                                  NBN2B1 RATING ADJUSTMENT                     
*                                  NUMDEMS NUMBER OF DEMOS REQUESTED            
*              OUTPUT              DOALIST LIST CONTAINING DEMO VALUES          
         SPACE 1                                                                
ADJEQUIV NTR1                                                                   
         USING NURECD,R7                                                        
         L     R7,NBAIO                                                         
         SR    R6,R6                                                            
         ICM   R6,1,NULEN          MULTIPLICAND                                 
         CLI   SPLITLEN,0          TAKE TOTAL LENGTH                            
         BE    *+8                 UNLESS SPLIT PIGGY                           
         IC    R6,SPLITLEN                                                      
         ST    R6,MULTER                                                        
         DROP  R7                                                               
         SPACE 1                                                                
         ZIC   R3,NBN0B2           ADJUST IMPRESSIONS                           
         ST    R3,DIVSOR                                                        
         MVI   HALF,C'E'           ADJUST WITH/WITHOUT IMP OVR                  
         LTR   R3,R3               ONLY ADJUST IF PROFILE IS NON-ZERO           
         BZ    *+8                                                              
         BAS   RE,ADJIMPRS                                                      
         SPACE 1                                                                
         ZIC   R3,NBN2B1           ADJUST RATING                                
         ST    R3,DIVSOR                                                        
         LTR   R3,R3               ONLY ADJUST IF PROFILE IS NON-ZERO           
         BZ    XITEQUIV                                                         
         TM    NBINDS,X'80'        OPTION TO EQUIVALENCE OVERRIDES              
         BO    ADJEQ7                                                           
         XC    DUB,DUB                                                          
         MVI   DUB+2,C'R'                                                       
         MVI   DUB+3,1             CHECK FOR HOMES RATING O/R                   
         BAS   RE,OVERELGT                                                      
         CLI   DMCB+12,0           DONT ADJUST IF OVERRIDDEN                    
         BE    XITEQUIV                                                         
         SPACE 1                                                                
ADJEQ7   BAS   RE,ADJGRPS                                                       
         SPACE 1                                                                
XITEQUIV B     XIT                                                              
*                                                                               
***********************************************************************         
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.8.5.2                                                            
*  TITLE: ADJUNIV- ADJUST IMPRESSIONS BY UNIVERSE                               
*           ONLY USED FOR DEMO VALUES DERIVED FROM ESTIMATES.                   
*                                                                     *         
* INPUTS : NBUNIV - TELLS VALUE TO ADJUST BY.                                   
***********************                                                         
ADJUNIV  NTR1                                                                   
*                                                                               
         OC    NBUNIV,NBUNIV       IF 0 THEN DON'T ADJUST                       
         BZ    XITAUN                                                           
         CLC   NBUNIV,=H'10000'    IF 100%, DONT ADJUST                         
         BE    XITAUN                                                           
*                                                                               
         SR    R6,R6                                                            
         ICM   R6,3,NBUNIV         MULTIPLIER                                   
         ST    R6,MULTER                                                        
         MVC   DIVSOR,=F'10000'    DIVISOR                                      
*                                                                               
         MVI   HALF,C'Y'           ADJUST WITH/WITHOUT IMP OVR                  
         BAS   RE,ADJIMPRS                                                      
**     NEW VALUE=OLD VALUE*NBUNIV/100                                           
*                                                                               
XITAUN   B     XIT                                                              
*                                                                               
*                                                                               
***********************************************************************         
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.8.5.3                                                            
*  TITLE: ADJFEED - ADJUST IMPRESSIONS BY FEED                                  
*                                                                     *         
* INPUTS : NBFEED  - TELLS VALUE TO ADJUST BY.                                  
*          NBUNST3 - IF COPY SPLIT, BYPASS FEED ENTIRELY.                       
***********************                                               *         
ADJFEED  NTR1                                                                   
         TM    NBUNST3,X'40'       IF COPY SPLIT THEN DON'T ADJUST              
         BO    XITAFD                                                           
         CLC   NBFEED,=H'0'        IF 0 THEN DON'T ADJUST                       
         BE    XITAFD                                                           
         CLC   NBFEED,=H'10000'    IF 100%, DONT ADJUST                         
         BE    XITAFD                                                           
*                                                                               
         SR    R6,R6                                                            
         ICM   R6,3,NBFEED         MULTIPLIER                                   
         ST    R6,MULTER                                                        
         MVC   DIVSOR,=F'10000'    DIVISOR                                      
*                                                                               
         MVI   HALF,C'Y'           ADJUST WITH/WITHOUT IMP OVR                  
         BAS   RE,ADJIMPRS         ADJUST IMPRESSIONS                           
**     NEW VALUE=OLD VALUE*NBFEED/100                                           
*                                                                               
XITAFD   B     XIT                                                              
*                                                                               
*                                                                               
***********************************************************************         
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.8.5.4                                                            
*  TITLE: ADJCOPY - ADJUST IMPRESSIONS AND POINTS BY COPY SPLIT                 
*                                                                     *         
* INPUTS : NBSPLOPT- IF X'10' ON - IGNORE COPY SPLIT.                           
*          NBUNST3 - TELLS WHETHER TO ADJUST OR NOT (X'40').                    
*          NBPRD2  - IF NON-ZERO, USE 100.00 MINUS NBFEED.                      
*          NBFEED  - VALUE TO ADJUST BY.                                        
***********************                                               *         
ADJCOPY  NTR1                                                                   
         TM    NBSPLOPT,X'10'      TEST IGNORE COPY SPLIT                       
         BO    XITACOPY                                                         
         TM    NBSPLOPT,X'80'      IS SPLIT OPTION SET                          
         BNO   XITACOPY                                                         
         TM    NBUNST3,X'40'       STATUS BIT SAYS THIS SHOUD BE DONE           
         BNO   XITACOPY                                                         
*                                  3/5/03                                       
**       CLC   NBSPLPRN,NBPRD2     TEST PRODUCT 1 OR 2                          
**       BE    *+18                                                             
         CLC   NBFEED,=H'10000'    PRD 1 - IF FEED=100%, DON'T ADJUST           
         BE    XITACOPY                                                         
***      B     ADJCOPY2                                                         
***      CLC   NBFEED,=H'0'        PRD 2 - IF FEED=0, DON'T ADJUST              
***      BE    XITACOPY                                                         
*                                                                               
ADJCOPY2 SR    R6,R6                                                            
         ICM   R6,3,NBFEED         MULTIPLIER                                   
         CLC   NBSPLPRN,NBPRD2     GOES TOGETHER WITH SPLITEM CHANGE            
         BNE   ADJCOPY4                                                         
***      LH    R5,=H'10000'        IF 2ND PRODUCT                               
***      SR    R5,R6               USE 100.00 MINUS NBFEED                      
***      LR    R6,R5                                                            
*****************************************************                           
ADJCOPY4 ST    R6,MULTER                                                        
         MVC   DIVSOR,=F'10000'    DIVISOR                                      
*                                                                               
         MVI   HALF,C'Y'           ADJUST WITH/WITHOUT IMP OVR                  
         BAS   RE,ADJIMPRS         ADJUST IMPRESSIONS                           
         TM    NBUNST3,X'01'       SHOULD GRPS ALSO BE ADJUSTED                 
         BNO   XITACOPY                                                         
         BAS   RE,ADJGRPS          ADJUST POINTS                                
**     NEW VALUE=OLD VALUE*NUFEED/100                                           
*                                                                               
XITACOPY B     XIT                                                              
*                                                                               
*                                                                               
***********************************************************************         
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.8.5.5                                                            
*  TITLE: ADJIMP - ADJUST IMPRESSIONS AND POINTS BY IMPACT                      
*                                                                     *         
* INPUTS : NBUSER+14- TELLS WHETHER TO ADJUST OR NOT.                           
*          NBIMPACT - VALUE TO ADJUST BY.                                       
***********************                                               *         
ADJIMP   NTR1                                                                   
         CLI   NBUSER+14,C'Y'      PROFILE SAYS IF THIS SHOUD BE DONE           
         BNE   XITAIMP                                                          
         CLC   NBIMPACT,=H'0'      IF 0 THEN DON'T ADJUST                       
         BE    XITAIMP                                                          
         CLC   NBIMPACT,=H'10000'   IF 100%, DONT ADJUST                        
         BE    XITAIMP                                                          
*                                                                               
         SR    R6,R6                                                            
         ICM   R6,3,NBIMPACT       MULTIPLIER                                   
         ST    R6,MULTER                                                        
         MVC   DIVSOR,=F'10000'    DIVISOR                                      
*                                                                               
         MVI   HALF,C'N'           DON'T ADJUST IF OVERRIDE                     
         BAS   RE,ADJIMPRS         ADJUST IMPRESSIONS                           
         BAS   RE,ADJGRPS          ADJUST POINTS                                
**     NEW VALUE=OLD VALUE*NUIMPACT/100                                         
*                                                                               
XITAIMP  B     XIT                                                              
*                                                                               
*                                                                               
***********************************************************************         
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.8.5.5                                                            
*  TITLE: ADJPG  - ADJUST IMPRESSIONS AND POINTS BY PACKAGE GUARANTEE           
*                                                                     *         
* INPUTS :                                                                      
*          NBGUAFAC - VALUE TO ADJUST BY.                                       
***********************                                               *         
ADJPG    NTR1                                                                   
         TM    NBINDS,X'40'        ..IF INCREASE PRECISION SET                  
         BZ    ADJP0                                                            
         MVC   MULTER,=F'100'      ..MULT ALL BY 100                            
         MVC   DIVSOR,=F'1'                                                     
         MVI   HALF,C'Y'           ..ADJUST ALL OVERRIDES                       
         MVI   IFDEM,C'Y'          ..AND ALL DEMO GUARANTREES                   
         BAS   RE,ADJIMPRS                                                      
****     BAS   RE,ADJGRPS                                                       
         MVI   HALF,0                                                           
         MVI   IFDEM,0                                                          
         B     ADJP0               RESUME NORMAL PROCESSING                     
*                                                                               
ADJP0    CLC   NBNGUFAC,=F'0'      NEW PACKAGE GUARANTEE                        
         BE    XITAPG                                                           
         TM    NBINDS,X'20'        SKIP GUARANTEE FACTOR                        
         BO    XITAPG                                                           
         MVC   MULTER,NBNGUFAC     NEW PKG GUA IS 4 DECIMALS                    
*****    TM    NBINDS3,NBI3A2DC                                                 
*****    BNO   ADJP010                                                          
*****    MVC   DIVSOR,=F'10000'    FOR 2 DEC AGY                                
*****    B     ADJPG1A                                                          
ADJP010  MVC   DIVSOR,=F'1000000'                                               
         B     ADJPG1A                                                          
*                                                                               
ADJPG1A  MVI   HALF,C'N'           DON'T ADJUST IF OVERRIDE                     
         MVI   IFDEM,C'N'          DONT ADJUST IF DEMO GUARANTEE                
         BAS   RE,ADJIMPRS         ADJUST IMPRESSIONS                           
         BAS   RE,ADJGRPS          ADJUST POINTS                                
         MVI   IFDEM,0                                                          
**     NEW VALUE=OLD VALUE*NBGUAFAC/100                                         
*                                                                               
XITAPG   B     XIT                                                              
*                                                                               
***********************************************************************         
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.8.5.6                                                            
*  TITLE: ROUNDIMPRESSIONSTO10                                                  
*                                                                     *         
***********************                                               *         
ADJRND   NTR1                                                                   
*                                                                               
ADJ00    TM    NBINDS3,NBI3A2DC    2 DEC AGENCY ?                               
         BNO   DOADJ               NO-                                          
         TM    NBINDS3,NBI3B2DC    YES-NEW BOOK?                                
         BO    XITRND                  YES-DON'T ROUND                          
DOADJ    EQU   *                                                                
         TM    NBSBKEND,NBEXPDM6   FULL EXPANDED DEMOS?                         
         BO    ADJRND2             YES                                          
         CLI   NBHUNOPT,C'Y'       WHEN NEW SET-UP IS INSTALLED                 
         BNE   ADJRND2                                                          
*                                                                               
         CLI   NBPOSTYP,C'N'       ONLY ROUND FOR MAIN NETWORK                  
         BE    ADJRND2                                                          
         CLI   NBPOSTYP,C'S'       ONLY ROUND FOR SYNDICATION                   
         BE    ADJRND2                                                          
         CLI   NBPOSTYP,C'H'       ONLY ROUND FOR HISPANIC                      
         BNE   XITRND                                                           
*                                                                               
         CLI   NBSTATYP,C'C'       IF HISAPANIC AND CABLE                       
         BNE   ADJRND2                                                          
         TM    NBINDS3,NBI3A2DC    AND 2 DEC AGY                                
         BO    XITRND              DON'T ROUND                                  
         SPACE 1                                                                
ADJRND2  LA    R6,10                                                            
         CLI   NBHUNOPT,C'Y'       HUNOPT AFFECTS FINAL RESULT                  
         BNE   *+8                                                              
         LA    R6,100                                                           
*                                                                               
*!!!     L     R5,DOAIHOME         DO FOR HOMES IMP                             
         MVC   FULL,DOAIHOME       DO FOR HOMES IMP                             
         NI    FULL,X'FF'-X'80'    TURN OFF OVERRIDE INDICATOR                  
         L     R5,FULL                                                          
                                                                                
         SR    R4,R4                                                            
*                                                                               
         TM    NBSBKEND,NBEXPDM6+NBEXPDEM   EXPANDED DEMO VALUES?               
         BZ    ADJRND5             NO                                           
ADJRND4B LA    R6,10               JUST MULTIPLY BY 10                          
         TM    NBSBKEND,NBEXPDM6   FULL EXPANSION ?                             
         BZ    ADJRND6             NO                                           
**                                 YES                                          
         BAS   RE,CHKRESLT         IF NOT ACTUAL DEMO                           
         BE    ADJRND6             GET OUT                                      
         L     R6,=F'10000'        ELSE INCREASE PREC FOR ESTASACT              
         CLI   NBPOSTYP,C'C'       BUT IF CABLE                                 
         BNE   *+8                                                              
         L     R6,=F'1000'                                                      
         B     ADJRND6                                                          
*                                                                               
CHKRESLT NTR1                                                                   
         LA    RE,RESULTAB         RESULT TABLE OF NON-ACTUAL VALUES            
CHKR10   CLI   0(RE),X'FF'                                                      
         BE    XIT                 EOF-DON'T SAVE R1                            
         CLC   0(1,RE),RESULT                                                   
         BE    CHKNOT                                                           
         CLI   RESULT,0                                                         
         BE    CHKNOT                                                           
         LA    RE,1(RE)                                                         
         B     CHKR10                                                           
CHKNOT   LTR   RE,RE                                                            
         XIT1                                                                   
RESULTAB DC    C'ELZYBW'                                                        
         DC    X'FF'                                                            
**************************************************                              
*                                                                               
ADJRND5  LA    R1,5                                                             
         LA    R4,10                                                            
         CLI   NBPREOPT,C'Y'                                                    
         BNE   SETADDR                                                          
         CLI   NBPOSTYP,C'H'                                                    
         BNE   SETADDR                                                          
         CLI   NBSURVEY,C'C'                                                    
         BNE   SETADDR                                                          
         BAS   RE,CHKRESLT         IF ACTUAL DEMO                               
         BE    SETADDR             GET OUT                                      
         LA    R1,50                                                            
         LA    R4,100                                                           
SETADDR  ST    R1,ADDER                                                         
         ST    R4,DIVSOR                                                        
         SR    R4,R4                                                            
                                                                                
*                                                                               
         A     R5,ADDER                                                         
         D     R4,DIVSOR                                                        
ADJRND6  MR    R4,R6                                                            
*                                                                               
         ST    R5,FULL                                                          
         TM    NBINDS9,NBI9DOV     DEMO OVERRIDES INDICATOR                     
         JZ    ADJRND7                                                          
         TM    DOAIHOME,X'80'      OVERRIDE?                                    
         JZ    *+8                                                              
         OI    FULL,X'80'          TURN INDICATOR BACK ON                       
ADJRND7  MVC   DOAIHOME(4),FULL                                                 
*!!!     ST    R5,DOAIHOME                                                      
*                                                                               
         LA    R2,DOADMSET         FIRST SET OF DEMOS                           
         SR    R7,R7                                                            
         ICM   R7,1,NUMDEMS        SETS CONDITION CODE ALSO                     
         BZ    XITRND              NO MORE DEMOS REQUESTED                      
*                                                                               
*!!!ARNDLOOP L     R5,ALIMPOFF(R2)                                              
ARNDLOOP MVC   FULL,ALIMPOFF(R2)                                                
         NI    FULL,X'FF'-X'80'    TURN OFF OVERRIDE INDICATOR                  
         L     R5,FULL                                                          
                                                                                
         SR    R4,R4                                                            
         TM    NBSBKEND,NBEXPDEM+NBEXPDM6   EXPANDED DEMO VALUES?               
         BNZ   ARNDLP5                      YES/JUST X 10/NO ROUND              
ARNDLP3  A     R5,ADDER                     NO                                  
         D     R4,DIVSOR                                                        
ARNDLP5  MR    R4,R6                                                            
                                                                                
***      CVD   R5,DUB                                                           
****     GOTO1 =V(PRNTBL),DMCB,=C'DUB',DUB,C'DUMP',8,=C'1D'                     
**                                                                              
ARNDLP6  ST    R5,FULL                                                          
         TM    NBINDS9,NBI9DOV     DEMO OVERRIDES INDICATOR                     
         JZ    ARNDLP7                                                          
         TM    ALIMPOFF(R2),X'80'  OVERRIDE?                                    
         JZ    *+8                                                              
         OI    FULL,X'80'          TURN INDICATOR BACK ON                       
ARNDLP7  MVC   ALIMPOFF(4,R2),FULL                                              
*ARNDLP6  ST    R5,ALIMPOFF(R2)                                                 
         LA    R2,ALLENGTH(R2)                                                  
         BCT   R7,ARNDLOOP                                                      
*                                                                               
XITRND   B     XIT                                                              
***********************************************************************         
*                                                                               
         EJECT                                                                  
*******************************************************************             
*                                                                               
ADJIMPRS NTR1                                                                   
         GOTO1 =A(ADJUST),DMCB,(0,(RC)),RR=RELO                                 
         XIT1                                                                   
************                                                                    
* ADJUST GRPS- ADJUSTS GRPS BY MULTER/DIVSOR                                    
*     DOES NOT ADJUST IF OVERRIDE ELEMENT EXISTS FOR THE GRP                    
*   INPUTS: MULTER,DIVSOR                                                       
*           OVELCODE - APPLICABLE OVERRIDE ELEMENT NUMBER                       
*                                                                               
ADJGRPS  NTR1                                                                   
         GOTO1 =A(ADJUST),DMCB,(1,(RC)),RR=RELO                                 
         XIT1                                                                   
         EJECT                                                                  
************                                                                    
* ADJUST DEMO GUARANTEES - ADJUSTS IMPRSSIONS, GRPS  BY DEMO GUARANTEE          
*     IF IT EXISTS.                                                             
*                                                                               
*   LOCALS: DIVSOR,MULTER - FACTORS                                             
*                                                                               
ADJDG    NTR1                                                                   
         TM    NBINDS,X'20'        SKIP DEMO GUARANTEE FACTOR                   
         BO    XITDG                                                            
         L     R1,=F'1000000'      NEW DEMO GUARANTEE IS 4 DECIMALS             
         CVD   R1,DUB                                                           
         MVC   PDIVSOR,DUB                                                      
         SRL   R1,1                                                             
         CVD   R1,DUB                                                           
         MVC   PADDER,DUB                                                       
*                                  DO FOR HOMES IMP                             
         CLI   NBDEMRAW,C'Y'       LOOKING FOR RAW NUMBERS                      
         BE    XITDG               DON'T ADJUST/JUST EXIT                       
         XC    DUB,DUB                                                          
         MVC   DUB+2(2),DIAIHOME+1                                              
         BAS   RE,OVERELGT                                                      
         CLI   DMCB+12,0           DONT ADJUST IF OVERRIDDEN                    
         BE    ADGOTHER                                                         
         GOTO1 NBHELLO,DMCB,(C'G',UNTFIL),(X'B4',NBAIO),0                       
         CLI   DMCB+12,0           IS THERE HOMES GUARANTEE FACTOR?             
         BNE   ADGOTHER                                                         
***      MVC   BYTE,DIAIHOME+2     CHECK DEMO MATCH                             
         MVC   HALF(1),DIAIHOME       CHECK DEMO MATCH                          
         MVC   HALF+1(1),DIAIHOME+2     CHECK DEMO MATCH                        
         L     R6,DMCB+12                                                       
         BAS   RE,CHKC2                                                         
         BNE   ADGOTHER                                                         
*                                                                               
ADJD10   L     R3,DMCB+12          A(GUARANTEE ELEMENT)                         
         USING NUNDGD,R3                                                        
         SR    R1,R1               GET FACTOR IN MULTER                         
         ICM   R1,15,NUNDGFAC                                                   
         CVD   R1,DUB                                                           
         MVC   PMULTER,DUB                                                      
*!!!     L     R5,DOAIHOME         DO FOR HOMES IMP                             
         MVC   FULL,DOAIHOME       DO FOR HOMES IMP                             
         NI    FULL,X'FF'-X'80'    TURN OFF OVERRIDE INDICATORS                 
         L     R5,FULL                                                          
*                                                                               
         CVD   R5,DUB                                                           
         ZAP   PWORK,DUB                                                        
         MP    PWORK,PMULTER                                                    
         AP    PWORK,PADDER                                                     
         DP    PWORK,PDIVSOR                                                    
         MVC   DUB,PWORK                                                        
         CVB   R5,DUB                                                           
*!!!     ST    R5,DOAIHOME                                                      
         ST    R5,FULL                                                          
         TM    NBINDS9,NBI9DOV     DEMO OVERRIDES INDICATOR                     
         JZ    ADJD20                                                           
         TM    DOAIHOME,X'80'      OVERRIDE?                                    
         JZ    *+8                                                              
         OI    FULL,X'80'          TURN INDICATOR BACK ON                       
ADJD20   MVC   DOAIHOME,FULL                                                    
*                                                                               
*!!!     L     R5,DOAGHOME         DO FOR HOMES GRP                             
         MVC   FULL,DOAGHOME       DO FOR HOMES GRP                             
         NI    FULL,X'FF'-X'80'    TURN OFF OVERRIDE INDICATORS                 
         L     R5,FULL                                                          
*                                                                               
         CVD   R5,DUB                                                           
         ZAP   PWORK,DUB                                                        
         MP    PWORK,PMULTER                                                    
         AP    PWORK,PADDER                                                     
         DP    PWORK,PDIVSOR                                                    
         MVC   DUB,PWORK                                                        
         CVB   R5,DUB                                                           
*!!!     ST    R5,DOAGHOME                                                      
         ST    R5,FULL                                                          
         TM    NBINDS9,NBI9DOV     DEMO OVERRIDES INDICATOR                     
         JZ    ADJD30                                                           
         TM    DOAGHOME,X'80'      OVERRIDE?                                    
         JZ    *+8                                                              
         OI    FULL,X'80'          TURN INDICATOR BACK ON                       
ADJD30   MVC   DOAGHOME,FULL                                                    
         DROP  R3                                                               
*                                                                               
ADGOTHER LA    R2,DOADMSET         FIRST SET OF DEMOS                           
         SR    R7,R7                                                            
         ICM   R7,1,NUMDEMS        SETS CONDITION CODE ALSO                     
         BZ    XITDG               NO MORE DEMOS REQUESTED                      
         LA    R6,DIADMSET+6       DEMO CODE FOR IMPRESSION                     
         L     RF,NBADEM                                                        
         ST    RF,ACURDEMO         A(CURRENT DEMO BEING PROCESSED)              
*                                                                               
         CLI   NBDEMRAW,C'Y'       LOOKING FOR RAW NUMBERS                      
         BE    ADGNEXT                                                          
ADGLOOP  XC    DUB,DUB                                                          
         MVC   DUB+1(3),0(R6)                                                   
         L     RF,ACURDEMO                                                      
         CLI   2(RF),0             COMSCORE?                                    
         JE    ADGNEXT                                                          
         BAS   RE,OVERELGT                                                      
         CLI   DMCB+12,0           DONT ADJUST IF OVERRIDDEN                    
         BE    ADGNEXT                                                          
         CLI   1(R6),X'21'         DONT ADJUST IF USER DEMO                     
         BE    ADGNEXT                                                          
         GOTO1 NBHELLO,DMCB,(C'G',UNTFIL),(X'B4',NBAIO),0                       
         CLI   DMCB+12,0           DONT ADJUST IF NO DEMO GUARANTEE             
         BNE   ADGNEXT                                                          
***      MVC   BYTE,2(R6)          CHECK IF DEMOS MATCH                         
         MVC   HALF(1),0(R6)          CHECK IF DEMOS MATCH                      
         MVC   HALF+1(1),2(R6)        CHECK IF DEMOS MATCH                      
         LR    R1,R6               SAVE R6                                      
         L     R6,DMCB+12                                                       
         BAS   RE,CHKC2                                                         
         LR    R6,R1               RESTORE R6                                   
         BNE   ADGNEXT                                                          
*                                                                               
         L     R3,DMCB+12                                                       
         USING NUNDGD,R3                                                        
         SR    R1,R1                                                            
         ICM   R1,15,NUNDGFAC                                                   
         CVD   R1,DUB                                                           
         MVC   PMULTER,DUB                                                      
*!!!     L     R1,ALIMPOFF(2)                                                   
         MVC   FULL,ALIMPOFF(2)                                                 
         NI    FULL,X'FF'-X'80'    TURN OFF OVERRIDE INDICATOR                  
         L     R1,FULL                                                          
*                                                                               
         CVD   R1,DUB                                                           
         ZAP   PWORK,DUB                                                        
         MP    PWORK,PMULTER                                                    
*** PXZ  CLI   NBSURVEY,C'C'       IF CABLE    9/12/91 TEMPORARY                
         CLI   NBPOSTYP,C'C'       IF CABLE    9/12/91 TEMPORARY                
         BE    CABLIT                                                           
         AP    PWORK,=P'5000000'                                                
         DP    PWORK,=P'10000000'                                               
         MP    PWORK(11),=P'10'                                                 
         ZAP   DUB,PWORK+3(8)                                                   
         B     STORIT                                                           
*                                                                               
CABLIT   AP    PWORK,=P'500000'           9/9/91                                
         DP    PWORK,=P'1000000'          9/9/91                                
         MVC   DUB,PWORK+4         9/9/91                                       
*                                                                               
STORIT   CVB   R5,DUB                                                           
*!!!     ST    R5,ALIMPOFF(R2)                                                  
         ST    R5,FULL                                                          
         TM    NBINDS9,NBI9DOV     DEMO OVERRIDES INDICATOR                     
         JZ    STORIT2                                                          
         TM    ALIMPOFF(R2),X'80'  OVERRIDE?                                    
         JZ    *+8                                                              
         OI    FULL,X'80'          TURN INDICATOR BACK ON                       
STORIT2  MVC   ALIMPOFF(4,R2),FULL                                              
*                                                                               
*!!!     L     R5,ALGRPOFF(R2)                                                  
         MVC   FULL,ALGRPOFF(R2)                                                
         NI    FULL,X'FF'-X'80'                                                 
         L     R5,FULL                                                          
*                                                                               
         CVD   R5,DUB                                                           
         ZAP   PWORK,DUB                                                        
         MP    PWORK,PMULTER                                                    
         AP    PWORK,PADDER                                                     
         DP    PWORK,PDIVSOR                                                    
         MVC   DUB,PWORK                                                        
         CVB   R5,DUB                                                           
*!!!     ST    R5,ALGRPOFF(R2)                                                  
         ST    R5,FULL                                                          
         TM    NBINDS9,NBI9DOV     DEMO OVERRIDES INDICATOR                     
         JZ    STORIT4                                                          
         TM    ALGRPOFF(R2),X'80'  OVERRIDE?                                    
         JZ    *+8                                                              
         OI    FULL,X'80'          TURN INDICATOR BACK ON                       
STORIT4  MVC   ALGRPOFF(4,R2),FULL                                              
         DROP  R3                                                               
*                                                                               
ADGNEXT  LA    R2,ALLENGTH(R2)                                                  
         LA    R6,9(R6)            NEXT IMPRESSION DEMO CODE                    
         L     RF,ACURDEMO                                                      
         AHI   RF,3                                                             
         ST    RF,ACURDEMO                                                      
         BCT   R7,ADGLOOP          (CAN USE ANY OF THE DEMO CODES)              
*                                                                               
XITDG    B     XIT                                                              
*                                                                               
*                                                                               
*   GET A DEMO OVERRIDE - THIS CHECKS 5 POSITIONS TO MAKE SURE                  
*   THAT WE REALLY GET A MANUAL OVERRIDE. FOR NAD EST./ACT. THE                 
*   5TH POSITION IS ZERO ONLY IF IT IS AN OVERRIDE. BECAUSE OF THIS             
*   WE ARE ABLE TO DISTINGUISH THE 'DD'/'DE' OVERRRIDE ELEMENTS FROM            
*   THE 'DD'/'DE' NORMAL ELEMENTS.                                              
*                                                                               
OVERELGT LR    R0,RE                                                            
         GOTO1 NBHELLO,DMCB,(C'G',UNTFIL),(OVELCODE,NBAIO),            X        
               (5,DUB)                                                          
         LR    RE,R0                                                            
         BR    RE                                                               
************                                                                    
*                                                                               
*                                                                               
         EJECT                                                                  
************************* HIPO ****************************************         
*  NV1.8.6                                                                      
*  TITLE: SUBROUTINE MOVESTDM - PUT ESTIMATE DEMOS IN BLOCK.                    
*           CONVERT THEM FROM THE DEMOUT OUTPUT LIST                            
*               TO THE COMPRESSED FORMAT OF NETBLOCK.                           
*                                                                     *         
*  CALLED FROM: STPUTEM - ANY TIME DEMOS ARE REQUESTED                          
*                                                                     *         
*  INPUTS: DOALIST - CONTAINS THE 1 WORD DEMO VALUES GIVEN BY DEMOUT. *         
*                                                                               
*  OUTPUTS: NETBLOCK -                                                          
*           NDDEMBLK - DEMO OUTPUT BLOCK.                                       
*                                                                     *         
*  LOCALS: R2 - ADDRESS OF CURRENT DEMO VALUE IN DOALIST              *         
*          R3 - COUNTER USED TO DO EACH REQUESTED DEMO.               *         
*          R6 - ADDRESS OF NET DEMO UNIVERSE BLOCK                              
*          R7 - ADDRESS OF NET DEMO BLOCK                                       
*                                                                     *         
***** NOTE: ALMOST IDENTICAL TO MOVACTDM                                        
*                                                                     *         
***********************************************************************         
*                                                                               
MOVESTDM NTR1                                                                   
         LA    R4,NBESTSHR                                                      
         MVC   0(2,R4),DOASHARE+2  USE LEAST SIG 2-BYTES OF SHARE               
         MVC   2(2,R4),DOAHUT+2    USE LEAST SIG 2-BYTES OF HUT                 
         MVC   6(2,R4),DOAVHOME+2  USE LEAST SIG 2-BYTES OF HOMES               
         MVC   8(2,R4),DOAGHOME+2  USE LEAST SIG 2-BYTES OF HOME GRP            
         MVC   10(4,R4),DOAIHOME   USE ALL 4 BYTES OF HOME IMPRESSION           
         BRAS  RE,SETHOVR          SET HOME OVERRIDE INDICATORS                 
*                                                                               
         OC    NBADEM,NBADEM       DOES NET DEMO BLOCK EXIST?                   
         BZ    XITMESTD                                                         
         TM    NBINDS6,NBI6XDEM    EXPANDED DEMO BLOCK?                         
         BO    MOVESTXD                                                         
         L     R7,NBADEM                                                        
         USING NDDEMBLK,R7                                                      
*                                                                               
         SR    R3,R3                                                            
         ICM   R3,1,NDNDEMOS       ALSO SETS COND CODE                          
         BZ    XITMESTD            NO DEMOS REQUESTED                           
         LA    R4,NDESTDEM         POINT R4 TO NEXT DEMOS                       
         LA    R2,DOADMSET                                                      
*                                                                               
*                                  DO FOR EACH DEMO REQUESTED                   
MOVEM    MVC   0(2,R4),2(R2)       USE LEAST SIG 2-BYTES OF VPH                 
         MVC   2(2,R4),6(R2)       USE LEAST SIG 2-BYTES OF GRP                 
         MVC   4(4,R4),8(R2)       USE ALL 4 BYTES OF IMPRESSION                
*                                                                               
         TM    NBINDS9,NBI9DOV     SET DEMO OVERRIDE INDICATOR?                 
         JZ    MOVEM2                                                           
         TM    0(R2),X'80'         OVERRIDE?                                    
         JZ    *+8                                                              
         OI    0(R4),X'80'                                                      
         TM    4(R2),X'80'         OVERRIDE?                                    
         JZ    *+8                                                              
         OI    2(R4),X'80'                                                      
         TM    8(R2),X'80'         OVERRIDE?                                    
         JZ    *+8                                                              
         OI    4(R4),X'80'                                                      
*                                                                               
MOVEM2   LA    R4,8(R4)            NEXT SET OF DEMO VALUES                      
         LA    R2,ALLENGTH(R2)                                                  
         BCT   R3,MOVEM                                                         
*                                                                               
         MVC   NDESTWUN,DOAWUNIV   MOVE WEIGHTD UNIVERSE                        
*                                                                               
         OC    NDAUBLOK,NDAUBLOK   DOES UNIV BLOCK EXIST?                       
         BZ    XITMESTD                                                         
         L     R6,NDAUBLOK                                                      
         USING NDUNBLOK,R6                                                      
         MVC   NDUEHOME,DOUHOME     MOVE UNIVERSES                              
         ZIC   R1,NDNDEMOS                                                      
         SLL   R1,2                X 4 FOR L'VALUE                              
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   NDUEDEM(0),DOUDMSET                                              
*                                                                               
XITMESTD B     XIT                                                              
         DROP  R6                                                               
         DROP  R7                                                               
*                                                                               
MOVESTXD L     R7,NBADEM                                                        
         USING XDDEMBLK,R7                                                      
*                                                                               
         SR    R3,R3                                                            
         ICM   R3,1,XDNDEMOS       ALSO SETS COND CODE                          
         BZ    XITMESTD            NO DEMOS REQUESTED                           
         LA    R4,XDESTDEM         POINT R4 TO NEXT DEMOS                       
         LA    R2,DOADMSET                                                      
*                                                                               
*                                  DO FOR EACH DEMO REQUESTED                   
MOVEMX   MVC   0(2,R4),2(R2)       USE LEAST SIG 2-BYTES OF VPH                 
         MVC   2(2,R4),6(R2)       USE LEAST SIG 2-BYTES OF GRP                 
         MVC   4(4,R4),8(R2)       USE ALL 4 BYTES OF IMPRESSION                
*                                                                               
         TM    NBINDS9,NBI9DOV     SET DEMO OVERRIDE INDICATOR?                 
         JZ    MOVEMX2                                                          
         TM    0(R2),X'80'         OVERRIDE?                                    
         JZ    *+8                                                              
         OI    0(R4),X'80'                                                      
         TM    4(R2),X'80'         OVERRIDE?                                    
         JZ    *+8                                                              
         OI    2(R4),X'80'                                                      
         TM    8(R2),X'80'         OVERRIDE?                                    
         JZ    *+8                                                              
         OI    4(R4),X'80'                                                      
*                                                                               
MOVEMX2  LA    R4,8(R4)            NEXT SET OF DEMO VALUES                      
         LA    R2,ALLENGTH(R2)                                                  
         BCT   R3,MOVEMX                                                        
*                                                                               
         MVC   XDESTWUN,DOAWUNIV   MOVE WEIGHTD UNIVERSE                        
*                                                                               
         OC    XDAUBLOK,XDAUBLOK   DOES UNIV BLOCK EXIST?                       
         BZ    XITMESTD                                                         
         L     R6,XDAUBLOK                                                      
         USING NDUNBLOK,R6                                                      
         MVC   NDUEHOME,DOUHOME     MOVE UNIVERSES                              
         ZIC   R1,XDNDEMOS                                                      
         SLL   R1,2                X 4 FOR L'VALUE                              
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   NDUEDEM(0),DOUDMSET                                              
         B     XIT                                                              
         DROP  R6                                                               
         DROP  R7                                                               
*************************************************************                   
         EJECT                                                                  
************************* HIPO ****************************************         
*  NV1.8.10                                                                     
*  TITLE: SUBROUTINE MOVACTDM - PUT ACTUAL DEMOS IN BLOCK.                      
*           CONVERT THEM FROM THE DEMOUT OUTPUT LIST                            
*               TO THE COMPRESSED FORMAT OF NETBLOCK.                           
*                                                                     *         
*  CALLED FROM: STPUTEM - ANY TIME DEMOS ARE REQUESTED                          
*                                                                     *         
*  INPUTS: DOALIST - CONTAINS THE 1 WORD DEMO VALUES GIVEN BY DEMOUT. *         
*                                                                               
*  OUTPUTS: NETBLOCK -                                                          
*           NDDEMBLK - DEMO OUTPUT BLOCK.                                       
*                                                                     *         
*  LOCALS: R2 - ADDRESS OF CURRENT DEMO VALUE IN DOALIST              *         
*          R3 - COUNTER USED TO DO EACH REQUESTED DEMO.               *         
*          R6 - ADDRESS OF NET DEMO UNIVERSE BLOCK                              
*          R7 - ADDRESS OF NET DEMO BLOCK                                       
*                                                                     *         
***** NOTE: ALMOST IDENTICAL TO MOVESTDM                                        
*                                                                     *         
***********************************************************************         
*                                                                               
MOVACTDM NTR1                                                                   
         LA    R4,NBACTSHR                                                      
         MVC   0(2,R4),DOASHARE+2  USE LEAST SIG 2-BYTES OF SHARE               
         MVC   2(2,R4),DOAHUT+2    USE LEAST SIG 2-BYTES OF HUT                 
         MVC   6(2,R4),DOAVHOME+2  USE LEAST SIG 2-BYTES OF HOMES               
         MVC   8(2,R4),DOAGHOME+2  USE LEAST SIG 2-BYTES OF HOME GRP            
         MVC   10(4,R4),DOAIHOME   USE ALL 4 BYTES OF HOME IMPRESSION           
         BRAS  RE,SETHOVR          SET HOME OVERRIDE INDICATORS                 
*                                                                               
         OC    NBADEM,NBADEM       DOES NET DEMO BLOCK EXIST?                   
         BZ    XITMACTD                                                         
         TM    NBINDS6,NBI6XDEM    EXPANDED DEM BLOCK?                          
         BO    MOVACTDX                                                         
         L     R7,NBADEM                                                        
         USING NDDEMBLK,R7                                                      
*                                                                               
         SR    R3,R3                                                            
         ICM   R3,1,NDNDEMOS       ALSO SETS COND CODE                          
         BZ    XITMACTD            NO DEMOS REQUESTED                           
         LA    R4,NDACTDEM         POINT R4 TO NEXT DEMOS                       
         LA    R2,DOADMSET                                                      
*                                                                               
*                                  DO FOR EACH DEMO REQUESTED                   
MOVACT   MVC   0(2,R4),2(R2)       USE LEAST SIG 2-BYTES OF VPH                 
         MVC   2(2,R4),6(R2)       USE LEAST SIG 2-BYTES OF GRP                 
         MVC   4(4,R4),8(R2)       USE ALL 4 BYTES OF IMPRESSION                
*                                                                               
         TM    NBINDS9,NBI9DOV     SET DEMO OVERRIDE INDICATOR?                 
         JZ    MOVACT2                                                          
         TM    0(R2),X'80'         OVERRIDE?                                    
         JZ    *+8                                                              
         OI    0(R4),X'80'                                                      
         TM    4(R2),X'80'         OVERRIDE?                                    
         JZ    *+8                                                              
         OI    2(R4),X'80'                                                      
         TM    8(R2),X'80'         OVERRIDE?                                    
         JZ    *+8                                                              
         OI    4(R4),X'80'                                                      
*                                                                               
MOVACT2  LA    R4,8(R4)            NEXT SET OF DEMO VALUES                      
         LA    R2,ALLENGTH(R2)                                                  
         BCT   R3,MOVACT                                                        
*                                                                               
         MVC   NDACTWUN,DOAWUNIV   MOVE WEIGHTD UNIVERSE                        
*                                                                               
         OC    NDAUBLOK,NDAUBLOK   DOES UNIV BLOCK EXIST?                       
         BZ    XITMACTD                                                         
         L     R6,NDAUBLOK                                                      
         USING NDUNBLOK,R6                                                      
         MVC   NDUAHOME,DOUHOME     MOVE UNIVERSES                              
         ZIC   R1,NDNDEMOS                                                      
         SLL   R1,2                                                             
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   NDUADEM(0),DOUDMSET                                              
*                                                                               
XITMACTD B     XIT                                                              
         DROP  R6                                                               
         DROP  R7                                                               
*                                                                               
MOVACTDX L     R7,NBADEM                                                        
         USING XDDEMBLK,R7                                                      
*                                                                               
         SR    R3,R3                                                            
         ICM   R3,1,XDNDEMOS       ALSO SETS COND CODE                          
         BZ    XITMACTD            NO DEMOS REQUESTED                           
         LA    R4,XDACTDEM         POINT R4 TO NEXT DEMOS                       
         LA    R2,DOADMSET                                                      
*                                                                               
*                                  DO FOR EACH DEMO REQUESTED                   
MOVACTX  MVC   0(2,R4),2(R2)       USE LEAST SIG 2-BYTES OF VPH                 
         MVC   2(2,R4),6(R2)       USE LEAST SIG 2-BYTES OF GRP                 
         MVC   4(4,R4),8(R2)       USE ALL 4 BYTES OF IMPRESSION                
*                                                                               
         TM    NBINDS9,NBI9DOV     SET DEMO OVERRIDE INDICATOR?                 
         JZ    MOVACTX2                                                         
         TM    0(R2),X'80'         OVERRIDE?                                    
         JZ    *+8                                                              
         OI    0(R4),X'80'                                                      
         TM    4(R2),X'80'         OVERRIDE?                                    
         JZ    *+8                                                              
         OI    2(R4),X'80'                                                      
         TM    8(R2),X'80'         OVERRIDE?                                    
         JZ    *+8                                                              
         OI    4(R4),X'80'                                                      
*                                                                               
MOVACTX2 LA    R4,8(R4)            NEXT SET OF DEMO VALUES                      
         LA    R2,ALLENGTH(R2)                                                  
         BCT   R3,MOVACTX                                                       
*                                                                               
         MVC   XDACTWUN,DOAWUNIV   MOVE WEIGHTD UNIVERSE                        
*                                                                               
         OC    XDAUBLOK,XDAUBLOK   DOES UNIV BLOCK EXIST?                       
         BZ    XITMACTD                                                         
         L     R6,XDAUBLOK                                                      
         USING NDUNBLOK,R6                                                      
         MVC   NDUAHOME,DOUHOME     MOVE UNIVERSES                              
         ZIC   R1,XDNDEMOS                                                      
         SLL   R1,2                                                             
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   NDUADEM(0),DOUDMSET                                              
         B     XIT                                                              
         DROP  R6                                                               
         DROP  R7                                                               
*************************************************************                   
         EJECT                                                                  
*                                                                               
************************* HIPO ****************************************         
*  NUMBER: NV1.8.8                                                              
*  TITLE: MERGEDUM - MERGE DUMMY RECORD INTO THE UNIT RECORD AND                
*           TELL THE CONTROLLER THAT THE RECORD HAS BEEN CHANGED.               
*           CURRENTLY (TEMPORARY?) THE ACTUAL DEMO ELEMENTS (43,45,             
*           5E) AND THE NETVALUE ELEMENT (71) ARE DELETED AND ALL               
*           ELEMENTS IN DUMMY RECORD ARE ADDED.                                 
*           IDEALLY, THIS SHOULD BE HANDLED BY THE DEMO SYSTEM.                 
*                                                                               
* CALLED FROM : STPUTEM - ANYTIME THE UNIT RECORD IS MODIFIED                   
* CALLS TO :    DELACTEL - DELETES THE ACTUAL ELEMENTS                          
*                                                                     *         
*  INPUTS: NBAIO - ADDRESS OF UNIT RECORD                             *         
*          DUMMYREC - RECORD TO MERGE                                           
*                                                                               
*  LOCALS - R7 - POINTS TO NEW UNIT RECORD                                      
*           R6 - USED BY NEXTEL. POINTS TO CURRENT ELEMENT.                     
*                                                                     *         
*  GLOBAL OUTPUTS: NEW UNIT RECORD POINTED TO BY NBAIO                *         
*                  NBUPUNIT - SAYS THAT UNIT WAS UPDATED                        
*                                                                     *         
***********************************************************************         
MERGEDUM NTR1                                                                   
*                                                                               
         BAS   RE,DELACTEL         DELETE ACTUAL AND NETVALUE ELEMENTS          
*                                                                               
****  NOW ADD ELEMENTS FROM DUMMYREC ONE AT A TIME                              
         LA    R6,DUMMYREC+23      A(FIRST ELEMENT IN DUMMYREC)                 
ADDLOOP  CLI   0(R6),X'00'         CHECK FOR LAST ELEMENT                       
         BE    XITMERD                                                          
         XC    DMCB(24),DMCB                                                    
         GOTO1 NBHELLO,DMCB,(C'P',UNTFIL),NBAIO,(R6)                            
         BAS   RE,NEXTEL                                                        
         B     ADDLOOP                                                          
XITMERD  B     XIT                                                              
*********************************************************************           
         EJECT                                                                  
************************* HIPO ****************************************         
*  NUMBER:                                                                      
*  TITLE: DELACTEL - DELETES ALL THE ACTUAL DEMO ELEMENTS AND THE               
*           NETVALUE ELEMENT FROM THE UNIT RECORD.                              
*           CURRENTLY (TEMPORARY?) THE ACTUAL DEMO ELEMENTS (43,45,             
*           5E) AND THE NETVALUE ELEMENT (71) ARE DELETED.                      
*           IDEALLY, THIS SHOULD BE HANDLED BY THE DEMO SYSTEM.                 
*                                                                               
* CALLED FROM : MERGEDUM - ANYTIME THE UNIT RECORD IS MODIFIED                  
*               STPREACT - IF THE NETVALUE ELEMENT HAS CHANGED                  
*                                                                     *         
*  INPUTS: NBAIO - ADDRESS OF UNIT RECORD                             *         
*                                                                               
*  LOCALS - R7 - POINTS TO NEW UNIT RECORD                                      
*           R6 - USED BY NEXTEL. POINTS TO CURRENT ELEMENT.                     
*                                                                     *         
*  GLOBAL OUTPUTS: NEW UNIT RECORD POINTED TO BY NBAIO                *         
*                  NBUPUNIT - SAYS THAT UNIT WAS UPDATED                        
*                                                                     *         
***********************************************************************         
DELACTEL NTR1                                                                   
*                                                                               
         L     R7,NBAIO                                                         
         USING NURECD,R7           UNIT RECORD                                  
         LA    R6,NUMAINEL         POINT TO FIRST ELEMENT                       
         MVI   SRCHEL,X'00'        SEARCH FOR ALL ELEMENTS                      
DANEXTEL BAS   RE,NEXTEL           GET NEXT ELEMENT                             
         BNE   DADODEL             IF LAST ELEMENT                              
         CLI   0(R6),X'40'         LOW ACTUAL DEMO RANGE                        
         BL    DANEXTEL                                                         
         CLI   0(R6),X'5D'         HIGH ACTUAL DEMO RANGE                       
         BL    DADELEL                                                          
         CLI   0(R6),X'5E'                                                      
         BE    DADELEL                                                          
         CLI   0(R6),X'71'         NET VALUE ELEMENT                            
         BE    DADELEL                                                          
         CLI   0(R6),X'DE'         OVERRRIDE ELEMENTS                           
         BNE   DANEXTEL                                                         
         TM    NUOVFLG-NUOVEL(R6),X'80' FROM FILE GO ALSO                       
         BO    DADELEL                                                          
         B     DANEXTEL                                                         
*                                                                               
DADELEL  MVI   0(R6),X'FF'         MARK ELEMENT FOR DELETION                    
         B     DANEXTEL                                                         
*                                       **** DELETE ALL ELS WITH X'FF'          
DADODEL  GOTO1 NBHELLO,DMCB,(C'D',UNTFIL),(X'FF',NBAIO),0                       
*                                                                               
XITDELA  B     XIT                                                              
         DROP  R7                                                               
*********************************************************************           
         EJECT                                                                  
************************* HIPO ****************************************         
*  TITLE: MERGEEST - MERGE DUMMY RECORD INTO THE UNIT RECORD AND                
*           TELL THE CONTROLLER THAT THE RECORD HAS BEEN CHANGED.               
*           THIS ONLY REPLACES THE ESTIMATED DEMO ELEMENTS (31,33,35,           
*           5D) AND THE NETVALUE ELEMENT (71) ARE DELETED AND ALL               
*           ELEMENTS IN DUMMY RECORD ARE ADDED.                                 
*           IDEALLY, THIS SHOULD BE HANDLED BY THE DEMO SYSTEM.                 
*                                                                               
*                                                                     *         
*  INPUTS: NBAIO - ADDRESS OF UNIT RECORD                             *         
*          DUMMYREC - RECORD TO MERGE                                           
*                                                                               
*  LOCALS - R7 - POINTS TO NEW UNIT RECORD                                      
*           R6 - USED BY NEXTEL. POINTS TO CURRENT ELEMENT.                     
*                                                                     *         
*  GLOBAL OUTPUTS: NEW UNIT RECORD POINTED TO BY NBAIO                *         
*                  NBUPUNIT - SAYS THAT UNIT WAS UPDATED                        
*                                                                     *         
***********************************************************************         
MERGEEST NTR1                                                                   
*                                                                               
         L     R7,NBAIO                                                         
         USING NURECD,R7           UNIT RECORD                                  
         LA    R6,NUMAINEL         POINT TO FIRST ELEMENT                       
         MVI   SRCHEL,X'00'        SEARCH FOR ALL ELEMENTS                      
MENEXTEL BAS   RE,NEXTEL           GET NEXT ELEMENT                             
         BNE   MEDODEL             IF LAST ELEMENT                              
         CLI   0(R6),X'30'         LOW EST. DEMO RANGE                          
         BL    MENEXTEL                                                         
         CLI   0(R6),X'40'         HIGH DEMO RANGE                              
         BL    MEDELEL                                                          
         CLI   0(R6),X'5D'         EVN BOOK ELEMENT                             
         BE    MEDELEL                                                          
         CLI   0(R6),X'71'         NET VALUE ELEMENT                            
         BE    MEDELEL                                                          
         B     MENEXTEL                                                         
*                                                                               
MEDELEL  MVI   0(R6),X'FF'         MARK ELEMENT FOR DELETION                    
         B     MENEXTEL                                                         
*                                       **** DELETE ALL ELS WITH X'FF'          
MEDODEL  GOTO1 NBHELLO,DMCB,(C'D',UNTFIL),(X'FF',NBAIO),0                       
*                                                                               
****  NOW ADD ELEMENTS FROM DUMMYREC ONE AT A TIME                              
         LA    R6,DUMMYREC+23      A(FIRST ELEMENT IN DUMMYREC)                 
AESLOOP  CLI   0(R6),X'00'         CHECK FOR LAST ELEMENT                       
         BE    XITMEST                                                          
         GOTO1 NBHELLO,DMCB,(C'P',UNTFIL),NBAIO,(R6)                            
         BAS   RE,NEXTEL                                                        
         B     AESLOOP                                                          
XITMEST  B     XIT                                                              
         DROP  R7                                                               
*********************************************************************           
         EJECT                                                                  
*                                                                               
************************* HIPO ****************************************         
*  NUMBER: NV1.8.9                                                              
*  TITLE: NEWNVEL                                                     *         
*                                                                     *         
*  COMMENTS: CREATES A NETVALUE ELEMENT AND APPENDS IT TO THE DUMMY             
*              RECORD. A SUBSEQUENT CALL TO DEMAND WILL MOVE THIS               
*              ELEMENT TO THE UNIT RECORD.                                      
*           IF CABLE: CREATES AN ACTUAL UNIVERSE RECORD BY COPYING THE          
*                 ESTIMATED UNIVERSE AND CHANGING THE ELEMENT CODE              
*                                                                               
*  CALLED FROM: DOACTNB CASE OF STPUTEM                               *         
*               DOBWAVVP CASE OF STPUTEM                              *         
*               DOESTVPH CASE OF STPUTEM                                        
*                                                                     *         
*  CALLS TO: HELLO - APPENDS ELEMENT TO DUMMYREC                      *         
*                                                                     *         
*  GLOBAL OUTPUTS: DUMMYREC - CONTAINS A NETVALUE ELEMENT             *         
*                                                                     *         
*  LOCAL OUTPUTS: DUMNVEL - DUMMY NETVALUE ELEMENT                    *         
*                                                                     *         
***********************************************************************         
         USING NUNVD,R7                                                         
NEWNVEL  NTR1                                                                   
*                                                                               
         MVI   NBUPUNIT,C'Y'       UNIT RECORD IS CHANGED                       
*                                                                               
         XC    DUMNVEL,DUMNVEL     CLEAR DUMMY ELEMENT                          
         LA    R7,DUMNVEL                                                       
         MVC   NUNVEL,NVELCODE     ELEMENT CODE                                 
         MVI   NUNVLEN,NUNVELLN    LENGTH OF ELEMENT                            
         MVC   NUNVRES,NBRESULT    RESULT                                       
*                                                                               
         CLI   HAVDAY,C'Y'         FORCE A RELOOKUP FOR NEXT TIME               
         BNE   *+8                 IF DAYLY DATA USED                           
         MVI   NUNVRES,C'D'                                                     
*                                                                               
         MVC   NUNVNRES,NBRESULN   NAD  RESULT                                  
         MVC   NUNVAFFT,NBAFFTIM   AFFADAVIT TIME                               
         MVC   NUNVUS12,NBUSER+12  FROM PROFILE                                 
         MVC   NUNVNTI,NBNTI       PROGRAM CODE                                 
         MVC   NUNVTIME,NBTIME     START,END TIME                               
         MVC   NUNVDATE,NBACTDAT   DATE                                         
         MVC   NUNVBLAK,BLAKLIST   LIST OF BLACK WEEKS                          
         MVC   NUNVBMSK,BLAKMASK   MASK OF BLACK WEEKS                          
         MVC   NUNVUST2,NBUNST2    UNIT STATUS 2                                
         MVC   NUNVPDT,NBPOSTDT    POST DATA TYPE                               
         MVC   NUNVUS23,SVPSTAFD   FROM PROFILE2 (POST AFFID)                   
         MVC   NUNVUS24,NBUSER2+4  FROM PROFILE2                                
         MVC   NUNVAFDT,NBSDAFDT   AFFID DATE                                   
         MVC   NUNVMIR,NBMIRTYP    MIRROR CODE                                  
         MVC   NUNVNDDF,NBNADDEF   NAD DEF ACTIVITY DATE                        
         MVC   NUNVTCAR,NBTCARLV   TCAR LEVEL                                   
*                                                                               
*                                  ADD ELEMENT TO DUMMYREC                      
         L     RF,NBHELLO                                                       
         GOTO1 (RF),DMCB,(C'p',PAVFIL),DUMMYREC,DUMNVEL,0,F'3000'               
         CLI   DMCB+12,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*        GOTO1 NBHELLO,DMCB,(C'P',PAVFIL),DUMMYREC,DUMNVEL,0                    
*                                                                               
         TM    NBUNST2,X'08'        IF CABLE DEMOS                              
         BZ    XITNNV                                                           
         XC    WORK,WORK                                                        
         L     R7,NBAIO            . GET EST UNIV ELEMENT                       
         USING NURECD,R7                                                        
         LA    R6,NUMAINEL                                                      
         MVI   SRCHEL,X'31'                                                     
         BAS   RE,NEXTEL                                                        
         BE    NVE2                                                             
         DC    H'0'                                                             
         USING NUEUD,R6             . GET ITS LENGTH                            
NVE2     ZIC   R2,NUEULEN                                                       
         BCTR  R2,0                 . DECREMENT LENGTH                          
         EXMVC R2,WORK,NUEUEL       .  AND MOVE IT TO WORK                      
*                                                                               
         LA    R6,WORK              . CHANGE IT TO ACT UNIV ELEMENT             
         MVI   NUEUEL,X'49'                                                     
*                                   . ADD IT TO DUMMYREC                        
*        GOTO1 NBHELLO,DMCB,(C'P',PAVFIL),DUMMYREC,WORK,0                       
         L     RF,NBHELLO                                                       
         GOTO1 (RF),DMCB,(C'p',PAVFIL),DUMMYREC,WORK,0,F'3000'                  
         CLI   DMCB+12,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
XITNNV   B     XIT                                                              
         DROP  R6                                                               
         DROP  R7                                                               
***********************************************************************         
         EJECT                                                                  
************************* HIPO ****************************************         
*  NUMBER: NV1.8.L.2                                                            
*  TITLE: GETPRGRC- READ THE PROGRAM FILE FOR THE SPECIFIED PROGRAM.            
*                                                                     *         
*  CALLED FROM: DOPROG CASE OF STPUTEM                                *         
*                                                                     *         
*  CALLS TO: DATAMGR                                                  *         
*                                                                     *         
*  INPUTS: UNITRECORD (IN NETBLOCK) - VALUES FOR KEY TO LOOK UP PRG REC         
*          NBMARKET - THE ACTUAL MARKET NUMBER                                  
*                                                                     *         
*  GLOBAL OUTPUTS: WORKREC - CONTAINS THE PROGRAM RECORD.             *         
*                                                                     *         
*                  CC=EQ - FOUND PROGRAM RECORD                       *         
*                  CC=NEQ - PROGRAM RECORD NOT FOUND                  *         
* ****************************************************************    *         
GETPRGRC NTR1                                                                   
         GOTO1 =A(XTROUTIN),DMCB,(0,(RC)),RR=RELO                               
*                                                                               
GETPRGX  B     XIT                                                              
***********************************************************************         
         EJECT                                                                  
************************* HIPO ****************************************         
*  NUMBER: 1.10.1                                                               
*  TITLE: CHEKNVEL - SEE IF NETVALUE ELEMENT HAS CHANGED                        
*                                                                               
*  COMMENTS: DECIDES WHETHER TO READ THE DEMO FILE OR USE THE ONES FROM         
*             THE ACTUAL ELEMENT.                                               
*                                                                               
*  CALLED FROM: STASSRES - STATE TO ASSIGN RESULT                               
*                                                                               
*  CALLS TO : NEXTEL - MACRO TO SEARCH FOR ELEMENTS                             
*                                                                               
*  INPUTS: R6 - THE ADDRESS OF THE NETVALUE ELEMENT                             
*          UNIT RECORD                                                          
*          BLAKLIST - LIST OF BLACK WEEKS FOR THIS UNIT                         
*          NETBLOCK - CONTAINS FIELDS OF THE UNIT RECORD                        
*                                                                               
*  GLOBAL OUTPUTS: NVELSTAT -  SET TO 'Y' IF NETVALUE HAS CHANGED               
*                                                ELSE 'N'                       
*                  SAVRESLT - THE RESULT FIELD OF THE NETVALUE ELEMENT          
*                                                                               
***********************************************************************         
CHEKNVEL NTR1                                                                   
         MVI   NVELSTAT,C'Y'       ASSUME CHANGED UNTIL PROVE OTHERWISE         
         USING NUNVD,R6            R6 POINTS TO NETVALUE ELEMENT                
         CLI   NBREROPT,0          OPTION TO RERATE ANYWAY                      
         BNE   XITCKNV                                                          
**********************************************************                      
****     CLC   SAVEBOOK,=X'5C30'   TEMP FIX FOR CABLE    *                      
****     BH    XITCKNV               FORCE A RERATE      *                      
**********************************************************                      
*COMPARE ELEMENT TO CURRENT VALUES. IF ANY CHANGED THEN EXIT.                   
         MVI   NVELEX,C'Y'         ELEMENT EXISTS                               
         MVC   SAVRESLT,NUNVRES    SAVE OLD NBRESULT                            
         MVC   SAVRESLTN,NUNVNRES   SAVE OLD NAD NBRESULT                       
         CLI   NBREVOPT,0          IF REVOPT IS SET                             
         BNE   XITCKNV             THEN RELOOK UP DEMOS                         
         CLC   NUNVAFFT,NBAFFTIM   AFFADAVIT TIMES                              
         BNE   XITCKNV                                                          
         CLC   NUNVAFDT,NBSDAFDT   AFFADAVIT DATE                               
         BNE   XITCKNV                                                          
         CLC   NUNVMIR,NBMIRTYP    MIRROR CODE                                  
         BNE   XITCKNV                                                          
         CLC   NUNVUS12,NBUSER+12  VALUE OF NBUSER+12                           
         BNE   XITCKNV                                                          
         CLC   NUNVNTI,NBNTI       PROGRAM NUMBER                               
         BNE   XITCKNV                                                          
         CLC   NUNVTIME,NBTIME     START-END TIME                               
         BNE   XITCKNV                                                          
         CLC   NUNVDATE,NBACTDAT   DATE                                         
         BNE   XITCKNV                                                          
         CLC   NUNVBLAK,BLAKLIST   BLACK-WEEKS                                  
         BNE   XITCKNV                                                          
         CLC   NUNVUST2,NBUNST2    UNIT STATUS 2 (FOR CABLE)                    
         BNE   XITCKNV                                                          
         CLC   NUNVPDT,NBPOSTDT    POSTING DATA TYPE                            
         BNE   XITCKNV                                                          
         CLC   NUNVUS23,SVPSTAFD   VALUE OF NBUSER2+3 (POST AFFID)              
         BNE   XITCKNV                                                          
         CLC   NUNVUS24,NBUSER2+4  VALUE OF NBUSER2+4                           
         BNE   XITCKNV                                                          
         CLC   NUNVNDDF,NBNADDEF   VALUE OF NAD DEF. ACT DATE                   
         BNE   XITCKNV                                                          
         CLC   NUNVTCAR,NBTCARLV   TCAR LEVEL                                   
         BNE   XITCKNV                                                          
         CLI   HAVNAD,C'Y'                                                      
         BNE   CHKNVELN                                                         
         B     XITCKNV             UNCONDITIONAL FOR NOW                        
***********************************************************************         
*NAD                                                                            
*        NEED TO CHECK NAD ELEMENTS HERE TO SEE IF LOOKUP HAS BEEN              
*        DONE. IF NOT CHECK IF BOOK IS IN YET.                                  
*                  NO - JUST EXIT                                               
*                 YES - IS NAD BOOK ON FILE                                     
*                         NO - EXIT                                             
*                        YES - SET ELEMENT CHANGED                              
*                                                                               
         CLI   NUNVNRES,C'D'       NUMBERS FROM DEMO FILE                       
         BE    CHKNVELN                                                         
         GOTO1 ASUBR,DMCB,('INITDBE',(RC))                                      
         MVC   DBFILE,=C'NAD'                                                   
         MVC   DBSELSTA+3(2),=C'PN' SET FOR NAD STATION                         
*                                                                               
** PXZ   CLI   NBPOSTYP,C'H'       SET HISPANIC IF REQ.                         
         CLI   NBSURVEY,C'H'       SET HISPANIC IF REQ.                         
         BNE   *+10                                                             
         MVC   DBSELSTA+3(2),=C' H'                                             
*                                                                               
         MVI   DBFUNCT,DBVLNBK                                                  
         GOTO1 NBDATCON,DMCB,(0,EBCDDAT),(3,DUB)                                
         MVC   DBSELBK+1(1),DUB+1  BOOK IS MONTH                                
         GOTO1 NBDEMAND,DMCB,DBLOCK,0  CHECK FOR BOOK ON FILE                   
         CLI   DBERROR,0           FOR IT - SO ALLOW LOOKUP                     
         BE    XITCKNV                                                          
*                                                                               
CHKNVELN MVI   NVELSTAT,C'N'       ELEMENT HAS NOT CHANGED                      
*                                                                               
XITCKNV  B     XIT                                                              
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                               
XIT      XIT1                                                                   
*                                                                               
         GETEL  R6,NBDTADSP,SRCHEL                                              
         EJECT                                                                  
****************************************************                            
*                                                                               
*******PARAMETERS                                                               
NVELCODE DC    X'71'               ELEMENT CODE FOR NV ELEMENT                  
*                                                                               
BITTBL   DC    X'80'               BIT POS 1 ON                                 
         DC    X'40'               BIT POS 2 ON                                 
         DC    X'20'               BIT POS 3 ON                                 
         DC    X'10'               BIT POS 4 ON                                 
*                                                                               
* HELLO FILE NAMES                                                              
*                                                                               
PAVFIL   DC    CL8'PAVFIL'                                                      
SPTFILE  DC    CL8'SPTFILE'                                                     
UNTFIL   DC    CL8'UNTFIL'                                                      
*                                                                               
         SPACE 4                                                                
* CHECK TO SEE IF DEMO GUARANTEE X'B4' ELEMENT MATCHES DMEO IN LIST             
* INPUT : R6 POINTS TO ELEMENT                                                  
*         BYTE HAS DEMO VALUE                                                   
*                                                                               
*OUTPUT : CC = IF DEMO MATCH                                                    
*         DMCB+12 HAS ADDRESS OF B4 ELEMENT                                     
*                                                                               
CHKC2    NTR1                                                                   
         LA    R2,1                                                             
*CC2      CLC   BYTE,4(R6)          DEMOS MATCH                                 
CC2      CLC   HALF+1(1),4(R6)          DEMOS MATCH                             
         BNE   CC3                                                              
         CLC   HALF(1),2(R6)        DEMOS MATCH                                 
         BE    CCYES                                                            
CC3      ZIC   R1,1(R6)                                                         
         AR    R6,R1                                                            
         CLI   0(R6),X'B4'                                                      
         BE    CC2                                                              
         B     *+6                                                              
CCYES    SR    R2,R2                                                            
         STCM  R6,15,DMCB+12                                                    
         LTR   R2,R2                                                            
         XIT1                                                                   
*                                                                               
         EJECT                                                                  
******* EXTERNALS                                                               
         LTORG                                                                  
*                                                                               
         EJECT                                                                  
ORDTBL   DS    0C                  TABLE TRANSLATES EBCDIC DIGITS TO            
*         ORDINAL VALUE (I.E. A=1, B=2 ETC.)                                    
*         USE ORDTBL-193 TO ADDRESS IT. VALUES OUTSIDE OF RANGE C1-E9           
*          WILL RETURN GARBAGE.                                                 
         DC    X'010203040506070809'   A-I EBCDIC C1-C9                         
         DC    7X'00'                  JUNK EBCDIC CA-D0                        
         DC    X'0A0B0C0D0E0F101112'   J-R  EBCDIC D1-D9                        
         DC    8X'00'                  JUNK EBCDIC DA-E1                        
         DC    X'131415161718191A'     S-Z  EBCDIC E2-E9                        
*                                                                               
         EJECT                                                                  
         LTORG                                                                  
*                                                                               
ADJEXPDM NTR1  BASE=*,LABEL=*      ADJUST EXPANDED DEMOS VALUES                 
         TM    NBSBKEND,NBEXPDEM   EXPANDED DEMO VALUE?                         
         BNO   ENDEXPD                                                          
*                                                                               
***      TM    NBSBKEND,NBEXPDM6   SEND FULL EXPANDED DEMO?                     
***      BO    ENDEXPD                                                          
*                                                                               
         L     R3,=F'1000'         DIVIDE BY 1000                               
         CLI   NBPOSTYP,C'N'       FOR NETWORK                                  
         BE    EXPD05                                                           
         CLI   NBPOSTYP,C'S'       AND SYND ONLY                                
         BE    EXPD05                                                           
*****    CLI   NBPOSTYP,C'H'       AND HISPANIC                                 
*****    BE    EXPD05                                                           
         L     R3,=F'100'          DIVIDE BY 100 FOR CABLE                      
EXPD05   SR    R0,R0                                                            
         L     R1,DOAIHOME                                                      
*** CHANGED THEIR MINDS - ROUND DON'T TRUNCATE                                  
         C     R3,=F'1000'                                                      
         BNE   EXPD07                                                           
         A     R1,=F'500'          ROUND FOR NET                                
         B     EXPD08                                                           
EXPD07   A     R1,=F'50'           ROUND FOR CABLE                              
***                                                                             
EXPD08   DR    R0,R3                                                            
         ST    R1,DOAIHOME                                                      
         SR    RE,RE                                                            
         ICM   RE,1,NUMDEMS                                                     
         BZ    ENDEXPD                                                          
         LA    RF,DOADMSET         DO REST OF DEMOS                             
EXPD10   L     R1,ALIMPOFF(RF)                                                  
         SR    R0,R0                                                            
*** CHANGED THEIR MINDS - ROUND DON'T TRUNCATE                                  
         C     R3,=F'1000'                                                      
         BNE   EXPD12                                                           
         A     R1,=F'500'          ROUND FOR NET                                
         B     EXPD14                                                           
EXPD12   A     R1,=F'50'           ROUND FOR CABLE                              
***                                                                             
EXPD14   DR    R0,R3                                                            
         ST    R1,ALIMPOFF(RF)                                                  
         LA    RF,ALLENGTH(RF)                                                  
         BCT   RE,EXPD10                                                        
ENDEXPD  XIT1                                                                   
*                                                                               
SETHOVR  NTR1  BASE=*,LABEL=*                                                   
         TM    NBINDS9,NBI9DOV     SET DEMO OVERRIDE INDICATOR?                 
         JZ    SETHOVRX                                                         
         TM    DOASHARE,X'80'                                                   
         JZ    *+8                                                              
         OI    0(R4),X'80'                                                      
         TM    DOAHUT,X'80'                                                     
         JZ    *+8                                                              
         OI    2(R4),X'80'                                                      
         TM    DOAVHOME,X'80'                                                   
         JZ    *+8                                                              
         OI    6(R4),X'80'                                                      
         TM    DOAGHOME,X'80'                                                   
         JZ    *+8                                                              
         OI    8(R4),X'80'                                                      
         TM    DOAIHOME,X'80'                                                   
         JZ    *+8                                                              
         OI    10(R4),X'80'                                                     
SETHOVRX XIT1                                                                   
         LTORG                                                                  
*                                                                               
* IS VTYPE ONE OF SPECIAL TYPES:A,C,P1,P2,P3                                    
CHKACP   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
*                                                                               
CHKACP0  DS    0H                                                               
         CLI   NBSURVEY,C'C'       CABLE?                                       
         BE    CHKACP00                                                         
         CLI   NBSURVEY,C'O'       OTHER = CABLE                                
         BNE   CHKACPN                                                          
CHKACP00 CLI   NBVTYPS,C'C'        .IF CABLE AND VTYPE=C                        
         BE    CHKACPX                                                          
         CLI   NBVTYPS,C'A'        .OR IF CABLE AND VTYPE=A                     
         BE    CHKACPX                                                          
         CLI   NBVTYPS,C'P'        .OR P1,P2,P3                                 
         BNE   CHKACPX                                                          
         CLI   NBVTYPS+1,C'1'                                                   
         BL    CHKACPN             NO                                           
         CLI   NBVTYPS+1,C'3'                                                   
         BH    CHKACPN             NO                                           
         SR    R1,R1                                                            
         LTR   R1,R1                                                            
         B     CHKACPX             YES                                          
CHKACPN  LTR   RE,RE               NO                                           
CHKACPX  XIT1                                                                   
         EJECT                                                                  
PRDFUDGE NTR1  BASE=*,LABEL=*                                                   
         CLI   WORK,0              TABLE OF USED CODES ?                        
         BNE   PRDF20                                                           
         LA    RE,WORK                                                          
         L     R1,NBAPROD                                                       
         USING NXBLK,R1                                                         
         LA    R1,NXPRDS                                                        
         USING NXPRDS,R1                                                        
         ZIC   R2,NBPRDNO                                                       
PRDF10   CLI   NXPCL1,0                                                         
         BE    *+14                                                             
         MVC   0(1,RE),NXPCL1                                                   
         LA    RE,1(RE)          BUMP WORK STORAGE AREA                         
         LA    R1,NXPPLENE(R1)   BUMP PROD STORAG AREA                          
         BCT   R2,PRDF10                                                        
PRDF20   LA    R1,1                                                             
PRDF21   LA    RE,WORK                                                          
PRDF22   CLC   0(1,R1),0(RE)                                                    
         BNE   *+12                                                             
         LA    R1,1(R1)                                                         
         B     PRDF21                                                           
         LA    RE,1(RE)                                                         
         CLI   0(RE),0           END OF TABLE OF CODES IN WORK                  
         BNE   PRDF22                                                           
         STC   R1,0(RE)            ADD TO WORK LIST                             
         STC   R1,BYTE             AND PASS BACK                                
         XIT1                                                                   
                                                                                
PRDFXIT  XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
GETPRD   NTR1  BASE=*,LABEL=*                                                   
         MVI   WORK,0              SET FLAG                                     
         L     R2,NBACLI           **IF USER DOESN'T PASS                       
         LTR   R2,R2               **CLIENT RECORD                              
         BNZ   GETPRD30                                                         
*  XYZ SHOULDN'T EVERYBODY GO THROUGH HERE ?                                    
*  NO - APPLICTION WILL HVE TO PASS CLIENT REC                                  
*     - WHEN AGENCY GOES TO OVERFLOW PRODS                                      
*     - DON'T WANT TO FORCE APPS TO PROVIDE SPACE                               
*     - TILL THEY NBEED TO                                                      
         L     R2,ACLIREC                                                       
         XC    KEY,KEY                                                          
         MVC   KEY+1(3),NBACTAM    AM/CLI                                       
         CLC   0(13,R2),KEY        DO WE ALREADY HAVE IT?                       
         BE    GETPRD30                                                         
         GOTO1 NBDM,DMCB,=C'DMREAD ',=C'SPTDIR  ',KEY,KEY,0                     
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         LA    R3,KEY+14                                                        
***      LA    R4,DMWORK                                                        
***      GOTO1 NBDM,DMCB,=C'GETREC ',=C'SPTFIL  ',(R3),(R2),(R4)                
* GETREC WITHOUT USING DMWORK                                                   
         GOTO1 NBDM,DMCB,=C'DMRDIR',=C'SPTFIL  ',(R3),(R2)                      
GETPRD30 DS    0H                                                               
         USING CLTHDR,R2                                                        
         LA    RE,CLIST                                                         
         LA    RF,220                                                           
         SPACE 1                                                                
GETPRD32 CLC   BYTE,3(RE)                                                       
         BE    GETPRD50                                                         
         CLI   3(RE),0             END?                                         
         BE    GETPRD40                                                         
         LA    RE,4(RE)                                                         
         BCT   RF,GETPRD32                                                      
         CLI   WORK,2                                                           
         BE    GETPRD40                                                         
         MVI   WORK,2                                                           
         L     RE,NBACLI                                                        
         LA    RE,CLIST2           EXTENDED LIST                                
         LA    RF,35                                                            
         B     GETPRD32                                                         
GETPRD40 MVC   WORK(3),=C'UNA'                                                  
         B     *+10                                                             
GETPRD50 MVC   WORK(3),0(RE)          3-BYTE PRODUCT                            
GETPRDX  XIT1                                                                   
         DROP  R2                                                               
         LTORG                                                                  
         EJECT                                                                  
* RETURN 1 BYTE PROD CODE                                                       
* WORK HAS 3 CHAR CODE - RETURNS 1 BYTE IN BYTE                                 
GETPRD2  NTR1  BASE=*,LABEL=*                                                   
         MVI   WORK+3,0            SET FLAG                                     
         L     R2,NBACLI           **IF USER DOESN'T PASS                       
         LTR   R2,R2               **CLIENT RECORD                              
         BNZ   GETP2R30                                                         
*  XYZ SHOULDN'T EVERYBODY GO THROUGH HERE ?                                    
*  NO - APPLICTION WILL HVE TO PASS CLIENT REC                                  
*     - WHEN AGENCY GOES TO OVERFLOW PRODS                                      
*     - DON'T WANT TO FORCE APPS TO PROVIDE SPACE                               
*     - TILL THEY NBEED TO                                                      
         L     R2,ACLIREC                                                       
         XC    KEY,KEY                                                          
         MVC   KEY+1(3),NBACTAM    AM/CLI                                       
         CLC   0(13,R2),KEY        DO WE ALREADY HAVE IT?                       
         BE    GETP2R30                                                         
         GOTO1 NBDM,DMCB,=C'DMREAD ',=C'SPTDIR  ',KEY,KEY,0                     
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         LA    R3,KEY+14                                                        
****     LA    R4,DMWORK                                                        
****     GOTO1 NBDM,DMCB,=C'GETREC ',=C'SPTFIL  ',(R3),(R2),(R4)                
         GOTO1 NBDM,DMCB,=C'DMRDIR',=C'SPTFIL  ',(R3),(R2)                      
GETP2R30 DS    0H                                                               
         USING CLTHDR,R2                                                        
         LA    RE,CLIST                                                         
         LA    RF,220                                                           
         SPACE 1                                                                
GETP2R32 CLC   WORK(3),0(RE)                                                    
         BE    GETP2R50                                                         
         CLI   3(RE),0             END?                                         
         BE    GETP2R40                                                         
         LA    RE,4(RE)                                                         
         BCT   RF,GETP2R32                                                      
         CLI   WORK+3,2                                                         
         BE    GETP2R40                                                         
         MVI   WORK+3,2                                                         
         L     RE,NBACLI                                                        
         LA    RE,CLIST2           EXTENDED LIST                                
         LA    RF,35                                                            
         B     GETP2R32                                                         
GETP2R40 MVI   BYTE,0                                                           
         B     *+10                                                             
GETP2R50 MVC   BYTE,3(RE)          1-BYTE PRODUCT                               
GETP2RX  XIT1                                                                   
         DROP  R2                                                               
         LTORG                                                                  
         EJECT                                                                  
ADJ2DEC  NTR1  BASE=*,LABEL=*                                                   
         TM    NBINDS3,NBI3A2DC    2 DEC AGY?                                   
         BNO   XITADJD                                                          
         TM    NBINDS3,NBI3B2DC    NEW BOOK                                     
         BO    XITADJD                                                          
         CLI   NBPOSTYP,C'N'        OLD BOOK/NETWORK OR SYND                    
         BE    *+12                                                             
         CLI   NBPOSTYP,C'S'                                                    
         BNE   XITADJD                                                          
         SR    R0,R0                                                            
         L     R1,DOAGHOME                                                      
         M     R0,=F'10'                                                        
         ST    R1,DOAGHOME                                                      
         CLI   NBHUNOPT,C'Y'       IF HUNOPT SET                                
         BE    ADJDEM4                                                          
         L     R1,DOAIHOME         DON'T ADJ IMPS                               
         M     R0,=F'10'                                                        
         ST    R1,DOAIHOME                                                      
ADJDEM4  LA    R2,25               HARD CODED MAX DEMO NUMBER                   
         LA    R3,DOADMSET                                                      
ADJDEM5  L     R1,4(R3)                                                         
         M     R0,=F'10'                                                        
         ST    R1,4(R3)                                                         
         CLI   NBHUNOPT,C'Y'                                                    
         BE    ADJDEM6                                                          
         L     R1,8(R3)            DON'T ADJ IMPS                               
         M     R0,=F'10'                                                        
         ST    R1,8(R3)                                                         
ADJDEM6  LA    R3,12(R3)                                                        
         BCT   R2,ADJDEM5                                                       
XITADJD  XIT1                                                                   
                                                                                
* ROUND IMPS AND RTG FOR 2 DEC AGY                                              
ADJ2RNDA NTR1  BASE=*,LABEL=*                                                   
         TM    NBINDS3,NBI3A2DC    2 DEC AGY?                                   
         BNO   XITADJRA                                                         
         CLI   NBSURVEY,C'N'        NETWORK OR SYND                             
         BE    ADJ2R10                                                          
         CLI   NBSURVEY,C'S'                                                    
         BE    ADJ2R10                                                          
         CLI   NBSURVEY,C'H'                                                    
         BE    ADJ2R10                                                          
         BNE   XITADJRA                                                         
ADJ2R10  SR    R0,R0                                                            
         L     R1,DOAGHOME                                                      
         LA    R1,5(R1)                                                         
         D     R0,=F'10'                                                        
         SR    R0,R0                                                            
         M     R0,=F'10'                                                        
         ST    R1,DOAGHOME                                                      
****     CLI   NBHUNOPT,C'Y'       IF HUNOPT SET                                
****     BE    ADJRNA4                                                          
         L     R1,DOAIHOME         DON'T ADJ IMPS                               
         LA    R1,50(R1)            ADD 5                                       
         D     R0,=F'100'                                                       
         SR    R0,R0                                                            
         M     R0,=F'100'                                                       
         ST    R1,DOAIHOME                                                      
ADJRNA4  LA    R2,25               HARD CODED MAX DEMO NUMBER                   
         LA    R3,DOADMSET                                                      
ADJRNA5  L     R1,4(R3)                                                         
         LA    R1,5(R1)                                                         
         D     R0,=F'10'                                                        
         SR    R0,R0                                                            
         M     R0,=F'10'                                                        
         ST    R1,4(R3)                                                         
***      CLI   NBHUNOPT,C'Y'                                                    
***      BE    ADJRNA6                                                          
         L     R1,8(R3)            DON'T ADJ IMPS                               
         LA    R1,50(R1)                                                        
         D     R0,=F'100'                                                       
         SR    R0,R0                                                            
         M     R0,=F'100'                                                       
         ST    R1,8(R3)                                                         
ADJRNA6  LA    R3,12(R3)                                                        
         BCT   R2,ADJRNA5                                                       
XITADJRA XIT1                                                                   
*                                                                               
                                                                                
* ROUND ACTUAL IMPRESSIONS FOR 2 DEC AGENCY                                     
ADJRNDI  NTR1  BASE=*,LABEL=*                                                   
         TM    NBINDS3,NBI3A2DC    2 DEC AGY?                                   
         BNO   XITRNDI                                                          
         SR    R0,R0                                                            
****     CLI   NBHUNOPT,C'Y'      WILL I NEED THIS?                             
****     BE    ADJRNI4                                                          
         SR    R0,R0                                                            
         L     R1,DOAIHOME        ROUND IMPRESSION                              
         LA    R1,5(R1)                                                         
         D     R0,=F'10'                                                        
         SR    R0,R0                                                            
         M     R0,=F'10'                                                        
         ST    R1,DOAIHOME                                                      
ADJRNI4  LA    R2,25               HARD CODED MAX DEMO NUMBER                   
         LA    R3,DOADMSET                                                      
ADJRNI5  DS    0H                                                               
***      CLI   NBHUNOPT,C'Y'                                                    
***      BE    ADJRNI6                                                          
         L     R1,8(R3)            DON'T ADJ IMPS                               
         SR    R0,R0                                                            
         LA    R1,5(R1)                                                         
         D     R0,=F'10'                                                        
         SR    R0,R0                                                            
         M     R0,=F'10'                                                        
         ST    R1,8(R3)                                                         
ADJRNI6  LA    R3,12(R3)                                                        
         BCT   R2,ADJRNI5                                                       
XITRNDI  XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
**********************************************************                      
************                                                                    
* ADJUST IMPRESSIONS - ADJUSTS IMPRSSIONS BY MULTER/DIVSOR                      
*     DOES NOT ADJUST IF OVERRIDE ELEMENT EXISTS FOR THE IMPRESSION             
*     HALF CONTROLS WHAT TYPE OF ADJUSTMENT                                     
*       E= EQUIVALENCE, Y = ADJUST OVERRIDES                                    
*   INPUTS: MULTER,DIVSOR                                                       
*           OVELCODE - APPLICABLE OVERRIDE ELEMENT NUMBER                       
*                                                                               
         EJECT                                                                  
*--OVERFLOW ROUTINES                                                            
*                                                                               
         DS    0F                                                               
ADJUST   NMOD1 0,**ADJST,R9,R8                                                  
         L     RC,0(R1)            REESTABLISH WORKING STORAGE                  
*                                                                               
         ZIC   RF,0(R1)                                                         
         SLL   RF,2                                                             
         B     OVBRANCH(RF)                                                     
*                                                                               
OVBRANCH B     ADJI                ADJUST IMPRESSIONS                           
         B     ADJG                ADJUST GRPS                                  
         EJECT                                                                  
*-- STORE BUY DATE OF UNIT IN WORK                                              
ADJI     XC    KEYSAV2,KEYSAV2                                                  
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNTFIL  '),(X'99',NBAIO),0  TEMP           
         CLI   DMCB+12,0                                 TEMP                   
         BE    *+6                                    TEMP                      
         DC    H'0'                                TEMP                         
         L     R5,DMCB+12                       TEMP                            
         MVC   WORK(3),4(R5)                 TEMP                               
         TM    NBUNITST,X'01'      TEMP              IF MAKEGOOD                
         BNO   ADJI10                                                           
*                                                   GET MADE GOOD UNIT          
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNTFIL  '),(X'06',NBAIO),0                 
AIS50    CLI   DMCB+12,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         L     R5,DMCB+12                                                       
         USING NUMGD,R5                                                         
         XC    KEY,KEY                                                          
         LA    R4,KEY                                                           
         USING NUKPKEY,R4                                                       
         MVI   NUKPTYPE,X'84'                                                   
         MVC   NUKPAM,NBACTAM                                                   
         MVC   NUKPCLT,NBACTCLI                                                 
         MVC   NUKPNET,NBACTNET                                                 
         MVC   NUKPPROG,NUMGPCOD                                                
         MVC   NUKPDATE,NUMGDATE                                                
         MVC   NUKPEST,NBACTEST                                                 
         MVC   NUKPSUB,NUMGSUB                                                  
         MVC   NUKPDP,NBACTDP                                                   
*-..IF A UNIT IS MADE GOOD BY ITSELF WE GO INTO A LOOP                          
         CLC   KEYSAV2,KEY                                                      
         BE    AISDOIT                                                          
         MVC   KEYSAV2,KEY                                                      
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         XC    DMCB(24),DMCB                                                    
         GOTO1 NBDM,DMCB,=C'DMRDHI',=C'UNTDIR',KEY,KEY                          
         CLC   KEY(20),KEYSAVE                                                  
**-->    BE    *+6                                                              
**-->    DC    H'0'                                                             
*        BNE   ADJI10     MAKE/MADE GOOD TANGLE LET CLIENT HANDLE IT            
         BNE   ADJI05     CHECK IF CALLED BY XTRACT                             
*                                                                               
         LA    R4,KEY+21                                                        
         L     R5,AWORKREC                                                      
         XC    DMCB(24),DMCB                                                    
         XC    DMWORK(96),DMWORK                                                
         GOTO1 NBDM,DMCB,(0,=C'GETREC'),=CL8'UNTFIL',(R4),(R5),DMWORK           
         L     R4,AWORKREC                                                      
         USING NURECD,R4                                                        
         CLI   0(R4),0                                                          
         BNE   *+6                                                              
         DC    H'0'                                                             
         TM    NUUNITST,X'01'      ANOTHER MAKEGOOD/NOW MISSED                  
         BNO   AISDOIT                                                          
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNTFIL  '),(X'06',(R5)),0                  
         B     AIS50                                                            
*                                                                               
AISDOIT  DS    0H                                                               
         GOTO1 NBDM,DMCB,=C'DMRDHI',=C'UNTDIR',NBKEY,NBKEY                      
         L     R5,AWORKREC                                                      
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNTFIL  '),(X'99',(R5)),0                  
         CLI   DMCB+12,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         L     R5,DMCB+12                                                       
         MVC   WORK(3),4(R5)                                                    
         DROP  R4,R5                                                            
*                                                                               
ADJI05   TM    NBINDS7,NBI7XTCT    IF FROM XTRACT                               
         BZ    ADJI10              NO                                           
         GOTO1 NBDM,DMCB,=C'DMRDHI',=C'UNTDIR',NBKEY,NBKEY                      
         B     XITX                                                             
*                                                                               
ADJI10   L     R1,DIVSOR                                                        
         MH    R1,=H'10'           *8/8/89                                      
         ST    R1,DIVSOR                                                        
         SRL   R1,1                DIVSOR/2 IS ROUNDING FACTOR                  
         ST    R1,ADDER                                                         
*                                 CONVERT FOR PACKED DECIMAL                    
         L     R5,MULTER                                                        
         CVD   R5,DUB                                                           
         ZAP   PMULTER,DUB                                                      
         L     R5,ADDER                                                         
         CVD   R5,DUB                                                           
         ZAP   PADDER,DUB                                                       
         L     R5,DIVSOR                                                        
         CVD   R5,DUB                                                           
         ZAP   PDIVSOR,DUB                                                      
*                                  DO FOR HOMES IMP                             
         CLI   HALF,C'Y'                                                        
         BE    AIS101                                                           
         CLI   HALF,C'E'                                                        
         BE    AIS100                                                           
         XC    DUB,DUB                                                          
         MVC   DUB+2(2),DIAIHOME+1                                              
         BAS   RE,HELLOW                                                        
         CLI   DMCB+12,0           DONT ADJUST IF OVERRIDDEN                    
         BE    AISOTHER                                                         
         B     AIS101                                                           
         SPACE 1                                                                
AIS100   CLI   NBN2B1,0            IF RATINGS ARE EQUIVALENCED                  
         BE    AIS101                                                           
         CLI   IFDEM,C'N'          FROM PACKAGE GUARANTEE                       
         BE    *+12                                                             
         TM    NBINDS,X'80'        OPTION TO EQUIVALENCE OVERRIDES              
         BO    AIS101                                                           
         XC    DUB,DUB                                                          
         MVI   DUB+2,C'R'          WHEN ADJUSTING FOR EQUIVALENCY               
         MVI   DUB+3,1             CHECK FOR HOMES RATING O/R                   
         BAS   RE,HELLOW                                                        
         CLI   DMCB+12,0           DONT ADJUST IF OVERRIDDEN                    
         BE    AISOTHER                                                         
         SPACE 1                                                                
AIS101   CLI   IFDEM,C'N'          FOR PACKAGE GUARANTEES                       
         BNE   AIS102                                                           
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNTFIL  '),(X'B4',NBAIO),0                 
         CLI   DMCB+12,0           DONT ADJUST IF DEMO GUARANTEE FACTOR         
         BNE   AIS102                                                           
         L     R6,DMCB+12                                                       
***      MVC   BYTE,DIAIHOME+2                                                  
         MVC   HALF(1),DIAIHOME                                                 
         MVC   HALF+1(1),DIAIHOME+2                                             
         BAS   RE,CHKC22                                                        
         BE    AISOTHER                                                         
         SPACE 1                                                                
*!!!AIS102   L     R5,DOAIHOME         DO FOR HOMES IMP                         
AIS102   MVC   FULL,DOAIHOME       DO FOR HOMES IMP                             
         NI    FULL,X'FF'-X'80'    TURN OFF OVERRIDE INDICATOR                  
         L     R5,FULL                                                          
                                                                                
         C     R5,=F'0'                                                         
         BE    AIS103                                                           
         CVD   R5,DUB                                                           
         ZAP   PWORK,DUB                                                        
         MP    PWORK,PMULTER                                                    
*** PXZ  CLI   NBSURVEY,C'C'                                                    
         CLI   NBPOSTYP,C'C'                                                    
         BE    DOMULT                                                           
         CLC   WORK(3),=XL3'5B060E'   ******TEMP*******                         
         BL    *+10                   ******TEMP*******                         
DOMULT   MP    PWORK,=P'10'        *8/8/89                                      
         AP    PWORK,PADDER                                                     
         DP    PWORK,PDIVSOR                                                    
         ZAP   DUB,PWORK(8)                                                     
*** PXZ  CLI   NBSURVEY,C'C'       CABLE IMPRESSIONS                            
         CLI   NBPOSTYP,C'C'       CABLE IMPRESSIONS                            
         BE    SKIPMULT                                                         
         CLC   WORK(3),=XL3'5B060E'   ******TEMP*******                         
         BNL   *+10                   ******TEMP*******                         
         MP    DUB,=P'10'             ******TEMP*******                         
SKIPMULT CVB   R5,DUB                                                           
AIS103   ST    R5,FULL                                                          
         TM    NBINDS9,NBI9DOV     DEMO OVERRIDES INDICATOR                     
         JZ    AIS104                                                           
         TM    DOAIHOME,X'80'  OVERRIDE?                                        
         JZ    *+8                                                              
         OI    FULL,X'80'          TURN INDICATOR BACK ON                       
AIS104   MVC   DOAIHOME(4),FULL                                                 
*!!!AIS103   ST    R5,DOAIHOME                                                  
*                                                                               
AISOTHER LA    R2,DOADMSET         FIRST SET OF DEMOS                           
         SR    R7,R7                                                            
         ICM   R7,1,NUMDEMS        SETS CONDITION CODE ALSO                     
         BZ    XITIMPRS            NO MORE DEMOS REQUESTED                      
         LA    R6,DIADMSET+6       DEMO CODE FOR IMPRESSION                     
         L     RF,NBADEM                                                        
         ST    RF,ACURDEMO                                                      
*                                                                               
AISLOOP  L     RF,ACURDEMO                                                      
*        CLI   2(RF),0             COMSCORE?                                    
*        JE    AISNEXT                                                          
         CLI   IFDEM,C'N'          FOR PACKAGE GUARANTEES                       
         BNE   AISLOOP2                                                         
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNTFIL  '),(X'B4',NBAIO),0                 
         CLI   DMCB+12,0           DONT ADJUST IF DEMO GUARANTEE                
         BNE   AISLOOP2                                                         
***      MVC   BYTE,2(R6)                                                       
         MVC   HALF(1),0(R6)                                                    
         MVC   HALF+1(1),2(R6)                                                  
         LR    R1,R6               SAVE R6                                      
         L     R6,DMCB+12                                                       
         BAS   RE,CHKC22                                                        
         LR    R6,R1               RESTORE R6                                   
         BE    AISNEXT                                                          
         SPACE 1                                                                
AISLOOP2 CLI   HALF,C'E'           EQUIVALENCE USERS ALSO                       
         BE    *+12                 AS PER LESLIE - 88/06/09                    
         CLI   1(R6),X'21'         DONT ADJUST IF USER DEMO                     
         BE    AISNEXT                                                          
         CLI   HALF,C'Y'                                                        
         BE    AIS200                                                           
         CLI   HALF,C'E'                                                        
         BE    AISLOOP3                                                         
         XC    DUB,DUB                                                          
         MVC   DUB+1(3),0(R6)                                                   
         BAS   RE,HELLOW                                                        
         CLI   DMCB+12,0           DONT ADJUST IF OVERRIDDEN                    
         BE    AISNEXT                                                          
         B     AIS200                                                           
         SPACE 1                                                                
AISLOOP3 CLI   NBN2B1,0            IF RATINGS ARE EQUIVALENCED,                 
         BE    AISLOOP4                                                         
         CLI   IFDEM,C'N'          FROM PACKAGE GUARANTEE                       
         BE    *+12                                                             
         TM    NBINDS,X'80'        OPTION TO EQUIV OVERRIDES                    
         BO    AIS200                                                           
         XC    DUB,DUB                                                          
         MVI   DUB+2,C'R'          WHEN ADJUSTING FOR EQUIVALENCY               
         MVI   DUB+3,1             CHECK FOR HOMES RATING O/R                   
         BAS   RE,HELLOW                                                        
         CLI   DMCB+12,0           DONT ADJUST IF OVERRIDDEN                    
         BE    AISNEXT                                                          
         SPACE 1                                                                
AISLOOP4 CLI   NBN2B1,0            IF RATINGS ARE EQUIVALENCED,                 
         BE    AIS200                                                           
         XC    DUB,DUB                                                          
         MVC   DUB+1(3),0(R6)      CHECK FOR TARGET RATING 0/R                  
         MVI   DUB+2,C'R'          WHEN ADJUSTING FOR EQUIVALENCY               
         BAS   RE,HELLOW                                                        
         CLI   DMCB+12,0           DONT ADJUST IF OVERRIDDEN                    
         BE    AISNEXT                                                          
         SPACE 1                                                                
*!!!AIS200   L     R5,ALIMPOFF(R2)                                              
AIS200   MVC   FULL,ALIMPOFF(R2)                                                
         NI    FULL,X'FF'-X'80'    TURN OFF OVERRIDE INDICATOR                  
         L     R5,FULL                                                          
                                                                                
         C     R5,=F'0'                                                         
         BE    AIS220                                                           
         CVD   R5,DUB                                                           
         ZAP   PWORK,DUB                                                        
         MP    PWORK,PMULTER                                                    
*** PXZ  CLI   NBSURVEY,C'C'       CABLE IMPRESSIONS                            
         CLI   NBPOSTYP,C'C'       CABLE IMPRESSIONS                            
         BE    DOMULT2                                                          
         CLC   WORK(3),=XL3'5B060E'   ******TEMP*******                         
         BL    *+10                   ******TEMP*******                         
DOMULT2  MP    PWORK,=P'10'        *8/8/89                                      
         AP    PWORK,PADDER                                                     
         DP    PWORK,PDIVSOR                                                    
         ZAP   DUB,PWORK(8)                                                     
*** PXZ  CLI   NBSURVEY,C'C'       CABLE IMPRESSIONS                            
         CLI   NBPOSTYP,C'C'       CABLE IMPRESSIONS                            
         BE    SKIPM2                                                           
         CLC   WORK(3),=XL3'5B060E'   ******TEMP*******                         
         BNL   *+10                   ******TEMP*******                         
         MP    DUB,=P'10'             ******TEMP*******                         
SKIPM2   CVB   R5,DUB                                                           
*        CLI   PAKADJ,C'Y'         IF PKG GUAR ADJ                              
*        BNE   AIS220                                                           
*        LA    R5,50(R5)            ROUND TO NEAREST TENTH                      
*        SR    R4,R4                                                            
*        D     R4,=F'100'                                                       
*        M     R4,=F'100'                                                       
AIS220   ST    R5,FULL                                                          
         TM    NBINDS9,NBI9DOV     DEMO OVERRIDES INDICATOR                     
         JZ    AIS225                                                           
         TM    ALIMPOFF(R2),X'80'  OVERRIDE?                                    
         JZ    *+8                                                              
         OI    FULL,X'80'          TURN INDICATOR BACK ON                       
AIS225   MVC   ALIMPOFF(4,R2),FULL                                              
*!!! AIS220   ST    R5,ALIMPOFF(R2)                                             
         SPACE 1                                                                
AISNEXT  LA    R2,ALLENGTH(R2)                                                  
         LA    R6,9(R6)            NEXT IMPRESSION DEMO CODE                    
         L     RF,ACURDEMO                                                      
         AHI   RF,3                                                             
         ST    RF,ACURDEMO                                                      
         BCT   R7,AISLOOP                                                       
*--READJUST DIVISOR AND ADDER 8/8/89                                            
XITIMPRS SR    R4,R4                                                            
         L     R5,DIVSOR                                                        
         D     R4,=F'10'                                                        
         ST    R5,DIVSOR                                                        
         SRL   R5,1                DIVSOR/2 IS ROUNDING FACTOR                  
         ST    R5,ADDER                                                         
XITX     XIT1                                                                   
         EJECT                                                                  
* ADJUST GRPS- ADJUSTS GRPS BY MULTER/DIVSOR                                    
*     DOES NOT ADJUST IF OVERRIDE ELEMENT EXISTS FOR THE GRP                    
*   INPUTS: MULTER,DIVSOR                                                       
*           OVELCODE - APPLICABLE OVERRIDE ELEMENT NUMBER                       
ADJG     L     R1,DIVSOR                                                        
         SRL   R1,1                DIVSOR/2 IS ROUNDING FACTOR                  
         ST    R1,ADDER                                                         
*                                                                               
         L     R5,MULTER                                                        
         CVD   R5,DUB                                                           
         ZAP   PMULTER,DUB                                                      
         L     R5,ADDER                                                         
         CVD   R5,DUB                                                           
         ZAP   PADDER,DUB                                                       
         L     R5,DIVSOR                                                        
         CVD   R5,DUB                                                           
         ZAP   PDIVSOR,DUB                                                      
*                                  DO FOR HOMES GRP                             
         CLI   IFDEM,C'N'          FROM PACKAGE GUARANTEE                       
         BE    *+12                                                             
         TM    NBINDS,X'80'        OPTION TO EQUIV OVERRIDES                    
         BO    ADJGRPS1                                                         
         XC    DUB,DUB                                                          
         MVC   DUB+2(2),DIAGHOME+1                                              
         BAS   RE,HELLOW                                                        
         CLI   DMCB+12,0           DONT ADJUST IF OVERRIDDEN                    
         BE    AGROTHER                                                         
         SPACE 1                                                                
ADJGRPS1 CLI   IFDEM,C'N'          FOR PACKAGE GUARANTEES                       
         BNE   ADJGRPS2                                                         
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNTFIL  '),(X'B4',NBAIO),0                 
         CLI   DMCB+12,0           DEMO GUARANTEE FACTOR                        
         BNE   ADJGRPS2            NO/ADJUST                                    
         L     R6,DMCB+12                                                       
***      MVC   BYTE,DIAIHOME+2                                                  
         MVC   HALF(1),DIAIHOME                                                 
         MVC   HALF+1(1),DIAIHOME+2                                             
         BAS   RE,CHKC22           CHECK DEMO MATCH                             
         BE   AGROTHER                                                          
*                                                                               
*!!!ADJGRPS2 L     R5,DOAGHOME         DO FOR HOMES GRP                         
ADJGRPS2 MVC   FULL,DOAGHOME       DO FOR HOMES GRP                             
         NI    FULL,X'FF'-X'80'    TURN OFF OVERRIDE INDICATOR                  
         L     R5,FULL                                                          
                                                                                
         CVD   R5,DUB                                                           
         ZAP   PWORK,DUB                                                        
         MP    PWORK,PMULTER                                                    
         AP    PWORK,PADDER                                                     
         DP    PWORK,PDIVSOR                                                    
         ZAP   DUB,PWORK(8)                                                     
         CVB   R5,DUB                                                           
*!!!     ST    R5,DOAGHOME                                                      
         ST    R5,FULL                                                          
         TM    NBINDS9,NBI9DOV     DEMO OVERRIDES INDICATOR                     
         JZ    *+16                                                             
         TM    DOAGHOME,X'80'      OVERRIDE?                                    
         JZ    *+8                                                              
         OI    FULL,X'80'          TURN INDICATOR BACK ON                       
         MVC   DOAGHOME(4),FULL                                                 
*                                                                               
AGROTHER LA    R2,DOADMSET         FIRST SET OF DEMOS                           
         SR    R7,R7                                                            
         ICM   R7,1,NUMDEMS        SETS CONDITION CODE ALSO                     
         BZ    XITGRP              NO MORE DEMOS REQUESTED                      
         LA    R6,DIADMSET+3       DEMO CODE FOR GRP                            
         SPACE 1                                                                
AGRLOOP  CLI   IFDEM,C'N'          FOR PACKAGE GUARANTEES                       
         BNE   AGRLOOP2                                                         
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNTFIL  '),(X'B4',NBAIO),0                 
         CLI   DMCB+12,0           DONT ADJUST IF DEMO GUARANTEE                
         BNE   AGRLOOP2                                                         
***      MVC   BYTE,2(R6)          CHK DEMO MATCH                               
         MVC   HALF(1),0(R6)          CHK DEMO MATCH                            
         MVC   HALF+1(1),2(R6)          CHK DEMO MATCH                          
         LR    R1,R6               SAVE R6                                      
         L     R6,DMCB+12                                                       
         BAS   RE,CHKC22                                                        
         LR    R6,R1               RESTORE R6                                   
         BE    AGRNEXT                                                          
         SPACE 1                                                                
AGRLOOP2 CLI   IFDEM,C'N'          FROM PACKAGE GUARANTEE                       
         BE    *+12                                                             
         TM    NBINDS,X'80'        OPTION TO EQUIV OVERRIDES                    
         BO    AGRLOOP4                                                         
         XC    DUB,DUB                                                          
         MVC   DUB+1(3),0(R6)                                                   
         BAS   RE,HELLOW                                                        
         CLI   DMCB+12,0           DONT ADJUST IF OVERRIDDEN                    
         BE    AGRNEXT                                                          
         SPACE 1                                                                
AGRLOOP4 CLI   HALF,C'E'           EQUIVALENCE USERS ALSO                       
         BE    *+12                 AS PER LESLIE 88/06/09                      
         CLI   1(R6),X'21'         DONT ADJUST IF USER DEMO                     
         BE    AGRNEXT                                                          
*!!!     L     R5,ALGRPOFF(R2)                                                  
         MVC   FULL,ALGRPOFF(R2)                                                
         NI    FULL,X'FF'-X'80'    TURN OFF OVERRIDE INDICATOR                  
         L     R5,FULL                                                          
                                                                                
         CVD   R5,DUB                                                           
         ZAP   PWORK,DUB                                                        
         MP    PWORK,PMULTER                                                    
         AP    PWORK,PADDER                                                     
         DP    PWORK,PDIVSOR                                                    
         ZAP   DUB,PWORK(8)                                                     
         CVB   R5,DUB                                                           
*!!!     ST    R5,ALGRPOFF(R2)                                                  
         ST    R5,FULL                                                          
         TM    NBINDS9,NBI9DOV     DEMO OVERRIDES INDICATOR                     
         JZ    *+16                                                             
         TM    ALGRPOFF(R2),X'80'  OVERRIDE?                                    
         JZ    *+8                                                              
         OI    FULL,X'80'          TURN INDICATOR BACK ON                       
         MVC   ALGRPOFF(4,R2),FULL                                              
         SPACE 1                                                                
AGRNEXT  LA    R2,ALLENGTH(R2)                                                  
         LA    R6,9(R6)            NEXT GRP DEMO CODE                           
         BCT   R7,AGRLOOP                                                       
*                                                                               
XITGRP   XIT1                                                                   
*                                                                               
*************************************************************                   
*                                                                               
*                                                                               
HELLOW   NTR1                                                                   
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNTFIL  '),(OVELCODE,NBAIO),      X        
               (5,DUB)                                                          
         B     XITX                                                             
         SPACE 4                                                                
*                                                                               
* CHECK TO SEE IF DEMO GUARANTEE X'B4' ELEMENT MATCHES DMEO IN LIST             
* INPUT : R6 POINTS TO ELEMENT                                                  
*         BYTE HAS DEMO VALUE                                                   
*                                                                               
*OUTPUT : CC = IF DEMO MATCH                                                    
*         DMCB+12 HAS ADDRESS OF B4 ELEMENT                                     
*                                                                               
CHKC22   NTR1                                                                   
         LA    R2,1                                                             
*CC22     CLC   BYTE,4(R6)          DEMOS MATCH                                 
CC22     CLC   HALF(1),2(R6)          DEMOS MATCH                               
         BNE   CC23                YES                                          
         CLC   HALF+1(1),4(R6)                                                  
         BE    CCYES2                                                           
CC23     ZIC   R1,1(R6)                                                         
         AR    R6,R1                                                            
         CLI   0(R6),X'B4'                                                      
         BE    CC22                                                             
         B     *+6                                                              
CCYES2   SR    R2,R2                                                            
         STCM  R6,15,DMCB+12                                                    
         LTR   R2,R2                                                            
         B     XITX                                                             
         EJECT                                                                  
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
* OVERFLOW ROUTINES                                                             
*                                                                               
XTROUTIN NMOD1 0,**XTROU,R9,R8                                                  
         L     RC,0(R1)            REESTABLISH WORKING STORAGE                  
         ZIC   RF,0(R1)                                                         
         SLL   RF,2                                                             
         B     ROUTINE(RF)                                                      
*                                                                               
ROUTINE  DS    0H                                                               
         B     GETPROG             (0) READS PROGRAM RECORD                     
         B     GETNADDF            (1) READS NAD DEFINITION RECORD              
         B     GETEST              (2) READS ESTIMATE RECOPRDCORD               
         B     CABNCAT             (3) REPLACE CABLE CATEGRY BY NAD CAT         
         DC    H'0'                                                             
         EJECT                                                                  
*                                                                               
*                                                                               
*                                                                               
*        READS PROGRAM RECORD CC= IF FOUND                                      
GETPROG  DS    0H                                                               
         XC    KEY,KEY                                                          
         LA    R3,KEY                                                           
         USING NPGRECD,R3                                                       
         MVC   NPGKTYP,=X'0D20'                                                 
         MVC   NPGKAM,NBACTAM                                                   
         MVC   NPGKNET,NBMARKET                                                 
         MVC   NPGKPROG,NBACTPRG                                                
         MVC   NPGKEND,NBACTDAT                                                 
         CLI   NBMGDOPT,C'Y'       USE MADE GOOD FOR PROGGRAM                   
         BNE   GTPRG2                                                           
         MVC   NPGKPROG,NBMGFPCD   YES/SET MADE GOOD FOR PROG/DATE              
         MVC   NPGKEND,NBMGFDAT                                                 
*                                                                               
GTPRG2   MVC   KEYSAVE,KEY                                                      
         GOTO1 NBDM,DMCB,=C'DMRDHI',=C'SPTDIR',KEY,KEY                          
         CLC   KEY(NPGKEND-NPGKEY),KEYSAVE  TEST FOR SAME PROGRAM               
         BNE   GETPROGX            EXIT IMMEDIATELY W CC=NEQ                    
         LA    R2,KEY+14           A(DISK ADDRESS)                              
         L     R5,AWORKREC                                                      
         GOTO1 NBDM,DMCB,=C'GETREC',=C'SPTFIL',(R2),(R5),DMWORK                 
**       GOTO1 NBDM,DMCB,=C'GETREC',=C'SPTFIL',(R2),WORKREC,DMWORK              
         CR    RB,RB               SET CC=EQ                                    
*                                  ************                                 
GETPROGX XIT1                                                                   
         DROP  R3                                                               
         EJECT                                                                  
*                                                                               
*        READS NED DAF. RECORD CC= IF FOUND                                     
GETNADDF DS    0H                                                               
         OC    NBDM,NBDM                                                        
         BZ    GETNADX                                                          
         XC    KEY,KEY                                                          
         LA    R3,KEY                                                           
         USING NNDRECD,R3                                                       
         MVC   NNDKTYP,=X'0D16'                                                 
*****    MVC   NNDKAM,NBACTAM                                                   
         L     R1,NBAIO                                                         
         CLI   0(R1),X'04'         IS IT UNIT                                   
         BNE   GETNADX                                                          
         MVC   NNDKAM,1(R1)        SET IN AGY/MED                               
         MVC   NNDCODE,DINNDCOD                                                 
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 NBDM,DMCB,=C'DMRDHI',=C'SPTDIR',KEY,KEY                          
         CLC   KEY(13),KEYSAVE     TEST FOR SAME PROGRAM                        
         BNE   GETNADX             EXIT IMMEDIATELY W CC=NEQ                    
         LA    R2,KEY+14           A(DISK ADDRESS)                              
         L     R5,AWORKREC                                                      
         GOTO1 NBDM,DMCB,=C'GETREC',=C'SPTFIL',(R2),(R5),DMWORK                 
***      GOTO1 NBDM,DMCB,=C'GETREC',=C'SPTFIL',(R2),WORKREC,DMWORK              
         CR    RB,RB               SET CC=EQ                                    
*                                  ************                                 
GETNADX  XIT1                                                                   
         DROP  R3                                                               
         EJECT                                                                  
*                                                                               
*        READS ESTIMATE RECORD CC= IF FOUND                                     
GETEST   DS    0H                                                               
         OC    NBDM,NBDM                                                        
         BZ    GETESTX                                                          
         XC    KEY,KEY                                                          
         LA    R3,KEY                                                           
         USING ESTHDR,R3                                                        
         L     R1,NBAIO                                                         
         USING NURECD,R1                                                        
         CLI   0(R1),X'04'         IS IT UNIT                                   
         BNE   GETESTX                                                          
         MVC   EKEYAM,NUKAM        SET IN AGY/MED                               
         MVC   EKEYCLT,NUKCLT      SET IN CLIENT                                
         MVC   EKEYPRD,=CL3'POL'   SET IN POL PROD                              
         MVC   EKEYEST,NUKEST      SET IN AGY/MED                               
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 NBDM,DMCB,=C'DMRDHI',=C'SPTDIR',KEY,KEY                          
         CLC   KEY(13),KEYSAVE     TEST FOR SAME ESTIMATE                       
         BNE   GETESTX             EXIT IMMEDIATELY W CC=NEQ                    
         LA    R2,KEY+14           A(DISK ADDRESS)                              
         L     R5,AWORKREC                                                      
         GOTO1 NBDM,DMCB,=C'GETREC',=C'SPTFIL',(R2),(R5),DMWORK                 
**       GOTO1 NBDM,DMCB,=C'GETREC',=C'SPTFIL',(R2),WORKREC,DMWORK              
         CR    RB,RB               SET CC=EQ                                    
*                                  ************                                 
GETESTX  XIT1                                                                   
         DROP  R1,R3                                                            
         EJECT                                                                  
*                                                                               
* CABNCAT - IF NADFLG IS ON, CHANGE ALL NAD CATEGORIES FROM 0 TO 1              
*           (EXCEPT FOR HOMES)                                                  
*        R3-> TABLE OF DEMOS                                                    
*        R4 = MAX NO OF DEMOS                                                   
*                                                                               
CABNCAT  DS    0H                                                               
** PXZ   CLI   NBPOSTYP,C'C'  FOR CABLE ONLY                                    
         CLI   NBSURVEY,C'C'  FOR CABLE ONLY                                    
         BNE   CABNCATX                                                         
         CLI   NADFLG,C'Y'                                                      
         BNE   CABNCATX                                                         
*                                                                               
CABNCT2  CLI   0(R3),X'FF'                                                      
         BE    CABNCATX                                                         
         OC    0(3,R3),0(R3)                                                    
         BZ    CABNCATX                                                         
         CLI   0(R3),0        REPLACE CATEGORY 0 BY 1                           
         BNE   CABNCT5                                                          
         CLI   2(R3),1        BUT NOT FOR HOMES                                 
         BE    CABNCT5                                                          
         MVI   0(R3),1                                                          
CABNCT5  LA    R3,3(R3)                                                         
         BCT   R4,CABNCT2                                                       
*                                                                               
CABNCATX XIT1                                                                   
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
* SUBROUTINES FOR DEMOS                                                         
*SUBR INCLUDED HERE                                                             
       ++INCLUDE NENTVLDEMO                                                     
         LTORG                                                                  
         EJECT                                                                  
MULTIPD  DSECT                 DSECT FOR MULTI PRODUCT HANDLINE                 
         DS    0F                                                               
ORIGIMPS DS    CL88                SAVE AREA  FOR EST IMPS                      
SUBTIMPS DS    CL88                                                             
RAWIMPSE DS    CL88                                                             
ORIGGRPS DS    CL88                SAVE AREA  FOR EST GRPS                      
SUBTGRPS DS    CL88                                                             
RAWGRPSE DS    CL88                                                             
ACTGIMPS DS    CL88                SAVE AREA FOR ACT IMPS                       
ACTTIMPS DS    CL88                                                             
RAWIMPSA DS    CL88                                                             
ACTGGRPS DS    CL88                SAVE AREA FOR ACT IMPS                       
ACTTGRPS DS    CL88                                                             
RAWGRPSA DS    CL88                                                             
ADROIMP  DS    A                                                                
ADRTIMP  DS    A                                                                
ARAWIMPS DS    A                                                                
ADROGRP  DS    A                                                                
ADRTGRP  DS    A                                                                
ARAWGRPS DS    A                                                                
ADDRADJ  DS    A                   ADDRESS OF ADJEST/ADJACT ROUTINE             
MBPCTSV  DS    F                   MULTIPROD PCT SAVE AREA                      
MULTILEN EQU   *-MULTIPD                                                        
         EJECT                                                                  
*                                                                               
**** WORKING STORAGE                                                            
*                                                                               
CONVWORK DSECT                                                                  
*                                                                               
SRCHEL   DS    CL1                                                              
ESTSEXST DS    CL1                 SET TO C'T' IF ESTIMATE DEMOS ON REC         
ACTSEXST DS    CL1                 SET TO C'T' IF ACTUAL DEMOS ON REC           
HAVNAD   DS    CL1                 SET TO 'Y' IF NAD DEMOS                      
HAVBTU   DS    CL1                 SET TO 'Y' IF USER FILE DEMOS                
DIVSWTCH DS    CL1                 SET TO 'Y' IF OVERRIDES ADJUSTED             
HAVDAY   DS    CL1                 SET TO 'Y' IF DAYLIES EXIST                  
DBNETWXT DS    CL32                NETW EXTENSION AREA                          
APRODLCL DS    CL1                 USER DID NOT PASS ADDRESS IN NBAPROD         
*                                  NETVALUE SET NBAPROD WITH LOCAL              
*                                  STORAGE - CLEAR NBAPROD                      
BTUEXT   DC    C'UFIL'                                                          
BTUEX1   DC    A(0)                                                             
BTUEX2   DC    A(0)                                                             
*                                                                               
ELEM1    DS    F                   LOCATION OF FIRST ELEMENT ON RECORD          
DMCB     DS    6F                  WORK AREA FOR GOTOS                          
STATUS   DS    CL1                                                              
RELO     DS    F                                                                
ASUBR    DS    A                   A(SUBR)                                      
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
WORK     DS    CL150                                                            
BYTE     DS    CL1                                                              
SVBYTE   DS    X                                                                
*                                                                               
ACURDEMO DS    A                                                                
*                                                                               
SAVWGT   DS    0H                  PLACE TO SAVE WEIGHT                         
         DS    CL1                   FIRST BYTE                                 
SVBYTE2  DS    CL1                   SECOND BYTE                                
*                                                                               
SVDBXLIV DS    C                   SAVED DBXLIVE                                
*                                                                               
SVPSTAFD DS    CL1                   SAVE POST AFFIDS PROFILE                   
*                                                                               
SAVEBOOK DS    0CL2                                                             
SAVEYEAR DS    CL1                                                              
SAVEWEEK DS    CL1                                                              
*                                                                               
ESTHOME  DS    F                   ESTIMATED HOMES RTG FOR FORMULA              
*                                                                               
SAVRESLT DS    CL1                 NTI RESULT FROM NETVALUE ELEMENT             
SAVRESLTN DS   CL1                 NAD RESULT FROM NETVALUE ELEMENT             
SAVBTYPE  DS   CL1                 NAD SAVE BOOK TYPE                           
*                                                                               
SAVERE   DS    A                                                                
*                                                                               
NUMDEMS  DS    CL1                 NUMBER OF DEMOS REQUESTED                    
RESULT   DS    CL1                 USED AS CONTROL IN DODEM STATES              
         DS    CL1                  (SOMETIMES THIS IS 2 CHARS LONG)            
*                                                                               
UDEORA   DS    CL1                 C'E' OR C'A' FOR ACT OR EST USER DEM         
UD7NAME  DS    CL7                 NAME OF CURRENT USER DEMO                    
USERUNV  DS    F                   SAVE AREA FOR USER DEMOS                     
USERVPH  DS    F                                                                
USERIMP  DS    F                                                                
USERGRP  DS    F                                                                
*                                                                               
SUMWVPH  DS    F                                                                
SUMWIMP  DS    F                                                                
SUMWUNIV DS    F                                                                
SUMWGT   DS    F                                                                
*                                                                               
BLAKLIST DS    CL4                 FOR BLACK WKS: HAS WEEKS TO BE AVGED         
BLAKMASK DS    CL1                 MASK TELLS IF POCK PC EXISTS FOR BW          
EBCDDAT  DS    CL6                 EBCDIC 6-BYTE DATE OF UNIT                   
NVELSTAT DS    CL1                 SET TO 'Y' IF NETVALUE EL CHANGED            
NVELEX   DS    CL1                 SET TO 'Y' IF NETVALUE EL EXISTS             
*                                                                               
******** ARGS TO DEMO ADJUSTMENT ROUTINES                                       
MULTER   DS    F                   MULTIPLICAND FOR ADJUSTING DEMOS             
DIVSOR   DS    F                   DIVISOR FOR ADJUSTING DEMOS                  
ADDER    DS    F                   FACTOR TO ADD FOR ROUNDING                   
PMULTER  DS    PL8                 *MULTIPLICAND FOR ADJUSTING DEMOS            
PDIVSOR  DS    PL8                 *DIVISOR FOR ADJUSTING DEMOS                 
PADDER   DS    PL8                 *FACTOR TO ADD FOR ROUNDING                  
PWORK    DS    PL16                PACKED WORK AREA                             
OVELCODE DS    CL1                 ELEMENT CODE OF OVERRIDE ELEMNT              
*                                   (X'DD' FOR ESTS, X'DE' FOR ACTS)            
IFDEM    DS    CL1                 N=NO OVERRIDE IF DEMO OVERRIDE               
*                                                                               
DIALIST  DS    0C                  INPUT TO DEMOUT                              
DIASHARE DS    CL3                                                              
DIAHUT   DS    CL3                                                              
DIAVHOME DS    CL3                                                              
DIAGHOME DS    CL3                                                              
DIAIHOME DS    CL3                                                              
DIADMSET DS    (50*3)CL3           UP TO 25 SETS OF 3 DEMO REQUESTS             
         DS    CL1                 MAY BE NEEDED TO MARK END-OF-LIST            
DIULIST  DS    0C                   UNIVERSE LIST                               
DIUHOME  DS    CL3                                                              
DIUDMSET DS    50CL3                                                            
         DS    CL1                  E-O-L                                       
DIALEN   EQU   *-DIALIST           LENGTH OF DIALIST                            
DINLIST  DS    0C                   NAD LIST                                    
DINNDCOD DS    CL6                 SAVE NAD DEFINITION CODE                     
DINDMSET DS    50CL3                                                            
         DS    CL1                  E-O-L                                       
DINEND   EQU   *                   END OF DINLIST                               
SPLITLEN DS    XL1                 SPLIT LENGTH FOR PIGGIES                     
SPLITCST DS    H                   COST PERCENTAGE FOR PIGGIES                  
SPLITPRE DS    H                   PREVIOUS SPLIT PERCENTAGE                    
*                                                                               
ALVPHOFF EQU   0                   VPH OFFSET IN DOALIST                        
ALGRPOFF EQU   4                   GRP OFFSET IN DOALIST                        
ALIMPOFF EQU   8                   IMP OFFSET IN DOALIST                        
ALLENGTH EQU  12                   LENGTH OF ENTRY IN DEMSET OF DOALIST         
*                                                                               
DOALIST  DS    0F                  OUTPUT FROM DEMOUT                           
DOASHARE DS    F                                                                
DOAHUT   DS    F                                                                
DOAVHOME DS    F                                                                
DOAGHOME DS    F                                                                
DOAIHOME DS    F                                                                
DOADMSET DS    (50*3)F             UP TO 25 SETS OF 3 DEMOS EACH                
DOAWUNIV DS    F                   WEIGHTED UNIVERSE                            
DOULIST  DS    0F                   UNIVERSE LIST                               
DOUHOME  DS    F                                                                
DOUDMSET DS    50F                                                              
DOALEN   EQU   *-DOALIST                                                        
*                                                                               
*                                                                               
*              DBLOCK HERE                                                      
         PRINT OFF                                                              
       ++INCLUDE DEDBLOCK                                                       
         PRINT ON                                                               
*                                                                               
DBXTLWRK DS    CL24                SPACE FOR DBXTLD FOR DBLOCK                  
*                                                                               
SAVDBLK  DS    CL(L'DBLOCK)        AREA TO SAVE DBLOCK                          
*                                                                               
*                                  MATHFACS BLOCK                               
MATHFACS DS    0F                  DEMOMATH BLOCK                               
MATHABLK DS    A                   A(DBLOCK)                                    
MATHFCTR DC    F'0'                WEIGHTING FAVTOR                             
MATHIFIL DS    CL3                 INPUT FILE FORMAT                            
MATHOFIL DS    CL3                 OUTPUT FILE FORMAT                           
MATHOSRC DS    CL3                 OUTPUT SOURCE FORMAT                         
MATHFACL EQU   *-MATHFACS                                                       
*                                                                               
DEELEM   DS    CL1                 FAKE OVERRIDE ELEMENT                        
         DS    CL1                                                              
         DS    CL1                                                              
         DS    CL1                                                              
         DS    CL2                                                              
DEELLEN  EQU   *-DEELEM            LENGTH O OVERRIDE ELEM                       
AFTERDE  DS    CL1                                                              
*                           WHERE DO I CLEAR THIS TO ZEROS                      
BLDCHGD  DC    45XL1'00'            15X3 PRODS                                  
BLDCHGDX DC    X'00'                                                            
*                                                                               
KEY      DS    CL32                                                             
KEYSAVE  DS    CL32                                                             
KEYSAV2  DS    CL20                                                             
*                                                                               
DUMNVEL  DS    CL50                DUMMY NETVALUE ELEMENT WORK AREA             
*                                                                               
OUTFORM  DS    CL10                OUTPUT FORMAT BLOCK AREA                     
*                                                                               
DMWORK   DS    12D                 WORK AREA FOR DATAMGR                        
*                                                                               
SAVE35AD DS    F                   SAVED ADDRESS OF EST HOMES ELEM              
SAVE35EL DS    CL4                 SAVED ESTIMATED HUT AND RTG                  
*                                                                               
SAVEOVR  DS    30F                 SAVE AREA FOR HUNDREDS OVERRIDES             
*                                                                               
DEMPREC  DS    CL14                TABLE CONTAINING DEMO PRECISIONS             
*                                                                               
MIRDIV   DS    X                   DIV DIVSOR BY # DAY/TIME ENTRIES             
NADFLG   DS    C                  TEMPORARY. WILL BE SENT AS AN OPTION          
*                                                                               
DEMOXTBL DS    CL25                2ND LIST FOR PLD FOR                         
*                                  NON XPANDED DEMO BLOCK                       
         DS    F                                                                
***************************** NOTE ****************************                 
INTPROD  DS    CL3            NBINTPRD IS 1 CHAR                                
PRODTBL  DS   CL100           COVERED BY NETXBLKD                               
***************************** NOTE ****************************                 
AWORKREC DS    F                   ADDRESS OF WORKREC                           
ACLIREC  DS    F                   ADDRESS OF CLIREC                            
*                                  10 MAX DAY/TIMES - SEE PODDAYTM              
NLIVEXT  DC    CL4'NLIV'                                                        
         DC    XL5'00'             4(DBXLIVNX) + 1(DBXLIVE)                     
CAVGEXT  DC    CL4'CAVG'                                                        
         DC    XL5'00'             4(DBXCAVNX) + 1(DBXCAV)                      
DBXPODT  DC    CL4'PDMS'                                                        
         DC    XL6'00'             4(DBXPMNX) + 2(DBXPMIN)                      
*                                                                               
DUMMYREC DS    CL2000              MAX SIZE OF DUMMY AREA                       
         DS    CL1000           NEWXTRA WORK AREA - PROBLEM???                  
WORKREC  DS    CL3000              WORK AREA TO READ RECORDS INTO               
*                        **** DUMMREC AND WORKREC MUST BE KEPT                  
*                        **** TOGETHER ***                                      
CLIREC   DS    CL1500                                                           
***************************** ONE AFTER THE OTHER****************               
CONVWRKX EQU   *                                                                
         SPACE 1                                                                
*              DEDEMEQUS HERE                                                   
*              SPGENSTA                                                         
*              SPGENPROG                                                        
*              SPGENNAD                                                         
*              DEDBEXTRAD                                                       
*              NEGENUNIT                                                        
*              NEGENUSER                                                        
*              NEGENPACK                                                        
*              NETBLOCKD                                                        
*              NETDEMOD                                                         
*              NETUNIVD                                                         
         PRINT ON                                                               
       ++INCLUDE DEDEMEQUS                                                      
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE SPGENSTA                                                       
       ++INCLUDE SPGENPROG                                                      
       ++INCLUDE SPGENNAD                                                       
       ++INCLUDE SPGENEST                                                       
       ++INCLUDE SPGENCLT                                                       
       ++INCLUDE DEDBEXTRAD                                                     
       ++INCLUDE NEGENUNIT                                                      
       ++INCLUDE SPBVALD                                                        
       ++INCLUDE NEGENUSER                                                      
       ++INCLUDE NEGENPACK                                                      
       ++INCLUDE NEGENDPT                                                       
       ++INCLUDE SPGENPRD                                                       
       ++INCLUDE NETXBLKD                                                       
       ++INCLUDE DEDEMEQUS2                                                     
         DSECT NEBLOCK                                                          
       ++INCLUDE NETBLOCKN                                                      
NDBLK    DSECT                                                                  
       ++INCLUDE NETDEMOT                                                       
         ORG   NDDEMBLK                                                         
       ++INCLUDE NETDEMOP                                                       
       ++INCLUDE NETUNIVD                                                       
       ++INCLUDE NETBLKXTND                                                     
       ++INCLUDE DEDEMFILE                                                      
         PRINT ON                                                               
*        <   > DEMO OVERRIDE ELEMENT OLD FORMAT FOR DEMOMATH                    
         SPACE 1                                                                
DEOVD    DSECT                                                                  
DEOVEL   DS    CL1                 ELEMENT CODE X'DD' FOR ESTIMATED             
*                                               X'DE' FOR ACTUALS               
DEOVLEN  DS    CL1                 ELEMENT LENGTH (6 OR 8)                      
DEOVMOD  DS    CL1                 MODIFIER                                     
DEOVNUM  DS    CL1                 DEMO. NUMBER                                 
DEOVVAL  DS    CL2                 VALUE (HALF/FULL FOR LENGTH 6/8)             
         SPACE 1                                                                
                                                                                
         SPACE                                                                  
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'032NENETVALUE02/28/20'                                      
         END                                                                    
