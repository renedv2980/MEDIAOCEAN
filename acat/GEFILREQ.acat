*          DATA SET GEFILREQ   AT LEVEL 056 AS OF 01/06/97                      
*&&      SET   NOP=N                                                            
*CATALP GEFILREQ                                                                
         TITLE 'GENERALISED REQUEST OVERLAY'                                    
FILREQ   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,GEFILREQ,R6,RR=RE                                              
         USING TWAD,RA                                                          
         USING WORKD,R9                                                         
         L     R8,AGWORK                                                        
         USING GWORKD,R8                                                        
         L     RC,AOVERWRK                                                      
         USING OVERWRKD,RC                                                      
         ST    RE,BORELO                                                        
         MVC   SVPARMS,0(R1)                                                    
         ST    R1,CALLR1                                                        
         LH    R7,=Y(TWSAVE-TWAD)                                               
         A     R7,ATWA                                                          
         USING MYSAVED,R7                                                       
*                                                                               
         SRL   RF,24                                                            
         CLM   RF,1,=AL1(ROUTSN)                                                
         BNH   *+6                 UNKNOWN ROUTINE                              
         DC    H'0'                                                             
         SLL   RF,2                                                             
         B     ROUTS(RF)                                                        
         SPACE 1                                                                
ROUTS    DS    0XL4                                                             
         B     OBJECT              OBJECT INVOKER                               
         B     INIT                INITIALIZATION CALL                          
*                                                                               
ROUTSN   EQU   (*-ROUTS)/4                                                      
         SPACE 1                                                                
***********************************************************************         
* EXITS                                                               *         
***********************************************************************         
         SPACE 1                                                                
EXITH    CLI   *,0                 SET CC HIGH                                  
         B     EXIT                                                             
EXITL    CLI   *,FF                SET CC LOW                                   
         B     EXIT                                                             
EXITOK   CR    RB,RB               SET CC EQUAL                                 
         SPACE 1                                                                
EXIT     L     R1,CALLR1           RETURN PARAMETERS                            
         MVC   0(L'SVPARMS,R1),SVPARMS                                          
         XIT1  ,                   EXIT WITH CC SET                             
         SPACE 2                                                                
EXITNV   MVC   FVMSGNO,=AL2(FVFNOTV)                                            
         B     EXITL               EXIT WITH FIELD NOT VALID SET                
EXITNO   MVC   FVMSGNO,=AL2(FVFNONE)                                            
         B     EXITL               EXIT WITH FIELD NOT INPUT SET                
EXITNOTN MVC   FVMSGNO,=AL2(FVFNOTN)                                            
         B     EXITL               EXIT WITH FIELD NOT NUMERIC SET              
*                                                                               
FLTXL    MVI   SVPARMS,0           EXIT LOW FOR FILTER                          
         B     EXITOK                                                           
FLTXE    MVI   SVPARMS,1           EXIT EQUAL FOR FILTER                        
         B     EXITOK                                                           
FLTXH    MVI   SVPARMS,2           EXIT HIGH FOR FILTER                         
         B     EXITOK                                                           
*                                                                               
DIE      DC    H'0'                                                             
         EJECT                                                                  
***********************************************************************         
* TABLE ITERATION ROUTINE  - EXPECTS RF TO HOLD A(TABLE)              *         
*                          - EXPECTS R1 TO HOLD VERB                  *         
***********************************************************************         
         SPACE 1                                                                
         USING OBJTABD,RF                                                       
ITER     CLI   OBJVERB,EOT         E.O.T.                                       
         BE    EXITOK           ** NEED TO SET HIGH IF NOT OVERRIDE             
         CLM   R1,1,OBJVERB        R1 HOLDS EQUATED VERB                        
         BE    ITER02              MATCHED                                      
         LA    RF,OBJTABL(RF)                                                   
         B     ITER                ITERATE THIS TABLE                           
*                                                                               
ITER02   ICM   RF,15,OBJADR        ROUTINE TO HANDLE THE VERB                   
         LA    R1,SVPARMS                                                       
         A     RF,BORELO                                                        
         BR    RF                                                               
         DROP  RF                                                               
         SPACE 2                                                                
***********************************************************************         
* OBJECT CONTROLLER - INTERFACES TO ALL USER OBJECTS                  *         
*                                                                     *         
* P1 HOLDS EQUATED VERB                                               *         
***********************************************************************         
         SPACE 1                                                                
OBJECT   L     R1,SVPARMS                                                       
         LA    RF,TABLEOO          KNOWN OBJECTS                                
         B     ITER                                                             
*                                                                               
TABLEOO  DS    0X                                                               
         DC    AL1(ORECH),AL1(0,0,0),AL4(EXITH)                                 
         DC    AL1(ODATA),AL1(0,0,0),AL4(DATA)                                  
         DC    AL1(OSES),AL1(0,0,0),AL4(NTRSES)                                 
         DC    AL1(OLIST),AL1(0,0,0),AL4(LIST)                                  
         DC    AL1(OSUBACT),AL1(0,0,0),AL4(SUB)                                 
         DC    AL1(OACTH),AL1(0,0,0),AL4(EXITH)                                 
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* INITIALIZATION                                                      *         
***********************************************************************         
         SPACE 1                                                                
INIT     OC    GCREQFIL,GCREQFIL                                                
         BNZ   *+10                                                             
         MVC   GCREQFIL,REQUEST                                                 
*                                                                               
         GOTO1 VDICTAT,BOPARM,C'LU  ',DCLIST,DSLISTU                            
         GOTO1 (RF),(R1),C'LL  ',,DSLISTL                                       
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT                                                         *         
*                                                                     *         
* P1 HOLDS EQUATED OBJECT IDENTIFIER                                  *         
* P2 HOLDS EQUATED DATA IDENTIFIER OR 0 IF GLOBAL DATA ACTION         *         
* P3 BYTE  0   HOLDS EQUATED DATA VERB IF P2 IS ZERO                  *         
* P3 BYTES 1-3 HOLDS EQUATED ACTION VERB                              *         
* P4 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
* P5 HOLDS A(FIELD TABLE ENTRY) OR ZERO IF P2 IS ZERO                 *         
*                                                                     *         
* FIELD DATA IS EXTRACTED/OUTPUT INTO FVIFLD                          *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
DATA     ICM   R1,15,SVPARMS2      R1=DATA IDENTIFIER                           
         BNZ   DATA02              ACTION IS ON A DATA OBJECT                   
*                                                                               
         L     R2,SVPARMS4                                                      
         USING FDRRECD,R2                                                       
         XR    R1,R1                                                            
         IC    R1,SVPARMS3         GET GLOBAL VERB                              
         LA    RF,DTATABL          TABLE OF GLOBAL VERBS                        
         B     ITER                ITERATE TABLE                                
*                                                                               
DATA02   LA    RF,KNOWTAB          TABLE OF KNOWN OBJECTS                       
         USING KNOWTABD,RF                                                      
*                                                                               
DATA04   CLC   KNOWID,=AL2(EOT)    REACH END - NOT A KNOWN DATA TYPE            
         BE    EXITH                                                            
         CLM   R1,3,KNOWID         IS THIS A KNOWN TYPE?                        
         BE    DATA06                                                           
         LA    RF,KNOWLQ(RF)                                                    
         B     DATA04                                                           
*                                                                               
DATA06   ICM   RF,15,KNOWADD       A(KNOWN OBJECT)                              
         A     RF,BORELO           RELOCATE IT                                  
*                                                                               
         LM    R1,R2,SVPARMS3      R1 HOLDS VERB                                
         USING FDRRECD,R2          R2 HOLDS A(RECORD)                           
         BR    RF                                                               
         SPACE 1                                                                
*                                                                               
DTATABL  DS    0X                                                               
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* TABLE OF KNOWN RECORD OBJECTS                                       *         
***********************************************************************         
         SPACE 1                                                                
KNOWTAB  DC    AL2(00001),AL4(FLTDTA)    FILTERS                                
         DC    AL2(00002),AL4(DSPDTA)    DISPLAY LINE                           
         DC    AL2(00003),AL4(DSPDTA)    SUB-ACTION LINE                        
         DC    AL2(EOT)                                                         
         SPACE 1                                                                
KNOWTABD DSECT                                                                  
KNOWID   DS    XL2                 IDENTIFIER                                   
KNOWADD  DS    AL4                 A(OBJECT)                                    
KNOWLQ   EQU   *-KNOWTABD                                                       
*                                                                               
FILREQ   CSECT                                                                  
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT FOR FILTERS                                             *         
*                                                                     *         
* R1 HOLDS EQUATED VERB                                               *         
* R2 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
***********************************************************************         
         SPACE 1                                                                
FLTDTA   LA    RF,FLTTBL           TABLE OF KNOWN VERBS                         
         B     ITER                ITERATE TABLE                                
*                                                                               
FLTTBL   DC    AL1(DFVAL),AL1(0,0,0),AL4(VALFLT)                                
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* VALIDATE FILTERS                                                    *         
***********************************************************************         
         SPACE 1                                                                
VALFLT   CLC   FVIFLD,SVFVIFLD     FIELD CHANGED?                               
         BE    *+8                 NO                                           
         OI    LSSCIND1,LSSCIFLT   SET REBUILD LIST                             
         MVC   SVFVIFLD,FVIFLD                                                  
*                                                                               
         XC    FAGY,FAGY                                                        
         XC    FAGYF,FAGYF                                                      
*                                                                               
         LA    R0,BLOCK                                                         
         LH    R1,=Y(BLOCKLQ)                                                   
         XR    RE,RE                                                            
         XR    RF,RF                                                            
         MVCL  R0,RE               RESET FILTER BLOCK                           
*                                                                               
         CLI   FVILEN,0            ANY INPUT?                                   
         BE    EXITOK              NO                                           
*                                                                               
         L     RF,FVADDR                                                        
         LA    R4,BLOCK                                                         
         GOTOX VSCANNER,BOPARM,(RF),(10,(R4))                                   
         CLI   4(R1),0                                                          
         BE    EXITOK                                                           
*                                                                               
         XR    R0,R0                                                            
         IC    R0,4(R1)            COUNT OF LINES IN FILTER BLOCK               
*                                                                               
         TM    CUSTAT,CUSDDS       DDS TERMINAL ONLY CAN FILTER                 
         BZ    VFLT06              ON U=??                                      
*                                                                               
         LR    RF,R0                                                            
         LA    R1,BLOCK                                                         
         USING SCANBLKD,R1                                                      
VFLT02   CLI   SC1STLEN,1          U=FILTER                                     
         BNE   VFLT04                                                           
         CLI   SC1STFLD,C'U'                                                    
         BNE   VFLT04                                                           
*                                                                               
         MVI   FAGY,C'Y'           SET AGENCY FILTER                            
         CLC   =CL3'ALL',SC2NDFLD                                               
         BNE   *+14                                                             
         MVC   FAGYF,=X'FFFF'      SET ALL FLAG                                 
         B     *+10                                                             
         MVC   FAGYF,SC2NDFLD                                                   
*                                                                               
VFLT04   LA    R1,SCBLKLQ(R1)                                                   
         BCT   RF,VFLT02                                                        
*                                                                               
VFLT06   B     EXITOK                                                           
         DROP  RF,R1                                                            
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT FOR DISPLAY LINE                                        *         
*                                                                     *         
* R1 HOLDS EQUATED VERB                                               *         
* R2 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
***********************************************************************         
         SPACE 1                                                                
DSPDTA   LA    RF,DSPTBL           TABLE OF KNOWN VERBS                         
         B     ITER                ITERATE TABLE                                
*                                                                               
DSPTBL   DC    AL1(DDIS),AL1(0,0,0),AL4(DISDSP)                                 
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* DISPLAY REQUEST LINE                                                *         
***********************************************************************         
         SPACE 1                                                                
DISDSP   L     R3,ATLST                                                         
         USING TLSTD,R3                                                         
         XC    BOELEM,BOELEM                                                    
         LA    R4,BOELEM           R4 = A(CURRENT OUTPUT BUFFER)                
         LA    R5,TLCARD+REQFRST-REQCODE                                        
*                                                                               
         CLI   FAGY,C'Y'           AGENCY REQUIRED                              
         BNE   DDSP02                                                           
         MVC   0(2,R4),TLAGY                                                    
         CLI   TLAGY+1,C'*'                                                     
         BNE   *+12                                                             
         LA    R4,3(R4)                                                         
         B     DDSP02                                                           
*                                                                               
         XR    RF,RF                                                            
         CURED (RF),(4,FVIFLD),0,DMCB=BOPARM,ALIGN=LEFT                         
         AR    R4,R0                                                            
         LA    R4,1(R4)                                                         
*                                                                               
DDSP02   PACK  BODUB1,2(2,R5)                                                   
         CVB   RE,BODUB1                                                        
         BCTR  RE,0                                                             
         EX    RE,*+4                                                           
         MVC   1(0,R4),4(R5)                                                    
         LA    R4,2(RE,R4)         NEXT FREE IN BOELEM                          
         LA    R5,5(RE,R5)                                                      
         CLI   0(R5),C'*'          END OF REQUEST                               
         BE    DDSP06                                                           
         LA    R5,1(R5)                                                         
*                                                                               
         LR    RF,R4                                                            
         LA    RE,BOELEM                                                        
         SR    RF,RE                                                            
         CLM   RF,1,=AL1(L'FVIFLD)                                              
         BH    DDSP06                                                           
*                                                                               
         MVI   0(R4),C'-'          'KEY' FIELD SEPARATOR                        
         CLC   =C'08',0(R5)                                                     
         BNL   *+8                                                              
         MVI   0(R4),C','          'DATA' FIELD SEPARATOR                       
*                                                                               
         CLI   0(R5),C'*'          END OF REQUEST                               
         BNE   DDSP02                                                           
*                                                                               
DDSP06   MVI   0(R4),C'*'          DENOTE END OF REQUEST                        
         MVC   FVIFLD,BOELEM                                                    
         B     EXITOK                                                           
         DROP  R3                                                               
         SPACE 2                                                                
***********************************************************************         
* NTRSES OBJECT                                                       *         
*                                                                     *         
* P1 HOLDS EQUATED OBJECT                                             *         
* P2 HOLDS EQUATED VERB                                               *         
* P3 HOLDS A(INITIAL LIST KEY)                                        *         
* P4 HOLDS A(MEMORY COVERED BY FESD)                                  *         
***********************************************************************         
         SPACE 1                                                                
NTRSES   LM    R0,R3,SVPARMS                                                    
         USING SSAVD,R2                                                         
         USING FESD,R3                                                          
         LA    RF,NSSTABL                                                       
         B     ITER                                                             
*                                                                               
NSSTABL  DS    0X                                                               
         DC    AL1(SNTRIN),AL1(0,0,0),AL4(NTRIN)                                
         DC    AL1(SXITOUT),AL1(0,0,0),AL4(XITOUT)                              
         DC    AL1(EOT)                                                         
         SPACE 1                                                                
***********************************************************************         
* PROCESS PARAMETER LIST FOR NTRSES TRANSFER                          *         
***********************************************************************         
         SPACE 1                                                                
         PUSH  USING                                                            
NTRIN    B     EXITOK                                                           
         POP   USING                                                            
         SPACE 1                                                                
***********************************************************************         
* PROCESS PARAMETER LIST FOR NTRSES TRANSFER                          *         
***********************************************************************         
         SPACE 1                                                                
XITOUT   B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* SUB ACTION OBJECT                                                   *         
* -----------------                                                   *         
* P3 = A(SUB-ACTION FIELD)                                            *         
***********************************************************************         
         SPACE 1                                                                
SUB      LM    R0,R3,SVPARMS                                                    
         USING SSAVD,R2                                                         
         LA    RF,SUBTABL                                                       
         B     ITER                                                             
*                                                                               
SUBTABL  DC    AL1(SAVAL),AL1(0,0,0),AL4(SUBVAL)                                
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* VALIDATE SUB ACTION FOR LIST                                        *         
***********************************************************************         
         SPACE 1                                                                
SNGL     USING SUBACTSD,LSNTRY                                                  
MLTI     USING SUBACTSD,LMNTRY                                                  
SUBVAL   XC    LSNTRY,LSNTRY       CLEAR CURRENT SUB-ACTION ELEMENT             
         OI    LSSCIND1,LSSCIINP   KEEP CURRENT SCREEN                          
         L     R2,SVPARMS3         R2=A(SUB-ACTION FIELD)                       
         USING FHD,R2                                                           
         L     R4,ATLST            R4=A(TSAR RECORD)                            
         USING TLSTD,R4                                                         
*                                                                               
         TM    FHII,FHIIVA         FIELD VALIDATED?                             
         BO    *+8                 NO                                           
         OI    LSSCIND1,LSSCIINP   YES - KEEP CURRENT SCREEN                    
*                                                                               
         GOTOX ('FLDVAL',AGROUTS),FHD                                           
         CLI   FVILEN,0            SUB-ACTION ENTERED?                          
         BE    SVALOK              NO                                           
         CLI   FVIFLD,C'*'         IGNORE THIS FIELD?                           
         BE    SVALOK              YES                                          
*                                                                               
         XR    RE,RE                                                            
         ICM   RE,1,FVXLEN                                                      
         BZ    SVAL12              ONLY 1 CHARACTER INPUT                       
         LA    RF,FVIFLD(RE)       TEST SUFFIX CHARACTER                        
*                                                                               
         CLI   0(RF),C'0'          NUMERICAL ENDING?                            
         BL    SVAL12              NO                                           
         CLI   0(RF),C'9'                                                       
         BH    SVAL12              NO                                           
*                                                                               
         XR    R1,R1               R1 HOLDS LENGTH                              
SVAL06   CLI   0(RF),C'0'          STILL NUMERIC?                               
         BL    SVAL08              NO                                           
         CLI   0(RF),C'9'                                                       
         BH    SVAL08              NO                                           
         LA    R1,1(R1)                                                         
         BCTR  RF,0                                                             
         BCT   RE,SVAL06                                                        
*                                                                               
SVAL08   BCTR  R1,0                                                             
         EX    R1,SVALPAK          OBTAIN PACKED NUMBER                         
         CVB   R0,GCDUB1                                                        
         EX    R1,SVALMVE          CLEAR NUMBER FROM INPUT FIELD                
         B     SVAL10                                                           
*                                                                               
SVALPAK  PACK  GCDUB1,1(0,RF)      PACK NUMBER INTO GCDUB1                      
SVALMVE  MVC   1(0,RF),BCSPACES    CLEAR NUMERIC PORTION OF FIELD               
*                                                                               
SVAL10   STH   R0,LMCOUNT          SAVE MULTILINE ACTION REPEAT NUMBER          
         XR    R0,R0                                                            
         IC    R0,FVILEN           REVALIDATE FVIFLD                            
         GOTOX ('FLDVAL',AGROUTS),0                                             
*                                                                               
SVAL12   LA    R3,SUBACTS          TRY TO MATCH SUB-ACTION                      
         USING SUBACTSD,R3                                                      
         XR    R1,R1                                                            
         IC    R1,FVXLEN           LENGTH OF INPUT                              
*                                                                               
SVAL14   CLC   SUBUPR,=AL2(EOT)    REACHED END OF TABLE?                        
         BE    SVALL               YES - INVALID SUB-ACTION                     
         XR    RF,RF                                                            
         ICM   RF,3,SUBUPR         TRY TO MATCH UPPERCASE NAME                  
         A     RF,AOVERWRK                                                      
         EX    R1,SUBMCH                                                        
         BE    SVAL16                                                           
         XR    RF,RF                                                            
         ICM   RF,3,SUBLWR         TRY TO MATCH LOWERCASE NAME                  
         A     RF,AOVERWRK                                                      
         EX    R1,SUBMCH                                                        
         BE    SVAL16                                                           
         LA    R3,SUBACTLQ(R3)                                                  
         B     SVAL14                                                           
*                                                                               
SUBMCH   CLC   FVIFLD(0),0(RF)                                                  
*                                                                               
SVAL16   MVC   LSNTRY,0(R3)        SAVE THIS SINGLE ENTRY                       
         OI    FHII,FHIIVA         SET FIELD VALID                              
         ICM   RF,15,SNGL.SUBRTN   PROCESSING ROUTINE                           
         A     RF,BORELO                                                        
         BR    RF                                                               
         DROP  R3                                                               
*                                                                               
SVALOK   B     EXITOK                                                           
*                                                                               
SVALL    B     EXITL                                                            
         DROP  R2,R4                                                            
*                                                                               
SUBACTS  DS    0H              *** TABLE OF VALID SUB-ACTIONS ***               
         DC    AL2(FU@DEL-OVERWRKD,FL@DEL-OVERWRKD),AL4(SUB_DEL)                
         DC    AL2(FU@CAN-OVERWRKD,FL@CAN-OVERWRKD),AL4(SUB_DEL)                
         DC    AL2(EOT)                                                         
*                                                                               
SUBACTSD DSECT                                                                  
SUBUPR   DS    AL2                 DISPLACEMENT TO UPPERCASE NAME               
SUBLWR   DS    AL2                 DISPLACEMENT TO LOWER CASE NAME              
SUBRTN   DS    AL4                 DISPLACEMENT TO VALIDATION ROUTINE           
SUBACTLQ EQU   *-SUBACTSD                                                       
*                                                                               
FILREQ   CSECT                                                                  
         SPACE 2                                                                
***********************************************************************         
* ROUTINE TO DELETE A LINE FROM THE LIST                              *         
***********************************************************************         
         SPACE 1                                                                
SUB_DEL  L     R3,ATLST                                                         
         USING TLSTD,R3                                                         
         OI    LSLNIND1,LSLNIDEL   TELL CONTROLELR TO DELETE LINE               
*                                                                               
         GOTOX VDMGR,BOPARM,DMRDIR,GCREQFIL,TLRDA,AIOREC                        
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                RECORD NOT FOUND                             
*                                                                               
         L     R2,AIOREC                                                        
         USING REQHDRD,R2                                                       
         XR    RE,RE                                                            
         IC    RE,REQFLAG                                                       
         SRL   RE,4                                                             
         LA    RE,1(RE)                                                         
         LA    R2,REQEOH           FIRST DATA CARD                              
         MVC   0(2,R2),=CL2'99'    CONVENTION FOR DELETED CARD                  
         LA    R2,80(R2)                                                        
         BCT   RE,*-10                                                          
*                                                                               
         GOTOX (RF),(R1),=C'DMWRT'                                              
         B     EXITOK                                                           
         DROP  R2,R3                                                            
         SPACE 2                                                                
***********************************************************************         
* LIST OBJECT                                                         *         
*                                                                     *         
* P1 HOLDS EQUATED OBJECT                                             *         
* P2 HOLDS EQUATED VERB                                               *         
* P3 HOLDS CURRENT KEY BUILD AREA                                     *         
* P4 HOLDS PREVIOUS KEY                                               *         
***********************************************************************         
         SPACE 1                                                                
LIST     LM    R0,R3,SVPARMS                                                    
         LA    RF,LISTABL                                                       
         B     ITER                                                             
*                                                                               
LISTABL  DC    AL1(LGETFRST),AL1(0,0,0),AL4(FLST)                               
         DC    AL1(LGETNEXT),AL1(0,0,0),AL4(BLST)                               
         DC    AL1(LTSARDIR),AL1(0,0,0),AL4(TSARDIR)                            
         DC    AL1(LTSARFIL),AL1(0,0,0),AL4(TSARFIL)                            
         DC    AL1(LLSTFRST),AL1(0,0,0),AL4(FTFLST)                             
         DC    AL1(LINIT),AL1(0,0,0),AL4(ILST)                                  
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* INITIALISE LIST                                                     *         
***********************************************************************         
         SPACE 1                                                                
ILST     OI    LSSTAT1,LSSTSAR                                                  
         B     EXITOK                                                           
         SPACE 2                                                                
***********************************************************************         
* FIRST TIME FOR LIST BUILD                                           *         
***********************************************************************         
         SPACE 1                                                                
FTFLST   XC    THISNUM,THISNUM                                                  
         XC    TOTNUM,TOTNUM                                                    
         B     EXITOK                                                           
         SPACE 2                                                                
***********************************************************************         
* FIRST TIME FOR LIST PAGE                                            *         
***********************************************************************         
         SPACE 1                                                                
FLST     XC    THISADDR,THISADDR                                                
         MVC   THISADDR(L'REQCDE),REQCDE                                        
         MVI   THISADDR+3,FF                                                    
         B     BLST03                                                           
         SPACE 2                                                                
***********************************************************************         
* BUILD LIST                                                          *         
***********************************************************************         
         SPACE 1                                                                
BLST     OC    TOTNUM,TOTNUM       WITHIN RECORD CURRENTLY?                     
         BZ    BLST02              NO                                           
*                                                                               
         LH    RF,THISNUM          INCREMENT CURRENT PROCESS COUNT              
         LA    RF,1(RF)                                                         
         STH   RF,THISNUM                                                       
         CLC   THISNUM,TOTNUM      STILL WITHIN RECORD?                         
         BL    EXITOK              YES                                          
*                                                                               
BLST02   L     RF,AIOREC           SET A(NEXT RECORD)                           
         USING REQHDRD,RF                                                       
         MVC   THISADDR,REQLINK                                                 
         DROP  RF                                                               
*                                                                               
         XC    TOTNUM,TOTNUM                                                    
         XC    THISNUM,THISNUM                                                  
*                                                                               
BLST03   GOTOX VDMGR,BOPARM,DMRDIR,GCREQFIL,THISADDR,AIOREC                     
         CLI   8(R1),0                                                          
         BE    BLST04                                                           
         XC    THISADDR,THISADDR                                                
         B     EXITL               END OF FILE                                  
*                                                                               
BLST04   L     RF,AIOREC           SET NUMBER OF CARDS IN RECORD                
         USING REQHDRD,RF                                                       
         XR    R1,R1                                                            
         IC    R1,REQFLAG                                                       
         SRL   R1,4                                                             
         STH   R1,TOTNUM                                                        
*                                                                               
         CLC   REQCODE,REQCDE                                                   
         BNE   BLST                                                             
*                                                                               
         TM    CUSTAT,CUSDDS                                                    
         BZ    BLST06                                                           
         CLI   FAGY,C'Y'           U=FILTER                                     
         BNE   BLST06                                                           
         CLC   =X'FFFF',FAGYF      U=ALL                                        
         BE    BLST08                                                           
         CLC   REQAGY,FAGYF                                                     
         BE    BLST08                                                           
         B     BLST                                                             
*                                                                               
BLST06   CLC   REQAGY,CUAALF       MATCH AGENCY ALPHA                           
         BNE   BLST                                                             
*                                                                               
BLST08   XC    BOHALF1,BOHALF1                                                  
         LA    R4,BLOCK                                                         
         USING SCANBLKD,R4                                                      
*                                                                               
BLST10   CLI   SC1STLEN,0          END OF FILTERS                               
         BE    BLSTX                                                            
         CLI   SC1STLEN,1                                                       
         BNE   BLST12                                                           
         CLI   SC1STFLD,C'U'       U=FILTER?                                    
         BNE   BLST12                                                           
         LA    R4,SCBLKLQ(R4)      YES - IGNORE IT                              
         B     BLST10                                                           
*                                                                               
BLST12   LH    R1,BOHALF1                                                       
         MH    R1,=H'80'                                                        
         LA    R1,REQFRST(R1)                                                   
         LA    R0,67(R1)           END OF FIELD                                 
         ST    R0,QEND                                                          
*                                                                               
BLST13   PACK  BODUB1,2(2,R1)                                                   
         CVB   RE,BODUB1                                                        
         BCTR  RE,0                                                             
         LA    R3,4(R1)            R3 POINTS TO DATA                            
*                                                                               
         MVC   BOWORK1,BCSPACES                                                 
         ICM   R5,15,QALSTFLD      LAST FIELD REACHED                           
         BZ    BLST14                                                           
         CLC   0(2,R5),0(R1)       CONTINUATION CARD?                           
         BNE   BLST14              NO                                           
*                                                                               
         PACK  BODUB1,2(2,R5)                                                   
         CVB   RE,BODUB1                                                        
         LA    R3,BOWORK1(RE)                                                   
         BCTR  RE,0                                                             
         EX    RE,*+4                                                           
         MVC   BOWORK1(0),4(R5)                                                 
*                                                                               
         PACK  BODUB1,2(2,R1)                                                   
         CVB   RE,BODUB1                                                        
         BCTR  RE,0                                                             
         EX    RE,*+4                                                           
         MVC   0(0,R3),4(R1)                                                    
         B     BLST20                                                           
*                                                                               
BLST14   CLC   =C'DDS,',0(R3)                                                   
         BNE   BLST16                                                           
         SH    RE,=H'4'                                                         
         LA    R3,4(R3)                                                         
         B     BLST18                                                           
*                                                                               
BLST16   CLC   =C'OV,',0(R3)                                                    
         BE    *+14                                                             
         CLC   =C'ON',0(R3)                                                     
         BNE   BLST18                                                           
         SH    RE,=H'3'                                                         
         LA    R3,3(R3)                                                         
*                                                                               
BLST18   EX    RE,*+4                                                           
         MVC   BOWORK1(0),0(R3)                                                 
*                                                                               
BLST20   ST    R1,QALSTFLD                                                      
         XR    RE,RE                                                            
         IC    RE,SC1STLEN                                                      
         CLI   SC1STLEN,3          COMPARE FOR AT LEAST 3 CHARS                 
         BH    *+8                                                              
         LA    RE,3                                                             
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         BE    BLST24                                                           
         CLC   BOWORK1(0),SC1STFLD                                              
*                                                                               
         PACK  BODUB1,2(2,R1)      NO MATCH - TRY NEXT FIELD                    
         CVB   RE,BODUB1                                                        
         LA    R1,4(RE,R1)                                                      
         CLI   0(R1),C'*'          END OF REQUEST?                              
         BE    BLST                                                             
         LA    R1,1(R1)                                                         
         C     R1,QEND                                                          
         BNL   BLST22                                                           
         CLI   0(R1),C' '          END OF CARD?                                 
         BNE   BLST13                                                           
*                                                                               
BLST22   LH    R1,BOHALF1                                                       
         LA    R1,1(R1)                                                         
         STH   R1,BOHALF1                                                       
         B     BLST12                                                           
*                                                                               
BLST24   LA    R4,SCBLKLQ(R4)                                                   
         XC    QALSTFLD,QALSTFLD                                                
         B     BLST10                                                           
*                                                                               
BLSTX    XC    BOHALF1,BOHALF1                                                  
         XC    QALSTFLD,QALSTFLD                                                
         B     EXITOK                                                           
         DROP  RF                                                               
         SPACE 2                                                                
***********************************************************************         
* GET TSAR RECORD INFORMATION FROM DIRECTORY RECORD                   *         
***********************************************************************         
         SPACE 1                                                                
         USING REQHDRD,R2                                                       
         USING TLSTD,R3                                                         
TSARDIR  L     R3,ATLST                                                         
         MVC   TLRLEN,=AL2(TLLENQ)                                              
         B     EXITOK                                                           
         DROP  R2,R3                                                            
         SPACE 2                                                                
***********************************************************************         
* GET TSAR RECORD INFORMATION FROM FILE RECORD                        *         
***********************************************************************         
         SPACE 1                                                                
         USING REQHDRD,R2                                                       
         USING TLSTD,R3                                                         
TSARFIL  LH    RF,THISNUM          CURRENT PROCESS NUMBER (ZERO BASED)          
         MH    RF,=H'80'           FIXED WIDTH CARDS                            
         LA    RF,REQEOH(RF)       RF POINTS TO CURRENT REQUEST CARD            
         LA    RE,TLCARD                                                        
*                                                                               
         MVC   0(80,RE),0(RF)                                                   
*                                                                               
         MVC   TLAGY,REQAGY        SET AGENCY                                   
         MVC   TLORIG,REQORIG      SET ORIGIN                                   
*                                                                               
         MVC   TLRDA,THISADDR                                                   
         MVC   TLNUMBR,THISNUM                                                  
         B     EXITOK                                                           
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* LITERALS & CONSTANTS                                                *         
***********************************************************************         
         SPACE 1                                                                
         LTORG                                                                  
FF       EQU   X'FF'                                                            
FFFF     EQU   X'FFFF'                                                          
REQUEST  DC    CL8'REQUEST'                                                     
DMRDIR   DC    CL8'DMRDIR '                                                     
REQCDE   DC    CL2'NV'                PUT THIS IN W/S                           
*                                                                               
         DS    0D                                                               
DCLIST   DCDDL GE#DEL,3,L                                                       
         DCDDL GE#CAN,3,L                                                       
         DC    X'00'                                                            
         SPACE 1                                                                
***********************************************************************         
* OVERLAY WORKING STORAGE                                             *         
***********************************************************************         
         SPACE 1                                                                
OVERWRKD DSECT                                                                  
CALLR1   DS    A                                                                
SVPARMS  DS    0XL24                                                            
SVPARMS1 DS    A                                                                
SVPARMS2 DS    A                                                                
SVPARMS3 DS    A                                                                
SVPARMS4 DS    A                                                                
SVPARMS5 DS    A                                                                
SVPARMS6 DS    A                                                                
*                                                                               
TOTNUM   DS    H                                                                
THISNUM  DS    H                                                                
THISADDR DS    F                                                                
*                                                                               
QEND     DS    A                                                                
QALSTFLD DS    A                                                                
*                                                                               
LSNTRY   DS    XL(SUBACTLQ)        SINGLE ENTRY ACTION                          
LMNTRY   DS    XL(SUBACTLQ)        MULTIPLE ENTRY ACTION                        
LMCOUNT  DS    XL(SUBACTLQ)        REPEAT COUNT FOR MULTIPLES                   
*                                                                               
FAGY     DS    X                                                                
FAGYF    DS    XL2                                                              
*                                                                               
DSLISTU  DS    0F                                                               
FU@DEL   DS    CL3                                                              
FU@CAN   DS    CL3                                                              
*                                                                               
DSLISTL  DS    0F                                                               
FL@DEL   DS    CL3                                                              
FL@CAN   DS    CL3                                                              
*                                                                               
BLOCK    DS    10CL(SCBLKLQ)                                                    
BLOCKLQ  EQU   *-BLOCK                                                          
*                                                                               
REQHDRD  DSECT                                                                  
       ++INCLUDE DMREQHDR                                                       
REQCODE  DS    CL2                                                              
REQAGY   DS    CL2                                                              
         DS    CL1                                                              
REQNMBR  DS    CL6                                                              
         DS    CL1                                                              
REQFRST  DS    0C                                                               
*                                                                               
MYSAVED  DSECT                                                                  
SVFVIFLD DS    CL(L'FVIFLD)                                                     
*                                                                               
*        GEFILWORK                                                              
         PRINT OFF                                                              
       ++INCLUDE GEFILWORK                                                      
         PRINT ON                                                               
*        DDDDEQUS                                                               
         PRINT OFF                                                              
       ++INCLUDE DDDDEQUS                                                       
         PRINT ON                                                               
*        DDSCANBLKD                                                             
         PRINT OFF                                                              
       ++INCLUDE DDSCANBLKD                                                     
         PRINT ON                                                               
*                                                                               
TLSTD    DSECT                                                                  
         ORG   TLKSRT                                                           
TLAGY    DS    XL2                 AGENCY                                       
TLORIG   DS    XL2                 ORIGIN                                       
         ORG   TLUSER                                                           
TLNUMBR  DS    XL2                                                              
TLCARD   DS    CL80                REQUEST CARD                                 
TLLENQ   EQU   *-TLSTD                                                          
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'056GEFILREQ  01/06/97'                                      
         END                                                                    
