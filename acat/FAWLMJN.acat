*          DATA SET FAWLMJN    AT LEVEL 002 AS OF 04/27/10                      
*CATALP FAWLMJN                                                                 
         TITLE 'WLMJN - INTERFACE TO IBM WLM JOIN ENCLAVE'                      
********************************************************************            
* FACPACK - INTERFACE TO WORKLOAD MANAGER JOIN AN EXISTING ENCLAVE              
* MODULE:   FAWLMJN                                                             
*                                                                               
*                                                                               
* PROPERTIES:                                                                   
*    PROBLEM STATE                                                              
*    ADDRESSING MODE= 31                                                        
*    PRIMARY ASC MODE SAC 0                                                     
*    DISPATCHABLE UNIT = TCB                                                    
*                                                                               
* INVOCATION:                                                                   
*        SAM31                                                                  
*        SAC   0                                                                
*        GOTO1 VWLMJN,WLMPARM,C'WLJN'                                           
*                                                                               
* PROCESSING:                                                                   
* - ENCLAVE TOKEN MUST BE PRESERVED                                             
* - ESTABLISH STANDEAD DDS LINKAGE                                              
* - UPON ENTRY REGISTER 1 CONTAINS THE ADDRESS OF THE PARAMETER                 
*   REQUEST BLOCK.                                                              
* - ESTABLISH REGISTER 7 AS THE REQUEST BLOCK BASE REGISTER                     
* - SWITCH TO SUPERVISOR STATE                                                  
* - ISSUE IWMEJOIN TO JOIN AN EXISTING ENCLAVE                                  
* - SWITCH BACK TO PROBLEM STATE                                                
* - RETURN TO CALLER                                                            
*                                                                               
*IWMJOIN                                                                        
*THE PURPOSE OF THIS SERVICE IS TO ALLOW THE TASK INVOKING THIS                 
*SERVICE TO JOIN AN ENCLAVE FOR THE PURPOSE OF PERFORMANCE MANAGEMENT.          
*THE SCOPE OF THIS SERVICE AFFECTS ONLY A SINGLE TASK AT THE TIME THE           
*SERVICE IS INVOKED.                                                            
*                                                                               
*HOWEVER ANY TASKS WHICH ARE ATTACHED BY THE CURRENT TASK SUBSEQUENTLY          
*WILL ALSO BECOME PART OF THE ENCLAVE ENVIRONMENT.                              
*THIS INHERITANCE OF THE ENCLAVE ATTRIBUTE WILL APPLY TO ANY FURTHER            
*LEVEL OF NEWLY ATTACHED SUBTASKS AS WELL.                                      
*                                                                               
*HOWEVER SUBTASKS WHICH EXIST AT THE TIME THIS SERVICE IS INVOKED               
*WILL NOT BECOME PART OF THE ENCLAVE ENVIRONMENT NOR WILL ANY SUBTASKS          
*WHICH ARE CREATED SUBSEQUENTLY BY THESE NON-ENCLAVE TASKS BECOME PART          
*OF THE ENCLAVE ENVIRONMENT, UNLESS THEY EXPLICITLY JOIN.                       
*                                                                               
*                                                                               
* XXXX0857    NAME: IwmRsnCodeAlreadyInEnclave                                  
*                                                                               
*             MEANING: CURRENT DISPATCHABLE WORKUNIT                            
*             IS ALREADY IN AN ENCLAVE.                                         
*                                                                               
*             ACTION:  AVOID REQUESTING THIS FUNCTION                           
*             WHILE THE CALLER IS ALREADY IN AN                                 
*             ENCLAVE.                                                          
*                                                                               
*                                                                               
********************************************************************            
         USING WLMED,R7                                                         
FAWLMJN  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,**WLMJN                                                        
         LR    R7,R1               PARAMETER LIST ADDRESS                       
         XR    R0,R0                                                            
         ST    R0,ECLVRETC         CLEAR RETURN CODE                            
         ST    R0,ECLVRSNC         CLEAR REASON CODE                            
         ICM   R0,15,ECLVTOKEN                                                  
         BZ    EXIT                Do nothing                                   
*                                                                               
         MODESET MODE=SUP          Switch to supervisor state                   
*                                                                               
         IWMEJOIN ETOKEN=ECLVTOKEN,                                    +        
               RETCODE=ECLVRETC,                                       +        
               RSNCODE=ECLVRSNC,                                       +        
               MF=(E,JOINLIST,COMPLETE)                                         
*PVD        ENCLAVESERVER=YES,                                     +            
*                                                                               
         MODESET MODE=PROB         Switch back to problem state                 
*                                                                               
         ICM   RF,15,ECLVRETC      Check for errors                             
         JNZ   ERROR10             Return code error           WLM              
         OC    ECLVRSNC,ECLVRSNC   REASON CODE OK ?            WLM              
         JZ    EXIT                All okay                                     
*                                                                               
ERROR10  STM   R0,RF,DUMPREGS                                                   
         MVC   ABEND#,=CL3'916'                                                 
         AP    ERRORS,=P'1'                                                     
         MVC   MSGFROM,0(R7)       Calling label                                
                                                                                
         LA    R0,8                                                             
         LA    R1,MSGRTC           Format return code                           
ERROR12  XR    RE,RE                                                            
         SAM24 ,                   32 bit shift between regs                    
         SLDL  RE,4                                                             
         SAM31 ,                                                                
         LA    RE,HEX2CHR(RE)                                                   
         MVC   0(1,R1),0(RE)                                                    
         LA    R1,1(,R1)                                                        
         BRCT  R0,ERROR12                                                       
                                                                                
         L     RF,ECLVRSNC         Reason code                                  
         LA    R0,8                                                             
         LA    R1,MSGRSNC          Format reason code                           
ERROR14  XR    RE,RE                                                            
         SAM24 ,                   32 bit shift between regs                    
         SLDL  RE,4                                                             
         SAM31 ,                                                                
         LA    RE,HEX2CHR(RE)                                                   
         MVC   0(1,R1),0(RE)                                                    
         LA    R1,1(,R1)                                                        
         BRCT  R0,ERROR14                                                       
                                                                                
         LA    R3,MSG1                                                          
         WTO   TEXT=(R3)                                                        
                                                                                
EXIT     XMOD1 1                                                                
                                                                                
         LTORG ,                                                                
         EJECT ,                                                                
***********************************************************************         
* Constants                                                                     
***********************************************************************         
         DS    0D                                                               
         DC    CL4'WR#='                                                        
WARNS    DC    PL8'0'                                                           
         DC    CL4'ER#='                                                        
ERRORS   DC    PL8'0'                                                           
         DC    CL6'ABEND='                                                      
ABEND#   DC    CL3' '                                                           
         DC    CL3' '                                                           
         DC    C'DUMPREGS'                                                      
DUMPREGS DC    18A(0)                                                           
                                                                                
HEX2CHR  DC    C'0123456789ABCDEF'                                              
                                                                                
***********************************************************************         
* RTC = 8, RSN = 857 is already in an enclave                                   
***********************************************************************         
MSG1     DC    AL2(MSG1LNQ)                                                     
MSG1TXT  DC    C'**U916** FAWLMJN Join Enclave error      '                     
         ORG   *-5                                                              
MSGFROM  DS    CL4                                                              
         ORG                                                                    
         DC    C' RTC='                                                         
MSGRTC   DC    C'00000000'                                                      
         DC    C' RSN='                                                         
MSGRSNC  DC    C'00000000'                                                      
MSG1LNQ  EQU   *-MSG1TXT                                                        
*                                                                               
         DS    0D                                                               
         IWMEJOIN PLISTVER=IMPLIED_VERSION,MF=(L,JOINLIST)                      
         EJECT                                                                  
***********************************************************************         
* DSECTS                                                                        
***********************************************************************         
* FAWLMED                                                                       
       ++INCLUDE FAWLMED                                                        
* CVT                                                                           
         CVT DSECT=YES                                                          
* IWMYCON                                                                       
         IWMYCON                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002FAWLMJN   04/27/10'                                      
         END                                                                    
