*          DATA SET PPREP0602  AT LEVEL 086 AS OF 05/01/02                      
*CATALP PP0602N                                                                 
         TITLE 'PP0602 - PRINTPAK DETAIL BILLING'                               
PP0602   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,PP0602                                                         
         SPACE 2                                                                
         L     RA,0(R1)                                                         
         USING PPWORKD,RA                                                       
         L     RC,PPFILEC                                                       
         LA    R9,4095(RC)                                                      
         LA    R9,1(R9)                                                         
         USING PPFILED,RC,R9                                                    
         LA    R8,SPACEND                                                       
         USING PBILWRKD,R8                                                      
         SPACE 2                                                                
*                                                                               
         CLI   MODE,PROCBUY                                                     
         BE    PRBY                                                             
         CLI   MODE,UNSRTREC                                                    
         BE    CHKSRT                                                           
         CLI   MODE,LBUYPUB                                                     
         BE    LBYPUB                                                           
         CLI   MODE,FBUYEST                                                     
         BE    FBYE                                                             
         CLI   MODE,LBUYEST                                                     
         BE    LBYE                                                             
         CLI   MODE,FBUYPRO                                                     
         BE    FBYP                                                             
         CLI   MODE,LBUYPRO                                                     
         BE    LBYP                                                             
         CLI   MODE,LBUYDST                                                     
         BE    LBYD                                                             
         CLI   MODE,LBUYREG                                                     
         BE    LBYR                                                             
         CLI   MODE,LBUYDIV                                                     
         BE    LBYV                                                             
         CLI   MODE,FBUYCLI                                                     
         BE    FBYC                                                             
         CLI   MODE,LBUYCLI                                                     
         BE    LBYC                                                             
         CLI   MODE,FBUYREQ                                                     
         BE    FBYQ                                                             
         CLI   MODE,LBUYREQ                                                     
         BE    LBYQ                                                             
         CLI   MODE,RUNFRST                                                     
         BE    RFRST                                                            
         CLI   MODE,RUNLAST                                                     
         BE    RLAST                                                            
EXIT     DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
RFRST    DS    0H                                                               
         RELOC RELO                                                             
*                                                                               
FBYQ     DS    0H                                                               
FBYC     DS    0H                                                               
FBYP     DS    0H                                                               
FBYE     DS    0H                                                               
LBYQ     DS    0H                                                               
*                                                                               
         L     R3,RELO                                                          
         GOTO1 =V(START),RR=R3                                                  
*                                                                               
         CLI   MODE,FBUYCLI                                                     
         BNE   EXIT                                                             
*                                                                               
         MVI   PUBACT,C'N'         RESET ACTIVE SWS                             
         MVI   DSTACT,C'N'                                                      
         MVI   REGACT,C'N'                                                      
*                                  SET VARIOUS OPTS FROM PROFILES               
         MVC   JOBOPT,PCLTPROF+15                                               
         MVC   PREVOPT,PCLTPROF+17                                              
         CLI   PREVOPT,C'0'                                                     
         BNE   *+8                                                              
         MVI   PREVOPT,C'N'                                                     
         MVI   DATOPT,C'0'                                                      
         OC    PROGPROF,PROGPROF                                                
         BZ    EXIT                                                             
         MVC   JOBOPT,PROGPROF+3                                                
         CLI   JOBOPT,C'N'                                                      
         BNE   *+8                                                              
         MVI   JOBOPT,C'0'         DEFAULT IS 0                                 
         MVC   PREVOPT,PROGPROF+4                                               
         MVC   DATOPT,PROGPROF+5                                                
*                                                                               
         B     EXIT                                                             
         SPACE 2                                                                
LBYPUB   DS    0H                  LAST FOR PUB                                 
         BAS   RE,PBEND                                                         
         B     EXIT                                                             
         SPACE 2                                                                
LBYE     DS    0H                  LAST FOR EST                                 
         CLI   ESTSW,C'A'          EST = ALL                                    
         BNE   EXIT                                                             
         BAS   RE,DBINV                                                         
         B     EXIT                                                             
         SPACE 2                                                                
LBYP     DS    0H                  LAST FOR PROD                                
         CLI   PRDSW,C'P'                                                       
         BH    EXIT                                                             
         BAS   RE,DBINV            IF SINGLE PROD-DO INVOICE NOW                
         B     EXIT                                                             
         SPACE 2                                                                
LBYD     DS    0H                  LAST FOR DIST                                
         BAS   RE,DSTEND                                                        
         B     EXIT                                                             
         SPACE 2                                                                
LBYR     DS    0H                  LAST FOR REGION                              
         BAS   RE,REGEND                                                        
         B     EXIT                                                             
         SPACE 2                                                                
LBYV     DS    0H                  LAST FOR DIVISION                            
         CLI   QDIV,C' '                                                        
         BE    EXIT                                                             
         BAS   RE,DBINV                                                         
         B     EXIT                                                             
         SPACE 2                                                                
LBYC     DS    0H                  LAST FOR CLIENT                              
         BAS   RE,DBINV                                                         
         B     EXIT                                                             
         SPACE 2                                                                
RLAST    DS    0H                                                               
         L     R3,RELO                                                          
         GOTO1 =V(INVREG),RR=R3                                                 
*                                                                               
         B     EXIT                                                             
         SPACE 2                                                                
*        UNSRTREC - CHECK REG/DST OK                                            
CHKSRT   DS    0H                                                               
         CLC   QREGION(6),SPACES                                                
         BE    CKS13                                                            
*                               IF BY REG OR DIST, NO MULTIPLE ASSIGN           
         L     R2,ALTLREC                                                       
         LTR   R2,R2                                                            
         BZ    CKS13               NO LTLREC                                    
         LA    R2,33(R2)                                                        
         MVI   ELCODE,X'71'                                                     
         MVI   BYTE,0                                                           
         CLI   0(R2),X'71'                                                      
         BE    CKS12B                                                           
*                                                                               
CKS12    DS    0H                                                               
         BAS   RE,NEXTEL                                                        
         BNE   CKS13                                                            
*                                                                               
CKS12B   DS    0H                                                               
         USING PUBDSTEL,R2                                                      
         CLC   PUBDCLT,PCLTKCLT                                                 
         BNE   CKS12                                                            
         LA    RF,PDIVKDIV                                                      
         CLI   QDIV,C' '                                                        
         BNE   *+8                                                              
         LA    RF,PPRDDIV                                                       
         CLC   PUBDDIV,0(RF)                                                    
         BNE   CKS12                                                            
         CLI   BYTE,0                                                           
         BE    CKS12F                                                           
*                                                                               
         MVC   P(80),QPROG                                                      
         MVC   PSECOND(80),=C'**MULTIPLE REG/DIST ASSGNS - INVALID REG/D        
               ST**'                                                            
         GOTO1 REPORT                                                           
         MVI   MODE,LBUYREQ                                                     
         B     EXIT                                                             
*                                                                               
CKS12F   DS    0H                                                               
         MVI   BYTE,1                                                           
         B     CKS12                                                            
*                                                                               
CKS13    DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
*                                  PROCESS BUY                                  
         SPACE 2                                                                
PRBY     DS    0H                                                               
         CLC   THISYM,PBDBDATE     MONTH BREAK                                  
         BE    PRBY2                                                            
         CLI   THISYM,0                                                         
         BE    PRBY1                                                            
         BAS   RE,DBINV            FINISH INVOICE                               
         MVI   DONESW,0                                                         
PRBY1    DS    0H                                                               
         MVI   CDSKIP,C'N'         RESET CD SW                                  
         MVC   THISYM,PBDBDATE                                                  
         MVI   PBSW,0              SET PUB NOT PRINTED                          
         OC    MASTPRD,MASTPRD                                                  
         BNZ   PRBY2                                                            
         XC    WORK,WORK                                                        
         GOTO1 AGETFORM,DMCB,WORK       GET MAST HDR                            
PRBY2    DS    0H                                                               
         CLI   PCLTPROF+13,C'0'    PAID OR ORDERED                              
         BE    PRBY3                                                            
         MVC   BUYGRS(12),GROSS    ORDERED                                      
         B     PRBY4                                                            
PRBY3    DS    0H                                                               
         MVC   BUYGRS(12),PGROSS   PAID                                         
PRBY4    DS    0H                                                               
         TM    PBUYCNTL,X'80'      IF DELETED,                                  
         BZ    PRBY4B                                                           
         OC    BGROSS(16),BGROSS   MUST HAVE BEEN BILLED                        
         BZ    EXIT                                                             
         XC    BUYGRS(12),BUYGRS                                                
PRBY4B   DS    0H                                                               
         CLC   BUYGRS(12),BGROSS    TEST ALL BILLED                             
         BE    EXIT                                                             
*                                  CHECK DATE                                   
PRBY5    DS    0H                                                               
         CLC   PBDBDATE,BEND                                                    
         BH    EXIT                                                             
         CLC   PBDBDATE,BSTART                                                  
         BNL   PRBY6                                                            
         CLC   PBDBDATE(2),BSTART                                               
         BNL   EXIT                                                             
         CLI   PRIORSW,C'N'                                                     
         BE    EXIT                                                             
*                                                                               
PRBY6    DS    0H                                                               
         MVC   IRCTOTS(12),BUYGRS  SAVE GROSS,CD,AC                             
         GOTO1 APOST,DMCB,PBUYREC                                               
*                                       POST SUBTRACTS OUT BILLED               
*                                  ROLL TO TOTALS                               
         LA    R2,VENTOTS                                                       
         LA    R3,4                VEN,DST,REG,INV                              
PRBY8    DS    0H                                                               
         LM    R5,R7,0(R2)                                                      
         A     R5,BUYGRS                                                        
         A     R6,BUYAC                                                         
         A     R7,BUYCD                                                         
         STM   R5,R7,0(R2)                                                      
         LA    R2,12(R2)                                                        
         BCT   R3,PRBY8                                                         
*                                                                               
         MVC   BUYGRS(12),IRCTOTS  RESTORE GROSS,CD AC, FOR PRINTING            
*                                                                               
*              TEST SKIP PRINTING OF BUYS WITH ONLY CD CHANGED                  
*                                                                               
         CLI   PROGPROF+8,C'Y'     TEST PROFILE OPTION TO SKIP                  
         BNE   PRBY12              NO                                           
         LA    RF,MASTHDR                                                       
         USING HDRKEYD,RF                                                       
         TM    BILBASA,X'04'       TEST CD IN FORMULA BASE                      
         BNZ   PRBY12              YES-PRINT ITEM                               
         CLC   BUYGRS(8),BGROSS    TEST CD ONLY CHANGED                         
         BNE   PRBY12              NO                                           
         MVI   CDSKIP,C'Y'         SET NOT TO PRINT CD TOTAL                    
         B     EXIT                SKIP THIS BUY                                
         DROP RF                                                                
PRBY12   DS    0H                                                               
         MVI   HEADSW,C'D'                                                      
         BAS   RE,FMTBUY                                                        
         MVI   PUBACT,C'Y'         SET ACTIVE SWS                               
         MVI   DSTACT,C'Y'                                                      
         MVI   REGACT,C'Y'                                                      
*                                                                               
         B     EXIT                                                             
         EJECT                                                                  
*                             FORMAT BUY LINE & PRINT                           
         SPACE 2                                                                
FMTBUY   NTR1                                                                   
         SPACE 2                                                                
         CLI   PBSW,C'P'           TEST HAVE PRINTED PUB                        
         BE    FB2                 YES                                          
         BAS   RE,PBFRST                                                        
*                                                                               
FB2      DS    0H                                                               
         LA    R5,PPBYOWRK                                                      
         USING PPBYOUTD,R5                                                      
         LA    RF,PBUYREC                                                       
         ST    RF,PBYOINPT                                                      
         MVC   PBYODTCN,DATCON                                                  
         LA    RF,GROSS                                                         
         ST    RF,PBYOVALS                                                      
         MVI   PBYOCTL,X'2A'            ZZZ + COMS + NO LINE NO.                
         GOTO1 PPBYOUT,DMCB,PPBYOUTD                                            
*                                                                               
         L     RF,PBYOLNS               LINES NEEDED                            
         OC    BGROSS(16),BGROSS                                                
         BZ    FB2B2               NO PREVIOUS                                  
         CLI   PREVOPT,C'N'                                                     
         BNE   FB2B                                                             
         LA    RF,1(RF)            ONE LINE FOR ALL                             
         B     FB2B2                                                            
FB2B     DS    0H                  PREV BILLS                                   
         MVI   ELCODE,X'26'                                                     
         LA    R2,PBUYREC+33                                                    
         BAS   RE,NEXTEL                                                        
         BNE   *+12                                                             
         LA    RF,1(RF)            ONE LINE FOR EACH                            
         B     *-12                                                             
*                                                                               
FB2B2    DS    0H                                                               
         TM    JOBOPT,X'01'                                                     
         BZ    *+8                                                              
         LA    RF,1(RF)            AD NO. OR COPY NO                            
         TM    JOBOPT,X'02'                                                     
         BZ    *+8                                                              
         LA    RF,2(RF)            CAPTION                                      
*                                                                               
         CLI   DATOPT,C'0'         ADDITIONAL DATE AS COMMENT                   
         BE    *+8                                                              
         LA    RF,1(RF)                                                         
*                                                                               
FB2X     DS    0H                                                               
*                                                                               
         STC   RF,LNEED                                                         
         STC   RF,PRINTSW                                                       
         CLC   P,SPACES                                                         
         BE    FB3                                                              
         LA    RF,3(RF)                                                         
         STC   RF,LNEED                                                         
         MVI   SPACING,2                                                        
         GOTO1 APRNT                                                            
*                                                                               
FB3      DS    0H                                                               
         MVC   P+2(8),PBYOINS                                                   
         CLI   PBYOINS2,C' '       TEST 2ND INS DTE                             
         BNH   *+14                                                             
         MVI   PSECOND+1,C'+'                                                   
         MVC   PSECOND+2(8),PBYOINS2                                            
*                                  PRD-EST                                      
FB4      DS    0H                                                               
         CLI   ESTSW,C'E'                                                       
         BNH   FB5                                                              
         MVC   HALF,PBUYKEST                                                    
         LH    R0,HALF                                                          
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  WORK(3),DUB                                                      
*                                                                               
         MVC   P+13(3),WORK                                                     
         CLI   PRDSW,C'T'                                                       
         BL    FB6                                                              
         MVC   P+11(3),PBUYKPRD                                                 
         MVC   P+15(3),WORK                                                     
         MVI   P+14,C'-'                                                        
         B     FB6                                                              
*                                                                               
FB5      DS    0H                                                               
         CLI   PRDSW,C'T'                                                       
         BL    FB6                                                              
         MVC   P+13(3),PBUYKPRD                                                 
*                                                                               
FB6      DS    0H                                                               
         MVC   P+22(20),PBYOSPC                                                 
         MVC   PSECOND+22(20),PBYOSPC2                                          
*                                                                               
         CLI   QMEDIA,C'N'                                                      
         BNE   FB10                                                             
*                                                                               
         CLI   PBYOSPC,C' '                                                     
         BH    FB8                                                              
         MVC   P+22(7),PBYOUNTS                                                 
         MVC   P+30(8),PBYOUR                                                   
         CLC   PBYOUR,SPACES                                                    
         BE    FB8                                                              
         CLI   PCLTPROF+13,C'0'                                                 
         BNE   FB8                                                              
         CLC   PGROSS,GROSS                                                     
         BE    FB8                                                              
         LA    R7,P+38                                                          
         CLI   0(R7),C' '                                                       
         BH    *+8                                                              
         BCT   R7,*-8                                                           
         MVC   1(2,R7),=C'**'       FLAG CHANGE SINCE PAY                       
FB8      DS    0H                                                               
         LA    RF,PSECOND+22                                                    
         CLI   0(RF),C' '                                                       
         BNH   FB9                                                              
*                                                                               
         LA    RF,15(RF)                                                        
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,*-8                                                           
         LA    RF,3(RF)                                                         
*                                                                               
FB9      DS    0H                                                               
         MVC   0(11,RF),PBYOPRM                                                 
*                                                                               
*                                                                               
FB10     DS    0H                                                               
         MVI   TOTSW,0                                                          
         GOTO1 AFMTLIN,DMCB,BUYGRS                                              
*                                                                               
         GOTO1 APRNT                                                            
*                                                                               
         CLC   PBUYKPRD,=C'ZZZ'                                                 
         BNE   FB14                                                             
         CLI   PRDSW,C'T'                                                       
         BL    FB14                                                             
*                                                                               
         MVC   P+22(6),=C'ALLOC='                                               
         MVC   P+28(L'PBYOZZZ),PBYOZZZ                                          
FB13     DS    0H                                                               
         GOTO1 APRNT                                                            
*                                       PRINT ANY COMMENT                       
FB14     DS    0H                                                               
         CLI   DATOPT,C'S'                                                      
         BNE   FB14F                                                            
*                             ADDITIONAL DATE                                   
         LA    R2,PBDSDATE    SALE DATE                                         
         OC    0(3,R2),0(R2)                                                    
         BZ    FB14F                                                            
         MVC   P+22(13),=C'ON-SALE DATE='                                       
         GOTO1 DATCON,DMCB,(3,0(R2)),(5,P+36)                                   
         GOTO1 APRNT                                                            
*                                                                               
FB14F    DS    0H                                                               
         LA    R2,PBYOCOMS                                                      
         LA    R3,5                                                             
*                                                                               
FB15     DS    0H                                                               
         CLI   0(R2),C' '                                                       
         BNH   FB18                                                             
         MVC   P+22(47),0(R2)                                                   
*                                                                               
FB17     DS    0H                                                               
         GOTO1 APRNT                                                            
         LA    R2,47(R2)                                                        
         BCT   R3,FB15                                                          
*                                                                               
FB18     DS    0H                                                               
         CLI   PBYOBFD,C' '                                                     
         BNH   FB18C                                                            
         MVC   P+22(27),PBYOBFD                                                 
         GOTO1 APRNT                                                            
*                                                                               
FB18C    DS    0H                                                               
         OC    PBDJOB,PBDJOB                                                    
         BZ    FB19                                                             
         CLI   JOBOPT,C'0'    NO AD INFOR                                       
         BE    FB19                                                             
         CLI   JOBOPT,C'A'                                                      
         BE    FB18D               AD NO ONLY                                   
         XC    WORK(25),WORK                                                    
         MVC   WORK(10),PBUYKEY                                                 
         MVI   WORK+3,X'15'                                                     
         MVC   WORK+10(6),PBDJOB                                                
         CLC   PJOBREC(25),WORK                                                 
         BE    FB18CX                                                           
         MVC   SAVKEY,KEY                                                       
         MVC   KEY(25),WORK                                                     
         GOTO1 HIGH                                                             
*                                                                               
         CLC   KEY(25),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 GET,DMCB,PJOBREC                                                 
*                                                                               
         MVC   KEY,SAVKEY                                                       
         GOTO1 HIGH                                                             
*                                                                               
FB18CX   DS    0H                                                               
         TM    JOBOPT,X'30'                                                     
         BZ    FB18D                                                            
*                                       COPY NO.                                
         MVC   P+22(06),=C'COPY ='                                              
         MVC   P+22+7(17),PJOBCPY                                               
         B     FB18F                                                            
*                                       AD. NO.                                 
FB18D    DS    0H                                                               
         MVC   P+22(08),=C'AD NO. ='                                            
         MVC   P+22+9(6),PBDJOB                                                 
*                                                                               
FB18F    DS    0H                                                               
         GOTO1 APRNT                                                            
*                                                                               
         TM    JOBOPT,X'02'                                                     
         BZ    FB19                                                             
*                                  CAPTION                                      
         MVC   P+22(25),PJOBCAP1                                                
         MVC   PSECOND+22(25),PJOBCAP2                                          
         GOTO1 APRNT                                                            
*                                                                               
*                                  PREVIOUS BILLING                             
FB19     DS    0H                                                               
         OC    BGROSS(16),BGROSS                                                
         BZ    FB22                                                             
         CLI   PREVOPT,C'N'                                                     
         BNE   FB20                                                             
         MVI   TOTSW,C'('                                                       
         GOTO1 AFMTLIN,DMCB,BGROSS                                              
*                                                                               
         MVC   P+17(19),=C'(PREVIOUSLY BILLED)'                                 
         GOTO1 APRNT                                                            
         B     FB22                                                             
*                                                                               
FB20     DS    0H                                                               
         MVI   ELCODE,X'26'                                                     
         LA    R2,PBUYREC+33                                                    
FB21     DS    0H                                                               
         BAS   RE,NEXTEL                                                        
         BNE   FB22                                                             
         USING PBILELEM,R2                                                      
         CLI   PBLDATE,0                                                        
         BE    FB21                                                             
         CLI   QPRODUCT,C' '                                                    
         BE    *+14                                                             
         CLC   PBPRD,PPRDKPRD                                                   
         BNE   FB21                                                             
         CLC   PBLDATE,BTODAY                                                   
         BNE   *+14                                                             
         CLC   PBINVNO,INVNO       SKIP THIS BILL                               
         BE    FB21                                                             
*                                                                               
         MVC   P+17(20),=C'(INVOICE MM-SS-NNNN)'                                
*                                                                               
         MVC   X(12),INVNO         SAVE INVNO AND PINVNO                        
         MVC   HALF,PBINVNO                                                     
         LH    RF,HALF                                                          
         BCTR  RF,R0                                                            
         STH   RF,INVNO                                                         
         MVC   PBILKBMN+1(1),PBLDATE+1   SET BILL MO FOR GETNUM FMT             
         OI    INVNO,X'80'                                                      
         GOTO1 AGETNUM                                                          
*                                                                               
         MVC   P+17+9(10),PINVNO                                                
         MVC   INVNO(12),X         RESTORE                                      
*                                                                               
         MVI   TOTSW,C'('                                                       
         GOTO1 AFMTLIN,DMCB,PBGROSS                                             
*                                                                               
         GOTO1 APRNT                                                            
         B     FB21                                                             
*                                                                               
FB22     DS    0H                                                               
         CLI   PRINTSW,1                                                        
         BE    FB23                                                             
         GOTO1 APRNT               SKIP A LINE IF PRINTED MORE THAN 1           
*                                                                               
FB23     DS    0H                                                               
         B     EXIT                                                             
         SPACE 3                                                                
         EJECT                                                                  
*                                  FBUYPUB                                      
         SPACE 2                                                                
PBFRST   NTR1                                                                   
         MVI   PBSW,C'P'                                                        
         OC    PUBZNAME,SPACES                                                  
         LA    R4,P+1                                                           
         CLI   QMEDIA,C'N'                                                      
         BE    PBF2                                                             
*                                  MAG FORMAT                                   
         MVC   0(20,R4),PUBNAME                                                 
         MVC   132(20,R4),PUBZNAME                                              
         B     PBF8                                                             
*                                  NEWSPAPER                                    
PBF2     DS    0H                                                               
         LA    R5,132(R4)                                                       
         CLI   PUBCITY,C' '                                                     
         BNH   PBF4                                                             
         CLI   PUBSTATE,C' '                                                    
         BNH   PBF4                                                             
         LA    R5,132+4(R4)                                                     
         MVC   0(2,R4),PUBSTATE                                                 
         MVI   2(R4),C','                                                       
         MVC   4(16,R4),PUBCITY                                                 
         LA    R4,20(R4)                                                        
         CLI   0(R4),C' '                                                       
         BH    *+8                                                              
         BCT   R4,*-8                                                           
         LA    R4,3(R4)                                                         
*                                                                               
PBF4     DS    0H                                                               
         MVC   0(20,R4),PUBNAME                                                 
         MVC   0(20,R5),PUBZNAME                                                
*                                                                               
PBF8     DS    0H                                                               
         LA    R4,20(R4)                                                        
PBF8A    DS    0H                                                               
         CLI   0(R4),C' '                                                       
         BH    PF8B                                                             
         CLI   132(R4),C' '                                                     
         BH    PF8B                                                             
         BCT   R4,PBF8A                                                         
PF8B     DS    0H                                                               
         LA    R5,132(R4)                                                       
         IC    R6,PAGYPROF+12                                                   
         GOTO1 PUBEDIT,DMCB,((R6),PBUYKPUB),4(R4)                               
*                                                                               
*                                                                               
PBF8C    DS    0H                                                               
         MVI   3(R4),C'('                                                       
         LA    R4,21(R4)                                                        
         CLI   0(R4),C' '                                                       
         BH    *+8                                                              
         BCT   R4,*-8                                                           
         MVI   1(R4),C')'                                                       
*                                                                               
*                                  SEE IF HAVE CLIENT VENDOR NO.                
         LA    R2,PUBREC+33                                                     
         MVI   ELCODE,X'14'                                                     
         CLI   0(R2),X'14'                                                      
         BE    PBF10                                                            
PBF9     DS    0H                                                               
         BAS   RE,NEXTEL                                                        
         BNE   PBF11                                                            
         USING PUBREPEL,R2                                                      
PBF10    DS    0H                                                               
         CLC   PUBRPOFF,PBUYKCLT                                                
         BE    PBF10A                                                           
         CLI   PUBRPOFF,X'FF'                                                   
         BNE   PBF9                                                             
         CLC   PUBRPOFF+1(1),PCLTOFF                                            
         BNE   PBF9                                                             
*                                                                               
PBF10A   DS    0H                                                               
         CLI   PUBCVEN,X'FF'                                                    
         BE    PBF11                                                            
         OC    PUBCVEN,PUBCVEN                                                  
         BZ    PBF11                                                            
         CLC   PUBCVEN,SPACES                                                   
         BE    PBF11                                                            
         CLI   PUBCVEN,X'99'                                                    
         BH    PBF10B                                                           
         IC    R6,PAGYPROF+12                                                   
         GOTO1 PUBEDIT,DMCB,((R6),PUBCVEN),4(R5)                                
         B     PBF10D                                                           
PBF10B   DS    0H                                                               
         MVC   4(12,R5),PUBCVEN                                                 
PBF10D   DS    0H                                                               
         MVI   3(R5),C'('                                                       
         LA    R5,21(R5)                                                        
         CLI   0(R5),C' '                                                       
         BH    *+8                                                              
         BCT   R5,*-8                                                           
         MVI   1(R5),C')'                                                       
*                                  CHECK REG/DST REQ OK                         
PBF11    DS    0H                                                               
         CLC   QREGION(6),SPACES                                                
         BE    PBF13                                                            
*                               IF BY REG OR DIST, NO MULTIPLE ASSIGN           
         L     R2,ALTLREC                                                       
         LTR   R2,R2                                                            
         BZ    PBF13               NO LTLREC                                    
         LA    R2,33(R2)                                                        
         MVI   ELCODE,X'71'                                                     
         MVI   BYTE,0                                                           
         CLI   0(R2),X'71'                                                      
         BE    PBF12B                                                           
*                                                                               
PBF12    DS    0H                                                               
         BAS   RE,NEXTEL                                                        
         BNE   PBF13                                                            
*                                                                               
PBF12B   DS    0H                                                               
         USING PUBDSTEL,R2                                                      
         CLC   PUBDCLT,PCLTKCLT                                                 
         BNE   PBF12                                                            
         LA    RF,PDIVKDIV                                                      
         CLI   QDIV,C' '                                                        
         BNE   *+8                                                              
         LA    RF,PPRDDIV                                                       
         CLC   PUBDDIV,0(RF)                                                    
         BNE   PBF12                                                            
         CLI   BYTE,0                                                           
         BE    *+6                                                              
         DC    H'0'                MULTIPLE ASSIGNS                             
         MVI   BYTE,1                                                           
         B     PBF12                                                            
*                                                                               
PBF13    DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
*                                  LBUYPUB                                      
         SPACE 2                                                                
PBEND    NTR1                                                                   
         MVI   PBSW,0                                                           
         CLI   PUBACT,C'Y'                                                      
         BNE   EXIT                                                             
         MVC   P+19(14),=C'*VENDOR TOTAL*'                                      
         MVI   TOTSW,1                                                          
         GOTO1 AFMTLIN,DMCB,VENTOTS                                             
*                                                                               
         MVI   SPACING,2                                                        
         OI    LNEED,X'80'         PRINT ON SAME PAGE                           
         GOTO1 APRNT                                                            
*                                                                               
         XC    VENTOTS,VENTOTS                                                  
         MVI   PUBACT,C'N'                                                      
         B     EXIT                                                             
         EJECT                                                                  
*                                  LBUYDST                                      
         SPACE 2                                                                
DSTEND   NTR1                                                                   
         SPACE 2                                                                
         CLI   DSTACT,C'Y'                                                      
         BNE   EXIT                                                             
         CLI   QDIST,C' '                                                       
         BE    DE4                                                              
         LA    RF,MASTHDR                                                       
         USING HDRKEYD,RF                                                       
         CLI   BILRDSW,C'S'                                                     
         BE    DE6                                                              
         MVC   P+05(21),=C'** DISTRICT TOTALS **'                               
         MVI   TOTSW,2                                                          
         GOTO1 AFMTLIN,DMCB,DSTTOTS                                             
*                                                                               
         MVI   SPACING,2                                                        
         OI    LNEED,X'80'         PRINT ON SAME PAGE                           
         GOTO1 APRNT                                                            
         MVI   FORCEHED,C'Y'                                                    
DE4      XC    DSTTOTS,DSTTOTS                                                  
         MVI   DSTACT,C'N'                                                      
         B     EXIT                                                             
DE6      XC    DSTTOTS,DSTTOTS                                                  
         MVI   DSTACT,C'N'                                                      
         BAS   RE,DBINV                                                         
         B     EXIT                                                             
         EJECT                                                                  
*                                  LBUYREG                                      
         SPACE 2                                                                
REGEND   NTR1                                                                   
         SPACE 2                                                                
         CLI   REGACT,C'Y'                                                      
         BNE   EXIT                                                             
         CLI   QREGION,C' '                                                     
         BE    RE4                                                              
         LA    RF,MASTHDR                                                       
         USING HDRKEYD,RF                                                       
         CLI   BILRDSW,C'S'                                                     
         BE    RE6                                                              
         MVC   P+05(19),=C'** REGION TOTALS **'                                 
         MVI   TOTSW,2                                                          
         GOTO1 AFMTLIN,DMCB,REGTOTS                                             
*                                                                               
         MVI   FORCEHED,C'N'                                                    
         OI    LNEED,X'80'         PRINT ON SAME PAGE                           
         MVI   SPACING,3                                                        
         GOTO1 APRNT                                                            
         MVI   FORCEHED,C'Y'                                                    
RE4      XC    REGTOTS,REGTOTS                                                  
         MVI   REGACT,C'N'                                                      
         B     EXIT                                                             
RE6      XC    REGTOTS,REGTOTS                                                  
         MVI   REGACT,C'N'                                                      
         BAS   RE,DBINV                                                         
         B     EXIT                                                             
         SPACE 3                                                                
*                                                                               
NEXTEL   DS    0H                                                               
         SR    R0,R0                                                            
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         CLI   0(R2),0                                                          
         BE    NEXTEL2                                                          
         CLC   ELCODE,0(R2)                                                     
         BER   RE                                                               
         B     NEXTEL+2                                                         
NEXTEL2  LTR   RE,RE                                                            
         BR    RE                                                               
         SPACE 3                                                                
         EJECT                                                                  
         SPACE 2                                                                
DBINV    NTR1                                                                   
         SPACE 2                                                                
         OC    THISYM,THISYM                                                    
         BNZ   DBINV1                                                           
         CLI   QBILMODE,C'P'                                                    
         BE    DBINVX                                                           
         MVC   THISYM,BSTART                                                    
         IC    R0,MODE                                                          
         MVI   MODE,FBUYCLI                                                     
         GOTO1 =V(START),RR=RB                                                  
         STC   R0,MODE                                                          
         XC    WORK,WORK                                                        
         GOTO1 AGETFORM,DMCB,WORK                                               
DBINV1   DS    0H                                                               
         CLI   DONESW,0                                                         
         BE    DBINV2                                                           
         CLC   DONESW,MODE                                                      
         BE    DBINV2                                                           
         B     DBINVX                                                           
DBINV2   DS    0H                                                               
         LA    RF,MASTHDR                                                       
         USING HDRKEYD,RF                                                       
         CLI   BILRDSW,C'S'                                                     
         BE    DBINV2B                                                          
         DROP  RF                                                               
         MVI   PREGKEY,0           DONT PRINT REG AND DST                       
         MVI   PDSTKEY,0           NAMES NOW UNLESS SEPARATE BILLS              
DBINV2B  DS    0H                                                               
         BAS   RE,PBEND                                                         
         BAS   RE,DSTEND                                                        
         BAS   RE,REGEND                                                        
         MVI   ADBSW,0                                                          
*                                                                               
         GOTO1 ABLDREV             BUILD LIST OF REVERSALS                      
*                                                                               
*                                                                               
         MVI   WORK+3,X'FF'                                                     
         GOTO1 APOST,DMCB,WORK     ROLL UP TOTALS                               
*                                                                               
         XC    NXTYM,NXTYM                                                      
DBINV4   DS    0H                                                               
         GOTO1 AADBIL                                                           
*                                                                               
         CLI   ADBSW,0                                                          
         BE    DBINV20                                                          
*                                  PRINT DETAIL TOTALS                          
         MVI   LNEED,2                                                          
         CLI   PRDSW,C'T'                                                       
         BNL   DBINV5                                                           
         L     RF,REVNUM           NO. OF REVSLS                                
         LA    RF,4(RF)                                                         
         CLC   BFORM,DFLTFORM                                                   
         BE    *+8                                                              
         LA    RF,6(RF)            ROOM FOR BILLING FORMULA                     
         STC   RF,LNEED                                                         
DBINV5   DS    0H                                                               
         MVC   P+5(12),=C'** TOTALS **'                                         
         MVI   TOTSW,2                                                          
         GOTO1 AFMTLIN,DMCB,INVTOTS                                             
*                                                                               
         GOTO1 APRNT                                                            
*                                                                               
         GOTO1 APRNT               SKIP A LINE                                  
         CLI   PRDSW,C'T'                                                       
         BL    *+8                                                              
         MVI   FORCEHED,C'Y'                                                    
         MVI   HEADSW,C'N'         NO DETAIL HEADLINES                          
         GOTO1 APRTACC             PRINT PRD TOTS, REVERSALS & ADJS             
*                                                                               
         GOTO1 AGETNUM             GET NEXT INV NUM                             
*                                                                               
DBINV20  DS    0H                                                               
         MVI   FORCEHED,C'Y'                                                    
         MVC   PAGE,=H'1'                                                       
         XC    REVNUM,REVNUM                                                    
         XC    BSNUM,BSNUM         RESET TABLE                                  
         XC    VENTOTS(48),VENTOTS     CLEAR ALL TOTS                           
         MVC   DONESW,MODE                                                      
         MVC   PREGKEY(1),PAGYKEY  RESET REG/DST PRINT                          
         MVC   PDSTKEY(1),PAGYKEY                                               
         CLI   MODE,PROCBUY                                                     
         BE    DBINVX                                                           
         MVI   MASTPRD,0           CLEAR MASTHDR                                
DBINVX   DS    0H                                                               
         B     EXIT                                                             
DFLTFORM DC    X'0505000000'                                                    
         SPACE 3                                                                
         LTORG                                                                  
         SPACE 2                                                                
PPBYOWRK DS    CL700                                                            
         SPACE 2                                                                
*                                                                               
       ++INCLUDE PBILWRK                                                        
*                                                                               
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE PPNEWFILE                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'086PPREP0602 05/01/02'                                      
         END                                                                    
