*          DATA SET DDMAINT    AT LEVEL 004 AS OF 07/24/06                      
*CATALP DDMAINT                                                                 
         TITLE 'DDMAINT - DATA DICTIONARY MAINTENANCE UTILITY'                  
**********************************************************                      
*                                                        *                      
* DATA DICTIONARY MAINTENANCE UTILITY PROGRAM.           *                      
* MERGE INPUT DATA FILE WITH PAN DATA DICTIONARY TERMS   *                      
* AND NOTE BOOKS. CREATE OUTPUT DATA FILE, FILTERING     *                      
* ON SELECTED LANGUAGE AND SEARCH STRING.                *                      
* INPUT DATA FILE SHOULD FOLLOW THE SYNTAX RULES FOR     *                      
* DATA DICTIONARY FILES AND CAN CONTAIN MULTIPLE         *                      
* LANGUAGE ENTRIES FOR EACH EQUATE NUMBER GROUP.         *                      
*                                                        *                      
* INPUT JCL CARD DATA LINES:                             *                      
*   TERMS=<PAN DATA DICTIONARY TERMS BOOK NAME>          *                      
*   NOTES=<PAN DATA DICTIONARY NOTES BOOK NAME>          *                      
*   NEWTERMS=<PAN DATA DICTIONARY TERMS NEW BOOK NAME>   *                      
*   NEWNOTES=<PAN DATA DICTIONARY NOTES NEW BOOK NAME>   *                      
*   LANG=<LIST OF LANGUAGES FOR OUTPUT FILTER>           *                      
*        USE INITIAL LETTER AS DEFINED IN SYSTEM LANGTAB *                      
*        OR 'X' FOR ALL LANGUAGES MERGED IN ONE LANGTAB  *                      
*        DEFAULT IS ALL LANGUAGES                        *                      
*   FILTER=<OUTPUT FILTER STRING>                        *                      
*                                                        *                      
**********************************************************                      
         SPACE 1                                                                
DDMAINT  CSECT                                                                  
         PRINT NOGEN                                                            
         NBASE 0,*DDMAN*,=V(REGSAVE),RA,R9                                      
*                                                                               
         GOTO1 =V(STXITER),DMCB,A(DUMPLIST)                                     
*                                                                               
         L     R8,=V(CPRINT)                                                    
         USING DPRINT,R8                                                        
         MVC   TITLE,SPACES                                                     
         MVC   TITLE(35),=C'DATA DICTIONARY MAINTENANCE UTILITY'                
         B     MAIN                                                             
         EJECT                                                                  
***********************************************************************         
* MAIN (HIGHEST LEVEL ROUTINE)                                        *         
***********************************************************************         
         SPACE 1                                                                
MAIN     BAS   RE,VALCARDS         VALIDATE JCL CARD DATA LINES                 
         BNE   MXIT                  EXIT IF ERROR                              
         BAS   RE,MRGTERMS         MERGE WITH DDIN FILE TERM LINES              
         BNE   MXIT                                                             
*                                                                               
         BAS   RE,MRGNOTES         MERGE WITH DDIN FILE NOTE LINES              
         BNE   MXIT                                                             
*                                                                               
         CLI   RETCODE,0           TEST IF ANY DATA MERGED                      
         BE    MX20                                                             
         BAS   RE,BLDDDOUT         BUILD SELECTED ENTRIES IN DDOUT FILE         
         BNE   MXIT                                                             
*                                                                               
MXIT     TM    SWITCH1,SWITNEW     TEST PAN TERMS BOOK REPLACE REQUEST          
         BNZ   MX10                                                             
         NI    RETCODE,X'FF'-SWITNEW  FLAG NO REPLACE                           
*                                                                               
MX10     TM    SWITCH1,SWINNEW     TEST PAN NOTES BOOK REPLACE REQUEST          
         BNZ   MX20                                                             
         NI    RETCODE,X'FF'-SWINNEW  FLAG NO REPLACE                           
*                                                                               
MX20     XBASE RC=RETCODE,RL=1                                                  
         EJECT                                                                  
***********************************************************************         
* VALIDATE JCL CARD DATA LINES AS DEFINED IN CARDTBL                  *         
***********************************************************************         
         SPACE 1                                                                
VALCARDS NTR1                                                                   
*                                                                               
VCLP1    GOTO1 =V(CARDS),DMCB,P,=C'RE00'                                        
         CLC   =C'/*',P            IF END OF JCL                                
         BE    VCEND                 CHECK REQUIRED CARDS INPUT                 
*                                                                               
         LA    RE,CARDTBL          INITIALISE TABLE POINTER                     
*                                                                               
VCLP2    CLI   0(RE),0             END OF TABLE                                 
         BE    VCLP1               CARD NOT IN TABLE                            
         SR    RF,RF                                                            
         IC    RF,CLENGTH(RE)                                                   
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   P(0),CSTRING(RE)    COMPARE STRING                               
         BE    VCLP2X                                                           
         LA    RE,L'CARDTBL(RE)    GET NEXT ENTRY                               
         B     VCLP2                                                            
*                                                                               
VCLP2X   SR    RF,RF               MATCH FOUND                                  
         IC    RF,CROUTINE(RE)                                                  
         SLL   RF,2                                                             
         B     *+0(RF)             BRANCH TO PROCESSING ROUTINE                 
*                                    FROM JUMP TABLE                            
         B     VCTERMS                                                          
         B     VCNOTES                                                          
         B     VCTNEW                                                           
         B     VCNNEW                                                           
         B     VCLANG                                                           
         B     VCFILTER                                                         
         B     VCLP1                                                            
*                                  CARD DATA ERROR CONDITIONS                   
VCEND    B     VCYES                                                            
*                                  CARD DATA ERROR CONDITIONS                   
VCERR1   GOTO1 =V(PRINTER)         INVALID CARD                                 
         MVI   ERROR,1                                                          
         BAS   RE,ERRPRT                                                        
         B     VCNO                                                             
*                                                                               
VCNO     B     NO                  EXIT ERROR CONDITION                         
*                                                                               
VCYES    B     YES                 EXIT OK                                      
         EJECT                                                                  
***********************************************************************         
* ROUTINES TO PROCESS EACH JCL CARD DATA LINE                         *         
***********************************************************************         
         SPACE 1                                                                
*                                                                               
VCTERMS  MVC   TERMNAME,P+6        TERMS=<NAME>                                 
         B     VCLP1                                                            
*                                                                               
VCNOTES  MVC   NOTENAME,P+6        NOTES=<NAME>                                 
         B     VCLP1                                                            
*                                                                               
VCTNEW   MVC   TNEWNAME,P+9        NEWTERMS=<NAME>                              
         OI    SWITCH1,SWITNEW     FLAG PAN1 REPLACE                            
         B     VCLP1                                                            
*                                                                               
VCNNEW   MVC   NNEWNAME,P+9        NEWNOTES=<NAME>                              
         OI    SWITCH1,SWINNEW     FLAG PAN1 REPLACE                            
         B     VCLP1                                                            
*                                                                               
VCLANG   MVC   LANGBUFF,P+5        LANG=<LIST OF LANGUAGES>                     
         MVI   LANGBUFF+9,C' '     MARK END OF BUFF                             
         LA    RF,LANGBUFF                                                      
*                                  SAVE IN INTERNAL BUFFER                      
VCLLP1   CLI   0(RF),C' '                                                       
         BNE   VCLLP1A                                                          
         B     VCLP1                                                            
VCLLP1A  CLI   0(RF),X'A9'         IF LOWER CASE CONVERT TO UPPER               
         BH    VCLLP1B                                                          
         CLI   0(RF),X'81'                                                      
         BL    VCLLP1B                                                          
         ZIC   RE,0(RF)                                                         
         LA    R0,X'40'                                                         
         AR    RE,R0               CONVERT TO UPPER                             
         STCM  RE,1,0(RF)                                                       
VCLLP1B  LA    RF,1(RF)                                                         
         B     VCLLP1                                                           
*                                                                               
VCFILTER MVC   FILTER,P+7          FILTER=<STRING>                              
         SR    RF,RF                                                            
         LA    R1,P+7                                                           
*                                                                               
VCFLP1   CLI   0(R1),C' '                                                       
         BE    VCFLP1X                                                          
         LA    RF,1(RF)                                                         
         LA    R1,1(R1)                                                         
         LA    RE,P+7+L'FILTER                                                  
         CR    R1,RE                                                            
         BNH   VCFLP1                                                           
*                                                                               
VCFLP1X  STC   RF,FILTERL          SAVE LENGTH OF FILTER STRING                 
         B     VCLP1                                                            
         EJECT                                                                  
***********************************************************************         
* MERGE DDIN AND PAN DATA DICTIONARY TERM FILES                       *         
* LINES FROM DDIN ARE ADDED TO OR REPLACE THOSE IN THE PAN TERMS FILE *         
* MERGED OUTPUT TO WORK FILE 'TERMWORK'                               *         
* LINES READ FROM DDIN ARE STACKED IN BUFFER ANEWTAB, AT ANEWREC      *         
* LINES READ FROM PAN FILE ARE STACKED IN BUFFER AOLDTAB, AT AOLDREC  *         
* STACK ADDRESSES RESET FOR EACH NEW ENTRY EQUATE NUMBER GROUP        *         
* LAST LINES HELD IN BUFFERS RECORD1 FOR DDIN AND RECORD2 FOR PAN FILE*         
* ROUTINE IS CALLED TO PROCESS BUFFERS, MERGING NEW AND OLD ENTRIES   *         
* IN THE CORRECT EQUATE NUMBER AND LANGUAGE ORDER.                    *         
* THIS ALLOWS FOR NEW ENTRIES TO BE SUBMITTED IN DDIN AS MULTIPLE     *         
* LANGUAGE ENTRIES IN ANY ORDER FOR A GIVEN EQUATE NUMBER.            *         
***********************************************************************         
         SPACE 1                                                                
MRGTERMS NTR1                                                                   
         MVC   P,SPACES                                                         
         MVC   P(19),=C'MERGING TERMS FILE:'                                    
         MVC   P+21(10),TERMNAME                                                
         GOTO1 =V(PRINTER)                                                      
         OPEN  (DDIN,INPUT,TERMWORK,OUTPUT)                                     
*                                                                               
         MVC   PANNAME,TERMNAME                                                 
         MVC   NEWNAME,TNEWNAME                                                 
         BAS   RE,BLDP1CMD         WRITE PAN#1 CONTROL LINE                     
         BNE   MT01                                                             
         PUT   TERMWORK,LINEBUFF                                                
*                                                                               
MT01     XC    OLDEQU,OLDEQU       INITIALISE COUNTS                            
         LA    RF,1                                                             
         STH   RF,EQUNUM                                                        
         MVI   LANGSAVE,C' '                                                    
         MVI   EOFIN,0                                                          
         CLC   TERMNAME,SPACES                                                  
         BNE   *+8                                                              
         OI    EOFIN,EOF2                                                       
         MVI   RECORD1,0           INDICATE FIRST LINES                         
         MVI   RECORD2,0                                                        
*                                                                               
* READ LINES FROM DDIN FILE AND SAVE ANY DATA RETURNED IN BUFFERS               
*                                                                               
MTLP1    TM    EOFIN,EOF1          SKIP IF END OF FILE                          
         BNZ   MTLP2                                                            
         CLI   RECORD1,0           DO FIRST LINE                                
         BE    MTLP1A                                                           
         CLC   ANEWTAB,ANEWREC     IF BUFFER STACK NOT FULL                     
         BNE   MTLP2                                                            
         B     MTLP1B                LOAD LAST LINE AND READ NEXT               
*                                                                               
MTLP1A   BAS   RE,READDDIN         READ NEXT LINE FROM DDIN                     
         BNE   MTEOF1              END OF FILE                                  
         CLC   DDREC,SPACES                                                     
         BE    MTLP1A              IGNORE BLANK LINE                            
         CLI   DDREC,C'*'                                                       
         BE    MTLP1A              IGNORE COMMENT LINE                          
         CLI   DDREC+5,C' '                                                     
         BE    MTLP1A              IGNORE NOTE ENTRY                            
         CLC   DDREC(5),SPACES     IF BLANK CONTINUATION LINE                   
         BNE   *+10                                                             
         MVC   DDREC(5),RECORD1    USE LAST EQUATE NUMBER                       
         MVC   RECORD1,DDREC       SAVE NEW LINE IN BUFFER                      
         L     RE,ANEWTAB                                                       
         C     RE,ANEWREC                                                       
         BE    MTLP1B              IF BUFFER STACK EMPTY LOAD INTO IT           
         CLC   RECORD1(4),0(RE)    IF NEW EQUATE NUMBER GROUP                   
         BE    MTLP1B                                                           
         B     MTLP2                 END THIS READ SEQUENCE OF DDIN             
*                                                                               
MTLP1B   L     RF,ANEWREC          LOAD LINE INTO BUFFER STACK                  
         MVC   0(80,RF),RECORD1                                                 
         LA    RF,80(RF)                                                        
         ST    RF,ANEWREC          BUMP STACK POINTER                           
         B     MTLP1A                                                           
*                                                                               
MTEOF1   OI    EOFIN,EOF1          SET DDIN END OF FILE FLAG                    
         B     MTLP2                                                            
*                                                                               
* READ LINES FROM PAN TERM FILE AND SAVE ANY DATA RETURNED IN BUFFERS           
*                                                                               
MTLP2    TM    EOFIN,EOF2          SKIP IF END OF FILE                          
         BNZ   MT02                                                             
         CLI   RECORD2,0           DO FIRST LINE                                
         BE    MTLP2A                                                           
         CLC   AOLDTAB,AOLDREC     IF BUFFER STACK NOT FULL                     
         BNE   MT02                                                             
         B     MTLP2B                LOAD LAST LINE AND READ NEXT               
*                                                                               
MTLP2A   GOTO1 GPANIC              READ NEXT LINE FROM PAN DD FILE              
         BNE   MTNO                  EXIT IF ERROR                              
         CLI   EOFFLAG,0                                                        
         BNE   MTEOF2              END OF FILE                                  
         LA    R2,LINEBUFF                                                      
         BAS   RE,VALLINE                                                       
         BNE   MTLP2A              IGNORE NON DATA LINE                         
         CLC   LINEBUFF(5),SPACES  IF BLANK CONTINUATION LINE                   
         BNE   *+10                                                             
         MVC   LINEBUFF(5),RECORD2   USE LAST EQUATE NUMBER                     
         MVC   RECORD2,LINEBUFF    SAVE NEW LINE IN BUFFER                      
         L     RE,AOLDTAB                                                       
         C     RE,AOLDREC                                                       
         BE    MTLP2B              IF BUFFER STACK EMPTY LOAD INTO IT           
         CLC   RECORD2(4),0(RE)    IF NEW EQUATE NUMBER GROUP                   
         BNE   MT02                END THIS READ SEQUENCE FOR PAN FILE          
*                                                                               
MTLP2B   L     RF,AOLDREC          LOAD LINE INTO BUFFER STACK                  
         MVC   0(80,RF),RECORD2                                                 
         LA    RF,80(RF)                                                        
         ST    RF,AOLDREC          BUMP STACK POINTER                           
         B     MTLP2A                                                           
*                                                                               
MTEOF2   OI    EOFIN,EOF2          SET PAN DD END OF FILE FLAG                  
         B     MT02                                                             
*                                                                               
MT02     BAS   RE,MRGBUFF          PROCESS INPUT BUFFER STACKS                  
         BNE   MTNO                                                             
*                                  CONTINUE PROCESSING IF,                      
         CLI   EOFIN,EOF1+EOF2                                                  
         BNE   MTLP1                EITHER FILE NOT AT END READ ON              
         CLC   ANEWTAB,ANEWREC                                                  
         BNE   MTLP1                DDIN BUFFER STACK CONTAINS DATA             
         CLC   AOLDTAB,AOLDREC                                                  
         BNE   MTLP1                PAN DD BUFFER STACK CONTAINS DATA           
         B     MTYES               ELSE EXIT                                    
*                                                                               
MTNO     CLOSE (TERMWORK)                                                       
         B     NO                  ERROR EXIT                                   
*                                                                               
MTYES    CLOSE (TERMWORK)                                                       
         CLI   RECORD1,0                                                        
         BNE   MTY01                                                            
         CLI   RECORD2,0                                                        
         BE    YES                 EXIT OK                                      
*                                                                               
MTY01    OI    RETCODE,SWITNEW     FLAG NEW TERMS FILE CREATED                  
         MVC   P,SPACES                                                         
         MVC   P(12),=C'TERMS MERGED'                                           
         GOTO1 =V(PRINTER)                                                      
         B     YES                 EXIT OK                                      
         EJECT                                                                  
***********************************************************************         
* MERGE DDIN AND PAN DATA DICTIONARY NOTES FILES                      *         
* LINES FROM DDIN ARE ADDED TO THOSE IN THE PAN NOTES FILE            *         
* MERGED OUTPUT TO WORK FILE 'NOTEWORK'                               *         
* LINES HELD IN BUFFERS RECORD1 FOR DDIN AND RECORD2 FOR PAN FILE     *         
* FROM WHICH THEY ARE DIRECTLY MERGED.                                *         
***********************************************************************         
         SPACE 1                                                                
MRGNOTES NTR1                                                                   
         MVC   P,SPACES                                                         
         MVC   P(19),=C'MERGING NOTES FILE:'                                    
         MVC   P+21(10),NOTENAME                                                
         GOTO1 =V(PRINTER)                                                      
         OPEN  (DDIN,INPUT,NOTEWORK,OUTPUT)                                     
*                                                                               
         MVC   PANNAME,NOTENAME                                                 
         MVC   NEWNAME,NNEWNAME                                                 
         BAS   RE,BLDP1CMD         WRITE PAN#1 CONTROL LINE                     
         BNE   MN01                                                             
         PUT   NOTEWORK,LINEBUFF                                                
*                                                                               
MN01     XC    OLDEQU,OLDEQU       INITIALISE COUNTS                            
         LA    RF,1                                                             
         STH   RF,EQUNUM                                                        
         MVI   LANGSAVE,C' '                                                    
         MVI   EOFIN,0                                                          
         CLC   NOTENAME,SPACES                                                  
         BNE   *+8                                                              
         OI    EOFIN,EOF2                                                       
         MVI   RECORD1,0           MARK FIRST LINES                             
         MVI   RECORD2,0                                                        
*                                                                               
MNLP1    BAS   RE,READDDIN         READ LINE FROM DDIN                          
         BNE   MNEOF1              END OF FILE                                  
         CLC   DDREC,SPACES                                                     
         BE    MNLP1               IGNORE BLANK LINE                            
         CLI   DDREC,C'*'                                                       
         BE    MNLP1               IGNORE COMMENT                               
         CLI   DDREC+5,C' '                                                     
         BNE   MNLP1               IGNORE IF NOT NOTE ENTRY                     
         MVC   RECORD1,DDREC       SAVE LINE                                    
         B     MN02                MERGE LINES                                  
*                                                                               
MNEOF1   OI    EOFIN,EOF1          SET DDIN END OF FILE FLAG                    
         B     MN02                                                             
*                                                                               
MNLP2    GOTO1 GPANIC              READ PAN DD NOTE FILE LINE                   
         BNE   MNNO                EXIT ERROR                                   
         CLI   EOFFLAG,0                                                        
         BNE   MNEOF2              END OF FILE                                  
         LA    R2,LINEBUFF                                                      
         BAS   RE,VALLINE                                                       
         BNE   MNLP2               IGNORE NON DATA LINE                         
         MVC   RECORD2,LINEBUFF    SAVE LINE                                    
         B     MN02                                                             
*                                                                               
MNEOF2   OI    EOFIN,EOF2          SET PAN DD END OF FILE FLAG                  
         B     MN02                                                             
*                                                                               
MN02     CLI   EOFIN,EOF1+EOF2     IF END OF BOTH FILES                         
         BE    MNYES                 EXIT OK                                    
         TM    EOFIN,EOF2          IF END OF PAN FILE                           
         BNZ   MN03                  WRITE DDIN LINE                            
         TM    EOFIN,EOF1          IF END OF DDIN FILE                          
         BNZ   MN04                  WRITE PAN LINE                             
         CLC   RECORD1(4),RECORD2  COMPARE EQUATE #                             
         BL    MN03                WRITE DDIN LINE                              
         B     MN04                WRITE PAN LINE                               
*                                                                               
MN03     CLI   RECORD1,0           SKIP IF FIRST LINE NOT READ                  
         BE    MNLP1                                                            
         LA    R2,RECORD1          WRITE DDIN LINE TO WORK FILE                 
         BAS   RE,WRTNOTE                                                       
         BNE   MNNO                EXIT ERROR                                   
         MVC   LINEBUFF,RECORD1                                                 
         MVC   LINEBUFF+72(8),=C'NEW NOTE'                                      
         BAS   RE,PRTLINE                                                       
         B     MNLP1               CONTINUE PROCESSING FILES                    
*                                                                               
MN04     CLI   RECORD2,0           SKIP IF FIRST LINE NOT READ                  
         BE    MNLP2                                                            
         LA    R2,RECORD2          WRITE PAN LINE TO WORK FILE                  
         BAS   RE,WRTNOTE                                                       
         BNE   MNNO                                                             
         B     MNLP2               CONTINUE PROCESSING FILES                    
*                                                                               
MNNO     CLOSE (NOTEWORK)                                                       
         B     NO                  EXIT ERROR                                   
*                                                                               
MNYES    CLOSE (NOTEWORK)                                                       
         CLI   RECORD1,0                                                        
         BNE   MNY01                                                            
         CLI   RECORD2,0                                                        
         BE    YES                 EXIT OK                                      
*                                                                               
MNY01    OI    RETCODE,SWINNEW     FLAG NEW NOTES FILE CREATED                  
         MVC   P,SPACES                                                         
         MVC   P(12),=C'NOTES MERGED'                                           
         GOTO1 =V(PRINTER)                                                      
         B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* BUILD PAN1 CONTROL LINE "++UPDATE NAME,0,ALL"                       *         
* EITHER "++UPDATE NAME,0,ALL"                                        *         
* OR     "++ADD NAME"                                                 *         
***********************************************************************         
         SPACE 1                                                                
BLDP1CMD NTR1                                                                   
         MVC   LINEBUFF,SPACES                                                  
         CLC   NEWNAME,SPACES                                                   
         BE    NO                                                               
         CLC   NEWNAME,PANNAME                                                  
         BNE   BC10                                                             
         MVC   LINEBUFF(8),=C'++UPDATE'                                         
         MVC   LINEBUFF+9(L'NEWNAME),NEWNAME                                    
         LA    R1,LINEBUFF+9+L'NEWNAME                                          
*                                                                               
BCLP1    CLI   0(R1),C' '                                                       
         BNE   BC01                                                             
         BCT   R1,BCLP1                                                         
*                                                                               
BC01     MVC   1(6,R1),=C',0,ALL'                                               
         B     YES                                                              
*                                                                               
BC10     MVC   LINEBUFF(5),=C'++ADD'                                            
         MVC   LINEBUFF+6(L'NEWNAME),NEWNAME                                    
         LA    R1,LINEBUFF+6+L'NEWNAME                                          
*                                                                               
BCLP2    CLI   0(R1),C' '                                                       
         BNE   BC11                                                             
         BCT   R1,BCLP2                                                         
*                                                                               
BC11     MVC   1(4,R1),=C',BAL'                                                 
         B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* BUILD DDOUT FILE FROM SELECTED ENTRIES IN TERM AND NOTE FILES       *         
* LINES ARE ADDED IN SEQUENCE AND OUTPUT TO DDOUT                     *         
* LINES HELD IN BUFFERS RECORD1 FOR TERMWORK AND RECORD2 FOR NOTEWORK *         
* FROM WHICH THEY ARE DIRECTLY MERGED.                                *         
* SUBROUTINES FILTER OUTPUT FOR REQUESTED LANGUAGES AND SEARCH STRING *         
***********************************************************************         
         SPACE 1                                                                
BLDDDOUT NTR1                                                                   
         MVC   P,SPACES                                                         
         MVC   P(14),=C'BUILDING DDOUT'                                         
         GOTO1 =V(PRINTER)                                                      
         OPEN  (TERMWORK,INPUT,NOTEWORK,INPUT,DDOUT,OUTPUT)                     
*                                                                               
         MVI   EOFIN,0             INTIALISE COUNTS                             
         MVI   FLAGS,FLAGOFF                                                    
         MVI   RECORD1,0                                                        
         MVI   RECORD2,0                                                        
*                                                                               
BDLP1    BAS   RE,READTERM         READ LINE FROM TERMWORK                      
         BNE   BDEOF1              END OF FILE                                  
         CLC   DDREC(2),=C'++'                                                  
         BE    BDLP1               IGNORE PAN#1 CONTROL LINES                   
         MVC   RECORD1,DDREC       SAVE LINE                                    
         B     BD02                MERGE LINES                                  
*                                                                               
BDEOF1   OI    EOFIN,EOF1          SET TERMWORK END OF FILE FLAG                
         B     BD02                                                             
*                                                                               
BDLP2    BAS   RE,READNOTE         READ LINE FROM NOTEWORK                      
         BNE   BDEOF2              END OF FILE                                  
         CLC   DDREC(2),=C'++'                                                  
         BE    BDLP2               IGNORE PAN#1 CONTROL LINES                   
         MVC   RECORD2,DDREC       SAVE LINE                                    
         B     BD02                MERGE LINES                                  
*                                                                               
BDEOF2   OI    EOFIN,EOF2          SET NOTEWORK END OF FILE FLAG                
         B     BD02                                                             
*                                                                               
BD02     CLI   EOFIN,EOF1+EOF2     IF END OF BOTH FILES                         
         BE    BDYES                 EXIT OK                                    
         TM    EOFIN,EOF2          IF END OF NOTEWORK FILE                      
         BNZ   BD03                  WRITE TERMWORK LINE                        
         TM    EOFIN,EOF1          IF END OF TERMWORK FILE                      
         BNZ   BD04                  WRITE NOTEWORK LINE                        
         CLC   RECORD1(4),RECORD2  COMPARE EQUATE NUMBERS                       
         BL    BD03                                                             
         B     BD04                                                             
*                                                                               
BD03     CLI   RECORD1,0           SKIP IF FIRST LINE NOT READ                  
         BE    BDLP1                                                            
         LA    R2,RECORD1          WRITE TERMWORK LINE                          
         BAS   RE,WRTDDOUT                                                      
         BNE   BDNO                EXIT ERROR                                   
         B     BDLP1               CONTINUE PROCESSING FILES                    
*                                                                               
BD04     CLI   RECORD2,0           SKIP IF FIRST LINE NOT READ                  
         BE    BDLP2                                                            
         LA    R2,RECORD2          WRITE NOTEWORK LINE                          
         BAS   RE,WRTDDOUT                                                      
         BNE   BDNO                                                             
         B     BDLP2               CONTINUE PROCESSING FILES                    
*                                                                               
BDNO     CLOSE (DDOUT)                                                          
         B     NO                  EXIT ERROR                                   
*                                                                               
BDYES    CLOSE (DDOUT)                                                          
         MVC   P,SPACES                                                         
         MVC   P(11),=C'DDOUT BUILT'                                            
         GOTO1 =V(PRINTER)                                                      
         B     YES                 EXIT OK                                      
         EJECT                                                                  
***********************************************************************         
* MERGE SAVED TERMS RECORDS FROM NEW AND OLD BUFFER STACKS AND        *         
* WRITE TO TERMWORK FILE.                                             *         
* OUTPUT DATA FROM BUFFER WITH LOWEST ENTRY GROUP EQUATE NUMBER.      *         
* IF SAME MERGE AND REPLACE DDIN NEW LANGUAGE ENTRIES WITH PAN OLD    *         
* ENTRIES AND OUTPUT IN ORDER.                                        *         
* UPDATE BUFFER POINTERS TO INDICATE BUFFER STACKS PROCESSED          *         
* SKIP PROCESSED BUFFER STACKS ON ENTRY.                              *         
***********************************************************************         
         SPACE 1                                                                
MRGBUFF  NTR1                                                                   
         MVI   NEWTABF,FLAGOFF     SET BUFFER PROCESSED FLAGS                   
         MVI   OLDTABF,FLAGOFF                                                  
         LA    R1,SYMBLTAB                                                      
*                                                                               
* PROCESS FOR EACH LANGAUGE IN SYMBOL TABLE                                     
*                                                                               
MBLP1    CLI   0(R1),SYMBNOT                                                    
         BE    MBLP1X              EXIT END OF LANGUAGES                        
         L     R4,ANEWTAB          START OF DDIN BUFFER STACK                   
         L     R3,AOLDTAB          START OF PAN TERM BUFFER STACK               
         CLC   ANEWREC,ANEWTAB     IF DDIN STACK EMPTY                          
         BE    MB01                  PROCESS PAN BUFFER                         
         CLC   AOLDREC,AOLDTAB     IF PAN TERM STACK EMPTY                      
         BE    MBLP2                 PROCESS DDIN BUFFER                        
         CLC   0(4,R4),0(R3)       IF PAN ENTRTY EQUATE#< DDIN                  
         BH    MB01                  PROCESS PAN BUFFERS                        
*                                                                               
* PROCESS FOR EACH ENTRY IN DDIN BUFFER STACK                                   
*                                                                               
MBLP2    C     R4,ANEWREC                                                       
         BE    MBLP2X              EXIT END OF STACK                            
         CLC   4(1,R4),0(R1)       PROCESS IF CURRENT LANGUAGE PASS             
         BNE   MBLP2A                                                           
         LA    R2,0(R4)            WRITE ENTRY TO OUTPUT                        
         BAS   RE,WRTTERM                                                       
         BNE   MBNO                EXIT ERROR                                   
         MVI   FLAGS,FLAGOFF       CLEAR PRINT FLAG                             
         L     R3,AOLDTAB          IF PAN BUFFER SAME EQUATE#                   
         CLC   AOLDREC,AOLDTAB                                                  
         BE    MBLP3X                                                           
         CLC   0(4,R4),0(R3)                                                    
         BL    MBLP3X                                                           
*                                                                               
* MARK EACH MATCHING ENTRY IN PAN TERM BUFFER STACK FOR REPLACE                 
*                                                                               
MBLP3    C     R3,AOLDREC                                                       
         BE    MBLP3X              TO END                                       
         CLC   4(1,R3),4(R4)       CHECK CURRENT LANGUAGE SYMBOL                
         BNE   MBLP3A                                                           
         CLC   0(72,R3),0(R4)      CHECK DATA IS DIFFERENT                      
         BE    MBLP3B              IF NOT SKIP REPLACE                          
         MVC   LINEBUFF,SPACES     LIST WARNING MESSAGE                         
         MVC   LINEBUFF+13(11),=C'*** WARNING'                                  
         MVC   LINEBUFF+26(13),=C'TERM REPLACED'                                
         BAS   RE,PRTLINE                                                       
         MVC   LINEBUFF,0(R3)                                                   
         MVC   LINEBUFF+72(8),=C'OLD TERM'                                      
         BAS   RE,PRTLINE                                                       
         MVC   LINEBUFF,0(R4)                                                   
         MVC   LINEBUFF+72(8),=C'NEW TERM'                                      
         BAS   RE,PRTLINE                                                       
MBLP3B   MVI   4(R3),0             MARK PAN TERM STACK ENTRY REPLACED           
         MVI   FLAGS,FLAGON        FLAG LIST MESSAGE                            
MBLP3A   LA    R3,80(R3)           NEXT PAN STACK ENTRY                         
         B     MBLP3                                                            
MBLP3X   CLI   FLAGS,FLAGON                                                     
         BE    MBLP2A                                                           
         MVC   LINEBUFF,0(R4)                                                   
         MVC   LINEBUFF+72(8),=C'NEW TERM'                                      
         BAS   RE,PRTLINE                                                       
*                                                                               
MBLP2A   LA    R4,80(R4)           NEXT DDIN STACK ENTRY                        
         B     MBLP2                                                            
MBLP2X   MVI   NEWTABF,FLAGON      FLAG DDIN STACK BUFFER PROCESSED             
*                                                                               
MB01     L     R4,ANEWTAB                                                       
         L     R3,AOLDTAB                                                       
         CLC   AOLDREC,AOLDTAB     IF ENTRIES IN PAN TERM STACK,                
         BE    MB02                                                             
         CLC   ANEWREC,ANEWTAB                                                  
         BE    MBLP4                                                            
         CLC   0(4,R4),0(R3)       AND ENTRY EQUATE#< DDIN                      
         BL    MB02                                                             
*                                                                               
* PROCESS FOR EACH ENTRY IN PAN TERM FILE BUFFER STACK                          
*                                                                               
MBLP4    C     R3,AOLDREC                                                       
         BE    MBLP4X                                                           
         CLC   4(1,R3),0(R1)       MATCH CURRENT LANGUAGE SYMBOL                
         BNE   MBLP4A                                                           
         LA    R2,0(R3)            WRITE ENTRY TO OUTPUT                        
         BAS   RE,WRTTERM                                                       
         BNE   MBNO                                                             
MBLP4A   LA    R3,80(R3)           NEXT PAN TERM STACK ENTRY                    
         B     MBLP4                                                            
MBLP4X   MVI   OLDTABF,FLAGON      FLAG PAN STACK PROCESSED                     
*                                                                               
MB02     LA    R1,1(R1)            NEXT LANGUAGE SYMBOL                         
         B     MBLP1                                                            
*                                                                               
MBLP1X   CLI   NEWTABF,FLAGON                                                   
         BNE   *+10                                                             
         MVC   ANEWREC,ANEWTAB     INDICATE DDIN STACK PROCESSED                
         CLI   OLDTABF,FLAGON                                                   
         BNE   *+10                                                             
         MVC   AOLDREC,AOLDTAB     INDICATE PAN TERM STACK PROCESSED            
         B     MBYES                                                            
*                                                                               
MBNO     B     NO                  EXIT ERROR                                   
*                                                                               
MBYES    B     YES                 EXIT OK                                      
         EJECT                                                                  
***********************************************************************         
* WRITE RECORD TO TERMWORK, CHECKING FORMAT                           *         
***********************************************************************         
         SPACE 1                                                                
WRTTERM  NTR1                                                                   
         BAS   RE,GETNUM           CHECK EQUATE NUMBER                          
         BNE   WTERR                                                            
         BAS   RE,GETLANG          CHECK LANGUAGE SYMBOL                        
         BNE   WTERR                                                            
         MVC   LINEBUFF,SPACES                                                  
         CLC   EQUNUM,OLDEQU       CHECK EQUATE# ASCENDING ORDER                
         BL    WTERR                                                            
         BE    WT01                                                             
         MVI   LINEBUFF,C'*'       IF # = OUTPUT NULL COMMENT LINE              
         PUT   TERMWORK,LINEBUFF                                                
*                                                                               
WT01     MVC   LINEBUFF(72),0(R2)  WRITE DATA LINE                              
         PUT   TERMWORK,LINEBUFF                                                
         B     WTYES                                                            
*                                                                               
WTERR    MVC   LINEBUFF,0(R2)      OUTPUT ERROR MESSAGE                         
         BAS   RE,PRTLINE                                                       
         MVI   ERROR,2                                                          
         BAS   RE,ERRPRT                                                        
         B     WTNO                                                             
*                                                                               
WTNO     B     NO                  EXIT ERROR                                   
*                                                                               
WTYES    B     YES                 EXIT OK                                      
         EJECT                                                                  
***********************************************************************         
* WRITE RECORD TO NOTEWORK, CHECKING FORMAT                           *         
***********************************************************************         
         SPACE 1                                                                
WRTNOTE  NTR1                                                                   
         BAS   RE,GETNUM           CHECK EQUATE NUMBER                          
         BNE   WNERR                                                            
         BAS   RE,GETLANG          CHECK LANGUAGE SYMBOL                        
         BNE   WNERR                                                            
         MVC   LINEBUFF,SPACES                                                  
         CLC   EQUNUM,OLDEQU       CHECK EQUATE# ASCENDING ORDER                
         BL    WNERR                                                            
*                                                                               
WN01     MVC   LINEBUFF(72),0(R2)  OUTPUT DATA LINE                             
         PUT   NOTEWORK,LINEBUFF                                                
         B     WNYES                                                            
*                                                                               
WNERR    MVC   LINEBUFF,0(R2)      ERROR MESSAGE                                
         BAS   RE,PRTLINE                                                       
         MVI   ERROR,2                                                          
         BAS   RE,ERRPRT                                                        
         B     WNNO                                                             
*                                                                               
WNNO     B     NO                  EXIT ERROR                                   
*                                                                               
WNYES    B     YES                 EXIT OK                                      
         EJECT                                                                  
***********************************************************************         
* WRITE RECORD TO DDOUT, FILTER ON LANGUAGE AND STRING                *         
***********************************************************************         
         SPACE 1                                                                
WRTDDOUT NTR1                                                                   
         CLI   0(R2),C'*'          SKIP NULL COMMENT LINE                       
         BNE   WD01                                                             
         CLI   FLAGS,FLAGON                                                     
         BE    WDYES                                                            
         MVI   FLAGS,FLAGON                                                     
         B     WD02                                                             
*                                                                               
WD01     OC    LANGBUFF,LANGBUFF   FILTER LANGUAGE SYMBOL IF ANY                
         BZ    WD03                                                             
         MVC   LANGSAVE,4(R2)                                                   
         BAS   RE,LANGFILT                                                      
         BNE   WDYES                                                            
*                                                                               
WD03     CLC   FILTER,SPACES       FILTER SEARCH STRING IF ANY                  
         BE    WD04                                                             
         BAS   RE,STRGFILT                                                      
         BNE   WDYES                                                            
*                                                                               
WD04     MVI   FLAGS,FLAGOFF       FLAG DATA LINE                               
*                                                                               
WD02     MVC   LINEBUFF,0(R2)      WRITE DATA LINE                              
         PUT   DDOUT,LINEBUFF                                                   
         B     WDYES                                                            
*                                                                               
WDNO     B     NO                                                               
*                                                                               
WDYES    B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* VALIDATE SYNTAX OF DATA DICTIONARY PAN BOOK CONTROL LINE            *         
***********************************************************************         
         SPACE 1                                                                
VALLINE  NTR1                                                                   
         CLC   =C'*          DATA SET',0(R2)                                    
         BNE   VALL06                                                           
         B     VALLNO                                                           
*                                                                               
VALL06   CLC   =C'TITLE',0(R2)                                                  
         BNE   VALL07                                                           
         B     VALLNO                                                           
*                                                                               
VALL07   CLC   =C'EJECT',0(R2)                                                  
         BNE   VALL08                                                           
         MVC   P,SPACES                                                         
         ZAP   LINE,=P'75'                                                      
         B     VALLNO                                                           
*                                                                               
VALL08   CLC   =C'SPACE',0(R2)                                                  
         BNE   VALL10                                                           
         MVC   P,SPACES                                                         
         CLI   6(R2),C' '                                                       
         BE    VALLNO                                                           
         ZIC   R3,6(R2)                                                         
         SLL   R3,28                                                            
         SRL   R3,28                                                            
         OR    R3,R3                                                            
         BZ    VALLNO                                                           
*                                                                               
VALL09   GOTO1 =V(PRINTER)                                                      
         BCT   R3,VALL09                                                        
         B     VALLNO                                                           
*                                                                               
VALL10   CLI   0(R2),C'*'          IF NOT CONTROL, PROCESS ENTRY                
         BNE   VALLYES                                                          
         B     VALLNO                                                           
*                                                                               
VALLYES  B     YES                 EXIT DATA LINE                               
*                                                                               
VALLNO   B     NO                  EXIT CONTROL LINE                            
         EJECT                                                                  
***********************************************************************         
* GET EQUATE NUMBER AND SAVE BINARY VALUE IN WORK STORE               *         
***********************************************************************         
         SPACE 1                                                                
GETNUM   NTR1                                                                   
         MVC   OLDEQU,EQUNUM       SAVE OLD EQUATE NUMBER                       
         SR    R3,R3               TO HOLD BINARY EQU NUMBER                    
         SR    R4,R4               TO COUNT TOTAL NUMBER OF DIGITS              
*                                                                               
GNLP1    CLI   0(R2),C'0'          IF CHAR IN ('0'..'9')                        
         BL    GNLP1X                THEN VALID                                 
         CLI   0(R2),C'9'                                                       
         BH    GNLP1X                                                           
*                                                                               
         NI    0(R2),X'0F'         CONVERT EBCDIC TO BINARY                     
         SR    RE,RE                                                            
         ICM   RE,1,0(R2)          LOAD SINGLE DIGIT                            
         OI    0(R2),X'F0'         CONVERT BACK TO EBCDIC                       
         MH    R3,=H'10'           NUM = NUM*10 + ORD(CHAR)                     
         AR    R3,RE                                                            
         LA    R4,1(R4)                                                         
         LA    R2,1(R2)            BUMP POINTER                                 
         CH    R4,=H'04'                                                        
         BNH   GNLP1                                                            
         B     GNNO                INVALID NUMBER                               
*                                                                               
GNLP1X   EQU   *                                                                
         OR    R4,R4               "EXPECTING EQUATE NUMBER"                    
         BZ    GNNO                IF # DIGIT < 1                               
         OR    R3,R3                                                            
         BZ    GNNO                INVALID NUMBER(I.E. 0000)                    
         STH   R3,EQUNUM           SAVE BINARY EQU # VALUE                      
         B     GNYES                                                            
*                                                                               
GNNO     B     NO                                                               
*                                                                               
GNYES    B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* GET LANGUAGE SYMBOL                                                 *         
***********************************************************************         
         SPACE 1                                                                
GETLANG  NTR1                                                                   
         CLI   4(R2),DELIM                                                      
         BE    GLNO                DOES NOT CONTAIN LANGUAGE CHAR.              
*                                                                               
         LA    RE,SYMBLTAB         TEST VALIDITY OF LANGUAGE CHARACTER          
         CLI   4(R2),SYMBQUE                                                    
         BE    GLLP1X                                                           
         CLI   4(R2),SYMBNOT                                                    
         BE    GLLP1X                                                           
GLLP1    CLI   0(RE),SYMBNOT                                                    
         BE    GLNO                INVALID LANGUAGE CHAR.                       
         CLC   4(1,R2),0(RE)                                                    
         BE    GLLP1X                                                           
         LA    RE,1(RE)                                                         
         B     GLLP1                                                            
*                                                                               
GLLP1X   EQU   *                                                                
         CLI   5(R2),DELIM                                                      
         BE    GL01                                                             
         CLI   5(R2),COMMON        IF '?' OR '<', PROCESS NORMALLY              
         BE    GL01                                                             
         CLI   5(R2),COMMON2                                                    
         BE    GL01                                                             
         CLI   5(R2),C' '          NOTE LINE                                    
         BNE   GLNO                                                             
*                                                                               
GL01     MVC   LANGSAVE,4(R2)                                                   
*                                                                               
GLUS     EQU   *                                                                
*&&US                                                                           
         CLI   4(R2),SYMBUSA       IF 'U' OR 'C'                                
         BNE   GLUSA                 THEN OVERWRITE PREV. ENTRY                 
         MVI   LANGSAVE,SYMBENG                                                 
         B     GLUSX                                                            
GLUSA    CLI   4(R2),SYMBCAN                                                    
         BNE   GLUSX                                                            
         MVI   LANGSAVE,SYMBFRE                                                 
         B     GLUSX                                                            
*                                                                               
GLUSX    EQU   *                                                                
*&&                                                                             
         B     GLYES                                                            
*                                                                               
GLNO     B     NO                                                               
*                                                                               
GLYES    B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* FILTER LINE AGAINST LANGUAGE REQUEST                                *         
***********************************************************************         
         SPACE 1                                                                
LANGFILT NTR1                                                                   
         LA    R1,LANGBUFF                                                      
*                                                                               
LFLP1    CLI   0(R1),0                                                          
         BE    LF01                                                             
         CLI   0(R1),C' '                                                       
         BE    LFNO                                                             
         CLC   0(1,R1),LANGSAVE    COMPARE LANGUAGE CHARACTER                   
         BE    LFYES                                                            
         B     LF01                                                             
*                                                                               
LF01     LA    R1,1(R1)                                                         
         B     LFLP1                                                            
*                                                                               
LFNO     B     NO                                                               
*                                                                               
LFYES    B     YES                                                              
***********************************************************************         
* FILTER LINE AGAINST STRING FILTER REQUEST OF VALUE EITHER;          *         
* EITHER 'STRING' FILTERS FROM START OF TEXT,                         *         
* OR     '*STRING' SEARCH TEXT FOR 'STRING'                           *         
* COMPARE EACH STRING IN ENTRY FROM LAST/LONGEST TO FIRST/SHORTEST    *         
***********************************************************************         
         SPACE 1                                                                
STRGFILT NTR1                                                                   
         LA    R1,71(R2)                                                        
         LA    R4,72                                                            
         CLI   0(R1),C' '          FIRST NON BLANK CHARACTER                    
         BNE   SF01                                                             
         BCTR  R1,0                BACKTRACK POINTER                            
         BCT   R4,*-10                                                          
         B     SFYES                                                            
*                                                                               
SF01     CLI   0(R1),DELIM                                                      
         BNE   SFYES                                                            
*                                                                               
SFLP1    BCTR  R1,0                BACKTRACK POINTER                            
         CLI   0(R1),DELIM                                                      
         BE    SF03                                                             
         CLI   0(R1),COMMON                                                     
         BE    SF03                                                             
         CLI   0(R1),COMMON2                                                    
         BE    SF03                                                             
         BCT   R4,SFLP1                                                         
         B     SFNO                                                             
*                                                                               
SF03     LR    R3,R1               SAVE ENTRY POSITION                          
         LA    R1,1(R1)                                                         
         SR    RF,RF                                                            
         ICM   RF,1,FILTERL        LENGTH OF FILTER STRING                      
         BZ    SFYES                                                            
         LA    RE,FILTER                                                        
         CLI   0(RE),C'*'          SEE IF SEARCH STRING                         
         BNE   SF04                  ELSE COMPARE FROM START                    
         LA    RE,1(RE)            SEARCH FOR STRING MATCH                      
         BCT   RF,SF05                                                          
         B     SFYES                                                            
*                                                                               
SF05     CLC   0(1,RE),0(R1)                                                    
         BE    SF04                                                             
*                                                                               
SF02     LA    R1,1(R1)                                                         
         CLI   0(R1),DELIM                                                      
         BNE   SF05                                                             
         LR    R1,R3                                                            
         B     SFLP1                                                            
*                                                                               
SF04     MVC   STRGSAVE(L'FILTER),0(R1)                                         
         LR    R0,RF                                                            
         LA    R5,STRGSAVE         CONVERT TO UPPERCASE                         
*                                                                               
SF06     CLI   0(R5),X'81'                                                      
         BL    SF07                                                             
         CLI   0(R5),X'A9'                                                      
         BH    SF07                                                             
         OI    0(R5),X'40'                                                      
*                                                                               
SF07     LA    R5,1(R5)                                                         
         BCT   R0,SF06                                                          
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   0(0,RE),STRGSAVE    COMPARE STRING                               
         BE    SFYES                                                            
         LA    RF,1(RF)                                                         
         CLI   FILTER,C'*'                                                      
         BE    SF02                                                             
         LR    R1,R3                                                            
         B     SFLP1                                                            
*                                                                               
SFNO     B     NO                  EXIT NOT FOUND                               
*                                                                               
SFYES    B     YES                 EXIT OK                                      
         EJECT                                                                  
***********************************************************************         
*  READ LINE FROM PAN BOOK INTO BUFFER                                *         
***********************************************************************         
         SPACE 1                                                                
GPANIC   NTR1                                                                   
         GOTO1 =V(PANIC),DMCB,(X'80',=C'READ'),=C'PAN',PANNAME,LINEBUFF         
         TM    DMCB+8,X'40'        DISK ERROR?                                  
         BZ    *+6                                                              
         DC    H'00'                                                            
         TM    DMCB+8,X'10'        PAN BOOK IN LIBRARY?                         
         BNZ   GPNO                                                             
         B     GPYES                                                            
*                                                                               
GPNO     MVC   P,SPACES            PAN BOOK NOT FOUND                           
         MVC   P,PANNAME                                                        
         GOTO1 =V(PRINTER)                                                      
         MVI   ERROR,0                                                          
         BAS   RE,ERRPRT                                                        
         B     NO                                                               
*                                                                               
GPYES    MVC   EOFFLAG,DMCB+8      PAN BOOK FOUND, SAVE EOF FLAG                
         B     YES                                                              
         SPACE 1                                                                
***********************************************************************         
* PRINT ERROR MESSAGE                                                 *         
***********************************************************************         
         SPACE 1                                                                
ERRPRT   ST    RE,SAVERE                                                        
         LA    RE,ERRTAB                                                        
         SR    RF,RF                                                            
         ICM   RF,1,ERROR                                                       
         MH    RF,=H'40'                                                        
         AR    RE,RF                                                            
         MVC   P,SPACES                                                         
         MVC   P+13(10),=C'*** ERROR '                                          
         MVC   P+23(L'ERRMSG0),0(RE)                                            
         GOTO1 =V(PRINTER)                                                      
         MVI   ERROR,0                                                          
         L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 1                                                                
***********************************************************************         
*  PRINT DATA DICTIONARY LINE                                         *         
***********************************************************************         
         SPACE 1                                                                
PRTLINE  DS    0H                                                               
         MVC   P(L'LINEBUFF),LINEBUFF                                           
         ST    RE,SAVERE                                                        
         GOTO1 =V(PRINTER)                                                      
         L     RE,SAVERE                                                        
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* READ DDIN FILE, INDICATING IF EOF                                   *         
***********************************************************************         
         SPACE 1                                                                
READDDIN NTR1                                                                   
         GET   DDIN,DDREC                                                       
         B     YES                                                              
*                                                                               
EODADDI  CLOSE (DDIN)                                                           
         B     NO                                                               
***********************************************************************         
* READ TERMWORK FILE, INDICATING IF EOF                               *         
***********************************************************************         
         SPACE 1                                                                
READTERM NTR1                                                                   
         GET   TERMWORK,DDREC                                                   
         B     YES                                                              
*                                                                               
EODADTW  CLOSE (TERMWORK)                                                       
         B     NO                                                               
***********************************************************************         
* READ NOTEWORK FILE, INDICATING IF EOF                               *         
***********************************************************************         
         SPACE 1                                                                
READNOTE NTR1                                                                   
         GET   NOTEWORK,DDREC                                                   
         B     YES                                                              
*                                                                               
EODADNW  CLOSE (NOTEWORK)                                                       
         B     NO                                                               
         SPACE 1                                                                
*                                                                               
YES      SR    RC,RC               RETURN CC EQUAL                              
NO       LTR   RC,RC               RETURN CC NOT EQUAL                          
XIT      XIT1                                                                   
*                                                                               
         EJECT                                                                  
DUMPLIST DS    0F                                                               
         DC    A(DDMAINT,65000)                                                 
         ORG   *-4                                                              
         DC    X'80'                                                            
         ORG                                                                    
*                                                                               
         LTORG                                                                  
*                                                                               
* DATA CONTROL BLOCK DEFINITIONS                                                
*                                                                               
DDIN     DCB   DDNAME=DDIN,DSORG=PS,RECFM=FB,                          *        
               MACRF=GM,EODAD=EODADDI                                           
DDOUT    DCB   DDNAME=DDOUT,DSORG=PS,RECFM=FB,MACRF=PM                          
TERMWORK DCB   DDNAME=TERMWORK,DSORG=PS,RECFM=FB,                      *        
               MACRF=(GM,PM),EODAD=EODADTW                                      
NOTEWORK DCB   DDNAME=NOTEWORK,DSORG=PS,RECFM=FB,                      *        
               MACRF=(GM,PM),EODAD=EODADNW                                      
         EJECT                                                                  
* CARDTBL DEFINES JOB CONTROL INPUT PARAMETER CARDS                             
* AL1    LENGTH OF CARD NAME                                                    
* AL1    ACTION ROUTINE NUMBER                                                  
* XL1    ACTION FLAGS                                                           
* CL8    CARD NAME                                                              
*                                                                               
CARDTBL  DS    0CL14                                                            
         DC    AL1(06,01),X'00',CL11'TERMS='                                    
         DC    AL1(06,02),X'00',CL11'NOTES='                                    
         DC    AL1(09,03),X'00',CL11'NEWTERMS='                                 
         DC    AL1(09,04),X'00',CL11'NEWNOTES='                                 
         DC    AL1(05,05),X'00',CL11'LANG='                                     
         DC    AL1(07,06),X'00',CL11'FILTER='                                   
CARDTBLX DC    AL1(00)                                                          
CLENGTH  EQU   0                                                                
CROUTINE EQU   1                                                                
CFLAG    EQU   2                                                                
CSTRING  EQU   3                                                                
         EJECT                                                                  
*                                                                               
* WORKING STORAGE                                                               
*                                                                               
*                                                                               
DMCB     DS    6F                                                               
DUB      DS    D                                                                
WORK     DS    CL17                                                             
         DS    0H                                                               
SAVERE   DS    A                   SAVE RE                                      
*                                                                               
OLDMAXN  DC    H'0001'             VALID PREV. MAXNUM                           
MAXNUM   DC    H'0001'             CURRENT HIGH EQU NUMBER                      
LASTEQU  DC    H'0000'             LAST EQU NUMBER                              
OLDEQU   DC    H'0000'             OLD EQU NUMBER                               
EQUNUM   DC    H'0001'             CURRENT EQU NUMBER                           
*                                                                               
LANGSAVE DC    C' '                LANGAUGE SYMBOL SAVE                         
*                                  DATA DICTIONARY SYNTAX CONTROLS              
COMMON   EQU   C'?'                                                             
COMMON2  EQU   C'<'                                                             
DELIM    EQU   C'>'                DELIMETER                                    
DELIM2   DC    CL2'>>'             INCLUDE DELIM AS A CHAR                      
SPLITTER EQU   C':'                SPECIAL EQUATE NUMBER INDICATOR              
ONEOFF   EQU   X'FE'               MASK TO TURN OFF X'01'                       
TWOOFF   EQU   X'FD'               MASK TO TURN OFF X'02'                       
END1     EQU   X'FE'               END OF TABLE MARKER                          
END2     DC    XL2'FFFF'           END OF ADDRS OR LITERALS                     
OLDLANG  DC    CL1'E'              CURR. LANG. CODE                             
STRLEN   DC    XL1'00'             CURRENT STRING LENGTH                        
*                                                                               
EOFIN    DC    XL1'00'             END OF FILE FLAG                             
EOF1     EQU   X'01'               EOF DDIN FILE                                
EOF2     EQU   X'02'               EOF PAN DD FILE                              
PANNAME  DC    CL10' '             PAN FILE OLD NAME                            
NEWNAME  DC    CL10' '             PAN FILE NEW NAME                            
TERMNAME DC    CL10' '             TERMS PAN DATA FILE NAME                     
NOTENAME DC    CL10' '             NOTES PAN DATA FILE NAME                     
TNEWNAME DC    CL10' '             NEW TERMS PAN DATA FILE NAME                 
NNEWNAME DC    CL10' '             NEW NOTES PAN DATA FILE NAME                 
*                                                                               
FILTER   DC    CL20' '             SEARCH FILTER STRING                         
STRGSAVE DC    CL20' '             STRING BUFFER FOR FILTER COMPARE             
FILTERL  DC    XL1'00'             SEARCH FILTER STRING LENGTH                  
*                                                                               
LANGBUFF DC    XL10'00'            LANG. REQUESTS                               
LANGREPL DC    CL10'EUFGISDX'      REPLACED LANGUAGE LINE FLAG BUFFER           
*                                  LANGUAGE CHARACTER TABLE                     
SYMBLTAB DC    CL10'EUFCGISDX'                                                  
SYMBLANG DC    CL1'E'              DEFAULT ENGLISH                              
SYMBENG  EQU   C'E'                ENGLISH(UK OR USA)                           
SYMBUSA  EQU   C'U'                ENGLISH(USA)                                 
SYMBCAN  EQU   C'C'                FRENCH(CANADA)                               
SYMBFRE  EQU   C'F'                FRENCH                                       
SYMBGER  EQU   C'G'                GERMAN                                       
SYMBITA  EQU   C'I'                ITALIAN                                      
SYMBSPA  EQU   C'S'                SPANISH                                      
SYMBDUT  EQU   C'D'                DUTCH                                        
SYMBQUE  EQU   C'Q'                EQUATE SYMBOL                                
SYMBNOT  EQU   C'X'                NOT USED                                     
*                                  CONTROL ACTION INDICATORS                    
SWITCH1  DC    XL1'00'             DEFAULT OFF                                  
SWITNEW  EQU   X'01'               IF ON THEN NEW PAN TERMS BOOK                
SWINNEW  EQU   X'02'               IF ON THEN NEW PAN NOTES BOOK                
*                                                                               
FLAGS    DC    XL1'00'             INTERNAL CONTROL FLAG                        
FLAGON   EQU   X'01'               FLAG ON                                      
FLAGOFF  EQU   X'00'               FLAG OFF                                     
*                                  ADDRESSES OF INTERNAL DATA TABLES            
RETCODE  DC    XL1'00'             XBASE RETURN CODE                            
EOFFLAG  DC    XL1'00'             PAN BOOK READ EOF FLAG                       
*                                                                               
ERROR    DC    XL1'00'             FLAG TO INDIATE INVALID INPUT                
COUNTERR DC    XL1'00'             COUNTER OF COMPILATION ERRORS                
ERRTAB   DS    0H                  ERROR REPORT STRINGS                         
ERRMSG0  DC    CL40'DATASET NOT FOUND IN PAN LIBRARY'                           
ERRMSG1  DC    CL40'INVALID CONTROL CARD INPUT'                                 
ERRMSG2  DC    CL40'INVALID INPUT FILE LINE'                                    
*                                                                               
LINEBUFF DS    CL80                                                             
HEXBUFF  DS    CL160               TO STORE ALPHA DATA IN HEX                   
OUTBUFF  DS    CL80                OBJECT LINE                                  
*                                                                               
DDREC    DS    CL80                READ LINE BUFFER FOR SEQUENTIAL DATA         
RECORD1  DS    CL80                DDIN LAST LINE SAVE BUFFER                   
RECORD2  DS    CL80                PAN TERMS FIEL LAST LINE SAVE BUFFER         
*                                                                               
NEWTABF  DS    XL1                 FLAG DDIN BUFFER STACK PROCESSED             
OLDTABF  DS    XL1                 FLAG PAN TERM BUFFER STACK PROCESSED         
ANEWTAB  DC    A(NEWTAB)           START DDIN LINE BUFFER STACK                 
AOLDTAB  DC    A(OLDTAB)           START PAN TERM LINE BUFFER STACK             
ANEWREC  DC    A(NEWTAB)           POINTER TO END OF DDIN LINE STACK            
AOLDREC  DC    A(OLDTAB)           POINTER TO END OF PAN TERM STACK             
NEWTAB   DS    20CL80              DDIN LINE BUFFER STACK                       
OLDTAB   DS    20CL80              PAN TERM FILE BUFFER STACK                   
         EJECT                                                                  
       ++INCLUDE FASYSLST                                                       
       ++INCLUDE FALANGTAB                                                      
         EJECT                                                                  
*                                                                               
       ++INCLUDE FASYSLSTD                                                      
       ++INCLUDE FALANG                                                         
         EJECT                                                                  
       ++INCLUDE DDDPRINT                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004DDMAINT   07/24/06'                                      
         END                                                                    
