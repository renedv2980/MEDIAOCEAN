*          DATA SET CXAPMC     AT LEVEL 040 AS OF 02/03/95                      
*CATALP CXAPMC                                                                  
         TITLE 'CXAPMC - CONTROL EXTRACT - TS APPROVER GROUP MANAGERS'          
**********************************************************************          
* AL4 A(EXTRACT RECORD)                                              *          
* AL4 A(CONFILE RECORD)                                              *          
**********************************************************************          
         SPACE 1                                                                
         PRINT NOGEN                                                            
CXAPMC   CSECT                                                                  
         NMOD1 WORKX-WORKD,*CXAPMC*,RR=RA                                       
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RA,RELO                                                          
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         USING CXAPMD,R2           R2=A(EXTRACT RECORD)                         
         USING SAAPREC,R3          R3=A(CONFILE RECORD)                         
*                                                                               
CXALEN   XC    CXAPMLEN,CXAPMLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   CXAPMLEN(2),=AL2(CXAPMDL)                                        
         MVC   CXAPMTYP,CXAPMDQ                                                 
         MVI   CXAPMDX,MXTRTQ                                                   
         MVI   CXAPMDX+1,MXTRTQ                                                 
*                                                                               
CXAAGY   MVI   CXAPMAGY-1,MXTRTQ   AGENCY ALPHA ID                              
         MVC   CXAPMAGY,SAAPAGY                                                 
*                                                                               
CXACOD   MVI   CXAPMAGC-1,MXTRTQ   APPROVER GROUP CODE                          
         MVC   CXAPMAGC,SAAPAGR                                                 
*                                                                               
         MVI   CXAPMMON-1,MXTRTQ                                                
         MVI   CXAPMPER-1,MXTRTQ                                                
*                                                                               
         CLI   SAVEACTN,C'C'       TEST SAVED ACTION CODE                       
         BNE   *+12                                                             
         MVI   CXAPMACT,C'A'       IF LAST TIME CHANGE GOTO ADD PROCESS         
         B     CXATAB                                                           
         CLI   CXAPMACT,C'C'       IF CHANGE DO 'K' DELETE ALL ENTRIES          
         BE    CXAKOF                                                           
         CLI   CXAPMACT,C'D'       IF DELETE DO 'K' DELETE ALL ENTRIES          
         BE    CXAKOF                                                           
         B     CXATAB              OTHER ACTIONS GOTO ADD PROCESS               
*                                                                               
CXAKOF   MVC   SAVEACTN,CXAPMACT   SAVE THIS ACTION                             
         MVI   CXAPMACT,C'K'       OVERIDE WITH 'K' DELETE ALL ENTRIES          
         CLI   SAVEACTN,C'C'                                                    
         BE    RETEXIT             IF CHANGE RETURN FOR ADDS                    
         B     CXAEOT                                                           
*                                                                               
         USING PERTABD,R5                                                       
CXATAB   ICM   R5,15,APERTAB                                                    
         BZ    *+12                                                             
         LA    R5,PERTLEN(R5)                                                   
         B     CXAPER                                                           
         L     R5,=A(PERTABC)                                                   
         BAS   RE,INITPERT                                                      
*                                                                               
CXAPER   CLI   PERTNUM,0           TEST IF LAST PERSON PROCESSED                
         BE    CXAEOT                                                           
         C     R5,APERTEND                                                      
         BH    CXAEOT                                                           
         ST    R5,APERTAB                                                       
         SR    RF,RF                                                            
         IC    RF,PERTMON                                                       
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  CXAPMMON,DUB+5(3)                                                
         SR    RF,RF                                                            
         ICM   RF,3,PERTNUM                                                     
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  CXAPMPER,DUB+5(3)                                                
*                                                                               
         B     RETEXIT                                                          
*                                                                               
RETEXIT  L     R1,APARM            EXIT WITH RETURN REQUIRED                    
         OI    8(R1),X'40'         SET RETURN FLAG                              
         B     EXIT                                                             
*                                                                               
CXAEOT   XC    APERTAB,APERTAB     END OF TABLE                                 
         MVI   SAVEACTN,C' '       CLEAR SAVED ACTION CODE                      
         L     R1,APARM                                                         
         MVI   8(R1),X'88'                                                      
         B     EXIT                                                             
*                                                                               
DELREC   XC    APERTAB,APERTAB                                                  
         L     R1,APARM            DELETE THIS RECORD                           
         MVI   8(R1),X'80'                                                      
*                                                                               
EXIT     XMOD1 1                                                                
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO LOAD MANAGER PERSON NUMBER TABLE FROM APPROVER RECORD    *         
* R5=A(PERSON TABLE)                                                  *         
***********************************************************************         
         SPACE 1                                                                
         USING PERTABD,R5                                                       
INITPERT NTR1  ,                                                                
         LR    RE,R5                                                            
         LA    RF,PERTABL                                                       
         XCEF                                                                   
         LA    R4,SAAPDATA         R4=A(FIRST ELEMENT)                          
*                                                                               
         USING SAMAND,R4                                                        
IPER010  CLI   SAMANEL,0           GET ELEMENT FROM RECORD                      
         BE    IPER100                                                          
         CLI   SAMANEL,SAMANELQ                                                 
         BE    IPER030                                                          
IPER020  SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R4)                                                         
         CH    RF,=H'1'                                                         
         BNH   EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R4,RF                                                            
         B     IPER010                                                          
*                                                                               
IPER030  MVC   PERTMON,SAMANORD                                                 
         MVC   PERTNUM,SAMANID                                                  
         LA    R5,PERTLEN(R5)                                                   
         B     IPER020                                                          
*                                                                               
IPER100  ST    R5,APERTAB          SAVE A(ENTRY)                                
         ST    R5,APERTEND         SAVE A(END OF TABLE)                         
*                                                                               
IPERX    EQU   *                                                                
         XIT1                                                                   
         EJECT                                                                  
         DS    0D                                                               
SAVEACTN DC    CL1' '                                                           
SPACES   DC    80C' '                                                           
         DS    0D                                                               
         LTORG                                                                  
         SPACE 1                                                                
       ++INCLUDE MXTRT                                                          
       ++INCLUDE CXRECID                                                        
         SPACE 1                                                                
         DS    0D                                                               
VHEXOUT  DC    V(HEXOUT)                                                        
VCRYPT   DC    V(CRYPT)                                                         
         SPACE 1                                                                
         DS    0D                                                               
APERTAB  DC    A(0)                PERSON TABLE POINTER                         
APERTEND DC    A(0)                PERSON TABLE END OF TABLE POINTER            
         DS    0D                  PERSON TABLE                                 
PERTABC  DC    (PERTABL)X'00'                                                   
PERTABL  EQU   (PERTMAX*PERTLEN)                                                
PERTMAX  EQU   6                   MAXIMUM POSSIBLE PERSONS PER RECORD          
         SPACE 1                                                                
         DS    0D                                                               
         SPACE 2                                                                
PERTABD  DSECT                     DSECT FOR PERSON TABLE                       
PERTMON  DS    XL1                 MANAGER ORDER NUMBER                         
PERTNUM  DS    XL2                 PERSON NUMBER                                
PERTLEN  EQU   *-PERTABD                                                        
         SPACE 2                                                                
         EJECT                                                                  
* INCLUDE CXTRACT LOCAL DSECTS                                                  
         SPACE 1                                                                
       ++INCLUDE CXTRACTD                                                       
         EJECT                                                                  
WORKD    DSECT                                                                  
PARM     DS    0XL16               PARAMETER LIST                               
AXREC    DS    A                   A(EXTRACT RECORD)                            
CFREC    DS    A                   A(CONFILE RECORD)                            
         DS    A                                                                
         DS    A                                                                
*                                                                               
DMCB     DS    6F                                                               
DUB      DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
RELO     DS    A                                                                
*                                                                               
PERNEXT  DS    A                   NEXT ENTRY IN PERSON TABLE                   
PEREND   DS    A                   END OF PERSON TABLE ENTRIES                  
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE CXAPMD                                                         
         EJECT                                                                  
       ++INCLUDE CTGENFILE                                                      
         EJECT                                                                  
       ++INCLUDE SEACSFILE                                                      
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'040CXAPMC    02/03/95'                                      
         END                                                                    
