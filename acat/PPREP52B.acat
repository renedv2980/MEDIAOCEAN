*          DATA SET PPREP52B   AT LEVEL 012 AS OF 03/02/05                      
*CATALP PP52B                                                                   
         TITLE 'PP5202 - CHANGE LOG'                                            
*                                                                               
* KWAN 06/00       PRINT USER FIELDS AFTER ESTIMATE BILLING SUMMARY             
*                                                                               
* SMYE 05/00       CHANGES FOR LARGER PPNEWFILE                                 
*                                                                               
* BPLA 10/98       AT ENDXRQ  CLEAR FMULTMED (FIRST MEDIA FOUND)                
*                                                                               
* BPLA 1/96        CHANGES FOR NET $ VOLUME CONTRACT                            
*                  ("N" IN PRBLIND)                                             
*                                                                               
* BPLA 3/95        ADD /L AND /I TO CONTRACT RATE DISPLAY                       
*                  CHECKING PRBLIND  (X'08')                                    
*                                                                               
* BPLA 11/22/94    OLD AOR (DUPONT STYLE) DISABLED                              
*                                                                               
* BPLA  11/10/93   IN ENDEST IF PRD = SPACES ONLY CLEAR ACCUMS THROUGH          
*                  PRODUCT IF PRD NOT SPACES ONLY CLEAR THROUGH EST             
*                  IN ENDCLT - NO BRAND SUMMARIES IF QPRODUCT IS SPACES         
*                  AND QEST IS NOT.                                             
*                                                                               
* BPLA  10/15/93   IN ENDMOD CHECK IF COMSW IS 'E' WHEN MODE                    
*                  IS LBUYPRO OR LBUYEST - IF SO THEN JUST EXIT                 
*                  (MEANS THERE WAS NOT ACTIVITY)                               
*                                                                               
* BPLA  10/6/93    IN ENDPRO IF LASTMODE WAS FBUYPRO OR FBUYEST                 
*                  THE JUST EXIT (THIS HAPPENS FOR EST= BLANK                   
*                  REQUESTS FOR ALL PRDS                                        
*                  ALSO FIX *-MEDIA REQUEST MEDIAUL                             
*                                                                               
* BPLA  4/8/93     CHANGES FOR USER FIELDS                                      
*                                                                               
* BPLA  2/11/93    AT ENDREG ROLL GRDTOTS TO DRDTOTS FOR                        
*                  REG/DST RECAP                                                
*                  AT ENDMOD USE DRDTOTS FOR REG/DST RECAP                      
*                                                                               
* BPLA  10/22/92   CHANGES FOR AOR CONTRACT (FOR THOSE AGYS                     
*                  THAT DON'T SET-UP THEIR OWN)                                 
* BPLA  9/29/92 TEMP FIX REMOVED - WILL DO LATER USING                          
*               NEW AOR CONTROLS                                                
* BPLA  9/28/92 TEMP FIX TO ALLOW DUPONT AGENCIES ACCESS TO BBDO                
*               CONTRACTS                                                       
* BPLA  4/2/91  MANY FRENCH FIXES                                               
*                                                                               
* ROSA  6/2/88 PRINT C FOR C RATES  PRBIND =X'04'                 L03           
*                                                                 L03           
* ROSA 5/25/88 PRINT S FOR S RATES  PRBIND =X'02'                 L02           
*                                                                 L02           
* ROSA 5/10/88 WHEN CONTRACT RATE DATA IS REQUESTED INSERT A 'N' L01            
*      WHEN THIS IS A NET RATE (PRBIND = X'20')                   L01           
*                                                                 L01           
         TITLE 'PP5202 - MONTHLY PUB TOTALS'                                    
ENDPTOT  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,ENDPTOT                                                        
         USING PPWORKD,RA                                                       
         L     RC,PPFILEC                                                       
         USING PPFILED,RC,R9                                                    
         USING PP52WRKD,R8                                                      
*                                                                               
         XC    DMCB,DMCB                                                        
         LA    R1,DMCB                                                          
         USING DICTATED,R1                                                      
         MVI   DDACTN,DDACTNL                                                   
         MVI   DDRETN,DDCASEU                                                   
         MVI   DDSYS,4                                                          
         MVC   DDLANG,RCLANG                                                    
         LA    RF,DCLIST                                                        
         STCM  RF,7,DDIADR                                                      
         LA    RF,DSLIST                                                        
         STCM  RF,7,DDOADR                                                      
         GOTO1 ADICTATE                                                         
         DROP  R1                                                               
*                                                                               
* PRINT MONTHLY PUB TOTALS                                                      
*                                                                               
         XC    FULL,FULL                                                        
         LA    R3,ESCDETS                                                       
         LA    R0,3                                                             
ENDPTOT2 CLI   0(R3),1                                                          
         BNE   *+8                                                              
         OI    FULL,X'01'                                                       
         CLI   0(R3),2                                                          
         BNE   *+8                                                              
         OI    FULL,X'02'                                                       
         CLI   0(R3),X'11'         ACTUAL COST                                  
         BNE   *+8                                                              
         MVI   FULL,X'11'                                                       
         LA    R3,1(R3)                                                         
         BCT   R0,ENDPTOT2                                                      
         SR    RE,RE               TOTALS TAKE 4 LINES ON 1 PAGE                
         IC    RE,LINE                                                          
         LA    RE,4(RE)                                                         
         TM    FULL,X'03'                                                       
         BNO   *+8                                                              
         LA    RE,4(RE)            4 MORE LINES FOR NET                         
         STC   RE,BYTE                                                          
         CLC   BYTE,MAXLINES                                                    
         BL    *+8                                                              
         MVI   LINE,99                                                          
*                                                                               
         GOTO1 APRNT               SKIP A LINE                                  
*                                                                               
         MVC   P+10(L'PP@MOTOT),PP@MOTOT                                        
         TM    ESCPBTOT,X'04'       TEST ED/VEN TOTS                            
         BZ    ENDPTOT3            ONLY VENDOR                                  
         MVC   P+10(L'PP@VNTOT),PP@VNTOT                                        
         CLI   EDVENSW,C'V'                                                     
         BE    ENDPTOT3                                                         
         MVC   P+10(L'PP@EDTOT),PP@EDTOT                                        
         CLI   QMEDIA,C'O'                                                      
         BNE   ENDPTOT3                                                         
         MVC   P+10(L'PP@EDTOT),SPACES                                          
         MVC   P+18(L'PP6TOTAL),PP6TOTAL     TOTALS                             
*                                                                               
ENDPTOT3 DS    0H                                                               
         CLI   EDVENSW,C'M'                                                     
         BNE   *+10                                                             
         MVC   P+10(L'PP@MARKT),PP@MARKT                                        
         MVI   BYTE,1              SET MONTH CODE                               
         MVI   FULL+1,X'01'        GRS                                          
         TM    FULL,X'01'                                                       
         BNZ   *+8                                                              
         MVI   FULL+1,X'02'        NET                                          
         CLI   FULL,X'11'          ACTUAL COST                                  
         BNE   *+8                                                              
         MVI   FULL+1,X'11'                                                     
         BAS   RE,ENDPFMT                                                       
         MVI   BYTE,2                                                           
         TM    FULL,X'03'               TEST DOING BOTH                         
         BNO   *+10                     NO                                      
         MVC   P+13(L'PP@GROST),PP@GROST                                        
         BAS   RE,ENDPFMT                                                       
         MVI   BYTE,3                                                           
         MVI   SPACING,2                                                        
         BAS   RE,ENDPFMT                                                       
*                                                                               
         TM    FULL,X'03'          TEST DOING BOTH                              
         BNO   ENDPTOT4                                                         
         MVI   FULL+1,X'02'        NET NOW                                      
         MVI   BYTE,1                                                           
         BAS   RE,ENDPFMT                                                       
         MVI   BYTE,2                                                           
         MVC   P+14(L'PP@NETT),PP@NETT                                          
         BAS   RE,ENDPFMT                                                       
         MVI   BYTE,3                                                           
         BAS   RE,ENDPFMT                                                       
*                                                                               
ENDPTOT4 DS    0H                                                               
*                                                                               
         CLI   EDVENSW,C'E'                                                     
         BNE   ENDPTOT6                                                         
         CLI   MODE,LBUYVEN                                                     
         BNE   ENDPTOT6                                                         
         GOTO1 APRNT               NO NEW PAGE FOR VEN TOTS                     
*                                                                               
         B     ENDPTOTX                                                         
ENDPTOT6 DS    0H                                                               
         MVI   FORCEHED,C'Y'                                                    
*                                                                               
ENDPTOTX DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
ENDPFMT  NTR1                                                                   
         SPACE 2                                                                
         LA    R4,P+27                                                          
         SR    R5,R5                                                            
         IC    R5,BYTE             BYTE HAS VALUES 1-3 FOR START MONTH          
         MH    R5,=H'3'                                                         
         LA    R5,MONTHS-3(R5)                                                  
         MVC   0(3,R4),0(R5)                                                    
         MVC   18(3,R4),9(R5)                                                   
         MVC   36(3,R4),18(R5)                                                  
         MVC   54(3,R4),27(R5)                                                  
*                                                                               
ENDPFMT1 LA    R5,RMTAB            SET R5 TO FIRST MONTH OF REQUEST             
ENDPFMTA CLC   0(2,R5),BSTART                                                   
         BE    ENDPFMT2                                                         
         LA    R5,2(R5)                                                         
         B     ENDPFMTA                                                         
*                                                                               
ENDPFMT2 CLC   BYTE,1(R5)          FIND MONTH IN TABLE                          
         BE    ENDPFMT4                                                         
         LA    R5,2(R5)                                                         
         CLI   0(R5),0                                                          
         BNE   ENDPFMT2                                                         
         B     ENDPFMT6            TRY NEXT MONTH                               
ENDPFMT4 LA    R0,RMTAB            CLACULATE DISPLACEMENT                       
         SR    R5,R0               GET DSPL X 2                                 
         MH    R5,=Y(ROWL/2)                                                    
         L     R1,APUBTOT                                                       
         CLI   EDVENSW,C'E'                                                     
         BE    EPFMT4B                                                          
         L     R1,AVENTOT                                                       
         CLI   EDVENSW,C'V'                                                     
         BE    EPFMT4B                                                          
         L     R1,AMKTTOT                                                       
EPFMT4B  DS    0H                                                               
         CLI   FULL+1,X'11'        ACTUAL COST                                  
         BE    EPFMT4D                                                          
*                                                                               
         LR    R6,R1                                                            
         AR    R6,R5               ADD INDEX                                    
         MVC   DUB,0(R6)           GROSS IS IN DUB                              
*                                                                               
         CLI   FULL+1,X'01'                                                     
         BE    ENDPFMT5                                                         
         SP    DUB,16(8,R6)        LESS AGYCOM = NET                            
*                                                                               
         B     ENDPFMT5                                                         
*                                                                               
EPFMT4D  DS    0H                                                               
*                                                                               
         LR    R6,R1                                                            
         AR    R6,R5               ADD INDEX                                    
         MVC   DUB,40(R6)          GROSS IS IN DUB                              
*                                                                               
ENDPFMT5 DS    0H                                                               
         CP    DUB,=P'0'                                                        
         BE    ENDPFMT6                                                         
         ST    RE,SAVERE1                                                       
         ST    RF,SAVERF                                                        
         ST    R1,SAVER1                                                        
         MVI   MYCTAB+3,2                                                       
         CURED (P8,DUB),(10,4(R4)),MYCTAB,FLOAT=-,MODADDR=ACUREDIT              
         L     R1,SAVER1                                                        
         L     RF,SAVERF                                                        
         L     RE,SAVERE1                                                       
*                                                                               
ENDPFMT6 LA    R4,18(R4)           POINT AHEAD 3 MONTHS                         
         IC    R5,BYTE                                                          
         LA    R5,3(R5)                                                         
         STC   R5,BYTE                                                          
         CLI   BYTE,12                                                          
         BNH   ENDPFMT1                                                         
ENDPFMT8 DS    0H                                                               
         GOTO1 APRNT                                                            
         B     ENDPTOTX                                                         
         SPACE 2                                                                
         LTORG                                                                  
         SPACE 3                                                                
DCLIST   DS    0C                                                               
         DCDDL PP#MOTOT,14                                                      
         DCDDL PP#VNTOT,14                                                      
         DCDDL PP#EDTOT,14                                                      
         DCDDL PP#TOTAL,6,LABEL=PP6TOTAL                                        
         DCDDL PP#MARKT,7,R                                                     
         DCDDL PP#GROST,9                                                       
         DCDDL PP#NETT,7                                                        
DCLISTX  DC    X'00'                                                            
*                                                                               
DSLIST   DS    0C                                                               
         DSDDL PRINT=YES                                                        
         PRINT GEN                                                              
DSLISTX  EQU   *                                                                
         PRINT NOGEN                                                            
         TITLE 'PP5202 - END BREAK ROUTINES'                                    
ENDBRKS  CSECT                                                                  
         NMOD1 0,ENDBRKS                                                        
         USING PPWORKD,RA                                                       
         L     RC,PPFILEC                                                       
         USING PPFILED,RC,R9                                                    
         USING PP52WRKD,R8                                                      
*                                                                               
         XC    DMCB,DMCB                                                        
         LA    R1,DMCB                                                          
         USING DICTATED,R1                                                      
         MVI   DDACTN,DDACTNL                                                   
         MVI   DDRETN,DDCASEU                                                   
         MVI   DDSYS,4                                                          
         MVC   DDLANG,RCLANG                                                    
         LA    RF,DC1LIST                                                       
         STCM  RF,7,DDIADR                                                      
         LA    RF,DS1LIST                                                       
         STCM  RF,7,DDOADR                                                      
         GOTO1 ADICTATE                                                         
         DROP  R1                                                               
         MVI   PRNTSW,C'S'                                                      
         CLI   MODE,LBUYXRQ                                                     
         BL    ENDCB                                                            
         BE    EB4                                                              
         CLI   ESTACTIV,0                                                       
         BE    ENDCB                                                            
         CLI   MODE,LBUYDST                                                     
         BH    ENDCB                                                            
EB4      DS    0H                                                               
         MVC   P,SPACES                                                         
         MVC   PSECOND,SPACES                                                   
         MVC   MID1,SPACES                                                      
         MVC   MID2,SPACES                                                      
         CLI   MODE,LBUYDST                                                     
         BE    ENDDST                                                           
         CLI   MODE,LBUYREG                                                     
         BE    ENDREG                                                           
         CLC   QEST,=C'ALL'                                                     
         BNE   *+12                                                             
         CLI   MODE,LBUYEST                                                     
         BE    ENDEST                                                           
         CLI   MODE,LBUYPRO                                                     
         BE    ENDPRO                                                           
         CLI   MODE,LBUYDIV                                                     
         BE    ENDDIV                                                           
         CLI   MODE,LBUYCLI                                                     
         BE    ENDCLT                                                           
         CLI   MODE,LBUYXRQ                                                     
         BE    ENDXRQ                                                           
         B     ENDCB                                                            
* END OF DISTRICT                                                               
*                                                                               
ENDDST   CLC   QDIST,=C'ALL'                                                    
         BNE   ENDDSTX                                                          
*                                                                               
         CLI   QOPT3,C'R'          SUPPRESS REG/DST SUMMARIES                   
         BNE   *+8                                                              
         MVI   PRNTSW,C'N'                                                      
*                                                                               
         TM    RDPACT,X'40'         TEST DIST ACTIVE                            
         BNZ   *+8                                                              
         MVI   PRNTSW,C'N'                                                      
*                                                                               
         LA    R4,MYSUMHD8+35                                                   
         SR    R6,R6                                                            
         ICM   R6,7,DSHD                                                        
         LA    R7,L'PP@DIST                                                     
         BCTR  R7,0                                                             
         EX    R7,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(R6)                                                    
         LA    R5,10(R4)           FOR REST OF HEADING                          
         CLI   0(R5),C' '                                                       
         BNE   *+8                                                              
         BCT   R5,*-8                                                           
         LA    R5,2(R5)                                                         
         GOTO1 AESSUM,DMCB,ADSTTOT                                              
*                                                                               
         MVC   WRITTOTS(72),GRDTOTS                                             
         GOTO1 AWFMOD                                                           
*                                                                               
         EX    R7,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(R6)                                                    
         GOTO1 AESSUM,DMCB,ADSTTOTB                                             
*                                                                               
ENDDSTX  L     R2,ADSTTOT                                                       
         BAS   RE,ENDCLR                                                        
         L     R2,ADSTTOTB                                                      
         BAS   RE,ENDCLR                                                        
         MVI   LINE,99                                                          
         B     ENDCB                                                            
         EJECT                                                                  
* END OF REGION                                                                 
*                                                                               
ENDREG   CLC   QREGION,=C'ALL'                                                  
         BNE   ENDREGX                                                          
*                                                                               
         CLI   QOPT3,C'R'          SUPPRESS REG/DST SUMMARIES                   
         BNE   *+8                                                              
         MVI   PRNTSW,C'N'                                                      
*                                                                               
         TM    RDPACT,X'80'         TEST REG ACTIVE                             
         BNZ   *+8                                                              
         MVI   PRNTSW,C'N'                                                      
*                                                                               
         CLC   QDIST,=C'ALL'                                                    
         BNE   *+10                                                             
         XC    PDSTKEY,PDSTKEY                                                  
*                                                                               
         LA    R4,MYSUMHD8+35                                                   
         SR    R6,R6                                                            
         ICM   R6,7,RGHD                                                        
         LA    R7,L'PP@REGN                                                     
         BCTR  R7,0                                                             
         EX    R7,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(R6)                                                    
         LA    R5,8(R4)            FOR REST OF HEADING                          
         CLI   0(R5),C' '                                                       
         BNE   *+8                                                              
         BCT   R5,*-8                                                           
         LA    R5,2(R5)                                                         
         GOTO1 AESSUM,DMCB,AREGTOT                                              
*                                                                               
         MVC   WRITTOTS(72),GRDTOTS                                             
         GOTO1 AWFMOD                                                           
*                                                                               
         LA    R0,ROWL/8      ROLL TO DRDTOTS FOR RECAPS                        
         LA    RE,GRDTOTS                                                       
         LA    R5,DRDTOTS                                                       
ENDREG5  AP    0(8,R5),0(8,RE)                                                  
         LA    RE,8(RE)                                                         
         LA    R5,8(R5)                                                         
         BCT   R0,ENDREG5                                                       
*                                                                               
         EX    R7,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(R6)                                                    
         LA    R5,8(R4)            FOR REST OF HEADING  **                      
         CLI   0(R5),C' '                               **                      
         BNE   *+8                                      **                      
         BCT   R5,*-8                                   **                      
         LA    R5,2(R5)                                 **                      
         GOTO1 AESSUM,DMCB,AREGTOTB                                             
*                                                                               
ENDREGX  L     R2,AREGTOT                                                       
         BAS   RE,ENDCLR                                                        
         L     R2,AREGTOTB                                                      
         BAS   RE,ENDCLR                                                        
         MVI   LINE,99                                                          
         B     ENDCB                                                            
         EJECT                                                                  
* END OF ESTIMATE                                                               
*                                                                               
ENDEST   DS    0H                                                               
         CLC   QDIST,=C'ALL'                                                    
         BNE   *+10                                                             
         XC    PDSTKEY,PDSTKEY                                                  
*                                                                               
         CLC   QREGION,=C'ALL'                                                  
         BNE   *+10                                                             
         XC    PREGKEY,PREGKEY                                                  
*                                                                               
         CLC   QPROG,=C'53'                                                     
         BE    ENDESTX                                                          
         LA    R4,MYSUMHD8+35                                                   
         SR    R6,R6                                                            
         ICM   R6,7,ESHD                                                        
         LA    R7,L'PP@ESTM                                                     
         BCTR  R7,0                                                             
         EX    R7,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(R6)                                                    
         LA    R5,10(R4)           FOR REST OF HEADING                          
         CLI   0(R5),C' '                                                       
         BNE   *+8                                                              
         BCT   R5,*-8                                                           
         LA    R5,2(R5)                                                         
         GOTO1 AESSUM,DMCB,AESTTOT                                              
*                                                                               
         BAS   RE,ENDMOD                                                        
*                                                                               
ENDEST2  DS    0H                                                               
*                                                                               
         SR    R6,R6                                                            
         ICM   R6,7,ESHD                                                        
         LA    R7,L'PP@ESTM                                                     
         BCTR  R7,0                                                             
         EX    R7,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(R6)                                                    
         GOTO1 AESSUM,DMCB,AESTTOTB                                             
*                                                                               
         BAS   RE,ENDMOD           PRINT USER FIELDS AFTER EST BILLING          
*                                                                               
ENDESTX  L     R2,AESTTOT                                                       
         BAS   RE,ENDCLR                                                        
         L     R2,AESTTOTB                                                      
         BAS   RE,ENDCLR                                                        
         MVI   LINE,99                                                          
*                                                                               
         CLI   QPRODUCT,C' '                                                    
         BE    ENDESTX2                                                         
*                                                                               
ENDESTX1 DS    0H                                                               
         CLC   QPRODUCT,=C'ZZZ'                                                 
         BNE   ENDCB                                                            
*                                                                               
         CLC   QPROG,=C'53'                                                     
         BE    ENDCB                                                            
*                                                                               
ENDESTX2 DS    0H                                                               
**NEW 2/18/88                                                                   
         CLI   RCALLALL,C'Y'          SEE IF SPECIAL EC/52 REQUEST              
         BE    ENDESTX5               FROM BILLING REQUEST                      
*                                     NO BRAND SUMMARIES                        
**NEW 2/18/88                                                                   
         GOTO1 ABRDSUM                                                          
*                                                                               
         B     ENDESTX5                                                         
*                                                                               
****  NOTE - I USED TO GO TO ENDCLTX INSTEAD AND WAS CLEARING                   
****         CLIENT TOTALS WHEN REQUEST WAS FOR PRD BLANK AND                   
****         ALL ESTS                                                           
ENDESTX5 DS    0H                                                               
         LA    R0,6                CLEAR THROUGH AESTTOT                        
         LA    R3,APUBTOT                                                       
         L     R2,0(R3)                                                         
         BAS   RE,ENDCLR                                                        
         LA    R3,4(R3)            NEXT TOTAL ADDRESS                           
         BCT   R0,*-12                                                          
         LA    R0,3                CLEAR THROUGH AESTTOTB                       
         LA    R3,ADSTTOTB                                                      
         L     R2,0(R3)                                                         
         BAS   RE,ENDCLR                                                        
         LA    R3,4(R3)            NEXT TOTAL ADDRESS                           
         BCT   R0,*-12                                                          
*                                                                               
         CLC   QPRODUCT,SPACES    IF PRD IS SPACES ALSO                         
         BNE   ENDCB              CLEAR PRDTOTS                                 
*                                                                               
         LA    R3,APRDTOT                                                       
         BAS   RE,ENDCLR                                                        
         LA    R3,APRDTOTB                                                      
         BAS   RE,ENDCLR                                                        
         MVI   LINE,99                                                          
         B     ENDCB                                                            
         EJECT                                                                  
*                                                                               
* END OF PRODUCT                                                                
*                                                                               
ENDPRO   DS    0H                                                               
         L     RF,AJOBLST               CLEAR JOBLST                            
         XC    0(200,RF),0(RF)                                                  
         CLI   QPRODUCT,C' '                                                    
         BE    ENDPROX                                                          
         CLC   QEST,=C'ALL'                                                     
         BE    ENDPRO2                                                          
*                                                                               
         CLI   LASTMODE,FBUYPRO          SEE IF LAST MODE WAS FBUYPRO           
         BE    ENDPROX                   MEANS THERE WEREN'T ANY BUYS           
*                                        DON'T GO TO ENDEST                     
         CLI   LASTMODE,FBUYEST          SEE IF LAST MODE WAS FBUYEST           
         BE    ENDPROX                   MEANS THERE WEREN'T ANY BUYS           
*                                        DON'T GO TO ENDEST                     
         L     R2,APRDTOT                                                       
         BAS   RE,ENDCLR                                                        
         L     R2,APRDTOTB                                                      
         BAS   RE,ENDCLR                                                        
         B     ENDEST                                                           
*                                                                               
ENDPRO2  LA    R4,MYSUMHD8+35                                                   
         SR    R6,R6                                                            
         ICM   R6,7,PRHD                                                        
         LA    R7,L'PP@PRO                                                      
         BCTR  R7,0                                                             
         EX    R7,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(R6)                                                    
         LA    R5,11(R4)           FOR REST OF HEADING                          
         XC    PESTKEY,PESTKEY                                                  
         GOTO1 AESSUM,DMCB,APRDTOT                                              
*                                                                               
         EX    R7,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(R6)                                                    
         GOTO1 (RF),(R1),APRDTOTB                                               
*                                                                               
         MVI   LINE,99                                                          
ENDPROX  L     R2,APRDTOT                                                       
         BAS   RE,ENDCLR                                                        
         L     R2,APRDTOTB                                                      
         BAS   RE,ENDCLR                                                        
         B     ENDCB                                                            
         EJECT                                                                  
* END OF DIVISION                                                               
*                                                                               
ENDDIV   DS    0H                                                               
         CLC   QDIST,=C'ALL'                                                    
         BNE   *+10                                                             
         XC    PDSTKEY,PDSTKEY                                                  
*                                                                               
         CLC   QREGION,=C'ALL'                                                  
         BNE   *+10                                                             
         XC    PREGKEY,PREGKEY                                                  
*                                                                               
         CLC   QDIV,SPACES                                                      
         BE    ENDDIVX                                                          
         XC    PPRDKEY,PPRDKEY                                                  
         CLI   QPRODUCT,C' '                                                    
         BE    ENDDIV2                                                          
         CLC   QPRODUCT,=C'ALL'                                                 
         BNE   ENDDIVX                                                          
ENDDIV2  DS    0H                                                               
*                                                                               
         CLC   QEST,=C'ALL'                                                     
         BNE   *+10                                                             
         XC    PESTKEY,PESTKEY                                                  
         LA    R4,MYSUMHD8+35                                                   
         SR    R6,R6                                                            
         ICM   R6,7,DVHD                                                        
         LA    R7,L'PP@DIV                                                      
         BCTR  R7,0                                                             
         EX    R7,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(R6)                                                    
         LA    R5,10(R4)           FOR REST OF HEADING                          
         CLI   0(R5),C' '                                                       
         BNE   *+8                                                              
         BCT   R5,*-8                                                           
         LA    R5,2(R5)                                                         
         GOTO1 AESSUM,DMCB,ADIVTOT                                              
*                                                                               
         CLC   QDIV,=C'ALL'                                                     
         BNE   ENDDIV2B                                                         
         CLI   QPRODUCT,C' '                                                    
         BE    ENDDIV2B                                                         
         B     ENDDIV2D                                                         
ENDDIV2B DS    0H                                                               
         BAS   RE,ENDMOD                                                        
ENDDIV2D DS    0H                                                               
*                                                                               
         EX    R7,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(R6)                                                    
         GOTO1 (RF),(R1),ADIVTOTB                                               
*                                                                               
         CLC   QDIV,=C'ALL'                                                     
         BE    ENDDIVX                                                          
         CLI   QPRODUCT,C' '                                                    
         BE    ENDDIV3                                                          
         CLC   QPRODUCT,=C'ALL'                                                 
         BNE   ENDDIVX                                                          
*                                                                               
ENDDIV3  DS    0H                                                               
**NEW 2/18/88                                                                   
         CLI   RCALLALL,C'Y'          SEE IF SPECIAL EC/52 REQUEST              
         BE    ENDDIVX                FROM BILLING REQUEST                      
*                                     NO BRAND SUMMARIES                        
**NEW 2/18/88                                                                   
         GOTO1 ABRDSUM                                                          
*                                                                               
ENDDIVX  L     R2,ADIVTOT                                                       
         BAS   RE,ENDCLR                                                        
         L     R2,ADIVTOTB                                                      
         BAS   RE,ENDCLR                                                        
         MVI   LINE,99                                                          
         B     ENDCB                                                            
         EJECT                                                                  
* END OF CLIENT                                                                 
*                                                                               
ENDCLT   DS    0H                                                               
         CLC   QDIST,=C'ALL'                                                    
         BNE   *+10                                                             
         XC    PDSTKEY,PDSTKEY                                                  
*                                                                               
         CLC   QREGION,=C'ALL'                                                  
         BNE   *+10                                                             
         XC    PREGKEY,PREGKEY                                                  
*                                                                               
         CLC   QPRODUCT,=C'ALL'                                                 
         BE    ENDCLT1                                                          
         CLI   QPRODUCT,C' '                                                    
         BNE   ENDCLTX                                                          
ENDCLT1  DS    0H                                                               
         CLC   QDIV,=C'ALL'                                                     
         BE    ENDCLT1A                                                         
         CLI   QDIV,C' '                                                        
         BNE   ENDCLTX                                                          
ENDCLT1A DS    0H                                                               
         XC    PDIVKEY,PDIVKEY                                                  
         XC    PPRDKEY,PPRDKEY                                                  
*                                                                               
         CLC   QEST,=C'ALL'                                                     
         BNE   *+10                                                             
         XC    PESTKEY,PESTKEY                                                  
*                                                                               
         LA    R4,MYSUMHD8+35                                                   
         SR    R6,R6                                                            
         ICM   R6,7,CLHD                                                        
         LA    R7,L'PP@CLI                                                      
         BCTR  R7,0                                                             
         EX    R7,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(R6)                                                    
         LA    R5,08(R4)           FOR REST OF HEADING                          
         CLI   0(R5),C' '                                                       
         BNE   *+8                                                              
         BCT   R5,*-8                                                           
         LA    R5,2(R5)                                                         
         GOTO1 AESSUM,DMCB,ACLTTOT                                              
*                                                                               
         BAS   RE,ENDMOD                                                        
*                                                                               
         EX    R7,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(R6)                                                    
         GOTO1 (RF),(R1),ACLTTOTB                                               
*                                                                               
         CLI   QPRODUCT,C' '                                                    
         BE    ENDCLT3                                                          
         CLC   QPRODUCT,=C'ALL'                                                 
         BNE   ENDCLTX                                                          
*                                                                               
ENDCLT3  DS    0H                                                               
**NEW 2/18/88                                                                   
         CLI   RCALLALL,C'Y'          SEE IF SPECIAL EC/52 REQUEST              
         BE    ENDCLTX                FROM BILLING REQUEST                      
*                                     NO BRAND SUMMARIES                        
**NEW 2/18/88                                                                   
*                                                                               
*                                                                               
*        DO BRAND SUMMARIES IF PRD= ALL                                         
*        OR PRD= BLANK AND EST IS NOT ALL                                       
*                                                                               
         CLC   QPRODUCT,=C'ALL'                                                 
         BE    ENDCLT5                                                          
         CLC   QEST,=C'ALL'                                                     
         BE    ENDCLTX               CAN'T DO BRAND SUMMARIES                   
*                                    IN THIS CASE SINCE BRAND                   
*                                    SUMMARIES WERE DONE IN ENDEST              
*                                                                               
ENDCLT5  DS    0H                                                               
         GOTO1 ABRDSUM                                                          
*                                                                               
ENDCLTX  DS    0H                                                               
         LA    R0,9                                                             
         LA    R3,APUBTOT                                                       
         L     R2,0(R3)                                                         
         BAS   RE,ENDCLR                                                        
         LA    R3,4(R3)            NEXT TOTAL ADDRESS                           
         BCT   R0,*-12                                                          
         LA    R0,6                                                             
         LA    R3,ADSTTOTB                                                      
         L     R2,0(R3)                                                         
         BAS   RE,ENDCLR                                                        
         LA    R3,4(R3)                                                         
         BCT   R0,*-12                                                          
         MVI   LINE,99                                                          
         B     ENDCB                                                            
         EJECT                                                                  
* END OF X-MEDIA REQ                                                            
*                                                                               
ENDXRQ   DS    0H                                                               
*                                                                               
         MVI   FMULTMED,C' '    CLEAR FIRST MEDIA FOUND                         
*                                                                               
         MVC   MEDIANM(L'PP@PMEST),PP@PMEST                                     
         MVC   MEDIAUL,SPACES                                                   
         MVC   MEDIAUL(L'PP$PMEST),PP$PMEST                                     
*                                                                               
         CLC   QPRODUCT,=C'ALL'                                                 
         BNE   *+10                                                             
         XC    PPRDKEY,PPRDKEY      CLEAR PRODUCT                               
*                                                                               
         LA    R4,MYSUMHD8+35                                                   
         SR    R6,R6                                                            
         ICM   R6,7,XMHD                                                        
         LA    R7,L'PP@PRINT                                                    
         BCTR  R7,0                                                             
         EX    R7,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(R6)                                                    
         LA    R5,07(R4)           FOR REST OF HEADING                          
         CLI   0(R5),C' '                                                       
         BNE   *+8                                                              
         BCT   R5,*-8                                                           
         LA    R5,2(R5)                                                         
         GOTO1 AESSUM,DMCB,AXMDTOT                                              
*                                                                               
         BAS   RE,ENDMOD                                                        
*                                                                               
         EX    R7,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(R6)                                                    
         GOTO1 (RF),(R1),AXMDTOTB                                               
*                                                                               
ENDXRQX  DS    0H                                                               
         L     R2,AXMDTOT                                                       
         BAS   RE,ENDCLR                                                        
         L     R2,AXMDTOTB                                                      
         BAS   RE,ENDCLR                                                        
         MVI   LINE,99                                                          
         B     ENDCB                                                            
*                                                                               
         SPACE 2                                                                
ENDCB    DS    0H                                                               
         MVI   PRNTSW,0                                                         
ENDCB2   DS    0H                                                               
         XIT1                                                                   
         SPACE 2                                                                
*                                                                               
*                                                                               
*                                                                               
ENDCLR   DS    0X                                                               
         LA    RF,191                                                           
         ZAP   0(8,R2),=P'0'                                                    
         LA    R2,8(R2)                                                         
         BCT   RF,*-10                                                          
         BR    RE                                                               
*                                                                               
*                                                                               
*                                                                               
         EJECT                                                                  
ENDMOD   NTR1                                                                   
         SPACE 2                                                                
         L     R3,ATOT                                                          
         LA    RE,ACCL/8           191 SETS                                     
         LR    RF,R3                                                            
         CP    0(8,RF),=P'0'                                                    
         BNE   ENDM5               MUST STILL PRINT RECAP                       
         LA    RF,8(RF)                                                         
         BCT   RE,*-14                                                          
*                                                                               
         CLI   ESTACTIV,C'A'       CHK FOR REQ ACTIVITY                         
         BNE   EM5                                                              
*                                                                               
         CLI   MODE,LBUYPRO                                                     
         BE    ENDM2                                                            
         CLI   MODE,LBUYEST                                                     
         BNE   ENDM5                                                            
*                                                                               
ENDM2    CLI   COMSW,C'E'     WILL BE 'E' IF THE WAS ACTIVITY                   
         BE    ENDM5                                                            
         MVI   COMSW,0                                                          
         B     EM5        EXIT - SO I WON'T RE-PRINT USER COMMENTS              
*                         OR PRINT BLANK PAGES                                  
*                                                                               
ENDM5    DS    0H            DISPLAY USER FIELDS MOVED TO COMPRT                
*                                                                               
EM1      DS    0H                                                               
         B     EM2          MUST NOW ALWAYS GO TO COMPRT                        
*                                                                               
EM2      DS    0H                                                               
         CLI   LINE,99            SEE IF GOING TO NEW PAGE                      
         BE    EM2C               YES - NO NEED TO SKIP                         
         MVI   SPACING,3                                                        
         GOTO1 APRNT                                                            
EM2C     DS    0H                                                               
         MVI   COMSW,C'S'                                                       
         GOTO1 ACOMPRT                                                          
         MVI   COMSW,0                                                          
EM3      DS    0H                                                               
         CLI   ESCPBREC,C'0'       PUB RECAP                                    
         BNE   EM4                 YES                                          
         CLC   QREGION,=C'ALL'                                                  
         BE    EM3C                                                             
         CLC   QDIST,=C'ALL'                                                    
         BNE   EM5                                                              
EM3C     DS    0H                  FOR REG/DST RECAP                            
         MVC   WRITTOTS(72),DRDTOTS                                             
         GOTO1 AWFMOD                                                           
*                                                                               
         LA    RE,DRDTOTS          INITIALIZE DRDTOT (9PL8)                     
         LA    RF,9                                                             
         ZAP   0(8,RE),=P'0'                                                    
         LA    RE,8(RE)                                                         
         BCT   RF,*-10                                                          
*                                                                               
EM4      DS    0H                                                               
         MVC   WRITTOTS(72),GRDTOTS                                             
         GOTO1 AWFMOD                                                           
EM5      DS    0H                                                               
         B     ENDCB2                                                           
         SPACE 3                                                                
         LTORG                                                                  
         SPACE 2                                                                
DC1LIST  DS    0C                                                               
         DCDDL PP#PMEST,25,C                                                    
         DCDDL PP#PMEST,25,CU                                                   
         DCDDL PP#DIST,12                                                       
         DCDDL PP#REGN,12                                                       
         DCDDL PP#ESTM,12                                                       
         DCDDL PP#PRO,12                                                        
         DCDDL PP#DIV,12                                                        
         DCDDL PP#CLI,12                                                        
         DCDDL PP#PRINT,12                                                      
DC1LISTX DC    X'00'                                                            
*                                                                               
DS1LIST  DS    0C                                                               
         DSDDL PRINT=YES                                                        
DS1LISTX EQU   *                                                                
         PRINT NOGEN                                                            
         SPACE 3                                                                
*                                                                               
DSHD     DC    AL3(PP@DIST)                                                     
RGHD     DC    AL3(PP@REGN)                                                     
ESHD     DC    AL3(PP@ESTM)                                                     
PRHD     DC    AL3(PP@PRO)                                                      
DVHD     DC    AL3(PP@DIV)                                                      
CLHD     DC    AL3(PP@CLI)                                                      
XMHD     DC    AL3(PP@PRINT)                                                    
         TITLE 'PP5202 - SET UP CONTRACT PRINT LINES'                           
BUYCON   CSECT                                                                  
         NMOD1 0,BUYCON                                                         
         USING PPWORKD,RA          RA=V(PPWORKC)                                
         L     RC,PPFILEC                                                       
         USING PPFILED,RC,R9       R9=RC+4096                                   
         USING PP52WRKD,R8         R8=A(PP52WRKC)                               
*                                                                               
         L     RF,PPWORK2C                                                      
         USING PPWORK2D,RF                                                      
         MVC   ACONIO1,ACONIO      (A)PCONREC                                   
         DROP  RF                                                               
*                                                                               
         XC    DMCB,DMCB                                                        
         LA    R1,DMCB                                                          
         USING DICTATED,R1                                                      
         MVI   DDACTN,DDACTNL                                                   
         MVI   DDRETN,DDCASEU                                                   
         MVI   DDSYS,4                                                          
         MVC   DDLANG,RCLANG                                                    
         LA    RF,DC2LIST                                                       
         STCM  RF,7,DDIADR                                                      
         LA    RF,DS2LIST                                                       
         STCM  RF,7,DDOADR                                                      
         GOTO1 ADICTATE                                                         
         DROP  R1                                                               
         L     R6,AKLINES          LINE 1                                       
         CLI   0(R6),0             FIND FIRST 0                                 
         BE    *+12                                                             
         LA    R6,30(R6)                                                        
         B     *-12                                                             
*                                                                               
* SEE IF CONTRACT EXISTS                                                        
*                                                                               
         XC    WORK,WORK                                                        
         MVC   WORK(7),PBUYKEY     BUILD CON KEY                                
*                                                                               
BC2X     DS    0H                                                               
*                                                                               
         CLI   PCLTPROF+5,C'2'                                                  
         BNE   *+10                                                             
         MVC   WORK+4(3),PCLTPROF+6  MASTER CLIENT                              
         MVI   WORK+3,X'10'                                                     
         MVC   WORK+7(6),PBUYKEY+10                                             
*                                                                               
BC3D     DS    0H                                                               
         OC    SADVDATA,SADVDATA    SEE IF AOR SITUATION                        
         BZ    BC3X                                                             
         CLC   SADVDATA,QAGENCY     SEE IF I AM THE AOR                         
         BE    BC3X                                                             
         TM    SADVDATA+15,X'20'    SEE IF I HAVE MY OWN CONTRACTS              
         BNO   BC3X                                                             
         MVC   WORK(2),SADVDATA     SET AOR IN WORK                             
         MVC   WORK+4(3),SADVDATA+2 MUST USE ADV (CLT CODE ON AOR)              
         TM    SADVDATA+15,X'01'    SEE IF PUB LINK REQUIRED                    
         BNO   BC3X                                                             
*                                                                               
         LA    R2,PUBREC+33                                                     
         MVI   ELCOD,X'80'                                                      
BC3F     BAS   RE,BCNEXTEL                                                      
         BNE   BC3X                                                             
         USING PUBADVD,R2                                                       
         CLC   PUBAORCD,SADVDATA                                                
         BNE   BC3F                                                             
         MVC   WORK+7(6),PUBADVPC     AOR PUB                                   
*                                                                               
BC3X     DS    0H                                                               
         L     RF,ACONIO1          (A)PCONREC                                   
         USING PCONREC,RF                                                       
         CLC   WORK(13),PCONKEY                                                 
         BNE   BC4                                                              
         CLC   PCONSDT,PBUYKDAT                                                 
         BH    BC4                                                              
         CLC   PCONEDT,PBUYKDAT                                                 
         BNL   BC8                                                              
*                                                                               
         DROP  RF                                                               
*                                                                               
BC4      DS    0H                  NO CONTRACT                                  
         CLI   CONSW,C'C'                                                       
         BNE   *+8                                                              
         BAS   RE,ENDCON                                                        
         CLI   QMEDIA,C'N'                                                      
         BNE   BUYCONX                                                          
         LA    R2,PUBREC+33                                                     
         MVI   ELCOD,X'20'                                                      
         BAS   RE,BCNEXTEL                                                      
         BNE   BUYCONX                                                          
         USING PUBGENEL,R2                                                      
         CLI   PUBFLAT,C' '                                                     
         BNH   BUYCONX                                                          
         CLC   CONWRK(13),WORK     TEST ALREADY PRINTED FLAT/OPEN               
         BE    BUYCONX             YEA                                          
*                                  SET UP DUMMY CONTRACT                        
         MVI   CONSW,C'N'                                                       
         XC    CONWRK,CONWRK                                                    
         MVC   CONWRK(13),WORK     PART OF KEY                                  
         LA    R7,CONWRK                                                        
         MVC   PCONELEM-PCONREC(2,R7),=X'1025'                                  
         MVC   PCONSDT-PCONREC(6,R7),BSTART                                     
         MVC   PRBELEM-PCONREC(2,R7),=X'202A'                                   
         ZAP   PRBLEVEL-PCONREC(5,R7),=P'0'                                     
         MVC   PRBDESC-PCONREC(17,R7),SPACES                                    
         ZAP   PRBPCT-PCONREC(3,R7),=P'0'                                       
         CLI   PUBFLAT,C'F'                                                     
         BNE   *+8                                                              
         MVI   PRBIND-PCONREC(R7),X'01'                                         
         GOTO1 ARTLOOK,DMCB,0,PUBREC,CONWRK,ARTLKELS                            
*                                                                               
         L     RF,ARTLKELS                                                      
         CLI   0(RF),0           NO RATE ON FILE                                
         BNE   BC10                                                             
         MVC   0(L'PP@FLAT,R6),PP@FLAT                                          
         CLI   PUBFLAT,C'F'                                                     
         BE    *+10                                                             
         MVC   0(L'PP@OPEN,R6),PP@OPEN                                          
         B     BUYCONX                                                          
         DROP  R2                                                               
*                                                                               
BC8      DS    0H                                                               
         CLI   CONSW,0                                                          
         BE    BC9                                                              
*                                                                               
         L     RF,ACONIO1          (A)PCONREC                                   
         USING PCONREC,RF                                                       
*                                                                               
         CLC   CONWRK(25),PCONKEY  SAME CONTRACT                                
         BE    BUYCONXX            YES- DONE                                    
*                                                                               
         DROP  RF                                                               
*                                                                               
         BAS   RE,ENDCON                                                        
BC9      DS    0H                                                               
*                                                                               
         L     RF,ACONIO1          (A)PCONREC                                   
         USING PCONREC,RF                                                       
*                                                                               
         MVI   CONSW,C'C'                                                       
         MVC   CONWRK(25),PCONKEY                                               
         MVC   0(L'PP@CONTR,R6),PP@CONTR                                        
         MVC   HALF,PCONNUM                                                     
         LH    R0,HALF                                                          
         EDIT  (R0),(3,9(R6)),ALIGN=LEFT                                        
*                                                                               
         GOTO1 DATCON,DMCB,(3,PCONSDT),(5,12(R6))                               
*                                                                               
         L     RF,ACONIO1          (A)PCONREC                                   
*                                                                               
*NOP*    GOTO1 (RF),(R1),(3,PCONEDT),(5,21(R6))                                 
         GOTO1 DATCON,(R1),(3,PCONEDT),(5,21(R6))                               
         MVI   20(R6),C'-'                                                      
*                                                                               
         LA    R6,30(R6)           LINE 2                                       
* CHECK FOR RATELOOK REQUIRED                                                   
         L     R2,ACONIO1          (A)PCONREC                                   
*                                                                               
*NOP*    LA    R2,PCONREC+33                                                    
         LA    R2,33(R2)                                                        
         MVI   ELCOD,X'20'                                                      
         BAS   RE,BCNEXTEL                                                      
*                                                                               
         BNE   BUYCON20                                                         
         LA    R7,PRBIND-PRBELEM(R2)                                            
         TM    0(R7),X'80'                                                      
         BO    BCRTX                                                            
         CLI   QMEDIA,C'N'                                                      
         BNE   BCRTX                                                            
         CP    PRBRATE-PRBELEM(5,R2),=P'0'                                      
         BNE   BCRTX                                                            
* AWAY WE GO                                                                    
         L     RF,ACONIO1          (A)PCONREC                                   
*                                                                               
         GOTO1 ARTLOOK,DMCB,0,PUBREC,PCONREC,ARTLKELS                           
*                                                                               
         DROP  RF                                                               
*                                                                               
BC10     DS    0H                                                               
         L     RF,ARTLKELS                                                      
         CLI   0(RF),0                                                          
         BE    BUYCON20                                                         
         CLI   0(R1),0                                                          
         BNE   BCRTX                                                            
         L     R2,ARTLKELS                                                      
*                                                                               
BCRTX    B     *+12                                                             
*                                                                               
BUYCON2  BAS   RE,BCNEXTEL                                                      
         BNE   BUYCON20                                                         
         CLI   QMEDIA,C'N'                                                      
         BE    BUYCON2B                                                         
         CLI   QPRODUCT,C' '                                                    
         BE    BUYCON2B                                                         
         LA    R4,PRBOPEN-PRBELEM(R2)                                           
         OC    0(3,R4),0(R4)                                                    
         BZ    BUYCON2B                                                         
         CLC   0(3,R4),PPRDKPRD    TEST RIGHT PRODUCT                           
         BNE   BUYCON2                                                          
BUYCON2B DS    0H                                                               
         LA    R4,PRBDATE-PRBELEM(R2)                                           
         OC    0(3,R4),0(R4)                                                    
         BZ    BUYCON4                                                          
         MVC   0(L'PP@EFFCT,R6),PP@EFFCT                                        
         LA    R5,L'PP@EFFCT+1(R6)                                              
         CLI   ESCCONC,C'S'                                                     
         BNE   *+14                                                             
         MVC   0(L'PP@RTCHA,R6),PP@RTCHA                                        
         LA    R5,L'PP@RTCHA+1(R6)                                              
         GOTO1 DATCON,DMCB,(3,(R4)),(5,(R5))                                    
         LA    R6,30(R6)                                                        
*                                                                               
BUYCON4  LA    R4,PRBLIND-PRBELEM(R2)                                           
         CLI   0(R4),C'S'                                                       
         BNE   BUYCON4B                                                         
         MVI   0(R6),C'S'          SPECIAL                                      
         LA    R7,0(R6)            IF S, DONT PRINT LEVEL,ETC                   
         B     BUYCON6A                                                         
**                                                                              
BUYCON4B LA    R4,PRBLEVEL-PRBELEM(R2)                                          
         CP    0(L'PRBLEVEL,R4),=P'0'                                           
         BNZ   BUYCON5                                                          
         MVC   0(L'PP@OPEN,R6),PP@OPEN                                          
         LA    R4,PRBIND-PRBELEM(R2)                                            
         TM    0(R4),X'01'                                                      
         BZ    BUYCON6                                                          
         MVC   0(L'PP@FLAT,R6),PP@FLAT                                          
         B     BUYCON6                                                          
*                                                                               
* LEVEL NOT 0 SO DISPLAY IT                                                     
*                                                                               
BUYCON5  XC    WORK2,WORK2                                                      
         EDIT  (P5,(R4)),(10,WORK2),ALIGN=LEFT                                  
         LA    R4,PRBLIND-PRBELEM(R2)                                           
         CLI   0(R4),C'$'                                                       
         BE    BUYCON5A                                                         
         CLI   0(R4),C'N'      OR NET $ VOLUME                                  
         BNE   BUYCON5D                                                         
BUYCON5A MVI   0(R6),C'$'                                                       
         MVC   1(10,R6),WORK2                                                   
         B     BUYCON6                                                          
*                                                                               
BUYCON5D LA    R7,WORK2                                                         
         AR    R7,R0               POINT PAST LAST CHAR                         
         MVC   0(1,R7),0(R4)       MOVE IND                                     
         MVC   0(10,R6),WORK2                                                   
*                                                                               
BUYCON6  LA    R7,12(R6)           FLOAT REST OF LINE                           
         BAS   RE,BCFLOAT                                                       
         LA    R7,2(R7)            START OF NEXT FIELD                          
*                                                                               
BUYCON6A LA    R4,PRBPCT-PRBELEM(R2)                                            
         CP    0(L'PRBPCT,R4),=P'0'                                             
         BZ    BUYCON8                                                          
         ST    RE,SAVERE1                                                       
         ST    RF,SAVERF                                                        
         ST    R1,SAVER1                                                        
         MVI   MYCTAB+3,2                                                       
         CURED (P3,(R4)),(5,(R7)),MYCTAB,ALIGN=LEFT,MODADDR=ACUREDIT            
         L     R1,SAVER1                                                        
         L     RF,SAVERF                                                        
         L     RE,SAVERE1                                                       
         AR    R7,R0                                                            
         SH    R7,=H'3'                                                         
         CLC   0(3,R7),=C'.00'                                                  
         BE    *+8                                                              
         LA    R7,3(R7)                                                         
         MVC   0(L'PP@PCT,R7),PP@PCT                                            
         LA    R7,5(R7)                                                         
*                                                                               
BUYCON8  LA    R4,PRBRATE-PRBELEM(R2)                                           
         CLI   ESCCONC,C'S'        TEST SUPPRESS RATE/DATE                      
*                                                                               
         BE    BUYCON18            YES                                          
*                                                                               
         CP    0(L'PRBRATE,R4),=P'0'                                            
         BZ    BUYCON10                                                         
         TM    PRBIND-PRBELEM(R2),X'10'     TEST NET RATE           L01         
         BNO   *+8    IF NET PRESENT      MOVE AN N                 L01         
         MVI   0(R7),C'N'       FOR 'N'                             L01         
         TM    PRBIND-PRBELEM(R2),X'02'     TEST   S RATE           L02         
         BNO   *+8                        MOVE AN S                 L02         
         MVI   0(R7),C'S'       FOR 'S'                             L02         
         TM    PRBIND-PRBELEM(R2),X'04'     TEST   C RATE           L03         
         BNO   *+8                        MOVE AN C                 L03         
         MVI   0(R7),C'C'       FOR 'C'     COMMISSION ONLY         L03         
         LA    R7,1(R7)                                             L01         
         TM    PRBIND-PRBELEM(R2),X'40'     TEST TOTAL RATE                     
         BNZ   BCON8A                       YES                                 
         TM    PRBIND-PRBELEM(R2),X'20'     TEST UNIT RATE                      
         BNZ   BUYCON8A                                                         
         CLI   QMEDIA,C'N'         IF NEWS - ASSUME UNIT RATE                   
         BE    BUYCON8A                                                         
BCON8A   DS    0H                                                               
         ST    RE,SAVERE1                                                       
         ST    RF,SAVERF                                                        
         ST    R1,SAVER1                                                        
         MVI   MYCTAB+3,2                                                       
         PRINT GEN                                                              
         CURED (P5,(R4)),(10,(R7)),MYCTAB,COMMAS=YES,ALIGN=LEFT,MODADDRX        
               =ACUREDIT,CURSYMB=YES                                            
         PRINT NOGEN                                                            
         L     R1,SAVER1                                                        
         L     RF,SAVERF                                                        
         L     RE,SAVERE1                                                       
*                                                                               
         LR    RF,R7                                                            
         AR    RF,R0                                                            
         SH    RF,=H'3'                                                         
         CLC   0(3,RF),=C'.00'                                                  
         BNE   *+10                                                             
         MVC   0(3,RF),SPACES                                                   
*                                                                               
         B     BUYCON10                                                         
BUYCON8A ST    RE,SAVERE1                                                       
         ST    RF,SAVERF                                                        
         ST    R1,SAVER1                                                        
         MVI   MYCTAB+3,5                                                       
         CURED (P5,(R4)),(10,(R7)),MYCTAB,ALIGN=LEFT,MODADDR=ACUREDIT           
         L     R1,SAVER1                                                        
         L     RF,SAVERF                                                        
         L     RE,SAVERE1                                                       
*                                                                               
         LR    RF,R7                                                            
         AR    RF,R0                                                            
         LR    RE,RF                                                            
         SH    RE,=H'3'                                                         
BUYCON8B DS    0H                                                               
         CLI   0(RF),C'0'                                                       
         BH    BUYCON8C                                                         
         MVI   0(RF),C' '                                                       
         BCTR  RF,R0                                                            
         CR    RF,RE                                                            
         BNL   BUYCON8B                                                         
BUYCON8C DS    0H                                                               
         MVC   1(2,RF),=C'/I'                                                   
         TM    PRBIND-PRBELEM(R2),X'08'    SEE IF INCH RATE INPUT               
         BO    BUYCON8F                                                         
         CLI   PRBLIND-PRBELEM(R2),C'I'                                         
         BE    BUYCON8F                                                         
         MVC   1(2,RF),=C'/L'                                                   
BUYCON8F DS    0H                                                               
         LA    R7,12(R7)           ALLOW FOR /I OR /L                           
         B     BYCON10A                                                         
*                                                                   L01         
BUYCON10 LA    R7,10(R7)                                                        
BYCON10A BAS   RE,BCFLOAT                                                       
         LA    R7,2(R7)            NEXT SLOT IS +2                              
*                                                                               
         LA    R4,PRBDESC-PRBELEM(R2)  FILL REST OF LINE WITH DESC              
         CLI   0(R4),X'FF'         TEST FOR SPECIAL OUTDOOR FIELDS              
         BE    BUYCON12               YES                                       
         LR    RF,R7               SAVE R7                                      
         XC    WORK(18),WORK                                                    
         MVC   WORK(17),0(R4)                                                   
         CLI   QMEDIA,C'N'         IF NEWSPAPERS                                
         BNE   BYCON10F                                                         
         CLC   WORK(2),=C'R='      CHECK FOR RATE CODE                          
         BNE   BYCON10F                                                         
         XC    WORK(18),WORK                                                    
         MVC   WORK(5),0(R4)                                                    
         MVI   WORK+5,C' '                                                      
         LA    R1,WORK+5                                                        
         CLI   WORK+4,C' '                                                      
         BH    BYCON10B                                                         
         LA    R1,WORK+4                                                        
         CLI   WORK+3,C' '                                                      
         BH    BYCON10B                                                         
         LA    R1,WORK+3                                                        
*                                                                               
BYCON10B MVI   0(R1),C','                                                       
         MVC   1(12,R1),5(R4)     RATE CODE DESCRIPTION                         
*                                                                               
BYCON10F DS    0H                                                               
         LA    R7,WORK+17                                                       
         BAS   RE,BCFLOAT                                                       
         LA    RE,WORK                                                          
         SR    R7,RE                                                            
         LR    R0,R7               R0 = LENGTH OF DESC - 1                      
         LR    R7,RF               RESTORE R7                                   
         LA    RE,28(R6)                                                        
         SR    RE,R7               SET RE FOR EX MOVE                           
         CR    RE,R0                                                            
         BL    BUYCON11            WILL NOT FIT                                 
         CH    RE,=H'16'                                                        
         BNH   *+8                                                              
         LA    RE,16                                                            
         EX    RE,BUYCONMW      (FROM WORK)                                     
         B     BUYCON18                                                         
BUYCON11 DS    0H                                                               
         LA    R6,30(R6)                                                        
         MVC   0(18,R6),WORK       WHOLE DESC ON 2ND LINE                       
         B     BUYCON18                                                         
*                                                                               
BUYCON12 DS    0H                  SPECIAL OUTDOOR FIELDS                       
         LA    R5,WORK                                                          
         XC    WORK(24),WORK                                                    
         USING PPBYOUTD,R5                                                      
         ST    R4,PBYOINPT                                                      
         MVI   PBYOCTL,X'80'       SET SPACE ONLY INPUT                         
         GOTO1 PPBYOUT,DMCB,PPBYOUTD                                            
*                                  PUT SHOWING AND REG,ILLUM                    
*                                  INTO 30 CHARS                                
         MVC   WORK(11),PBYOSPC1                                                
         MVC   WORK+13(20),PBYOSPC2                                             
         DROP  R5                                                               
*                                                                               
         CLI   WORK+40,C' '                                                     
         BE    BUYCON13                                                         
         LA    R7,WORK+20                                                       
         BAS   RE,BCFLOAT                                                       
         MVI   1(R7),C'-'                                                       
         MVC   3(20,R7),WORK+40                                                 
BUYCON13 DS    0H                                                               
         MVC   0(30,R6),WORK                                                    
*                                                                               
*                                                                               
BUYCON18 DS    0H                                                               
         LR    RF,R6                                                            
         SH    RF,=H'30'                                                        
         CLC   0(30,RF),0(R6)      CHECK FOR LINE DUPLICATION                   
         BNE   BUYCON19                                                         
         XC    0(30,R6),0(R6)                                                   
         B     BUYCON2                                                          
BUYCON19 DS    0H                                                               
         SH    RF,=H'60'                                                        
         CLC   0(60,RF),60(RF)     CHECK FOR 2 LINE DUPLICATION                 
         BNE   BUYCN19B                                                         
         XC    60(60,RF),60(RF)                                                 
         SH    R6,=H'30'                                                        
         B     BUYCON2                                                          
BUYCN19B DS    0H                                                               
         CLI   0(R6),0                                                          
         BE    BUYCON2                                                          
         LA    R6,30(R6)           NEXT DISPLAY LINE                            
         B     BUYCON2                                                          
*                                                                               
BUYCON20 DS    0H                                                               
*NOP*    LA    R2,PCONREC+33                                                    
         L     R2,ACONIO1          (A)PCONREC                                   
         LA    R2,33(R2)                                                        
*                                                                               
BUYCON22 MVI   ELCOD,X'40'                                                      
         BAS   RE,BCNEXTEL                                                      
         BNE   BUYCONX                                                          
         CLC   2(2,R2),=C'E-'                                                   
         BE    BUYCON24                                                         
         CLC   2(2,R2),=C'E+'                                                   
         BNE   BUYCON22                                                         
BUYCON24 SR    RE,RE                                                            
         IC    RE,1(R2)                                                         
         SH    RE,=H'5'            CODE/LEN/E- +1 TO SET FOR EX                 
         BNP   BUYCON22                                                         
         LR    R7,R6                                                            
         LA    R4,4(R2)                                                         
         EX    RE,BUYCONMV                                                      
*                                                                               
         LA    R6,30(R6)           NEXT DISPLAY LINE                            
         B     BUYCON22                                                         
*                                                                               
* STORE PRINT LINES REQUIRED IN HOB OF ACURKLN                                  
*                                                                               
BUYCONX  L     RE,AKLINES                                                       
         SR    R0,R0               SET BCT COUNTER                              
         CLI   0(RE),0                                                          
         BE    *+12                                                             
         LA    RE,30(RE)                                                        
         BCT   R0,*-12                                                          
         LPR   R0,R0                                                            
         STC   R0,ACURKLN                                                       
*                                                                               
BUYCONXX DS    0H                                                               
         XMOD1 1                                                                
BUYCONMV MVC   0(0,R7),0(R4)       * EXECUTED *                                 
BUYCONMW MVC   0(0,R7),WORK        * EXECUTED *                                 
         EJECT                                                                  
BCFLOAT  CLI   0(R7),C' '                                                       
         BH    BCFLOATX                                                         
         BCT   R7,BCFLOAT                                                       
BCFLOATX BR    RE                                                               
         SPACE 2                                                                
BCNEXTEL SR    R0,R0                                                            
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         CLI   0(R2),0                                                          
         BE    BCNXTELX                                                         
         CLC   ELCOD,0(R2)                                                      
         BCR   8,RE                                                             
         B     BCNEXTEL                                                         
BCNXTELX LTR   R2,R2                                                            
         BR    RE                                                               
         SPACE 3                                                                
ENDCON   DS    0H                  FINISH CONTRACT                              
         LR    R0,RE                                                            
         MVI   CONSW,0                                                          
ENDC1    DS    0H                                                               
         L     RE,ACURKLN          PRINT ANY REMAINING K LINES                  
         CLI   0(RE),0                                                          
         BE    ENDC2                                                            
         MVC   P+1(30),0(RE)                                                    
         LA    RE,30(RE)                                                        
         ST    RE,ACURKLN                                                       
         GOTO1 APRNT                                                            
*                                                                               
         B     ENDC1                                                            
ENDC2    DS    0H                                                               
         L     RE,AKLINES                                                       
         LA    RF,120*30                                                        
         XCEF                                                                   
         SPACE 2                                                                
         L     R6,AKLINES                                                       
         ST    R6,ACURKLN                                                       
         LR    RE,R0                                                            
         BR    RE                                                               
         SPACE 2                                                                
         LTORG                                                                  
*                                                                               
ACONIO1  DS    A                   ADDRESS OF PCONREC FROM PPG                  
         SPACE 3                                                                
DC2LIST  DS    0C                                                               
         DCDDL PP#FLAT,4                                                        
         DCDDL PP#OPEN,6                                                        
         DCDDL PP#CONTR,8                                                       
         DCDDL PP#EFFCT,10                                                      
         DCDDL PP#RTCHA,13                                                      
         DCDDL PP#PCT,4,R                                                       
DC2LISTX DC    X'00'                                                            
*                                                                               
DS2LIST  DS    0C                                                               
         DSDDL PRINT=YES                                                        
DS2LISTX EQU   *                                                                
*                                                                               
***********************************************************************         
*                                                                               
         TITLE 'PP5202 - ESSUM - PRINT MONTHLY SUMMARIES'                       
*                                                                               
ESSUM    CSECT                                                                  
         NMOD1 0,ESSUM                                                          
         USING PPWORKD,RA          RA=V(PPWORKC)                                
         L     RC,PPFILEC                                                       
         USING PPFILED,RC,R9       R9=RC+4096                                   
         USING PP52WRKD,R8         R8=A(PP52WRKC)                               
*                                                                               
         MVI   SUMSW,C'I'          FIRST SUMMARY                                
         MVC   SUMSUMS,ESCSUMS                                                  
         CLC   1(3,R1),ADSTTOTB+1                                               
         BL    *+14                                                             
         MVI   SUMSW,C'B'          SECOND SUMMARY                               
         MVC   SUMSUMS,ESCSUMSB                                                 
*                                                                               
         LA    R2,QTRTOTS          INITIALIZE QTRTOT (9PL8)                     
         LA    R3,9                                                             
         ZAP   0(8,R2),=P'0'                                                    
         LA    R2,8(R2)                                                         
         BCT   R3,*-10                                                          
         LA    R2,GRDTOTS          INITIALIZE GRDTOT (9PL8)                     
         LA    R3,9                                                             
         ZAP   0(8,R2),=P'0'                                                    
         LA    R2,8(R2)                                                         
         BCT   R3,*-10                                                          
*                                                                               
         L     R3,0(R1)            GET ACCUM ADDRESS                            
         ST    R3,ATOT             SAVE ADDR OF ACCUM                           
*                                                                               
         XC    DMCB,DMCB                                                        
         LA    R1,DMCB                                                          
         USING DICTATED,R1                                                      
         MVI   DDACTN,DDACTNL                                                   
         MVI   DDRETN,DDCASEU                                                   
         MVI   DDSYS,4                                                          
         MVC   DDLANG,RCLANG                                                    
         LA    RF,DC3LIST                                                       
         STCM  RF,7,DDIADR                                                      
         LA    RF,DS3LIST                                                       
         STCM  RF,7,DDOADR                                                      
         GOTO1 ADICTATE                                                         
         DROP  R1                                                               
*                                                                               
         LR    RF,R3               SAVE REGISTER 3                              
         LA    R2,190              LAST PL8 IS PRD CODE                         
         CP    0(8,R3),=P'0'       ANYTHING?                                    
         BNE   ESSUM02                                                          
         LA    R3,8(R3)                                                         
         BCT   R2,*-14             190 SETS                                     
         LR    R3,RF                                                            
         B     ESSUMX                                                           
*                                                                               
ESSUM02  DS    0X                                                               
         LR    R3,RF               RESTORE REGISTER 3                           
         MVI   LINE,99                                                          
         MVI   SPACING,1                                                        
*                                  SET HEADING                                  
         LA    R2,ESCSUM1          1ST SUMM                                     
         CLI   SUMSW,C'I'                                                       
         BE    *+8                                                              
         LA    R2,ESCSUM2                                                       
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,7,OMHD                                                        
         CLI   0(R2),C'S'          SALE                                         
         BE    ESSUM04                                                          
         SR    RF,RF                                                            
         ICM   RF,7,BMHD                                                        
         CLI   0(R2),C'B'          BILABLE                                      
         BE    ESSUM04                                                          
         SR    RF,RF                                                            
         ICM   RF,7,PYMHD                                                       
         CLI   0(R2),C'P'          PAYABLE                                      
         BE    ESSUM04                                                          
         SR    RF,RF                                                            
         ICM   RF,7,CLMHD                                                       
         CLI   0(R2),C'C'          CLOSING                                      
         BE    ESSUM04                                                          
         SR    RF,RF                                                            
         ICM   RF,7,PMHD                                                        
         CLI   QMEDIA,C'O'                                                      
         BE    ESSUM04                                                          
         SR    RF,RF                                                            
         ICM   RF,7,IMHD                                                        
*                                                                               
ESSUM04  DS    0H                                                               
         MVC   0(45,R5),0(RF)      R5 SET BY CALLER                             
*                                                                               
         CLI   SUMSW,C'B'                                                       
         BE    ESSUM1                                                           
         CLI   ESCBUDG,0                                                        
         BE    ESSUM1                                                           
         GOTO1 AGETBUD                                                          
*                                                                               
*                                  SEE IF ANY BUDGET $                          
*                                  NOTE - BUDSW REMAINS SET FOR                 
*                                  RECAPS AND BRD SUMMARIES                     
         MVI   BUDSW,0                                                          
         CP    ACCL-16(8,R3),=P'0'           TEST HAVE NON-MONTHLY              
         BE    *+12                                                             
         MVI   BUDSW,1             HAVE BUDGET $                                
         B     ESSUMA                                                           
*                                                                               
         LA    R3,0(R3)                                                         
         LA    R4,ROWL                                                          
         LA    R5,ACCL-17(R3)                                                   
         CP    DBUD(8,R3),=P'0'                                                 
         BE    *+12                                                             
         MVI   BUDSW,1             HAVE BUDGET $                                
         B     *+8                                                              
         BXLE  R3,R4,*-18                                                       
         L     R3,ATOT                                                          
*                                                                               
ESSUMA   DS    0H                                                               
         CLI   BUDSW,0             TEST BUDGET DATA                             
         BNE   *+10                YES                                          
         MVC   SUMSUMS,ESCSUMSB    NO- SUPRESS BUDGET COLS                      
ESSUM1   DS    0H                                                               
*                                                                               
         LA    R2,RMTAB                                                         
*                                                                               
ESSUM2   CLI   ESCZMTH,C'Y'        SEE IF SHOWING 0 MTHS IN REQ PER.            
         BNE   ESSUM2A                                                          
         CLC   0(2,R2),BSTART      SEE IF MTH IN REQ PERIOD                     
         BL    ESSUM2A                                                          
         CLC   0(2,R2),BEND                                                     
         BH    ESSUM2A                                                          
         B     ESSUM2A5            ALWAYS DISPLAY MTHS IN REQ PERIOD            
*                                                                               
ESSUM2A  LR    RF,R3               SAVE REGISTER 3                              
         LA    RE,ROWL/8                                                        
         CP    0(8,R3),=P'0'       ANY DOLLARS?                                 
         BNE   ESSUM2A1                                                         
         LA    R3,8(R3)                                                         
         BCT   RE,*-14             9 SETS                                       
         LR    R3,RF                                                            
         B     ESSUM4                                                           
*                                                                               
ESSUM2A1 DS    0X                                                               
         LR    R3,RF               RESTORE REGISTER 3                           
*                                                                               
         TM    ESCBUDG,X'01'       TEST MONTHLY BUDG                            
         BNZ   ESSUM2A5            YES - OK                                     
*                                                                               
         LR    RF,R3               SAVE REGISTER 3                              
         LA    RE,8                (ROWL-8)/8                                   
         CP    0(8,R3),=P'0'       ANY DOLLARS?                                 
         BNE   ESSUM2A2                                                         
         LA    R3,8(R3)                                                         
         BCT   RE,*-14                                                          
         LR    R3,RF                                                            
         B     ESSUM2B                                                          
ESSUM2A2 LR    R3,RF               RESTORE REGISTER 3                           
*                                                                               
ESSUM2A5 DS    0X                                                               
         LA    R4,P+17                                                          
         GOTO1 DATCON,DMCB,(3,(R2)),(6,(R4))                                    
         GOTO1 ASUMFMT,DMCB,(0,(R3))                                            
*                                                                               
         BAS   RE,ESSUMPRT                                                      
*                                                                               
ESSUM2B  DS    0H                                                               
         LA    R0,ROWL/8           9 SETS                                       
         LR    R4,R3                                                            
         LA    R5,QTRTOTS                                                       
ESSUM2D  DS    0H                                                               
         AP    0(8,R5),0(8,R4)                                                  
         LA    R4,8(R4)                                                         
         LA    R5,8(R5)                                                         
         BCT   R0,ESSUM2D                                                       
*                                                                               
ESSUM4   DS    0H                                                               
         SR    R1,R1                                                            
         SR    R0,R0                                                            
         IC    R1,1(R2)                                                         
         LA    R1,1(R1)                                                         
         D     R0,=F'3'                                                         
         LTR   R0,R0                                                            
         BNZ   *+8                                                              
         LA    R0,3                                                             
         STC   R0,BYTE                                                          
         CLC   BYTE,ESCQTRS                                                     
         BE    ESQTR                                                            
         CLI   BYTE,1                                                           
         BNE   ESSUM6                                                           
         CLI   ESCQTRS,0                                                        
         BE    ESQTR                                                            
*                                                                               
ESSUM6   LA    R2,2(R2)                                                         
         LA    R3,ROWL(R3)         NEXT ACCUM                                   
         OC    0(2,R2),0(R2)       TEST DONE                                    
         BNZ   ESSUM2                                                           
         B     ESQTR                                                            
*                                                                               
***********************************************************************         
*                                                                               
ESSUM6A  DS    0H                                                               
         BAS   RE,ESSUMPRT                                                      
         L     R3,ATOT                                                          
         MVC   DUB,ACCL-16(R3)     BUDG TOTAL                                   
         CP    DUB,=P'0'                                                        
         BE    ESSUM6C                                                          
*                                                                               
         CP    GRDTOTS+32(8),=P'0'          TEST MONTHLY BUDG DATA              
         BE    ESSUM6B                                                          
*                                                                               
         LA    RE,QTRTOTS          INITIALIZE QTRTOT (9PL8)                     
         LA    RF,9                                                             
         ZAP   0(8,RE),=P'0'                                                    
         LA    RE,8(RE)                                                         
         BCT   RF,*-10                                                          
*                                                                               
         MVC   QTRTOTS+32(8),DUB                                                
         GOTO1 ASUMFMT,DMCB,QTRTOTS                                             
*                                                                               
         MVC   P+14(L'PP@NONMO),PP@NONMO                                        
         MVC   PSECOND+14(L'PP@AUTH),PP@AUTH                                    
         MVI   SPACING,2                                                        
         BAS   RE,ESSUMPRT                                                      
*                                                                               
ESSUM6B  DS    0X                                                               
         MVC   DUB,ACCL-16(R3)                                                  
         AP    GRDTOTS+32(8),DUB                                                
*                                                                               
ESSUM6C  DS    0H                                                               
         MVC   P+17(L'PP8TOTAL),PP8TOTAL                                        
         GOTO1 ASUMFMT,DMCB,(1,GRDTOTS)                                         
         BAS   RE,ESSUMPRT                                                      
         CLI   SUMSW,C'B'                                                       
         BE    ESSUMX                                                           
         TM    ESCBUDG,X'02'       TEST YEARLY BUDGET OPT                       
         BZ    ESSUMX                                                           
*                                                                               
         CP    GRDTOTS+32(8),=P'0'           NO BUDGET $ HERE                   
         BE    ESSUMX                                                           
*                                                                               
         BAS   RE,ESSUMPRT         SKIP A LINE                                  
         MVI   BYTE,1              GROSS                                        
         TM    ESCBUDG,X'80'                                                    
         BZ    *+8                                                              
         MVI   BYTE,2              NET                                          
         TM    ESCBUDG,X'40'       COST                                         
         BZ    *+8                                                              
         MVI   BYTE,X'11'                                                       
         LA    R5,ESCSUMS          FIND RIGHT COLLUMN                           
         LA    R4,P+17                                                          
         LA    R0,5                                                             
ESSUM7   DS    0H                                                               
         CLC   BYTE,0(R5)                                                       
         BE    ESSUM8                                                           
         LA    R5,1(R5)                                                         
         LA    R4,15(R4)                                                        
         BCT   R0,ESSUM7                                                        
         B     ESSUMX              NO PLACE TO PUT BUDG                         
*                                                                               
ESSUM8   DS    0H                                                               
         MVC   0(L'PP@LAUTH,R4),PP@LAUTH                                        
         LA    R4,15(R4)                                                        
         MVC   SUMSUMS,=5X'FF'                                                  
         LA    R0,ESCSUMS                                                       
         SR    R5,R0                                                            
         LA    R5,SUMSUMS(R5)      POINT TO RIGHT COL                           
         MVI   0(R5),10            AUTH                                         
         GOTO1 ASUMFMT,DMCB,(1,GRDTOTS)                                         
*                                                                               
         LR    R7,R4                                                            
         CLI   0(R7),C' '                                                       
         BNE   *+12                                                             
         LA    R7,1(R7)                                                         
         B     *-12                                                             
         ST    R7,FULL             ADDRESS OF 1ST NON-BLANK                     
         BAS   RE,ESSUMPRT                                                      
         MVI   0(R5),12            GROSS LESS AUTH                              
         TM    ESCBUDG,X'80'                                                    
         BZ    *+8                                                              
         MVI   0(R5),13            NET LESS AUTH                                
         TM    ESCBUDG,X'40'                                                    
         BZ    *+8                                                              
         MVI   0(R5),X'14'         COST LESS BUDG                               
         GOTO1 ASUMFMT,DMCB,(1,GRDTOTS)                                         
*                                                                               
         MVI   SPACING,2                                                        
         LR    R7,R4                                                            
         BCTR  R7,R0                                                            
         CLI   1(R7),C' '                                                       
         BNE   *+12                                                             
         LA    R7,1(R7)                                                         
         B     *-12                                                             
         CLI   1(R7),C'-'                                                       
         BE    *+8                                                              
         MVI   0(R7),C'+'                                                       
         C     R7,FULL                                                          
         BNH   *+8                                                              
         L     R7,FULL                                                          
         MVC   132(17,R4),0(R4)    MOVE TO PSECOND                              
         MVC   P,SPACES                                                         
         LA    R4,14(R4)                                                        
         SR    R4,R7                                                            
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R7),MYDASH      UNDERLINE                                    
         BAS   RE,ESSUMPRT                                                      
*                                                                               
ESSUMX   XMOD1 1                                                                
*                                                                               
***********************************************************************         
*                                                                               
         EJECT                                                                  
ESQTR    CLI   ESCQTRS,0           TEST OPTION                                  
         BE    ESQTRX                                                           
*                                                                               
         LA    RE,QTRTOTS          CHECKING QTRTOT (9PL8)                       
         LA    RF,9                                                             
         CP    0(8,RE),=P'0'                                                    
         BNE   ESQTR1              SOMETHING IS IN QTRTOT, STOP LOOP            
         LA    RE,8(RE)                                                         
         BCT   RF,*-14                                                          
         B     ESQTRX              NOTHING IS IN QTRTOTS                        
*                                                                               
ESQTR1   MVC   P+17(L'PP@QTOT),PP@QTOT                                          
         GOTO1 ASUMFMT,DMCB,(1,QTRTOTS)                                         
         MVI   SPACING,2                                                        
         BAS   RE,ESSUMPRT                                                      
ESQTRX   LA    R0,ROWL/8           9 SETS                                       
         LA    R4,QTRTOTS                                                       
         LA    R5,GRDTOTS                                                       
ESQTRX2  AP    0(8,R5),0(8,R4)                                                  
         LA    R4,8(R4)            NEXT QTRTOT                                  
         LA    R5,8(R5)            NEXT GRDTOT                                  
         BCT   R0,ESQTRX2                                                       
*                                                                               
         LA    RE,QTRTOTS          INITIALIZE QTRTOT (9PL8)                     
         LA    RF,9                                                             
         ZAP   0(8,RE),=P'0'                                                    
         LA    RE,8(RE)                                                         
         BCT   RF,*-10                                                          
*                                                                               
         CLI   0(R2),0                                                          
         BE    ESSUM6A                                                          
         B     ESSUM6                                                           
         SPACE 3                                                                
*                                                                               
***********************************************************************         
*                                                                               
ESSUMPRT DS    0H                                                               
         LR    R0,RE                                                            
         CLI   PRNTSW,C'N'                                                      
         BE    ESSUMP4                                                          
         MVC   HEAD8,MYSUMHD8                                                   
         MVC   HEAD9,SPACES                                                     
         MVC   HEAD10,MYSUMH10                                                  
         MVC   HEAD11,MYSUMH11                                                  
         MVC   HEAD12,MYSUMH12                                                  
         CLI   SUMSW,C'B'                                                       
         BE    ESSUMP2                                                          
         CLI   BUDSW,0             TEST BUDGET DATA                             
         BNE   ESSUMP3             YES                                          
*                                  SUPPRESS BUDGET COLLS                        
ESSUMP2  DS    0H                                                               
         MVC   HEAD10,MYSMBH10                                                  
         MVC   HEAD11,MYSMBH11                                                  
         MVC   HEAD12,MYSMBH12                                                  
ESSUMP3  DS    0H                                                               
         MVC   HEAD11+17(L'PP@MONTH),PP@MONTH                                   
         MVC   HEAD12+17(L'PPUMONTH),PPUMONTH                                   
         LA    RE,HEAD11+17                                                     
         LA    RF,HEAD12+17                                                     
         CLI   0(RE),C' '                                                       
         BNE   ESSUMP4                                                          
         MVI   0(RF),C' '                                                       
         LA    RE,1(RE)                                                         
         LA    RF,1(RF)                                                         
         B     *-20                                                             
         SPACE 3                                                                
ESSUMP4  DS    0H                                                               
         GOTO1 APRNT                                                            
*                                                                               
         LR    RE,R0                                                            
         BR    RE                                                               
*                                                                               
***********************************************************************         
*                                                                               
         EJECT                                                                  
         LTORG                                                                  
         SPACE 3                                                                
DC3LIST  DS    0C                                                               
         DCDDL PP#NONMO,11                                                      
         DCDDL PP#AUTH,14                                                       
         DCDDL PP#TOTS,8,LABEL=PP8TOTAL                                         
         DCDDL PP#LAUTH,15                                                      
         DCDDL PP#QTOT,8                                                        
         DCDDL PP#MONTH,5,R                                                     
         DCDDL PP#MONTH,5,RU,LABEL=PPUMONTH                                     
         DCDDL PP#IMOSM,45                                                      
         DCDDL PP#OMOSM,45                                                      
         DCDDL PP#BILM,45                                                       
         DCDDL PP#PMOSM,45                                                      
         DCDDL PP#PYMOS,45                                                      
         DCDDL PP#CMOSM,45                                                      
DC3LISTX DC    X'00'                                                            
*                                                                               
DS3LIST  DS    0C                                                               
         DSDDL PRINT=YES                                                        
         PRINT GEN                                                              
DS3LISTX EQU   *                                                                
         PRINT NOGEN                                                            
         SPACE 3                                                                
*                                                                               
IMHD     DC    AL3(PP@IMOSM)                                                    
OMHD     DC    AL3(PP@OMOSM)                                                    
BMHD     DC    AL3(PP@BILM)                                                     
PMHD     DC    AL3(PP@PMOSM)                                                    
PYMHD    DC    AL3(PP@PYMOS)                                                    
CLMHD    DC    AL3(PP@CMOSM)                                                    
*                                                                               
***********************************************************************         
*                                                                               
         TITLE 'PP5202 - BRDSUM - PRINT BRAND SUMMARIES'                        
*                                                                               
BRDSUM   CSECT                                                                  
         NMOD1 0,**BRDSUM                                                       
         USING PPWORKD,RA          RA=V(PPWORKC)                                
         L     RC,PPFILEC                                                       
         USING PPFILED,RC,R9       R9=RC+4096                                   
         USING PP52WRKD,R8         R8=A(PP52WRKC)                               
*                                                                               
         L     RF,PPWORK2C                                                      
         USING PPWORK2D,RF                                                      
         MVC   ACONIO2,ACONIO      (A)PCONREC                                   
         DROP  RF                                                               
*                                                                               
         XC    DMCB,DMCB                                                        
         LA    R1,DMCB                                                          
         USING DICTATED,R1                                                      
         MVI   DDACTN,DDACTNL                                                   
         MVI   DDRETN,DDCASEU                                                   
         MVI   DDSYS,4                                                          
         MVC   DDLANG,RCLANG                                                    
         LA    RF,DC4LIST                                                       
         STCM  RF,7,DDIADR                                                      
         LA    RF,DS4LIST                                                       
         STCM  RF,7,DDOADR                                                      
         GOTO1 ADICTATE                                                         
         DROP  R1                                                               
*                                                                               
         LA    R3,QTRTOTS          INITIALIZE QTRTOT (9PL8)                     
         LA    R5,9                                                             
         ZAP   0(8,R3),=P'0'                                                    
         LA    R3,8(R3)                                                         
         BCT   R5,*-10                                                          
         LA    R3,GRDTOTS          INITIALIZE GRDTOT (9PL8)                     
         LA    R5,9                                                             
         ZAP   0(8,R3),=P'0'                                                    
         LA    R3,8(R3)                                                         
         BCT   R5,*-10                                                          
*                                                                               
         MVI   LINE,99             FORCE PAGE                                   
         SPACE 2                                                                
*                                                                               
* GET TABLES IN ALPHA SEQUENCE                                                  
*                                                                               
         L     R3,ABRDT                                                         
         L     R5,ABRDTX                                                        
         BAS   RE,BRDSRT                                                        
         L     R3,ABRDTB                                                        
         L     R5,ABRDTBX                                                       
         BAS   RE,BRDSRT                                                        
BRDSUM2  DS    0H                                                               
         EJECT                                                                  
*                                                                               
* READ PRDHDRS SO CAN PRINT NAMES AT END OF SUMMARIES                           
*                                                                               
         MVC   WORK(L'KEY),KEY     SAVE LAST PPGKEY                             
         XC    KEY,KEY                                                          
         MVC   KEY(7),PCLTKEY      A/M/X/CLT                                    
         MVI   KEY+3,6                                                          
         L     R3,APRDTAB                                                       
         LA    R4,24                                                            
         L     R5,APRDTABX                                                      
         L     R6,ABRDT            POINT TO FIRST PRD                           
         BAS   RE,BRDHIGH                                                       
         B     *+8                                                              
BRDPRD2  BAS   RE,BRDSEQ                                                        
         CLC   KEY(7),KEYSAVE      SAME A/M/X/CLT                               
         BNE   BRDPRD4                                                          
         CLC   KEY+7(3),=C'ZZZ'    DONT PUT ZZZ IN TABLE                        
         BE    BRDPRD2                                                          
* CHECK IF BRAND IS ACTIVE                                                      
* R6 POINTS TO LAST PRD USED IN TABLE                                           
         LA    RE,ACCL                                                          
         L     RF,ABRDTX                                                        
BRDPRD3  OC    ACCL-8(3,R6),ACCL-8(R6)  CHECK LAST                              
         BZ    BRDPRD4             DONE                                         
         CLC   ACCL-8(3,R6),KEY+7                                               
         BH    BRDPRD2             IF TABLE HI, NOT ACTIVE                      
         BE    *+12                BRAND IS ACTIVE                              
         BXLE  R6,RE,BRDPRD3                                                    
         B     BRDPRD4             NO MORE ACTIVE BRANDS                        
*                                                                               
         BAS   RE,BRDGET                                                        
*                                                                               
         L     RF,ACONIO2          (A)PCONREC                                   
         USING PCONREC,RF                                                       
*                                                                               
         MVC   0(3,R3),PPRDKPRD-PPRDKEY+PCONREC                                 
         MVI   3(R3),C'='                                                       
         MVC   4(20,R3),PPRDNAME-PPRDKEY+PCONREC                                
         BXLE  R3,R4,BRDPRD2                                                    
         DC    H'0'                EXPAND PRD TABLE                             
*                                                                               
         DROP  RF                                                               
*                                                                               
BRDPRD4  XC    0(24,R3),0(R3)      SET LAST PRD FLAG                            
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY,WORK                                                         
         BAS   RE,BRDHIGH          RESTOR LAST PPGKEY                           
*                                                                               
BRDPRDX  DS    0H                                                               
         B     BRDS0                                                            
         EJECT                                                                  
BRDHIGH  MVC   KEYSAVE,KEY                                                      
         LA    R2,DMRDHI                                                        
         B     *+8                                                              
BRDSEQ   LA    R2,DMRSEQ                                                        
*                                                                               
         ST    RE,SAVERE                                                        
         GOTO1 DATAMGR,DMCB,(DMINBTS,(R2)),PRTDIR,KEY,KEY                       
         B     BRDCHK                                                           
BRDGET   ST    RE,SAVERE                                                        
*                                                                               
         L     RF,ACONIO2          (A)PCONREC                                   
         USING PCONREC,RF                                                       
*                                                                               
         GOTO1 DATAMGR,DMCB,(DMINBTS,GETREC),PRTFILE,KEY+27,           X        
               PCONREC,DMWORK                                                   
*                                                                               
         DROP  RF                                                               
*                                                                               
BRDCHK   DS    0H                                                               
         MVC   BYTE,DMCB+8                                                      
         NC    BYTE,DMOUTBTS                                                    
         BZ    *+6                                                              
         DC    H'0'                                                             
         L     RE,SAVERE                                                        
         BR    RE                                                               
         EJECT                                                                  
*                                                                               
BRDS0    DS    0H                                                               
         MVI   SUMSW,C'I'                                                       
         MVC   MYSUMHD8,SPACES                                                  
         MVC   SUMSUMS,ESCSUMS                                                  
         CLI   BUDSW,0             TEST BUDGET DATA                             
         BNE   *+10                YES                                          
         MVC   SUMSUMS,ESCSUMSB    NO- SUPRESS BUDGET COLS                      
BRDS1    DS    0H                                                               
         LA    R2,ESCSUM1                                                       
         CLI   SUMSW,C'I'                                                       
         BE    *+8                                                              
         LA    R2,ESCSUM2                                                       
*                                                                               
         CLI   0(R2),C'N'                                                       
         BE    BRDSTX2                                                          
         SR    RF,RF                                                            
         ICM   RF,7,BROMHD                                                      
         CLI   0(R2),C'S'          SALE                                         
         BE    BRDS1A                                                           
         SR    RF,RF                                                            
         ICM   RF,7,BRCMHD                                                      
         CLI   0(R2),C'C'          CLOSING                                      
         BE    BRDS1A                                                           
         SR    RF,RF                                                            
         ICM   RF,7,BRBMHD                                                      
         CLI   0(R2),C'B'          BILLABLE                                     
         BE    BRDS1A                                                           
         SR    RF,RF                                                            
         ICM   RF,7,BRPYMHD                                                     
         CLI   0(R2),C'P'          PAYABLE                                      
         BE    BRDS1A                                                           
         SR    RF,RF                                                            
         ICM   RF,7,BRPMHD                                                      
         CLI   QMEDIA,C'O'                                                      
         BE    BRDS1A                                                           
         SR    RF,RF                                                            
         ICM   RF,7,BRIMHD                                                      
*                                                                               
BRDS1A   DS    0H                                                               
         MVC   MYSUMHD8+35(55),0(RF)         SET HEADING                        
         LA    R2,RMTAB                                                         
*                                                                               
         XC    IMDSPL(8),IMDSPL    CLEAR MONTH INDEX                            
BRDS2    L     R3,ABRDT                                                         
         LA    R4,ACCL                                                          
         L     R5,ABRDTX                                                        
         CLI   SUMSW,C'B'                                                       
         BNE   *+12                                                             
         L     R3,ABRDTB                                                        
         L     R5,ABRDTBX                                                       
*                                                                               
         GOTO1 DATCON,DMCB,(3,(R2)),(6,P+4)                                     
*                                                                               
* TEST FOR ACTIVITY IN ANY PRD FOR THIS MONTH                                   
*                                                                               
         A     R3,IMDSPL           POINT TO PROPER ACCUM                        
BRDS3    CLI   ESCZMTH,C'Y'        SEE IF SHOWING 0 MTHS IN REQ PER.            
         BNE   BRDS3C                                                           
         CLC   0(2,R2),BSTART      SEE IF IN REQ PER.                           
         BL    BRDS3C                                                           
         CLC   0(2,R2),BEND                                                     
         BH    BRDS3C                                                           
         LR    R6,R3                                                            
         S     R6,IMDSPL                                                        
         CLI   ACCL-8(R6),C' '     CHK FOR A PRD CODE                           
         BNH   BRDS3C                                                           
         B     BRDS5               ALWAYS SHOW MTHS IN REQ PER.                 
*                                                                               
BRDS3C   DS    0X                                                               
*                                                                               
*                                  TEST ACTIVE                                  
*                                                                               
         LR    RF,R3               SAVE REGISTER 3                              
         LA    RE,ROWL/8                                                        
         CP    0(8,R3),=P'0'       ANY DOLLARS?                                 
         BNE   BRDS3CA                                                          
         LA    R3,8(R3)                                                         
         BCT   RE,*-14             9 SETS                                       
         LR    R3,RF                                                            
         B     BRDS6                                                            
BRDS3CA  LR    R3,RF               RESTORE REGISTER 3                           
*                                                                               
         TM    ESCBUDG,X'01'       TEST MONTHLY BUDG                            
         BNZ   BRDS5               YES - OK                                     
*                                                                               
*                                  NO - NEED OTHER ACTIVITY                     
*                                                                               
         LR    RF,R3               SAVE REGISTER 3                              
         LA    RE,8                                                             
         CP    0(8,R3),=P'0'       ANY DOLLARS?                                 
         BNE   BRDS3CB                                                          
         LA    R3,8(R3)                                                         
         BCT   RE,*-14             8 SETS                                       
         LR    R3,RF                                                            
         B     BRDS6                                                            
BRDS3CB  LR    R3,RF               RESTORE REGISTER 3                           
*                                                                               
BRDS5    LR    R6,R3                                                            
         S     R6,IMDSPL                                                        
         MVC   P+17(3),ACCL-8(R6)                                               
         BAS   RE,BRDSFMT                                                       
*                                                                               
         OC    IMDSPL,IMDSPL       TEST FIRST TIME                              
         BZ    BRDS6               YES                                          
*                                                                               
* ROLL TO BRD TOTAL AT 0(R6)                                                    
*                                                                               
         MVC   DUB,0(R3)                                                        
         AP    0(8,R6),DUB                                                      
*                                                                               
         MVC   DUB,8(R3)                                                        
         AP    8(8,R6),DUB                                                      
*                                                                               
         MVC   DUB,16(R3)                                                       
         AP    16(8,R6),DUB                                                     
*                                                                               
         MVC   DUB,24(R3)                                                       
         AP    24(8,R6),DUB                                                     
*                                                                               
         MVC   DUB,32(R3)                                                       
         AP    32(8,R6),DUB                                                     
*                                                                               
         MVC   DUB,40(R3)          COST                                         
         AP    40(8,R6),DUB                                                     
*                                                                               
         MVC   DUB,48(R3)          GROSS CHG                                    
         AP    48(8,R6),DUB                                                     
*                                                                               
         MVC   DUB,56(R3)          CD CHG                                       
         AP    56(8,R6),DUB                                                     
*                                                                               
         MVC   DUB,64(R3)          AC CHG                                       
         AP    64(8,R6),DUB                                                     
*                                                                               
BRDS6    BXLE  R3,R4,BRDS3         LOOP FOR EACH PRD                            
*                                                                               
BRDS10   MVC   P,SPACES                                                         
         MVC   P+16(L'PP7TOTAL),PP7TOTAL                                        
         BAS   RE,BRDSTOT          PRINT MONTH TOTAL (IF NON-ZERO)              
*                                                                               
         LA    RE,GRDTOTS          INITIALIZE GRDTOT (9PL8))                    
         LA    RF,ROWL/8           9 SETS                                       
         ZAP   0(8,RE),=P'0'                                                    
         LA    RE,8(RE)                                                         
         BCT   RF,*-10                                                          
*                                                                               
         LA    R2,2(R2)                                                         
         OC    0(2,R2),0(R2)       TEST LAST MONTH                              
         BZ    BRDS15                                                           
         L     R1,IMDSPL           BUMP MONTH DISPLACEMENT                      
         LA    R1,ROWL(R1)                                                      
         ST    R1,IMDSPL                                                        
         B     BRDS2               DO NEXT MONTH                                
         SPACE 2                                                                
BRDS15   MVC   P,SPACES                                                         
         BAS   RE,BRDSPRT                                                       
*                                  NOW SEE IF ANY NON-MONTHLY                   
*                                  BUDG $ TO PRINT                              
         CLI   SUMSW,C'B'                                                       
         BE    BRDS20A                                                          
         CLI   BUDSW,0                                                          
         BE    BRDS20A                                                          
*                                                                               
         MVC   P+4(L'PP2NONMO),PP2NONMO                                         
         MVC   PSECOND+4(L'PP2AUTH),PP2AUTH                                     
         MVI   SPACING,2                                                        
*                                                                               
         L     R3,ABRDT                                                         
BRDS16   DS    0H                                                               
         OC    ACCL-8(3,R3),ACCL-8(R3)     TEST LAST                            
         BZ    BRDS19                                                           
*                                                                               
         MVC   DUB,ACCL-16(R3)     BUDG TOTAL                                   
         CP    DUB,=P'0'                                                        
         BE    BRDS18                                                           
*                                                                               
         CP    32(8,R3),=P'0'                                                   
         BE    BRDS17                                                           
*                                                                               
         CLI   P+4,C' '                                                         
         BE    *+8                                                              
         BAS   RE,BRDSPRT                                                       
         MVC   P+17(3),ACCL-8(R3)  PRD CODE                                     
         MVC   QTRTOTS,0(R3)                                                    
*                                                                               
         ZAP   0(8,R3),=P'0'                                                    
         ZAP   8(8,R3),=P'0'                                                    
         ZAP   16(8,R3),=P'0'                                                   
         ZAP   24(8,R3),=P'0'                                                   
         ZAP   32(8,R3),=P'0'                                                   
*                                                                               
         MVC   32(8,R3),ACCL-16(R3)                                             
*                                                                               
         BAS   RE,BRDSFMT                                                       
         MVC   0(ROWL,R3),QTRTOTS                                               
*                                                                               
BRDS17   DS    0X                                                               
         MVC   DUB,ACCL-16(R3)                                                  
         AP    32(8,R3),DUB                                                     
BRDS18   BXLE  R3,R4,BRDS16                                                     
*                                                                               
BRDS19   DS    0H                                                               
         MVC   P+16(L'PP7TOTAL),PP7TOTAL                                        
         BAS   RE,BRDSTOT                                                       
         MVC   P,SPACES                                                         
         MVC   PSECOND,SPACES                                                   
*                                                                               
         LA    RE,GRDTOTS          CKING IF ANYTHING IS IN GRDTOTS              
         LA    RF,ROWL/8           9 SETS                                       
         CP    0(8,RE),=P'0'                                                    
         BNE   *+16                YES, SOMETHING IS IN THERE                   
         LA    RE,8(RE)                                                         
         BCT   RF,*-14                                                          
         B     BRDS20A             NO, ALL ZEROS IN GRDTOTS                     
*                                                                               
         LA    RE,GRDTOTS          INITIALIZE GRDTOT (9PL8))                    
         LA    RF,ROWL/8           9 SETS                                       
         ZAP   0(8,RE),=P'0'                                                    
         LA    RE,8(RE)                                                         
         BCT   RF,*-10                                                          
*                                                                               
         BAS   RE,BRDSPRT                                                       
*                                                                               
BRDS20A  L     R3,ABRDT            NOW PRINT BRAND TOTALS                       
         CLI   SUMSW,C'B'                                                       
         BNE   *+8                                                              
         L     R3,ABRDTB                                                        
*                                                                               
         MVC   P+04(L'PP@TOTAL),PP@TOTAL                                        
BRDS21   OC    ACCL-8(3,R3),ACCL-8(R3)  TEST LAST                               
         BZ    BRDS30                                                           
*                                                                               
         MVC   P+17(3),ACCL-8(R3)       MOVE PRD CODE                           
         BAS   RE,BRDSFMT                                                       
*                                                                               
         BXLE  R3,R4,BRDS21                                                     
*                                                                               
BRDS30   BAS   RE,BRDSPRT          SKIP A LINE                                  
*                                                                               
         MVC   P+8(L'PP@ESTOT),PP@ESTOT                                         
         BAS   RE,BRDSTOT                                                       
*                                                                               
         LA    RE,GRDTOTS          INITIALIZE GRDTOT (9PL8))                    
         LA    RF,ROWL/8           9 SETS                                       
         ZAP   0(8,RE),=P'0'                                                    
         LA    RE,8(RE)                                                         
         BCT   RF,*-10                                                          
*                                                                               
         EJECT                                                                  
*                                                                               
* PRINT PRD TABLE                                                               
*                                                                               
         L     R3,APRDTAB                                                       
         LA    R4,24                                                            
         L     R5,APRDTABX                                                      
*                                                                               
* SEE HOW MANY LINES WE NEED (IT LOOKS NICE ON 1 PAGE)                          
*                                                                               
         SR    RE,RE                                                            
         CLI   0(R3),0                                                          
         BE    *+12                                                             
         LA    RE,1(RE)                                                         
         BXLE  R3,R4,*-12                                                       
         SRL   RE,2                4 PRDS/LINE                                  
         SR    RF,RF                                                            
         IC    RF,LINE                                                          
         LA    RF,4(RE,RF)                                                      
         STC   RF,BYTE                                                          
         CLC   BYTE,MAXLINES                                                    
         BNH   *+8                                                              
         MVI   LINE,99                                                          
*                                                                               
         MVC   P+5(L'PP@PCDTO),PP@PCDTO                                         
         MVI   SPACING,2                                                        
         BAS   RE,BRDSPRT                                                       
         L     R3,APRDTAB                                                       
BRDST2   ZAP   BYTE,=P'4'                                                       
         LA    R4,P+5                                                           
BRDST4   CLI   0(R3),0             TEST LAST PRD                                
         BE    BRDSTX                                                           
         MVC   0(24,R4),0(R3)                                                   
         LA    R4,26(R4)                                                        
         LA    R3,24(R3)                                                        
         SP    BYTE,=P'1'                                                       
         BP    BRDST4                                                           
         BAS   RE,BRDSPRT                                                       
         B     BRDST2                                                           
BRDSTX   CP    BYTE,=P'4'                                                       
         BE    *+8                                                              
         BAS   RE,BRDSPRT                                                       
*                                                                               
*                                                                               
BRDSTX2  DS    0H                                                               
         CLI   SUMSW,C'B'                                                       
         BE    BRDSX               DONE                                         
BRDSTZ   MVI   SUMSW,C'B'                                                       
         MVC   SUMSUMS,ESCSUMSB                                                 
         MVI   LINE,99                                                          
         B     BRDS1                                                            
*                                                                               
BRDSX    L     R3,ABRDT                                                         
         LA    R4,ACCL                                                          
         L     R5,ABRDTBX                                                       
*                                                                               
         LR    RE,R3               CLEAR TABLE  (191PL8)                        
         LA    RF,ACCL/8           ACCL/8=191                                   
         ZAP   0(8,RE),=P'0'                                                    
         LA    RE,8(RE)                                                         
         BCT   RF,*-10                                                          
*                                                                               
         BXLE  R3,R4,*-18                                                       
         MVI   LINE,99                                                          
         XMOD1 1                                                                
*                                                                               
ACONIO2  DS    A                   ADDRESS OF PCONREC FROM PPG                  
*                                                                               
***********************************************************************         
*                                                                               
         EJECT                                                                  
*                                                                               
* R3 POINTS TO GROSS/CSH DISC                                                   
*                                                                               
BRDSFMT  ST    RE,SAVERE                                                        
         GOTO1 ASUMFMT,DMCB,(R3)                                                
*                                                                               
         LA    R1,ROWL/8           9 SETS                                       
         LR    RF,R3               SAVE R3                                      
         LA    RE,GRDTOTS                                                       
BRDSFMT2 DS    0X                                                               
         AP    0(8,RE),0(8,R3)                                                  
         LA    RE,8(RE)                                                         
         LA    R3,8(R3)                                                         
         BCT   R1,BRDSFMT2                                                      
         LR    R3,RF               RESTORE R3                                   
*                                                                               
         BAS   RE,BRDSPRT                                                       
         L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
***********************************************************************         
*                                                                               
BRDSTOT  DS    0H                                                               
         ST    RE,SAVERE                                                        
         CLI   ESCZMTH,C'Y'        SEE IF SHOWING ZERO MTHS                     
         BNE   BRDSTOT8                                                         
         CLC   P+16(7),=C'*TOTAL*'   SEE IF DOING MTH OR QUARTER TOTAL          
         BNE   BRDSTOT8            NO                                           
*                                ONLY SHOW ZERO MTHS WITHIN REQ PERIOD          
         CLC   0(2,R2),BSTART      CHK MTH VS REQ START                         
         BL    BRDSTOT8                                                         
         CLC   0(2,R2),BEND        CHK MTH VS REQ END                           
         BH    BRDSTOT8                                                         
         B     BRDSTOT9                                                         
*                                                                               
BRDSTOT8 DS    0X                                                               
         LA    RE,GRDTOTS          CKING IF ANYTHING IS IN GRDTOTS              
         LA    RF,ROWL/8           9 SETS                                       
         CP    0(8,RE),=P'0'                                                    
         BNE   *+16                YES, SOMETHING IS IN THERE                   
         LA    RE,8(RE)                                                         
         BCT   RF,*-14                                                          
         B     BRDSTOTX            NO, ALL ZEROS IN GRDTOTS                     
*                                                                               
BRDSTOT9 GOTO1 ASUMFMT,DMCB,(1,GRDTOTS)                                         
         MVI   SPACING,2                                                        
         BAS   RE,BRDSPRT                                                       
*                                                                               
BRDSTOTX MVC   P,SPACES                                                         
         L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
***********************************************************************         
*                                                                               
         SPACE 2                                                                
BRDSPRT  DS    0H                                                               
*                                                                               
         MVC   HEAD8,MYSUMHD8                                                   
         MVC   HEAD9,SPACES                                                     
         MVC   HEAD10,MYSUMH10                                                  
         MVC   HEAD11,MYSUMH11                                                  
         MVC   HEAD12,MYSUMH12                                                  
*                                                                               
         CLI   SUMSW,C'B'                                                       
         BE    BRDSPRT2                                                         
         CLI   BUDSW,0             TEST BUDGET DATA                             
         BNE   BRDSPRT3            YES                                          
*                                  NO SUPPRESS BUDGET COLS                      
BRDSPRT2 DS    0H                                                               
         MVC   HEAD10,MYSMBH10                                                  
         MVC   HEAD11,MYSMBH11                                                  
         MVC   HEAD12,MYSMBH12                                                  
*                                                                               
BRDSPRT3 DS    0H                                                               
         LR    R0,RE                                                            
         MVC   HEAD11+04(L'PP2MONTH),PP2MONTH                                   
         MVC   HEAD12+04(L'PP2UMONTH),PP2UMONTH                                 
         MVC   HEAD11+17(L'PP@PRD),PP@PRD                                       
         MVC   HEAD12+17(L'PPUPRD),PPUPRD                                       
*                                                                               
         GOTO1 APRNT                                                            
*                                                                               
         LR    RE,R0                                                            
         BR    RE                                                               
*                                                                               
***********************************************************************         
*                                                                               
BRDSRT   DS    0H                                                               
         LR    R7,R3                                                            
         LA    R4,ACCL                                                          
         OC    ACCL-8(3,R3),ACCL-8(R3)       CKING PRD CODE                     
         BZ    *+8                                                              
         BXLE  R3,R4,*-10                                                       
         SH    R3,=Y(ACCL+1)                                                    
         CR    R3,R7                                                            
         BNHR  RE                  ONE OR NONE                                  
         LR    R5,R3               R5 = A(LAST) -1                              
         LR    R3,R7                                                            
         LCR   R2,R4               R2 = COMPL OF LENGTH                         
         SR    R6,R6                                                            
BRDSRT2  CLC   ACCL-8(3,R3),ACCL*2-8(R3)     COMP                               
         BNH   BRDSRT4                       NO EXCH (SWAPPING)                 
*                                                                               
         XC    0(256,R3),ACCL(R3)                                               
         XC    256(256,R3),ACCL+256(R3)                                         
         XC    512(256,R3),ACCL+512(R3)                                         
         XC    768(256,R3),ACCL+768(R3)                                         
         XC    1024(256,R3),ACCL+1024(R3)                                       
         XC    1280(ACCL-1280,R3),ACCL+1280(R3)                                 
*                                                                               
         XC    ACCL(256,R3),0(R3)                                               
         XC    ACCL+256(256,R3),256(R3)                                         
         XC    ACCL+512(256,R3),512(R3)                                         
         XC    ACCL+768(256,R3),768(R3)                                         
         XC    ACCL+1024(256,R3),1024(R3)                                       
         XC    ACCL+1280(ACCL-1280,R3),1280(R3)                                 
*                                                                               
         XC    0(256,R3),ACCL(R3)                                               
         XC    256(256,R3),ACCL+256(R3)                                         
         XC    512(256,R3),ACCL+512(R3)                                         
         XC    768(256,R3),ACCL+768(R3)                                         
         XC    1024(256,R3),ACCL+1024(R3)                                       
         XC    1280(ACCL-1280,R3),ACCL+1280(R3)                                 
*                                                                               
         LA    R6,1                          EX SW                              
BRDSRT4  BXLE  R3,R4,BRDSRT2                 NEXT COMP                          
         BCTR  R6,RE                         EXIT IF NO EXCHS                   
         LR    R3,R7                         RESTORE START                      
         BXH   R5,R2,BRDSRT2                 SHORTEN AND DO NEXT PASS           
         BR    RE                                                               
         SPACE 2                                                                
         LTORG                                                                  
*                                                                               
***********************************************************************         
*                                                                               
DC4LIST  DS    0C                                                               
         DCDDL PP#BIMOS,55                                                      
         DCDDL PP#OSMOS,55                                                      
         DCDDL PP#BBMOS,55                                                      
         DCDDL PP#BPTMS,55                                                      
         DCDDL PP#BPMOS,55                                                      
         DCDDL PP#BCMOS,55                                                      
         DCDDL PP#TOTS,7,LABEL=PP7TOTAL                                         
         DCDDL PP#NONMO,11,LABEL=PP2NONMO                                       
         DCDDL PP#AUTH,14,LABEL=PP2AUTH                                         
         DCDDL PP#TOTAL,6                                                       
         DCDDL PP#ESTOT,15                                                      
         DCDDL PP#PCDTO,36,F                                                    
         DCDDL PP#MONTH,5,R,LABEL=PP2MONTH                                      
         DCDDL PP#MONTH,5,RU,LABEL=PP2UMONTH                                    
         DCDDL PP#PRD,3                                                         
         DCDDL PP#PRD,3,LU,LABEL=PPUPRD                                         
DC4LISTX DC    X'00'                                                            
*                                                                               
DS4LIST  DS    0C                                                               
         DSDDL PRINT=YES                                                        
         PRINT GEN                                                              
DS4LISTX EQU   *                                                                
         PRINT NOGEN                                                            
         SPACE 3                                                                
*                                                                               
BRIMHD   DC    AL3(PP@BIMOS)                                                    
BROMHD   DC    AL3(PP@OSMOS)                                                    
BRBMHD   DC    AL3(PP@BBMOS)                                                    
BRPMHD   DC    AL3(PP@BPTMS)                                                    
BRPYMHD  DC    AL3(PP@BPMOS)                                                    
BRCMHD   DC    AL3(PP@BCMOS)                                                    
         SPACE 2                                                                
*                                                                               
***********************************************************************         
*                                                                               
         TITLE 'PP5202 - FORMAT SUMMARY LINES'                                  
SUMFMT   CSECT                                                                  
         NMOD1 0,SUMFMT                                                         
         SPACE 1                                                                
         USING PPWORKD,RA                                                       
         L     RC,PPFILEC                                                       
         USING PPFILED,RC,R9                                                    
         USING PP52WRKD,R8                                                      
*                                                                               
         L     R3,0(R1)            R3 POINTS TO ACCUM                           
         MVC   BYTE,0(R1)          TOTAL SW                                     
SUMF2    DS    0H                                                               
         LA    R4,P+32                                                          
         LA    R5,SUMSUMS                                                       
         LA    R6,5                                                             
SUMF4    CLI   0(R5),0                                                          
         BE    SUMF6                                                            
         CLI   0(R5),X'FF'                                                      
         BE    SUMF4B                                                           
         SR    RF,RF                                                            
         IC    RF,0(R5)                                                         
         SLL   RF,2                X4                                           
         BAS   RE,SUMBRTAB-4(RF)                                                
SUMF4B   DS    0H                                                               
         LA    R4,15(R4)                                                        
         LA    R5,1(R5)                                                         
         BCT   R6,SUMF4                                                         
         B     SUMFX               NO ROOM FOR LINES                            
*                                                                               
SUMF6    TM    ESCLINS,X'03'       TEST LINES ON SUMS                           
         BZ    SUMFX                                                            
         MVC   DUB,24(R3)                                                       
         CLI   ESCCIRC,C'T'                                                     
         BE    SUMF6A                                                           
         TM    ESCLINS,X'02'                                                    
         BZ    SUMFX                                                            
         CLI   QMEDIA,C'N'                                                      
         BNE   SUMF6A                                                           
         TM    ESCLINS,X'20'       LINES VS INCHES                              
         BNZ   SUMF6A                                                           
         AP    DUB,=P'7'                                                        
         DP    DUB,=P'14'          INCH = 14 LINES                              
         ZAP   DUB,DUB(6)                                                       
SUMF6A   DS    0H                                                               
         ST    RE,SAVERE1                                                       
         ST    RF,SAVERF                                                        
         MVI   MYCTAB+3,0                                                       
         CURED (P8,DUB),(14,1(R4)),MYCTAB,COMMAS=YES,MODADDR=ACUREDIT           
         L     RF,SAVERF                                                        
         L     RE,SAVERE1                                                       
         CLI   BYTE,1                                                           
         BNE   *+8                                                              
         MVI   15(R4),C'*'                                                      
SUMFX    XMOD1 1                                                                
*                                                                               
***********************************************************************         
*                                                                               
SUMBRTAB DS    0H                                                               
         B     SUMGR                                                            
         B     SUMNT                                                            
         B     SUMCD                                                            
         B     SUMGLCD                                                          
         B     SUMNLCD                                                          
         DC    4X'00'                                                           
         DC    4X'00'                                                           
         DC    4X'00'                                                           
         DC    4X'00'                                                           
         B     SUMBUD                                                           
         B     SUMBUD                                                           
         B     SUMGLBD                                                          
         B     SUMNLBD                                                          
         DC    4X'00'                                                           
         DC    4X'00'                                                           
         DC    4X'00'                                                           
         B     SUMACT                                                           
         DC    4X'00'                                                           
         B     SUMBUD                                                           
         B     SUMCLBD                                                          
*                                                                               
         B     SUMCHGG             GROSS CHANGE                                 
         B     SUMCHGN             NET CHANGE                                   
         B     SUMCHGCD            CD CHANGE                                    
         B     SUMCHGGC            GROSS-CD CHANGE                              
         B     SUMCHGNC            NET-CD CHANGE                                
*                                                                               
         DC    4X'00'      LEAVE GAP FOR FUTURE COST CHANGE                     
*                                                                               
         B     SUMOLDG             GROSS OLD                                    
         B     SUMOLDN             NET OLD                                      
         B     SUMOLDCD            CD OLD                                       
         B     SUMOLDGC            GROSS-CD OLD                                 
         B     SUMOLDNC            NET-CD OLD                                   
*                                                                               
SUMGR    MVC   DUB,0(R3)           GROSS                                        
         BAS   R7,SUMED                                                         
         BR    RE                                                               
*                                                                               
SUMNT    MVC   DUB,0(R3)           GROSS                                        
         SP    DUB,16(8,R3)        LESS AGY COMM                                
         BAS   R7,SUMED                                                         
         BR    RE                                                               
*                                                                               
SUMCD    MVC   DUB,8(R3)           CD                                           
         BAS   R7,SUMED                                                         
         BR    RE                                                               
*                                                                               
SUMGLCD  MVC   DUB,0(R3)           GROSS                                        
         SP    DUB,8(8,R3)         LESS CD                                      
         BAS   R7,SUMED                                                         
         BR    RE                                                               
*                                                                               
SUMNLCD  MVC   DUB,0(R3)           GROSS                                        
         SP    DUB,8(8,R3)         LESS CD                                      
         SP    DUB,16(8,R3)        LESS AC                                      
         BAS   R7,SUMED                                                         
         BR    RE                                                               
*                                                                               
SUMBUD   MVC   DUB,32(R3)          BUDGET                                       
         BAS   R7,SUMED                                                         
         BR    RE                                                               
*                                                                               
SUMGLBD  MVC   DUB,0(R3)                                                        
         SP    DUB,32(8,R3)                                                     
         BAS   R7,SUMED                                                         
         BR    RE                                                               
*                                                                               
SUMNLBD  MVC   DUB,0(R3)                                                        
         SP    DUB,16(8,R3)                                                     
         SP    DUB,32(8,R3)                                                     
         BAS   R7,SUMED                                                         
         BR    RE                                                               
*                                                                               
SUMACT   DS    0H                  ACTUAL COST                                  
         LR    R7,RE                                                            
         MVC   DUB,40(R3)          COST                                         
         LR    RE,R7                                                            
         BAS   R7,SUMED                                                         
         BR    RE                                                               
         SPACE 2                                                                
SUMCLBD  DS    0H                  COST LESS BUDG                               
         LR    R7,RE                                                            
         MVC   DUB,40(R3)                                                       
         SP    DUB,32(8,R3)                                                     
         LR    RE,R7                                                            
         BAS   R7,SUMED                                                         
         BR    RE                                                               
*                                                                               
SUMCHGG  MVC   DUB,48(R3)          GROSS                                        
         BAS   R7,SUMED                                                         
         BR    RE                                                               
*                                                                               
SUMCHGN  MVC   DUB,48(R3)          GROSS                                        
         SP    DUB,64(8,R3)        LESS AGY COMM                                
         BAS   R7,SUMED                                                         
         BR    RE                                                               
*                                                                               
SUMCHGCD MVC   DUB,56(R3)          CD                                           
         BAS   R7,SUMED                                                         
         BR    RE                                                               
*                                                                               
SUMCHGGC MVC   DUB,48(R3)          GROSS                                        
         SP    DUB,56(8,R3)        LESS CD                                      
         BAS   R7,SUMED                                                         
         BR    RE                                                               
*                                                                               
SUMCHGNC MVC   DUB,48(R3)          GROSS                                        
         SP    DUB,56(8,R3)        LESS CD                                      
         SP    DUB,64(8,R3)        LESS AC                                      
         BAS   R7,SUMED                                                         
         BR    RE                                                               
*                                                                               
SUMOLDG  MVC   DUB,0(R3)          GROSS                                         
         SP    DUB,48(8,R3)       LESS CHANGE = OLD GROSS                       
         BAS   R7,SUMED                                                         
         BR    RE                                                               
*                                                                               
SUMOLDN  MVC   DUB,0(R3)          NEW GROSS                                     
         SP    DUB,16(8,R3)       -NEW AC= NEW NET                              
         MVC   DOUBLE,48(R3)       CHANGE GROSS                                 
         SP    DOUBLE,64(8,R3)     LESS CHG AC = CHG NET                        
         SP    DUB,DOUBLE          NEW NET - CHG NET= OLD NET                   
         BAS   R7,SUMED                                                         
         BR    RE                                                               
*                                                                               
SUMOLDCD MVC   DUB,8(R3)          NEW CD                                        
         SP    DUB,56(8,R3)       -CHG CD = OLD CD                              
         BAS   R7,SUMED                                                         
         BR    RE                                                               
*                                                                               
SUMOLDGC MVC   DUB,0(R3)          NEW GROSS                                     
         SP    DUB,8(8,R3)        LESS NEW CD = NEW G-CD                        
         MVC   DOUBLE,48(R3)      CHG GROSS                                     
         SP    DOUBLE,56(8,R3)    LESS CHG CD = CHG G-CD                        
         SP    DUB,DOUBLE         NEW G-CD MINUS CHG G-CD= OLD G-CD             
         BAS   R7,SUMED                                                         
         BR    RE                                                               
*                                                                               
SUMOLDNC MVC   DUB,0(R3)           NEW GROSS                                    
         SP    DUB,8(8,R3)         LESS NEW CD                                  
         SP    DUB,16(8,R3)        LESS NEW AC = NEW N-CD                       
         MVC   DOUBLE,48(R3)       CHG GROSS                                    
         SP    DOUBLE,56(8,R3)     LESS CHG CD                                  
         SP    DOUBLE,64(8,R3)     LESS CHG AC = CHG N-CD                       
         SP    DUB,DOUBLE          NEW N-CD MINUS CHG N-CD = OLD N-CD           
         BAS   R7,SUMED                                                         
         BR    RE                                                               
*                                                                               
***********************************************************************         
*                                                                               
SUMED    CLI   BYTE,1                                                           
         BE    SUMED2                                                           
*                                                                               
         CP    DUB,=P'0'                                                        
         BNE   *+6                                                              
         BR    R7                                                               
*                                                                               
SUMED2   DS    0H                                                               
         CLI   ESCCENTS,C'$'    NO CENTS IN DST + HIGHER ACCUMS                 
         BNE   SUMED4                                                           
         ST    RE,SAVERE1                                                       
         ST    RF,SAVERF                                                        
         ST    R1,SAVER1                                                        
         MVI   MYCTAB+3,0                                                       
         CURED (P8,DUB),(14,1(R4)),MYCTAB,COMMAS=YES,                  +        
               FLOAT=-,MODADDR=ACUREDIT                                         
         L     R1,SAVER1                                                        
         L     RF,SAVERF                                                        
         L     RE,SAVERE1                                                       
         B     SUMED6                                                           
*                                                                               
SUMED4   DS    0H                                                               
         ST    RE,SAVERE1                                                       
         ST    RF,SAVERF                                                        
         ST    R1,SAVER1                                                        
         MVI   MYCTAB+3,2                                                       
         CURED (P8,DUB),(14,1(R4)),MYCTAB,COMMAS=YES,                  +        
               FLOAT=-,MODADDR=ACUREDIT                                         
         L     R1,SAVER1                                                        
         L     RF,SAVERF                                                        
         L     RE,SAVERE1                                                       
SUMED6   CLI   BYTE,1                                                           
         BNER  R7                                                               
         MVI   15(R4),C'*'                                                      
         BR    R7                                                               
*                                                                               
***********************************************************************         
*                                                                               
         SPACE 3                                                                
         LTORG                                                                  
*                                                                               
PUBADVD  DSECT                                                                  
       ++INCLUDE PPPUBADVEL                                                     
*                                                                               
         PRINT NOGEN                                                            
       ++INCLUDE PP52WRK                                                        
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE PPDDEQUS                                                       
       ++INCLUDE PPREPWORK                                                      
       ++INCLUDE PPREPWORK2                                                     
         PRINT ON                                                               
         ORG   Q2USER                                                           
QCOMM    DS    CL6            REQUESTED STANDARD COMMENT                        
         ORG                                                                    
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE PPNEWFILE                                                      
       ++INCLUDE PPMODEQU                                                       
       ++INCLUDE DDDICTATED                                                     
*                                                                               
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'012PPREP52B  03/02/05'                                      
         END                                                                    
