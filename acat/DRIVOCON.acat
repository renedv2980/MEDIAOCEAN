*          DATA SET DRIVOCON   AT LEVEL 108 AS OF 07/09/13                      
*CATALP DRIVOCON                                                                
         TITLE 'DRIVOCON - OUTPUT CONTROLLER FOR DRIVER'                        
DRIVOUT  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 DOUTDEND-DOUTD,**DROUT*,R9,R8,R7,CLEAR=YES                       
         USING DOUTD,RC                                                         
         L     RA,0(R1)                                                         
         USING GLOBALD,RA                                                       
         ST    RC,AOCOND                                                        
         CLI   GLANYERR,C'Y'                                                    
         BE    XIT                                                              
         CLI   GLABCSW,0           ABC HANDLES OWN LOGIC                        
         BNE   OUTABC                                                           
         CLI   GLANYSRT,C'Y'       TEST ANY RECORDS PUT TO SORTER               
         BE    DROUT2                                                           
         GOTO1 =V(DRIVPUT),DMCB,(RA),0  NO-DRIVPUT MAY HAVE TO EMPTY            
         CLI   GLANYSRT,C'Y'               ITS STACK                            
         BNE   XIT                                                              
         SPACE 1                                                                
DROUT2   BAS   RE,OUTINIT                                                       
         BAS   RE,OUTSORT                                                       
         ICM   R3,15,AOUTPDCB                                                   
         BZ    XIT                                                              
         CLOSE ((R3),)                                                          
         FREEPOOL (R3)                                                          
         B     XIT                                                              
         EJECT                                                                  
*              SPECIAL ROUTINES FOR ABC                                         
         SPACE 1                                                                
OUTABC   BAS   RE,OUTINIT                                                       
         CLI   SPLATSW,0           MAY BE USING DRIVOUT JUST TO SPACE           
         BNE   OUTSPLAT                                                         
         L     R5,GLAIO            R5=A(RECORD FORMATTED BY DRIVIN)             
         MVI   1(R5),1             ABC RECORDS ARE AT LEVEL 1                   
         L     R3,=A(TEMPREC)                                                   
         MOVE ((R3),4000),(R5)     REPLICATE CURRENT RECORD                     
*                                  JUST USING DRIVADD TO SET ACTIVITY           
         GOTO1 =V(DRIVADD),DMCB,(RA),(R3),(R5)                                  
         CLI   DMCB,1              ARE ALL ADDITIVE FIELDS ZERO?                
         BNE   OUTABC2                                                          
         TM    GLABCOPT,GLABCSUP   OPTION TO SUPPRESS ZEROS                     
         BO    XIT                                                              
         SPACE 1                                                                
OUTABC2  BAS   RE,OUTPOST                                                       
         BAS   RE,DETAIL                                                        
         B     XIT                                                              
         SPACE 1                                                                
OUTSPLAT CLI   SPLATSW,X'FF'                                                    
         BE    OUTLAST                                                          
         MVC   NLINES,SPLATSW      PASS LINES TO SPACE IN SPLATSW               
         BAS   RE,SPLAT                                                         
         MVI   SPLATSW,0                                                        
         B     XIT                                                              
         SPACE 1                                                                
OUTLAST  BAS   RE,DOWNLAST         LAST TIME FOR ABC                            
         B     XIT                                                              
         EJECT                                                                  
*              INITIALIZATION FOR OUT ROUTINES                                  
         SPACE 3                                                                
OUTINIT  NTR1                                                                   
         USING DOUTD,RC                                                         
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(197),SPACES                                             
         MVC   LASTPDET,SPACES                                                  
         MVC   SAVENRBX,SPACES                                                  
         MVC   SAVEBXCL,SPACES                                                  
         MVC   LASTP2,SPACES                                                    
         MVC   LASTMID,SPACES                                                   
         MVC   THISMID,SPACES                                                   
         MVC   SAVEDET,SPACES                                                   
         MVC   SAVEP2,SPACES                                                    
         MVC   SAVEP3,SPACES                                                    
         MVC   SAVEP4,SPACES                                                    
         MVC   LASTP3,SPACES                                                    
         MVC   LASTP4,SPACES                                                    
         MVC   P,SPACES                                                         
         SPACE 1                                                                
         XC    CUMEBIN,CUMEBIN     CLEAR CUME COUNTERS                          
         ZAP   CUMEPACK,=P'0'                                                   
         SPACE 1                                                                
         LA    R0,12                                                            
         LA    R1,CDWNDUBS         INITIALIZE COMPUTE DOWNCAST                  
         SPACE 1                                                                
OUTINIT2 ZAP   0(8,R1),=P'0'                                                    
         LA    R1,8(R1)                                                         
         BCT   R0,OUTINIT2                                                      
         SPACE 1                                                                
         MVI   COMPSTAT,0          RESET COMPUTE CHECKER                        
         SPACE 1                                                                
         GOTO1 =V(DRIVRANK),DMCB,(RA),=C'CLR'                                   
         GOTO1 =V(DRIVVTST),DMCB,(X'FF',(RA))  INITIALIZE                       
         L     R2,GLAHEDHK         INTERCEPT SO WE GET                          
         L     R1,=V(DRIVHEAD)     THE HEAD HOOK                                
         ST    R1,0(R2)                                                         
         XC    TOTCNTS,TOTCNTS                                                  
         CLI   GLABCSW,0           DON'T CLEAR FOR ABC                          
         BNE   OUTINIT4            IT HAS THE RECORD THERE ALREADY!             
         L     RE,GLAIO                                                         
         LA    RF,1000                                                          
         XCEF                                                                   
         SPACE 1                                                                
OUTINIT4 L     R6,ABOX                                                          
         USING BOXD,R6                                                          
         TM    GLDOWNLD,GLDLACTV                                                
         BNO   OUTINIT5            IF WE ARE DOWNLOADING,                       
*****    MVC   BOXWIDTH,=F'165'    FORCE WIDE PRINT                             
         MVC   BOXCOLS,SPACES                                                   
         MVC   BOXCOLSR,SPACES                                                  
*                                                                               
OUTINIT5 OC    GLAOUTP,GLAOUTP     ANY OUTPUT FILE SPECIFIED?                   
         BZ    OUTINIX              NO                                          
         ICM   RF,15,ATWADCON      MAKE SURE TWADCONS AVAIL                     
         BNZ   *+6                                                              
         DCHO                      NEED ANOTHER WAY TO FIND DYNALLOC!           
         ICM   RF,15,TDYNALLO-TWADCOND(RF)     (HINT - ITS IN DMGRL)            
         BNZ   *+6                                                              
         DCHO                                                                   
*                                                                               
         LHI   R0,3                NUMBER OF RETRIES FOR ALLOC                  
OUTINIT6 GOTO1 (RF),DMCB,(X'80',=CL8'OUTP'),                           X        
               (X'87',=XL6'000003000005'),                             X        
               (X'C0',GLAOUTP)                                                  
         OC    DMCB+12(4),DMCB+12                                               
         BZ    *+10                NO ERROR, CONTINUE                           
         BCT   R0,OUTINIT6          ELSE TRY AGAIN                              
         DCHO                                                                   
*                                                                               
         LA    R6,OUTP                                                          
         OPEN  ((R6),(OUTPUT))                                                  
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DCHO                                                                   
         ST    R6,AOUTPDCB                                                      
*                                                                               
OUTINIX  B     XIT                                                              
         EJECT                                                                  
*              MAIN I/O AND PROGRAM FLOW                                        
         SPACE 3                                                                
OUTSORT  NTR1                                                                   
         SPACE 1                                                                
OUT      GOTO1 =V(DRIVSORT),DMCB,(RA)     RECORD INTO GLANEXT                   
         CLI   8(R1),X'80'         EOF HANDLING                                 
         BNE   OUT2                                                             
         BAS   RE,CLRTMPBF         CLEAR TEMPBUFF FOR NEXT REQUEST              
         MVI   CBLEV,0                                                          
         MVI   GLCBLEV,0                                                        
         L     R1,GLAIO                                                         
         CLI   0(R1),0             IF WE ARE NOT ON FIRST TIME                  
         BE    XIT                                                              
         BAS   RE,MULTLT                                                        
         BAS   RE,DOWNLAST                                                      
         B     XIT                                                              
         SPACE 1                                                                
OUT2     CLI   0(R1),1             REJECT INACTIVE RECORDS                      
         BE    OUT                                                              
         L     R5,GLANEXT                                                       
         L     R1,GLAIO                                                         
         CLC   0(1,R1),0(R5)       IF THIS IS A NEW RECORD TYPE                 
         BE    *+8                                                              
         MVI   COMPSTAT,0          RESET COMPUTES STATUS                        
         BAS   RE,SETGLINT                                                      
         USING GLINTD,R2                                                        
         CLC   1(1,R5),GLDETLEV    ONLY INTERESTED IN DETAILS                   
         BNE   OUTTOT                                                           
         BAS   RE,CHECKVAL         CHECK FOR MIN/MAX                            
         BE    OUT4                                                             
         BAS   RE,BACKTEMP         FAILED - BACK THIS OUT OF TOTALS             
         BAS   RE,BACKOUT                                                       
         B     OUT                          AND REJECT THIS RECORD              
         SPACE 1                                                                
OUT4     BAS   RE,SETCB            SET CONTROL BREAK LEVEL                      
         MVC   GLCBLEV,CBLEV                                                    
         L     R1,GLAIO                                                         
         CLI   0(R1),0             IF WE ARE NOT ON FIRST TIME                  
         BE    *+8                                                              
         BAS   RE,MULTLT           DEAL WITH LAST TIMES                         
         SPACE 1                                                                
         CLC   0(1,R1),0(R5)       IF THIS IS A NEW RECORD TYPE                 
         BE    OUT6                                                             
         L     R1,AMYM1            CLEAR MIDLINES                               
         MVC   0(198,R1),SPACES                                                 
         MVC   198(198,R1),SPACES                                               
         MVC   LASTPDET,SPACES     RESET CONTINUATION FEATURE                   
         MVC   LASTP2,SPACES                                                    
         MVC   SAVEBXCL,SPACES                                                  
         XC    TOTCNTS,TOTCNTS     RESET TOTAL COUNTERS                         
         XC    DETTOTS,DETTOTS     RESET DETAILED TOTAL INDICATORS              
         L     R1,GLAFHEAD                                                      
         LTR   R1,R1                                                            
         BZ    OUT6                                                             
         MVI   0(R1),C'Y'          FORCE HEADLINES AFTER LAST TIMES             
         TM    GLINDS2,GLMIDHED    OR OPTIONALLY                                
         BNO   OUT6                                                             
         MVI   0(R1),C'M'          FORCE MIDHEAD                                
         SPACE 1                                                                
OUT6     BAS   RE,BUFFPOST         POST TOTALS FROM TEMPBUFF                    
         L     R6,GLAIO            MOVE DETAIL INTO GLAIO                       
         LH    R1,GLRECLEN                                                      
         MOVE  ((R6),(R1)),(R5)                                                 
         L     R5,GLAIO                                                         
*******  BAS   RE,BUFFPO6          POST, INT. COMPUTE ETC                       
         BAS   RE,OUTPOST                                                       
         BAS   RE,SETGLINT                                                      
         BAS   RE,MULTFT           HANDLE ANY FIRST TIME ROUTINES               
         OC    GLARANK,GLARANK     UNLESS RECORD RANKED                         
         BNZ   OUT8                                                             
         TM    GLRECIND,GLCLRANK                                                
         BO    OUT8                                                             
         BAS   RE,DETAIL           DO THE DETAILS                               
         B     OUT                                                              
         SPACE 1                                                                
OUT8     BAS   RE,PUTRANK          IF WE'RE RANKING RECORDS                     
         B     OUT                 WE NEED TO GO TO BUFFALO NOW                 
         SPACE 1                                                                
OUTTOT   BAS   RE,BUFFSAVE         SAVE TOTAL RECORDS FOR THE MOMENT            
         B     OUT                                                              
         EJECT                                                                  
*              CHECK FOR MIN/MAX VALUES                                         
         SPACE 3                                                                
*              INPUT               R5=A(RECORD TO BE CHECKED)                   
         SPACE 1                                                                
CHECKVAL NTR1                                                                   
         BAS   RE,SETGLINT                                                      
         USING GLINTD,R2                                                        
         LH    R2,GLRECLEN         R1=RECORD LENGTH +2 (FOR LENGTH)             
         CH    R2,=H'4096'                                                      
         BL    *+8                                                              
         CH    R2,=H'4096'                                                      
         L     R3,=A(TEMPREC)      SAVE RECORD SO WE CAN TEST IT                
         MOVE  ((R3),(R2)),(R5)                                                 
         LR    R5,R3                   NOW R5 ADDRESSES TEMP RECORD             
         GOTO1 =V(DRIVICMP),DMCB,(RA)  MAYBE INTERNAL COMPUTES                  
         BAS   RE,COMPREC              OR DRIVER COMPUTES                       
         GOTO1 =V(DRIVVTST),DMCB,(RA)  DRIVVTST DOES THE WORK                   
         B     XIT                     AND SETS THE CC                          
         EJECT                                                                  
*              BUFFSAVE - TUCK AWAY RECORD                                      
         SPACE 3                                                                
*              INPUT               R5=A(RECORD)                                 
*              OUTPUT              TUCK AWAY INTO TEMPBUFF                      
         SPACE 1                                                                
BUFFSAVE NTR1                                                                   
         L     R3,=A(TEMPBUFF)                                                  
         LA    R3,8(R3)            BYPASS ACCOUNTING FIELDS                     
         USING GLINTD,R2                                                        
         BAS   RE,SETGLINT                                                      
         LH    R2,GLRECLEN         R1=RECORD LENGTH +2 (FOR LENGTH)             
         DROP  R2                                                               
         LA    R2,2(R2)                                                         
         SPACE 1                                                                
BUFFSV2  OC    0(2,R3),0(R3)       LOOK FOR AN EMPTY SLOT                       
         BZ    BUFFSV4                                                          
         CLC   2(2,R3),0(R5)       OR FOR RECORD AT SAME LEVEL                  
         BE    BUFFSV6                                                          
         LH    R0,0(R3)                                                         
         AR    R3,R0                                                            
         B     BUFFSV2                                                          
         SPACE 1                                                                
BUFFSV4  L     R4,=A(TEMPBUFF)                                                  
         L     RE,0(R4)            N'BYTES IN BUFF SO FAR                       
         AR    RE,R2               ADD THIS ONE                                 
         C     RE,4(R4)            CHECK V MAX                                  
         BL    *+6                                                              
         DC    H'0'                **** INCREASE TEMPBUFF OR SOMETHING          
         ST    RE,0(R4)                                                         
         SPACE 1                                                                
BUFFSV6  SH    R5,=H'2'            ABOUT TO MOVE 2 EXTRA BYTES                  
         MOVE  ((R3),(R2)),(R5)                                                 
         STH   R2,0(R3)            SAVE RECORD LENGTH                           
         B     XIT                                                              
         SPACE 1                                                                
SETGLINT ZIC   R2,0(R5)            FROM RECORD NUMBER                           
         LTR   R2,R2               PROTECT FOR BUDGET (NO RECORDS)              
         BNZ   *+8                                                              
         LA    R2,1                                                             
         BCTR  R2,0                                                             
         SLL   R2,2                                                             
         LA    R2,GLAINTD(R2)                                                   
         L     R2,0(R2)                                                         
         BR    RE                                                               
         EJECT                                                                  
*              BUFFPOST - EMPTY TEMPBUFF AND POST                               
         SPACE 3                                                                
*              INPUT               TEMPBUFF                                     
         SPACE 1                                                                
BUFFPOST NTR1                                                                   
         L     R3,=A(TEMPBUFF)                                                  
         XC    0(4,R3),0(R3)       RESET BYTES USED TO 0                        
         LA    R3,8(R3)                                                         
         L     R5,GLAIO                                                         
         SPACE 1                                                                
BUFFPO2  OC    0(2,R3),0(R3)       LOOK FOR AN EMPTY SLOT                       
         BZ    XIT                 ALL DONE                                     
         LH    R4,0(R3)            LENGTH OF THIS RECORD (+2)                   
         SH    R4,=H'2'                                                         
         MOVE  ((R5),(R4)),2(R3)   RETURN RECORD TO GLAIO                       
         BAS   RE,BUFFPO6                                                       
*******  BAS   RE,COMPREC                                                       
*******  BAS   RE,OUTPOST                                                       
         LA    R4,2(R4)                                                         
         XCEF  (R3),(R4)           CLEAR                                        
         AR    R3,R4                                                            
         B     BUFFPO2                                                          
         SPACE 1                                                                
BUFFPO4  NTR1                                                                   
         MVC   GLRECNO,0(R5)       SET RECORD                                   
         MVC   GLLEVEL,1(R5)       AND LEVEL NUMBER                             
         BAS   RE,SETGLINT                                                      
         ST    R2,GLATHID                                                       
         GOTO1 =V(DRIVICMP),DMCB,(RA)   CONTROL INTERNAL COMPUTES               
         BAS   RE,OUTPOST          POST RECORD IN CASE OF VERTICAL %            
         BAS   RE,COMPREC          CHECK COMPUTES FOR RECORD                    
         BAS   RE,OUTPOST          THEN REPOST THIS                             
         B     XIT                                                              
         SPACE 1                                                                
BUFFPO6  NTR1                                                                   
         MVC   GLRECNO,0(R5)       SET RECORD                                   
         MVC   GLLEVEL,1(R5)       AND LEVEL NUMBER                             
         BAS   RE,SETGLINT                                                      
         ST    R2,GLATHID                                                       
         BAS   RE,OUTPOST          POST RECORD IN CASE OF VERTICAL %            
         BAS   RE,COMPREC          CHECK COMPUTES FOR RECORD                    
         BAS   RE,OUTPOST          THEN REPOST THIS                             
         B     XIT                                                              
         SPACE 3                                                                
* ROUTINE TO CLEAR TEMPBUFF                                                     
*                                                                               
         SPACE 1                                                                
CLRTMPBF LR    R0,RE                                                            
         L     RE,=A(TEMPBUFF)                                                  
         XC    0(4,RE),0(RE)                                                    
         L     RF,4(RE)                                                         
         LA    RE,8(RE)                                                         
         XCEFL ,                                                                
         LR    RE,R0                                                            
         BR    RE                                                               
         EJECT                                                                  
*              PUTTING RECORDS OUT TO BUFFALO                                   
         SPACE 3                                                                
*                                  R4=A(BUFFALOC)                               
*                                  R5=PRESENT DATA RECORD                       
PUTRANK  NTR1                                                                   
         USING GLINTD,R2                                                        
         L     R4,=A(BUFFALOC)                                                  
         USING BUFFALOD,R4                                                      
         CLI   ANYRANK,C'Y'        IF THIS IS THE FIRST PUT                     
         BE    PUTRANK2                                                         
         LH    R1,GLRECLEN         NEED TO INITIALIZE BUFFALO CSECT             
         ST    R1,BUFFLCOM                                                      
         LA    R1,16(R1)           ADD LENGTH OF ABVAL AND ABCOUNT              
         SR    RE,RE                                                            
         L     RF,=F'50000'        HOW MANY WILL FIT INTO 50000 BYTES?          
         DR    RE,R1                                                            
         BCTR  RF,0                                                             
         ST    RF,BUFFCRMX         TELL BUFFALO ABOUT THAT                      
         GOTO1 BUFFALO,DMCB,=C'SET',(R4)                                        
         ZAP   ABCOUNT,=P'0'                                                    
         ZAP   BBCOUNT,=P'0'                                                    
         ZAP   BBVAL,=P'0'                                                      
         SPACE 1                                                                
PUTRANK2 MVI   ANYRANK,C'Y'                                                     
         BAS   RE,RANKVAL          PUT CURRENT RECORD TO TEMP                   
*                                  HANDLE COMPUTES ON TEMP RECORD               
*                                  AND GET RANK VALUE INTO ABVAL                
         AP    ABCOUNT,=P'1'       COUNT TO KEY TO STOP BUFFALO                 
*                                  COMBINING SAME VALUE RECORDS                 
         LH    R6,GLRECLEN                                                      
         MOVE  (ABDATA,(R6)),(R5)                                               
         GOTO1 BUFFALO,DMCB,=C'PUT',(R4),ABVAL                                  
*                                                                               
         B     XIT                                                              
         EJECT                                                                  
*              GET RANK VALUE INTO ABVAL                                        
*                                                                               
RANKVAL  NTR1                                                                   
*                                                                               
         USING GLINTD,R2                                                        
*                                                                               
         LH    R4,GLRECLEN         R1=RECORD LENGTH +2 (FOR LENGTH)             
*                                                                               
         CH    R4,=H'4096'                                                      
         BL    *+8                                                              
         LH    R4,=H'4096'                                                      
*                                                                               
         L     R3,=A(TEMPREC)      SAVE RECORD FOR THE MOMENT                   
         MOVE  ((R3),(R4)),(R5)                                                 
*                                                                               
         ZAP   ABVAL,=P'0'         BUFFALO RECORD IS THAT VALUE                 
*                                                                               
         ICM   R1,15,GLARANK       NEED TO COMPUTE VALUE TO RANK                
         BZ    RANKVAL2            NOT NECESSARILY ANYTHING                     
*                                                                               
         GOTO1 =V(DRIVICMP),DMCB,(RA)  MAYBE INTERNAL COMPUTES                  
*                                                                               
         BAS   RE,COMPREC              OR DRIVER COMPUTES                       
*                                                                               
         L     R1,GLARANK          NEED TO COMPUTE VALUE TO RANK                
         USING DRIND,R1                                                         
*                                                                               
         ICM   R1,15,DRINCOMP                                                   
         BZ    RANKVAL2            NOTHING TO RANK                              
         DROP  R1                                                               
*                                                                               
         ST    R1,DMCB+4                                                        
         GOTO1 =V(DRIVCOMP),DMCB,(RA),,(R5)                                     
*                                                                               
*        RESULT OF COMPUTATION RETURNED IN DUB                                  
*                                                                               
         AP    DUB,=P'100000000000000'  MAKE ALL NUMBERS SAME SIGN              
*                                                                               
         CP    DUB,=P'0'                PROTECT AGAINST HUGE NEGATIVE           
         BL    RANKVAL2                                                         
*                                                                               
         ZAP   ABVAL,=P'200000000000000'  INIT TO MAX VALUE                     
*                                                                               
         CP    DUB,ABVAL                  SET ABVAL TO VALUE OR MAX             
         BH    *+10                                                             
         ZAP   ABVAL,DUB                                                        
*                                                                               
RANKVAL2 DS    0H                  COMPLEMENT RANKING VALUE                     
*                                                                               
         ZAP   DUB,=P'200000000000000'  INIT TO MAX VALUE                       
         SP    DUB,ABVAL           COMPLEMENT RANKING VALUE                     
         ZAP   ABVAL,DUB                                                        
*                                                                               
         GOTO1 =V(DRIVRANK),DMCB,(RA),=C'PUT',(R5),ABCOUNT                      
*                                                                               
         MOVE  ((R5),(R4)),(R3)    RESTORE ORIGINAL RECORD                      
*                                                                               
         B     XIT                                                              
         EJECT                                                                  
*              DEALING WITH RANK OUTPUT                                         
         SPACE 3                                                                
*                                  FOR EACH RANK RECORD ROUTINE WILL            
*                                  ESTABLISH RANK NUMBER WHICH WILL             
*                                  REPLACE VALUE IN RECORD AND SET              
*                                  RANK NUMBER (RANKEQU) AND EQUALITY           
*                                  SWITCH (EQUSW)                               
         SPACE 1                                                                
RANKOUT  NTR1                                                                   
         CLI   ANYRANK,C'Y'        ANYTHING PENDING?                            
         BNE   XIT                                                              
         MVI   ANYRANK,C'N'                                                     
         MVI   EQUSW,C'N'                                                       
         ZAP   LASTVAL,=P'0'                                                    
         BAS   RE,RANKGET                                                       
         XC    RANKNO,RANKNO                                                    
         MVC   RANKEQU,=F'1'                                                    
*                                                                               
RANKOUT2 BAS   RE,RANKGET                                                       
         TM    ABCOUNT,X'80'       'EOF'?                                       
         BO    XIT                                                              
         CP    ABVAL,LASTVAL       COMPARE THIS AGAINST LAST                    
         BE    RANKOUT4                                                         
         MVC   RANKEQU,RANKNO      DIFFERENT SO RESET RANK NO                   
         MVI   EQUSW,C'N'                                                       
         TM    BBCOUNT,X'80'       IS NEXT EOF?                                 
         BO    RANKOUT4                                                         
         ZAP   LASTVAL,ABVAL                                                    
         CP    ABVAL,BBVAL         COMPARE THIS AGAINST NEXT                    
         BNE   *+8                                                              
         MVI   EQUSW,C'Y'                                                       
         SPACE 1                                                                
RANKOUT4 OC    GLRNKMAX,GLRNKMAX   OPTION TO SET MAX TO RANK                    
         BZ    RANKOUT6                                                         
         CLC   RANKEQU,GLRNKMAX                                                 
         BNH   RANKOUT6                                                         
*                                  BACK DETAIL OUT OF TOTALS                    
         BAS   RE,BACKOUT          (TOTALS ARE NOT IN TEMP FOR RANK)            
         B     RANKOUT2                                                         
         SPACE 1                                                                
RANKOUT6 LA    R5,ABDATA           POST FROM BUFFALO 'A' DATA                   
******   BAS   RE,OUTPOST          POST RECORD IN CASE OF VERT %                
******   BAS   RE,COMPREC          CHECK COMPUTES FOR RECORD                    
******   BAS   RE,OUTPOST          NOW RE-POST                                  
         BAS   RE,DETAIL           AND HANDLE DETAIL LINE                       
         B     RANKOUT2                                                         
         EJECT                                                                  
*              ROUTINE TO SHUFFLE BUFFALO RECORDS                               
         SPACE 3                                                                
*                                  R4=A(BUFFALOC)                               
         SPACE 1                                                                
RANKGET  NTR1                                                                   
         L     R4,=A(BUFFALOC)                                                  
         USING BUFFALOD,R4                                                      
         L     R1,RANKNO           ADD 1 TO RANK NUMBER                         
         LA    R1,1(R1)                                                         
         ST    R1,RANKNO                                                        
         MOVE  (ABVAL,1000),BBVAL  MOVE B TO A                                  
         TM    ABCOUNT,X'80'       IS THIS THE FINAL RECORD?                    
         BNO   RANKGET2                                                         
         ZAP   BBVAL,=P'0'         CLEAR VAL & COUNT                            
         ZAP   BBCOUNT,=P'0'                                                    
         B     XIT                                                              
         SPACE 1                                                                
RANKGET2 LA    R2,=C'HIGH'         READ HIGH FIRST TIME                         
         CP    BBCOUNT,=P'0'                                                    
         BZ    *+8                                                              
         LA    R2,=C'SEQ'          OR SEQUENTIAL                                
         GOTO1 BUFFALO,DMCB,(R2),(R4),BBVAL,1                                   
         TM    DMCB+8,X'80'        DID BUFFALO SET EOF                          
         BNO   RANKGET4                                                         
         OI    BBCOUNT,X'80'       YES SO MARK B RECORD                         
         ZAP   BBVAL,=P'200000000000000'                                        
         MVI   DMCB+8,0            AND STOP CONFUSION IN DMCB                   
         SPACE 1                                                                
RANKGET4 DS    0H                                                               
         GOTO1 =V(DRIVRANK),DMCB,(RA),=C'GET',BBDATA,BBCOUNT                    
         B     XIT                 SO WE CAN SPOT IT NEXT TIME                  
         EJECT                                                                  
*              ROUTINE MOVES RECORD TO PRESET SPOT                              
         SPACE 3                                                                
*                                  R5=A(RECORD TO BE POSTED)                    
OUTPOST  NTR1                                                                   
         MVC   GLRECNO,0(R5)       SET RECORD                                   
         MVC   GLLEVEL,1(R5)       AND LEVEL NUMBER                             
         BAS   RE,SETGLINT                                                      
         ST    R2,GLATHID                                                       
         USING GLINTD,R2                                                        
         LH    R1,GLRECLEN                                                      
         ZIC   R6,GLLEVEL          AND LEVEL NUMBER                             
         SLL   R6,2                                                             
         LA    R6,GLARECL0(R6)                                                  
         OI    0(R6),X'80'         INDICATE THERE'S A REC AT THIS LEVEL         
         L     R6,0(R6)            FIGURE OUT WHERE TO POST                     
         ST    R6,AACTREC                                                       
         ST    R6,GLATHREC                                                      
         MOVE  ((R6),(R1)),(R5)    AND DO IT                                    
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE BACKS OUT DETAIL RECORD FROM TEMPORARY TOTALS            
         SPACE 3                                                                
*                                  R5=A(RECORD TO BE BACKED OUT)                
         SPACE 1                                                                
BACKTEMP NTR1                                                                   
         L     R3,=A(TEMPBUFF)     ROUTINE TO BACK OUT OF TEMPBUFF              
         LA    R3,8(R3)                                                         
         XC    BACKLEVS,BACKLEVS                                                
         SPACE 1                                                                
BACKTMP2 OC    0(2,R3),0(R3)       ANYTHING THERE?                              
         BZ    XIT                                                              
         LA    R6,2(R3)            POINT TO A TOTAL RECORD                      
         CLC   0(1,R6),0(R5)       CHECK REC. MATCH                             
         BNE   XIT                                                              
*                                  SUBTRACT DETAIL AT (R5)                      
*                                  FROM TOTAL AT (R6)                           
         GOTO1 =V(DRIVADD),DMCB,(C'-',(RA)),(R6),(R5)                           
         ZIC   R1,1(R6)                                                         
         LA    R1,BACKLEVS(R1)                                                  
         MVI   0(R1),1             NOTE BACK-OUT AT THIS LEVEL                  
         AH    R3,0(R3)                                                         
         B     BACKTMP2                                                         
         SPACE 1                                                                
BACKLEVS DC    XL24'00'                                                         
         EJECT                                                                  
*              ROUTINE BACKS OUT DETAIL RECORD FROM TOTALS                      
         SPACE 3                                                                
*                                  R5=A(RECORD TO BE BACKED OUT)                
         SPACE 1                                                                
BACKOUT  NTR1                                                                   
         MVC   GLRECNO,0(R5)       SET RECORD                                   
         MVC   GLLEVEL,1(R5)       AND LEVEL NUMBER                             
         BAS   RE,SETGLINT                                                      
         ST    R2,GLATHID                                                       
         USING GLINTD,R2                                                        
         MVI   THISLEV,0                                                        
         SPACE 1                                                                
BACKOUT2 CLC   THISLEV,GLLEVEL     ALL LEVELS DONE?                             
         BE    XIT                                                              
         ZIC   R6,THISLEV          PICK UP CURRENT LEVEL NUMBER                 
         SLL   R6,2                                                             
         LA    R6,GLARECL0(R6)                                                  
         TM    0(R6),X'80'         IS THERE A TOTAL AT THIS LEVEL?              
         BNO   BACKOUT4                                                         
         L     R6,0(R6)            FIGURE OUT WHERE THIS TOTAL IS               
*                                  SUBTRACT DETAIL AT (R5)                      
*                                  FROM TOTAL AT (R6)                           
         ZIC   R1,1(R6)                                                         
         LA    R1,BACKLEVS(R1)                                                  
         CLI   0(R1),1             CHECK WE DIDN'T ALREADY BACK OUT             
         BE    BACKOUT4                                                         
         GOTO1 =V(DRIVADD),DMCB,(C'-',(RA)),(R6),(R5)                           
         SPACE 1                                                                
BACKOUT4 AI    THISLEV,1                                                        
         B     BACKOUT2                                                         
         EJECT                                                                  
*              ROUTINE CONTROLS COMPUTES ON RECORD                              
         SPACE 3                                                                
*                                  R5=A(RECORD TO BE COMPUTED)                  
         SPACE 1                                                                
COMPREC  NTR1                                                                   
         CLI   COMPSTAT,1                                                       
         BE    XIT                 FORGET IT - NO COMPUTES NEEDED               
         MVI   COMPSTAT,1                                                       
         BAS   RE,COMP2                                                         
         BAS   RE,COMP2            SECOND TIME TO ALLOW RIGHT TO LEFT           
         B     XIT                                                              
         SPACE 1                                                                
COMP2    NTR1                                                                   
         ZIC   R2,0(R5)            RECORD NUMBER                                
         BCTR  R2,0                                                             
         SLL   R2,2                                                             
         LA    R2,GLAINTD(R2)                                                   
         L     R2,0(R2)                                                         
*******  ST    R2,GLATHID                                                       
         USING GLINTD,R2                                                        
         L     R3,GLAFIN                                                        
         DROP  R2                                                               
         SPACE 1                                                                
COMP20   LR    R4,R3               NOTE AN INPUT FIELD                          
         B     COMPNEXT                                                         
         SPACE 1                                                                
         USING DROD,R3                                                          
COMP30   OC    DROACOMP,DROACOMP   IN ORDER TO QUALIFY FIELD                    
         BZ    COMP30X             MUST HAVE AN ATTACHED COMPUTE                
         LTR   R4,R4               AND ALSO A PREVIOUS 'IN'                     
         BZ    COMP30X                                                          
         USING DRIND,R4                                                         
         MVI   COMPSTAT,2                                                       
         MVC   DMCB+4(4),DROACOMP                                               
         GOTO1 =V(DRIVCOMP),DMCB,(RA),,(R5)                                     
         TM    DROOPTS,X'10'       IF ABSOLUTE VALUE                            
         BNO   COMP40                                                           
         CP    DUB,=P'0'           AND IF NEGATIVE NUMBER                       
         BNL   COMP40                                                           
         MP    DUB,=P'-1'          MAKE POSITIVE                                
COMP40   DS    0H                                                               
         LH    R2,DRINDISP         POSITION TO INPUT FIELD                      
         AR    R2,R5                                                            
         CLI   DRINTYPE,C'P'       SELECT TYPE                                  
         BE    COMPPACK                                                         
         CLI   DRINTYPE,C'B'                                                    
         BE    COMPBIN                                                          
         B     COMP30X                                                          
         SPACE 1                                                                
COMPPACK ZIC   R1,DRINLEN          PACKED                                       
         BCTR  R1,0                                                             
         SLL   R1,4                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         ZAP   0(0,R2),DUB                                                      
         B     COMP30X                                                          
         SPACE 1                                                                
COMPBIN  CVB   R1,DUB                                                           
         CLI   DRINFLEN,2                                                       
         BL    COMPBIN1                                                         
         BE    COMPBIN2                                                         
         CLI   DRINFLEN,4                                                       
         BL    COMPBIN3                                                         
         BE    COMPBIN4                                                         
         SPACE 1                                                                
COMPBIN1 STC   R1,0(R2)                                                         
         B     COMP30X                                                          
         SPACE 1                                                                
COMPBIN2 STH   R1,0(R2)                                                         
         B     COMP30X                                                          
         SPACE 1                                                                
COMPBIN3 ST    R1,DUB                                                           
         MVC   0(3,R2),DUB+1                                                    
         B     COMP30X                                                          
         SPACE 1                                                                
COMPBIN4 ST    R1,0(R2)                                                         
         SPACE 1                                                                
COMP30X  SR    R4,R4                                                            
         SPACE 1                                                                
COMPNEXT ZIC   R1,1(R3)                                                         
         AR    R3,R1                                                            
         CLI   0(R3),X'20'                                                      
         BE    COMP20                                                           
         CLI   0(R3),X'30'                                                      
         BE    COMP30                                                           
         CLI   0(R3),X'10'                                                      
         BE    XIT                                                              
         CLI   0(R3),0                                                          
         BNE   COMPNEXT                                                         
         B     XIT                                                              
         DROP  R4                                                               
         DROP  R3                                                               
         SPACE 1                                                                
COMPSTAT DC    X'00'               CHANGED TO 1 IF NO COMPUTES                  
*                                  CHANGED TO 2 IF COMPUTES OK                  
         EJECT                                                                  
*              SET CONTROL BREAK                                                
         SPACE 3                                                                
*                                  R5=A(RECORD COMING FROM SORT)                
*                                  PREVIOUS DETAIL IS IN GLAIO                  
         SPACE 1                                                                
SETCB    NTR1                                                                   
         L     R6,GLAIO                                                         
         BAS   RE,SETGLINT                                                      
         USING GLINTD,R2                                                        
         CLI   GLRECLEV,0          UNLESS RECORD IS IMBEDDED                    
         BNE   SCB2                                                             
         MVI   CBLEV,0                                                          
         CLC   0(1,R5),0(R6)       TEST CHANGE OF RECORD                        
         BNE   XIT                                                              
         SPACE 1                                                                
SCB2     LA    R3,GLLCBS           NOW SET UP TO TEST OTHER LEVELS              
         LA    R4,1                R4=LEVEL NUMBER                              
         AH    R5,GLCONLEN                                                      
         AH    R6,GLCONLEN                                                      
         SPACE 1                                                                
SCB4     STC   R4,CBLEV                                                         
         ZIC   R1,0(R3)            PICK UP CUMULATIVE LENGTH                    
         LTR   R1,R1                                                            
         BNZ   SCB6                                                             
         BCTR  R4,0                                                             
         STC   R4,CBLEV                                                         
         B     XIT                                                              
         SPACE 1                                                                
SCB6     BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R5),0(R6)       LOOK FOR A CHANGE AT THIS LEVEL              
         BNE   XIT                 FOUND                                        
         LA    R3,1(R3)            GO AND TRY NEXT LOWER LEVEL                  
         LA    R4,1(R4)                                                         
         B     SCB4                                                             
         EJECT                                                                  
*              CONTROL MULTIPLE FIRST TIME ROUTINES                             
         SPACE 3                                                                
MULTFT   NTR1                                                                   
*                                  CBLEV IS SET TO BREAK LEVEL                  
         L     R2,GLATHID                                                       
         USING GLINTD,R2                                                        
         MVC   GLLEVEL,CBLEV       START FROM LOWEST UP TO DETAIL               
         SPACE 1                                                                
MULTFT2  CLC   GLLEVEL,GLDETLEV    DON'T NEED FT AT DETAIL LEVEL                
         BE    XIT                                                              
         BAS   RE,SINGFT           HANDLE FT ROUTINES AT THIS LEVEL             
         AI    GLLEVEL,1                                                        
         B     MULTFT2                                                          
         EJECT                                                                  
*              CONTROL MULTIPLE LAST TIME ROUTINES                              
         SPACE 3                                                                
MULTLT   NTR1                                                                   
*                                  CBLEV IS SET TO BREAK LEVEL                  
         L     R5,GLAIO            NEED GLINTD FOR PREVIOUS                     
         BAS   RE,SETGLINT                                                      
         USING GLINTD,R2                                                        
         CLC   CBLEV,GLDETLEV      DON'T NEED LT ON DETAIL                      
         BE    XIT                                                              
         ZIC   R1,GLDETLEV         SO PROCESS FROM DETAIL-1 LEVEL               
         BCTR  R1,0                TO CBLEV SETTING BACKWARDS                   
         SPACE 1                                                                
MULTLT2  STC   R1,GLLEVEL                                                       
         BAS   RE,SINGLT           HANDLE LT ROUTINES AT THIS LEVEL             
         CLC   GLLEVEL,CBLEV                                                    
         BE    XIT                                                              
         BCTR  R1,0                                                             
         B     MULTLT2                                                          
         EJECT                                                                  
*              CONTROL FIRST, LAST AND TOTAL ROUTINES                           
         SPACE 3                                                                
SINGFT   NTR1                                                                   
         MVI   BREAK,C'F'                                                       
         MVI   GLHOOK,GLFIRST                                                   
         B     SING2                                                            
         SPACE 1                                                                
SINGLT   NTR1                                                                   
         MVI   BREAK,C'L'                                                       
         MVI   GLHOOK,GLLAST                                                    
         CLI   GLLEVEL,0                                                        
         BE    SINGL0                                                           
         CLC   GLLEVEL,GLRNKLEV    MAY NEED TO RELEASE RANK RECORDS             
         BNE   SING2                                                            
         BAS   RE,RANKOUT                                                       
         SPACE 1                                                                
SING2    XC    GLARGS,GLARGS                                                    
         MVC   GLARGS(1),GLLEVEL                                                
         BAS   RE,SETAOUT          PICK UP A(THIS OUT) IF ANY                   
         MVC   GLADTENT,ALEVOUT                                                 
         XC    GLAROUT,GLAROUT                                                  
         MVI   GLAROUT,1                                                        
         BAS   RE,GOHOOK           HOOK TO APPLICATION FOR FIRST/LAST           
         OC    ALEVOUT,ALEVOUT                                                  
         BZ    XIT                 (NONE AT THIS LEVEL)                         
         USING DROD,R3                                                          
         L     R3,ALEVOUT                                                       
         CLI   DROFLFOL,0          ANY FIRST/LAST SPECIFIED?                    
         BE    SINGTOT                                                          
         MVI   ELCODE,X'44'        YES SO GO AND LOOK FOR THEM                  
         ZIC   R0,DROFLFOL                                                      
         BAS   RE,EXFLT                                                         
         SPACE 1                                                                
SINGTOT  CLI   BREAK,C'L'          IF LAST TIME ROUTINE                         
         BNE   XIT                                                              
         CLI   DROTLFOL,0          ARE THERE ANY TOTALS SPECIFIED?              
         BE    XIT                                                              
         MVI   ELCODE,X'48'                                                     
         ZIC   R0,DROTLFOL                                                      
         BAS   RE,EXFLT            THEN EXECUTE ANY CONTROLS                    
         B     XIT                                                              
         SPACE 1                                                                
SINGL0   L     R2,GLATHID          SPECIAL ROUTINE FOR LEVEL 0                  
         USING GLINTD,R2                                                        
         L     R3,GLARECEL         PICK UP ADDRESS OF RECORD ELEMENT            
         USING DRRECD,R3                                                        
         CLI   DRRECTOT,0          AND SEE IF THERE ARE ANY TOTALS              
         BE    XIT                                                              
         MVI   ELCODE,X'48'                                                     
         ZIC   R0,DRRECTOT                                                      
         BAS   RE,EXFLT                                                         
         B     XIT                                                              
         EJECT                                                                  
*              EXECUTE FIRST, LAST OR TOTAL ELEMENT                             
         SPACE 3                                                                
EXFLT    NTR1                                                                   
         MVI   AUTALLSW,C'Y'                                                    
*                                  R0=NUMBER OF ELEMENTS TO COME                
*                                  ELCODE = X'44' OR X'48'                      
*                                  BREAK = C'F' OR 'L'                          
*                                  R3=A(OUT) OR A(RECORD) FOR LEVEL 0           
         L     R2,GLATHID                                                       
         USING GLINTD,R2                                                        
         XC    GLAIFLD,GLAIFLD                                                  
         XC    GLATOUT,GLATOUT                                                  
         CLI   GLLEVEL,0                                                        
         BE    EXFLT2                                                           
         USING DROD,R3                                                          
         ST    R3,GLATOUT          SAVE A(OUT FOR TOTAL ROUTINES)               
         L     R1,DROIADD          PICK UP A(IN) IF ANY                         
         LTR   R1,R1                                                            
         BZ    EXFLT2                                                           
         USING DRIND,R1                                                         
         LH    R1,DRINDISP                                                      
         DROP  R1                                                               
         A     R1,AACTREC                                                       
         ST    R1,GLAIFLD          RECORD A(INPUT FIELD IN RECORD)              
         SPACE 1                                                                
EXFLT2   ZIC   R1,1(R3)                                                         
         LTR   R1,R1                                                            
         BZ    XIT                                                              
         AR    R3,R1                                                            
         CLC   0(1,R3),ELCODE      LOOK FOR MATCH ON ELEMENT CODE               
         BNE   EXFLT2                                                           
         USING DRFLD,R3                                                         
         CLI   ELCODE,X'48'                                                     
         BE    EXFLT4                                                           
         CLC   DRFLTYPE,BREAK      AND FIRST/LAST                               
         BNE   EXFLTEND                                                         
         SPACE 1                                                                
EXFLT4   DS    0H                  CHECK ANY CONDITIONALS                       
         GOTO1 =V(DRIVTEST),DMCB,(RA),DRFLIFS                                   
         BNE   EXFLTEND                                                         
         SPACE 1                                                                
         CLI   ELCODE,X'48'        ARE WE HANDLING TOTALS?                      
         BNE   EXFLT5                                                           
         CLI   DRFLDET,0           IS THIS DETAILED TOTALS ?                    
         BE    *+12                                                             
         TM    GLTOTIND,GLTITOT    YES - ONLY GET REGULAR TOTALS IF             
         BZ    EXFLTEND                  TOTALS AT LEVEL ABOVE DETAIL           
         L     RF,GLAIFLD          PROTECT GLAIFLD                              
         BAS   RE,TOTALS           YES - SO LOOK FOR NUMBERS                    
         XC    CUMEBIN,CUMEBIN     CLEAR CUME COUNTERS                          
         ZAP   CUMEPACK,=P'0'                                                   
         ZIC   R1,GLLEVEL                                                       
         SLL   R1,3                                                             
         LA    R1,CDWNDUBS(R1)     CLEAR DOWNCAST                               
         ZAP   0(8,R1),=P'0'                                                    
         ST    RF,GLAIFLD                                                       
         CLI   TOTPEND,C'Y'                                                     
         BE    EXFLT5                                                           
         TM    GLNORBOX,X'40'      IF TOTALS SUPPRESSED                         
         BNO   EXFLT9                                                           
         MVI   HORIZSW,1              DO A RULE NEXT TIME                       
         B     EXFLT9                                                           
         SPACE 1                                                                
EXFLT5   OC    DRFLRADD,DRFLRADD   ANY ROUTINE TO HANDLE?                       
         BZ    EXFLT6                                                           
         MVC   GLAROUT,DRFLRADD                                                 
         MVC   GLLABEL,DRFLROUT                                                 
         MVC   GLARGS,DRFLARGS                                                  
*                                  (GLAIFLD SET ABOVE)                          
         MVC   GLAOFLD,DRFLAPOS                                                 
         ST    R3,GLADTENT                                                      
         MVI   GLHOOK,GLROUT                                                    
         OI    GLINDS,X'40'        (X'40' INDICATES HOOK FROM TOTAL)            
         BAS   RE,GOHOOK                                                        
         NI    GLINDS,X'BF'                                                     
         CLI   GLHOOK,GLEDIT       'ALL' CAN STILL BE PUT IN                    
         BE    EXFLT6              AUTOMATICALLY                                
         MVI   AUTALLSW,C'N'                                                    
         SPACE 1                                                                
EXFLT6   CLI   DRFLELEN,60         ANY LITERALS FOR US TO HANDLE                
         BE    EXFLT8                                                           
         ZIC   R1,DRFLELEN         YES                                          
         SH    R1,=H'60'                                                        
         STC   R1,LITLEN           SET LENGTH                                   
         LA    R1,DRFLLIT                                                       
         ST    R1,ALITIN           ADDRESS                                      
         MVC   ALITOUT,DRFLAPOS    AND WHERE IT SHOULD GO                       
         BAS   RE,DOLIT                                                         
         MVI   AUTALLSW,C'N'                                                    
         SPACE 1                                                                
EXFLT8   CLI   LINETYPE,C'T'       ANY TOTALS WAITING FOR PRINT?                
         BNE   EXFLT9              LINETYPE SET IN TOTALS ROUTINE               
         BAS   RE,AUTOALL          CAN PUT 'ALL' AUTOMATICALLY                  
         BAS   RE,ROBOXOFF         MAY TURN OFF ROW BOXES                       
         MVC   NLINES,GLSPACE                                                   
         BAS   RE,SPLAT                                                         
         BAS   RE,ROBOXON          (MAY TURN ROW BOXES BACK ON)                 
         MVI   LINETYPE,0                                                       
         SPACE 1                                                                
EXFLT9   OC    DRFLRADD,DRFLRADD   IF LAST ROUTINE JUST CALLED                  
         BZ    EXFLT9A                                                          
         CLI   BREAK,C'L'                                                       
         BNE   EXFLT9A                                                          
         TM    GLINDS3,GLLSTLIN    AND USER WANTS 'LAST' LINES PRINTED          
         BZ    EXFLT9A                                                          
         BAS   RE,DETAIL           THEN PRINT 'LAST' LINES                      
         TM    GLINDS3,GLLSTLIN    UNTIL USER SAYS STOP                         
         BO    *-8                                                              
         SPACE 1                                                                
EXFLT9A  CLI   DRFLSPAC,0          ANY PRINTING TO DO                           
         BE    EXFLT10                                                          
         CLI   BREAK,C'L'          ONLY LAST GETS HANDLED HERE                  
         BNE   EXFLT10                                                          
         TM    GLINDS,GLISDONT     TEST SUPPRESS SPACING FOR REJECTED           
         BZ    *+12                                          PRNT LINES         
         CLI   DONTSW,C'Y'         YES-TEST PRINT LINE REJECTED                 
         BE    EXFLT10                 YES-DO NOT PRINT BLANK LINE(S)           
         MVC   NLINES,DRFLSPAC     SET NUMBER OF LINES                          
         BAS   RE,SPLAT            AND DO IT                                    
*                                                                               
EXFLT10  TM    DRFLOPT,X'80'       OPTION TO SKIP NEXT TIME                     
         BNO   EXFLT11                                                          
         MVC   LASTPDET,SPACES     STOP 'CONTINUED' FEATURE                     
         MVC   LASTP2,SPACES                                                    
         MVC   SAVEP3,SPACES                                                    
         MVC   SAVEP4,SPACES                                                    
         L     R1,GLAFHEAD         YES SO SET FORCEHED                          
         LTR   R1,R1                                                            
         BZ    EXFLT12                                                          
         MVI   0(R1),C'Y'                                                       
         XC    LASTMID,LASTMID                                                  
         SPACE 1                                                                
EXFLT11  TM    DRFLOPT,X'10'       MIDHEAD OPTION                               
         BNO   EXFLT12                                                          
         MVC   LASTPDET,SPACES     STOP 'CONTINUED' FEATURE                     
         MVC   LASTP2,SPACES                                                    
         MVC   LASTP3,SPACES                                                    
         MVC   LASTP4,SPACES                                                    
         L     R1,GLAFHEAD         YES SO SET FORCEHED TO M                     
         LTR   R1,R1                                                            
         BZ    EXFLT12                                                          
         MVI   0(R1),C'M'                                                       
         XC    LASTMID,LASTMID                                                  
         SPACE 1                                                                
EXFLT12  TM    DRFLOPT,X'40'       OPTION TO RESET PAGE NUMBER                  
         BNO   EXFLT14                                                          
         L     R1,GLAPAGE          YES                                          
         LTR   R1,R1                                                            
         BZ    EXFLT14                                                          
         MVC   0(2,R1),=H'1'                                                    
         SPACE 1                                                                
EXFLT14  TM    DRFLOPT,X'20'       OPTION TO RESET SUBPAGE                      
         BNO   EXFLT16                                                          
         L     R1,GLASPAGE         YES                                          
         LTR   R1,R1                                                            
         BZ    EXFLT16                                                          
         MVC   0(2,R1),=H'1'                                                    
         SPACE 1                                                                
EXFLT16  CLI   DRFLSPAC,0          ANY PRINTING TO DO                           
         BE    EXFLT18                                                          
         CLI   BREAK,C'L'          DEALING WITH LAST HERE                       
         BE    EXFLT18                                                          
         TM    GLINDS,GLISDONT     TEST SUPPRESS SPACING FOR REJECTED           
         BZ    *+12                                          PRNT LINES         
         CLI   DONTSW,C'Y'         YES-TEST PRINT LINE REJECTED                 
         BE    EXFLT18                 YES-DO NOT PRINT BLANK LINE(S)           
         MVC   NLINES,DRFLSPAC     SET NUMBER OF LINES                          
         BAS   RE,SPLAT            AND DO IT                                    
         SPACE 1                                                                
EXFLT18  DS    0H                                                               
         TM    DRFLOPT,X'08'       CHECK IF PRINTING HORIZON                    
         BNO   EXFLT20                                                          
         MVI   HORIZSW,1                                                        
         B     EXFLT20                                                          
         SPACE 1                                                                
EXFLT20  DS    0H                                                               
         SPACE 1                                                                
EXFLTEND BCT   R0,EXFLT2                                                        
         B     XIT                                                              
         EJECT                                                                  
*              SUPPORT ROW BOXES OFF AND ON FOR TOTALS                          
         SPACE 3                                                                
ROBOXOFF NTR1                                                                   
         TM    GLNORBOX,X'40'      OPTION TO REMOVE ROW BOXES                   
         BNO   XIT                   WHEN WE ARE DOING TOTALS                   
         TM    GLDOWNLD,GLDLACTV                                                
         BO    XIT                 NOT INTERESTED IF DOWNLOADING                
         SPACE 1                                                                
         L     R6,ABOX                                                          
         USING BOXD,R6                                                          
         MVC   BOXCOLS(198),SAVEBXCL    (THIS GOT SET IN DRIVHEAD)              
         LA    R1,BOXCOLS                                                       
         L     R0,AFCBOX           ADDRESS OF FIRST COLUMN BOX                  
         SR    R0,R1                                                            
         BNP   XIT                                                              
         SPACE 1                                                                
RBOFF2   CLI   0(R1),C'C'          LOOK FOR 'CENTERS'                           
         BNE   *+8                                                              
         MVI   0(R1),C' '          AND REMOVE                                   
         LA    R1,1(R1)                                                         
         BCT   R0,RBOFF2                                                        
         MVC   SAVENRBX,BOXCOLS    SAVE REDUCED BOXES                           
         SPACE 1                                                                
         L     R1,GLALINE          ADD ACTIVE LINES INTRO RE                    
         ZIC   RE,0(R1)                                                         
         L     R2,AMYP1                                                         
         LA    R0,20                                                            
         SPACE 1                                                                
RBOFF4   CLC   0(198,R2),SPACES                                                 
         BE    *+8                                                              
         LA    RE,1(RE)                                                         
         LA    R2,198(R2)                                                       
         BCT   R0,RBOFF4                                                        
         SPACE 1                                                                
         LA    RE,3(RE)            MAKE SURE THERE IS ENOUGH ROOM               
         L     R1,GLALMAX                                                       
         ZIC   RF,0(R1)                                                         
         CR    RE,RF                  FOR TOTALS TO PRINT                       
         BL    RBOFF6                                                           
         L     R1,GLAFHEAD                                                      
         MVI   0(R1),C'Y'          NO - SO FORCE HEADLINES                      
         MVI   HORIZSW,0                                                        
         LA    R1,SAVENRBX                                                      
         ST    R1,BOXALTCL         PASS ALT MID COLUMNS TO (BOX) PRINT          
         B     XIT                                                              
         SPACE 1                                                                
RBOFF6   BAS   RE,PRHORIZ                                                       
         B     XIT                                                              
         EJECT                                                                  
*              SUPPORT ROW BOXES ON AND HORIZONTAL PRINTING                     
         SPACE 3                                                                
ROBOXON  NTR1                                                                   
         TM    GLNORBOX,X'40'                                                   
         BNO   XIT                                                              
         L     R6,ABOX                                                          
         USING BOXD,R6                                                          
         MVC   BOXCOLS(198),SAVEBXCL    RESTORE BOX COLUMNS                     
         MVC   SAVENRBX,SPACES                                                  
         MVI   HORIZSW,2                                                        
         MVC   LASTPDET,SPACES     FORCE NO SUPPRESSION OF LEADING DATA         
         B     XIT                                                              
         SPACE 1                                                                
*                                  PRINT A HORIZONTAL LINE                      
PRHORIZ  NTR1                                                                   
         L     R6,ABOX                                                          
         MVI   BOXREQ,C'B'                                                      
         USING BOXD,R6                                                          
         CLC   SAVENRBX,SPACES                                                  
         BNE   *+10                                                             
         MVC   BOXCOLS(198),SAVEBXCL    RESTORE BOX COLUMNS                     
         BAS   RE,GOPRINT                                                       
         MVI   HORIZSW,0                                                        
         B     XIT                                                              
         DROP  R6                                                               
         EJECT                                                                  
*              ROUTINE TO PUT IN 'ALL' AUTOMATICALLY                            
         SPACE 3                                                                
AUTOALL  NTR1                                                                   
         CLI   AUTALLSW,C'Y'       NOT IF USER HAD LITERAL OR ROUTINE           
         BNE   XIT                                                              
         SPACE 1                                                                
AUTALL2  ZIC   R1,1(R3)            LOOK FOR NEXT OUT ELEMENT                    
         AR    R3,R1                                                            
         CLI   0(R3),0                                                          
         BE    XIT                                                              
         CLI   0(R3),X'30'                                                      
         BNE   AUTALL2                                                          
         USING DROD,R3                                                          
         GOTO1 =V(DRIVTEST),DMCB,(RA),DROIFS                                    
         BNE   AUTALL2                                                          
         CLI   DROLTYP,C'P'                                                     
         BNE   AUTALL2                                                          
         CLI   DROLEN,3                                                         
         BL    XIT                 NOT ROOM FOR ANYTHING                        
         L     R4,DROAPOS                                                       
         LTR   R4,R4                                                            
         BZ    AUTALL2                                                          
*******  MVC   0(3,R4),=C'ALL'     ROOM FOR 'ALL'                               
         MVC   0(3,R4),LOCALALL    ROOM FOR 'ALL'                               
         CLI   DROLEN,5                                                         
         BL    *+16                                                             
         MVC   0(5,R4),=C'*ALL*'   ROOM FOR '*ALL*'                             
         MVC   1(3,R4),LOCALALL                                                 
         B     XIT                                                              
         EJECT                                                                  
*              SET ADDRESS OF OUT AT SPECIFIED LEVEL                            
         SPACE 3                                                                
SETAOUT  NTR1                                                                   
         L     R2,GLATHID                                                       
         USING GLINTD,R2                                                        
         L     R3,GLAFOUT                                                       
         USING DROD,R3                                                          
         SPACE 1                                                                
SAO2     CLI   0(R3),0             NO GOOD IF 0 OR X'10'                        
         BE    SAOMISS                                                          
         CLI   0(R3),X'10'                                                      
         BE    SAOMISS                                                          
         CLI   0(R3),X'30'         WE NEED AN X'30' AT REQUIRED LEVEL           
         BNE   SAONEXT                                                          
         CLC   DROLEV,GLLEVEL                                                   
         BNE   SAONEXT                                                          
         ST    R3,ALEVOUT          FOUND                                        
         B     YES                                                              
         SPACE 1                                                                
SAONEXT  ZIC   R1,1(R3)                                                         
         AR    R3,R1                                                            
         B     SAO2                                                             
         SPACE 1                                                                
SAOMISS  XC    ALEVOUT,ALEVOUT                                                  
         B     NO                                                               
         EJECT                                                                  
*              OVERALL CONTROL OF DETAIL AND TOTALS                             
         SPACE 3                                                                
DETAIL   NTR1                                                                   
         NI    GLINDS,(X'FF'-GLTOTLIN)                                          
         BAS   RE,BUFFPO4                                                       
         MVI   LINETYPE,C'D'                                                    
         TM    GLDOWNLD,GLDLACTV                                                
         BNO   DET2                                                             
         L     R2,GLATHID          DOWNLOAD                                     
         USING GLINTD,R2                                                        
         ZIC   R1,GLDETLEV                                                      
         BAS   RE,TOTCON                                                        
         GOTO1 =V(DRIVDOWN)                                                     
         MVI   LINETYPE,0                                                       
         CLI   GLHOOK,GLBCKOUT     IF BACKING DETAILS OUT OF TOTALS             
         BNE   *+16                                                             
         L     R5,GLAIO            POINT TO INPUT RECORD                        
         BAS   RE,BACKOUT          BACKOUT DETAILS                              
         MVI   GLHOOK,GLDONT                                                    
*                                                                               
         B     XIT                                                              
         SPACE 1                                                                
TOTALS   NTR1                                                                   
         XC    CUMEBIN,CUMEBIN     CLEAR CUME COUNTERS                          
         ZAP   CUMEPACK,=P'0'                                                   
         MVI   LINETYPE,C'T'                                                    
         OI    GLINDS,GLTOTLIN                                                  
         TM    GLDOWNLD,GLDLACTV                                                
         BZ    *+12                                                             
         TM    GLDOWNLD,GLDLTOTS                                                
         BZ    DET3B                                                            
         ZIC   R5,GLLEVEL                                                       
         SLL   R5,2                                                             
         LA    R5,GLARECL0(R5)                                                  
         TM    0(R5),X'80'         CHECK A RECORD IS THERE                      
         BZ    DET2                                                             
         L     R5,0(R5)                                                         
         CLC   GLRECNO,0(R5)       MAKE SURE IT'S THE RIGHT RECORD              
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   GLLEVEL,1(R5)       AND LEVEL NUMBER                             
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 =V(DRIVICMP),DMCB,(RA)   CONTROL INTERNAL COMPUTES               
         BAS   RE,COMPREC          CHECK COMPUTES FOR RECORD                    
         SPACE 1                                                                
DET2     NI    GLINDS,(X'FF'-GLDETFTS)   RESET FIRST TIME SWITCH                
         L     R2,GLATHID                                                       
         USING GLINTD,R2                                                        
         L     R3,GLARECEL         PICK UP ADDRESS OF RECORD ELEMENT            
         USING DRRECD,R3                                                        
         MVC   GLRECLAB,DRRECLAB        AND SET RECORD LABEL                    
         L     R3,GLAFOUT          PICK UP ADDRESS OF FIRST OUT                 
         USING DROD,R3                                                          
         ZIC   R1,GLLEVEL                                                       
         CLI   LINETYPE,C'D'                                                    
         BNE   *+8                                                              
         IC    R1,GLDETLEV                                                      
         ZIC   R0,GLMIDLEV                                                      
         CR    R1,R0               IF THIS LEVEL IS BELOW MID LEVEL             
         BH    DET3                                                             
         L     RF,AMYM1            CLEAR MID-LINES                              
         MVC   0(198,RF),SPACES                                                 
         MVC   198(198,RF),SPACES                                               
******** MVC   LASTMID,SPACES      THIS CAUSED EXTRA MIDLINES TO                
*                                  PRINT BEFORE TOTALS SO I NOOPED              
         SPACE 1                                                                
DET3     LR    RF,R1                                                            
         SLL   RF,2                                                             
         LA    RF,GLARECL0(RF)     RF=A(ADDRESS OF RECORD AT THIS LEV)          
         BAS   RE,TOTCON           CHECK FOR UNNECESSARY TOTALS                 
         BE    *+8                                                              
         NI    0(RF),X'7F'         YES-SUPPRESS TOTAL                           
         TM    0(RF),X'80'         TEST RECORD EXISTS AT THIS LEVEL             
         BO    DET3A                                                            
         TM    GLINDS3,GLLSTLIN    NO-OK IF FORMATTING A 'LAST' LINE            
         BO    DET3A                                                            
         MVI   TOTPEND,C'N'        ELSE SUPPRESS TOTAL                          
         MVI   LINETYPE,0                                                       
         B     XIT                                                              
         SPACE 1                                                                
DET3A    NI    0(RF),X'7F'         YES-MAKE SURE IT DOESN'T IN FUTURE           
         CLI   LINETYPE,C'T'                                                    
         BNE   DET3C                                                            
         TM    GLDOWNLD,GLDLACTV+GLDLTOTS   TEST DOWNLOADING TOTALS             
         BNO   DET3C                                                            
         GOTO1 =V(DRIVDOWN)        YES                                          
*                                                                               
DET3B    MVI   TOTPEND,C'N'        NO TOTAL PENDING                             
         MVI   LINETYPE,0                                                       
         B     XIT                                                              
*                                                                               
DET3C    L     RF,0(RF)            A(RECORD AT THIS LEVEL)                      
         ST    RF,ATHISREC                                                      
         SPACE 1                                                                
*                                  CHECK THIS OUT FOR CONDITIONALS              
DET4     GOTO1 =V(DRIVTEST),DMCB,(RA),DROIFS                                    
         BNE   DETEND                                                           
         CLI   GLDETHED,C'Y'       UNLESS USER ASKED SPECIALLY                  
         BE    DET5                                                             
         CLI   DROLTYP,C'H'        DON'T BOTHER WITH HEADLINES HERE             
         BE    DETEND                                                           
         SPACE 1                                                                
DET5     CLI   LINETYPE,C'T'       TEST TOTAL LINE                              
         BNE   *+12                                                             
         TM    DROOPTS,X'40'       AND COLUMN ASKS US NOT TO TOTAL              
         BO    DETEND              YES-SKIP THIS COLUMN                         
*                                                                               
         CLI   LINETYPE,C'D'       TEST DETAIL LINE                             
         BNE   *+12                                                             
         TM    DROOPTS,X'20'       AND COLUMN ASKS US FOR TOTALS ONLY           
         BO    DETEND              YES-SKIP THIS COLUMN                         
*                                                                               
         ST    R3,GLADTENT                                                      
         L     R5,DROAPOS          R5=A(OUTPUT)                                 
         CLI   DROELEN,140         DO WE NEED TO HANDLE A LITERAL               
         BE    DET6                                                             
         ZIC   R1,DROELEN          YES                                          
         SH    R1,=H'140'                                                       
         STC   R1,LITLEN           PASS ITS LENGTH                              
         LA    R1,DROLIT                                                        
         ST    R1,ALITIN           ITS ADDRESS                                  
         ST    R5,ALITOUT          AND WHERE IT GOES                            
         BAS   RE,DOLIT                                                         
         L     R5,ALITOUT          R5=A(NEXT SPACE)                             
         LA    R5,1(R5)            LEAVE A BLANK                                
         SPACE 1                                                                
DET6     ST    R5,GLAOFLD                                                       
         XC    GLAIFLD,GLAIFLD                                                  
         L     R1,DROIADD                                                       
         LTR   R1,R1                                                            
         BZ    DET8                                                             
         USING DRIND,R1                                                         
         ZIC   RE,DRINLEN                                                       
         LH    RF,DRINDISP                                                      
         A     RF,ATHISREC                                                      
         ST    RF,GLAIFLD                                                       
         CLI   DRINTYPE+1,C'+'     SKIP SIGNIFICANCE TESTS                      
         BE    DET8                FOR ADDITIVE FIELDS                          
         CLC   DRINLEV,GLDETLEV    AND FOR ALL COLUMNS BY OPTION                
         BNH   *+12                                                             
         TM    GLINDS3,GLSKPSIG                                                 
         BO    DET8                                                             
         DROP  R1                                                               
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         OC    0(0,RF),0(RF)       ANY SIGNIFICANT DATA?                        
         BZ    DETEND              NO SO NO NEED TO PROCESS                     
*                                                                               
         TM    GLTOTIND,GLTIDET    TEST ANY DETAILED TOTALS                     
         BZ    DET8                NO                                           
         CLC   DROLEV,GLDETLEV     X'FF' ALLOWED FOR DETAIL LEVEL               
         BNL   DET8                                                             
         ZIC   R4,DROLEV                                                        
         LA    R4,DETTOTS-1(R4)                                                 
         CLI   0(RF),X'FF'         TEST FOR X'FF'                               
         BNE   DET6A                                                            
         BCTR  RE,0                                                             
         LTR   RE,RE                                                            
         BM    DET6B                                                            
         EX    RE,*+8                                                           
         B     *+10                                                             
         CLC   1(0,RF),0(RF)                                                    
         BE    DET6B                                                            
         SPACE 1                                                                
DET6A    MVI   0(R4),0             INDICATE NOT DETAIL TOTAL                    
         B     DET8                                                             
         SPACE 1                                                                
DET6B    TM    GLINDS2,GLPBREAK    YES-IF PAGE BREAK REQUIRED,                  
         BZ    DET6C                                                            
         OC    DETTOTS,DETTOTS     THEN FORCE PAGE BREAK IF LAST                
         BNZ   DET6C               TIME WAS NOT A DETAIL TOTAL                  
         L     RE,GLAFHEAD                                                      
         MVI   0(RE),C'Y'                                                       
         SPACE 1                                                                
DET6C    MVI   0(R4),X'FF'         INDICATE DETAIL TOT AT THIS LEVEL            
         LTR   R5,R5               TEST PRINT=NO                                
         BZ    DETEND                                                           
         ZIC   RE,DROLEN                                                        
         BCTR  RE,0                                                             
         EX    RE,*+8              CLEAR THE OUTPUT FIELD                       
         B     *+10                                                             
         MVC   0(0,R5),SPACES                                                   
         CLI   DROLTYP,C'M'        IS IT A MIDLINE                              
         BNE   DET7                                                             
         EX    RE,*+8              YES - CLEAR THE UNDERLINE                    
         B     DET7                                                             
         MVC   198(0,R5),SPACES                                                 
*                                                                               
DET7     CLI   DROLEN,3            PUT 'ALL' IN OUTPUT FIELD                    
         BL    DETEND                                                           
         MVC   0(3,R5),=C'ALL'                                                  
         CLI   DROLEN,5                                                         
         BL    DETEND                                                           
         MVC   0(5,R5),=C'*ALL*'                                                
         B     DET12                                                            
         DROP  R2                                                               
         SPACE 1                                                                
DET8     OC    DRORADD,DRORADD     IS THERE A USER ROUTINE FOR THIS             
         BZ    DET10                                                            
         MVI   GLHOOK,GLROUT       YES SO SET UP FOR A HOOK                     
         MVC   GLLABEL,DROROUT                                                  
         MVC   GLAROUT,DRORADD                                                  
         MVC   GLARGS,DROARGS                                                   
         OC    DROACOMP,DROACOMP   DO WE NEED TO COMPUTE THIS FIELD             
         BZ    DET9                                                             
         MVC   DMCB+4(4),DROACOMP                                               
         MVC   DMCB+8(4),ATHISREC                                               
         GOTO1 =V(DRIVCOMP),DMCB,(RA)                                           
         TM    DROOPTS,X'10'       IF ABSOLUTE VALUE                            
         BNO   DET8A                                                            
         CP    DUB,=P'0'           AND IF NEGATIVE NUMBER                       
         BNL   DET8A                                                            
         MP    DUB,=P'-1'          MAKE POSITIVE                                
DET8A    DS    0H                                                               
         MVI   GLHOOK,GLROUT       RESET THIS - CAN CHANGE IN COMP              
         LA    R1,DUB              NOW HAVE RESULT IN DUB                       
         ST    R1,GLAIFLD          OFFER THE ADDRESS OF THIS TO USER            
         SPACE 1                                                                
DET9     BAS   RE,GOHOOK                                                        
         CLI   GLHOOK,GLEDIT       DID USER WANT US TO EDIT                     
         BE    DET10                                                            
         GOTO1 =V(DRIVALIN)                                                     
         B     DET12                                                            
         SPACE 1                                                                
DET10    GOTO1 =V(DRIVFORM)        OTHERWISE DRIVER WILL DEAL WITH IT           
         SPACE 1                                                                
DET12    DS    0H                                                               
         CLI   DROLTYP,C'M'        TEST MIDLINE                                 
         BNE   DETEND                                                           
         L     R1,AMYM1            YES-COMPARE TO LAST MIDLINE                  
         CLC   LASTMID,0(R1)                                                    
         BE    DETEND                                                           
         MVC   LASTPDET,SPACES     FORCE NO SUPPRESSION OF LEADING DATA         
         SPACE 1                                                                
DETEND   ZIC   R1,1(R3)                                                         
         AR    R3,R1                                                            
         CLI   0(R3),X'30'                                                      
         BE    DET4                                                             
         CLI   0(R3),0                                                          
         BE    DETEND2                                                          
         CLI   0(R3),X'10'                                                      
         BNE   DETEND                                                           
         SPACE 1                                                                
DETEND2  MVC   NLINES,GLSPACE                                                   
         TM    GLINDS2,X'80'       OPTION NOT TO SUPPRESS                       
         BNO   *+10                                                             
         MVC   LASTPDET,SPACES     FORCE NO SUPPRESSION OF LEADING DATA         
         BAS   RE,SUPPRESS         GO AND CHECK FOR SUPPRESSION                 
         CLI   LINETYPE,C'T'       TOTALS WILL PRINT AFTER WE HAVE              
         BE    XIT                 PROCESSED ANY TOTAL LITERALS ETC             
         BAS   RE,SPLAT                                                         
         MVI   LINETYPE,0                                                       
         B     XIT                                                              
         EJECT                                                                  
*              CHECK FOR UNNECESSARY TOTALS                                     
         SPACE 3                                                                
TOTCON   NTR1                                                                   
         STC   R1,TOTCONLV         R1=LEVEL NUMBER                              
         MVI   TOTPEND,C'Y'                                                     
         LTR   R1,R1               ALWAYS ALLOW LEVEL 0                         
         BZ    YES                                                              
         USING GLINTD,R2                                                        
         ZIC   R0,GLDETLEV         R0=N'LEVELS TO CHECK                         
         DROP  R2                                                               
         SR    R0,R1                                                            
         SLL   R1,1                                                             
         LA    R1,TOTCNTS(R1)      ADDRESS TO LEVEL COUNTER                     
         LH    RE,0(R1)            AND BUMP BY 1                                
         LA    RE,1(RE)                                                         
         STH   RE,0(R1)                                                         
         CLI   LINETYPE,C'D'       ALWAYS DO DETAILS                            
         BE    YES                                                              
         SPACE 1                                                                
TOTCNT2  LA    R1,2(R1)            FIND COUNT FOR NEXT LOWEST LEVEL             
         OC    0(2,R1),0(R1)                                                    
         BNZ   TOTCNT4                                                          
         BCT   R0,TOTCNT2                                                       
         B     TOTCNT6                                                          
         SPACE 1                                                                
TOTCNT4  LH    RE,0(R1)                                                         
         XC    0(2,R1),0(R1)                                                    
         CH    RE,=H'1'            DON'T HANDLE IF ONE OR LESS                  
         BH    YES                                                              
         SPACE 1                                                                
TOTCNT6  TM    GLINDS,GLPALTOT     OPTION TO GET ALL TOTALS                     
         BO    YES                                                              
         ZIC   R1,TOTCONLV         (CLEAR OUTCOUNT IN DRIVROUT)                 
         SLL   R1,2                                                             
         A     R1,=V(OUTCACCS)                                                  
         XC    0(4,R1),0(R1)                                                    
         B     NO                                                               
         SPACE 1                                                                
TOTCONLV DS    XL1                                                              
         EJECT                                                                  
*              POSSIBLE SUPPRESSION OF LEADING DETAIL DATA                      
         SPACE 3                                                                
SUPPRESS NTR1                                                                   
         USING GLINTD,R2                                                        
         L     R3,AMYP1                                                         
         MVI   CONTSW,C'N'                                                      
         MVC   SAVEDET,0(R3)                                                    
         MVC   SAVEP2,198(R3)                                                   
         MVC   SAVEP3,198*2(R3)   LINE 3                                        
         MVC   SAVEP4,198*3(R3)   LINE 4                                        
         LA    R4,GLPDCBS                                                       
         LA    R0,GLNUMLEV                                                      
         SPACE 1                                                                
SUP2     CLI   0(R4),0                                                          
         BE    SUP4                                                             
         ZIC   R1,0(R4)            PICK UP LENGTH OF COMPOSITE DISP.            
         BCTR  R1,0                FOR THIS LEVEL                               
         AH    R1,GLPDISP                                                       
         EX    R1,SUPCOMP              IS DATA THE SAME?                        
         BNE   SUPEND                                                           
         EX    R1,SUPCOMP2                                                      
         BNE   SUPEND                                                           
         EX    R1,SUPCOMP3                                                      
         BNE   SUPEND                                                           
         EX    R1,SUPCOMP4                                                      
         BNE   SUPEND                                                           
         EX    R1,SUPSPACE             AND NOT SPACES?                          
         BE    SUPEND                                                           
*****    EX    R1,SUPCLEAR         THEN CLEAR ON THIS LINE                      
*****    EX    R1,NXTCLEAR              AND, MAYBE ON NEXT                      
*****    EX    R1,NXTCLEA3                                                      
*****    EX    R1,NXTCLEA4                                                      
*** CLEAR ALL 20 PRINT LINES IF FIRST FOUR DON'T MATCH                          
*** (USED TO ONLY CLEAR 1ST 4)                                                  
         LR    RE,R3                                                            
         LHI   RF,20               20 PRINT LINES                               
*                                                                               
SUP3     EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RE),SPACES                                                   
         LA    RE,198(RE)                                                       
         BCT   RF,SUP3                                                          
*                                                                               
         MVI   CONTSW,C'S'         AND SET CONTINUATION SWITCH                  
*                                  S=SUSPENSE UNTIL ACTUAL PRINT                
         SPACE 1                                                                
SUP4     LA    R4,1(R4)                                                         
         BCT   R0,SUP2                                                          
         SPACE 1                                                                
SUPEND   DS    0H                                                               
         B     XIT                                                              
         SPACE 1                                                                
         DROP  R2                                                               
         SPACE 1                                                                
SUPCOMP  CLC   SAVEDET(0),LASTPDET                                              
SUPCOMP2 CLC   SAVEP2(0),LASTP2                                                 
SUPCOMP3 CLC   SAVEP3(0),LASTP3                                                 
SUPCOMP4 CLC   SAVEP4(0),LASTP4                                                 
SUPSPACE CLC   SAVEDET(0),SPACES                                                
SUPCLEAR MVC   0(0,R3),SPACES                                                   
NXTCLEAR MVC   198(0,R3),SPACES                                                 
NXTCLEA3 MVC   198*2(0,R3),SPACES                                               
NXTCLEA4 MVC   198*3(0,R3),SPACES                                               
         EJECT                                                                  
*              ROUTINE TO CONTROL THE PRINTING OF DETAIL LINES                  
         SPACE 3                                                                
SPLAT    NTR1                                                                   
         L     R6,GLATHID                                                       
         USING GLINTD,R6                                                        
         TM    GLDOWNLD,GLDLACTV                                                
         BO    XIT                 DON'T WRITE IF DOWNLOADING                   
         MVI   GLAROUT,1           HOOK TO APPLICATION TO SAY                   
         MVI   GLHOOK,GLPRINT      WE'RE ABOUT TO PRINT                         
         BAS   RE,GOHOOK                                                        
         MVI   DONTSW,C'N'                                                      
         CLI   GLHOOK,GLDONT       APPLIC. CAN THEN REJECT                      
         BE    *+8                                                              
         CLI   GLHOOK,GLBCKOUT     APPLIC. CAN THEN REJECT                      
         BNE   SPLAT4                                                           
*                                                                               
         MVI   DONTSW,C'Y'                                                      
         L     R2,AMYP1                                                         
         LA    R0,20                                                            
*                                                                               
SPLAT2   MVC   0(198,R2),SPACES    SO WE'LL CLEAR PRINT LINES                   
         LA    R2,198(R2)                                                       
         BCT   R0,SPLAT2                                                        
*                                                                               
         CLI   GLHOOK,GLBCKOUT     IF BACKING DETAILS OUT OF TOTALS             
         BNE   SPLAT3                                                           
*                                                                               
         L     R5,GLAIO               POINT TO  INPUT RECORD                    
         BAS   RE,BACKOUT             PERFORM BACKOUT                           
         MVI   GLHOOK,GLDONT          REVERT TO GLDONT FOR SAFETY               
*                                                                               
SPLAT3   DS    0H                                                               
         B     XIT                                                              
         SPACE 1                                                                
SPLAT4   MVC   LASTPDET,SAVEDET    ACTUALLY PRINTING SO SET                     
         MVC   LASTP2,SAVEP2       CONTINUATION DETAILS                         
         MVC   LASTP3,SAVEP3                                                    
         MVC   LASTP4,SAVEP4                                                    
         CLI   CONTSW,C'S'                                                      
         BNE   *+8                                                              
         MVI   CONTSW,C'Y'                                                      
         BAS   RE,TESTMID          SEE IF WE NEED TO HANDLE MIDS                
         BAS   RE,TESTHEAD         SEE IF WE ARE GOING TO DO HEADS              
         CLI   TOPSW,C'Y'          IF WE ARE ABOUT TO DO HEADLINES              
         BNE   *+8                                                              
         BAS   RE,TESTMID             FIGURE OUT MIDLINES AGAIN                 
         L     R3,GLASPACE                                                      
         L     R5,GLAP1                                                         
         BAS   RE,SPLMIDS          PRINT MIDS IF NEEDED                         
         CLI   HORIZSW,0           IF A HORIZONTAL LINE IS PENDING              
         BE    *+8                                                              
         BAS   RE,PRHORIZ          PRINT A HORIZONAL LINE                       
         L     R2,AMYP1                                                         
         LA    R0,20                                                            
         CLC   0(198,R2),SPACES    TEST FIRST LINE IS BLANK                     
         BNE   SPLAT7                                                           
         CLI   TOPSW,C'Y'          YES-TEST TOP OF PAGE                         
         BE    SPLAT8                                                           
         B     SPLAT7              ALWAYS PRINT LINE 1                          
         SPACE 1                                                                
SPLAT6   CLC   0(198,R2),SPACES    NOW RELEASE THE PRINT LINES                  
         BE    SPLAT8                                                           
         SPACE 1                                                                
SPLAT7   MVC   0(198,R5),0(R2)     (SUPPORT WIDE AS WELL)                       
         CLI   TOPSW,C'Y'          IF WE ARE AT 'TOP' OF PAGE                   
         BNE   *+8                                                              
         BAS   RE,CONTIN           CHECK FOR CONTINUATION                       
         MVI   TOPSW,C'N'                                                       
         MVC   0(198,R2),SPACES    CLEAR MY BUFFER                              
         MVI   0(R3),1             SINGLE SPACE                                 
         BAS   RE,GOPRINT                                                       
         MVC   0(198,R5),SPACES    CLEAR USERS PRINT LINES                      
         MVC   198(198,R5),SPACES                                               
         SPACE 1                                                                
SPLAT8   LA    R2,198(R2)                                                       
         BCT   R0,SPLAT6                                                        
         CLI   NLINES,1            ANY EXTRA LINES NEEDED?                      
         BE    XIT                                                              
         CLI   TOPSW,C'Y'          YES-TEST STILL AT TOP OF PAGE                
         BE    XIT                     YES-THEN DON'T BOTHER                    
         IC    R0,NLINES                                                        
         BCTR  R0,0                                                             
         STC   R0,0(R3)                                                         
         BAS   RE,GOPRINT                                                       
         B     XIT                                                              
         DROP  R6                                                               
         EJECT                                                                  
*              TEST IF WE ARE ABOUT TO DO HEADLINES                             
         SPACE 3                                                                
*              OUTPUT              FORCEHED SET TO Y IF NEEDED                  
*                                  TOPSW RETURNED WITH THIS VALUE               
         SPACE 1                                                                
TESTHEAD NTR1                                                                   
         L     R2,AMYP1                                                         
         SR    R1,R1               FIRST COUNT ACTIVE LINES IN R1               
         LA    R0,20                                                            
         SPACE 1                                                                
THEAD2   CLC   0(198,R2),SPACES                                                 
         BE    *+8                                                              
         LA    R1,1(R1)                                                         
         LA    R2,198(R2)                                                       
         BCT   R0,THEAD2                                                        
         SPACE 1                                                                
         CLI   NUMMIDS,0           TEST ANY MIDLINES                            
         BE    THEAD4                                                           
         CH    R1,=H'4'            YES - MAKE SURE AT LEAST ROOM FOR            
         BNL   THEAD4                    4 PRINT LINES                          
         LA    R1,4                                                             
         SPACE 1                                                                
THEAD4   CLI   NLINES,0                                                         
         BNE   *+8                                                              
         MVI   NLINES,1                                                         
         IC    R0,NLINES                                                        
         AR    R1,R0               THEN ADD IN SPACING LINES                    
         IC    R0,NUMMIDS                                                       
         LTR   R0,R0               IF ANY MIDLINES                              
         BZ    *+8                                                              
         AH    R0,=H'2'            NEED SPACE BEFORE AND AFTER                  
         AR    R1,R0               AND ADD IN MID LINES                         
         L     R2,GLALINE                                                       
         IC    R0,0(R2)            AND ADD IN PRESENT LINE NUMBER               
         AR    R1,R0                                                            
         LA    R1,2(R1)            +2 FOR LUCK - HAD TROUBLE BEFORE             
         L     R2,GLALMAX                                                       
         IC    R0,0(R2)            R0=MAXLINES                                  
         CH    R0,=H'58'                                                        
         BH    *+8                                                              
         LA    R0,58                                                            
         CR    R1,R0               WILL THEY FIT?                               
         BNH   THEAD6                                                           
         L     R2,GLAFHEAD         NO SO FORCE HEADLINES ON FIRST PRINT         
         MVI   0(R2),C'Y'                                                       
         MVI   HORIZSW,0           AND STOP PRINTING HORIZ                      
         SPACE 1                                                                
THEAD6   L     R2,GLAFHEAD                                                      
         MVC   TOPSW,0(R2)         RETURN TOPSW FOR CONVENIENCE                 
         B     XIT                                                              
         EJECT                                                                  
*              TEST TO SEE IF MIDLINES REQUIRED                                 
         SPACE 3                                                                
*              OUTPUT              NUMMIDS SET TO 0 OR 2                        
*                                  MIDCONT SET TO C'Y' OR C'N'                  
         SPACE 1                                                                
TESTMID  NTR1                                                                   
         MVI   NUMMIDS,0           FIGURE OUT MIDLINES                          
         L     R2,AMYM1            SEE IF WE NEED TO HANDLE MIDS                
         CLC   0(198,R2),SPACES                                                 
         BE    XIT                 NO - NOTHING IN MID 1                        
         CLI   TOPSW,C'Y'                                                       
         BE    TESTMID2                                                         
         MVI   MIDCONT,C'Y'        YES - SET MIDLINE CONTINUED SWITCH           
         CLC   LASTMID,0(R2)       COMPARE TO LAST MIDLINE                      
         BE    XIT                                                              
         MVI   MIDCONT,C'N'        NOT EQUAL - NOT MIDLINE CONTD                
         SPACE 1                                                                
TESTMID2 MVC   LASTMID,0(R2)                                                    
         MVI   NUMMIDS,2           ALWAYS MIDS=2                                
         MVI   HORIZSW,0           CANCEL HORIZ PENDING IF DOING MIDS           
         B     XIT                                                              
         EJECT                                                                  
*              DEAL WITH MID LINE PRINTING                                      
         SPACE 3                                                                
SPLMIDS  NTR1                                                                   
         CLI   NUMMIDS,0                                                        
         BE    XIT                                                              
         L     R5,GLAP1            R5=A(PRINT LINE)                             
         L     R2,AMYM1                                                         
         MVC   THISMID,0(R2)       SAVE CURRENT MID1                            
         BAS   RE,SETMID                                                        
         L     R6,ABOX                                                          
         USING BOXD,R6                                                          
         CLI   TOPSW,C'Y'          ARE WE ABOUT TO DO HEADLINES?                
         BE    SPLMID4             NO, SO......                                 
         MVI   BOXREQ,C'C'         CLOSE EXISTING BOX                           
         MVC   0(198,R5),SPACES                                                 
         BAS   RE,GOPRINT                                                       
         MVC   0(198,R5),0(R2)     PRINT MID1                                   
         BAS   RE,GOPRINT                                                       
         MVC   0(198,R5),198(R2)   PRINT MID2                                   
         BAS   RE,GOPRINT                                                       
         MVI   BOXREQ,C'O'         OPEN NEW BOX                                 
         TM    GLINDS3,GLNSPAM     SUPPRESS SPACE AFTER MIDS?                   
         BNZ   *+14                YES                                          
         MVC   0(198,R5),SPACES    ELSE SPACE AFTER                             
         BAS   RE,GOPRINT                                                       
         MVC   0(198,R2),THISMID   RESTORE USERS MID1                           
         B     XIT                                                              
         SPACE 1                                                                
*                                  DOING HEADINGS - THINGS EASIER               
SPLMID4  MVC   0(198,R5),0(R2)     PRINT MID1                                   
         BAS   RE,GOPRINT                                                       
         MVC   0(198,R5),198(R2)   PRINT MID2                                   
         BAS   RE,GOPRINT                                                       
         CLC   SAVENRBX,SPACES     MAY NEED TO RESTORE REDUCED BOXES            
         BE    *+10                                                             
         MVC   BOXCOLS(198),SAVENRBX                                            
         TM    GLINDS3,GLNSPAM     SUPPRESS SPACE AFTER MIDS?                   
         BNZ   *+14                YES                                          
         MVC   0(198,R5),SPACES    ELSE SPACE AFTER                             
         BAS   RE,GOPRINT                                                       
         MVC   0(198,R2),THISMID   RESTORE USERS MID1                           
         B     XIT                                                              
         DROP  R6                                                               
         EJECT                                                                  
*              SET UP MIDLINES IF NEEDED                                        
*              TACK ON DATA, AND/OR "CONTINUED" AND UNDERLINE                   
         SPACE 3                                                                
SETMID   NTR1                                                                   
         L     R6,GLATHID                                                       
         USING GLINTD,R6                                                        
         L     R2,AMYM1                                                         
         CLC   GLMIDXTR,SPACES     ANY EXTRA DATA TO TACK ON?                   
         BE    SETMID4                                                          
         LA    R1,197(R2)          FIND END OF MID 1                            
         CLI   0(R1),C' '          TACK IT TO END OF MID                        
         BH    *+10                                                             
         BCTR  R1,0                                                             
         B     *-10                                                             
         MVC   3(L'GLMIDXTR,R1),GLMIDXTR                                        
         MVC   GLMIDXTR,SPACES     CLEAR EXTRA MIDLINE DATA                     
         SPACE 1                                                                
SETMID4  CLI   MIDCONT,C'Y'        IS CONTINUATION NEEDED ?                     
         BNE   SETMID6                                                          
         MVI   MIDCONT,C'N'                                                     
         LA    R1,197(R2)          YES - SO FIND END OF MID 1                   
         CLI   0(R1),C' '                                                       
         BH    *+10                                                             
         BCTR  R1,0                                                             
         B     *-10                                                             
*******  MVC   3(11,R1),=C'(CONTINUED)'                                         
         MVC   3(16,R1),LOCALCNT   (NOW LANGUAGE DEPENDANT)                     
         SPACE 1                                                                
SETMID6  LH    RE,GLPDISP          NOW CHECK FOR UNDERLINING                    
         LA    RE,1(RE,R2)                                                      
         ST    RE,DMCB                                                          
         MVI   DMCB,49                                                          
         LA    RE,198(RE)                                                       
         ST    RE,DMCB+4                                                        
*****    MVI   DMCB+4,X'BF'                                                     
         MVC   DMCB+4(1),GLUNDCHR                                               
         LA    R0,49                                                            
         SPACE 1                                                                
SETMID7  CLC   0(1,RE),GLUNDCHR    TEST LEFTOVER UNDERLINE ON MID2              
         BE    SETMID8             IF ANY LEFT, WE WILL CLEAR                   
         LA    RE,1(RE)                                                         
         BCT   R0,SETMID7                                                       
         CLC   198(198,R2),SPACES  OTHERWISE, IF REAL DATA ON LINE 2            
         BNE   SETMIDX             MAKE SURE WE DO NOT UNDERLINE                
         SPACE 1                                                                
SETMID8  MVC   198(198,R2),SPACES  CLEAR ANYTHING ON LINE 2                     
         GOTO1 UNDERLIN,DMCB       AND UNDERLINE                                
         SPACE 1                                                                
SETMIDX  B     XIT                                                              
         DROP  R6                                                               
         EJECT                                                                  
*              ROUTINES TO HANDLE CONTINUATION ON NEXT PAGE                     
         SPACE 3                                                                
CONTIN   NTR1                                                                   
         L     R2,GLATHID                                                       
         USING GLINTD,R2                                                        
         CLI   CONTSW,C'Y'         IS CONTINUATION RELEVANT?                    
         BNE   XIT                                                              
         MVI   CONTSW,C'N'                                                      
         CLI   LINETYPE,C'T'       NOT APPLICABLE FOR TOTAL LINES               
         BE    XIT                                                              
*        L     R1,WIDTH            YES                                          
*        BCTR  R1,0                                                             
*        L     RE,GLAP1                                                         
*        EX    R1,*+8                                                           
*        B     *+10                                                             
*        OC    0(0,RE),LASTPDET    MAKE SURE WE GET IT ALL                      
*                                                                               
*        COPY NON-BLANK DATA FROM LAST PRINTED DETAIL                           
*                                                                               
         L     R1,WIDTH            GET LINE LENGTH                              
         L     RE,GLAP1            POINT TO PRINT LINE                          
         LA    RF,LASTPDET         POINT TO LAST PRINTED DETAIL                 
*                                                                               
         CLI   0(RE),C' '          DON'T OVERWRITE SIGNIFICANT DATA             
         BH    *+18                                                             
         CLI   0(RF),C' '          ONLY COPY SIGNIFICANT DATA                   
         BNH   *+10                                                             
         MVC   0(1,RE),0(RF)       COPY DATA                                    
         LA    RE,1(RE)            BUMP POINTERS                                
         LA    RF,1(RF)                                                         
         BCT   R1,*-30                                                          
*                                                                               
         CLI   NUMMIDS,0           MIDLINES ALREADY DID CONTINUED               
         BNE   XIT                                                              
         SPACE                                                                  
         CLC   LASTP2,SPACES       IS THERE 2ND DETAIL LINE                     
         BE    CONTIN10                                                         
*        L     R4,WIDTH            YES/PUT IT IN P3                             
*        AR    RE,R4                                                            
*        AR    RE,R4                                                            
*        EX    R1,*+8                                                           
*        B     *+10                                                             
*        OC    0(0,RE),LASTP2                                                   
*                                                                               
*        COPY NON-BLANK DATA FROM LAST PRINTED SECOND DETAIL                    
*                                                                               
         L     R1,WIDTH            GET LINE LENGTH                              
         L     RE,GLAP1            POINT TO PRINT LINE                          
         AR    RE,R1               PUT SECOND DETAIL IN P3                      
         AR    RE,R1                                                            
         LA    RF,LASTP2           POINT TO LAST PRINTED SECOND DETAIL          
*                                                                               
         CLI   0(RE),C' '          DON'T OVERWRITE SIGNIFICANT DATA             
         BH    *+18                                                             
         CLI   0(RF),C' '          ONLY COPY SIGNIFICANT DATA                   
         BNH   *+10                                                             
         MVC   0(1,RE),0(RF)       COPY DATA                                    
         LA    RE,1(RE)            BUMP POINTERS                                
         LA    RF,1(RF)                                                         
         BCT   R1,*-30                                                          
*                                                                               
         L     RE,AMYP1            AND CLEAR IT FROM BUFFER                     
         MVC   198(198,RE),SPACES                                               
         SPACE                                                                  
CONTIN10 L     RE,GLAP1            MOVE P1 TO P2                                
         L     RF,WIDTH                                                         
         LR    R1,RF                                                            
         BCTR  R1,0                                                             
         AR    RF,RE                                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RF),0(RE)                                                    
         EX    R1,*+8              CLEAR P1                                     
         B     *+10                                                             
         MVC   0(0,RE),SPACES                                                   
         CLI   GLCNTWID,0                                                       
         BE    XIT                                                              
         ZIC   R0,GLCNTDSP         PICK UP CONTINUATION DISPLACEMENT            
         AR    RE,R0                                                            
         AH    RE,GLPDISP                                                       
         LA    RE,1(RE)                                                         
         MVC   0(4,RE),LOCALCNT+1  MOVE IN APPROPRIATE MESSAGE                  
         CLI   GLCNTWID,6                                                       
         BL    XIT                                                              
         MVC   0(5,RE),LOCALCNT    (CONT) OR (FORT)                             
         MVI   5(RE),C')'                                                       
         CLI   GLCNTWID,10                                                      
         BL    XIT                                                              
         MVC   0(9,RE),LOCALCNT+1  CONTINUED OR FORTSETZ                        
         CLI   GLCNTWID,12                                                      
         BL    XIT                                                              
         MVC   0(16,RE),LOCALCNT                                                
         B     XIT                                                              
         EJECT                                                                  
*              MISCELLANEOUS ROUTINES                                           
         SPACE 3                                                                
GOPRINT  NTR1                                                                   
         L     R6,ABOX                                                          
         USING BOXD,R6                                                          
         CLC   SAVENRBX,SPACES                                                  
         BNE   GOPRINT2                                                         
         CLC   BOXCOLS,SPACES                                                   
         BNE   GOPRINT2                                                         
         CLI   GLABCSW,0                                                        
         BNE   GOPRINT2                                                         
         MVC   BOXCOLS(198),SAVEBXCL    RESTORE BOX COLUMNS                     
         SPACE 1                                                                
GOPRINT2 L     R4,AREPWRKD         POINT TO REPORT WORKING STORAGE              
         ST    R4,DMCB                                                          
         LA    R1,DMCB                                                          
         CLI   AREPWRKD,C'R'       SKIP IF NOT FAREPORT                         
         BNE   *+6                 FOR SOME REASON, THIS PROGRAM                
         LR    R1,R4               EXPECTS R1 TO HAVE A(AREA)                   
*                                  NOT A(PARAMETER LIST)                        
         L     RF,REPORT                                                        
         GOTO1 (RF),(R1)                                                        
         B     XIT                                                              
         SPACE 3                                                                
*                                  WRAP UP IF DOWNLOADING                       
         SPACE 1                                                                
DOWNLAST NTR1                                                                   
         TM    GLDOWNLD,GLDLACTV                                                
         BNO   XIT                                                              
         MVI   DLFIRST,0           RESET AT END OF REPORT                       
         MVI   P,C':'              SEND COLON FOR REPORT END                    
*                                                                               
         ICM   R0,15,AOUTPDCB      OUTPUT TO FILE???                            
         BZ    DOWNL10              NO                                          
         PUT   (R0),P                                                           
         B     XIT                                                              
*                                                                               
DOWNL10  GOTO1 PRINT,DMCB,PFILL,=C'BL01'                                        
         B     XIT                                                              
         EJECT                                                                  
       ++INCLUDE DRIVOSHR                                                       
         EJECT                                                                  
*              BLOCK FOR EDITOR                                                 
         SPACE 3                                                                
       ++INCLUDE DDEBLOCK                                                       
         EJECT                                                                  
*              LTORG, RANK AREAS & BUFF                                         
         SPACE 1                                                                
         LTORG                                                                  
         SPACE 1                                                                
*                                                                               
OUTP     DCB   DDNAME=OUTP,                                            X        
               DSORG=PS,                                               X        
               LRECL=198,                                              X        
               BLKSIZE=0,                                              X        
               RECFM=FB,                                               X        
               MACRF=PM                                                         
*                                                                               
*                                  BUFFALO AREAS FOR RANKING                    
ABVAL    DS    PL8                                                              
ABCOUNT  DS    PL8                                                              
ABDATA   DS    984C                                                             
BBVAL    DS    PL8                                                              
BBCOUNT  DS    PL8                                                              
BBDATA   DS    984C                                                             
         SPACE 1                                                                
         BUFF  LINES=500,COMMENT=92,FLAVOR=DATA,KEYLIST=(16,A)                  
         SPACE 1                                                                
TEMPBUFF DC    F'0'                                                             
         DC    F'50000'                                                         
         DC    50000X'00'                                                       
         SPACE 1                                                                
         ENTRY TEMPREC                                                          
TEMPREC  DC    4096X'00'                                                        
         EJECT                                                                  
       ++INCLUDE DRIVOUTD                                                       
         SPACE 1                                                                
       ++INCLUDE DRGLOBAL                                                       
       ++INCLUDE DRLOCAL                                                        
       ++INCLUDE DRINTRECD2                                                     
       ++INCLUDE DRIVETABLE                                                     
         PRINT OFF                                                              
       ++INCLUDE DDBIGBOX                                                       
       ++INCLUDE DDBUFFALOD                                                     
       ++INCLUDE DDTWADCOND                                                     
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'108DRIVOCON  07/09/13'                                      
         END                                                                    
