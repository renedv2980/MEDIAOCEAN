*          DATA SET SPREPI2NU  AT LEVEL 137 AS OF 03/04/21                      
*CATALP SPI2NET                                                                 
         TITLE 'SPI2NETNU - I2 NETWORK UNIT RECORD HANDLER'                     
         PRINT NOGEN                                                            
***********************************************************************         
*                                                                               
*        PARM 1   BYTE 1    R=READ,W=WRITE                                      
*                 BYTE 0-3  FOR WRITES, A(BYELEMD)                              
*        PARM2    BYTE 0-3  FOR WRITES, A(IELEMD) OR A(0) IF NONE               
*                                                                               
*        NOTE - WRITE LOGIC ALSO EXTRACTS COMMENTS                              
***********************************************************************         
***********************************************************************         
* USER    JIRA       DATE                  CHANGE LOG                 *         
* ---- ----------  -------- ----------------------------------------- *         
* AKAT SPEC-46696  02/22/21 CARRY AND DISPLAY 4 BYTE DEMO VALUES      *         
* AKAT SPEC-48292  08/18/20 HONOR NEW STW ESTIMATES ON I2 FIELD ON I2N*         
* VGUP SPEC-37094  11/29/19 ADDED NEW MIRROR CODE Q AND R IN MIRTAB   *         
* AKAT SPEC-8274   02/28/17 "NOT CLEARED" TOTS TO MIRROR NPAY PAYABLES*         
***********************************************************************         
***********************************************************************         
* NAME   DATE   LEVEL                    COMMENTS                               
* ----   ----   -----   -----------------------------------------------         
* AKAT  AUG0312  131  - PASS STEWARDSHIP BLOCK TO NBASTWRD                      
* AKAT  FEB0111  130  - PATTERN SEQ/REF CONVERSION SUPPORT                      
* AKAT  JUN0210  129  - ONLY CENTERCUT MEANS ISCII CODE FOR X'24' ELEM          
*                     - DUAL MIRROR SUPPORT                                     
* AKAT  DEC0909  127  - BYLIN NOW 2 BYTES!                                      
* AKAT  APR1609  125  - ALWAYS SET AD-ID FLAG IF FOR HDEF OR CENTERCUT          
* AKAT  JUL2408  124  - TEST IF 2ND CMML IS AD-ID TO SET KEY CORRECTLY!         
***********************************************************************         
         SPACE 2                                                                
NETNU    CSECT                                                                  
         NMOD1 0,NETNU,RR=R2                                                    
         USING SPWORKD,RA,R9       SET BY SPI202                                
         LA    RC,SPACEND                                                       
         USING IMRWORK,RC                                                       
         ST    R2,RELO                                                          
         MVC   NETNUMOD,0(R1)                                                   
         MVC   PARMS,0(R1)                                                      
         LAY   R8,NETBLK                                                        
         A     R8,RELO                                                          
         USING NETBLOCK,R8                                                      
*                                                                               
         BC    0,NTU1              ONCE ONLY                                    
         OI    *-3,X'F0'                                                        
*                                                                               
***>     GOTO1 LOADER,DMCB,=C'T00A27  '    LOAD IN NETIO                        
***>     MVC   NETIO,DMCB+4                                                     
*                                                                               
         L     R4,ADBUY            USE AS WORK AREA FOR OPEN                    
         GOTO1 DATAMGR,DMCB,=C'OPEN',=C'SPOT',NUFLIST,(R4)                      
*                                                                               
         XC    SVNETVAL,SVNETVAL                                                
*                                                                               
NTU1     DS    0H                                                               
         XC    BQREP,BQREP         SET BINARY QREP                              
         CLC   RQREP,=C'000'                                                    
         BNH   NTU2                                                             
         PACK  DUB,RQREP                                                        
         CVB   R0,DUB                                                           
         STCM  R0,3,BQREP                                                       
*                                                                               
NTU2     DS    0H                                                               
***      CLC   PATCH(2),=C'N='        TEST FOR NETIO PATCH                      
***      BNE   NTU3                                                             
***      SR    RF,RF                                                            
***      ICM   RF,15,PATCH+2        DISP AT +2(4)                               
***      A     RF,NETIO                                                         
***      LLC   RE,PATCH+6          LENGTH AT +6(1)                              
***      BCTR  RE,R0                                                            
***      EX    RE,*+4                                                           
***      MVC   0(0,RF),PATCH+7     PATCH AT +7                                  
*                                                                               
NTU3     DS    0H                                                               
         CLI   NETNUMOD,C'W'       IF CALLED FOR WRITE                          
         BNE   NTU3A                                                            
         L     R5,PARM1            R5 = A(BYELEM)                               
         L     R6,PARM2            R6 = A (IELEM)                               
         BRAS  RE,NTU60            RE-WRITE RECORDS                             
         B     NTUX                                                             
*                                                                               
NTU3A    L     R5,ANXTBY           PLACE FOR FIRST BUY                          
         USING BYELEMD,R5                                                       
*                                                                               
         CLI   NUNETWK,0           FIRST TIME TEST                              
         BE    NTU4                                                             
         MVI   NBFUNCT,NBFRDHI     THEN NEED TO RESTORE READ                    
         B     NTU14               NO, CONTINUE WITH NEXT NETWORK               
*                                                                               
NTU4     XC    LASTNET,LASTNET                                                  
         LA    RE,NETBLOCK                                                      
         LH    RF,=Y(NBBLKEND-NETBLOCK)                                         
         XCEF                                                                   
         MVC   NBNETVAL,SVNETVAL   RESTORE A(NETVAL)                            
         LAY   RF,NMTABLE          NETWORK/MEDIA TABLE                          
         ST    RF,NBANBUFF         FOR NETIO                                    
         OI    NBINDS7,NBI7NTIO    TABLE CAN HOLD 1200 NETWORKS                 
*                                  SET SELECT OPTIONS                           
         MVC   NBSELAGY(3),QAGY    AGY/MEDIA                                    
         CLI   MEDFILT,C' '        IF HAVE MEDIA FILTER                         
         BNH   *+10                                                             
         MVC   NBSELMFL,MEDFILT    SET IT                                       
         MVC   NBSELCLI,CLT        CLT                                          
         MVC   NBSELPRD,PRD        PRODUCT  (**?? PIGGIES)                      
         CLC   =C'POL',QPRD                                                     
         BNE   *+10                                                             
         MVC   NBSELPRD,=C'POL'                                                 
         MVC   NBSELEST(2),BEST    END ST/END                                   
         CLC   =C'NO',QEST                                                      
         BNE   *+10                                                             
         MVC   NBSELEFL,QESTEND    EST FILTER                                   
         CLC   NBSELEFL,SPACES                                                  
         BNE   *+10                                                             
         XC    NBSELEFL,NBSELEFL                                                
         MVC   NBSELSTR(12),QSTART                                              
         LA    RF,STWRDLST         STEWARSHIP BLOCK                             
         ST    RF,NBASTWRD         STEWARDSHIP ESTIMATES NOW EXCLUDED           
*                                                                               
         CLI   BPRD2,0             TEST A SPECIFIC PIGGY REQUEST                
         BE    *+16                                                             
*        MVC   NBSELPIG(1),BPRD                                                 
*        MVC   NBSELPIG+1(1),BPRD2                                              
         MVC   NBSELPG3(3),PRD                                                  
         MVC   NBSELPG3+3(3),PRD2                                               
*                                                                               
         CLI   QSTA,C'A'           TEST SINGLE NETWORK REQ                      
         BL    NTU6                                                             
         CLI   QSTA,C'Z'                                                        
         BH    NTU6                                                             
         CLC   =C'ALL',QSTA                                                     
         BE    NTU6                                                             
         MVC   NBSELNET,QSTA                                                    
*                                  SET DATA OPTIONS                             
NTU6     DS    0H                                                               
         MVI   NBDATA,C'U'         UNITS                                        
*                                                                               
****     MVI   NBSEQ,C'N'          NETWORK SEQUENCE                             
****     OC    NBSELNET,NBSELNET   UNLESS SINGLE NETWORK ????                   
****     BZ    *+8                                                              
****     MVI   NBSEQ,C'D'          THEN DATE SEQ                                
*                                                                               
         MVI   NBSEQ,C'N'          NETWORK SEQUENCE                             
         CLC   QESTEND,SPACES      IF ESTIMATE RANGE -MUST USE N                
         BH    NTU8                                                             
         OC    NBSELNET,NBSELNET   UNLESS SINGLE NETWORK - USE D                
         BZ    *+8                                                              
         MVI   NBSEQ,C'D'          THEN DATE SEQ                                
*                                                                               
         CLC   =C'NO',QEST         NEVER USE P FOR ESTIMATE = NO                
         BE    NTU8                  OTHERWISE                                  
         MVI   NBSEQ,C'P'          PROGRAM SEQ SEEMS TO WORK BEST               
*                                                                               
NTU8     DS    0H                                                               
         CLI   PROFSTW,C'Y'        WANT STW ESTIMATES ON I2?                    
         BNE   *+8                 NO                                           
         OI    NBINDS4,NBI4STWY    NETIO TO INCLUDE STEWARD ESTIMATES           
         MVC   NBTRCOPT,RCTRACE    TRACE                                        
         MVI   NBFUNCT,NBFNORM     NORMAL FUNCTION                              
*                                                                               
         LAY   RF,NIOAREA                                                       
         A     RF,RELO                                                          
         ST    RF,NBAIO                                                         
         MVC   NBPRINT,PRINT                                                    
         MVC   NBLOADER,LOADER                                                  
         MVC   NBACOM,ACOMFACS                                                  
*                                                                               
NTU10    DS    0H                                                               
         GOTO1 NETIO,DMCB,NETBLOCK                                              
         MVC   SVNETVAL,NBNETVAL   SAVE A(NETVAL)                               
         CLI   NBERROR,NBINVPRD    IF INVALID PRODUCT ERROR                     
         BE    NTU11               SKIP                                         
         CLI   NBERROR,NBINVEST    IF INVALID EST ERROR                         
         BE    NTU11               SKIP                                         
         CLI   NBERROR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         CLI   NBMODE,NBPROCUN                                                  
         BE    NTU12                                                            
         CLI   NBMODE,NBREQLST                                                  
         BNE   NTU10                                                            
*                                                                               
NTU11    DS    0H                                                               
         MVI   NBACTNET,X'FF'      SET END OF FILE                              
         B     NTU12X                                                           
*                                                                               
         SPACE 3                                                                
*        PROCESS UNIT                                                           
*                                                                               
         SPACE 2                                                                
NTU12    DS    0H                                                               
         OC    BQREP,BQREP         SPECIAL REP REQUEST?                         
         BZ    *+14                                                             
         CLC   NBSREP,BQREP                                                     
         BNE   NTU10                                                            
*                                                                               
         CLC   NBSELPRD,=C'POL'    UNLESS POL                                   
         BE    *+14                                                             
*        CLC   NBPRD,BPRD          FILTER ON PRODUCT                            
         CLC   NBPR1CL3,PRD        FILTER ON 3 CHAR PRODUCT                     
         BNE   NTU10               (CAN NETIO BE TRUSTED?)                      
*                                                                               
*                                **PIGGYBACK FILTERING**                        
         CLI   BPRD2,0                                                          
         BE    NTU12B                                                           
*        CLC   NBPRD2,BPRD2                                                     
         CLC   NBPR2CL3,PRD2       MATCH ON 3 CHARACTER PRD2?                   
         BNE   NTU10               NO                                           
*                                                                               
NTU12B   DS    0H                                                               
         CLI   PIGCTL,C'N'         TEST TO SKIP PIGGY BACKS                     
         BNE   NTU12D                                                           
*        CLI   NBPRD2,0                                                         
*        BNE   NYU10                                                            
         OC    NBPR2CL3,NBPR2CL3   HAVE 3 CHARACTER PRD2?                       
         BNZ   NTU10               YES, SKIP                                    
*                                                                               
NTU12D   DS    0H                  TEST PRD GROUP REQUEST                       
         CLI   QPGR,C' '           TEST PRDGRP REQUEST                          
         BE    NTU12X              NO                                           
         CLC   =C'POL',QPRD        TEST POL REQUEST                             
         BNE   NTU12X              NO                                           
*                                                                               
*        L     R1,NBAIO            PULL PRD FROM UNIT                           
*        LA    R1,NUPRD-NUKEY(R1)                                               
*        MVC   BYTE,0(R1)                                                       
*                                                                               
* NEED TO CHECK IF PRD IN THIS PRDGRP                                           
         L     R1,SVPRDADR         POINT TO CURRENT PRD                         
NTU12F   CLC   2(3,R1),NBPR1CL3    MATCH PRODUCT CODE                           
         BE    NTU12X                                                           
         CLC   0(2,R1),6(R1)       NEXT PRD SAME PRDGRP                         
         BNE   NTU10               NO - SKIP PRD                                
         LA    R1,6(R1)            ELSE NEXT ENTRY                              
         OC    0(6,R1),0(R1)       NOT IN PGRP TABLE                            
         BNZ   NTU12F                                                           
*                                                                               
NTU12X   DS    0H                                                               
         CLC   NBACTNET,LASTNET    TEST NEW NETWORK                             
         BE    NTU14AA                                                          
         CLI   LASTNET,0           TEST FIRST TIME                              
         BNE   NTU13                                                            
         MVC   SVMIRPRO,I2APROF+2                                               
         B     NTU14                                                            
*                                                                               
NTU13    DS    0H                                                               
         MVC   NUNETWK,LASTNET     RETURN PREVIOUS TO CALLER                    
         B     NTU59X                                                           
*                                                                               
NTU14    DS    0H                  FIRST FOR NETWORK                            
         MVC   I2APROF+2(1),SVMIRPRO                                            
         BRAS  RE,CHKSTMIR         CHECK FOR MIRROR FLAG ON STATION             
*                                                                               
NTU14AA  MVC   LASTNET,NBACTNET                                                 
         CLI   NBACTNET,X'FF'                                                   
         BE    NTU13                                                            
         L     R7,NBAIO                                                         
         USING NURECD,R7                                                        
         MVI   BYSTAT2,0           STATUS BYTE 2                                
*                                  LOOK FOR MAKEGOOD ELEM                       
         LA    R2,NUDATA                                                        
         CLI   0(R2),X'07'                                                      
         BE    *+12                                                             
         MVI   ELCODE,X'07'                                                     
         BAS   RE,NXTEL                                                         
         BNE   NTU14A                                                           
         OI    BYSTAT2,BYSTMDGQ    SET MADE-GOOD                                
         B     NTU14B                                                           
*                                                                               
NTU14A   DS    0H                                                               
         TM    NBUNITST,X'42'      PREEMPT/MISSED                               
         BZ    NTU14C                                                           
         OI    BYSTAT2,BYSTPREQ                                                 
*                                  LOOK FOR CMML ELEM                           
NTU14B   DS    0H                                                               
         CLI   MISSOPT,C'C'        GET MISSED/MADE-GOOD DATA?                   
         BE    NTU14C              YES                                          
         CLI   MISSOPT,C'Y'        TEST SHOWING MISSED/MADE-GOOD                
         BNE   NTU10               NO, SKIP                                     
*                                                                               
NTU14C   DS    0H                                                               
         MVC   BYEST,NBACTEST      ESTIMATE                                     
         XC    BYLIN,BYLIN        *LINE NUMBER                                  
         MVC   BYSDT,NBACTDAT      START DATE                                   
         MVC   BYACTDAT,NBACTDAT   DATE - FOR REMATCH - BYSDT CHANGES           
         MVC   BYACTSQH,NBACTSQH   START QUARTER HOUR - FOR REMATCH             
         MVC   BYACTPRG,NBACTPRG   PROGRAM - FOR REMATCH                        
         MVC   BYACTDP,NBACTDP     DAYPART - FOR REMATCH                        
         MVI   BYUDT,0             DIFF BTWN ROT START AND UNIT START           
         MVC   BYTE,NBSDROT        ROTATION BYTE                                
         CLI   BYTE,0              IF ZERO                                      
         BNE   *+10                                                             
         MVC   BYTE,NBDAY          USE REGULAR DAY FIELD                        
         MVC   BYDAY,BYTE          DAY(S) OF WEEK                               
         CLI   I2YPROF+7,C'Y'      USE DAY NOT ROTATION - IF IS ONE             
         BNE   NTU14D                                                           
         CLI   NBDAY,0                                                          
         BE    NTU14D                                                           
         MVC   BYDAY,NBDAY         USE DAY                                      
         MVC   BYTE,BYDAY                                                       
*                                                                               
NTU14D   BRAS  RE,SETSEDAY         SET START-END DAYS - USES BYTE               
         MVC   BYSEDAY,BYTE       *START/END DAY                                
*                                  SET END DATE                                 
         MVC   BYEDT,NBACTDAT      MAY = START                                  
         LLC   R0,BYSEDAY          CALC DAYS IN ROTATOR                         
         LR    R3,R0                                                            
         SR    R2,R2                                                            
         SLDL  R2,28                                                            
         SRL   R3,28                                                            
         SR    R3,R2               R3 HAS NUMBER OF DAYS                        
         BNZ   NTU14F                                                           
         CLI   I0PROF+4,C'Y'                                                    
         BNE   NTU20               TO ADD TO GET TO END DATE                    
         B     NTU14G                                                           
NTU14F   DS    0H                                                               
         BP    NTU14G                                                           
         AHI   R3,7                IF END LESS START ADD 7 DAYS                 
*                                  (OUT OF WEEK)                                
NTU14G   DS    0H                                                               
         GOTO1 DATCON,DMCB,(2,BYSDT),DUB                                        
         GOTO1 GETDAY,(R1),DUB,FULL  SEE IF DAY OF WEEK FOR DATE                
         LLC   R0,BYSEDAY          IS SAME AS START DAY OF ROTATOR              
         SRL   R0,4                                                             
         LLC   RF,DMCB                                                          
         SR    R0,RF                                                            
         BZ    NTU14X              YES, OK                                      
*                                                                               
         LCR   R2,R0                                                            
         STC   R2,BYUDT            DIFF BTWN BYSDT AND UNIT DATE                
*                                                                               
         GOTO1 ADDAY,(R1),DUB,DUB,(R0)    NO, BACK UP TO ROTATOR START          
         GOTO1 DATCON,(R1),DUB,(2,BYSDT)                                        
*                                                                               
NTU14X   DS    0H                                                               
         GOTO1 ADDAY,(R1),DUB,DUB,(R3)                                          
         GOTO1 DATCON,(R1),DUB,(2,BYEDT)                                        
*                                                                               
NTU20    DS    0H                                                               
         GOTOR CVMIN,DMCB,NBTIME,BYSTIM   START/END TIME (MINUTES)              
         MVC   BYLEN,NBLEN         LENGTH                                       
         L     RF,NBACTUAL         COST=ACTUAL                                  
         TM    NBUNITST,X'80'        MINUS UNIT                                 
         BZ    *+6                                                              
         LNR   RF,RF                                                            
         STCM  RF,15,BYCOST                                                     
         GOTO1 =V(NETNET),DMCB,(NBRTTYPE,BYCOST),BYCOST  GROSS/NET              
         MVI   BYSTAT,0           *STATUS                                       
         ICM   RF,15,BYCOST                                                     
         LTR   RF,RF                                                            
         BNM   NTU20A                                                           
         OI    BYSTAT,X'01'        SET NEGATIVE INDICATOR                       
         LPR   RF,RF               BUT MAKE AMOUNT +                            
         STCM  RF,15,BYCOST                                                     
         ICM   RF,15,BYNET         NET TOO                                      
         LPR   RF,RF                                                            
         STCM  RF,15,BYNET                                                      
*                                                                               
NTU20A   CLI   I2IPROF,C'Y'        TIME + INTEG                                 
         BE    NTU20B              USE IT                                       
         CLI   I2IPROF+1,C'Y'      INTEG ONLY                                   
         BNE   NTU22               NO INTEGRATION AT ALL                        
         XC    BYCOST(8),BYCOST    CLEAR TIME CHARGES                           
*                                                                               
NTU20B   MVC   BYTE,NBRTTYPE       RATE TYPE                                    
         CLI   NBSDRTCV,C'I'       APPLY RATE TYPE TO INTEG                     
         BE    NTU20C                                                           
         CLI   NBSDRTCV,C'A'       APPLY RATE TYPE TO ALL                       
         BE    NTU20C                                                           
         CLI   NBSDRTCV,0          APPLY RATE TYPE TO ALL                       
         BE    NTU20C                                                           
         MVI   BYTE,0                                                           
NTU20C   GOTO1 =V(NETNET),DMCB,(BYTE,NBINTEG),WORK    GROSS/NET                 
         ICM   RF,15,WORK          MAKE POSITIVE AND ADD TO COST                
         LPR   RF,RF                                                            
         STCM  RF,15,BYINTEG       SET PLAIN INTEG COST                         
         ICM   RE,15,BYCOST        ADD INT TO TIME COST (IF ANY)                
         AR    RF,RE                                                            
         STCM  RF,15,BYCOST                                                     
*                                                                               
         ICM   RF,15,WORK+4        NET TOO                                      
         LPR   RF,RF                                                            
         ICM   RE,15,BYNET                                                      
         AR    RF,RE                                                            
         STCM  RF,15,BYNET                                                      
*                                                                               
NTU22    DS    0H                                                               
         MVC   BYPRD(2),NBPRD      PRODUCT(S)                                   
         MVC   BYPRDNT,NBPR1CL3    3 CHAR PRODUCT                               
         MVC   BYPRD2NT,NBPR2CL3   3 CHAR PRODUCT2                              
*        CLI   BYPRD,0             NONE=POL                                     
*        BNE   *+8                                                              
*        MVI   BYPRD,X'FF'                                                      
         OC    BYPRDNT,BYPRDNT     NONE=POL                                     
         BNZ   *+10                                                             
         MVC   BYPRDNT,=C'POL'                                                  
         MVI   BYNOWK,1            SPOTS/WEEK                                   
         MVI   BYUNACH,1           UNACHIEVED                                   
***>NOP  XC    BYAMPW,BYAMPW      *ACTIVE MINUTES PER WEEK                      
         XC    BYPREF,BYPREF       PATTERN REF NUMBER                           
         MVC   BYSPT,NBACTSUB      SPOT (LINE) NUMBER                           
***      XC    BYDEMVR(5),BYDEMVR *DEMOS                                        
         OC    BYSREP,NBSREP       SPECIAL REP                                  
         BZ    *+8                                                              
         MVI   SREPSW,C'Y'         SET HAVE SPECIAL REP                         
         MVI   BYORBNO,0          *ORBIT LINK NO.                               
         MVI   BYCOPY,0           *COPY CODE FOR TRAFFIC                        
         MVC   BYPKG,NBPACK        PACKAGE                                      
**>ABOVE MVC   BYINTEG,NBINTEG     INTEGRATION                                  
         XC    BYFILM(4),BYFILM    FILM 1 AND 2                                 
*                                                                               
         CLI   TRAFFRPT,C'N'       IF FILM REPORT                               
         BNE   NTU25               THEN NEED TO READ COMMLS                     
*                                                                               
NTU24    CLI   TRAFMBF,C'Y'        IF DOING FILMS                               
         BE    *+12                                                             
         CLI   TRAFMBF,C'B'                                                     
         BNE   NTU29                                                            
*                                                                               
NTU25    LA    R2,NUDATA           LOOK FOR CMML ELEM                           
         CLI   0(R2),X'21'                                                      
         BE    *+12                                                             
         MVI   ELCODE,X'21'                                                     
         BAS   RE,NXTEL                                                         
         BNE   NTU29                                                            
*                                                                               
         USING NUCMLEL,R2                                                       
         OC    NUCML1,NUCML1       TEST HAVE ANY CMML                           
         BZ    NTU27               NO, SKIP                                     
         CLC   =C'XXXXXX',NUCML1   SKIP THESE CRAP COMMLS                       
         BE    NTU27                                                            
         TM    NUCMLFLG,X'E0'      TEST PRD,LEN OR DATE CHANGE                  
         BNZ   NTU27               YES, REJECT                                  
*                                                                               
         XC    KEY,KEY                                                          
         LA    R3,KEY                                                           
         USING CMLKEY,R3                                                        
         MVC   AREC,ADBUY          USE BUY RECORD AREA FOR CMML                 
         MVC   CMLKID,=X'0A21'                                                  
         MVC   CMLKAM,BAGYMD                                                    
         MVC   CMLKCLT,BCLT                                                     
         MVC   CMLKCML,NUCML1      FIRST CMML                                   
*                                                                               
         TM    NUCMADFL,NUCMADF1   IS THIS AN AD-ID?                            
         BNO   *+12                                                             
         MVI   CMLKID+1,X'C1'      AD-ID POINTER IS 0AC1                        
         OI    BYSTAT2,BYSTADIQ    COMML SEEDED AS AD-ID                        
*                                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE                                                  
         BNE   NTU27               DON'T DUMP ANYMORE                           
****>    DC    H'0'                MISSING FILM                                 
NTU26    GOTO1 GET                                                              
         L     RF,AREC                                                          
         MVC   BYFILM,CMLSEQ+1-CMLKEY(RF)   SET CMML SEQ NO                     
*                                                                               
         MVC   BYPREF,NUCMLR3F     SET PATTERN REF NUM                          
         BRAS  RE,SAVEPREF         SAVE IN TABLE                                
*                                                                               
         OC    NUCML2,NUCML2       HAVE 2ND CMML?                               
         BZ    NTU27               NO                                           
         MVI   CMLKID+1,X'21'      DEFAULT IS FILMCODE!                         
         TM    NUCMADFL,NUCMADF2   IS 2ND CMML AN AD-ID?                        
         BNO   *+8                 NO                                           
         MVI   CMLKID+1,X'C1'      YES - AD-ID POINTER IS 0AC1                  
         MVC   CMLKCML,NUCML2                                                   
         GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                MISSING FILM                                 
         GOTO1 GET                                                              
         L     RF,AREC                                                          
         MVC   BYFILM2,CMLSEQ+1-CMLKEY(RF)  SET CMML SEQ NO                     
*                                                                               
NTU27    DS    0H                  BILLBOARD                                    
         TM    NUCMLFLG,X'04'      TEST BB REQUIRED                             
         BNZ   NTU28                                                            
         CLI   NUCMLBSL,0          OR PRESENT                                   
         BE    NTU29                                                            
         CLI   NUCMLBSL,X'FF'                                                   
         BE    NTU29                                                            
NTU28    DS    0H                                                               
         OI    BYSTAT2,BYSTBLBQ    SET BILLBOARD PRESENT                        
         DROP  R2,R3                                                            
*                                                                               
NTU29    DS    0H                                                               
         MVC   BYDISK,NBKEY+21     DISK ADDRESS (OVERLAYS DEMOS)                
*                                                                               
         XC    UPDGRS(8),UPDGRS    CLEAR PAID TOTS                              
         MVI   UPDSW,0                                                          
*                                  LOOK FOR SPECIAL RATE ELEMENTS               
         LA    R2,NUDATA                                                        
         CLI   0(R2),X'03'                                                      
         BE    *+12                                                             
         MVI   ELCODE,X'03'                                                     
NTU29A   BAS   RE,NXTEL            SPECIAL RATE ELEMENT?                        
         BNE   NTU29X              NO                                           
*                                                                               
         USING NUSPRD,R2                                                        
         CLI   NUSPRTYP,C'C'       ACTUAL COST TRADE CREDIT?                    
         BNE   NTU29A              NO                                           
         MVC   BYTRADE,NUSPRAMT                                                 
         DROP  R2                                                               
*                                  LOOK FOR PAY ELEMS                           
NTU29X   LA    R2,NUDATA                                                        
         CLI   0(R2),X'12'                                                      
         BE    *+12                                                             
         MVI   ELCODE,X'12'                                                     
NTU30    BAS   RE,NXTEL                                                         
         BNE   NTU32                                                            
*                                                                               
         USING NUPAYD,R2                                                        
         CLI   NUPAYTYP,C'T'       TIME CHARGES PAID?                           
         BE    NTU30A                                                           
         CLI   NUPAYTYP,C'I'       INTEGRATION PAID?                            
         BE    NTU30B                                                           
         B     NTU30                                                            
*                                  HAVE TIME PAID ELEM                          
NTU30A   CLI   I2IPROF+1,C'Y'      INTEG ONLY                                   
         BE    NTU30               THEN SKIP TIME PAID $                        
         B     NTU30X                                                           
*                                  HAVE INTEG PAID ELEM                         
NTU30B   CLI   I2IPROF+1,C'Y'      INTEG ONLY                                   
         BE    NTU30X              USE IT                                       
         CLI   I2IPROF,C'Y'        TIME + INTEG                                 
         BE    NTU30X              USE IT                                       
         B     NTU30               SKIP IT FOR TIME ONLY                        
*                                                                               
NTU30X   MVI   UPDSW,1                                                          
         ICM   RF,15,NUPAYGRS      TOTAL UP GROSS                               
         A     RF,UPDGRS                                                        
         ST    RF,UPDGRS                                                        
         ICM   RF,15,NUPAYNET      AND NET                                      
         A     RF,UPDNET                                                        
         ST    RF,UPDNET                                                        
         B     NTU30               NEXT ELEM                                    
         DROP  R2                                                               
*                                                                               
NTU32    DS    0H                                                               
         OC    BYCOST,BYCOST       IF BUY $                                     
         BNZ   NTU34                                                            
         CLI   UPDSW,1             OR PAY ELEMS                                 
         BE    NTU34               TEST PAID = ORD                              
         CLI   PROGPROF+13,C'Y'    ELSE TEST PROFILE TO TREAT $0                
         BE    NTU38               BUT NOT ACTUALLY PAID AS PAID                
         B     NTU39                                                            
*                                                                               
NTU34    DS    0H                  TEST UNIT PAID                               
         MVC   DUB(8),UPDGRS                                                    
         TM    BYSTAT,X'01'        IF MINUS COST                                
         BZ    NTU36                                                            
         LM    RE,RF,DUB           COMPLEMENT BEFORE PAID COMPARE               
         LCR   RE,RE                                                            
         LCR   RF,RF                                                            
         STM   RE,RF,DUB                                                        
*                                                                               
NTU36    DS    0H                  COMPARE PAID $ TO UNIT $                     
         CLC   DUB(8),BYCOST       IS IT TOTALLY PAID                           
         BNE   *+8                                                              
NTU38    DS    0H                                                               
         OI    BYSTAT,X'40'                                                     
*                                                                               
NTU38X   L     RF,UPDGRS           ROLL UP TO MATCH PAID TOTALS                 
         A     RF,PDGRS                                                         
         ST    RF,PDGRS                                                         
         L     RF,UPDNET                                                        
         A     RF,PDNET                                                         
         ST    RF,PDNET                                                         
*                                                                               
NTU39    DS    0H                                                               
         TM    NBUNITST,X'01'      MAKE GOOD?                                   
         BZ    *+8                                                              
         OI    BYSTAT,X'04'                                                     
*                                                                               
         TM    NBUNITST,X'20'      ACTUAL COST = NO BLANK COST                  
         BZ    *+8                                                              
         OI    BYSTAT,X'20'        SET COST ENETERED                            
*                                                                               
         CLI   MKACTV,C'Y'         MATCHMAKER ACTIVE                            
         BNE   NTU58                                                            
*                                                                               
         CLI   BFRINSW,C'Y'        BUFFIN INITIALIZED?                          
         BE    NTU57               YES, THEN JUST PUT REC                       
         LA    R0,BUFFAINI         INITIALIZE                                   
         GOTO1 =V(BUFFERIN),DMCB,((R0),ABUFFRIN),(0,0),ACOMFACS                 
         CLI   4(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         MVI   BFRINSW,C'Y'        SET INIT DONE                                
*                                                                               
NTU57    XC    WORK,WORK           PUT UNIT KEY AWAY FOR I2MM                   
         MVC   WORK(4),NBKEY+21    DISK ADDRESS                                 
         MVC   WORK+4(20),NUKEY    UNIT KEY                                     
         LA    R0,BUFFAPUT                                                      
         GOTO1 =V(BUFFERIN),DMCB,((R0),ABUFFRIN),(0,WORK),ACOMFACS              
         CLI   4(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
NTU58    CLI   NBMIRTYP,C' '                                                    
         BNH   NTU59                                                            
         CLI   I2APROF+2,C'Y'      EXPAND MIRROR UNITS?                         
         BNE   NTU59                                                            
         OC    I2APROF+3(2),I2APROF+3  EFFECTIVE MONTH FOR MIRRORS              
         BZ    NTU58M                                                           
         LLC   R1,I2APROF+3                                                     
         CLI   I2APROF+3,80        IF YEAR < 80 ADD 100 FOR CENTURY             
         BH    *+8                                                              
         AHI   R1,100                                                           
         STC   R1,I2APROF+3                                                     
         GOTO1 DATCON,DMCB,(2,BQENDP),(3,WORK)                                  
         CLC   WORK(2),I2APROF+3   IS MONTH OF REQ BEFORE EFF DATE              
         BL    NTU59               YES THEN DON'T EXPAND MIRRORS                
*                                                                               
NTU58M   BRAS  RE,MIRROR           ADD ANOTHER BUY FOR MIRROR                   
         BNE   NTUX                                                             
*                                                                               
NTU59    LA    R5,BYELEML(R5)     NEXT ENTRY                                    
         C     R5,ABUFFX                                                        
         BNH   NTU10                                                            
         MVI   ERROR,BUFERR                                                     
         B     NTUX                                                             
*                                                                               
NTU59X   DS    0H                  END OF BUYS                                  
         ST    R5,ANXTBY                                                        
         BCTR  R5,0                                                             
         ST    R5,ALBY                                                          
         B     NTUX                                                             
*                                                                               
*        NEXT ELEMENT                                                           
*                                                                               
NXTEL    DS    0H                                                               
         LLC   R0,1(R2)                                                         
         AR    R2,R0                                                            
         CLI   0(R2),0                                                          
         BE    NXTEL2                                                           
         CLC   ELCODE,0(R2)                                                     
         BER   RE                                                               
         B     NXTEL                                                            
NXTEL2   DS    0H                                                               
         LTR   RE,RE                                                            
         BR    RE                                                               
         SPACE 2                                                                
*                                                                               
NTUX     DS    0H                                                               
         XIT1                                                                   
*                                                                               
NUFLIST  DC    CL8'UUNTFIL'                                                     
         DC    CL8'NUNTDIR'                                                     
         DC    CL10'X'                                                          
*                                                                               
NETNUMOD DS    CL1                                                              
LASTNET  DS    CL4                                                              
BQREP    DS    XL2                                                              
SVNETVAL DS    A                                                                
PARMS    DS    0XL24                                                            
PARM1    DS    F                                                                
PARM2    DS    F                                                                
PARM3    DS    F                                                                
PARM4    DS    F                                                                
PARM5    DS    F                                                                
PARM6    DS    F                                                                
*                                                                               
UPDGRS   DS    F                   UNIT PAID GROSS                              
UPDNET   DS    F                   AND NET                                      
UPDSW    DS    XL1                 PAID SWITCH                                  
*                                                                               
STWRDLST DS    XL256               STEWARDSHIP ESTIMATE LIST                    
*                                                                               
         LTORG                                                                  
*                                                                               
         DS    0D                                                               
NMTABLE  DS    XL6000              NET/MED TABLE FOR NETIO                      
         DS    0D                                                               
NIOAREA  DS    XL6000              IO AREA FOR NETIO                            
         DS    0D                                                               
NETBLK   DS    XL(NBBLKEND+1-NETBLOCK)     NETBLOCK AREA                        
*                                                                               
         EJECT                                                                  
*-----------------------------------------------------------                    
*        RE-WRITE RECORDS                                                       
*        GET COMMENTS, ETC...                                                   
*        (CALLED DURING MATCH REPORT PRINT)                                     
*-----------------------------------------------------------                    
*                                                                               
NTU60    NTR1  BASE=*,LABEL=*                                                   
         LAY   R7,NIOAREA2                                                      
         A     R7,RELO                                                          
         ST    R7,ANIO2                                                         
         USING NURECD,R7                                                        
         USING BYELEMD,R5          R5 POINTS TO BYELEM                          
         USING IELEMD,R6           R6 POINTS TO IELEM                           
*                                                                               
         GOTO1 DATAMGR,DMCB,GETREC,=C'UNTFILE',BYDISK,NURECD,NDMWRK             
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                  SET COMMENTS                                 
         MVC   ANXTCOM,ACOMTAB                                                  
         L     RF,ANXTCOM                                                       
         MVI   0(RF),0             CLEAR                                        
         CLI   PROGOPT,C'N'        TEST DOING PROGRAM NAMES                     
         BE    NTU63                                                            
*                                                                               
         MVC   WORK,SPACES                                                      
         LA    RE,WORK                                                          
         CLI   PROGOPT,C'C'        CODE AND NAME                                
         BNE   NTU62                                                            
         MVC   WORK(6),NUKPROG                                                  
         LA    RE,WORK+5                                                        
         CLI   0(RE),C' '                                                       
         BH    *+8                                                              
         BCT   RE,*-8                                                           
         MVI   1(RE),C'/'                                                       
         LA    RE,2(RE)                                                         
*                                                                               
NTU62    DS    0H                                                               
         MVC   0(L'NUPROGNM,RE),NUPROGNM                                        
         CLI   NUPROGNM,C' '       TEST ANY PROGRAM                             
         BH    *+8                                                              
         MVI   0(RE),C'*'                                                       
         LA    RE,L'NUPROGNM-1(RE)   GET LENGTH OF CODE/NAME                    
         CLI   0(RE),C' '                                                       
         BH    *+8                                                              
         BCT   RE,*-8                                                           
         LA    R0,WORK-4                                                        
         SR    RE,R0               RE HAS LENGTH +3                             
         L     RF,ANXTCOM                                                       
         MVI   0(RF),X'66'         SIMULATE COMMENT ELEM                        
         STC   RE,1(RF)            ELEM LENGTH                                  
         MVI   2(RF),0                                                          
         AHI   RE,-4                                                            
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   3(0,RF),WORK                                                     
         LA    RF,4(RF,RE)                                                      
         ST    RF,ANXTCOM                                                       
*                                                                               
NTU63    DS    0H                                                               
         CLI   MISSOPT,C'C'        GET MISSED/MADE-GOOD DATA?                   
         BE    *+12                YES                                          
         CLI   MISSOPT,C'Y'        MISSED/MAKEGOOD OPTION                       
         BNE   NTU64                                                            
         TM    NUUNITST,X'01'      IS BUY A MAKEGOOD                            
         BZ    NTU64                                                            
         L     RF,ANXTCOM                                                       
         LA    RE,30(RF)           ELEM LENGTH                                  
         ST    RE,ANXTCOM                                                       
         MVI   0(RF),X'66'         SIMULATE COMMENT ELEM                        
         MVI   1(RF),30            ELEM LENGTH                                  
         MVI   2(RF),0                                                          
         MVC   3(27,RF),SPACES                                                  
         MVC   3(6,RF),=C'MG FOR'                                               
         LA    R4,10(RF)                                                        
         LA    R2,NUDATA           GET MISSED SPOT DATA                         
*                                                                               
NTU63D   DS    0H                                                               
         CLI   0(R2),X'06'                                                      
         BE    NTU63F                                                           
         CLI   0(R2),0                                                          
         BE    NTU64                                                            
         LLC   R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     NTU63D                                                           
*                                                                               
NTU63F   DS    0H                                                               
         USING NUMGD,R2                                                         
         GOTO1 DATCON,DMCB,(2,NUMGDATE),(7,DUB)                                 
         MVC   0(5,R4),DUB                                                      
         LA    R4,6(R4)                                                         
         LLC   R0,NUMGSUB         LINE NUMBER                                   
         CHI   R0,1                                                             
         BNH   NTU63H                                                           
         MVI   0(R4),C'('                                                       
         EDIT  (R0),(3,1(R4)),ALIGN=LEFT                                        
         AR    R4,R0                                                            
         MVI   1(R4),C')'                                                       
         LA    R4,3(R4)                                                         
*                                                                               
NTU63H   DS    0H                                                               
         MVC   0(6,R4),NUMGPCOD    PROGRAM CODE                                 
         DROP  R2                                                               
*                                                                               
NTU64    DS    0H                                                               
         CLI   COMOPT,C'N'                                                      
         BE    NTU68                                                            
         LA    R2,NUDATA           LOOK FOR COMMENT ELEMENTS                    
*                                                                               
NTU65    DS    0H                                                               
         CLI   0(R2),X'04'         COMMENT ELEM                                 
         BE    NTR67                                                            
         CLI   0(R2),0                                                          
         BE    NTU68                                                            
NTU66    DS    0H                                                               
         SR    R0,R0                                                            
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     NTU65                                                            
NTR67    DS    0H                                                               
         L     RF,ANXTCOM                                                       
         SR    RE,RE                                                            
         IC    RE,1(R2)                                                         
         MVI   0(RF),X'66'         SPOTPAK COMMENTS ARE 66                      
         BCTR  RE,R0               DROP LENGTH BY ONE                           
         STC   RE,1(RF)                                                         
         AHI   RE,-4               MOVE FROM +4 TO +3                           
         BM    NTU66               TO MAKE SPOTPAK COMMENT                      
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   3(0,RF),4(R2)                                                    
*                                                                               
         LA    RF,4(RF,RE)                                                      
         ST    RF,ANXTCOM                                                       
         B     NTU66                                                            
*                                                                               
NTU68    DS    0H                                                               
         L     RF,ANXTCOM                                                       
         MVI   0(RF),0             SET END OF COMMENTS                          
*-----------------------------------------------------------                    
*       **  WRITEBACK **                                                        
*           ---------                                                           
*-----------------------------------------------------------                    
         CLI   PSTOPT,C'N'         SKIP IF POST OPTION = NO                     
         BE    NTU80                                                            
         CLI   PSTOPT,C'L'         SKIP IF POST OPTION = NO                     
         BE    NTU80               L=DUE TO I2A PROFILE LOCK                    
*                                                                               
         XC    SVIDAT,SVIDAT                                                    
         MVI   BYTE,0              SET WRITEBACK NOT NEEDED                     
         XC    DUB(2),DUB          CLEAR TIME IF NO AFFID                       
*                                                                               
         TM    BYSTAT,X'40'        IF PAID                                      
         BZ    NTU68X                                                           
         TM    BYSTAT2,BYSTMISQ    AND MISSED                                   
         BZ    NTU68X                                                           
         B     NTU80               LEAVE ALL AFFID/INVOICE INFO                 
*                                                                               
NTU68X   TM    BYSTAT2,BYSTMGHQ    X'01' MIRROR GHOST UNIT                      
         BNO   NTU69                                                            
         OC    IDAT,IDAT           TEST HAVE INVOICE ELEM                       
         BZ    NTU69B              UNMATCH UNIT IF UNMATCHED                    
         B     NTU78D              ELSE SKIP GHOST - MATCH ACTUAL               
*                                                                               
***      MVC   DUB(2),ITIM         MILITARY TIME-DUB=TIME                       
***      BRAS  RE,UNMIRROR         POST ACTUAL TIME - SETS DUB(2)               
***      B     NTU69M              HAVE AFFID TO SEED                           
*                                                                               
NTU69    TM    BYSTAT2,BYSTMIRQ    ACTUAL UNIT THAT HAS A MIRROR?               
         BZ    NTU69B                                                           
         BRAS  RE,MIRMATCH         CHECK IF MIRROR MATCHED TOO                  
         BE    NTU69B              OK TO MATCH                                  
         MVC   SVIDAT,IDAT         SAVE IDAT                                    
         XC    IDAT,IDAT           AND FORCE AN UNMATCH                         
*                                                                               
NTU69B   OC    IDAT,IDAT           TEST HAVE INVOICE ELEM                       
         BZ    *+14                                                             
         MVC   DUB(2),ITIM         MILITARY TIME-DUB=TIME                       
         B     NTU69M              HAVE AFFID TO SEED                           
*                                                                               
         CLI   I2IPROF,C'Y'        TIME + INT RUN                               
         BE    NTU69M              OK TO REMOVE AFFID                           
*                                                                               
         USING NUDTAD,R2           IF UNMATCHED - CHECK INV#S                   
NTU69C   LA    R2,NUDATA           MIGHT NEED TO REMOVE                         
NTU69D   DS    0H                                                               
         CLI   0(R2),X'18'         EXTRA INFO ELEM -                            
         BE    NTU69F                                                           
         BH    NTU69M              NO ELEM = OK TO REMOVE AFFID                 
         CLI   0(R2),0                                                          
         BE    NTU69M              NO ELEM - OK TO REMOVE AFFID                 
         LLC   R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     NTU69D                                                           
*                                                                               
NTU69F   CLI   I2IPROF+1,C'Y'      INT ONLY RUN                                 
         BNE   NTU69G                                                           
         CLC   NUDTINVN,SPACES     DOES TIME INV EXISTS                         
         BH    NTU73               YES -LEAVE AFFID/FILM STUFF THERE            
         B     NTU69M              ELSE OK TO REMOVE AFFID STUFF                
*                                                                               
NTU69G   CLC   NUDTIIVN,SPACES     TIME ONLY - IS THERE AN INT INV              
         BH    NTU73               YES -LEAVE AFFID/FILM STUFF THERE            
         B     NTU69M              ELSE OK TO REMOVE AFFID STUFF                
*                                                                               
NTU69M   CLC   NUAFFTIM,DUB        TEST RECORD CHANGED                          
         BE    *+14                                                             
         MVI   BYTE,1              SET WRITEBACK NEEDED                         
         MVC   NUAFFTIM,DUB                                                     
         LA    R2,NUDATA           GET 2ND DATA ELEM                            
*                                **NOTE WONT ADD ELEM IF NOT THERE              
NTU69Q   DS    0H                                                               
         CLI   0(R2),X'02'                                                      
         BE    NTU69R                                                           
         CLI   0(R2),0                                                          
         BE    NTU69X                                                           
         LLC   R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     NTU69Q                                                           
*                                                                               
NTU69R   DS    0H                                                               
         USING NUSDRD,R2                                                        
         CLC   NUSDAFDT,IDAT       TEST RECORD CHANGED                          
         BE    *+14                                                             
         MVI   BYTE,1              SET WRITEBACK NEEDED                         
         MVC   NUSDAFDT,IDAT                                                    
*                                                                               
NTU69X   DS    0H                                                               
NTU70    DS    0H                  AFFID FILM ELEMS                             
         CLI   TRAFPOST,C'Y'       TEST POSTING TRAFFIC INFO                    
         BNE   NTU73                                                            
*                                                                               
         LA    R2,NUDATA                                                        
         XC    HALF,HALF                                                        
         XC    FULL,FULL                                                        
*                                                                               
NTU70D   DS    0H                                                               
         CLI   0(R2),X'24'         COMMERCIAL ELEMENT                           
         BE    NTU70F                                                           
         BH    NTU70K                                                           
         CLI   0(R2),0                                                          
         BE    NTU70K                                                           
*                                                                               
NTU70E   DS    0H                                                               
         LLC   R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     NTU70D                                                           
*                                                                               
NTU70F   DS    0H                  TEST MATCHES FIRST PRD/FILM                  
         OC    FULL,FULL                                                        
         BNZ   *+8                                                              
         ST    R2,FULL             KEEP A(ELEM)                                 
         OC    IDAT,IDAT           TEST HAVE INVOICE ITEM                       
         BZ    NTU70J              NO, DELETE FILM ELEM                         
         USING NUPCELD,R2                                                       
         CLC   NUPCAPRD,IPRDNT     MATCH ON 3 CHARACTER PRODUCT?                
         BNE   NTU70H              NO                                           
         CLC   NUPCCML,IFCOD1                                                   
         BNE   NTU70H                                                           
         GOTO1 =A(SETICFLG),DMCB,BYTE  SET INV CML FLAG ON BYTE                 
         CLC   NUPCMLFL,BYTE       STILL HAVE THE SAME AD-ID/HDEF/CNTR?         
         BNE   NTU70H              NO - FLAG CHANGED - DELETE THIS ELEM         
         TM    HALF,X'80'          IF ALREADY HAVE THIS PRD/FILM                
         BNZ   NTU70J              THEN DELETE THIS DUPLICATE                   
         OI    HALF,X'80'          SET HAVE FIRST FILM OK                       
         B     NTU70E                                                           
*                                                                               
NTU70H   DS    0H                                                               
         CLC   NUPCAPRD,IPRD2NT    MATCH ON 3 CHARACTER PRODUCT?                
         BNE   NTU70J              NO                                           
         CLC   NUPCCML,IFCOD2                                                   
         BNE   NTU70J                                                           
         TM    HALF,X'40'          IF ALREADY HAVE THIS PRD/FILM                
         BNZ   NTU70J              THEN DELETE THIS DUPLICATE                   
         OI    HALF,X'40'          SET HAVE 2ND FILM OK                         
         B     NTU70E                                                           
*                                                                               
NTU70J   DS    0H                  DOESN'T MATCH EITHER PRD/FILM                
         GOTO1 RECUP,DMCB,(C'U',ANIO2),(R2),0,0   DELETE IT                     
         MVI   BYTE,1              SET REC ALTERED                              
         B     NTU70D                                                           
*                                                                               
NTU70K   DS    0H                  END OF RECORD                                
         OC    IDAT,IDAT           TEST HAVE INVOICE ITEM                       
         BZ    NTU73               NO, DO NOT ADD FILM THEN!!!                  
         OC    FULL,FULL                                                        
         BNZ   *+8                                                              
         ST    R2,FULL             KEEP ELEM INSERT ADDRESS                     
*                                                                               
         TM    HALF,X'80'          WAS 1ST FILM ALREADY THERE                   
         BNZ   NTU72                                                            
         OC    IFCOD1,IFCOD1       TEST FILM 1 PRESENT                          
         BZ    NTU72                                                            
         CLC   IFCOD1,SPACES       DON'T ADD X'24' ELEM WITH SPACES!            
         BE    NTU72                                                            
         LA    R2,WORK                                                          
         XC    WORK,WORK                                                        
         MVI   NUPCELD,X'24'                                                    
         MVI   NUPCLEN,NUPCELN                                                  
         MVC   NUPCPRD,IPRD                                                     
         MVC   NUPCCML,IFCOD1                                                   
*                                                                               
         GOTO1 =A(SETICFLG),DMCB,NUPCMLFL  SET INV CML FLAG                     
         MVC   NUPCAPRD,IPRDNT     3 CHARACTER PRODUCT                          
         L     R3,FULL             WHERE TO ADD ELEM                            
         GOTO1 RECUP,DMCB,(C'U',ANIO2),WORK,(R3),0                              
         MVI   BYTE,1              SET REC ALTERED                              
*                                                                               
NTU72    DS    0H                                                               
         TM    HALF,X'40'          WAS 2ND FILM ALREADY THERE                   
         BNZ   NTU73                                                            
         OC    IFCOD2,IFCOD2       TEST FILM 2 PRESENT                          
         BZ    NTU73                                                            
         CLC   IFCOD2,SPACES       DON'T ADD X'24' ELEM WITH SPACES!            
         BE    NTU73                                                            
         LA    R2,WORK                                                          
         XC    WORK,WORK                                                        
         MVI   NUPCELD,X'24'                                                    
         MVI   NUPCLEN,NUPCELN                                                  
         MVC   NUPCPRD,IPRD2                                                    
         MVC   NUPCCML,IFCOD2                                                   
         GOTO1 =A(SETICFLG),DMCB,NUPCMLFL  SET INV CML FLAG                     
         MVC   NUPCAPRD,IPRD2NT    3 CHARACTER PRODUCT                          
         L     R3,FULL             WHERE TO ADD ELEM                            
         GOTO1 RECUP,DMCB,(C'U',ANIO2),WORK,(R3),0                              
         MVI   BYTE,1              SET REC ALTERED                              
*                                                                               
*        WRITE BACK INVOICE NUMBER TO UNIT RECORD                               
*                                                                               
NTU73    DS    0H                                                               
*                                                                               
*        GET INVOICE NUMBER FROM TABLE                                          
*                                                                               
         XC    INVNUMB,INVNUMB     CLEAR INVOICE NUMBER                         
         OC    IDAT,IDAT           TEST HAVE INVOICE ITEM                       
         BZ    NTU73C              NO, CLEAR ANY INVOICE NUMBER                 
*                                                                               
         L     R2,AINOLST                                                       
         USING INOLSTD,R2                                                       
NTU73A   CLC   IINVID,INOLNUM      SAME INTERNAL INVOICE NUMBER                 
         BE    NTU73B                                                           
         AH    R2,INOLSTQ          NEXT INVOICE                                 
         CLI   0(R2),C' '          ANY MORE                                     
         BH    NTU73A                                                           
         B     NTU78               JUST SKIP THEN                               
NTU73B   MVC   INVNUMB,INOLINV     INVOICE NUMBER                               
         DROP  R2                                                               
*                                                                               
         USING NUDTAD,R2           NOW PUT INVOICE IN ELEM                      
NTU73C   LA    R2,NUDATA                                                        
NTU73D   DS    0H                                                               
         CLI   0(R2),X'18'         EXTRA INFO ELEM -                            
         BE    NTU73F                                                           
         BH    NTU73K              ELEMS IN ORDER - NEED TO ADD ONE             
         CLI   0(R2),0                                                          
         BE    NTU73K              NO ELEM - GO ADD                             
         LLC   R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     NTU73D                                                           
*                                                                               
NTU73F   DS    0H                                                               
         CLI   I2IPROF+1,C'Y'      INT ONLY RUN?                                
         BNE   NTU73G                                                           
         CLC   NUDTIIVN,INVNUMB    INT ONLY = SET INT INV                       
         BE    NTU78               THEN DONE                                    
         MVC   NUDTIIVN,INVNUMB    SET INVOICE NUMBER                           
         MVI   BYTE,1              SET REC ALTERED                              
         B     NTU78               THEN DONE                                    
*                                                                               
NTU73G   CLC   NUDTINVN,INVNUMB    SET TIME INVOICE NUMBER                      
         BE    NTU73H                                                           
         MVC   NUDTINVN,INVNUMB    SET INVOICE NUMBER                           
         MVI   BYTE,1              SET REC ALTERED                              
*                                                                               
NTU73H   CLI   I2IPROF,C'Y'        TIME PLUS INTEGRATION                        
         BNE   NTU78                                                            
         CLC   NUDTIIVN,INVNUMB    SET BOTH INV NUMBS                           
         BE    NTU78               THEN DONE                                    
         MVC   NUDTIIVN,INVNUMB    SET INVOICE NUMBER                           
         MVI   BYTE,1              SET REC ALTERED                              
         B     NTU78               THEN DONE                                    
*                                                                               
NTU73K   DS    0H                                                               
         ST    R2,FULL             SAVE ADDRESS TO ADD ELEM                     
         LA    R2,WORK             NEED TO ADD X'18' ELEM                       
         XC    WORK,WORK                                                        
         MVI   NUDTAEL,X'18'                                                    
         MVI   NUDTALEN,50                                                      
*                                                                               
         CLI   I2IPROF+1,C'Y'      INT ONLY=Y=DON'T SET TIME INV                
         BE    NTU73M                  ONLY SET INT INV                         
         MVC   NUDTINVN,INVNUMB    TIME INVOICE NUMBER                          
         CLI   I2IPROF,C'Y'        TIME+INT=Y=SET BOTH                          
         BNE   *+10                                                             
NTU73M   MVC   NUDTIIVN,INVNUMB    SET INT INVOICE NUMBER                       
*                                                                               
         L     R3,FULL             WHERE TO ADD ELEM                            
         GOTO1 RECUP,DMCB,(C'U',ANIO2),WORK,(R3),0                              
         MVI   BYTE,1              SET REC ALTERED                              
*                                                                               
NTU78    DS    0H                                                               
         CLI   RCWRITE,C'Y'                                                     
         BNE   NTU78B                                                           
         CLI   BYTE,0              TEST NEED TO WRITE                           
         BE    NTU78D                                                           
         GOTO1 DATAMGR,DMCB,PUTREC,=C'UNTFILE',BYDISK,NURECD,NDMWRK             
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
NTU78B   DS    0H                                                               
         B     NTU78D              **NO-OP RECORD DUMP                          
         LH    R2,NURLEN           DUMP RECORD                                  
         CHI   R2,858              13 LINES WORTH MAX                           
         BNH   *+8                                                              
         LHI   R2,858                                                           
         MVC   P(19),=C'NETWORK UNIT REC'                                       
         GOTO1 HEXOUT,DMCB,BYDISK,P+22,4,=C'N'                                  
         GOTO1 (RF),(R1),NURECD,P2,(R2)                                         
         GOTO1 AIRPRT                                                           
*                                                                               
NTU78D   DS    0H                                                               
         OC    IDAT,IDAT           IF UNMATCHING                                
         BNZ   NTU80                                                            
         TM    BYSTAT2,BYSTMIRQ    AND ACTUAL UNIT THAT HAS A MIRROR ?          
         BNO   NTU80                                                            
         TM    BYSTAT2,BYSTMGHQ    AND NOT MATCHED TO MIR GHOST                 
         BO    NTU80                                                            
         MVC   IDAT,SVIDAT         REPLACE SAVED AFFID DATE                     
*                                                                               
NTU80    J     NTUX                                                             
         DROP  R5,R6,R7                                                         
         LTORG                                                                  
*                                                                               
ANIO2    DS    A                                                                
SVIDAT   DS    XL2                                                              
INVNUMB  DS    CL10                                                             
         DS    0D                                                               
NDMWRK   DS    XL96                                                             
         DS    0D                                                               
NIOAREA2 DS    XL6000              MY OWN IO AREA                               
*                                                                               
         EJECT                                                                  
SETICFLG NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         L     R2,0(R1)            SET INV CML FLAG HERE                        
         TM    IFCODADI,X'80'+X'40'+X'20' COMML IS AD-ID                        
         BZ    *+8                 NO                                           
         OI    0(R2),NUPCADF1      YES - INDICATE AD-ID                         
         TM    IFCODADI,X'40'      COMML IS HDEF?                               
         BZ    *+8                 NO                                           
         OI    0(R2),NUPCHDF1      YES - INDICATE HDEF                          
         TM    IFCODADI,X'20'      COMML IS CENTERCUT?                          
         BZ    *+8                 NO                                           
         OI    0(R2),NUPCCTF1      YES - INDICATE CENTERCUT                     
         CLI   IFCODADI,X'20'      ONLY CENTERCUT FLAG IS ON?                   
         BNE   *+8                 NO                                           
         MVI   0(R2),0             YES - WE HAVE ISCII!                         
         J     NTUX                EXIT                                         
*---------------------------------------------------------------                
*        CHECK MASTER RECORD (NON CLIENT SPECIFIC) FOR MIRROR                   
*              FLAG THAT INDICATES STATION INVOICES MIRRORS WITH                
*              2 DETAILS PER MIRROR UNIT SO CREATE DUMMY UNIT                   
*---------------------------------------------------------------                
*                                                                               
CHKSTMIR NTR1  BASE=*,LABEL=*                                                   
         CLI   I2APROF+2,C'Y'    IF THEY DON'T WANT MIRRORS                     
         BNE   CMIRX               THEN DON'T BOTHER                            
         CLI   NBACTNET,X'FF'    IF DONE                                        
         BE    CMIRX               THEN DON'T BOTHER                            
         MVI   KEY,C'0'                                                         
         MVC   KEY+1(16),KEY                                                    
         MVI   KEY,C'S'                                                         
         MVC   KEY+1(1),QMED                                                    
         MVC   KEY+2(4),NBACTNET                                                
         MVI   KEY+6,C'N'                                                       
         MVC   KEY+7(2),QAGY                                                    
         GOTO1 READSTA                                                          
         USING STARECD,R6                                                       
         L     R6,ADSTAT                                                        
         TM    SFLAG1,SMIRRORS                                                  
         BO    *+8                                                              
         MVI   I2APROF+2,C'N'    STATION DOESN'T EXPAND MIRROR UNITS?           
         DROP  R6                                                               
CMIRX    J     NTUX                                                             
         EJECT                                                                  
*---------------------------------------------------------------                
*        SAVE PATTERN REFERENCE NUMBER IN APATREF TABLE                         
*---------------------------------------------------------------                
*                                                                               
SAVEPREF NTR1  BASE=*,LABEL=*                                                   
         USING BYELEMD,R5          R5 POINTS TO SAVED BUY DATA                  
*                                                                               
         L     R1,APATREF                                                       
SPR10    OC    0(3,R1),0(R1)                                                    
         BZ    SPR20               ADD HERE                                     
         CLC   BYPREF,0(R1)                                                     
         BE    SPRX                ALREADY IN TABLE                             
         LA    R1,3(R1)                                                         
         B     SPR10                                                            
*                                                                               
SPR20    MVC   0(3,R1),BYPREF                                                   
*                                                                               
SPRX     J     NTUX                                                             
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------                
*        ADD ANOTHER UNIT TO TABLE FOR MIRROR TIME AND ZERO COST                
*---------------------------------------------------------------                
*                                                                               
MIRROR   NTR1  BASE=*,LABEL=*                                                   
         USING BYELEMD,R5          R5 POINTS TO SAVED BUY DATA                  
*                                                                               
         LA    R1,MIRTAB                                                        
MIR10    CLC   NBMIRTYP,0(R1)      MATCH ON LETTER                              
         BE    MIR20                                                            
         LA    R1,6(R1)                                                         
         CLI   0(R1),X'FF'                                                      
         BNE   MIR10                                                            
         B     MIRX                                                             
*                                                                               
MIR20    MVC   HALF,2(R1)                                                       
         MVC   HALF2,4(R1)         DUAL MIRROR                                  
*                                                                               
MIR30    SR    R1,R1                                                            
         ICM   R1,3,BYSTIM                                                      
         AH    R1,HALF                                                          
         CHI   R1,2400                                                          
         BL    *+8                                                              
         SHI   R1,2400                                                          
         STCM  R1,3,FULL                                                        
*                                                                               
         ICM   R1,3,BYETIM                                                      
         AH    R1,HALF                                                          
         CHI   R1,2400                                                          
         BL    *+8                                                              
         SHI   R1,2400                                                          
         STCM  R1,3,FULL+2                                                      
*                                                                               
         OI    BYSTAT2,BYSTMIRQ    X'08' TO IND MIRROR                          
         OC    HALF2,HALF2         DUAL MIRROR?                                 
         BZ    MIR35               NO                                           
         CLC   HALF,HALF2          SECOND DUAL MIRROR?                          
         BNE   MIR34               NO                                           
         MVI   BYNSTAT,0           CLEAR THIS FLAG!                             
         OI    BYNSTAT,BYNDMIR2    YES - DUAL MIRROR2                           
         B     MIR36               DON'T BUMP MIRSEQ!                           
*                                                                               
MIR34    OI    BYNSTAT,BYNDMIR1    DUAL MIRROR1                                 
*                                                                               
MIR35    LH    R1,MIRSEQ           BUMP MIR SEQ NUMBER                          
         LA    R1,1(R1)                                                         
         STH   R1,MIRSEQ                                                        
*                                                                               
MIR36    MVC   BYMIRSEQ,MIRSEQ     AND SET IN UNIT AND GHOST                    
*                                                                               
MIR40    MVC   TEMPBUY,0(R5)       WILL BE TURNED ON IN BOTH                    
*                                                                               
         LA    R5,BYELEML(R5)     NEXT ENTRY                                    
         CLC   HALF,HALF2         DOING SECOND DUAL MIRROR?                     
         BNE   *+8                NO                                            
         LA    R5,BYELEML(R5)     YES - SKIP OVER 1ST GHOST MIR                 
         C     R5,ABUFFX                                                        
         BNH   MIR50                                                            
         MVI   ERROR,BUFERR                                                     
         B     MIRXNO                                                           
*                                                                               
MIR50    MVC   0(BYELEML,R5),TEMPBUY                                            
         MVC   BYSTIM(L'BYSTIM+L'BYETIM),FULL    MIRROR TIMES                   
         OI    BYSTAT2,BYSTMGHQ    X'01' TO IND MIRROR GHOST UNIT               
         XC    BYCOST,BYCOST                     ZERO COST                      
         XC    BYNET,BYNET                                                      
         MVC   BYORBNO,NBMIRTYP    SAVE MIRROR TYPE IN ORBIT#                   
         CLC   HALF,HALF2          JUST FINISHED DUAL MIRRORS?                  
         BE    MIRXYES             YES                                          
         OC    HALF2,HALF2         HAVE DUAL MIRROR TO PROCESS?                 
         BZ    MIRXYES             NO                                           
         SHI   R5,BYELEML                                                       
         MVC   HALF,HALF2          PROCESS SECOND MIRROR                        
         B     MIR30                                                            
*                                                                               
MIRXYES  SR    RC,RC                                                            
MIRXNO   LTR   RC,RC                                                            
MIRX     XIT1  REGS=(R5)                                                        
         DROP  R5                                                               
*                                                                               
TEMPBUY  DS    CL(BYELEML)                                                      
*                                                                               
*                                                                               
         DC    0H                                                               
MIRTAB   DC    C'A',X'00',H'240',H'0'   A = +4 HOURS                            
         DC    C'B',X'00',H'-300',H'0'  B = -5 HRS                              
         DC    C'C',X'00',H'-660',H'0'  C = -11                                 
         DC    C'D',X'00',H'-360',H'0'  D = -6                                  
         DC    C'E',X'00',H'-540',H'0'  E = -9                                  
         DC    C'F',X'00',H'180',H'0'   F = +3                                  
         DC    C'G',X'00',H'420',H'0'   G = +7                                  
         DC    C'H',X'00',H'-240',H'0'  H = -4                                  
         DC    C'I',X'00',H'390',H'0'   I = +6.5                                
         DC    C'J',X'00',H'450',H'0'   J = +7.5                                
         DC    C'P',X'00',H'180',H'360' P = +3 +6                               
         DC    C'M',X'00',H'180',H'300' M = +3 +5                               
         DC    C'N',X'00',H'120',H'240' N = +2 +4                               
         DC    C'Q',X'00',H'120',H'0'   Q = +2                                  
         DC    C'R',X'00',H'360',H'0'   R = +6                                  
         DC    X'FF'                                                            
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------                
*        CHECK IF GHOST UNIT MATCHES BEFORE SEEDING ACTUAL UNIT                 
*---------------------------------------------------------------                
*                                                                               
MIRMATCH NTR1  BASE=*,LABEL=*                                                   
         USING IELEM,R6            R6 POINTS TO MIRROR INVOICE DETAIL           
         USING BYELEMD,R5          R5 POINTS TO BYELEM                          
*                                                                               
         L     R7,AFBY                                                          
MMT10    C     R7,ALBY                                                          
         BH    MMTXNO              SAY NO IF CAN'T FIND GHOST                   
         CLC   BYMIRSEQ,BYMIRSEQ-BYELEM(R7)                                     
         BNE   MMT20                                                            
         CR    R5,R7               SKIP COMPARING TO ITSELF                     
         BNE   MMT30                                                            
MMT20    LA    R7,BYELEML(R7)                                                   
         B     MMT10                                                            
*                                                                               
MMT30    CLI   BYUNACH-BYELEM(R7),0     ANY UNMATCHED ON GHOST                  
         BNE   MMTXNO              THEN SAY NO TO AFFID SEEDING                 
         TM    BYNSTAT,BYNDMIR1    FIRST DUAL MIRROR?                           
         BNZ   MMT20               YES - GO BACK FOR SECOND                     
*                                                                               
MMTXYES  SR    RC,RC                                                            
MMTXNO   LTR   RC,RC                                                            
MMTX     J     NTUX                                                             
         DROP  R5,R6                                                            
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------                
*        WHEN POSTING AFFID TIME FOR GHOST UNIT - POST ACTUAL TIME!             
*---------------------------------------------------------------                
*                                                                               
UNMIRROR NTR1  BASE=*,LABEL=*                                                   
         USING IELEM,R6            R5 POINTS TO MIRROR INVOICE DETAIL           
         USING BYELEMD,R5          R5 POINTS TO BYELEM                          
*                                                                               
         LA    R1,UNMIRTAB                                                      
UMR10    CLC   BYORBNO,0(R1)       MATCH ON MIRROR LETTER                       
         BE    UMR20                                                            
         LA    R1,4(R1)                                                         
         CLI   0(R1),X'FF'                                                      
         BNE   UMR10                                                            
         B     UMRX                                                             
*                                                                               
UMR20    MVC   HALF,2(R1)          OFFSET TIME                                  
         LH    R1,HALF             CONVERT TO MILITARY                          
         SR    R0,R0                                                            
         D     R0,=F'60'                                                        
         MHI   R1,100                                                           
         AR    R0,R1                                                            
         CHI   R0,2400                                                          
         BNH   *+8                                                              
         SHI   R0,2400                                                          
         STH   R0,HALF                                                          
*                                                                               
         SR    R1,R1                                                            
         ICM   R1,3,DUB            GHOST MIRROR TIME                            
         AHI   R1,2400             +24 HRS                                      
         SH    R1,HALF             SUBTRACT THE MIRROR OFFSET                   
         CHI   R1,2400                                                          
         BL    *+8                                                              
         SHI   R1,2400                                                          
         STCM  R1,3,DUB            TO GET THE ACTUAL TIME                       
*                                                                               
UMRXYES  SR    RC,RC                                                            
UMRXNO   LTR   RC,RC                                                            
UMRX     J     NTUX                                                             
         DROP  R5,R6                                                            
*                                                                               
         DC    0H                                                               
UNMIRTAB DC    C'A',X'00',H'240'       A = +4 HOURS                             
         DC    C'B',X'00',H'-300'      B = -5 HRS                               
         DC    C'C',X'00',H'-660'      C = -11                                  
         DC    C'D',X'00',H'-360'      D = -6                                   
         DC    C'E',X'00',H'-540'      E = -9                                   
         DC    C'F',X'00',H'180'       F = +3                                   
         DC    C'G',X'00',H'420'       G = +7                                   
         DC    C'H',X'00',H'-240'      H = -4                                   
         DC    C'I',X'00',H'390'       I = +6.5                                 
         DC    C'J',X'00',H'450'       J = +7.5                                 
         DC    X'FF'                                                            
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------                
*        SETSEDAY - ROUTINE TO SET START-END DAY FROM DAY ROTATOR               
*                   ON INPUT BYTE HAS ROTATOR, ON OUTPUT HAS                    
*                   START DAY IN HIGH NIBBLE, END IN LOW,                       
*---------------------------------------------------------------                
SETSEDAY NTR1  BASE=*,LABEL=*                                                   
         SR    R0,R0                                                            
         SR    R1,R1                                                            
         LA    RF,SEDLST                                                        
         LA    R3,7                                                             
         LA    R5,SEDLST-1                                                      
*                                                                               
SSED4    DS    0H                                                               
         LLC   RE,0(RF)                                                         
         EX    RE,SEDTM            IS DAY ACTIVE?                               
         BZ    SSED6                                                            
         LR    RE,RF               YES                                          
         SR    RE,R5                                                            
         LTR   R0,R0               IS IT THE FIRST?                             
         BNZ   *+6                                                              
         LR    R0,RE               SET AS FIRST                                 
         LR    R1,RE               LASTEST IS LAST                              
*                                                                               
SSED6    DS    0H                                                               
         LA    RF,1(RF)                                                         
         BCT   R3,SSED4                                                         
*                                                                               
         SLL   R0,4                                                             
         OR    R0,R1               GET INTO 8 BITS                              
         STC   R0,BYTE             RETURN IN BYTE                               
*                                  SET BUY SPOT END DATE                        
SSEDX    J     NTUX                                                             
*                                                                               
SEDLST   DC    X'40201008040201'                                                
SEDTM    TM    BYTE,0                                                           
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------                
*        CONVERT MIL TIMES TO MINS                                              
*---------------------------------------------------------------                
CVMIN    NTR1  BASE=*,LABEL=*                                                   
         LM    R2,R3,0(R1)         R2 = INPUT, R3=OUTPUT                        
         LA    R0,2                                                             
         MVC   DUB(4),0(R2)                                                     
         OC    DUB+2(2),DUB+2      IF END TIME ZERO                             
         BNZ   *+10                                                             
         MVC   DUB+2(2),DUB        SET END = START                              
         LA    R2,DUB                                                           
*                                                                               
CVMIN2   DS    0H                                                               
         LH    R5,0(R2)                                                         
         CHI   R5,600                                                           
         BNL   *+8                                                              
         AHI   R5,2400                                                          
         SR    R4,R4                                                            
         D     R4,=F'100'                                                       
         MHI   R5,60                                                            
         AR    R4,R5               R4 = MINUTES                                 
         STC   R4,1(R3)                                                         
         SRL   R4,8                                                             
         STC   R4,0(R3)                                                         
         LA    R2,2(R2)                                                         
         LA    R3,2(R3)                                                         
         BCT   R0,CVMIN2                                                        
         J     NTUX                                                             
         LTORG                                                                  
*                                                                               
         EJECT                                                                  
*---------------------------------------------------------------                
*        DSECTS AND STUFF                                                       
*---------------------------------------------------------------                
*                                                                               
NETBLKD  DSECT                                                                  
         PRINT OFF                                                              
       ++INCLUDE NETBLOCKN                                                      
       ++INCLUDE NEGENUNIT                                                      
       ++INCLUDE SPTRCMML                                                       
STARECD  DSECT                                                                  
       ++INCLUDE SPGENSTA                                                       
       ++INCLUDE SPREPI2WKN                                                     
       ++INCLUDE DDBUFFD                                                        
       ++INCLUDE SPREPWORKD                                                     
         PRINT ON                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'137SPREPI2NU 03/04/21'                                      
         END                                                                    
