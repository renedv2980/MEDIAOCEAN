*          DATA SET PRWRIDLR   AT LEVEL 033 AS OF 02/20/20                      
*CATALP PRWRIDLR                                                                
         TITLE 'PRWRIDLR - MONEY KEYWORDS - ENTRY POINTS'                       
         ENTRY I$COMBO                                                          
         ENTRY IINVDLR                                                          
         ENTRY IDOLLAR                                                          
         ENTRY IREBATE                                                          
         ENTRY GETDLRS                                                          
PRWRIDLR CSECT                                                                  
         TITLE 'PRWRIDLR - CHANGE LOG'                                          
         TITLE 'PRWRIDLR - MONEY KEYWORDS - INPUT - IDLR'                       
***********************************************************************         
*                                                                     *         
*        FINANCIAL DATA ROUTINES                                      *         
*                                                                     *         
*        I$COMBO   COMBINE  DOLLAR INPUTS ROUTINE                     *         
*        IINVDLR   INVOICE RECORD DOLLARS ROUTINE                     *         
*        IDOLLAR   STANDARD DOLLAR INPUT ROUTINE                      *         
*        IREBATE   REBATE   DOLLAR INPUT ROUTINE                      *         
*        GETDLRS   INTERFACE TO GETINS                                *         
*                                                                     *         
*NTRY                                                                 *         
*                                                                     *         
*        R2==>  DRIVER INPUT AREA                                     *         
*        R8==>  SPOOL WORKAREA                                        *         
*        R9==>  PRINTWRITER WORKAREA                                  *         
*        RA==>  DRIVER GLOBAL AREA                                    *         
*        RC==>  GENCON WORKAREA                                       *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         PRINT NOGEN                                                            
         TITLE 'PRWRIDLR - COMBINED $ AMOUNTS - INPUT - I$COMBO'                
***********************************************************************         
*                                                                     *         
*        ROUTINE TO EXTRACT COMBINE $ AMOUNTS FROM BUY                *         
*                                                                     *         
*NTRY    GLARGS   = RECORD ID                                         *         
*                   C'B' - BUY      RECORD                            *         
*        GLARGS+1 = AMOUNT TYPE                                       *         
*                   C'O'  - ORDERED                                   *         
*                   C'B'  - BILLED                                    *         
*                   C'D'  - BILLED - PAID                             *         
*                   C'P'  - PAID                                      *         
*                   C'L'  - BILLABLE                                  *         
*                   C'Q'  - PAYABLE                                   *         
*                   C'1'  - OPEN ORDERED                              *         
*                   C'2'  - OPEN BILLED                               *         
*                   C'3'  - OPEN PAID                                 *         
*                   C'4'  - OPEN BILLABLE                             *         
*                   C'5'  - OPEN PAYABLE                              *         
*        GLARGS+2 = DATA TYPE                                         *         
*                   C'1'  - GROSS                                     *         
*                   C'2'  - GROSS LESS CASH DISCOUNT                  *         
*                   C'3'  - NET   LESS CASH DISCOUNT                  *         
*                   C'4'  - NET                                       *         
*                   C'5'  - CASH  DISCOUNT                            *         
*                   C'6'  - AGENCY COMMISSION                         *         
*                   C'7'  - TAX                                       *         
*                   C'8'  - GST                                       *         
*                   C'9'  - PST                                       *         
*        GLARGS+5 = GST INDICATOR                                     *         
*                   X'20' - INCLUDE INPUT  SIDE GST                   *         
*                   X'10' - INCLUDE OUTPUT SIDE GST                   *         
*        GLARGS+7 = FLOWCHART INDICATOR                               *         
*                   C'$'  - FLOWCHARTING DOLLAR AMOUNTS               *         
*        GLARGS+8 = DATETYPE FILTER                                   *         
*                   C'A'  - PAID   DATES                              *         
*                   C'L'  - BILLED DATES                              *         
*                   C'T'  - BILLED/PAID DATES                         *         
*                                                                     *         
*        R2==>  DRIVER INPUT AREA                                     *         
*                                                                     *         
*EXIT   0-7    -  PL8 DOLLARS                                         *         
*       8-15   -  PL8                                                 *         
*                                                                     *         
***********************************************************************         
***********************************************************************         
* USER    JIRA       DATE                  CHANGE LOG                 *         
* ---- ----------  -------- ----------------------------------------- *         
* AKAT SPEC-43345  02/20/20 REPORT CLEARED WHEN TOTAL FOR INSERTIONS  *         
*                           IS NEGATIVE BUT CLEARANCE IS POSITIVE     *         
***********************************************************************         
         EJECT                                                                  
         DS    0D                                                               
I$COMBO  NMOD1 0,**#I$CM                                                        
*                                                                               
         USING SPOOLD,R8           ESTABLISH SPOOL WORKAREA                     
         USING SYSD,R9             ESTABLISH PRINTWRITER WORKAREA               
         USING GLOBALD,RA          ESTABLISH DRIVER WORKAREA                    
*                                                                               
         LA    R7,PBLOCK           ESTABLISH PRINT BLOCK                        
         USING PBLOCK,R7                                                        
*                                                                               
         L     RC,GLAWORKD         RESET WORKING STORAGE POINTER                
         USING GEND,RC                                                          
*                                                                               
         MVC   I$CMTYPE,GLARGS+1   SAVE DESIRED AMOUNT TYPE                     
*                                                                               
         CLI   GLARGS+1,C'L'       IF BILLABLE WANTED                           
         BNE   *+8                                                              
         MVI   GLARGS+1,C'B'          ASK FOR BILLED DOLLARS                    
*                                                                               
         CLI   GLARGS+1,C'Q'       IF PAYABLE WANTED                            
         BE    *+8                                                              
         CLI   GLARGS+1,C'D'       IF BILLED - PAID WANTED                      
         BNE   *+8                                                              
         MVI   GLARGS+1,C'P'          ASK FOR PAID   DOLLARS                    
*                                                                               
         CLI   GLARGS+1,C'4'       IF OPEN BILLABLE WANTED                      
         BNE   *+8                                                              
         MVI   GLARGS+1,C'2'          ASK FOR OPEN BILLED DOLLARS               
*                                                                               
         CLI   GLARGS+1,C'5'       IF OPEN PAYABLE WANTED                       
         BNE   *+8                                                              
         MVI   GLARGS+1,C'3'          ASK FOR OPEN PAID   DOLLARS               
*                                                                               
         GOTO1 =A(IDOLLAR)         GET AMOUNTS                                  
*                                                                               
         ZAP   I$CMDLR,0(8,R2)     SAVE  DOLLARS                                
         ZAP   I$CMDLRT,8(8,R2)    SAVE TEST DOLLARS                            
*                                                                               
         MVI   GLARGS+1,C'O'       DEFAULT TO ORDERED AMOUNTS                   
*                                                                               
         CLI   I$CMTYPE,C'1'       IF OPEN AMOUNTS                              
         BL    *+8                                                              
         MVI   GLARGS+1,C'1'       ASK FOR OPEN ORDERED AMOUNTS                 
*                                                                               
         CLI   I$CMTYPE,C'D'       IF BILLED MINUS PAID AMOUNTS                 
         BNE   *+8                                                              
         MVI   GLARGS+1,C'B'       ASK FOR BILLED AMOUNTS                       
*                                                                               
         GOTO1 =A(IDOLLAR)         GET AMOUNTS                                  
*                                                                               
         SP    0(8,R2),I$CMDLR     RESULT IS ORDERED-AMOUNT TYPE                
         SP    8(8,R2),I$CMDLRT    TEST AMOUNT                                  
*                                                                               
         MVC   GLARGS+1(1),I$CMTYPE RESTORE TYPE                                
*                                                                               
I$COMBOX DS    0H                                                               
         XIT1                                                                   
*                                                                               
         DS    CL256               FOR NEW Z/OS PROCESSOR                       
I$CMTYPE DS    X                   ASKED FOR AMOUNT TYPE                        
I$CMDLR  DS    PL8                 INTERMEDIATE SAVEAREA FOR AMOUNT             
I$CMDLRT DS    PL8                 INTERMEDIATE TEST SAVEAREA                   
*                                                                               
         LTORG                                                                  
*                                                                               
         DROP                                                                   
*                                                                               
         TITLE 'PRWRIDLR - INVOICE RECORD AMOUNTS - INPUT - IINVDLR'            
***********************************************************************         
*                                                                     *         
*        ROUTINE TO EXTRACT DOLLAR  AMOUNTS FROM INVOICE DETAIL       *         
*                                                                     *         
*NTRY    GLARGS   = RECORD ID                                         *         
*                   C'X' - INVOICE  RECORD                            *         
*        GLARGS+1 = AMOUNT TYPE                                       *         
*                   C'I'  - ORDERED                                   *         
*        GLARGS+2 = DATA TYPE                                         *         
*                   C'1'  - GROSS                                     *         
*                   C'2'  - GROSS LESS CASH DISCOUNT                  *         
*                   C'3'  - NET   LESS CASH DISCOUNT                  *         
*                   C'4'  - NET                                       *         
*                   C'5'  - CASH  DISCOUNT                            *         
*                   C'6'  - AGENCY COMMISSION                         *         
*                   C'7'  - TAX                                       *         
*                   C'8'  - GST                                       *         
*                   C'9'  - PST                                       *         
*        GLARGS+5 = GST INDICATOR                                     *         
*                   X'20' - INCLUDE INPUT  SIDE GST                   *         
*                   X'10' - INCLUDE OUTPUT SIDE GST                   *         
*        GLARGS+7 = FLOWCHART INDICATOR                               *         
*                   C'$'  - FLOWCHARTING DOLLAR AMOUNTS               *         
*        GLARGS+8 = DATETYPE FILTER                                   *         
*                                                                     *         
*        R2==>  DRIVER INPUT AREA                                     *         
*                                                                     *         
*EXIT   0-7    -  PL8 DOLLARS                                         *         
*       8-15   -  PL8                                                 *         
*                                                                     *         
***********************************************************************         
         EJECT                                                                  
         DS    0D                                                               
IINVDLR  NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         USING SPOOLD,R8           ESTABLISH SPOOL WORKAREA                     
         USING SYSD,R9             ESTABLISH PRINTWRITER WORKAREA               
         USING GLOBALD,RA          ESTABLISH DRIVER WORKAREA                    
*                                                                               
         LA    R7,PBLOCK           ESTABLISH PRINT BLOCK                        
         USING PBLOCK,R7           ESTABLISH PRINT BLOCK                        
*                                                                               
         L     RC,GLAWORKD         RESET WORKING STORAGE POINTER                
         USING GEND,RC             ESTABLISH WORKING STORAGE                    
*                                                                               
*        BUILD DUMMY BUY RECORD WITH INFO FROM INVOICE                          
*                                                                               
         CLI   PBMODE,PBPROCIV     SKIP IF NOT PROCESSING INVOICES              
         BNE   IINVDLRX                                                         
*                                                                               
         ICM   R5,15,PBIDTELA      ESTABLISH INVOICE DETAIL ELEMENT             
         BZ    IINVDLRX                                                         
         USING PIMDTLEL,R5                                                      
*                                                                               
         L     R4,AIO1             BUILD DUMMY BUY IN I/OAREA1                  
         USING PBUYREC,R4                                                       
         XC    0(256,R4),0(R4)     INIT DUMMY RECORD                            
*                                                                               
         XC    PASSPRD,PASSPRD     CLEAR - MAY HAVE BAD DATA                    
*                                                                               
*        BUILD KEY                                                              
*                                                                               
         MVC   PBUYKAGY,AGENCY     AGENCY                                       
         MVC   PBUYKMED,PBMED      MEDIA                                        
         MVI   PBUYKRCD,PBUYKIDQ   BUY RECORD CODE                              
         MVC   PBUYKCLT,PBCLT      CLIENT                                       
*                                                                               
         MVC   PBUYKPRD,PIMSPRD                                                 
         CLC   PIMCPRD,=CL3' '     IF CORRECTED PRODUCT PRESENT                 
         BNH   *+10                                                             
         MVC   PBUYKPRD,PIMCPRD       USE IT                                    
*                                                                               
         MVC   PBUYKPUB(6),PBPUB   PUB                                          
         MVC   PBUYKDAT,PIMIDATE                                                
         MVC   PBUYKEST,PIMIEST                                                 
*                                                                               
         LHI   RF,33+116           KEY PLUS DESC ELM LENGTH                     
         STCM  RF,3,PBUYLEN        LENGTH OF BASIC RECORD                       
*                                                                               
*        BUILD BUY DESCRIPTION ELEMENT                                          
*                                                                               
         MVI   PBDELEM,X'20'       ID                                           
         MVI   PBDELEM+1,116       LENGTH                                       
*                                                                               
         MVC   PBDBDATE,PIMIDATE   BILLABLE DATE                                
*                                                                               
         MVC   PBDCOS,PIMCOST      COST                                         
         MVC   PBDCOSIN,PIMCSIND   COST INDICATOR                               
*                                                                               
         MVI   PBDCOSTY,C'T'       ASSUME TOTAL RATE FIRST                      
         TM    PIMDSTAT,X'80'                                                   
         BZ    *+8                                                              
         MVI   PBDCOSTY,C'U'       DETAIL HAS UNIT RATE                         
*                                                                               
         MVC   PBDPDATE,PIMIDATE   PAYABLE DATE                                 
         MVC   PBDPRCOS,PIMPREM    PREMIUM COST                                 
*                                                                               
*        RETRIEVE DATA FROM PUB RECORD                                          
*                                                                               
         L     R3,AIO3             ESTABLISH PUBREC                             
         USING PUBRECD,R3                                                       
*                                                                               
         SR    RF,RF                                                            
         LA    R6,PUBREC+33        POINT TO FIRST ELEMENT IN PUBREC             
*                                                                               
         ZAP   PBDACP,=P'15000'    DEFAULT AGENCY COMMISSION                    
         ZAP   PBDCD,=P'20'        DEFAULT CASH DISCOUNT                        
*                                                                               
IIVDPBLP DS    0H                                                               
*                                                                               
         USING PUBGEND,R6          LOOK FOR GENERAL INFO ELM                    
*                                                                               
         CLI   PUBGENEL,0          DONE AT END OF PUB RECORD                    
         BE    IIVDPBDN                                                         
*                                                                               
         CLI   PUBGENEL,PUBGENQ    IF GENERAL INFO ELM                          
         BNE   IIVDPB10                                                         
*                                                                               
         ZAP   PBDACP,PUBAC        AGENCY COMMISSION                            
*                                                                               
         TM    PIMDSTAT,X'40'      ANY CASH DISCOUNT?                           
         BZ    *+14                                                             
         ZAP   PBDCD,=P'0'         NONE                                         
         B     *+10                                                             
         MVC   PBDCD,PUBCD         YES, USE PUB REC'S CASH DISCOUNT             
*                                                                               
         B     IIVDPBCN                                                         
*                                                                               
IIVDPB10 DS    0H                                                               
*                                                                               
         CLI   PUBGENEL,PUBTAXQ    IF TAX ELEMENT                               
         BNE   IIVDPB20                                                         
*                                                                               
         USING PUBTAXD,R6          ESTABLISH TAX ELEMENT                        
*                                                                               
         CLC   PIMIDATE,PUBTAX1D   USE THE APPROPRIATE TAX RATE BASED           
         BL    IIVDPB15                ON THE EFFECTIVE DATE                    
         MVC   PBDTAX,PUBTAX1                                                   
         CLC   PIMIDATE,PUBTAX2D                                                
         BL    IIVDPB15                                                         
         MVC   PBDTAX,PUBTAX2                                                   
         CLC   PIMIDATE,PUBTAX3D                                                
         BL    IIVDPB15                                                         
         MVC   PBDTAX,PUBTAX3                                                   
*                                                                               
IIVDPB15 DS    0H                                                               
*                                                                               
         B     IIVDPBCN                                                         
*                                                                               
IIVDPB20 DS    0H                                                               
*                                                                               
IIVDPBCN DS    0H                                                               
*                                                                               
         IC    RF,1(R6)            BUMP TO NEXT ELEMENT                         
         LA    R6,0(RF,R6)                                                      
         B     IIVDPBLP                                                         
*                                                                               
IIVDPBDN DS    0H                                                               
*                                                                               
         MVC   PBDUIND,PIMUIND     UNIT INDICATOR                               
         MVC   PBDUNITS,PIMUNITS   UNITS                                        
*                                                                               
         GOTOR IDOLLAR             GET AMOUNTS                                  
*                                                                               
         L     RF,AIO1             CLEAR DUMMY BUY RECORD                       
         XC    0(256,RF),0(RF)                                                  
*                                                                               
IINVDLRX DS    0H                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         DROP                                                                   
*                                                                               
         TITLE 'PRWRIDLR - DOLLAR AMOUNTS - INPUT - IDOLLAR'                    
***********************************************************************         
*                                                                     *         
*        ROUTINE TO EXTRACT DOLLAR AMOUNTS FROM BUY RECORD            *         
*                                                                     *         
*NTRY    GLARGS   = RECORD ID                                         *         
*                   'B' - BUY    RECORD DATA                          *         
*                   'I' - BUY    RECORD DATA AND BILL HEADER DATA     *         
*                   'J' - BUY    RECORD DATA FOUND IN PAY  ELEMENTS   *         
*                   'K' - CLEARANCE STATUS RECORD DATA                *         
*                   'M' - BUY    RECORD DATA FOUND IN BILL ELEMENTS   *         
*                   'N' - BUY    RECORD DATA FOUND IN PAY  ELEMENTS   *         
*                   '               DOES NOT REQUIRE CHECK TO BE CUT  *         
*                   'O' - BUY    RECORD DATA FOUND IN ACH  ELEMENTS   *         
*                   'Q' - CASH APPLICATION DATA                       *         
*                   'X' - INVOICE RECORD DATA                         *         
*                   'Y' - NEW INVOICE RECORD DATA                     *         
*                                                                     *         
***********************************************************************         
*        GLARGS+1 = AMOUNT TYPE                                       *         
*                   C'O'  - ORDERED                                   *         
*                   C'B'  - BILLED                                    *         
*                   C'P'  - PAID                                      *         
*                   C'C'  - PLANNED COST                              *         
*                   C'1'  - OPEN ORDERED                              *         
*                   C'2'  - OPEN BILLED                               *         
*                   C'3'  - OPEN PAID                                 *         
*                   C'I'  - INVOICED                                  *         
*        GLARGS+2 = DATA TYPE                                         *         
*                   C'1'  - GROSS                                     *         
*                   C'2'  - GROSS LESS CASH DISCOUNT                  *         
*                   C'3'  - NET   LESS CASH DISCOUNT                  *         
*                   C'4'  - NET                                       *         
*                   C'5'  - CASH  DISCOUNT                            *         
*                   C'6'  - AGENCY COMMISSION                         *         
*                   C'7'  - TAX                                       *         
*                   C'8'  - GST                                       *         
*                   C'9'  - PST                                       *         
*        GLARGS+5 = GST INDICATOR                                     *         
*                   X'20' - INCLUDE INPUT  SIDE GST                   *         
*                   X'10' - INCLUDE OUTPUT SIDE GST                   *         
*        DLRTYPE = DATATYPE FILTER - SET BY ROUTINE IF NEEDED         *         
*                   C'P'  - PAID   DATA                               *         
*                   C'B'  - BILLED DATA                               *         
*        GLARGS+7 = FLOWCHART INDICATOR                               *         
*                   C'$'  - FLOWCHARTING DOLLAR AMOUNTS               *         
*        GLARGS+8 = DATETYPE FILTER                                   *         
*                   C'A'  - PAID   DATES                              *         
*                   C'L'  - BILLED DATES                              *         
*                   C'T'  - BILLED/PAID DATES                         *         
*                                                                     *         
*        R2==>  DRIVER INPUT AREA                                     *         
*                                                                     *         
*EXIT   0-7    -  PL8 DOLLARS                                         *         
*       8-15   -  PL8 TEST BUY DOLLARS                                *         
*       CC    NE  FAILED A FILTER TEST                                *         
*                                                                     *         
***********************************************************************         
         EJECT                                                                  
         DS    0D                                                               
IDOLLAR  NMOD1 0,**#IDLR                                                        
*                                                                               
         USING SPOOLD,R8           ESTABLISH SPOOL WORKAREA                     
         USING SYSD,R9             ESTABLISH PRINTWRITER WORKAREA               
         USING GLOBALD,RA          ESTABLISH DRIVER WORKAREA                    
*                                                                               
         LA    R7,PBLOCK           ESTABLISH PRINT BLOCK                        
         USING PBLOCK,R7                                                        
*                                                                               
         L     RC,GLAWORKD         RESET WORKING STORAGE POINTER                
         USING GEND,RC                                                          
*                                                                               
         ZAP   0(8,R2),=P'0'       INIT OUTPUT                                  
         ZAP   8(8,R2),=P'0'       INIT OUTPUT                                  
*                                                                               
         L     R6,AIO1             ESTABLISH BUY RECORD                         
         USING PBUYRECD,R6                                                      
*                                                                               
         CLI   PBUYKRCD,PBUYKIDQ   SKIP IF NOT A BUY RECORD                     
         BNE   IDOLLARX                                                         
*                                                                               
*        FILTER BUY ON LIVE AND TEST STATUS IF NECESSARY                        
*                                                                               
         TM    GLARGS+5,X'08'      IF REPORTING ONLY TEST BUYS                  
         BNO   IDLRTST1                                                         
*                                                                               
         CLI   PBDBFD,C'T'            KEEP IF TEST BUY                          
         BE    IDLRTSTX                                                         
         B     IDLRDROP               ELSE ALL DONE                             
*                                                                               
IDLRTST1 DS    0H                                                               
*                                                                               
         TM    GLARGS+5,X'04'      IF REPORTING OMLY TEST BUYS                  
         BNO   IDLRTSTX                                                         
*                                                                               
         CLI   PBDBFD,C'T'            REJECT IF A TEST BUY                      
         BE    IDLRDROP                                                         
*                                                                               
IDLRTSTX DS    0H                                                               
*                                                                               
IDLRFLT  DS    0H                                                               
*                                                                               
         SR    RF,RF               INIT DATE POINTER                            
*                                                                               
         CLI   GLARGS+8,C'I'       IF INSERT DATE FILTER                        
         BNE   *+8                                                              
         LA    RF,PBUYKDAT            USE BUY INSERT DATE                       
*                                                                               
         CLI   GLARGS+8,C'B'       IF  BILLABLE DATE FILTER                     
         BNE   *+8                                                              
         LA    RF,PBDBDATE             USE BILLABLE DATE                        
*                                                                               
         CLI   GLARGS+8,C'P'       IF  PAYABLE DATE FILTER                      
         BNE   *+8                                                              
         LA    RF,PBDPDATE             USE PAYABLE  DATE                        
*                                                                               
         CLI   GLARGS+8,C'S'       IF  ON-STAND DATE FILTER                     
         BNE   *+8                                                              
         LA    RF,PBDSDATE             USE ON-SALE  DATE                        
*                                                                               
         CLI   GLARGS+8,C'M'       IF  MATERIALS DATE FILTER                    
         BNE   *+8                                                              
         LA    RF,PBDMDATE             USE MATERIALS CLOSING DATE               
*                                                                               
         CLI   GLARGS+8,C'C'       IF  CLOSING  DATE FILTER                     
         BNE   *+8                                                              
         LA    RF,PBDMDATE             USE CLOSING DATE                         
*                                                                               
         LTR   RF,RF               DONE IF NO DATE FOUND TO FILTER ON           
         BZ    IDLRFLTX                                                         
*                                                                               
*        DATE MUST LIE WITHIN RANGE                                             
*                                                                               
         TM    PBQPTSW,PBQPTDYQ    IF GROUPING BY YEAR                          
         BNO   DFLTSTYN                                                         
*                                     CHECK ONLY MONTH                          
*                                                                               
         CLC   1(2,RF),GLARGS+10                                                
         BL    IDLRDROP                                                         
         CLC   1(2,RF),GLARGS+13                                                
         BH    IDLRDROP                                                         
*                                                                               
         B     IDLRFLTX                                                         
*                                                                               
DFLTSTYN DS    0H                                                               
*                                                                               
         CLC   0(3,RF),GLARGS+9                                                 
         BL    *+14                REJECT - BEFORE START DATE                   
         CLC   0(3,RF),GLARGS+12                                                
         BNH   IDLRFLTX            OKAY ACCEPT                                  
*                                                                               
         B     IDLRDROP            ELSE ALL DONE                                
*                                                                               
IDLRFLTX DS    0H                                                               
*                                                                               
         MVC   IDLRPVLS,PBGRS      SAVE GETINS DATA FOR TOTAL BUY               
         MVC   IDLROVLS,OGROSS     OPEN RATES                                   
         MVC   IDLRGVLS,GVALUES    GST  RATES                                   
         MVC   IDLROGVL,GOVALUE    OPEN GST RATES                               
*                                                                               
         LA    RF,PSTAREA          PST DATA FOR 10 PROVINCES                    
         LA    RE,IDLRPSTS                                                      
         LA    R0,10                                                            
*                                                                               
         MVC   0(PSTAREAL,RE),0(RF) PST DATA FOR 1 PROVINCE                     
         LA    RE,PSTAREAL(RE)                                                  
         LA    RF,PSTAREAL(RF)                                                  
         BCT   R0,*-14                                                          
*                                                                               
         LA    RF,PSTOAREA         OPEN PST DATA FOR 10 PROVINCES               
         LA    RE,IDLROPST                                                      
         LA    R0,10                                                            
*                                                                               
         MVC   0(PSTAREAL,RE),0(RF) PST DATA FOR 1 PROVINCE                     
         LA    RE,PSTAREAL(RE)                                                  
         LA    RF,PSTAREAL(RF)                                                  
         BCT   R0,*-14                                                          
*                                                                               
*        IF FLOWCHARTING AND PROCESSING BILLED AND/OR PAID DATA                 
*           NEED TO GO TO GETINS TO CALCULATE VALUES FOR COLUMN'S               
*           PERIOD                                                              
*                                                                               
IDLTGET  DS    0H                                                               
*                                                                               
         MVI   IDLRSVSW,0          INIT SAVE INDICATOR                          
         MVI   DLRTYPE,0           INIT DOLLAR TYPE                             
*                                                                               
         CLI   GLARGS+1,C'P'       SKIP UNLESS PAID DATA                        
         BE    *+8                                                              
         CLI   GLARGS+1,C'3'            OR OPEN  PAID DATA                      
         BE    *+8                                                              
         CLI   GLARGS+1,C'B'            OR BILLED DATA WANTED                   
         BE    *+8                                                              
         CLI   GLARGS+1,C'2'            OR OPEN BILLED DATA WANTED              
         BNE   IDLRGET9                                                         
*                                                                               
*        ALSO MUST BE FLOWCHARTING BASED ON DATED BUY ELEMENTS                  
*              IE. PAY AND BILL ELEMENTS                                        
*                                                                               
         CLI   GLARGS,C'M'         FORCE GETINS IF PROCESSING 1 ELEMENT         
         BE    *+8                                                              
         CLI   GLARGS,C'Q'                                                      
         BE    *+8                                                              
         CLI   GLARGS,C'I'                                                      
         BE    *+8                                                              
         CLI   GLARGS,C'J'                                                      
         BE    *+8                                                              
         CLI   GLARGS,C'N'                                                      
         BE    *+8                                                              
         CLI   GLARGS,C'K'                                                      
         BE    IDLRGET2                                                         
*                                                                               
         CLC   GLARGS+7(2),=C'$L'  FLOWCHARTING BILLED ELEMENTS                 
         BE    *+10                                                             
         CLC   GLARGS+7(2),=C'$T'  OR PAID AND/OR BILLED                        
         BE    *+10                                                             
         CLC   GLARGS+7(2),=C'$A'  OR PAID ELEMENTS                             
         BNE   IDLRGET9                                                         
*                                                                               
IDLRGET2 DS    0H                                                               
*                                                                               
         CLI   GLARGS+1,C'P'       IF PAID DATA                                 
         BE    *+8                                                              
         CLI   GLARGS+1,C'3'       OR OPEN  PAID DATA                           
         BNE   *+8                                                              
         MVI   DLRTYPE,C'P'           INDICATE PAID DATA WANTED                 
*                                                                               
         CLI   GLARGS+1,C'B'       IF BILLED DATA WANTED                        
         BE    *+8                                                              
         CLI   GLARGS+1,C'2'       OR OPEN BILLED DATA WANTED                   
         BNE   *+8                                                              
         MVI   DLRTYPE,C'B'           INDICATE BILLED DATA WANTED               
*                                                                               
         B     IDLRGET3                                                         
*                                                                               
IDLRGET9 DS    0H                                                               
*                                                                               
         CLI   GLARGS+1,C'I'       ALWAYS GO TO GETINS FOR INVOICE              
         BE    IDLRGET3                                                         
*                                                                               
         CLI   GLARGS+1,C'C'       ALWAYS GO TO GETINS FOR PLANNED COST         
         BE    IDLRGET3                                                         
*                                                                               
         TM    PBQACHGS,PBQACOPQ   IF DOING ADDL CHARGES                        
         BNO   IDLRGETX               ALWAYS GO TO GETINS                       
*                                                                               
IDLRGET3 DS    0H                                                               
*                                                                               
         MVI   IDLRSVSW,C'Y'       INDICATE WE WENT TO GETINS FOR DATA          
*                                                                               
         GOTOR GETDLRS             GET DOLLARS VIA GETINS                       
         BNE   IDOLLARX            EXIT ON ERRORS                               
*                                                                               
IDLRGETX DS    0H                                                               
*                                                                               
*        IF BUY IS DELETED THEN ORDERED DATA SET TO 0                           
*              GROSS                                                            
*              AGENCY COMMISSION                                                
*              CASH DISCOUNT                                                    
*              GST PAYABLE                                                      
*              PST PAYABLE                                                      
*                                                                               
         TM    PBUYCNTL,X'80'      TEST FOR DELETED RECORD                      
         BNO   IDLRDEL1                                                         
*                                                                               
         XC    PBGRS(PBUNITS-PBGRS),PBGRS CLEAR ORDERED COSTS                   
         XC    OGROSS(PBUNITS-PBGRS),OGROSS     OPEN RATE                       
         XC    GSTTAX,GSTTAX       GST PAYABLE                                  
         XC    GSTTAX-GVALUES+GOVALUE,GSTTAX-GVALUES+GOVALUE OPEN GST           
*                                                                               
         LA    RF,PSTAREA          PST FOR 10 PROVINCES                         
         LA    R0,10                                                            
*                                                                               
         XC    PSTTAX-PSTAREA(L'PSTTAX,RF),PSTTAX-PSTAREA(RF)                   
         LA    RF,PSTAREAL(RF)                                                  
         BCT   R0,*-10                                                          
*                                                                               
         LA    RF,PSTOAREA         OPEN PST FOR 10 PROVINCES                    
         LA    R0,10                                                            
*                                                                               
         XC    PSTTAX-PSTAREA(L'PSTTAX,RF),PSTTAX-PSTAREA(RF)                   
         LA    RF,PSTAREAL(RF)                                                  
         BCT   R0,*-10                                                          
*                                                                               
         B     IDLRDELX                                                         
*                                                                               
IDLRDEL1 DS    0H                                                               
*                                                                               
         CLI   GLARGS+1,C'C'       IF PLANNED COSTS                             
         BNE   IDLRDEL2                                                         
*                                                                               
         CLI   4(R1),C'X'          AND THERE ARE NO PLANNED COSTS               
         BNE   IDLRDELX                                                         
*                                                                               
         XC    PBGRS(PBUNITS-PBGRS),PBGRS CLEAR PLANNED COSTS                   
*                                                                               
         B     IDLRDELX                                                         
*                                                                               
IDLRDEL2 DS    0H                                                               
*                                                                               
IDLRDELX DS    0H                                                               
*                                                                               
*        DETERMINE RATES TO USE                                                 
*                                                                               
         LA    R1,PBGRS            DEFAULT TO BUY DATA                          
*                                                                               
         CLI   GLARGS+1,C'1'       IF OPEN RATES                                
         BL    *+8                                                              
         LA    R1,OGROSS              USE OPEN RATE DATA                        
*                                                                               
         CLI   GLARGS+1,C'B'       RESET POINTER IF BILLED DATA                 
         BE    *+8                                                              
         CLI   GLARGS+1,C'2'       RESET POINTER IF OPEN BILLED DATA            
         BNE   *+8                                                              
         LA    R1,PBBGROSS-PBGRS(R1)                                            
*                                                                               
         CLI   GLARGS+1,C'P'       RESET POINTER IF PAID DATA                   
         BE    *+8                                                              
         CLI   GLARGS+1,C'3'       RESET POINTER IF OPEN PAID DATA              
         BNE   *+8                                                              
         LA    R1,PBPGROSS-PBGRS(R1)                                            
*                                                                               
*        DETERMINE MONEY TYPE AND CALCULATE                                     
*                                                                               
IDLRGRS  DS    0H                                                               
*                                                                               
         LM    R3,R5,0(R1)         GET GROSS/AGYCOMM/CD                         
*                                                                               
         CLI   GLARGS+2,C'1'       GROSS                                        
         BNE   IDLRGRSN                                                         
*                                                                               
         LR    RF,R3               GROSS                                        
*                                                                               
         B     IDLRTYPX                                                         
*                                                                               
IDLRGRSN DS    0H                                                               
*                                                                               
IDLRGCD  DS    0H                                                               
*                                                                               
         CLI   GLARGS+2,C'2'       GROSS LESS CD                                
         BNE   IDLRGCDN                                                         
*                                                                               
         LR    RF,R3               GROSS                                        
         SR    RF,R5                LESS CD                                     
*                                                                               
         CLI   GLARGS+1,C'O'       IF ORDERED DATA                              
         BE    *+8                                                              
         CLI   GLARGS+1,C'1'       OR OPEN ORDERED DATA                         
         BNE   *+8                                                              
         ICM   RF,15,PBBLABLE-PBGRS(R1)    USE BILLABLE                         
*                                                                               
         B     IDLRTYPX                                                         
*                                                                               
IDLRGCDN DS    0H                                                               
*                                                                               
IDLRNCD  DS    0H                                                               
*                                                                               
         CLI   GLARGS+2,C'3'       NET LESS CD                                  
         BNE   IDLRNCDN                                                         
*                                                                               
         LR    RF,R3               GROSS                                        
         SR    RF,R4               LESS AGYCOM                                  
         SR    RF,R5               LESS CD                                      
*                                                                               
         CLI   GLARGS+1,C'O'       IF ORDERED DATA                              
         BE    *+8                                                              
         CLI   GLARGS+1,C'1'       OR OPEN ORDERED DATA                         
         BNE   *+8                                                              
         ICM   RF,15,PBPYABLE-PBGRS(R1)    USE PAYABLE                          
*                                                                               
         CLI   GLARGS+1,C'B'       IF BILLED DATA                               
         BE    *+8                                                              
         CLI   GLARGS+1,C'2'       OR OPEN BILLED DATA                          
         BNE   *+8                                                              
         ICM   RF,15,PBBILLED-PBBGROSS(R1)    USE BILLED AMOUNT                 
*                                                                               
         CLI   GLARGS+1,C'P'       IF PAID DATA                                 
         BNE   *+8                                                              
         ICM   RF,15,PBPAID-PBPGROSS(R1)    USE PAID AMOUNT                     
*                                                                               
         B     IDLRTYPX                                                         
*                                                                               
IDLRNCDN DS    0H                                                               
*                                                                               
IDLRNET  DS    0H                                                               
*                                                                               
         CLI   GLARGS+2,C'4'       NET                                          
         BNE   IDLRNETN                                                         
*                                                                               
         LR    RF,R3               GROSS                                        
         SR    RF,R4               LESS AGYCOM                                  
*                                                                               
         CLI   GLARGS+1,C'O'       IF ORDERED DATA                              
         BE    *+8                                                              
         CLI   GLARGS+1,C'1'       OR OPEN ORDERED DATA                         
         BNE   *+10                                                             
         ICM   RF,15,PBPYABLE-PBGRS(R1)    USE PAYABLE                          
         AR    RF,R5               PLUS CD                                      
*                                                                               
         CLI   GLARGS+1,C'B'       IF BILLED DATA                               
         BE    *+8                                                              
         CLI   GLARGS+1,C'2'       OR OPEN BILLED DATA                          
         BNE   *+10                                                             
         ICM   RF,15,PBBILLED-PBBGROSS(R1)    USE BILLED AMOUNT                 
         AR    RF,R5               PLUS CD                                      
*                                                                               
         CLI   GLARGS+1,C'P'       IF PAID DATA                                 
         BNE   *+10                                                             
         ICM   RF,15,PBPAID-PBPGROSS(R1)    USE PAID AMOUNT                     
         AR    RF,R5               PLUS CD                                      
*                                                                               
         B     IDLRTYPX                                                         
*                                                                               
IDLRNETN DS    0H                                                               
*                                                                               
IDLRCD   DS    0H                                                               
*                                                                               
         CLI   GLARGS+2,C'5'       CASH DISCOUNT                                
         BNE   IDLRCDN                                                          
*                                                                               
         LR    RF,R5               CD                                           
*                                                                               
         B     IDLRTYPX                                                         
*                                                                               
IDLRCDN  DS    0H                                                               
*                                                                               
IDLRAGY  DS    0H                                                               
*                                                                               
         CLI   GLARGS+2,C'6'       AGENCY COMMISSION                            
         BNE   IDLRAGYN                                                         
*                                                                               
         LR    RF,R4               AGENCY COMMISSION                            
*                                                                               
         B     IDLRTYPX                                                         
*                                                                               
IDLRAGYN DS    0H                                                               
*                                                                               
IDLRTAX  DS    0H                                                               
*                                                                               
         CLI   GLARGS+2,C'7'       TAX                                          
         BNE   IDLRTAXN                                                         
*                                                                               
         CLI   GLARGS+1,C'O'       IF ORDERED DATA                              
         BE    *+8                                                              
         CLI   GLARGS+1,C'1'       OR OPEN ORDERED DATA                         
         BNE   IDLRTAX1                                                         
*                                                                               
         L     RF,PBTAX-PBGRS(R1)     RETURN TAX                                
*                                                                               
         B     IDLRTYPX                                                         
*                                                                               
IDLRTAX1 DS    0H                                                               
*                                                                               
         L     RF,12(R1)           ACTUAL AMOUNT PAID OR BILLED                 
*                                                                               
*        TAX AMOUNT = (AMOUNT WITH TAX)*TAX RATE/(1+TAXRATE)                    
*                                                                               
         CVD   RF,IDLRPL8          CVD                                          
         ZAP   IDLRPL16,IDLRPL8    MOVE TO WORKAREA                             
         SRP   IDLRPL16,1,0     * 10 ROUNDING                                   
*                                                                               
         SR    RE,RE                                                            
         ICM   RE,7,PBDTAX         GET TAX RATE TO 4 DECIMALS                   
         CVD   RE,IDLRPL8          CVD                                          
*                                                                               
         MP    IDLRPL16,IDLRPL8    MULTIPLY                                     
*                                                                               
         AP    IDLRPL8,=P'1000000' ADD 1 TO TAX RATE                            
*                                                                               
         DP    IDLRPL16,IDLRPL8                                                 
         SRP   IDLRPL16(8),64-1,5  ROUND TO NEAREST PENNY                       
         CVB   RF,IDLRPL16         CVB FOR TAX AMOUNT                           
*                                                                               
         B     IDLRTYPX                                                         
*                                                                               
IDLRTAXN DS    0H                                                               
*                                                                               
IDLRGST  DS    0H                                                               
*                                                                               
         CLI   GLARGS+2,C'8'       GST                                          
         BNE   IDLRGSTN                                                         
*                                                                               
         SR    RF,RF               INIT FOR LATER CALCULATIONS                  
*                                                                               
         B     IDLRTYPX                                                         
*                                                                               
IDLRGSTN DS    0H                                                               
*                                                                               
IDLRPST  DS    0H                                                               
*                                                                               
         CLI   GLARGS+2,C'9'       PST                                          
         BNE   IDLRPSTN                                                         
*                                                                               
         SR    RF,RF               INIT FOR LATER CALCULATIONS                  
*                                                                               
         B     IDLRTYPX                                                         
*                                                                               
IDLRPSTN DS    0H                                                               
*                                                                               
IDLRTYPX DS    0H                                                               
*                                                                               
*        APPLY APPROPRIATE GST IF NEEDED                                        
*                                                                               
IDLRGST0 DS    0H                                                               
*                                                                               
         CLI   GLARGS+2,C'9'       SKIP     IF DOING PST ONLY                   
         BE    IDLRGSIX                                                         
*                                                                               
         CLI   GLARGS+2,C'8'       CONTINUE IF DOING GST                        
         BE    IDLRGST1                                                         
*                                                                               
         TM    GLARGS+5,X'20'      CONTINUE IF INPUT GST WANTED                 
         BO    IDLRGST1               FOR DATA ENTRY                            
*                                                                               
*        CHECK IF ADDING INPUT PST                                              
*                                                                               
         LR    R0,RF               SAVE    DOLLAR AMOUNT                        
         GOTO1 FNDFLTRA,DMCB,=AL2(PRQWRCFL),(2,=AL2(PRQCFPSI)),0                
         LR    RF,R0               RESTORE DOLLAR AMOUNT                        
*                                                                               
         OC    8(4,R1),8(R1)       INPUT PST WANTED                             
         BNZ   IDLRGST1                                                         
*                                                                               
         CLI   PBQADDGS,C'Y'          OR ALL MONEY FIELDS                       
         BE    IDLRGST1                                                         
*                                                                               
         B     IDLRGSIX                                                         
*                                                                               
IDLRGST1 DS    0H                                                               
*                                                                               
         LA    R1,GVALUES          POINT TO ORDERED GST VALUES                  
*                                                                               
         CLI   GLARGS+1,C'1'       RESET POINTER IF OPEN RATES                  
         BL    *+8                                                              
         LA    R1,GOVALUE                                                       
*                                                                               
         CLI   GLARGS+1,C'P'         IF PAID DATA                               
         BE    *+8                                                              
         CLI   GLARGS+1,C'3'         IF OPEN PAID DATA                          
         BNE   *+12                                                             
         A     RF,GSTTAXPD-GVALUES(R1)   ADD ON PAID GST                        
         B     IDLRGSIX                                                         
*                                                                               
         CLI   GLARGS+1,C'B'         IF BILLED DATA                             
         BE    *+8                                                              
         CLI   GLARGS+1,C'2'         IF OPEN BILLED DATA                        
         BNE   *+12                                                             
         A     RF,GSTTAXBL-GVALUES(R1)   ADD ON BILLED GST                      
         B     IDLRGSIX                                                         
*                                                                               
         A     RF,GSTTAX-GVALUES(R1)  ELSE ADD IN PAYABLE GST                   
*                                                                               
IDLRGSIX DS    0H                                                               
*                                                                               
         CLI   GLARGS+2,C'9'       CONTINUE IF DOING PST                        
         BE    IDLRPSI1                                                         
*                                                                               
         LR    R0,RF               SAVE    DOLLAR AMOUNT                        
         GOTO1 FNDFLTRA,DMCB,=AL2(PRQWRCFL),(2,=AL2(PRQCFPSI)),0                
         LR    RF,R0               RESTORE DOLLAR AMOUNT                        
*                                                                               
         OC    8(4,R1),8(R1)       INPUT PST WANTED                             
         BNZ   IDLRPSI1                                                         
*                                                                               
         B     IDLRPSIX                                                         
*                                                                               
IDLRPSI1 DS    0H                                                               
*                                                                               
         LA    R1,PSTAREA          POINT TO PST AREA                            
*                                                                               
         CLI   GLARGS+1,C'1'       IF OPEN RATES                                
         BL    *+8                                                              
         LA    R1,PSTOAREA            USE OPEN RATE DATA                        
*                                                                               
         LA    R0,10               TEN PROVINCES                                
*                                                                               
IDLRPSIL DS    0H                                                               
*                                                                               
         CLI   GLARGS+1,C'P'       IF PAID DATA                                 
         BE    *+8                                                              
         CLI   GLARGS+1,C'3'       OR OPEN PAID DATA                            
         BNE   *+12                                                             
         A     RF,PSTTAXPD-PSTAREA(R1)   ADD IN PST TAX PAID                    
         B     IDLRPSIC                                                         
*                                                                               
         CLI   GLARGS+1,C'B'       IF BILLED DATA                               
         BE    *+8                                                              
         CLI   GLARGS+1,C'2'       OR OPEN BILLED DATA                          
         BNE   *+12                                                             
         A     RF,PSTTAXBL-PSTAREA(R1)   ADD IN PST BILLED PAID                 
         B     IDLRPSIC                                                         
*                                                                               
         A     RF,PSTTAX-PSTAREA(R1) ELSE ADD IN PST TAX                        
*                                                                               
IDLRPSIC DS    0H                                                               
*                                                                               
         LA    R1,PSTAREAL(R1)                                                  
         BCT   R0,IDLRPSIL                                                      
*                                                                               
IDLRPSID DS    0H                                                               
*                                                                               
IDLRPSIX DS    0H                                                               
*                                                                               
         TM    GLARGS+5,X'10'      SKIP UNLESS OUTPUT GST/PST WANTED            
         BO    IDLRGSO1               FOR DATA ENTRY                            
*                                                                               
*        CHECK IF ADDING OUTPUT PST                                             
*                                                                               
         LR    R0,RF               SAVE    DOLLAR AMOUNT                        
         GOTO1 FNDFLTRA,DMCB,=AL2(PRQWRCFL),(2,=AL2(PRQCFPSO)),0                
         LR    RF,R0               RESTORE DOLLAR AMOUNT                        
*                                                                               
         OC    8(4,R1),8(R1)       OUTPUT PST WANTED                            
         BNZ   IDLRGSO1                                                         
*                                                                               
         CLI   PBQADDGS,C'O'          OR ALL MONEY FIELDS                       
         BE    IDLRGSO1                                                         
*                                                                               
         B     IDLRGSOX                                                         
*                                                                               
IDLRGSO1 DS    0H                                                               
*                                                                               
         LA    R1,GVALUES          POINT TO ORDERED GST VALUES                  
*                                                                               
         CLI   GLARGS+1,C'1'       RESET POINTER IF OPEN RATES                  
         BL    *+8                                                              
         LA    R1,GOVALUE                                                       
*                                                                               
         CLI   GLARGS+1,C'B'         IF BILLED DATA                             
         BE    *+8                                                              
         CLI   GLARGS+1,C'2'         IF OPEN BILLED DATA                        
         BNE   *+12                                                             
         A     RF,GSTTAXBL-GVALUES(R1)   ADD ON BILLED GST                      
         B     IDLRGSOX                                                         
*                                                                               
         A     RF,GSTTAX-GVALUES(R1)  ELSE ADD IN BILLABLE GST                  
*                                                                               
IDLRGSOX DS    0H                                                               
*                                                                               
         LR    R0,RF               SAVE    DOLLAR AMOUNT                        
         GOTO1 FNDFLTRA,DMCB,=AL2(PRQWRCFL),(2,=AL2(PRQCFPSO)),0                
         LR    RF,R0               RESTORE DOLLAR AMOUNT                        
*                                                                               
         OC    8(4,R1),8(R1)       NO OUTPUT PST WANTED                         
         BZ    IDLRPSOX                                                         
*                                                                               
         LA    R1,PSTAREA          POINT TO PST AREA                            
*                                                                               
         CLI   GLARGS+1,C'1'       IF OPEN RATES                                
         BL    *+8                                                              
         LA    R1,PSTOAREA            USE OPEN RATE DATA                        
*                                                                               
         LA    R0,10               TEN PROVINCES                                
*                                                                               
IDLRPSOL DS    0H                                                               
*                                                                               
         CLI   GLARGS+1,C'B'       IF BILLED DATA                               
         BE    *+8                                                              
         CLI   GLARGS+1,C'2'                                                    
         BNE   *+12                                                             
         A     RF,PSTTAXBL-PSTAREA(R1)   ADD IN BILLED PST TAX                  
         B     *+8                                                              
         A     RF,PSTTAX-PSTAREA(R1) ELSE ADD IN PST TAX                        
*                                                                               
IDLRPSOC DS    0H                                                               
*                                                                               
         LA    R1,PSTAREAL(R1)                                                  
         BCT   R0,IDLRPSOL                                                      
*                                                                               
IDLRPSOD DS    0H                                                               
*                                                                               
IDLRPSOX DS    0H                                                               
*                                                                               
IDLRGSTX DS    0H                                                               
*                                                                               
         CLI   GLARGS+1,C'P'       SKIP IF NOT PAID DATA                        
         BNE   IDLRCLRX                                                         
*                                                                               
         ICM   R5,15,PBCLSELA      POINT TO CLRST ELEMENT                       
         BZ    IDLRCLRX            SKIP IF NONE                                 
*                                                                               
         USING PPCLEL01,R5         ESTABLISH CLRST ELM                          
*                                                                               
         TM    PPCLSTAT,X'02'      SKIP IF NO INVOICE ELEMS                     
         BNO   IDLRCLRX                                                         
*                                                                               
         ICM   R4,15,PBCLINVA      POINT TO INVOICE ELEMENT                     
         BZ    IDLRCLRX            SKIP IF NONE                                 
*                                                                               
         USING PPCLEL03,R4         ESTABLISH INVOICE ELEMENT                    
*                                                                               
         BRAS  RE,GETTOTS          GET TOTALS FOR DEBITS AND CREDITS            
*                                                                               
         LLC   RE,PPCLLN03         GET ELEMENT LENGTH                           
         LA    R4,0(RE,R4)         POINT TO FOLLOWING CHECK ELEMENT             
*                                                                               
         USING PPCLEL05,R4         ESTABLISH CHECK ELEMENT                      
*                                                                               
         ZAP   IDLRRTDB,=P'0'      INIT DEBIT  RATIO                            
         ZAP   IDLRRTCR,=P'0'      INIT CREDIT RATIO                            
*                                                                               
         ICM   RE,15,PCL5NET       GET INVOICE NET DOLLARS                      
         BNZ   *+10                IF NET IS ZERO THEN                          
         ICM   R0,15,PCL5CD        AMOUNT IS NET AND WE WANT NET                
         SR    RE,R0               LESS CD                                      
*                                                                               
         ICM   R0,15,PPCLNET       GET TOTAL   NET DOLLARS                      
         TM    PPCLSTAT,X'20'      IF NOT CLEARED NET                           
         BO    *+12                                                             
         ICM   RE,15,PCL5GRS          USE GROSS                                 
         ICM   R0,15,PPCLGRS                                                    
*                                                                               
         CR    R0,RE               IF ONLY ONE INVOICE IN PAYMENT               
         BNE   *+18                                                             
         CVD   RF,IDLRPL8                                                       
         ZAP   0(8,R2),IDLRPL8         RETURN CLEARED AMOUNT                    
         B     IDLRCLRZ                                                         
*                                                                               
         CR    R0,RF               IF ONLY ONE BUY IN PAYMENT                   
         BNE   *+18                                                             
         CVD   RE,IDLRPL8                                                       
         ZAP   0(8,R2),IDLRPL8         RETURN INVOICE AMOUNT                    
         B     IDLRCLRZ                                                         
*                                                                               
         LA    R1,IDLRDEB          POINT TO DEBIT TOTALS                        
         LTR   RE,RE               IF NEGATIVE AMOUNT                           
         BNM   *+8                                                              
         LA    R1,IDLRCRD             POINT TO CREDIT TOTALS                    
*                                                                               
         CVD   RE,IDLRPL8          CVD                                          
*                                                                               
         ZAP   IDLRPL16,IDLRPL8    SAVE                                         
*                                                                               
         ICM   R3,15,0(R1)         GET TOTAL CLEARED                            
         BZ    IDLRCLRZ            SKIP IF NONE                                 
*                                                                               
         CVD   R3,IDLRPL8          CVD                                          
*                                                                               
         SRP   IDLRPL16,7,0        * 10**7 FOR ROUNDING                         
*                                                                               
         DP    IDLRPL16,IDLRPL8    CALC RATIO INVOICE TO TOTAL                  
         SRP   IDLRPL16(8),64-1,5  ROUND TO 6 DECIMALS                          
         ZAP   4(8,R1),IDLRPL16(8) SAVE RATIO                                   
*                                                                               
         CVD   RF,IDLRPL8          CVD AMOUNT BEING REPORTED                    
*                                                                               
         LA    R1,IDLRRTDB         ASSUME DEBIT                                 
         CP    IDLRPL8,=P'0'       IF NEGATIVE                                  
         BNM   IDLR10                                                           
         CLC   =C'JP',GLARGS       ISOLATE FIX TO THESE KEYWORDS                
         BNE   *+14                                                             
         CP    IDLRRTCR,=P'0'      IS CREDIT RATIO 0?                           
         BE    IDLR10              YES - USE DEBIT RATIO SPEC-43345             
         LA    R1,IDLRRTCR            USE CREDIT RATIO                          
*                                                                               
IDLR10   ZAP   IDLRPL16,0(8,R1)    GET RATIO                                    
*                                                                               
         MP    IDLRPL16,IDLRPL8    GET PRODUCT                                  
*                                                                               
         SRP   IDLRPL16,64-6,5     ROUND TO PENNIES                             
*                                                                               
         ZAP   0(8,R2),IDLRPL16    RETURN AMOUNT                                
*                                                                               
         B     IDLRCLRZ                                                         
*                                                                               
IDLRCLRX DS    0H                                                               
*                                                                               
         CVD   RF,IDLRPL8          PASS CALCULATED AMOUNT                       
         ZAP   0(8,R2),IDLRPL8                                                  
*                                                                               
IDLRCLRZ DS    0H                                                               
*                                                                               
         CLI   IDLRSVSW,0          IF WE WENT TO GETINS                         
         BE    IDLRRESX                                                         
*                                                                               
         MVC   PBGRS(L'IDLRPVLS),IDLRPVLS RESTORE TOTAL BUY                     
         MVC   OGROSS(L'IDLROVLS),IDLROVLS  GETINS DATA                         
         MVC   GVALUES(L'IDLRGVLS),IDLRGVLS                                     
         MVC   GOVALUE(L'IDLROGVL),IDLROGVL                                     
*                                                                               
         LA    RF,PSTAREA          PST DATA FOR 10 PROVINCES                    
         LA    RE,IDLRPSTS                                                      
         LA    R0,10                                                            
*                                                                               
         MVC   0(PSTAREAL,RF),0(RE) PST DATA FOR 1 PROVINCE                     
         LA    RE,PSTAREAL(RE)                                                  
         LA    RF,PSTAREAL(RF)                                                  
         BCT   R0,*-14                                                          
*                                                                               
         LA    RF,PSTOAREA         OPEN PST DATA FOR 10 PROVINCES               
         LA    RE,IDLROPST                                                      
         LA    R0,10                                                            
*                                                                               
         MVC   0(PSTAREAL,RF),0(RE) PST DATA FOR 1 PROVINCE                     
         LA    RE,PSTAREAL(RE)                                                  
         LA    RF,PSTAREAL(RF)                                                  
         BCT   R0,*-14                                                          
*                                                                               
IDLRRESX DS    0H                                                               
*                                                                               
*        IF WE ARE REPORTING TEST AND LIVE BUY DATA SEPARATELY                  
*           WE NEED TO RECORD TEST DATA IN SECOND BUCKET                        
*                                                                               
IDLR2ND  DS    0H                                                               
*                                                                               
         TM    PBQTOTTY,TOTTYPN   SKIP IF NORMAL TOTALS                         
         BO    IDLR2NDX                                                         
*                                                                               
         CLI   PBDBFD,C'T'        IF A TEST BUY                                 
         BNE   *+10                                                             
         ZAP   8(8,R2),0(8,R2)        RECORD IN SECOND BUCKET ALSO              
*                                                                               
IDLR2NDX DS    0H                                                               
*                                                                               
         TM    PBQCFOPT,PBQCFOUQ   IF OUTLET PROCESSING                         
         BNO   IDLROUTX                                                         
*                                                                               
*        CHECK IF THIS COLUMN INVOLVED                                          
*                                                                               
         GOTO1 FNDFLTRA,DMCB,=AL2(PRQCFOUT),0,0                                 
*                                                                               
         OC    8(4,R1),8(R1)       IF OUTLET COLUMN                             
         BZ    IDLROUTX                                                         
*                                                                               
         GOTO1 =V(OUTFLT)            GO APPLY (IN PRWRIOUT)                     
*                                     MAY APPLY PERCENT TO DOLLAR VALUE         
IDLROUTX DS    0H                                                               
*                                                                               
*        APPLY SUB ADCODE SHARE IF PRESENT                                      
*                                                                               
IDLRSHR  DS    0H                                                               
*                                                                               
         TM    PBBYOPT2,PBBYCPYQ   SKIP IF NOT DOING COPY SPLITS                
         BNO   IDLRSHRX                                                         
*                                                                               
         TP    PBSUBSHR            SKIP IF NO SHARE PER CENT                    
         BNZ   IDLRSHRX                                                         
*                                                                               
         TP    0(8,R2)             MAKE SURE WE HAVE AN AMOUNT                  
         BNZ   IDLRSHRX                                                         
*                                                                               
         ZAP   IDLRPL16,0(8,R2)    MOVE TO WORKAREA                             
         MP    IDLRPL16,PBSUBSHR   * PERCENT - 5 DECIMALS                       
         SRP   IDLRPL16,64-5,5     ROUND TO PENNIES                             
         ZAP   0(8,R2),IDLRPL16    RETURN NEW AMOUNT                            
*                                                                               
         TP    8(8,R2)             MAKE SURE WE HAVE A TEST AMOUNT              
         BNZ   IDLRSHRX                                                         
*                                                                               
         ZAP   IDLRPL16,8(8,R2)    MOVE TO WORKAREA                             
         MP    IDLRPL16,PBSUBSHR   * PERCENT - 5 DECIMALS                       
         SRP   IDLRPL16,64-5,5     ROUND TO PENNIES                             
         ZAP   8(8,R2),IDLRPL16    RETURN NEW AMOUNT                            
*                                                                               
IDLRSHRX DS    0H                                                               
*                                                                               
         CR    RB,RB               PASSED ALL FILTER TESTS                      
*                                                                               
         B     IDOLLARX                                                         
*                                                                               
IDLRDROP DS    0H                                                               
*                                                                               
         LTR   RB,RB               FAILED A FILTER TEST                         
*                                                                               
IDOLLARX DS    0H                                                               
         XIT1                                                                   
*                                                                               
         DROP  R4                                                               
*                                                                               
         TITLE 'PRWRIDLR - RATIOS - GETRTIO'                                    
***********************************************************************         
*                                                                     *         
*        ROUTINE TO GET TOTAL DEBITS AND CREDITS IN CLEARANCE         *         
*                                                                     *         
*NTRY                                                                 *         
*                                                                     *         
***********************************************************************         
         DS    0D                                                               
GETTOTS  NTR1  LABEL=*                                                          
*                                                                               
         XC    IDLRDEB,IDLRDEB     INIT TOTAL DEBITS                            
         XC    IDLRCRD,IDLRCRD     INIT TOTAL CREDITS                           
*                                                                               
         ICM   R5,15,PBCLSELA      POINT TO CURRENT 01 ELEMENT                  
         BZ    GETTOTSX            NONE - GET OUT                               
*                                                                               
         USING PPCLEL01,R5         ESTABLISH 01 ELEMENT                         
*                                                                               
         LLC   RF,PPCLEL01+1       GET ELEMENT LENGTH                           
         LA    R6,PPCLEL01(RF)     BUMP TO NEXT ELEMENT                         
*                                                                               
GTTLOOP  DS    0H                                                               
*                                                                               
         CLI   0(R6),0             DONE IF END OF RECORD                        
         BE    GTTDONE                                                          
*                                                                               
         CLI   0(R6),X'01'         DONE IF ANOTHER X'01' ELEMENT                
         BE    GTTDONE                                                          
*                                                                               
         CLI   0(R6),X'05'         MUST BE 05      ELEMENT                      
         BNE   GTTCONT                                                          
*                                                                               
         USING PPCLEL05,R6         ESTABLISH 05 ELEMENT                         
*                                                                               
         ICM   RF,15,PCL5NET       GET CLEARED NET                              
*                                                                               
         TM    PPCLSTAT,X'20'      IF NOT CLEARED NET                           
         BO    *+8                                                              
         ICM   RF,15,PCL5GRS          GET CLEARED GROSS                         
*                                                                               
         LA    R1,IDLRDEB          POINT TO CURRENT DEBIT TOTAL                 
*                                                                               
         LTR   RF,RF               IF AMOUNT IS NEGATIVE                        
         BNM   *+8                                                              
         LA    R1,IDLRCRD             POINT TO CREDIT TOTAL                     
*                                                                               
         ICM   RE,15,0(R1)         UPDATE TOTAL                                 
         AR    RE,RF                                                            
         STCM  RE,15,0(R1)                                                      
*                                                                               
GTTCONT  DS    0H                                                               
*                                                                               
         LLC   RF,PPCLEL05+1       GET ELEMENT LENGTH                           
         LA    R6,PPCLEL05(RF)     BUMP TO NEXT ELEMENT                         
*                                                                               
         B     GTTLOOP                                                          
*                                                                               
GTTDONE  DS    0H                                                               
*                                                                               
GETTOTSX DS    0H                                                               
         XIT1                                                                   
*                                                                               
         DROP  RB                                                               
*                                                                               
         DS    CL256               FOR NEW Z/OS PROCESSOR                       
IDLRDEB  DS    F                   TOTAL FOR DEBITS                             
IDLRRTDB DS    PL8                 RATIO OF INV AMT TO DEBIT  TOTAL             
IDLRCRD  DS    F                   TOTAL FOR CREDITS                            
IDLRRTCR DS    PL8                 RATIO OF INV AMT TO CREDIT TOTAL             
*                                                                               
IDLRSVSW DS    XL1                   DATA SAVED INDICATOR                       
IDLRPVLS DS    XL(PBVALUEX-PBGRS)    ORDERED       SAVE AREA                    
IDLROVLS DS    XL(PBVALUEX-PBGRS)    OPEN RATE     SAVE AREA                    
IDLRGVLS DS    XL(GVALUESL)          ORDERED   GST SAVE AREA                    
IDLROGVL DS    XL(GVALUESL)          OPEN RATE GST SAVE AREA                    
IDLRPSTS DS    XL(10*PSTAREAL)       ORDERED   PST SAVE AREA                    
IDLROPST DS    XL(10*PSTAREAL)       OPEN RATE PST SAVE AREA                    
*                                                                               
         DS    0D                  ALIGNMENT                                    
IDLRPL8  DS    PL8                 WORKAREA                                     
IDLRPL16 DS    PL16                WORKAREA                                     
*                                                                               
         LTORG                                                                  
*                                                                               
         DROP                                                                   
*                                                                               
         TITLE 'PRWRIDLR - REBATE $ AMOUNTS - INPUT - IREBATE'                  
***********************************************************************         
*                                                                     *         
*        ROUTINE TO EXTRACT REBATE $ AMOUNTS FROM BUY                 *         
*                                                                     *         
*NTRY    GLARGS   = RECORD ID                                         *         
*                   C'B' - BUY      RECORD                            *         
*        GLARGS+1 = AMOUNT TYPE                                       *         
*                   C'O'  - ORDERED                                   *         
*                   C'B'  - BILLED                                    *         
*                   C'P'  - PAID                                      *         
*                   C'L'  - BILLABLE                                  *         
*                   C'Q'  - PAYABLE                                   *         
*                   C'1'  - OPEN ORDERED                              *         
*                   C'2'  - OPEN BILLED                               *         
*                   C'3'  - OPEN PAID                                 *         
*                   C'4'  - OPEN BILLABLE                             *         
*                   C'5'  - OPEN PAYABLE                              *         
*        GLARGS+2 = DATA TYPE                                         *         
*                   C'1'  - GROSS                                     *         
*                   C'2'  - GROSS LESS CASH DISCOUNT                  *         
*                   C'3'  - NET   LESS CASH DISCOUNT                  *         
*                   C'4'  - NET                                       *         
*                   C'5'  - CASH  DISCOUNT                            *         
*                   C'6'  - AGENCY COMMISSION                         *         
*                   C'7'  - TAX                                       *         
*                   C'8'  - GST                                       *         
*                   C'9'  - PST                                       *         
*        GLARGS+5 = GST INDICATOR                                     *         
*                   X'20' - INCLUDE INPUT  SIDE GST                   *         
*                   X'10' - INCLUDE OUTPUT SIDE GST                   *         
*        GLARGS+7 = FLOWCHART INDICATOR                               *         
*                   C'$'  - FLOWCHARTING DOLLAR AMOUNTS               *         
*        GLARGS+8 = DATETYPE FILTER                                   *         
*                   C'A'  - PAID   DATES                              *         
*                   C'L'  - BILLED DATES                              *         
*                   C'T'  - BILLED/PAID DATES                         *         
*                                                                     *         
*        R2==>  DRIVER INPUT AREA                                     *         
*                                                                     *         
*EXIT   0-7    -  PL8 DOLLARS                                         *         
*       8-15   -  PL8                                                 *         
*                                                                     *         
***********************************************************************         
         EJECT                                                                  
         DS    0D                                                               
IREBATE  NMOD1 0,**#IRBT                                                        
*                                                                               
         USING SPOOLD,R8           ESTABLISH SPOOL WORKAREA                     
         USING SYSD,R9             ESTABLISH PRINTWRITER WORKAREA               
         USING GLOBALD,RA          ESTABLISH DRIVER WORKAREA                    
*                                                                               
         LA    R7,PBLOCK           ESTABLISH PRINT BLOCK                        
         USING PBLOCK,R7                                                        
*                                                                               
         L     RC,GLAWORKD         RESET WORKING STORAGE POINTER                
         USING GEND,RC                                                          
*                                                                               
         MVC   IRBTTYPE,GLARGS+1   SAVE DESIRED AMOUNT TYPE                     
*                                                                               
         L     RF,=A(IDOLLAR)      DEFAULT TO DOLLAR ROUTINE                    
*                                                                               
         CLI   GLARGS+1,C'L'       IF BILLABLE WANTED                           
         BE    *+8                                                              
         CLI   GLARGS+1,C'Q'       OR PAYABLE WANTED                            
         BNE   *+8                                                              
         L     RF,=A(I$COMBO)         USE COMBINED DOLLAR ROUTINE               
*                                                                               
         GOTO1 (RF)                GET AMOUNTS                                  
*                                                                               
         ZAP   IRBTDLR,0(8,R2)     SAVE  DOLLARS                                
         ZAP   IRBTDLRT,8(8,R2)    SAVE TEST DOLLARS                            
*                                                                               
*        TRANSLATE ORDERED AMOUNTS TO OPEN AMOUNT TYPES                         
*                                                                               
         LA    R1,IRBTTBL          POINT TO TRANSLATION TABLE                   
*                                                                               
         CLC   IRBTTYPE,0(R1)      MATCH TYPE TO TABLE ENTRY                    
         BE    *+18                                                             
         LA    R1,2(R1)            BUMP TO NEXT TABLE ENTRY                     
         CLI   0(R1),X'FF'         CONTINUE IF NOT END OF TABLE                 
         BNE   *-18                                                             
         DC    H'0'                CAN'T HANDLE THIS                            
*                                                                               
         MVC   GLARGS+1(1),1(R1)   SET OPEN AMOUNT TYPE                         
*                                                                               
         GOTO1 (RF)                RETURN TO PREVIOUSLY USED ROUTINE            
*                                                                               
         SP    0(8,R2),IRBTDLR     RESULT IS OPEN-ORDERED                       
         SP    8(8,R2),IRBTDLRT    TEST AMOUNT                                  
*                                                                               
         MVC   GLARGS+1(1),IRBTTYPE RESTORE TYPE                                
*                                                                               
IREBATEX DS    0H                                                               
         XIT1                                                                   
*                                                                               
IRBTTBL  DC    C'O1'               ORDERED                                      
         DC    C'B2'               BILLED                                       
         DC    C'P3'               PAID                                         
         DC    C'L4'               BILLABLE                                     
         DC    C'Q5'               PAYABLE                                      
         DC    X'FFFF'             END OF TABLE                                 
*                                                                               
         DS    CL256               FOR NEW Z/OS PROCESSOR                       
IRBTTYPE DS    X                   ASKED FOR AMOUNT TYPE                        
IRBTDLR  DS    PL8                 INTERMEDIATE SAVEAREA FOR AMOUNT             
IRBTDLRT DS    PL8                 INTERMEDIATE TEST SAVEAREA                   
*                                                                               
         LTORG                                                                  
*                                                                               
         DROP                                                                   
*                                                                               
         TITLE 'GET BILLED/PAID DOLLARS FOR DATE SPAN - GETDLRS'                
***********************************************************************         
*                                                                     *         
*        USE GETINS TO GET BILLED/PAID $                              *         
*                                                                     *         
* NTRY   I/O1 HAS BUY RECORD                                          *         
*        DLRTYPE      HAS DATA TYPE - PAID OR BILLED                  *         
*        GLARGS+9(6)  HAS START AND END DATES OF PERIOD               *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         DS    0D                                                               
GETDLRS  NMOD1 0,**#GDL                                                         
*                                                                               
         USING SPOOLD,R8           ESTABLISH SPOOL WORKAREA                     
         USING SYSD,R9             ESTABLISH PRINTWRITER WORKAREA               
         USING GLOBALD,RA          ESTABLISH DRIVER WORKAREA                    
*                                                                               
         LA    R7,PBLOCK           ESTABLISH PRINT BLOCK                        
         USING PBLOCK,R7                                                        
*                                                                               
         L     RC,GLAWORKD         RESET WORKING STORAGE POINTER                
         USING GEND,RC                                                          
*                                                                               
         L     R2,AIO1             ESTABLISH BUY RECORD                         
         USING PBUYRECD,R2                                                      
*                                                                               
         XC    GDLWORK(GDLWORKL),GDLWORK INIT WORKAREA                          
*                                                                               
*        SET ADDITIONAL CHARGES OPTION                                          
*                                                                               
         TM    PBQACHGS,PBQACOPQ   SKIP IF NO ADDITIONAL CHGS OPTIONS           
         BNO   GDLACHX                                                          
*                                                                               
         MVC   GDLACHCD,PBQACHG    INIT CHARGE CODE                             
         MVC   GDLACOPT,PBQACHGS   DEFAULT TO GLOBAL OPTIONS                    
*                                                                               
*        FIND COLUMN OVERRIDE                                                   
*                                                                               
         GOTO1 FNDFLTRA,DMCB,=AL2(PRQCFACH),0,0                                 
*                                                                               
         ICM   RF,15,8(R1)         POINT TO FOUND FILTER                        
         BZ    GDLACH10               SKIP IF NONE                              
*                                                                               
         USING FLTCTL,RF           ESTABLISH FILTER                             
*                                                                               
         SR    R1,R1                                                            
         ICM   R1,3,FLTFVAL        GET FILTER VALUE                             
*                                                                               
         CHI   R1,PRQACINC         IF INCLUDING ADDITIONAL CHARGES              
         BE    GDLACH10               SKIP - ITS THE DEFAULT                    
*                                                                               
         OI    GDLACOPT,PBQACOPQ   INDICATE ADDL CHARGE IN EFFECT               
*                                                                               
         CHI   R1,PRQACXFX         IF EXCLUDING FX ADDITIONAL CHARGES           
         BNE   *+12                                                             
         OI    GDLACOPT,PBQACXFQ      SET FLAG                                  
         B     GDLACH10                                                         
*                                                                               
PBQACXFQ EQU   X'04'                                                            
*                                                                               
         CHI   R1,PRQACEXC         IF EXCLUDING ADDITIONAL CHARGES              
         BNE   *+12                                                             
         OI    GDLACOPT,PBQACEXQ      SET FLAG                                  
         B     GDLACH10                                                         
*                                                                               
         CHI   R1,PRQACONL         IF ONLY      ADDITIONAL CHARGES              
         BNE   *+12                                                             
         OI    GDLACOPT,PBQACONQ      SET FLAG                                  
         B     GDLACH10                                                         
*                                                                               
         CHI   R1,PRQACONE         IF SINGLE ACHG CODE                          
         BNE   *+14                                                             
         OI    GDLACOPT,PBQAC1Q       SET FLAG                                  
         MVC   GDLACHCD,FLTFVAL+2     SAVE CHARGE CODE                          
*                                                                               
         CHI   R1,PRQACXFX         IF EXCLUDE FX ADDITIONAL CHARGE              
         BNE   *+8                                                              
         OI    GDLACOPT,PBQACXFQ      SET FLAG                                  
*                                                                               
GDLACH10 DS    0H                                                               
*                                                                               
         TM    GDLACOPT,PBQACLSQ   IF LISTING ACHGS                             
         BNO   *+12                                                             
         BRAS  RE,LSTACH              NEED SPECIAL HANDLING                     
         B     GDLACHX                                                          
*                                                                               
         TM    GDLACOPT,PBQACEXQ   IF EXCLUDING ACHGS                           
         BNO   *+12                                                             
         MVI   GDLPARM4,C'X'          SET INDICATOR                             
         B     GDLACHX                                                          
*                                                                               
         MVI   GDLPARM4,C'A'       ASSUME GETINS PARM6 ACTIVE                   
*                                                                               
         TM    GDLACOPT,PBQACXFQ   IF EXCLUDING FX ACHG                         
         BNO   *+8                                                              
         MVI   GDLPARM4,C'F'          SET INDICATOR                             
*                                                                               
         TM    GDLACOPT,PBQAC1Q    IF ONE SPECIFIC CHARGE                       
         BNO   *+16                                                             
         LA    RF,GDLACHCD            POINT TO IT                               
         ST    RF,GDLPARM6            SET IN PARAMETERS                         
         B     GDLACHX                                                          
*                                                                               
         TM    GDLACOPT,PBQACONQ   IF ONLY ADDITIONAL CHARGES                   
         BNO   *+16                                                             
         LA    RF,=C'ALL'             POINT TO IT                               
         ST    RF,GDLPARM6            SET IN PARAMETERS                         
         B     GDLACHX                                                          
*                                                                               
         MVI   GDLPARM4,0          UNKNOWN OPTION                               
*                                                                               
GDLACHX  DS    0H                                                               
*                                                                               
         CLI   PBUYKRCD,PBUYKIDQ   EXIT IF THERE IS NO BUY RECORD               
         BNE   GETDLRER                                                         
*                                                                               
         CLI   GDLPARM4,X'FF'      EXIT IF ERROR DETECTED                       
         BE    GETDLRER                                                         
*                                                                               
         OC    PASSPRD,PASSPRD     SKIP IF THERE IS NO PASSIVE PRODUCT          
         BZ    GDLPASSN                                                         
*                                                                               
         MVC   PBGRS,=C'CRATE'     SET DATA TYPE REQUEST                        
*                                                                               
         CLI   GLARGS+1,C'C'       IF PLANNED COSTS WANTED                      
         BNE   *+8                                                              
         MVI   GDLPARM2,C'P'       SET INDICATOR                                
*                                                                               
         GOTO1 PAGETINS,DMCB,(DLRTYPE,(R2)),(GDLPARM2,PBGRS),          X        
               (PBQOAPRD,PASSPRD),(GDLPARM4,GLARGS+9),=C'BOTH',GDLPARM6         
*                                                                               
         CLI   4(R1),C'X'          IF NO PLANNED COSTS                          
         BNE   *+14                                                             
         XC    PBGRS(12),PBGRS        CLEAR ORDERED DATA                        
         B     GDLPASSX                                                         
*                                                                               
         L     R1,16(R1)           POINT TO GST AREA                            
*                                                                               
         MVC   GVALUES(GVALUESL),0(R1)  SAVE GST DATA                           
*                                                                               
         LA    RF,PSTAREA          PST FOR 10 PROVINCES                         
         LA    RE,PSTAREA-GVALUES(R1)                                           
         LA    R0,10                                                            
*                                                                               
         MVC   0(PSTAREAL,RF),0(RE)                                             
         LA    RE,PSTAREAL(RE)                                                  
         LA    RF,PSTAREAL(RF)                                                  
         BCT   R0,*-14                                                          
*                                                                               
         XC    OGROSS(PBVALUEX-PBGRS),OGROSS INIT OPEN RATE DATA                
*                                                                               
         XC    GOVALUE,GOVALUE     OPEN GST DATA                                
*                                                                               
         LA    RF,PSTOAREA         OPEN PST FOR 10 PROVINCES                    
         LA    R0,10                                                            
*                                                                               
         XC    0(PSTAREAL,RF),0(RF)                                             
         LA    RF,PSTAREAL(RF)                                                  
         BCT   R0,*-10                                                          
*                                                                               
         CLI   PBFINAN,C'Y'        SKIP OPEN RATE CALC IF NOT FINANCIAL         
         BNE   GDLPASSX                 CLIENT                                  
*                                                                               
         MVC   OGROSS,=C'CRATE'    SET DATA REQUEST                             
*                                                                               
         GOTO1 PAGETINS,DMCB,(DLRTYPE,(R2)),(C'O',OGROSS),             X        
               (PBQOAPRD,PASSPRD),(GDLPARM4,GLARGS+9),=C'BOTH',GDLPARM6         
*                                                                               
         L     R1,16(R1)           POINT TO GST AREA                            
*                                                                               
         MVC   GOVALUE,0(R1)       SAVE GST DATA                                
*                                                                               
         LA    RF,PSTOAREA         PST FOR 10 PROVINCES                         
         LA    RE,PSTAREA-GVALUES(R1)                                           
         LA    R0,10                                                            
*                                                                               
         MVC   0(PSTAREAL,RF),0(RE)                                             
         LA    RE,PSTAREAL(RE)                                                  
         LA    RF,PSTAREAL(RF)                                                  
         BCT   R0,*-14                                                          
*                                                                               
GDLPASSX DS    0H                                                               
*                                                                               
         B     GETDLROK                                                         
*                                                                               
GDLPASSN DS    0H                                                               
*                                                                               
         MVC   PBGRS,=C'CRATE'     SET DATA TYPE REQUEST                        
*                                                                               
         CLI   GLARGS+1,C'C'       IF PLANNED COSTS WANTED                      
         BNE   *+8                                                              
         MVI   GDLPARM2,C'P'          SET PARAMETER                             
*                                                                               
         GOTO1 PAGETINS,DMCB,(DLRTYPE,(R2)),(GDLPARM2,PBGRS),          X        
               (PBQOAPRD,PBUYKPRD),(GDLPARM4,GLARGS+9),=C'BOTH',       X        
               GDLPARM6                                                         
*                                                                               
         CLI   4(R1),C'X'          IF NO PLANNED COSTS                          
         BNE   *+14                                                             
         XC    PBGRS(12),PBGRS        CLEAR ORDERED DATA                        
         B     GDLPRDX                                                          
*                                                                               
         ICM   R6,15,PBPAYELA                                                   
         BZ    *+16                                                             
         CLC   =C'AA',PPACCODE-PPAYELEM(R6)                                     
         B     *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     R1,16(R1)           POINT TO GST AREA                            
*                                                                               
         MVC   GVALUES(GVALUESL),0(R1)  SAVE GST DATA                           
*                                                                               
         LA    RF,PSTAREA          PST FOR 10 PROVINCES                         
         LA    RE,PSTAREA-GVALUES(R1)                                           
         LA    R0,10                                                            
*                                                                               
         MVC   0(PSTAREAL,RF),0(RE)                                             
         LA    RE,PSTAREAL(RE)                                                  
         LA    RF,PSTAREAL(RF)                                                  
         BCT   R0,*-14                                                          
*                                                                               
         XC    OGROSS(PBVALUEX-PBGRS),OGROSS INIT OPEN RATE DATA                
*                                                                               
         XC    GOVALUE,GOVALUE     OPEN GST DATA                                
*                                                                               
         LA    RF,PSTOAREA         OPEN PST FOR 10 PROVINCES                    
         LA    R0,10                                                            
*                                                                               
         XC    0(PSTAREAL,RF),0(RF)                                             
         LA    RF,PSTAREAL(RF)                                                  
         BCT   R0,*-10                                                          
*                                                                               
         CLI   PBFINAN,C'Y'        SKIP IF NOT A FINANCIAL CLIENT               
         BNE   GDLPRDX                                                          
*                                                                               
         MVC   OGROSS,=C'CRATE'                                                 
*                                                                               
         GOTO1 PAGETINS,DMCB,(DLRTYPE,(R2)),(C'O',OGROSS),             X        
               (PBQOAPRD,PBUYKPRD),(GDLPARM4,GLARGS+9),=C'BOTH',       X        
               GDLPARM6                                                         
*                                                                               
         L     R1,16(R1)           POINT TO GST AREA                            
*                                                                               
         MVC   GOVALUE,0(R1)       SAVE GST DATA                                
*                                                                               
         LA    RF,PSTOAREA         PST FOR 10 PROVINCES                         
         LA    RE,PSTAREA-GVALUES(R1)                                           
         LA    R0,10                                                            
*                                                                               
         MVC   0(PSTAREAL,RF),0(RE)                                             
         LA    RE,PSTAREAL(RE)                                                  
         LA    RF,PSTAREAL(RF)                                                  
         BCT   R0,*-14                                                          
*                                                                               
GDLPRDX  DS    0H                                                               
*                                                                               
GETDLROK DS    0H                  NORMAL EXIT                                  
*                                                                               
         CR    RB,RB               SET EQUAL CC                                 
         B     GETDLRSX                                                         
*                                                                               
GETDLRER DS    0H                  ERROR EXIT                                   
         LTR   RB,RB               SET UNEQUAL CC                               
*                                                                               
GETDLRSX DS    0H                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         TITLE 'HANDLE LISTING ADDITIONAL CHARGES - LSTACH'                     
***********************************************************************         
*                                                                     *         
*        DETERMINE TYPE OF DOLLARS TO BE RETURNED IN LISTIND          *         
*             SITUATION                                               *         
*                                                                     *         
*                                                                     *         
* NTRY   I/O1 HAS BUY RECORD                                          *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         DS    0D                                                               
LSTACH   NTR1  LABEL=*                                                          
*                                                                               
         USING SPOOLD,R8           ESTABLISH SPOOL WORKAREA                     
         USING SYSD,R9             ESTABLISH PRINTWRITER WORKAREA               
         USING GLOBALD,RA          ESTABLISH DRIVER WORKAREA                    
         USING PBLOCK,R7                                                        
         USING GEND,RC                                                          
         USING PBUYRECD,R2                                                      
*                                                                               
*        CHECK TYPE OF DOLLARS BEING PROCESSED NOW                              
*                                                                               
         TM    GDLACOPT,X'FF'-PBQACOPQ SKIP IF NO ADDL CHG OPTS ACTIVE          
         BZ    LSTACHX                                                          
*                                                                               
         MVI   GDLPARM4,C'X'       DEFAULT TO NO ADDITIONAL CHARGES             
         XC    GDLPARM6,GDLPARM6   INIT PARM 6                                  
*                                                                               
         TM    GDLACOPT,PBQACEXQ   DONE IF EXCLUDING ADDITIONAL CHRGES          
         BO    LSADFL10                                                         
*                                                                               
         TM    GDLACOPT,PBQACONQ+PBQAC1Q IF ONLY OR 1 ACHG                      
         BZ    LSADFL10                                                         
*                                                                               
         MVI   GDLPARM4,C'A'       DEFAULT TO ADDITIONAL CHARGES                
         LA    RF,GDLACHCD         POINT TO CHARGE CODE                         
*                                                                               
         OC    0(L'GDLACHCD,RF),0(RF) IF NO CHARGE AROUND                       
         BNZ   *+8                                                              
         LA    RF,=C'ALL'               RESET PARM6                             
*                                                                               
         ST    RF,GDLPARM6                                                      
*                                                                               
LSADFL10 DS    0H                                                               
*                                                                               
         ICM   R6,15,PBACHELA      IF ACHG ELEMENT PROVIDED                     
         BZ    LSAACHN                                                          
*                                                                               
         TM    GDLACOPT,PBQACEXQ   DONE IF EXCLUDING ADDITIONAL CHRGES          
         BO    LSAACHER                                                         
*                                                                               
         USING PACELEM,R6          ESTABLISH ACHG ELEMENT                       
*                                                                               
         TM    GDLACOPT,PBQAC1Q    IF RESTRICTED TO ONE CHARGE                  
         BNO   LSAACH20                                                         
*                                                                               
         CLC   PACCODE,GDLACHCD       ELM MUST MATCH SELECTED CHARGE            
         BNE   LSAACHER                                                         
*                                                                               
LSAACH20 DS    0H                                                               
*                                                                               
         LA    RF,PACCODE          SET A(ACHG CODE)                             
         ST    RF,GDLPARM6                                                      
         MVI   GDLPARM4,C'A'       SET GETINS FOR SINGLE ACHG CODE              
*                                                                               
         B     LSAACHX                                                          
*                                                                               
LSAACHER DS    0H                                                               
*                                                                               
         MVI   GDLPARM4,X'FF'         SET ERROR CONDITION                       
*                                                                               
LSAACHX  DS    0H                                                               
*                                                                               
         B     LSTACHX                                                          
*                                                                               
LSAACHN  DS    0H                                                               
*                                                                               
         ICM   R6,15,PBPAYELA      IF PAY ELEMENT PROVIDED                      
         BZ    LSAPAYN                                                          
*                                                                               
         USING PPAYELEM,R6         ESTABLISH PAY ELEMENT                        
*                                                                               
         CLI   1(R6),PPACCODE-PPAYELEM IF ACHG CODE IN PAY ELEM                 
         BNH   LSAPAY30                                                         
*                                                                               
         TM    GDLACOPT,PBQACEXQ   DONE IF EXCLUDING ADDITIONAL CHRGES          
         BO    LSAPAYER                                                         
*                                                                               
         TM    GDLACOPT,PBQAC1Q    IF RESTRICTED TO ONE CHARGE                  
         BNO   LSAPAY20                                                         
*                                                                               
         CLC   PPACCODE,GDLACHCD      ELM MUST MATCH SELECTED CHARGE            
         BNE   LSAPAYER               USE DEFAULT                               
*                                                                               
LSAPAY20 DS    0H                                                               
*                                                                               
         LA    RF,PPACCODE             SET A(PAY ACH CODE)                      
         ST    RF,GDLPARM6                                                      
         MVI   GDLPARM4,C'A'           SET GETINS FOR SINGLE PAY CODE           
*                                                                               
         B     LSAPAYX                                                          
*                                                                               
LSAPAY30 DS    0H                                                               
*                                                                               
         CLI   GLARGS,C'B'         IF BASIC BUY KEYWORD                         
         BNE   LSAPAYX                                                          
*                                                                               
LSAPAYER DS    0H                                                               
*                                                                               
         MVI   GDLPARM4,X'FF'         SET ERROR CONDITION                       
*                                                                               
LSAPAYX  DS    0H                                                               
*                                                                               
         B     LSTACHX                                                          
*                                                                               
LSAPAYN  DS    0H                                                               
*                                                                               
         ICM   R6,15,PBBLLELA       IF BILL ELEMENT PROVIDED                    
         BZ    LSABILN                                                          
*                                                                               
         USING PBILELEM,R6          ESTABLISH BILL ELEMENT                      
*                                                                               
         CLI   1(R6),PBACCODE-PBILELEM IF ACHG CODE IN BILL ELEM                
         BNH   LSABIL30                                                         
*                                                                               
         TM    GDLACOPT,PBQACEXQ   DONE IF EXCLUDING ADDITIONAL CHRGES          
         BO    LSABILER                                                         
*                                                                               
         TM    GDLACOPT,PBQAC1Q    IF RESTRICTED TO ONE CHARGE                  
         BNO   LSABIL20                                                         
*                                                                               
         CLC   PBACCODE,GDLACHCD      ELM MUST MATCH SELECTED CHARGE            
         BNE   LSABILER               USE DEFAULT                               
*                                                                               
LSABIL20 DS    0H                                                               
*                                                                               
         LA    RF,PBACCODE             SET A(BILL ACH CODE)                     
         ST    RF,GDLPARM6                                                      
         MVI   GDLPARM4,C'A'           SET GETINS FOR SINGLE ACH CODE           
*                                                                               
         B     LSABILX                                                          
*                                                                               
LSABIL30 DS    0H                                                               
*                                                                               
         CLI   GLARGS,C'B'         IF BASIC BUY KEYWORD                         
         BNE   LSABILX                                                          
*                                                                               
LSABILER DS    0H                                                               
*                                                                               
         MVI   GDLPARM4,X'FF'         SET ERROR CONDITION                       
*                                                                               
LSABILX  DS    0H                                                               
*                                                                               
         B     LSTACHX                                                          
*                                                                               
LSABILN  DS    0H                                                               
*                                                                               
*        DOING TOTAL BUY RUN NOW                                                
*                                                                               
LSATOT   DS    0H                                                               
*                                                                               
         CLI   GLARGS,C'B'         SKIP IF NOT TOTAL BUY KEYWORD                
         BNE   LSATOTER                                                         
*                                                                               
         TM    GDLACOPT,PBQACONQ+PBQAC1Q SKIP IF ONLY OR 1 ACHG                 
         BNZ   LSATOTER                                                         
*                                                                               
         TM    GDLACOPT,PBQACLSQ   SKIP IF NOT LISTING CHARGES                  
         BNO   LSATOT10                                                         
*                                                                               
         MVI   GDLPARM4,C'X'       DEFAULT TO NO ADDITIONAL CHARGES             
         XC    GDLPARM6,GDLPARM6   INIT PARM 6                                  
*                                                                               
LSATOT10 DS    0H                                                               
*                                                                               
         B     LSATOTX                                                          
*                                                                               
LSATOTER DS    0H                                                               
*                                                                               
         MVI   GDLPARM4,X'FF'         SET ERROR CONDITION                       
*                                                                               
LSATOTX  DS    0H                                                               
*                                                                               
         B     LSTACHX                                                          
*                                                                               
LSTACHX  DS    0H                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         DS    CL256               FOR NEW Z/OS PROCESSOR                       
GDLWORK  DS    0D                  WORKAREAS                                    
GDLPARM6 DS    A                   A(PARM 6 FOR GETINS                          
GDLACOPT DS    XL1                 ADDL CHGS OPTIONS WORKAREA                   
GDLPARM2 DS    XL1                 PARM 2-0 IN GETINS CALL                      
GDLPARM4 DS    XL1                 PARM 4-0 IN GETINS CALL                      
GDLACHCD DS    XL2                 CHARGE CODE                                  
GDLWORKL EQU   *-GDLWORK           LENGTH OF WORKAREAS                          
*                                                                               
         DROP                                                                   
*                                                                               
         TITLE   'PRWRIDLR DSECTS / STORAGE'                                    
* PRWRIWORKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE PRWRIWORKD                                                     
         EJECT                                                                  
DATELSTD DSECT                                                                  
       ++INCLUDE PRDATELSTD                                                     
         EJECT                                                                  
MYWORKD  DSECT                                                                  
         DS    10D                                                              
WORKAREA DS    60D                                                              
         SPACE 2                                                                
LASTIOD  DSECT                                                                  
SAVKEYS  DS   CL25                                                              
SVADKEY  DS   CL14                                                              
SVADCD   DS   CL6                                                               
SKPUB    DS   CL18                                                              
SVLSTDAT DS   CL4                                                               
MLTREC   DS   CL1                                                               
         PRINT ON                                                               
* DRGLOBAL                                                                      
         PRINT OFF                                                              
       ++INCLUDE DRGLOBAL                                                       
         PRINT ON                                                               
* DRIVETABLE                                                                    
         PRINT OFF                                                              
       ++INCLUDE DRIVETABLE                                                     
         PRINT ON                                                               
* DRINTRECD2                                                                    
         PRINT OFF                                                              
       ++INCLUDE DRINTRECD2                                                     
         PRINT ON                                                               
* DDSPOOLD                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDSPOOLD                                                       
         PRINT ON                                                               
* DDSPLWORKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDSPLWORKD                                                     
         PRINT ON                                                               
* PRGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE PRGENFILE                                                      
         PRINT ON                                                               
* PRWRIEQUS                                                                     
         PRINT OFF                                                              
       ++INCLUDE PRWRIEQUS                                                      
         PRINT ON                                                               
* DDCOMFACSD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACSD                                                     
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'033PRWRIDLR  02/20/20'                                      
         END                                                                    
