*          DATA SET ACSRCHPASS AT LEVEL 011 AS OF 01/08/16                      
*CATALP ACSRCHP                                                                 
ACSRCHP  TITLE 'ACCOUNT SEARCH PASSIVE POINTER MAINTENANCE MODULE'              
         PRINT NOGEN                                                            
***********************************************************************         
* P1: 0 = C'A' FOR RECORD THAT HAS BEEN ADDED                         *         
*         C'C' FOR RECORD THAT IS TO BE CHANGED                       *         
*         C'D' FOR RECORD THAT IS TO BE DELETED                       *         
*         C'R' FOR RECORD THAT IS TO BE RESTORED                      *         
*         C'F' TO REFRESH STATUS ON SAPRECD FOR ACCOUNT RECORD        *         
*     1 = N/D  (SETTING BELOW IS NO LONGER USED - TEST ALWAYS DONE)   *         
*         C'T' TO TEST RECORD IS SEARCHABLE BEFORE ADD/CHANGE/DELETE  *         
*     2 = C'F' IF ACCFIL RECORD                                       *         
*     3 = C'F' IF RUNNING OFF-LINE                                    *         
*                                                                     *         
* P2:   = A(ACCMST/ACCFIL RECORD)                                     *         
*                                                                     *         
* P3:   = A(D/A) IF ACCMST RECORD                                     *         
*                                                                     *         
* P4:   = A(NAME ELEMENT SAVE AREA)  (ACCOUNT RECORDS ONLY)           *         
*    OR = 0 TO DO A GETREC TO GET OLD NAME                            *         
*                                                                     *         
* P5:   = A(COMFACS)                                                  *         
*                                                                     *         
* P6: 0 = LEVEL OF ACCOUNT (IF ACTION A AND NO SEARCHABLE TEST)       *         
*   1-3 = A(ACCFACS)    (IF ACCFIL RECORD NEEDS TO BE CONVERTED)      *         
*                                                                     *         
* ON EXIT FOR ACTIONS A/C/D                                           *         
* P1: 0-1 = NO. OF SEARCH RECORDS ADDED                               *         
*     2-3 = NO. OF SEARCH RECORDS WRITTEN (DELETED/RESTORED)          *         
***********************************************************************         
                                                                                
* DPEA 017 11JUN02 NEW REFRESH ACTION FOR SAPRECD STATUS                        
* DPEA 018 26JUN02 REFRESH SAPRECD STATUS ON ACTION CHANGE                      
* DPEA 019 09SEP03 UPDATE FLEXIBILL COMP. RECORD FOR CLIENTS/WORKCODES          
* NSHE 020 13JAN05 ADD EXPENDITURE TYPE RECORD SEARCH                           
* DPEA 021 27JUL05 <LO01-4465> EXTRA DATA FOR ACSRCHIDR (VIA VSRCHXEC)          
*                  REFRESH STATUS ON ALL ACCOUNT SEARCH RECORDS                 
* NSHE 022 01SEP05 <LO01-4317> ADD DEPARTMENT SEARCH                            
* DPEA 023 05SEP05 <LO01-4465> ALLOW DOUBLE NO. OF ACCOUNT SEARCH RECS          
* DPEA 024 20SEP05 <LO01-4465> FIND LEVEL OF A/C BEFORE SEARCHABLE TEST         
* NSHE 025 05FEB07 <LO01-6033> USE ALTERNATIVE NAME                             
* MPEN 027 27MAR13 <BR54787L> FIX BUG FOR DELETED ACCOUNTS                      
* TKLU 028 28OCT15 <RD007727> ESTIMATE NAME SEARCH SUPPORT                      
* TKLU 029 02DEC15 <RD009622> ORDER NAME SEARCH SUPPORT + REMOVE                
*                  TRAILING GOTO 'COMMA' FOR NEW ASSEMBLER EXIT                 
                                                                                
ACSRCHP  CSECT                                                                  
         NMOD1 WORKL,**ACSP**,RR=RE,CLEAR=YES                                   
         USING WORKD,RC            RC=A(LOCAL W/S)                              
         USING SEARCHD,SRCHBLK                                                  
         ST    RE,RELO                                                          
         ST    R1,APARMS                                                        
         MVC   PARMS(PARMSL),0(R1)                                              
*                                                                               
         L     RE,PACOMFAC                                                      
         MVC   VDATAMGR,CDATAMGR-COMFACSD(RE)                                   
         MVC   VCALLOV,CCALLOV-COMFACSD(RE)                                     
         MVC   VSRCHPAS,=V(SRCHPASS)                                            
*                                                                               
         CLI   POFFLIN,POFFLINQ    OFF-LINE                                     
         BNE   ONLINE                                                           
         MVC   VACCEMU,=V(DMACCEMU)                                             
         MVI   SRCHIND1,X'C0'                                                   
         MVI   SRCHIND2,X'01'                                                   
         B     NTRY02                                                           
*                                                                               
ONLINE   L     RE,PAACCFAC                                                      
         MVC   VACCEMU,AACCEMU-ACCFACSD(RE)                                     
         L     RE,VSRCHPAS                                                      
         A     RE,RELO                                                          
         ST    RE,VSRCHPAS                                                      
         MVI   SRCHIND1,X'80'                                                   
         MVI   SRCHIND2,X'01'                                                   
*                                                                               
NTRY02   L     RF,PAREC                                                         
         MVI   RECTYPE,RECTAC      TEST ACCOUNT RECORD                          
         CLI   ACTKCPY-ACTRECD(RF),X'40'                                        
         JH    NTRY04                                                           
         MVI   RECTYPE,RECTWC      TEST WORK-CODE RECORD                        
         CLI   WCOKTYP-WCORECD(RF),WCOKTYPQ                                     
         JE    NTRY04                                                           
         MVI   RECTYPE,RECTMC      TEST MEDIA CODE RECORD                       
         CLI   PMDKTYP-PMDRECD(RF),PMDKTYPQ                                     
         JE    NTRY04                                                           
         MVI   RECTYPE,RECTEX      TEST EXPENDITURE TYPE RECORD                 
         CLI   ETYKTYP-ETYRECD(RF),ETYKTYPQ                                     
         JNE   NTRY02A                                                          
         CLI   ETYKSUB-ETYRECD(RF),ETYKSUBQ                                     
         JE    NTRY04                                                           
NTRY02A  MVI   RECTYPE,RECTES      TEST ESTIMATE RECORD                         
         CLI   ESTKTYP-ESTRECD(RF),ESTKTYPQ                                     
         JNE   NTRY02B                                                          
         CLI   ESTKSUB-ESTRECD(RF),ESTKSUBQ                                     
         JNE   NTRY02B                                                          
         CLI   ESTKSEQ-ESTRECD(RF),ESTKSMQ                                      
         JE    NTRY04                                                           
NTRY02B  MVI   RECTYPE,RECTOR      TEST ORDER RECORD                            
         CLI   ORDKTYP-ORDRECD(RF),ORDKTYPQ                                     
         JNE   NTRY02C                                                          
         CLC   ORDKORD-ORDRECD(L'ORDKORD,RF),=C'00000'                          
         JE    NTRY02C                                                          
         CLI   ORDKSEQ-ORDRECD(RF),0                                            
         JNE   NTRY02C                                                          
         TM    ORDRSTA2-ORDRECD(RF),ORDSEXEX                                    
         JNZ   NTRY04                                                           
NTRY02C  MVI   RECTYPE,RECTDP      TEST DEPARTMENT RECORD                       
         CLI   DPTKTYP-DPTRECD(RF),DPTKTYPQ                                     
         JNE   *+2                                                              
*                                                                               
NTRY04   DS    0H                                                               
         CLI   PRECFIL,PRECFILQ    TEST ACCFIL RECORD                           
         BNE   *+12                                                             
         BAS   RE,EMULATE          CONVERT TO ACCMST                            
         BNE   EXIT                                                             
*                                                                               
         BAS   RE,TEST             TEST IS SEARCHABLE                           
         BE    NTRY06                                                           
*&&UK*&& BAS   RE,UPDFXB           STILL UPDATE FLEXIBILL RECORD                
         L     RE,APARMS                                                        
         XC    0(4,RE),0(RE)       CLEAR COUNTERS                               
         B     EXITN                                                            
*                                                                               
NTRY06   DS    0H                                                               
*&&UK*&& BAS   RE,UPDFXB           UPDATE FLEXIBILL RECORD                      
*                                                                               
         CLI   PRECFIL,PRECFILQ    GET D/A OF ACCMST RECORD                     
         BNE   NTRY10                                                           
         BAS   RE,GETDA                                                         
         BNE   EXIT                                                             
*                                                                               
NTRY10   DS    0H                                                               
         LA    RE,ROUTS                                                         
         LA    R0,ROUTSN                                                        
NTRY12   DS    0H                                                               
         CLC   PACTION,0(RE)                                                    
         BE    NTRY20                                                           
         LA    RE,L'ROUTS(RE)                                                   
         BCT   R0,NTRY12                                                        
         DC    H'0'                                                             
NTRY20   DS    0H                                                               
         LH    RF,1(RE)                                                         
         AR    RF,RB                                                            
         BASR  RE,RF                                                            
*                                                                               
NTRYX    DS    0H                                                               
         L     RE,APARMS                                                        
         MVC   0(4,RE),SPARM       COPY TOTAL ADDS/WRITES                       
         B     EXITY                                                            
*                                                                               
ROUTS    DS    0XL3                * ROUTINES *                                 
         DC    AL1(PACTADD),AL2(ADD-ACSRCHP)                                    
         DC    AL1(PACTCHA),AL2(CHA-ACSRCHP)                                    
         DC    AL1(PACTDEL),AL2(DEL-ACSRCHP)                                    
         DC    AL1(PACTRES),AL2(RES-ACSRCHP)                                    
         DC    AL1(PACTREF),AL2(REF-ACSRCHP)                                    
ROUTSN   EQU   (*-ROUTS)/L'ROUTS                                                
         DS    0H                                                               
*                                                                               
EXITN    LTR   RB,RB                                                            
         B     EXIT                                                             
EXITY    CR    RB,RB                                                            
EXIT     XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO EMULATE ACCFIL RECORD                                    *         
***********************************************************************         
         SPACE 1                                                                
         PUSH  USING                                                            
EMULATE  L     RF,4(RD)            EXTEND W/S FOR RECORD                        
         LA    RD,EMUWORKL(RD)                                                  
         ST    RD,8(RF)                                                         
         ST    RF,4(RD)                                                         
*                                                                               
EMULATE0 NTR1  ,                                                                
         LHI   R3,EMUWORK-WORKD                                                 
         LA    R3,WORKD(R3)                                                     
         USING EMUWORK,R3                                                       
*                                                                               
*        GOTO1 VDATAMGR,DMCB,DTFAD,ACCOUNT                                      
*        L     RE,12(R1)                                                        
*        TM    ISFTYPE-ISDTF(RE),ISFTEMU                                        
*        BZ    EXITN               ACCFIL ISN'T EMULATED                        
*                                                                               
         L     RE,PAREC            COPY ACCFIL RECORD                           
         XR    RF,RF                                                            
         ICM   RF,3,ACCORLEN(RE)                                                
         LA    R0,EMUMST                                                        
         ST    R0,PAREC                                                         
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         GOTO1 ,DMCB,OLDN,,,PAREC                                               
         LR    R2,R1                                                            
         ICM   R2,8,EFFS                                                        
         GOTO1 VACCEMU             EMULATE RECORD                               
*                                                                               
         B     EXITY                                                            
         POP   USING                                                            
         SPACE 1                                                                
***********************************************************************         
* ROUTINE TO GET D/A                                                  *         
***********************************************************************         
         SPACE 1                                                                
         PUSH  USING                                                            
GETDA    NTR1  ,                                                                
         LHI   R3,EMUWORK-WORKD                                                 
         LA    R3,WORKD(R3)                                                     
         USING EMUWORK,R3                                                       
*                                                                               
         GOTO1 VDATAMGR,DMCB,(X'08',DMREAD),ACCDIR,PAREC,EMUDIR,WORK            
         BE    GETDA2                                                           
         CLI   8(R1),X'02'         TEST RECORD IS DELETED                       
         BNE   EXITN               ACC SYSTEM IS PROBABLY READ-ONLY             
*                                                                               
GETDA2   LA    RE,EMUDIR+(ACTKDA-ACTKEY)                                        
         ST    RE,PADA                                                          
*                                                                               
         B     EXITY                                                            
         POP   USING                                                            
         EJECT                                                                  
***********************************************************************         
* TEST RECORD IS SEARCHABLE                                           *         
***********************************************************************         
         SPACE 1                                                                
         PUSH  USING                                                            
TEST     NTR1  ,                                                                
         TM    RECTYPE,RECTAC                                                   
         BZ    EXITY               ALL WORK/MEDIA CODE RECORDS ARE OK           
*                                                                               
         L     R2,PAREC                                                         
         USING ACTRECD,R2                                                       
         MVC   ACCSTAT,ACTRSTAT-ACTRECD(RF)  COPY STATUS BYTE                   
         CLC   ACTKACT,SPACES      CHECK DEFINATELY AN ACCOUNT RECORD           
         BE    EXITN                                                            
         CLC   ACTKEY+ACTKEND(L'ACTKEY-ACTKEND),SPACES                          
         BNE   EXITN                                                            
*                                                                               
         MVC   ACCSTAT,ACTRSTAT    COPY STATUS BYTE                             
         CLC   PRODUL,ACTKUNT      TEST PRODUCTION                              
         BNE   *+8                                                              
         OI    RECINDS,RECIPROD                                                 
*                                                                               
         ICM   RF,15,=V(SRCHACC)                                                
         BNZ   TEST02                                                           
         GOTO1 VCALLOV,DMCB,0,X'D9000AEF'                                       
         CLI   4(R1),X'FF'                                                      
         BE    EXITN                                                            
         L     RF,0(R1)                                                         
TEST02   AH    RF,2(RF)                                                         
         USING SDSRCHD,RF          RF=A(SEARCH TYPE TABLE)                      
TEST04   CLC   SDSRUL,ACTKUNT      MATCH ON UNIT/LEDGER                         
         BE    TEST06                                                           
         AH    RF,SDSRCHLN                                                      
         CLC   SDSRCHLN,BZEROES                                                 
         BNE   TEST04                                                           
         B     EXITN                                                            
         DROP  RF                                                               
*                                                                               
TEST06   LA    R4,IOKEY                                                         
         USING CPYRECD,R4          R4=A(LEDGER RECORD)                          
         MVC   CPYKEY,SPACES                                                    
         MVC   CPYKCPY,ACTKEY                                                   
         GOTO1 VDATAMGR,DMCB,DMREAD,ACCDIR,CPYKEY,CPYKEY,WORK                   
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 VDATAMGR,DMCB,GETREC,ACCMST,CPYKDA,IOREC,WORK                    
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         GOTOR GETEL,DMCB,('CPYELQ',IOREC)                                      
         USING CPYELD,R3           FIND LEVEL OF ACCOUNT                        
         XC    ACCCMP,ACCCMP                                                    
         CLI   CPYLN,CPYSTATC-CPYELD                                            
         BL    TEST10                                                           
         MVC   ACCCMP,CPYSTATC                                                  
*                                                                               
TEST10   LA    R4,IOKEY                                                         
         USING LDGRECD,R4          R4=A(LEDGER RECORD)                          
         MVC   LDGKEY,SPACES                                                    
         MVC   LDGKEY(LDGKEND),ACTKEY                                           
         GOTO1 VDATAMGR,DMCB,DMREAD,ACCDIR,LDGKEY,LDGKEY,WORK                   
         JNE   *+2                                                              
         GOTO1 VDATAMGR,DMCB,GETREC,ACCMST,LDGKDA,IOREC,WORK                    
         JNE   *+2                                                              
*                                                                               
         GOTOR GETEL,DMCB,('ACLELQ',IOREC)                                      
         USING ACLELD,R3           FIND LEVEL OF ACCOUNT                        
         LA    RF,1                                                             
         LA    R5,ACCLV1                                                        
TEST12   XR    RE,RE                                                            
         IC    RE,ACLVLEN                                                       
         STC   RE,0(R5)                                                         
         LA    RE,ACTKACT(RE)                                                   
         CLC   0(L'ACTKACT,RE),SPACES                                           
         BE    TEST20                                                           
         LA    RF,1(RF)                                                         
         LA    R3,L'ACLVALS(R3)                                                 
         LA    R5,1(R5)                                                         
         B     TEST12                                                           
         DROP  R3                                                               
TEST20   STC   RF,PLVL                                                          
*                                                                               
         GOTOR GETEL,DMCB,('LDGELQ',IOREC)                                      
         USING LDGELD,R3                                                        
         CLI   LDGLN,LDGLNQ                                                     
         BL    EXITN                                                            
         TM    LDGSTAT2-LDGELD(R3),LDGSSRCH                                     
         BZ    EXITN                                                            
*                                                                               
         TM    RECINDS,RECIPROD    IF PRODUCTION, NEED OFFICE                   
         BZ    EXITY                                                            
         GOTOR GETEL,DMCB,('PPRELQ',ACTRECD)                                    
         BNE   TEST22                                                           
         MVC   ACCOFF,PPRGAOFF-PPRELD(R3)                                       
         CLI   ACCOFF,C' '         EXIT IF GOT IT                               
         BH    EXITY                                                            
*                                                                               
TEST22   DS    0H                                                               
         USING SAPRECD,IOKEY                                                    
         XC    SAPKEY,SAPKEY                                                    
         MVI   SAPKTYP,SAPKTYPQ                                                 
         MVC   SAPKCUL,ACTKCPY                                                  
         MVI   SAPKSUB,SAPKSUBQ                                                 
         XR    RE,RE               GET PRODUCT IF JOB,                          
         IC    RE,PLVL             OR CLIENT IF PRODUCT                         
         SHI   RE,1                                                             
         BNP   EXITY               (FINISHED IF CURRENT IS CLIENT)              
         STC   RE,SAPKLVL                                                       
         MVC   SAPKACT,SPACES                                                   
         IC    RE,ACCLV2                                                        
         CLI   PLVL,2                                                           
         BNE   *+8                                                              
         IC    RE,ACCLV1                                                        
         SHI   RE,1                                                             
         MVC   SAPKACT(0),ACTKACT                                               
         EX    RE,*-6                                                           
         GOTO1 VDATAMGR,DMCB,DMREAD,ACCDIR,SAPRECD,SAPRECD,WORK                 
         BNE   EXITY                                                            
         MVC   ACCOFF,SAPKSOFF                                                  
         B     EXITY                                                            
         SPACE 1                                                                
         POP   USING                                                            
         EJECT                                                                  
***********************************************************************         
* RECORD HAS JUST BEEN ADDED/RESTORED                                 *         
***********************************************************************         
         SPACE 1                                                                
RES      DS    0H                                                               
ADD      NTR1  ,                                                                
         GOTO1 SRCHPASS,SPARM,(C'N',SEARCHD),(SRCHIND1,PACOMFAC),PAREC,*        
               0,PADA,(SRCHIND2,ACC)                                            
*                                                                               
         GOTO1 CRTSAP              ADD SEARCH ACCOUNT CODE POINTER              
         BNE   EXIT                                                             
         CLI   PACTION,PACTRES                                                  
         BE    RES02                                                            
*                                                                               
ADD01    GOTO1 VDATAMGR,DMCB,DMADD,ACCDIR,IOKEY,IOKEY                           
         CLI   8(R1),0                                                          
         BE    ADD02                                                            
         CLI   8(R1),X'20'         TEST FOR DUPLICATE KEY ON ADD                
         BE    RES02                                                            
         DC    H'0'                                                             
*                                                                               
ADD02    JAS   RE,INCWRT           INCREMENT ADD TOTAL                          
*                                                                               
         B     EXITY                                                            
*                                                                               
RES02    LA    R2,IOKEY                                                         
         USING SAPRECD,R2                                                       
         GOTO1 VDATAMGR,DMCB,(X'88',DMREAD),ACCDIR,SAPRECD,IOREC                
         BE    RES04                                                            
         TM    8(R1),X'10'         TEST RECORD NOT FOUND                        
         BNZ   ADD01                                                            
         TM    8(R1),X'02'         TEST RECORD DELETED                          
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
RES04    MVC   SAPKSTAT,ACCSTAT                                                 
         NI    SAPKSTAT,FF-SAPSDELT                                             
         GOTO1 (RF),(R1),DMWRT,ACCDIR,SAPRECD,SAPRECD                           
         CLI   8(R1),0                                                          
         JNE   *+2                                                              
         DROP  R2                                                               
         JAS   RE,INCWRT                                                        
*                                                                               
         B     EXITY                                                            
         EJECT                                                                  
***********************************************************************         
* RECORD IS BEING CHANGED                                             *         
***********************************************************************         
         SPACE 1                                                                
CHA      NTR1  ,                                                                
         BAS    RE,GETBEF          GET PRE-CHANGE VERSION OF RECORD             
*                                                                               
         TM    RECTYPE,RECTDP                                                   
         JNZ   CHA10                                                            
         TM    RECTYPE,RECTEX                                                   
         JNZ   CHA10                                                            
         TM    RECTYPE,RECTES                                                   
         JNZ   CHA12                                                            
         TM    RECTYPE,RECTOR                                                   
         JNZ   CHA12                                                            
         TM    RECTYPE,RECTAC                                                   
         JZ    CHA18                                                            
         TM    ACCCMP,CPYSALTN                                                  
         JZ    CHA08                                                            
         GOTOR GETEL,DMCB,('XNMELQ',PAREC)                                      
         JNE   CHA10                                                            
         J     CHA16                                                            
CHA08    TM    ACCCMP,CPYSMEDN                                                  
         JZ    CHA10                                                            
         GOTOR GETEL,DMCB,('SNMELQ',PAREC)                                      
         JNE   CHA10                                                            
         J     CHA16                                                            
CHA10    GOTOR GETEL,DMCB,('NAMELQ',PAREC)                                      
         J     CHA16                                                            
CHA12    GOTOR GETEL,DMCB,('ENMELQ',PAREC)                                      
         JE    CHA16                                                            
         L     RE,PASAVNAM                                                      
         CLI   0(RE),ENMELQ                                                     
         JNE   CHA18                                                            
         J     CHA20                                                            
         USING NAMELD,R3                                                        
CHA16    LLC   RF,NAMLN                                                         
         BCTR  RF,0                                                             
         L     RE,PASAVNAM                                                      
         EX    RF,*+8                                                           
         JE    CHA20               EXIT IF NAMES HAVN'T CHANGED                 
         CLC   NAMELD(0),0(RE)                                                  
*                                                                               
CHA18    GOTO1 SRCHPASS,SPARM,(C'B',SEARCHD),(SRCHIND1,PACOMFAC),IOREC,*        
               SRCHLIST,PADA,(SRCHIND2,ACC)                                     
         MVI   0(R1),C'A'                                                       
***      GOTO1 (RF),(R1),,,PAREC,,,                                             
         GOTO1 (RF),(R1),,,PAREC                                                
*                                                                               
CHA20    DS    0H                  ENSURE SAPRECD STATUS REFRESHED              
         BAS   RE,REF                                                           
         B     EXIT                                                             
         B     REF                                                              
***********************************************************************         
* RECORD IS BEING DELETED                                             *         
***********************************************************************         
         SPACE 1                                                                
DEL      NTR1  ,                                                                
         BAS    RE,GETBEF          GET PRE-DELETE VERSION OF RECORD             
*                                                                               
         GOTO1 SRCHPASS,SPARM,(C'B',SEARCHD),(SRCHIND1,PACOMFAC),IOREC,*        
               SRCHLIST,PADA,(SRCHIND2,ACC)                                     
         MVI   0(R1),C'D'                                                       
***      GOTO1 (RF),(R1),,,,,,                                                  
         GOTO1 (RF),(R1)                                                        
*                                                                               
         GOTO1 CRTSAP              DELETE SEARCH ACCOUNT CODE POINTER           
         BNE   EXIT                                                             
         LA    R2,IOKEY                                                         
         USING SAPRECD,R2                                                       
         GOTO1 VDATAMGR,DMCB,(X'80',DMREAD),ACCDIR,SAPRECD,SAPRECD              
         MVC   SAPKSTAT,ACCSTAT                                                 
         OI    SAPKSTAT,SAPSDELT                                                
         GOTO1 (RF),(R1),DMWRT,ACCDIR,SAPRECD,SAPRECD                           
         CLI   8(R1),0                                                          
         JNE   *+2                                                              
         DROP  R2                                                               
         JAS   RE,INCWRT           INCREMENT WRITES TOTAL                       
*                                                                               
         B     EXITY                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO REFRESH STATUS AREA ON SAPRECD                           *         
***********************************************************************         
         SPACE 1                                                                
         PUSH  USING                                                            
REF      NTR1  ,                                                                
         L     R2,PAREC                                                         
         USING ACTRECD,R2                                                       
         BAS   RE,CRTSAP                                                        
         BNE   EXIT                                                             
         PUSH  USING                                                            
         USING SAPRECD,IOKEY                                                    
         GOTO1 VDATAMGR,DMCB,(X'08',DMREAD),ACCDIR,SAPRECD,SAPRECD              
         BE    REF02                                                            
         TM    8(R1),X'10'         TEST RECORD NOT FOUND                        
         BNZ   EXITN                                                            
         TM    8(R1),X'02'         TEST RECORD DELETED                          
         BNZ   *+6                                                              
         DC    H'0'                                                             
REF02    DS    0H                                                               
         MVC   RECSEQ,SAPKSEQ                                                   
         CLC   SAPKSTA,ACCSTAT     TEST STATUS IS OKAY                          
         BNE   REF10                                                            
         TM    RECINDS,RECIPROD                                                 
         BZ    EXITY                                                            
         CLC   SAPKSOFF,ACCOFF     TEST PRODUCTION OFFICE IS OKAY               
         BE    EXIT                                                             
*                                                                               
REF10    DS    0H                  UPDATE SAPRECD                               
         GOTO1 (RF),(R1),(X'88',DMREAD),ACCDIR,SAPRECD,SAPRECD                  
         MVC   SAPKSTAT,ACCSTAT                                                 
         MVC   SAPKSOFF,ACCOFF                                                  
         GOTO1 (RF),(R1),DMWRT,ACCDIR,SAPRECD,SAPRECD                           
         CLI   8(R1),0                                                          
         JNE   *+2                                                              
         POP   USING                                                            
         JAS   RE,INCWRT           INCREMENT WRITES TOTAL                       
*                                                                               
         PUSH  USING               UPDATE STATUS ON NORMAL SEARCH RECS          
         USING SRCRECD,IOKEY                                                    
         XC    SRCKEY,SRCKEY                                                    
         MVI   SRCKTYP,SRCKTYPQ                                                 
         MVC   SRCKCUL,ACTKCPY                                                  
         MVI   SRCKSUB,SRCKSQWD                                                 
         MVC   SRCKSEQ2,RECSEQ                                                  
         MVC   IOKEYSAV,SRCKEY                                                  
         LA    R5,SRCHBLK          R5 = A(LIST OF WORDS TO CHANGE)              
*                                                                               
         LA    R0,DMRDHI                                                        
         B     *+8                                                              
REF12    LA    R0,DMRSEQ                                                        
         GOTO1 VDATAMGR,DMCB,(X'80',(R0)),ACCDIR,SRCRECD,SRCRECD                
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   SRCKEY(SRCKWRD2-SRCKEY),IOKEYSAV                                 
         BNE   REF20                                                            
         CLC   SRCKSTAT,ACCSTAT                                                 
         BNE   REF14                                                            
         TM    RECINDS,RECIPROD                                                 
         BZ    REF12                                                            
         CLC   SRCKSOFF,ACCOFF                                                  
         BE    REF12                                                            
REF14    MVC   SRCKSTAT,ACCSTAT                                                 
         MVC   SRCKSOFF,ACCOFF                                                  
         MVC   0(L'SRCKWRD1,R5),SRCKWRD2                                        
         LA    R5,L'SRCKWRD1(R5)                                                
         GOTO1 (RF),(R1),DMWRT,ACCDIR,SRCRECD,SRCRECD                           
         JAS   RE,INCWRT                                                        
         B     REF12                                                            
*                                                                               
REF20    DS    0H                                                               
         MVI   0(R5),0                                                          
         LA    R5,SRCHBLK                                                       
REF22    DS    0H                                                               
         CLI   0(R5),0                                                          
         BE    REF30                                                            
         XC    SRCKEY,SRCKEY                                                    
         MVI   SRCKTYP,SRCKTYPQ                                                 
         MVC   SRCKCUL,ACTKCPY                                                  
         MVI   SRCKSUB,SRCKWDSQ                                                 
         MVC   SRCKWRD1,0(R5)                                                   
         MVC   SRCKSEQ1,RECSEQ                                                  
         MVC   SRCKACT,ACTKACT                                                  
         GOTO1 VDATAMGR,DMCB,(X'88',DMREAD),ACCDIR,SRCRECD,SRCRECD              
         CLI   8(R1),0                                                          
         BE    REF24                                                            
         TM    8(R1),X'02'                                                      
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
REF24    MVC   SRCKSTAT,ACCSTAT                                                 
         MVC   SRCKSOFF,ACCOFF                                                  
         GOTO1 (RF),(R1),DMWRT,ACCDIR,SRCRECD,SRCRECD                           
         JNE   *+2                                                              
         JAS   RE,INCWRT                                                        
*                                                                               
REF28    DS    0H                                                               
         LA    R5,L'SRCKWRD1(R5)                                                
         B     REF22                                                            
         POP   USING                                                            
*                                                                               
REF30    DS    0H                                                               
         B     EXITY                                                            
         SPACE 1                                                                
         POP   USING                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO GET OR CREATE DUMMY BEFORE RECORD                        *         
*                                                                     *         
* EXIT: IOREC=ACCMST BEFORE RECORD                                    *         
***********************************************************************         
         SPACE 1                                                                
GETBEF   NTR1  ,                                                                
*                                                                               
         LA    R2,IOREC                                                         
         USING ACTRECD,R2                                                       
         TM    RECTYPE,RECTDP      VALID TO SAVE NAME FOR ACC & EX & DP         
         JNZ   GETBEF02                                                         
         TM    RECTYPE,RECTEX                                                   
         JNZ   GETBEF02                                                         
         TM    RECTYPE,RECTES                                                   
         JNZ   GETBEF02                                                         
         TM    RECTYPE,RECTOR                                                   
         JNZ   GETBEF02                                                         
         TM    RECTYPE,RECTAC                                                   
         JZ    GETBEF04                                                         
GETBEF02 ICM   R3,15,PASAVNAM      TEST HAVE SAVED NAME                         
         USING NAMELD,R3                                                        
         BNZ   GETBEF10                                                         
GETBEF04 GOTO1 VDATAMGR,DMCB,GETREC,ACCMST,PADA,ACTRECD,WORK                    
         JNE   *+2                 GET RECORD                                   
         TM    RECTYPE,RECTDP                                                   
         JNZ   GETBEF08                                                         
         TM    RECTYPE,RECTEX                                                   
         JNZ   GETBEF08                                                         
         TM    RECTYPE,RECTES                                                   
         JNZ   GETBEF07                                                         
         TM    RECTYPE,RECTOR                                                   
         JNZ   GETBEF07                                                         
         TM    RECTYPE,RECTAC                                                   
         JZ    GETBEFX                                                          
         TM    ACCCMP,CPYSALTN                                                  
         JZ    GETBEF06                                                         
         GOTOR GETEL,DMCB,('XNMELQ',ACTRECD)                                    
         JNE   GETBEF08                                                         
         J     GETBEF09                                                         
GETBEF06 TM    ACCCMP,CPYSMEDN                                                  
         JZ    GETBEF08                                                         
         GOTOR GETEL,DMCB,('SNMELQ',ACTRECD)                                    
         JNE   GETBEF08                                                         
         J     GETBEF09                                                         
GETBEF07 GOTOR GETEL,DMCB,('ENMELQ',ACTRECD)                                    
         JE    GETBEF09                                                         
         LA    R3,DUMMYEL          SET TO DUMMY                                 
         J     GETBEF09                                                         
GETBEF08 GOTOR GETEL,DMCB,('NAMELQ',ACTRECD)                                    
GETBEF09 ST    R3,PASAVNAM                                                      
         J     GETBEFX                                                          
*                                                                               
GETBEF10 L     RE,PAREC            COPY KEY AND STATUS FOR DUMMY RECORD         
         MVC   ACTRECD(ACTRFST-ACTRECD),0(RE)                                   
         XR    RE,RE               COPY NAME ELEMENT                            
         ICM   RE,1,NAMLN                                                       
         JZ    GETBEF12                                                         
         MVC   ACTRFST(0),NAMELD                                                
         EX    RE,*-6                                                           
*                                                                               
GETBEF12 LA    RE,ACTRFST(RE)                                                   
         MVI   0(RE),0                                                          
         SR    RE,R1                                                            
         STCM  RE,3,ACTRLEN        SET RECORD LENGTH                            
*                                                                               
GETBEFX  B     EXIT                                                             
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO CREATE SEARCH ACCOUNT CODE POINTER RECORD                *         
*                                                                     *         
* EXIT:    CC=NOT EQUAL IF NOT SEARCHING ON ACCOUNT RECORD            *         
*       IOKEY=SAPRECD ACCDIR RECORD                                   *         
***********************************************************************         
         SPACE 1                                                                
CRTSAP   NTR1  ,                                                                
         TM    RECTYPE,RECTAC      TEST ACCOUNT RECORD                          
         BZ    EXITN                                                            
*                                                                               
         L     R3,PAREC            ADD SEARCH ACCOUNT CODE POINTER              
         USING ACTRECD,R3                                                       
         LA    R2,IOKEY                                                         
         USING SAPRECD,R2                                                       
         XC    SAPRECD(ACCKLEN),SAPRECD                                         
         MVI   SAPKTYP,SAPKTYPQ                                                 
         MVC   SAPKCUL,ACTKCPY                                                  
         MVI   SAPKSUB,SAPKSUBQ                                                 
         MVC   SAPKLVL,PLVL        ENSURE LEVEL NO. IS REASONABLE               
         MVC   SAPKACT,ACTKACT                                                  
         MVC   SAPKSTAT,ACCSTAT    COPY STATUS BYTE                             
         MVC   SAPKSEQ,RECSEQ                                                   
         L     RE,PADA                                                          
         MVC   SAPKDA,0(RE)                                                     
         TM    RECINDS,RECIPROD                                                 
         BZ    *+10                                                             
         MVC   SAPKSOFF,ACCOFF                                                  
         B     EXITY                                                            
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* UPDATE FLEXIBILL COMPANY RECORD                                     *         
***********************************************************************         
*&&UK                                                                           
         EJECT                                                                  
         PUSH  USING                                                            
UPDFXB   NTR1  ,                                                                
         L     R2,PAREC                                                         
         TM    RECTYPE,RECTWC      TEST WORK-CODE                               
         BZ    UFXB02                                                           
         IC    R0,WCOKCPY-WCOKEY(R2) R0 = COMPANY CODE                          
         LA    R4,FXBWCRVN-FXBELD  R4 = DISP. TO W/C REVISION NUMBER            
         B     UFXB10                                                           
*                                                                               
UFXB02   DS    0H                  TEST CLIENT ACCOUNT RECORD                   
         TM    RECINDS,RECIPROD                                                 
         BZ    EXIT                                                             
         CLI   PLVL,1                                                           
         BNE   EXIT                                                             
         IC    R0,ACTKCPY-ACTKEY(R2) R0 = COMPANY CODE                          
         LA    R4,FXBCLRVN-FXBELD  R4 = DISP. TO CLIENTS REVISION NO.           
*                                                                               
UFXB10   DS    0H                  GET FLEXIBILL COMPANY RECORD                 
K        USING FXBRECD,IOKEY                                                    
         XC    K.FXBKEY,K.FXBKEY                                                
         MVI   K.FXBKTYP,FXBKTYPQ                                               
         STC   R0,K.FXBKCPY                                                     
         MVI   K.FXBKSUB,FXBSUBQ                                                
         GOTO1 VDATAMGR,DMCB,DMREAD,ACCDIR,K.FXBKEY,K.FXBKEY,WORK               
         BNE   EXIT                                                             
         GOTO1 (RF),(R1),(X'80',GETREC),ACCMST,K.FXBKDA,IOREC,WORK              
         BNE   EXIT                                                             
         LA    R3,IOREC+FXBRFST-FXBRECD                                         
         USING FXBELD,R3                                                        
         XR    RF,RF                                                            
UFXB12   CLI   FXBEL,FXBELQ                                                     
         BNE   *+12                                                             
         CLI   FXBSUB,FXBSUBQ                                                   
         BE    UFXB20                                                           
         CLI   FXBEL,0                                                          
         BE    EXIT                                                             
         IC    RF,FXBLN                                                         
         BXH   R3,RF,UFXB12                                                     
*                                                                               
UFXB20   DS    0H                                                               
         LH    RE,FXBELD(R4)       INCREMENT REVISION NUMBER                    
         LA    RE,1(RE)                                                         
         STH   RE,FXBELD(R4)                                                    
         GOTO1 VDATAMGR,DMCB,PUTREC,ACCMST,K.FXBKDA,IOREC,WORK                  
         B     EXIT                                                             
         SPACE 1                                                                
         POP   USING                                                            
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* CALL VSRCHPASS                                                      *         
*                                                                     *         
* NTRY: R1 = VSRCHPASS PARAMETER LIST                                 *         
*       KEY OF ACCOUNT IS AMENDED TO INCLUDE ACTKXTRA                 *         
***********************************************************************         
         SPACE 1                                                                
         PUSH  USING                                                            
SRCHPASS NTR1  ,                                                                
         L     R2,8(R1)            R2 = A(ACCOUNT RECORD)                       
         USING ACTRECD,R2                                                       
         MVI   ACTKXHDR,ACTKXHDQ   PASS DATA USED BY ACSRCHDIR                  
         MVC   ACTKXDAT,ACCDAT                                                  
         GOTO1 VSRCHPAS                                                         
         MVC   ACTKXTRA,SPACES     ENSURE KEY RESTORED TO PROPER VALUE          
         XR    RE,RE                                                            
         S     RE,SBSEQNCE                                                      
         STCM  RE,7,RECSEQ                                                      
         B     EXIT                                                             
         POP   USING                                                            
         EJECT                                                                  
FF       EQU   X'FF'                                                            
ALL      EQU   X'00'                                                            
NOT      EQU   X'80'                                                            
         SPACE 1                                                                
         LTORG                                                                  
         SPACE 1                                                                
BZEROES  DC    XL4'00'                                                          
SPACES   DC    CL80' '                                                          
EFFS     DC    8X'FF'                                                           
         SPACE 1                                                                
PRODUL   DC    C'SJ'                                                            
ACCOUNT  DC    C'ACCOUNT  '                                                     
*TFAD    DC    C'DTFAD    '                                                     
ACCDIR   DC    C'ACCDIR   '                                                     
ACCMST   DC    C'ACCMST   '                                                     
ACCFIL   DC    C'ACCFIL   '                                                     
DMREAD   DC    C'DMREAD   '                                                     
DMRDHI   DC    C'DMRDHI   '                                                     
DMRSEQ   DC    C'DMRSEQ   '                                                     
DMADD    DC    C'DMADD    '                                                     
DMWRT    DC    C'DMWRT    '                                                     
GETREC   DC    C'GETREC   '                                                     
PUTREC   DC    C'PUTREC   '                                                     
OLDN     DC    C'OLDN     '                                                     
ACC      DC    C'ACC      '                                                     
         SPACE 1                                                                
DUMMYEL  DC    XL104'00'                                                        
         EJECT                                                                  
***********************************************************************         
* GET ELEMENT ROUTINE                                                 *         
*                                                                     *         
* NTRY: P1=(ELEMENT CODE, A(RECORD)                                   *         
* EXIT: R3=A(ELEMENT), RF=L(ELEMENT)                                  *         
*       CC=EQUAL IF FOUND                                             *         
***********************************************************************         
         SPACE 1                                                                
GETEL    XR    R3,R3                                                            
         ICM   R3,7,1(R1)                                                       
         LA    R3,ACTRFST-ACTRECD(R3)                                           
*                                                                               
GETEL02  CLI   0(R3),0                                                          
         JE    GETELNO                                                          
         CLC   0(1,R3),0(R1)                                                    
         JE    GETELYES                                                         
         LLC   RF,1(R3)                                                         
         AR    R3,RF                                                            
         J     GETEL02                                                          
*                                                                               
GETELYES CR    RE,RE                                                            
         BR    RE                                                               
*                                                                               
GETELNO  LTR   RE,RE                                                            
         BR    RE                                                               
         SPACE 1                                                                
***********************************************************************         
* INCREMENT WRITES TOTAL                                              *         
***********************************************************************         
         SPACE 1                                                                
INCWRT   LA    R1,SPARM                                                         
         ICM   RF,3,2(R1)                                                       
         LA    RF,1(RF)                                                         
         STCM  RF,3,2(R1)                                                       
         BR    RE                                                               
         SPACE 1                                                                
***********************************************************************         
* LOCAL W/S                                                           *         
***********************************************************************         
         SPACE 1                                                                
WORKD    DSECT                                                                  
PARMS    DS    0X                  * INPUT PARAMETERS *                         
PACTION  DS    XL1                 * ACTION *                                   
PACTADD  EQU   C'A'                RECORD ADDED                                 
PACTCHA  EQU   C'C'                RECORD CHANGED                               
PACTDEL  EQU   C'D'                RECORD DELETED                               
PACTRES  EQU   C'R'                RECORD RESTORED                              
PACTREF  EQU   C'F'                REFRESH SAPRECD STATUS BYTE                  
         DS    XL1                 N/D                                          
PRECFIL  DS    XL1                 RECORD IS ACCFIL                             
PRECFILQ EQU   C'F'                                                             
POFFLIN  DS    XL1                 RUNNING OFF-LINE                             
POFFLINQ EQU   C'F'                                                             
PAREC    DS    A                   A(ACCMST/ACCFIL RECORD)                      
PADA     DS    A                   A(DISK/ADDRESS) / 0 IF ACCFIL RECORD         
PASAVNAM DS    A                   A(SAVE NAME AREA)                            
PACOMFAC DS    A                   A(COMMON FACILITIES)                         
PLVL     DS    0XL1                LEVEL OF ACCOUNT RECORD                      
PAACCFAC DS    A                   A(ACCOUNT FACILITES)                         
PARMSL   EQU   *-PARMS                                                          
*                                                                               
VDATAMGR DS    A                   A(DATA MANAGER)                              
VCALLOV  DS    A                   A(CALLOV)                                    
VACCEMU  DS    A                   A(ACCOUNT EMULATOR)                          
VSRCHPAS DS    A                   A(SEARCH PASSIVE MAINT. ROUTINE)             
*                                                                               
RELO     DS    A                                                                
APARMS   DS    A                                                                
SPARM    DS    6A                  PARAMETERS FOR GESRCHPASS                    
DMCB     DS    6A                                                               
WORK     DS    XL100                                                            
*                                                                               
RECTYPE  DS    XL1                 RECORD TYPE                                  
RECTAC   EQU   X'80'               ACCOUNT CODE RECORD                          
RECTWC   EQU   X'40'               WORK CODE RECORD                             
RECTMC   EQU   X'20'               MEDIA CODE RECORD                            
RECTEX   EQU   X'10'               EXPENDITURE TYPE RECORD                      
RECTDP   EQU   X'08'               DEPARTMENT RECORD                            
RECTES   EQU   X'04'               ESTIMATE RECORD                              
RECTOR   EQU   X'02'               ORDER RECORD                                 
RECINDS  DS    XL1                                                              
RECIPROD EQU   X'80'               PRODUCTION ACCOUNT RECORD                    
*                                                                               
RECSEQ   DS    XL3                 SEARCH SEQUENCE NUMBER                       
*                                                                               
ACCSTAT  DS    XL1                 ACCOUNT RECORD STATUS BYTE                   
ACCDAT   DS    0XL7                ACCOUNT EXTRA DATA (SEE ACTKXDAT)            
ACCLVS   DS    0XL4                ACCOUNT LEVEL LENGTHS                        
ACCLV1   DS    XL1                 ACCOUNT LEVEL 1 LENGTH                       
ACCLV2   DS    XL1                 ACCOUNT LEVEL 2 LENGTH                       
ACCLV3   DS    XL1                 ACCOUNT LEVEL 3 LENGTH                       
ACCLV4   DS    XL1                 ACCOUNT LEVEL 4 LENGTH                       
ACCCMP   DS    XL1                 COMPANY STATUS BYTE C                        
ACCOFF   DS    CL2                 PRODUCTION OFFICE CODE                       
*                                                                               
SRCHIND1 DS    XL1                 INDICATOR BYTE 1 FOR GESRCHPASS              
SRCHIND2 DS    XL1                 INDICATOR BYTE 2 FOR GESRCHPASS              
         DS    0A                                                               
SRCHBLK  DS    XL512               SEARCH BLOCK                                 
SRCHLIST DS    XL((ACCKLEN*2*SBNAWXTQ)+1+8)                                     
         SPACE 1                                                                
IOKEY    DS    XL64                I/O KEY AREA                                 
IOKEYSAV DS    XL42                I/O KEY SAVE AREA                            
IOREC    DS    XL2048              I/O AREA FOR ACCMST RECORD                   
         DS    0D                                                               
WORKL    EQU   *-WORKD                                                          
         SPACE 1                                                                
EMUWORK  DS    0X                  ** FOR ACCFIL EMULATION **                   
EMUDIR   DS    XL(ACCKLEN)         ACCDIR RECORD                                
EMUMST   DS    XL2048              ACCMST RECORD                                
         DS    0D                                                               
EMUWORKL EQU   *-EMUWORK                                                        
         EJECT                                                                  
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* ACSRCHBLKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE ACSRCHBLKD                                                     
         PRINT ON                                                               
         SPACE 1                                                                
* GESRCHBLKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE GESRCHBLKD                                                     
         PRINT ON                                                               
         SPACE 1                                                                
* GESRCHDIRD                                                                    
         PRINT OFF                                                              
       ++INCLUDE GESRCHDIRD                                                     
         ORG   SDSRCHCD                                                         
SDSRUL   DS    CL2                 UNIT/LEDGER CODE                             
         PRINT ON                                                               
         SPACE 1                                                                
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DDACCFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDACCFACS                                                      
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'011ACSRCHPASS01/08/16'                                      
         END                                                                    
