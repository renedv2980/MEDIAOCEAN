*          DATA SET DDDATTIM   AT LEVEL 002 AS OF 11/03/93                      
*CATALP DATTIM                                                                  
         TITLE 'DATTIM - TIME IN SECONDS FROM BASE DATE 1992'                   
***********************************************************************         
* XL1 FUNCTION FLAGS                                                  *         
* AL3 A(4-BYTE BINARY DATETIME)                                       *         
*                                                                     *         
* XL1 RETURN CODE 0=OK ELSE ERROR                                     *         
* AL3 A(14-BYTE EBCDIC DATETIME YYYYMMDDHHMMSS)                       *         
*                                                                     *         
* IF PARAM1(1)=X'80' CONVERT P1 TO P2 (X'81') OR P2 TO P1 (X'82')     *         
* IF PARAM1(1)=X'01' BINARY DATE/TIME IN P1                           *         
* IF PARAM1(1)=X'02' EBCDIC DATE/TIME IN P2                           *         
***********************************************************************         
         SPACE 1                                                                
         PRINT NOGEN                                                            
DATTIM   CSECT                                                                  
         NMOD1 WRKX-WRKD,**DTTM**                                               
         USING WRKD,RC                                                          
         LR    R2,R1               R2=A(PARAM LIST)                             
         MVI   4(R2),0             SET OK RETURN CODE                           
         TM    0(R2),X'80'         TEST IF CALLER WANTS A CONVERSION            
         BO    DTTM2               YES THEN DONT NEED TO GET DATE TIME          
         EJECT                                                                  
***********************************************************************         
*GET DATE AND TIME FROM MVS AND CONVERT TO DDS DATE/TIME FORMAT       *         
***********************************************************************         
         SPACE 1                                                                
DTTM1    TIME  BIN                 R0=TIME(1/100 SECS),R1=DATE(JULIAN)          
         LTR   RF,RF                                                            
         BNZ   NOTIME                                                           
         SR    R4,R4                                                            
         LR    R5,R0                                                            
         D     R4,=F'100'          R5=TIME IN SECONDS                           
         ST    R5,SECS                                                          
*                                                                               
DTTM1B   CLM   R1,8,=X'01'         SET CENTURY                                  
         BNE   *+12                                                             
         ICM   R1,8,=X'20'                                                      
         B     *+8                                                              
         ICM   R1,8,=X'19'                                                      
*                                                                               
DTTM1C   XC    DUB,DUB             R1=P'YYYYDDD+'                               
         STCM  R1,3,DUB+6                                                       
         CVB   R4,DUB              R4=BINARY DAY IN YEAR NUMBER                 
         SRL   R1,12                                                            
         STCM  R1,7,DUB+5                                                       
         OI    DUB+7,X'0F'                                                      
         CVB   R1,DUB              R1=BINARY YEAR NUMBER                        
         SH    R1,STRYEAR                                                       
*                                                                               
DTTMID   SR    R0,R0                                                            
         D     R0,=F'4'                                                         
         MH    R1,DAYSTAB+8        R0=NUM OF DAYS IN MODULO 4 YEARS             
         LR    RF,R0                                                            
         SLL   RF,1                                                             
         AH    R1,DAYSTAB(RF)      PLUS NUM DAYS IN 0,1,2,OR 3 YEARS            
         AR    R1,R4               PLUS NUM DAYS IN THIS YEAR                   
         BCTR  R1,0                                                             
         M     R0,SECSDAY          TIMES NUM SECS IN A DAY                      
         AR    R1,R5               PLUS TIME OF DAY IN SECONDS                  
*                                                                               
DTTM1E   TM    0(R2),X'01'         TEST REQUESTED FORMAT                        
         BZ    DTTM1F                                                           
         SR    R3,R3               R3=A(BINARY DATETIME)                        
         ICM   R3,7,1(R2)                                                       
         BZ    BADCALL                                                          
         STCM  R1,15,0(R3)         RETURN DDS DATE/TIME IN P1                   
         B     DTTMX                                                            
*                                                                               
DTTM1F   TM    0(R2),X'02'         TEST IF CALLER WANTS EBCDIC                  
         BO    DTTM2A              YES                                          
         B     BADCALL                                                          
         EJECT                                                                  
***********************************************************************         
*CONVERT BINARY DATE/TIME IN P1 TO EBCDIC IN P2                       *         
***********************************************************************         
         SPACE 1                                                                
DTTM2    TM    0(R2),X'01'         TEST BINARY DATETIME IN P1                   
         BZ    DTDM3                                                            
         TM    0(R2),X'02'                                                      
         BO    BADCALL                                                          
         SR    R3,R3               R3=A(BINARY DATETIME)                        
         ICM   R3,7,1(R2)                                                       
         BZ    BADCALL                                                          
         ICM   R1,15,0(R3)         R1=BINARY DATE TIME                          
         BM    BADDATA                                                          
*                                                                               
DTTM2A   SR    R0,R0               R0/R1=SECONDS SINCE 1992                     
         D     R0,SECSDAY          DIVIDE BY NUM SECS IN A DAY                  
         ST    R0,SECS             R0=SECONDS                                   
         SR    R0,R0                                                            
         D     R0,=F'1461'         DIVIDE BY NUM OF DAYS IN 4 YRS               
         MH    R1,=H'4'            R1=RELATIVE YEAR NUMBER                      
*                                                                               
DTTM2B   C     R0,=F'365'          R0=DAY NUMBER 0 THRU 1460                    
         BH    *+12                                                             
         LA    RE,LEAPTAB          DAY IS IN LEAP YEAR                          
         B     DTTM2C                                                           
         SH    R0,=H'366'                                                       
         LA    RF,1(R1)            RF=RELATIVE YEAR NUMBER                      
         SRDL  R0,32                                                            
         D     R0,=F'365'          R0=DAY NUMBER 0 THRU 364                     
         AR    R1,RF               R1=RELATIVE YEAR NUMBER                      
         LA    RE,NORMTAB                                                       
*                                                                               
DTTM2C   AH    R0,=H'1'            R0=DAY NUM IN YEAR 1-365 (OR 366)            
         LA    RF,1                RF=MONTH NUMBER                              
DTTM2D   CH    R0,0(RE)                                                         
         BNH   DTTM2E                                                           
         SH    R0,0(RE)                                                         
         LA    RF,1(RF)                                                         
         LA    RE,2(RE)                                                         
         B     DTTM2D                                                           
*                                                                               
DTTM2E   AH    R1,STRYEAR          R1=ACTUAL YEAR                               
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  YEAR(4),DUB+5(3)                                                 
         CVD   RF,DUB              RF=ACTUAL MONTH                              
         OI    DUB+7,X'0F'                                                      
         UNPK  MONTH(2),DUB+6(2)                                                
         CVD   R0,DUB              R1=ACTUAL DAY                                
         OI    DUB+7,X'0F'                                                      
         UNPK  DAY(2),DUB+6(2)                                                  
*                                                                               
DTTM2F   L     R0,SECS             CONVERT SECONDS TO HHMMSS                    
         SRDL  R0,32                                                            
         D     R0,=F'60'                                                        
         CVD   R0,DUB              R0=SECOND                                    
         OI    DUB+7,X'0F'                                                      
         UNPK  SECOND(2),DUB+6(2)                                               
         SR    R0,R0                                                            
         D     R0,=F'60'           R0=MINUTE                                    
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  MINUTE(2),DUB+6(2)                                               
         CVD   R1,DUB              R1=HOUR                                      
         OI    DUB+7,X'0F'                                                      
         UNPK  HOUR(2),DUB+6(2)                                                 
         SPACE 1                                                                
DTTM2X   SR    R3,R3               RETURN YYYYMMDDHHMMSS IN P2                  
         ICM   R3,7,5(R2)                                                       
         BZ    BADCALL                                                          
         MVC   0(L'DATETIME,R3),DATETIME                                        
         B     DTTMX                                                            
         EJECT                                                                  
***********************************************************************         
*CONVERT EBCDIC DATE/TIME IN P2 TO BINARY IN P1                       *         
***********************************************************************         
         SPACE 1                                                                
DTDM3    TM    0(R2),X'02'         TEST EBCDIC DATE TIME IN P2                  
         BZ    BADCALL                                                          
         TM    0(R2),X'01'                                                      
         BO    BADCALL                                                          
         SR    R3,R3               R3=A(EBCDIC DATETIME)                        
         ICM   R3,7,5(R2)                                                       
         BZ    BADCALL                                                          
         MVC   DATETIME,0(R3)                                                   
*                                                                               
DTTM3A   MVC   DUB(8),ZEROS        TEST INPUT STRING NUMERIC                    
         MVZ   DUB(8),YEAR                                                      
         CLC   DUB(8),ZEROS                                                     
         BNE   BADDATA                                                          
         MVZ   DUB(6),HOUR                                                      
         CLC   DUB(6),ZEROS                                                     
         BNE   BADDATA                                                          
         CLC   YEAR,=C'1992'       VALIDATE DATE                                
         BL    BADDATA                                                          
         CLC   YEAR,=C'2060'                                                    
         BH    BADDATA                                                          
         CLC   MONTH,=C'01'                                                     
         BL    BADDATA                                                          
         CLC   MONTH,=C'12'                                                     
         BH    BADDATA                                                          
         CLC   DAY,=C'01'                                                       
         BL    BADDATA                                                          
         CLC   DAY,=C'31'                                                       
         BH    BADDATA                                                          
         CLC   HOUR,=C'00'         VALIDATE TIME                                
         BL    BADDATA                                                          
         CLC   HOUR,=C'23'                                                      
         BH    BADDATA                                                          
         CLC   MINUTE,=C'00'                                                    
         BL    BADDATA                                                          
         CLC   MINUTE,=C'59'                                                    
         BH    BADDATA                                                          
         CLC   SECOND,=C'00'                                                    
         BL    BADDATA                                                          
         CLC   SECOND,=C'59'                                                    
         BH    BADDATA                                                          
*                                                                               
DTTM3B   PACK  DUB,SECOND          CONVERT HHMMSS                               
         CVB   R1,DUB                                                           
         PACK  DUB,MINUTE                                                       
         CVB   R0,DUB                                                           
         MH    R0,=H'60'                                                        
         AR    R1,R0                                                            
         PACK  DUB,HOUR                                                         
         CVB   R0,DUB                                                           
         MH    R0,=H'3600'                                                      
         AR    R1,R0                                                            
         ST    R1,SECS                                                          
*                                                                               
DTTM3C   PACK  DUB,YEAR            CONVERT YEAR                                 
         CVB   R1,DUB                                                           
         SH    R1,STRYEAR                                                       
         BM    BADDATA                                                          
         SR    R0,R0                                                            
         D     R0,=F'4'                                                         
         MH    R1,DAYSTAB+8        R1=NUM OF DAYS IN MODULO 4 YEARS             
         LR    RF,R0                                                            
         SLL   RF,1                                                             
         AH    R1,DAYSTAB(RF)      PLUS NUM DAYS IN  0,1,2,OR 3 YEARS           
         LA    RE,NORMTAB                                                       
         LTR   R0,R0                                                            
         BNZ   DTTM3D                                                           
         LA    RE,LEAPTAB                                                       
*                                                                               
DTTM3D   PACK  DUB,MONTH           CONVERT MONTH                                
         CVB   RF,DUB                                                           
         SH    RF,=H'1'                                                         
         BZ    DTTM3F                                                           
DTTM3E   AH    R1,0(RE)            ADD NUMBER OF DAYS IN MONTH                  
         LA    RE,2(RE)                                                         
         BCT   RF,DTTM3E                                                        
*                                                                               
DTTM3F   PACK  DUB,DAY             CONVERT DAY                                  
         CVB   RF,DUB                                                           
         CH    RF,0(RE)            TEST WITH MAX DAY IN MONTH                   
         BH    BADDATA                                                          
         BCTR  RF,0                                                             
         AR    R1,RF               R1=TOTAL NUMBER OF DAYS                      
         M     R0,SECSDAY                                                       
         A     R1,SECS             R1=TOTAL NUMBER OF SECONDS                   
*                                                                               
DTTM3G   SR    R3,R3               R3=A(BINARY DATETIME)                        
         ICM   R3,7,1(R2)                                                       
         BZ    BADCALL                                                          
         STCM  R1,15,0(R3)         RETURM DDS DATE/TIME IN P1                   
         B     DTTMX                                                            
         EJECT                                                                  
***********************************************************************         
*ERROR AND NORMAL EXITS                                               *         
***********************************************************************         
         SPACE 1                                                                
NOTIME   MVI   4(R2),1             MVS CLOCK NOT WORKING                        
         B     DTTMX                                                            
BADCALL  MVI   4(R2),2             BAD PARAMETER LIST                           
         B     DTTMX                                                            
BADDATA  MVI   4(R2),3             BAD DATETIME DATA                            
         B     DTTMX                                                            
BADRANGE MVI   4(R2),4             DATE/TIME OUTSIDE RANGE                      
         B     DTTMX                                                            
         SPACE 1                                                                
DTTMX    CLI   4(R2),0             EXIT WITH CC EQL IF ALL OK                   
         XMOD1 1                                                                
         EJECT                                                                  
***********************************************************************         
*CONSTANTS AND LITERALS                                               *         
***********************************************************************         
         SPACE 1                                                                
SECSDAY  DC    F'86400'            NUMBER OF SECONDS IN A DAY                   
STRYEAR  DC    H'1992'             TIME STARTS AT THIS YEAR                     
ENDYEAR  DC    H'2060'             TIME ENDS AFTER THIS YEAR                    
ZEROS    DC    8C'0'                                                            
*                                                                               
NORMTAB  DC    AL2(31,28,31,30,31,30,31,31,30,31,30,31)                         
LEAPTAB  DC    AL2(31,29,31,30,31,30,31,31,30,31,30,31)                         
*                                                                               
DAYSTAB  DC    AL2(0000,0366,0731,1096,1461) DAYS IN 0,1,2,3,4 YEARS            
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*WORKING STORAGE AND DSECTS                                           *         
***********************************************************************         
         SPACE 1                                                                
WRKD     DSECT                     LOCAL WORKING STORAGE                        
DUB      DS    D                                                                
FULL     DS    F                                                                
SECS     DS    F                   TIME IN SECONDS                              
*                                                                               
DATETIME DS    0CL14               EBCDIC DATE/TIME                             
YEAR     DS    CL4                                                              
MONTH    DS    CL2                                                              
DAY      DS    CL2                                                              
HOUR     DS    CL2                                                              
MINUTE   DS    CL2                                                              
SECOND   DS    CL2                                                              
*                                                                               
WRKX     EQU   *                                                                
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002DDDATTIM  11/03/93'                                      
         END                                                                    
