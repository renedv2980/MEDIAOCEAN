*          DATA SET PPFILCON   AT LEVEL 210 AS OF 10/30/18                      
*PROCESS USING(WARN(15))                                                        
*CATALP PPFILCON                                                                
*                                                                               
         TITLE 'PPG MAIN FILE CONTROLLER - CHANGE LOG'                          
*  CHANGE LOG                                                                   
*                                                                               
*  SMUR SPEC-17729 NEW MEDIA D FOR DIGITAL MEDIA (18.3)                         
*                                                                               
*  BPLA 07/15     CHANGES FOR MEDIA B,V,W                                       
*                                                                               
*  BPLA 06/14     FIX FLAW IN ONE MEDIA/ALL AGENCIES READING                    
*                                                                               
*  BPLA 02/14     CHANGES FOR MEDIA L - SOCIAL                                  
*                                                                               
*  BPLA 11/13     USE INDICATOR FROM MASTC TO SET RCALLAGY                      
*                 INSTEAD OF CHECKING QAGENCY                                   
*                                                                               
*  BPLA 03/09     SORT CHANGE FOR BILLING REQUESTS                              
*                                                                               
*  BPLA 08/06     BUG FIX                                                       
*                                                                               
*  BOBY 11/05     2 CH MEDIA OFFICE CODES                                       
*                                                                               
*  SMYE 08/05     USE CORE-RESIDENT OFFICER                                     
*                                                                               
*  BPLA 8/01      CHANGES FOR PRODUCT GROUPS                                    
*                                                                               
*  SMYE 4/00      MODIFICATIONS FOR NEW PPNEWFILE                               
*                 FOR LARGER RECORDS, INCLUDING 4000-BYTE CONTRACT              
*                                                                               
*  SMYE 3/99      CHECK FOR PGADFAX GREATER THAN SPACES BEFORE                  
*                 MOVING PGADLIN3 AND PGADFAX INTO REP AREAS                    
*                                                                               
*  BPLA 9/98      CHANGES FOR MEDIA I - INTERACTIVE                             
*                 LOOK FOR ***INT TO FIND CHANGES                               
*                                                                               
*  SMYE 6/98      MORE MOD'S TO CONTRACT SORT IN SCN12.. FOR PUBLISHER          
*                 REQUESTS - IF QPUB+1 IS P=ALL, SORT BY PUBLISHER NAME         
*                                                                               
*  BPLA  6/98     NUMERIC YEARS IN QSTART AND QEND                              
*                 CHANGED TO Y2K FORMAT                                         
*                                                                               
*  SMYE 5/98      MODIFIED CONTRACT SORT FOR PUBLISHER REQUESTS                 
*                   (P=.... IN QPUB+1) IN SCN12..                               
*                                                                               
*  SMYE 5/97      USE CALL TO PPGETADR TO GET ADDRESSES (IN READREP..)          
*                                                                               
*  SMYE 10/96     MODIFIED SORTS FOR 2-CHARACTER OFFICE LISTS AND               
*                 FIXED BUG IN SCOUTPUT (WAS NOT DOING LBUYREP ON               
*                 CHANGE OF OFFICE (HIGH) OR CLT (HIGH))                        
*                                                                               
*  SMYE 4/96      ADDED PUBLISHER MODE TO NXTPUB                                
*                                                                               
*  BPLA 3/96      CHECK LENGTH OF PUBAOVEL BEFORE MOVING                        
*                 PUBAOLN3 AND PUBAOFAX INT REP AREAS                           
*                                                                               
*  BPLA 1/96      CHANGE CALLS TO DTCNV TO DATCON                               
*                                                                               
*  BPLA 3/9/95    BEFORE GOING TO DTCNV (IN SETDATES) SEE IF I HAVE A           
*                 DAY IN QSTART, IF NOT SET DAY TO 01                           
*                 THIS IS NEEDED TO PREVENT DUMPS IN DTCNV FOR                  
*                 RFP BILLING REQUESTS (THEY MAY HAVE NO DAY)                   
*                                                                               
*  BPLA 11/22/94  OLD AOR (DUPONT STYLE) DISABLED                               
*                                                                               
*  BPLA 1/24/94   CHANGE ANYCONT - IF AOR SITUATION                             
*                 USE RCONSADV+2 IN STEAD OF RCONSVCL                           
*                 WHEN MATCHING CLIENTS                                         
*                                                                               
*  BPLA 11/17/93  NEW SORT FOR CONTRACTS - DIV/REG/DST ORDER                    
*                                                                               
*  BPLA 11/1/93   CLEAR PRECREC(200) WAS 159                                    
*                 MOVE PUBAOLN3 AND PUBAOFAX INTO PREPLIN3 AND                  
*                 PREPFAX (IN READREP)                                          
*  BPLA 6/4/93    DON'T TRY TO PACK QESTEND FOR REPORTS AC AND AU               
*                 IT MAY CONTAIN THE AGENCY FILTER                              
*  BPLA 10/22/92  CHANGES TO READ THE AOR CONTRACT IN READCONT                  
*                 FOR THOSE AOR SITUATIONS WHERE THE AGENCIES                   
*                 DON'T HAVE THEIR OWN CONTRACTS.                               
*                                                                               
*  BPLA 9/22/92  NEW VALUE FOR FCGTTREP - "S" = SHIPPING ADDR                   
*                                                                               
*  BPLA 8/31/92  FIX BUG IN FLTBILL - PRODUCT WAS NOT IN KEY                    
*                IN REQUEST WAS FOR A DIV OR ALL DIVS                           
*  BPLA 6/9/92   FIX CHECKS USING FCPDFILT AND FCBLFILT TO ALLOW                
*                FOR "FREE" INSERTIONS WITH BILL AND/OR PAY ELEMENTS            
*                                                                               
*  BPLA 7/17/91  CODE FOR OFFICE LIST REQUESTS  - USING SORTBYCN                
*                ADD OFFICE TO SORTS IN SORTBYDS AND SORTBYCN                   
*                WHEN REQUEST IS FOR AN OFFICE LIST                             
*                                                                               
*  BPLA 3/26/91  ADD RD (DRAFT REBATE) TO CHECKS FOR BILLING PROGRAMS           
*                                                                               
*  BPLA 11/16/90 FIXED BUG IN READCONT - IF LOOKING FOR SAME PUB                
*                CHECKED LAST CONTRACT READ (USING ANYCONT)                     
*                IF IT DIDN'T COVER THE BUY I ANYCONT WAS BUMPING               
*                THE CONTRACT NUMBER WHICH I SHOULDN'T HAVE DONE                
*                NOW I GO BACK AND CHECK THE FIRST CONTRACT                     
*                FOR THE PUB.                                                   
*                                                                               
*  BPLA 7/10/90  LOGIC FOR 2 REQUEST CARDS                                      
*                                                                               
*  BPLA 8/6/90  LOGIC FOR $* (ALL OFFICE REQUESTS)                              
*                                                                               
         TITLE 'PPG MAIN FILE CONTROLLER'                                       
PPFILCON CSECT                                                                  
         ENTRY RDSPLIT                                                          
         ENTRY NXTPUB                                                           
         ENTRY ENDREQ                                                           
         EXTRN FELIST                                                           
         EXTRN FELISTX                                                          
         PRINT NOGEN                                                            
         NMOD1 0,PPFILCON,R9,R8,R7,R6                                           
         L     RA,=V(PPWORKC)                                                   
         USING PPWORKD,RA                                                       
*** NEW                                                                         
         L     RC,PPFILEC                                                       
*NOP*    L     RC,=V(PPFILCC)                                                   
*** NEW                                                                         
         LA    R5,2048(RC)                                                      
         LA    R5,2048(R5)                                                      
         LA    RE,4095(R5)                                                      
         LA    RE,1(RE)                                                         
*NOP*    USING PPFILED,RC,R5                                                    
         USING PPFILED,RC,R5,RE                                                 
         LA    R1,PCONREC                                                       
         L     RF,PPWORK2C                                                      
         USING PPWORK2D,RF                                                      
         ST    R1,ACONIO           STORE CONREC ADDRESS                         
         DROP  RE,RF                                                            
         EJECT                                                                  
*                   INITIALISATION, REQUEST READ AND FINALISATION               
         SPACE 3                                                                
         L     R1,=A(BASERD)       SAVE BASE RD FOR ENDREQ                      
         ST    RD,0(R1)                                                         
         L     R1,=V(MASTC)                                                     
         USING MASTD,R1                                                         
         MVC   SORTLOC,MCAPHAS3                                                 
         LA    RE,FINAL                                                         
         STM   0,RF,MCRUNLST                                                    
         LA    RE,LASTREQ                                                       
         STM   R0,RF,MCREQLST                                                   
** NEW 7/10/90                                                                  
         LA    RF,QCONTREQ                                                      
         LA    RE,QRECORD                                                       
         SR    RF,RE                                                            
         LA    RF,1(RF)                                                         
         STC   RF,MCRQCOL           SET CONT REQ COLUMN                         
         MVI   MCRQCHAR,C'*'        SET CONT REQ CHARACTER                      
*&&OS                                                                           
         L     RF,=V(SORTLOC)      AREA FOR SORTER                              
         ST    RF,SORTLOC                                                       
*&&                                                                             
         LA    RF,NEXTREQ                                                       
         ST    RF,ANXTREQ                                                       
*                                                                               
         L     RE,=A(FELIST)       POINT TO START OF LIST                       
         L     RF,=A(FELISTX)                                                   
         SR    RF,RE               GET LENGTH                                   
         XCEF                                                                   
*                                                                               
         L     R0,=A(FELIST)                                                    
         L     R1,=A(FELISTX)                                                   
         SR    R1,R0               GET LENGTH OF FELIST                         
         SR    R0,R0               CLEAR FOR DIVISION                           
         D     R0,=A(FELISTL)                                                   
         L     RE,=A(FEMAX)                                                     
         ST    R1,0(RE)            SET MISSING PARAM                            
*                                                                               
         MVI   MODE,RUNFRST                                                     
         BAS   RE,GO                                                            
         SPACE 2                                                                
READREQ  CP    RCRQTOT,RCENNUM                                                  
         BNL   EOF                                                              
         SPACE 2                                                                
READCRD  GOTO1 =V(REQSTART)                                                     
         L     R1,=V(MASTC)                                                     
         USING MASTD,R1                                                         
         MVC   QPROG(80),MCREQREC                                               
**NEW 7/10/90                                                                   
         L     RF,PPWORK2C                                                      
         USING PPWORK2D,RF                                                      
         MVC   QAREA2,SPACES       CLEAR 2ND REQUEST CARD                       
*                                                                               
         CLC   QPROG(2),=C'/*'                                                  
         BE    FINAL                                                            
**NEW 7/10/90                                                                   
         CLI   MCRQNUM,2                                                        
         BL    READC5                                                           
         MVC   QAREA2,MCIO+L'MCREQREC   MOVE SECOND CARD TO QAREA2              
*                                                                               
*        SET CURTAB FOR CUREDIT                                                 
*                                                                               
READC5   DS    0H                                                               
*                                                                               
         MVI   MCLANG,0          SET DEFAULTS                                   
         MVI   MCCTRY,0                                                         
         MVI   RCLANG,0                                                         
         MVI   RCCTRY,0                                                         
         MVC   CURTAB,=XL8'00000000215B4040'                                    
*                                                                               
         XC    SVOFCSTR,SVOFCSTR   INIT OFFICE SAVEAREAS                        
         XC    SVOFCEND,SVOFCEND                                                
*                                                                               
         CLI   QLANG,C'F'            FRENCH REPORT                              
         BNE   READC10                                                          
         MVI   RCCTRY,8              CANADA                                     
         MVI   RCLANG,4              FRENCH                                     
         MVI   CURTAB+4,X'11'                                                   
*                                                                               
         MVI   MCCTRY,8              ALSO SET IN MASTC                          
         MVI   MCLANG,4                                                         
*                                                                               
READC10  DS    0H                                                               
*                                                                               
         DROP  RF                                                               
         DROP  R1                                                               
*                                                                               
*  ATTEMPT TO FIX NUMERIC YEARS IN QSTART AND QEND INTO Y2K FORMAT              
*                                                                               
         LA    R1,QSTART                                                        
         BAS   RE,QY2K                                                          
         LA    R1,QEND                                                          
         BAS   RE,QY2K                                                          
*                                                                               
READCX   B     REQCON                                                           
*                                                                               
QY2K     NTR1                      IF YEAR IS NUMERIC, --> Y2K FMT              
         LA    R4,2                                                             
         LR    R5,R1                                                            
*                                                                               
QY2KA    CLI   0(R5),C'0'                                                       
         BL    QY2KX                                                            
         CLI   0(R5),C'9'                                                       
         BH    QY2KX                                                            
         LA    R5,1(R5)                                                         
         BCT   R4,QY2KA                                                         
*                                                                               
         LR    R5,R1                                                            
         MVC   WORK(2),0(R5)                                                    
         MVC   WORK+2(2),=C'01'                                                 
         MVC   WORK+4(2),=C'01'                                                 
         GOTO1 DATCON,DMCB,WORK,WORK+6                                          
         MVC   0(2,R5),WORK+6      CHANGE YEAR BYTES                            
QY2KX    XIT1                                                                   
*                                                                               
         SPACE 3                                                                
EOF      DS    0H                                                               
         SPACE 2                                                                
FINAL    MVI   MODE,RUNLAST                                                     
         BAS   RE,GO                                                            
         GOTO1 =V(DMSHMUSS),DMCB,(0,=CL8'DETACH'),(0,=CL8'PRTQ')                
*                                                                               
         SPACE 2                                                                
EXIT     DS    0H                                                               
FCEXT    DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
*                   FILTERING AND EDITING REQUESTS                              
         SPACE 3                                                                
REQCON   AP    RCRQTOT,=P'1'                                                    
*&&DO*&& GETIME TU                                                              
*&&DO*&& ST    R1,REQSTART                                                      
         CP    RCRQTOT,RCSTNUM     CHECK WE HAVE REACHED START OPTION           
         BL    READREQ                                                          
         CLI   RCRQONLY,C'Y'                                                    
         BE    RC4                                                              
         GOTO1 VREQREP,DMCB                                                     
         OC    DMCB(4),DMCB                                                     
         BZ    RC4                                                              
         AP    RCRQERR,=P'1'                                                    
         B     READREQ                                                          
         SPACE 2                                                                
RC4      XC    RQBILLDT,RQBILLDT   CLEAR BILLING DATE FILTER                    
*                                                                               
         L     RE,=A(FLTBREQ)                                                   
         MVI   0(RE),C'N'          RESET FLAG                                   
         CLC   QCNTLDT,SPACES                                                   
         BH    *+8                                                              
         MVI   0(RE),C'Y'          SET FLAG                                     
*                                                                               
         AP    RCRQVAL,=P'1'                                                    
         MVI   SORTSW,C'N'                                                      
*                                                                               
         MVI   RCALLAGY,C'N'                                                    
         L     R1,=V(MASTC)                                                     
         USING MASTD,R1                                                         
         TM    MCFLAGS2,MCCROSSF                                                
         BNO   RC4A                                                             
         DROP  R1                                                               
*                                                                               
         MVI   RCALLAGY,C'Y'                                                    
         MVC   QAGENCY,SPACES                                                   
*                                                                               
RC4A     DS    0H                                                               
*                                 RCALLALL SET TO 'Y' IF REQUEST                
*                                 IS FOR ALL ESTS WITH 'ES'                     
         MVI   RCALLALL,C'N'                                                    
RC4A1    CLC   =C'ALL',QEST                                                     
         BNE   RC4A3                                                            
RC4A2    CLC   =C'ES',QSTART                                                    
         BNE   RC4A3                                                            
         MVI   RCALLALL,C'Y'                                                    
*                                                                               
RC4A3    MVI   RCMULTIQ,C'N'                                                    
         CLI   QMEDIA,C'*'         MULTI-MEDIA REQUEST                          
         BNE   RC4A5                                                            
         MVI   RCMULTIQ,C'Y'                                                    
***INT                                                                          
         MVI   QMEDIA,C'B'         START WITH MOBILE                            
***INT                             WAS INTYERNET -I                             
         B     RC4B                                                             
*                                                                               
RC4A5    CLI   QMEDIA,C'C'         COMBINED MEDIA                               
         BNE   RC4B                                                             
         MVI   RCMULTIQ,C'C'                                                    
***INT                                                                          
         MVI   QMEDIA,C'B'         START WITH MOBILE                            
***INT                             WAS INTERNET - I                             
*                                                                               
*                                  CHECK HAVE RIGHT AGYHDR                      
RC4B     DS    0H                                                               
         MVC   SVPUBRQ,QPUB+1      SAVE TO TEST FOR PUBLISHER REQUEST           
*                                    (A SORT MAY "REPLACE" QPUB)                
         CLC   PAGYKAGY(3),QAGENCY                                              
         BE    RC4D                                                             
         XC    KEY,KEY                                                          
         MVC   KEY(3),QAGENCY                                                   
         MVI   KEY+3,X'01'                                                      
         GOTO1 HIGH                                                             
         CLC   KEY(25),KEYSAVE                                                  
         BE    RC4B2                                                            
         CLI   QMEDIA,C' '         IF MEDIA BLANK (=ALL)                        
         BNE   NEXTREQ                                                          
         CLC   KEY(2),KEYSAVE      NEED ONLY RIGHT AGY                          
         BNE   NEXTREQ                                                          
RC4B2    DS    0H                                                               
         GOTO1 GETAGY                                                           
         CLC   ORIGNAME(66),SPACES                                              
         BE    *+10                                                             
         MVC   PAGYNAME(66),ORIGNAME    NAME AND ADDR                           
         MVC   RCSVAGY(3),KEY                                                   
         SPACE 2                                                                
RC4C     MVC   PROFKEY,=CL12'P000'                                              
         MVC   PROFKEY+4(3),QAGENCY                                             
         CLI   QMEDIA,C'C'             COMBINED MEDIA FOR PPL1 ONLY             
         BNE   RC4C5                                                            
         MVI   PROFKEY+6,C'M'          USE MAG PROFILE                          
RC4C5    GOTO1 GETPROF,DMCB,PROFKEY,SYSPROF,DATAMGR                             
         MVC   PROFKEY+2(2),QPROG                                               
         GOTO1 (RF),(R1),,PROGPROF                                              
*                                                                               
RC4D     DS    0H                                                               
         EJECT                                                                  
*                   READ ONE/FIRST/NEXT CLIENT                                  
         SPACE 3                                                                
         MVC   RCSVMED,QMEDIA                                                   
         CLI   RCRQONLY,C'Y'                                                    
         BE    RC5                 READ REQUESTS ONLY                           
         CLI   FCRDCLI,C'N'        CHECK ALL READ SPECS 'N'                     
         BNE   RC6                                                              
         CLC   FCRDCLI+1(27),FCRDCLI                                            
         BNE   RC6                                                              
RC5      EQU   *                                                                
         MVI   MODE,PROCREQ                                                     
         BAS   RE,GO                                                            
         B     NEXTREQ                                                          
RC6      EQU   *                                                                
         MVI   FCOAPRD,C'N'        SUPPRESS OTHER AGY PRODS                     
         CLI   QPRODUCT,C'*'       UNLESS SPECIFICALLY EQ'D                     
         BNE   *+8                 (MAY BE OVERRIDDEN BY USER)                  
         MVI   FCOAPRD,C'Y'                                                     
         MVI   MODE,FBUYREQ                                                     
         BAS   RE,GO                                                            
*                                                                               
         BAS   RE,SETDATES                                                      
         XC    BQEST(4),BQEST      SET BQEST + BQESTEND                         
         CLC   QEST,=C'ALL'                                                     
         BE    RC6B                                                             
         CLI   QEST,C' '                                                        
         BE    RC6B                                                             
         PACK  DUB,QEST                                                         
         CVB   R0,DUB                                                           
         STH   R0,BQEST                                                         
*                                                                               
         CLC   QPROG,=C'AC'   FOR AC AND AU QESTEND HAS AGENCY FILTER           
         BE    RC6B                                                             
         CLC   QPROG,=C'AU'                                                     
         BE    RC6B                                                             
*                                                                               
         CLI   QESTEND,C' '                                                     
         BE    RC6B                                                             
         PACK  DUB,QESTEND                                                      
         CVB   R0,DUB                                                           
         STH   R0,BQESTEND                                                      
RC6B     DS    0H                                                               
         L     RF,=A(FECOUNT)                                                   
         XC    0(4,RF),0(RF)                                                    
         XC    PUBREC(25),PUBREC   CLEAR PUB KEY TO ENSURE FRESH                
*                                  READING OF PUB RECORD                        
         XC    RCONPUB,RCONPUB     RESET FOR READCONT                           
         MVC   RCSVCLI(09),SPACES                                               
         XC    RCSVEST,RCSVEST                                                  
         LA    RE,LASTREQ          CHECK IF SORT IS NEEDED                      
RC7A     DS    0H                                                               
         MVI   SAVEQSRT,0                                                       
         CLC   QSORT,=C'NO'                                                     
         BNE   *+14                                                             
         MVC   QSORT,SPACES                                                     
         B     ONECLI                                                           
*                                  TEST FOR CONTRACT SORT                       
         CLC   QPROG,=C'AC'        ADV CONTRACT - TREAT LIKE 12                 
         BE    RC7A1                                                            
         CLC   QPROG,=C'AR'  AOR CONTRACT LIST/ANAL/RATE CHG                    
         BE    RC7A1           FOR THE BUYING AGENCIES                          
         CLC   QPROG,=C'AU'        ADV CONTRACT UTIL - TREAT LIKE 12            
         BE    RC7A1                                                            
         CLC   QPROG,=C'12'        FOR 12 THRU 19                               
         BL    RC7A2                                                            
         CLC   QPROG,=C'19'                                                     
         BH    RC7A2                                                            
*                                                                               
RC7A1    CLI   QSORT,C' '          IF NO SORT                                   
         BE    NEXTREQ                                                          
         CLI   QPUB,C'0'           OR NOT ALL PUBS                              
         BNL   NEXTREQ             APPLIC PROGRAM HAS ALREADY DONE              
         B     SORTCN                                                           
RC7A2    DS    0H                                                               
         CLC   QSORT,SPACES                                                     
         BE    RC7C                                                             
         CLC   QSORT,=C'10'        SPECIAL USER SORT                            
         BNL   SORT10                                                           
         CLC   QPROG,=C'48'                                                     
         BE    SORT48                                                           
         CLC   QPROG,=C'46'        PP46 CAN NOW ALSO SORT                       
         BE    SORT48                                                           
         CLC   QPROG,=C'81'                                                     
         BE    SORT48                                                           
         CLC   QPROG,=C'NV'        TREAT NV LIKE PP27                           
         BE    RC7A5                                                            
         CLC   QPROG,=C'27'        PROGS 27-37 IN 'VENDOR SORT GROUP'           
         BL    SORT1A                                                           
         BNE   RC7B                                                             
*                            FOR 27, IF PRD NOT BLANK, DO 'NORMAL' SORT         
RC7A5    CLI   QPRODUCT,C' '                                                    
         BE    RC7B                                                             
         CLC   QSORT,=C'06'        ALPHA                                        
         BNE   *+10                                                             
         MVC   QSORT,=C'05'                                                     
         B     SORT1A                                                           
*                                                                               
RC7B     DS    0H                                                               
         CLC   QPROG,=C'37'        ALSO FOR PP37                                
         BH    SORT1A                                                           
         B     SORT2                                                            
*                                                                               
RC7C     DS    0H                                                               
         CLI   FCRDBUY,C'N'                                                     
         BE    ONECLI                                                           
         CLC   QREGION,=C'ALL'                                                  
         BE    SORT1                                                            
         CLC   QDIST,=C'ALL'                                                    
         BE    SORT1                                                            
         CLC   QEST,=C'ALL'                                                     
         BE    SORT1                                                            
         CLC   QPROG,=C'B1'        TEST BILLING REQUEST                         
         BE    RC7D                YES - SORT                                   
         CLC   QPROG,=C'D1'        TEST DRAFT BILLING REQUEST                   
         BE    RC7D                YES - SORT                                   
         CLC   QPROG,=C'R1'        TEST REBATE BILLING REQUEST                  
         BE    RC7D                YES - SORT                                   
         CLC   QPROG,=C'RD'        TEST DRAFT REBATE BILLING REQUEST            
         BE    RC7D                YES - SORT                                   
         B     ONECLI                                                           
*                                                                               
RC7D     CLC   QESTEND,SPACES      SEE IF EST RANGE OR FILTERED                 
         BNE   SORT1               MUST SORT FOR BILLING REQS                   
*                                                                               
         SPACE 2                                                                
ONECLI   EQU   *                                                                
         CLI   FCRDPUB,C'Y'                                                     
         BE    PUBLIST                                                          
         CLI   FCRDCLI,C'Y'                                                     
         BE    CLILIST                                                          
         XC    RCSVCLI,RCSVCLI     SET FIRST TIME IND                           
         CLC   QCLIENT(3),=C'ALL'                                               
         BE    NEXTCLI                                                          
         CLI   QCLIENT,C'*'                                                     
         BE    NEXTCLI                                                          
         CLI   QCLIENT,C'$'        TEST OFFICE LIST REQUEST                     
         BE    NEXTCLI                                                          
         CLI   QCLIENT,C'&&'       CLIENT GROUP REQUEST                         
         BE    NEXTCLI                                                          
* PROCESS ONE CLIENT REQUEST *                                                  
         XC    KEY,KEY                                                          
         MVC   KEY(3),RCSVAGY                                                   
         MVI   KEY+3,X'02'                                                      
         MVC   KEY+4(3),QCLIENT         ONE CLIENT - MUST BE THERE              
         GOTO1 HIGH                                                             
         CLC   KEY(25),KEYSAVE                                                  
         BNE   NEXTREQ                                                          
         GOTO1 GETCLI                                                           
         MVC   RCSVCLI,KEY+4                                                    
         B     CLI30                                                            
         SPACE 2                                                                
NEXTCLI  CLI   QCLIENT,C'$'        TEST OFFICE LIST REQUEST                     
         BE    CLI10                                                            
         XC    KEY,KEY                                                          
         MVC   KEY(7),RCSVAGY                                                   
         MVI   KEY+3,X'02'                                                      
         MVI   KEY+7,1                                                          
         GOTO1 HIGH                                                             
         CLC   KEYSAVE(4),KEY                                                   
         BNE   LASTREQ                                                          
         MVC   RCSVCLI(3),KEY+4                                                 
         GOTO1 GETCLI                                                           
         CLI   QCLIENT,C'*'        OFFICE REQUEST                               
         BE    CLI5                                                             
*                                                                               
         CLI   QCLIENT,C'&&'       CLIENT GROUP REQUEST                         
         BNE   CLI30                                                            
CLI5     BAS   RE,TSTCLI                                                        
         BNE   NEXTCLI                                                          
         B     CLI30                                                            
         SPACE 2                                                                
* PROCESS CLIENT LIST *                                                         
         SPACE 1                                                                
CLI10    OC    RCSVCLI,RCSVCLI     TEST FIRST TIME                              
         BNE   CLI12                                                            
         GOTO1 =A(BLDOFC),DMCB,(RC)                                             
         L     R4,=V(OFCLIST)                                                   
         OC    0(6,R4),0(R4)        NO CLIENTS FOUND                            
         BZ    LASTREQ                                                          
         B     CLI20                                                            
*                                                                               
CLI12    L     R4,=V(OFCLIST)                                                   
*                                                                               
CLI14    CLC   RCSVCLI,3(R4)       MATCH PREVIOUS CLIENT                        
         BE    CLI16                                                            
         LA    R4,6(R4)            NEXT CLIENT                                  
         OC    0(3,R4),0(R4)                                                    
         BNZ   CLI14                                                            
         DC    H'0'                CAN'T FIND PREVIOUS IN LIST                  
*                                                                               
CLI16    CLC   0(2,R4),6(R4)       TEST NEXT IS SAME OFFICE                     
         BNE   CLI18                                                            
         LA    R4,6(R4)            POINT TO NEXT ENTRY                          
         B     CLI22                                                            
*                                                                               
CLI18    MVI   MODE,OFCLAST                                                     
         BAS   RE,GO                                                            
*                                                                               
         CLI   6(R4),0             TEST NEXT IS EOL                             
         BE    LASTREQ                                                          
         LA    R4,6(R4)            BUMP TO NEXT                                 
*                                                                               
CLI20    MVC   RCSVOFC,2(R4)       SAVE NEW OFFICE CODE                         
         MVI   MODE,OFCFRST                                                     
         BAS   RE,GO                                                            
*                                                                               
CLI22    XC    KEY,KEY                                                          
         MVC   KEY(3),RCSVAGY                                                   
         MVI   KEY+3,X'02'                                                      
         MVC   KEY+4(3),3(R4)      MOVE CLIENT TO KEY                           
         GOTO1 HIGH                                                             
         CLC   KEY(25),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 GETCLI                                                           
         MVC   RCSVCLI,KEY+4                                                    
*                                                                               
CLI30    DS    0H                                                               
         MVC   PROFKEY,=CL12'P000'                                              
         MVC   PROFKEY+4(3),QAGENCY                                             
         MVC   PROFKEY+7(3),RCSVCLI                                             
         CLI   PCLTOFF,C' '                                                     
         BNH   *+14                                                             
         MVI   PROFKEY+10,C'*'                                                  
         MVC   PROFKEY+11(1),PCLTOFF                                            
*                                                                               
         GOTO1 GETPROF,DMCB,PROFKEY,SYSPROF,DATAMGR                             
*                                                                               
         MVC   PROFKEY+2(2),QPROG                                               
*                                                                               
         GOTO1 (RF),(R1),,PROGPROF                                              
         L     RF,=A(FECOUNT)                                                   
         XC    0(4,RF),0(RF)   MUST REBUILD FELIST FOR EACH CLT                 
         MVI   MODE,FBUYCLI                                                     
         BAS   RE,GO                                                            
         SPACE 2                                                                
CLI35    DS    0H                                                               
         CLI   MODE,LBUYCLI                                                     
         BE    DIV3                USER ASKS TO SKIP THIS CLIENT                
         CLC   RCSTCLI,SPACES      CHECK WE HAVE REACHED CLIENT START           
         BE    CLI40                                        OPTION              
         CLC   RCSTCLI,KEY+4                                                    
         BNE   NEXTCLI                                                          
         MVC   RCSTCLI,SPACES                                                   
         SPACE 1                                                                
CLI40    DS    0H                                                               
         EJECT                                                                  
* CONTROL DIVISIONS *                                                           
         SPACE 1                                                                
FIRSTDIV DS    0H                                                               
         CLI   FCRDDIST,C'Y'                                                    
         BE    DISTLIST                                                         
         CLI   FCRDLIST,C'Y'                                                    
         BE    LISTLIST                                                         
         MVC   RCSVDIV,QDIV                                                     
         CLC   QDIV,SPACES                                                      
         BE    FIRSTPRD                                                         
         CLC   QDIV,=C'ALL'                                                     
         BE    NEXTDIV                                                          
         XC    KEY,KEY                                                          
         MVC   KEY(7),RCSVAGY                                                   
         MVI   KEY+3,X'03'                                                      
         MVC   KEY+7(3),QDIV                                                    
         GOTO1 HIGH                                                             
         GOTO1 GETDIV                                                           
         MVI   MODE,FBUYDIV                                                     
         BAS   RE,GO                                                            
         B     FIRSTPRD                                                         
         SPACE 2                                                                
NEXTDIV  XC    KEY,KEY                                                          
         MVC   KEY(7),RCSVAGY                                                   
         MVI   KEY+3,X'03'                                                      
         MVC   KEY+7(3),RCSVDIV                                                 
         MVI   KEY+10,X'01'                                                     
         GOTO1 HIGH                                                             
         CLC   KEYSAVE(7),KEY                                                   
         BNE   DIV2                                                             
         MVC   RCSVDIV(3),KEY+7                                                 
         GOTO1 GETDIV                                                           
         MVI   MODE,FBUYDIV                                                     
         BAS   RE,GO                                                            
         B     FIRSTPRD                                                         
         SPACE 3                                                                
DIV2     DS    0H                                                               
         MVI   MODE,LBUYCLI                                                     
         BAS   RE,GO                                                            
         CLI   MODE,FBUYCLI                                                     
         BNE   DIV3                                                             
*                                  APPL. PROG HAS SET MODE AFTER                
*                                  LBUYCLI TO FBUYCLI*                          
*                                  PROCESS NEW CLIENT                           
         MVC   RCSVCLI(3),PCLTKCLT                                              
         B     FIRSTPRD                                                         
DIV3     EQU   *                                                                
         CLC   QCLIENT(3),=C'ALL'                                               
         BE    NEXTCLI                                                          
         CLI   QCLIENT,C'*'          CLIENT OFFICE                              
         BE    NEXTCLI                                                          
         CLI   QCLIENT,C'$'          OFFICE GROUP                               
         BE    NEXTCLI                                                          
         CLI   QCLIENT,C'&&'         CLIENT GROUP                               
         BE    NEXTCLI                                                          
         B     LASTREQ                                                          
         EJECT                                                                  
*                   CONTROL PRODUCT READING                                     
         SPACE 3                                                                
FIRSTPRD DS    0H                                                               
         MVC   RCSVPRD(3),SPACES                                                
         CLC   QPRODUCT,=C'ALL'                                                 
         BE    NEXTPRD                                                          
         CLC   QPRODUCT,SPACES                                                  
         BE    NEXTPRD                                                          
         SPACE 2                                                                
         XC    KEY,KEY             ONE PRODUCT                                  
         MVC   KEY(7),RCSVAGY                                                   
         MVC   KEY+7(3),QPRODUCT                                                
         MVI   KEY+3,X'06'                                                      
         GOTO1 HIGH                                                             
         CLC   KEY(25),KEYSAVE                                                  
         BE    FIRSTP5                                                          
*                               IF PRODUCT NOT FOUND                            
*                               AND ALL OF OFFICE CLIENT REQ                    
*                               GOTO NEXT CLIENT                                
         CLC   QCLIENT,=C'ALL'                                                  
         BE    NEXTCLI                                                          
         CLI   QCLIENT,C'*'      OR OFFICE REQ                                  
         BE    NEXTCLI                                                          
         CLI   QCLIENT,C'$'      OR OFFICE LIST                                 
         BE    NEXTCLI                                                          
         CLI   QCLIENT,C'&&'     CLIENT GROUP                                   
         BE    NEXTCLI                                                          
         B     NEXTREQ                                                          
*                                                                               
FIRSTP5  GOTO1 GETPROD                                                          
         MVC   RCSVPRD(3),QPRODUCT                                              
         CLC   QDIV,SPACES                                                      
         BE    PRDX                                                             
         MVC   WORK(64),KEY        SAVE KEY FOR FLTBILL                         
         XC    KEY,KEY             GET DIV REC                                  
         MVC   KEY(7),RCSVAGY                                                   
         MVI   KEY+3,X'03'                                                      
         MVC   KEY+7(3),PPRDDIV                                                 
         CLC   KEY(25),PDIVKEY                                                  
         BE    FIRSTP7             ALREADY HAVE                                 
         GOTO1 READ                                                             
         GOTO1 GETDIV                                                           
*                                                                               
FIRSTP7  DS    0H                                                               
         MVC   KEY(64),WORK        RESTORE KEY                                  
         B     PRDX                                                             
         SPACE 2                                                                
NEXTPRD  DS    0H                                                               
         CLI   FCRDBUY,X'21'                                                    
         BE    PRDX                                                             
         XC    KEY,KEY                                                          
         MVC   KEY(10),RCSVAGY                                                  
         MVI   KEY+10,X'01'                                                     
         MVI   KEY+3,X'06'                                                      
         GOTO1 HIGH                                                             
         CLC   KEYSAVE(7),KEY                                                   
         BNE   PRD4                                                             
         MVC   RCSVPRD(3),KEY+7                                                 
         CLC   RCSTPRD,SPACES      CHECK IF FIRST PRODUCT OPTION OK             
         BE    PRD2                                                             
         CLC   RCSTPRD,KEY+7                                                    
         BNE   NEXTPRD                                                          
         MVC   RCSTPRD,SPACES                                                   
         SPACE 2                                                                
PRD2     CLC   QPRODUCT,=C'ALL'                                                 
         BE    *+14                                                             
         CLC   QPRODUCT,SPACES                                                  
         BNE   PRD3                                                             
         CLI   FCRDACTV,C'Y'                                                    
         BE    PRD3                                                             
         CLC   KEY+7(3),=C'ZZZ'                                                 
         BE    NEXTPRD             IGNORE ZZZ IF ALL IS REQUESTED               
         SPACE 2                                                                
*                                                                               
PRD3     DS    0H                                                               
*                               SEE IF PRODUCT GROUP REQUEST                    
         L     RF,PPWORK2C                                                      
         USING PPWORK2D,RF                                                      
*                                                                               
         CLI   QCONTREQ,C'*'     BE SURE 2 CARD REQUEST                         
         BNE   PRD3X                                                            
*                                                                               
         CLI   Q2USER+24,C'A'   IF ALPHA THEN SCHEME CODE                       
         BL    PRD3X                                                            
         CLI   Q2USER+24,C'Z'                                                   
         BH    PRD3X                                                            
         GOTO1 =A(FLTPGRP),DMCB,(RC)    FILTER ON GROUP                         
         BE    PRD3X                                                            
         B     NEXTPRD          SKIP IF IN WRONG GROUP                          
*                                                                               
         DROP  RF                                                               
*                                                                               
PRD3X    GOTO1 GETPROD          FILTER ON DIVISION                              
         CLC   QDIV,SPACES                                                      
         BE    PRDX                                                             
         CLC   PPRDDIV,RCSVDIV                                                  
         BNE   NEXTPRD                                                          
         B     PRDX                                                             
         SPACE 2                                                                
PRD4     CLC   QDIV,=C'ALL'        CHANGE OF CLIENT                             
         BNE   DIV2                                                             
         MVI   MODE,LBUYDIV                                                     
         BAS   RE,GO                                                            
         B     NEXTDIV                                                          
         SPACE 2                                                                
PRDX     OC    RQBILLDT,RQBILLDT  TEST BILLING DATE FILTER                      
         BZ    PRDX2                                                            
         GOTO1 =A(FLTBILL),DMCB,(RC)                                            
         BE    PRDX2                                                            
* NOT ACTIVE *                                                                  
         CLI   FCRDBUY,X'21'                                                    
         BE    NEXTREQ                                                          
         B     NEXTPRD                                                          
* ACTIVE *                                                                      
PRDX2    CLI   FCRDBUY,X'21'                                                    
         BE    ONEEST                                                           
         EJECT                                                                  
*                   READING ESTIMATES FOR A PRODUCT                             
         SPACE 3                                                                
FIRSTEST DS    0H                                                               
         MVI   MODE,FBUYPRO                                                     
         BAS   RE,GO                                                            
         CLI   MODE,LBUYPRO                                                     
         BE    RETURN              USER ASKS TO SKIP THIS PROD                  
         CLI   FCRDEST,C'Y'                                                     
         BNE   ONEEST                                                           
         SPACE 2                                                                
SRTEST   CLI   FCRDEST,C'Y'        ENTRY USED BY SORT                           
         BNE   FIRSTBIL                                                         
         MVC   RCSVEST,BQEST       STARTING EST                                 
         XC    KEY,KEY                                                          
         B     NE2                                                              
         SPACE 2                                                                
NEXTEST  DS    0H                                                               
         XC    KEY,KEY                                                          
         MVI   KEY+12,1            BUMP TO NEXT                                 
NE2      DS    0H                                                               
         MVC   KEY(10),RCSVAGY                                                  
         MVI   KEY+3,X'07'                                                      
         MVC   KEY+10(2),RCSVEST                                                
         GOTO1 HIGH                                                             
         CLC   KEYSAVE(10),KEY                                                  
         BE    NE3                                                              
         MVI   MODE,LESTPRO          LAST FOR PRODUCT                           
         BAS   RE,GO                                                            
         B     NEXTPRD                                                          
*                                                                               
NE3      MVC   RCSVEST,KEY+10                                                   
         OC    BQEST,BQEST         TEST EST KEY OK                              
         BZ    NE4                                                              
         CLC   KEY+10(2),BQEST                                                  
         BE    NE4                                                              
         OC    BQESTEND,BQESTEND                                                
         BZ    NEXTPRD                                                          
         CLC   KEY+10(2),BQESTEND                                               
         BH    NEXTPRD                                                          
*                                                                               
NE4      DS    0H                                                               
         GOTO1 GETEST                                                           
         GOTO1 =A(FLTREST),DMCB,(RC)                                            
         BNE   NEXTEST                                                          
         BAS   RE,CHKES                                                         
         MVI   MODE,PROCEST                                                     
         BAS   RE,GO                                                            
         CLI   MODE,LESTPRO    SEE IF USER WANTS TO SKIP TO NEXT PRD            
         BE    NEXTPRD                                                          
         B     FIRSTBIL                                                         
         SPACE 3                                                                
CHKES    CLI   RCALLALL,C'Y'       TEST REQ HAD 'ES' DATES                      
         BE    CHKES2                                                           
         CLC   QSTART(2),=C'ES'                                                 
         BNER  RE                                                               
CHKES2   MVC   QSTART(12),PESTST                                                
*                                                                               
SETDATES DS    0H                  SET BQSTART + BQEND                          
         LR    R0,RE                                                            
         XC    BQSTART,BQSTART                                                  
         MVC   BQEND,=3X'FF'                                                    
         MVC   BFSTART(6),BQSTART                                               
         CLI   QSTART,C'0'                                                      
         BNH   SETD2                                                            
*                                                                               
         MVC   MYWORK(6),QSTART                                                 
         CLC   MYWORK+4(2),SPACES                                               
         BNE   *+10                                                             
         MVC   MYWORK+4(2),=C'01'                                               
*                                                                               
*******  GOTO1 DTCNV,DMCB,MYWORK,(1,BQSTART)                                    
         GOTO1 DATCON,DMCB,(0,MYWORK),(3,BQSTART)                               
SETD2    DS    0H                                                               
         CLI   QEND,C'0'                                                        
         BNH   SETD4                                                            
******** GOTO1 DTCNV,DMCB,QEND,(1,BQEND)                                        
         GOTO1 DATCON,DMCB,(0,QEND),(3,BQEND)                                   
*                                                                               
SETD4    DS    0H                                                               
         MVC   DUB(4),=AL2(6,3)                                                 
         CLI   QBPDATE,C'B'                                                     
         BE    SETD4B                                                           
         MVC   DUB(4),=AL2(5,5)                                                 
         CLI   QBPDATE,C'P'                                                     
         BE    SETD4B                                                           
         CLI   QBPDATE,C' '                                                     
         BNE   SETD6                                                            
         MVC   BFSTART(6),BQSTART                                               
         B     SETD6                                                            
*                                                                               
*                                  IF FILTERING ON BILLABLE DATE                
*                                  ESTABLISH INSERT DATE LIMITS                 
SETD4B   DS    0H                                                               
         MVC   BFSTART(6),BQSTART                                               
         CLI   BFEND,X'FF'                                                      
         BNE   *+10                                                             
         MVC   BFEND(3),BFSTART                                                 
*                                                                               
         SR    RF,RF                                                            
         IC    RF,BFSTART+1                                                     
         SH    RF,DUB+2                                                         
         STC   RF,BFSTART+1                                                     
         BP    SETD4C                                                           
         AH    RF,=H'12'                                                        
         STC   RF,BFSTART+1                                                     
         IC    RF,BFSTART                                                       
         BCTR  RF,R0                                                            
         STC   RF,BFSTART                                                       
*                                                                               
SETD4C   DS    0H                                                               
         SR    RF,RF                                                            
         IC    RF,BFEND+1                                                       
         AH    RF,DUB                                                           
         STC   RF,BFEND+1                                                       
         CLI   BFEND+1,12                                                       
         BNH   SETD4D                                                           
         SH    RF,=H'12'                                                        
         STC   RF,BFEND+1                                                       
         IC    RF,BFEND                                                         
         LA    RF,1(RF)                                                         
         STC   RF,BFEND                                                         
*                                                                               
SETD4D   DS    0H                                                               
         MVI   BFSTART+2,1                                                      
         MVI   BFEND+2,31                                                       
*                                                                               
*                                                                               
         CLC   QPROG,=C'04'                                                     
         BE    SETD5                                                            
         CLC   QPROG,=C'B1'        NEW BILLING  - MAY NEED PRIOR                
         BE    SETD5C              MONTHS                                       
         CLC   QPROG,=C'R1'     REBATE BILLING  - MAY NEED PRIOR                
         BE    SETD5C              MONTHS                                       
         CLC   QPROG,=C'RD'  DRAFT REBATE BILL  - MAY NEED PRIOR                
         BE    SETD5C              MONTHS                                       
         CLC   QPROG,=C'D1'  NEW DRAFT BILLING  - MAY NEED PRIOR                
         BE    SETD5C              MONTHS                                       
         CLC   QPROG,=C'E1'        NEW ESTIMATE - MAY NEED PRIOR                
         BE    SETD5C              MONTHS                                       
         CLC   QPROG,=C'06'                                                     
         BNE   SETD6                                                            
SETD5    DS    0H                                                               
         CLI   QBILMODE,C'P'                                                    
         BNE   SETD6                                                            
SETD5C   XC    BFSTART,BFSTART                                                  
SETD6    DS    0H                                                               
         LR    RE,R0                                                            
         BR    RE                                                               
*                                                                               
BFSTART  DS    XL3                                                              
BFEND    DS    XL3                                                              
         EJECT                                                                  
*                  READING ONE ESTIMATE                                         
         SPACE 3                                                                
ONEEST   DS    0H                                                               
         CLC   QEST,SPACES                                                      
         BE    FIRSTBIL                                                         
         CLC   QEST,=C'ALL'                                                     
         BE    FIRSTBIL                                                         
         XC    KEY,KEY                                                          
         MVC   KEY(10),RCSVAGY                                                  
         MVI   KEY+3,X'07'                                                      
         PACK  DUB,QEST            ADD REQUESTED ESTIMATE TO KEY                
         CVB   R2,DUB                                                           
         STH   R2,KEY+10                                                        
         CLC   QPRODUCT,SPACES                                                  
         BE    ONEEST2                                                          
         CLC   QPRODUCT,=C'ALL'                                                 
         BE    ONEEST2                                                          
         GOTO1 HIGH                                                             
         CLC   KEY(25),KEYSAVE                                                  
         BNE   NEXTREQ                                                          
         B     ONEEST3                                                          
ONEEST2  GOTO1 HIGH                                                             
         CLC   KEY(12),KEYSAVE                                                  
         BNE   RETURN                                                           
ONEEST3  EQU   *                                                                
         GOTO1 GETEST                                                           
         BAS   RE,CHKES                                                         
         EJECT                                                                  
*                   READ BILLS FOR A PRODUCT                                    
         SPACE 3                                                                
FIRSTBIL CLI   FCRDBILL,C'Y'                                                    
         BNE   FIRSTBKT                                                         
         XC    KEY,KEY                                                          
         MVC   KEY(10),RCSVAGY                                                  
         CLC   RCSVPRD,=C'ZZZ'                                                  
         BNE   *+10                                                             
         XC    KEY+7(3),KEY+7                                                   
         MVI   KEY+3,X'08'                                                      
         CLC   QEST,SPACES                                                      
         BE    BILL2                                                            
         CLC   QESTEND,SPACES       RANGE OR FILTERS                            
         BNE   BILL2                                                            
         CLC   QEST,=C'ALL'                                                     
         BE    BILL2                OTHERWISE MUST BE ONE EST                   
         MVC   KEY+10(2),PESTKEY+10                                             
         SPACE 2                                                                
BILL2    GOTO1 HIGH                                                             
         CLC   KEY(7),KEYSAVE                                                   
         BNE   FIRSTBKT                                                         
         CLC   RCSVPRD,=C'ZZZ'                                                  
         BE    BILL2A                                                           
         CLC   KEY+7(3),KEYSAVE+7                                               
         BNE   FIRSTBKT                                                         
BILL2A   DS    0H                                                               
         SPACE 2                                                                
BILL4    MVI   MODE,FBILEST                                                     
         BAS   RE,GO                                                            
         SPACE 2                                                                
BILL6    BAS   RE,FILTBILL                                                      
         CLI   BYTE,X'FF'                                                       
         BE    BILL8                                                            
         GOTO1 GETBILL                                                          
         CLI   QBPDATE,C'D'                                                     
         BNE   BILL7                                                            
         CLC   PBILLDAT,QSTART     FILTER ON DATE OF BILLING                    
         BL    BILL8                                                            
         CLI   QEND,C' '                                                        
         BE    BILL7                                                            
         CLC   PBILLDAT,QEND                                                    
         BH    BILL8                                                            
BILL7    DS    0H                                                               
         MVI   MODE,PROCBIL                                                     
         BAS   RE,GO                                                            
         SPACE 2                                                                
BILL8    GOTO1 SEQKSV                                                           
         CLC   KEYSAVE(10),KEY                                                  
         BE    BILL10                                                           
         MVI   MODE,LBILEST                                                     
         BAS   RE,GO                                                            
         CLC   RCSVPRD,=C'ZZZ'                                                  
         BNE   BILL8B                                                           
         CLC   KEY(7),KEYSAVE                                                   
         BE    BILL10                                                           
BILL8B   DS    0H                                                               
         MVI   MODE,LBILPRO                                                     
         BAS   RE,GO                                                            
         B     FIRSTBKT                                                         
         SPACE 2                                                                
BILL10   CLC   KEYSAVE(12),KEY                                                  
         BE    BILL6                                                            
         MVI   MODE,LBILEST                                                     
         BAS   RE,GO                                                            
         B     BILL4                                                            
         EJECT                                                                  
*        READ BUCKET RECORDS                                                    
         SPACE 2                                                                
FIRSTBKT DS    0H                                                               
         CLI   FCRDBKT,C'Y'                                                     
         BNE   FIRSTBUY                                                         
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY(10),RCSVAGY          MUST HAVE PRD                           
         MVI   KEY+3,X'09'                                                      
         MVC   KEY+10(2),PESTKEY+10     AND EST                                 
*                                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(25),KEYSAVE                                                  
         BNE   BKT6                                                             
*                                                                               
         GOTO1 GETBKT                                                           
         MVI   MODE,PROCBKT                                                     
         BAS   RE,GO                                                            
*                                                                               
BKT6     DS    0H                                                               
         EJECT                                                                  
*                   DOWN TO THE NITTY-GRITTY -                                  
*                   TIME TO PROCESS BUYS                                        
         SPACE 3                                                                
FIRSTBUY CLC   QSORT,SPACES                                                     
         BNE   BFORPROD                                                         
         CLI   FCRDBUY,C'N'                                                     
         BE    BUYX                                                             
         MVI   MODE,FBUYEST                                                     
         BAS   RE,GO                                                            
         CLI   FCGTPUB,C'N'                                                     
         BE    FB4                                                              
         MVI   FCGTPUB,C'Y'        GET BOTH PUB RECS                            
         CLC   QREGION(6),SPACES   DONT NEED LTLREC                             
         BNE   *+8                 UNLESS NEED REG/DST                          
         MVI   FCGTPUB,C'B'                                                     
FB4      DS    0H                                                               
         XC    KEY,KEY             BUILD KEY FOR FIRST                          
         MVC   KEY(10),RCSVAGY                                                  
         MVI   KEY+3,X'20'                                                      
         LA    R2,9                                                             
         XC    SVPUB,SVPUB                                                      
         CLI   QPUB,C'0'           ADD PUB IF NECESSARY                         
         BL    BUY1                                                             
         MVC   WORK(11),QPUB                                                    
         LA    R2,15                                                            
         CLC   QPUB+8(3),=C'ZZZ'   SEE IF DOING ALL ZONES + EDTS                
         BNE   FB5                                                              
         LA    R2,13                                                            
         MVC   WORK+8(3),SPACES    USE 'BASE' PUB NUMBER                        
FB5      GOTO1 PUBVAL,DMCB,WORK,SVPUB                                           
         MVC   KEY+10(6),SVPUB                                                  
         MVC   KEY+16(3),BFSTART                                                
*                                                                               
*                                                                               
BUY1     DS    0H                                                               
         CLI   FCRDBUY,X'21'                                                    
         BNE   BUY2                                                             
         MVI   KEY+3,X'21'                                                      
         MVC   KEY+7(6),SVPUB                                                   
         LA    R2,6                                                             
         CLI   QPUB,C'0'                                                        
         BL    BUY2                                                             
         LA    R2,12                                                            
         CLC   QPUB+8(3),=C'ZZZ'   SEE IF DOING ALL ZONES + EDTS                
         BNE   *+8                                                              
         LA    R2,10                                                            
         SPACE 2                                                                
BUY2     GOTO1 HIGH                                                             
BUY6     EX    R2,KEYCOMP          CHECK MATCH ON REQUESTED KEY                 
         BNE   BUYX                                                             
         MVI   MODE,C'S'           DONT READ BUY NOW                            
         BAS   RE,FILTER           FILTER                                       
         CLI   BYTE,X'FF'                                                       
         BE    BUY6                                                             
         SPACE 2                                                                
BUY8     BAS   RE,GETPUB           FIRST FOR PUBLICATION                        
         BAS   RE,FILTREG                                                       
         CLI   BYTE,X'FF'                                                       
         BE    BUY6                                                             
         CLI   HAVEBUY,C'Y'                                                     
         BE    BUY8B                                                            
         GOTO1 GETBUY                                                           
BUY8B    DS    0H                                                               
         BAS   RE,READREP                                                       
         MVI   MODE,FBUYPUB                                                     
         BAS   RE,GO                                                            
         MVC   SVPUB,KEY+10                                                     
         CLI   FCRDBUY,X'21'                                                    
         BNE   BUY10                                                            
*                                                                               
BUY9     DS    0H                                                               
         MVC   SVPUB,KEY+7                                                      
         MVI   MODE,FBUYPRO                                                     
         BAS   RE,GO                                                            
         MVC   PPRDKPRD,PBUYKPRD   FOR GETINS                                   
         B     BUY12                                                            
         SPACE 2                                                                
BUY10    DS    0H                                                               
         CLI   FCGTCONT,C'Y'                                                    
         BNE   BUY12                                                            
         BAS   RE,READCONT                                                      
         MVI   MODE,FBUYCNT                                                     
         BAS   RE,GO                                                            
         SPACE 2                                                                
BUY12    GOTO1 GETINS,DMCB,PBUYKEY,GROSS,(FCOAPRD,PPRDKPRD)                     
*                                                                               
         CLC   QREGION(6),SPACES                                                
         BE    BUY13                                                            
         GOTO1 =A(RDSPLIT),(R1),QREGION     GET SHR FOR THIS REG/EST            
*                                                                               
BUY13    DS    0H                                                               
         CLI   FCGTJOB,C'N'                                                     
         BE    *+8                                                              
         BAS   RE,READJOB                                                       
         MVI   MODE,PROCBUY        GO TO PROCESS BUY                            
         BAS   RE,GO                                                            
         SPACE 2                                                                
         GOTO1 SEQKSV                                                           
BUY14    DS    0H                                                               
         EX    R2,KEYCOMP          CHECK FOR MAIN C/B                           
         BE    BUY16                                                            
BUY15    DS    0H                                                               
         CLI   FCRDBUY,X'21'                                                    
         BE    BUY21                                                            
         MVI   MODE,LBUYCNT                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPUB                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYVEN                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYEST                                                     
         BAS   RE,GO                                                            
         B     BUYX                                                             
         SPACE 2                                                                
BUY16    DS    0H                                                               
         CLI   FCRDBUY,X'21'                                                    
         BE    BUY22                                                            
         CLC   KEYSAVE(16),KEY     PUB BREAK                                    
         BNE   BUY17               YES                                          
         BAS   RE,FILTER                                                        
         CLI   BYTE,X'FF'          PASS FILTER                                  
         BE    BUY14               NO                                           
         B     BUY18                                                            
*                                                                               
BUY17    MVI   MODE,LBUYCNT                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPUB                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYVEN                                                     
         BAS   RE,GO                                                            
* NOTE - NO LONGER CHECK PUB BREAKS TILL PASS FILTER                            
BUY17A   BAS   RE,FILTER                                                        
         MVI   MODE,0                                                           
         CLI   BYTE,X'FF'                                                       
         BNE   BUY17B                                                           
         EX    R2,KEYCOMP          CHECK C/B                                    
         BNE   BUY15                                                            
         B     BUY17A                                                           
         SPACE 2                                                                
BUY17B  DS    0H                                                                
         CLC   SVPUB(4),KEY+10                                                  
         BE    BUY8                                                             
         MVI   MODE,LBUYVEN                                                     
         BAS   RE,GO                                                            
         B     BUY8                                                             
         SPACE 3                                                                
BUY18    CLI   FCGTCONT,C'Y'       CHANGE OF CONTRACT                           
         BNE   BUY12                                                            
*                                                                               
*NEW BELOW - TO ADDRESS PCONREC                                                 
         L     RF,PPWORK2C                                                      
         USING PPWORK2D,RF                                                      
         L     RE,ACONIO                                                        
         USING PCONREC,RE                                                       
*NEW ABOVE - TO ADDRESS PCONREC                                                 
*                                                                               
         CLC   KEY+16(3),PCONSTRT                                               
         BL    BUY20                                                            
         CLC   KEY+16(3),PCONEND                                                
         BNH   BUY12                                                            
*                                                                               
         DROP  RE,RF                                                            
         SPACE 2                                                                
BUY20    MVI   MODE,LBUYCNT                                                     
         BAS   RE,GO                                                            
         B     BUY10                                                            
         SPACE 2                                                                
BUY21    DS    0H                                                               
         MVI   MODE,LBUYPRO                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPUB                                                     
         BAS   RE,GO                                                            
         B     BUYX                                                             
BUY22    DS    0H                                                               
         CLC   KEY(16),KEYSAVE     PRD BRK                                      
         BNE   BUY23               YES                                          
         BAS   RE,FILTER                                                        
         CLI   BYTE,X'FF'          PASS FILTER                                  
         BE    BUY14               NO                                           
         B     BUY12                                                            
*                                                                               
BUY23    DS    0H                                                               
         MVI   MODE,LBUYPRO                                                     
         BAS   RE,GO                                                            
         CLC   KEY(13),KEYSAVE                                                  
         BE    BUY23A                                                           
         MVI   MODE,LBUYPUB                                                     
         BAS   RE,GO                                                            
*        NOTE - NO LONGER CHECK BRKS UNTIL PASS FILTER                          
*                                                                               
BUY23A   DS    0H                                                               
         BAS   RE,FILTER                                                        
         CLI   BYTE,X'FF'                                                       
         BNE   BUY23B                                                           
         EX    R2,KEYCOMP                                                       
         BNE   BUY21                                                            
         B     BUY23A                                                           
BUY23B   DS    0H                                                               
         CLC   SVPUB,KEY+7         TEST PUB BREAK                               
         BE    BUY8                                                             
         MVI   MODE,LBUYPUB                                                     
         BAS   RE,GO                                                            
         B     BUY8                                                             
         SPACE 2                                                                
KEYCOMP  CLC   KEY(0),KEYSAVE                                                   
         SPACE 2                                                                
BUYX     DS    0H                                                               
         EJECT                                                                  
*                   CONTROL RETURNS                                             
         SPACE 3                                                                
RETURN   EQU   *                                                                
         CLI   FCRDBUY,X'21'                                                    
         BE    DIV2                                                             
         CLI   FCRDEST,C'Y'        FOR EST READ                                 
         BE    NEXTEST             GET NEXT                                     
         MVI   MODE,LBUYPRO                                                     
         BAS   RE,GO                                                            
         CLC   QSORT,SPACES                                                     
         BNE   AFTRPROD                                                         
         CLC   QPRODUCT,=C'ALL'                                                 
         BE    NEXTPRD                                                          
         CLC   QPRODUCT,SPACES                                                  
         BE    NEXTPRD                                                          
         CLC   QDIV,SPACES                                                      
         BE    RET2                                                             
         MVI   MODE,LBUYDIV                                                     
         BAS   RE,GO                                                            
         SPACE 2                                                                
RET2     DS    0H                                                               
         B     DIV2                END OF CLIENT                                
         EJECT                                                                  
*                   SORT SWITCHING CONTROL                                      
         SPACE 3                                                                
SORT1    DS    0H                                                               
         MVC   QSORT,=C'01'                                                     
SORT1A   DS    0H                                                               
         PACK  DUB,QSORT                                                        
         CVB   R0,DUB                                                           
         STC   R0,SAVEQSRT                                                      
*****    MVC   SRTRLEN(8),=A(54,50)                                             
         MVC   SRTRLEN(8),=A(56,52)                                             
         B     SORTBYDS                                                         
         SPACE 2                                                                
SORT2    DS    0H                                                               
         PACK  DUB,QSORT                                                        
         CVB   R0,DUB                                                           
         STC   R0,SAVEQSRT                                                      
         MVI   CLTPUBSW,C'C'       CLIENT HIGH                                  
**NEW 9/6/88                                                                    
         CLC   QPROG,=C'37'        SEE IF VENDOR PAYER'S LIST                   
         BE    SORT2A                                                           
**NEW 9/6/88                                                                    
         CLC   QPROG,=C'36'        OR VENDOR SUMMARY                            
         BNE   SORT2C                                                           
SORT2A   CLC   QCLIENT,=C'ALL'     FOR ALL CLTS                                 
         BE    SORT2B              USE PUB HIGH                                 
         CLI   QCLIENT,C'*'        OR OFFICES                                   
         BE    SORT2B                                                           
         CLI   QCLIENT,C'$'        OR OFFICE LIST                               
         BE    SORT2B                                                           
         CLI   QCLIENT,C'&&'       OR CLIENT GROUP                              
         BE    SORT2B                                                           
         B     SORT2C                                                           
*                                                                               
SORT2B   MVI   CLTPUBSW,C'P'   36 FOR ALL OR * QCLIENT-USE PUB HIGH             
***** SORT2C   MVC   SRTRLEN(8),=A(40,36)                                       
SORT2C   MVC   SRTRLEN(8),=A(42,38)                                             
         B     SORTBYCN                                                         
         SPACE 2                                                                
SORT48   DS    0H                  PUBLIST SORTS                                
         PACK  DUB,QSORT                                                        
         CVB   R0,DUB                                                           
         STC   R0,SAVEQSRT                                                      
         MVC   SRTRLEN(8),=A(29,25)                                             
         B     SORTPUBS                                                         
         SPACE 2                                                                
SORTCN   DS    0H                                                               
         MVI   SAVEQSRT,1                                                       
         CLC   QSORT,=C'00'                                                     
         BNH   SORTCN2                                                          
         PACK  DUB,QSORT                                                        
         CVB   R0,DUB                                                           
         STC   R0,SAVEQSRT                                                      
SORTCN2  DS    0H                                                               
         MVC   SRTRLEN(8),=A(47,47)                                             
         B     SORTCON                                                          
         SPACE 2                                                                
SORT10   DS    0H                                                               
         B     USERSORT                                                         
         SPACE 2                                                                
SAVEQSRT DC    X'00'                                                            
CLTPUBSW DS    C                                                                
SRTPARS  DS    0F                                                               
         DC    A(0)                                                             
         DC    V(SRTTABLE)                                                      
SRTCOUNT DC    F'0'                                                             
SRTRLEN  DC    F'0'                                                             
SRTKLEN  DC    F'0'                                                             
SRTMAX   DC    F'0'                                                             
*                                                                               
SRTREC   DS    CL100                                                            
SRTNEXT  DS    F                                                                
*                                                                               
SRTSORT  DC    C'SORT FIELDS=(1,'                                               
SRTSKLN  DC    CL2' '                                                           
         DC    C',A),FORMAT=BI '                                                
*                                                                               
SRTRCDC  DC    C'RECORD TYPE=F,LENGTH='                                         
SRTSRLN  DC    CL2' '                                                           
         DC    C' '                                                             
*                                                                               
*                                                                               
SAVESRT  DS    F                                                                
SVNUM    DS    H                                                                
SVPUB    DS    CL6                                                              
*                                                                               
SORTSW   DS    C                                                                
PROFKEY  DC    XL12'00'                                                         
         EJECT                                                                  
*                   SORT BUYS BY DISTRICT (CONTROL)                             
         SPACE 3                                                                
SORTBYDS DS    0H                                                               
         XC    SRTCOUNT,SRTCOUNT        CLEAR SORT TABLE                        
*****    MVI   SVCLTOFF,0                                                       
         XC    SVCLTOFF,SVCLTOFF                                                
*                                                                               
         CLI   QCLIENT,C'$'             SEE IF OFFICE LIST                      
         BNE   SDF1                                                             
         GOTO1 =A(BLDOFC),DMCB,(RC)                                             
         L     R4,=V(OFCLIST)                                                   
*****    OC    0(4,R4),0(R4)                                                    
         OC    0(6,R4),0(R4)                                                    
         BZ    LASTREQ                                                          
*                                                                               
SDF1     DS    0H                                                               
         LA    R3,SRTREC                                                        
         USING SORTRECD,R3                                                      
         CLI   FCGTPUB,C'N'                                                     
         BE    SDF4                                                             
         MVI   FCGTPUB,C'B'                                                     
         CLC   QREGION(6),SPACES   TEST NEED LTL                                
         BE    SDF2                NO                                           
         MVI   FCGTPUB,C'L'                                                     
         CLI   SAVEQSRT,1          TEST NEED NAME                               
         BE    SDF4                NO                                           
         MVI   FCGTPUB,C'Y'        NEED BOTH                                    
         B     SDF4                                                             
SDF2     DS    0H                                                               
SDF4     DS    0H                                                               
         EJECT                                                                  
*                   SORT BUYS BY DISTRICT (INPUT)                               
         SPACE 3                                                                
         XC    KEY,KEY             BUILD KEY FOR FIRST                          
         MVC   KEY(3),RCSVAGY                                                   
         MVI   KEY+3,X'20'                                                      
*                                                                               
         XC    SVPUB,SVPUB                                                      
         CLI   QPUB,C'0'                                                        
         BL    SD0                                                              
         MVC   WORK(11),QPUB                                                    
         CLC   QPUB+8(3),=C'ZZZ'   SEE IF DOING ALL ZONES + EDTS                
         BNE   *+10                                                             
         MVC   WORK+8(3),SPACES                                                 
         GOTO1 PUBVAL,DMCB,WORK,SVPUB                                           
*                                                                               
SD0      DS    0H                                                               
         CLC   QCLIENT,=C'ALL'                                                  
         BE    SDHIGH                                                           
         CLI   QCLIENT,C'*'       OFFICE REQUEST                                
         BE    SDHIGH                                                           
         CLI   QCLIENT,C'$'       OFFICE LIST REQUEST                           
         BE    SDHIGH                                                           
         CLI   QCLIENT,C'&&'      CLIENT GROUP                                  
         BE    SDHIGH                                                           
         MVC   KEY+4(3),QCLIENT                                                 
         MVC   RCSVCLI,QCLIENT                                                  
         CLI   SAVEQSRT,8         FOR REP OR PAYADDR SORTS                      
         BE    SD0C               MUST READ CLIENT HEADER ON INPUT              
         CLI   SAVEQSRT,9                                                       
         BE    SD0C                                                             
         CLC   QPROG,=C'06'        IF DETAIL BILLING                            
         BE    SD0C                                                             
         CLC   QPROG,=C'E1'        IF NEW ESTIMATE                              
         BE    SD0C                                                             
         CLC   QPROG,=C'D1'        IF NEW DRAFT BILLING                         
         BE    SD0C                                                             
         CLC   QPROG,=C'B1'        SEE IF NEW BILLING                           
         BE    SD0C                                                             
         CLC   QPROG,=C'RD'        DRAFT REBATE BILLING                         
         BE    SD0C                                                             
         CLC   QPROG,=C'R1'        SEE IF NEW REBATE BILLING                    
         BNE   SD0D                                                             
*                                                                               
SD0C     BAS   RE,TSTCLI           GET CLIENT HDR                               
         BNE   NEXTREQ             MIGHT GET CLIENT NOT ON FILE                 
*                                  FOR MULTI-MEDIA REQ                          
SD0D     XC    KEY,KEY             REBUILD KEY AFTER TSTCLI                     
         MVC   KEY(3),RCSVAGY                                                   
         MVI   KEY+3,X'20'                                                      
         MVC   KEY+4(3),QCLIENT                                                 
         CLC   QPRODUCT,=C'ALL'                                                 
         BE    SDHIGH                                                           
         CLI   QPRODUCT,C' '                                                    
         BE    SDHIGH                                                           
         MVC   KEY+7(3),QPRODUCT                                                
         CLC   QREGION(6),SPACES                                                
         BNE   SD1                                                              
         CLC   QDIV,SPACES                                                      
         BE    SD1B                                                             
*                                  GET PRDHDR FOR ONE PRD IF NEED               
*                                  REG/DST OR DIV                               
SD1      DS    0H                                                               
         MVC   WORK(64),KEY                                                     
         MVI   KEY+3,X'06'                                                      
         XC    KEY+10(15),KEY+10                                                
         GOTO1 HIGH                                                             
         CLC   KEY(25),KEYSAVE                                                  
         BNE   NEXTREQ                                                          
         GOTO1 GETPROD                                                          
         MVC   KEY(64),WORK                                                     
*                                                                               
SD1B     DS    0H                                                               
         OC    RQBILLDT,RQBILLDT   TEST BILL DATE FILTER                        
         BZ    SD1C                                                             
         GOTO1 =A(FLTBILL),DMCB,(RC)                                            
         BNE   NEXTREQ             IF NOT ACTIVE NEXT REQUEST                   
*                                                                               
SD1C     CLI   QEST,C' '                                                        
         BE    SDHIGH                                                           
         CLI   QESTEND,C' '                                                     
         BNE   SDHIGH                                                           
         CLI   RCALLALL,C'Y'       TEST REQ HAD ES DATES ORIGINALLY             
         BE    SDHIGH          CAN'T SET BFSTART/END OR BQSTART/BQEND           
         CLC   QSTART(2),=C'ES'    GET EST HDR FOR START/END                    
         BNE   SDHIGH                                                           
SD1D     MVC   WORK,KEY                                                         
         MVC   KEY+10(2),BQEST                                                  
*                                     SINCE BQEST IS ZEROS                      
         XC    KEY+12(13),KEY+12      MUST CLEAR REST OF KEY                    
         MVI   KEY+3,X'07'                                                      
         GOTO1 HIGH                                                             
         CLC   KEY(25),KEYSAVE                                                  
         BNE   NEXTREQ                                                          
         GOTO1 GETEST                                                           
         BAS   RE,CHKES                                                         
         MVC   KEY(64),WORK                                                     
*                                                                               
         SPACE 2                                                                
SDHIGH   EQU   *                                                                
         GOTO1 HIGH             GET FIRST                                       
         B     SD4                                                              
         SPACE 2                                                                
SD2      GOTO1 SEQKSV              GET NEXT                                     
         SPACE 2                                                                
SD4      CLC   KEYSAVE(4),KEY      CHECK CHANGE OF A/M                          
         BNE   SD5                                                              
         CLC   KEYSAVE(7),KEY                      CLIENT                       
         BE    SD4A                                                             
         L     RF,=A(FECOUNT)      MUST REBUILD FELIST FOR EACH CLIENT          
         XC    0(4,RF),0(RF)                                                    
*                                                                               
         CLC   QCLIENT,=C'ALL'                                                  
         BE    SD5A                                                             
         CLI   QCLIENT,C'*'       OFFICE REQUEST                                
         BE    SD4A0                                                            
         CLI   QCLIENT,C'&&'      OR CLIENT GROUP                               
         BE    SD4A0                                                            
         CLI   QCLIENT,C'$'       OFFICE LIST REQUEST                           
         BNE   SD5                                                              
*                                                                               
         L     R4,=V(OFCLIST)                                                   
SD4A00   CLC   KEY+4(3),3(R4)                                                   
         BE    SD4A00X                                                          
         LA    R4,6(R4)                                                         
         CLI   0(R4),0                                                          
         BNE   SD4A00                                                           
         B     SD4A0X                                                           
*                                                                               
SD4A00X  MVC   SVCLTOFF,0(R4)       SAVE OFFICE                                 
*                                                                               
SD4A0    BAS   RE,TSTCLI                                                        
         BE    SD5A2                                                            
SD4A0X   MVI   KEY+7,X'FF'                                                      
         B     SDHIGH              BUMP TO NEXT CLI                             
         SPACE 2                                                                
SD4A     CLC   KEYSAVE(10),KEY                     PRODUCT                      
         BE    SD6                                                              
         CLC   QPRODUCT,=C'ALL'                                                 
         BE    SD4B                                                             
         CLC   QPRODUCT,SPACES                                                  
         BE    SD4B                                                             
         B     SD5                                                              
         SPACE 2                                                                
SD4B     DS    0H                                                               
*                                                                               
         L     RF,PPWORK2C                                                      
         USING PPWORK2D,RF                                                      
*                                                                               
         CLI   QCONTREQ,C'*'     BE SURE 2 CARD REQUEST                         
         BNE   SD4BX                                                            
*                                                                               
         CLI   Q2USER+24,C'A'   IF ALPHA THEN SCHEME CODE                       
         BL    SD4BX                                                            
         CLI   Q2USER+24,C'Z'                                                   
         BH    SD4BX                                                            
         GOTO1 =A(FLTPGRP),DMCB,(RC)    FILTER ON GROUP                         
         BE    SD4BX                                                            
         B     SD4D             SKIP TO NEXT PRODUCT                            
*                                                                               
         DROP  RF                                                               
*                                                                               
SD4BX    DS    0H                                                               
         OC    RQBILLDT,RQBILLDT   TEST BILLING DATE FILTER                     
         BZ    SD4C                                                             
         GOTO1 =A(FLTBILL),DMCB,(RC)                                            
         BNE   SD4D                NO ACTIVITY                                  
SD4C     CLI   FCRDACTV,C'Y'                                                    
         BE    SD5A2                                                            
         CLC   KEY+7(3),=C'ZZZ'                                                 
         BNE   SD5A2                                                            
SD4D     MVI   KEY+10,X'FF'        FORCE NEXT PRODUCT                           
         B     SDHIGH                                                           
         SPACE 2                                                                
SD5      EQU   *                                                                
         XC    WORK,WORK                                                        
*                                                                               
         CLI   FCGTPUB,C'N'                                                     
         BE    SD502                                                            
         MVI   FCGTPUB,C'B'                                                     
         CLC   QREGION(6),SPACES   TEST NEED LTL                                
         BE    *+8                 NO                                           
         MVI   FCGTPUB,C'Y'                                                     
SD502    DS    0H                                                               
         XC    SVPUB,SVPUB                                                      
         B     SDOUTPUT                                                         
         SPACE 2                                                                
SD5A     DS    0H                                                               
         CLI   SAVEQSRT,8          FOR REP OR PAYADDR SORTS MUST                
         BE    SD5A1               MUST ALSO READ CLIENT ON INPUT               
         CLI   SAVEQSRT,9                                                       
         BE    SD5A1                                                            
         CLC   QPROG,=C'06'        IF DETAIL BILLING                            
         BE    SD5A1                                                            
         CLC   QPROG,=C'E1'        IF NEW ESTIMATE                              
         BE    SD5A1                                                            
         CLC   QPROG,=C'D1'        IF NEW DRAFT BILLING                         
         BE    SD5A1                                                            
         CLC   QPROG,=C'R1'        IF NEW REBATE BILLING                        
         BE    SD5A1                                                            
         CLC   QPROG,=C'RD'        IF NEW REBATE BILLING - DRAFT                
         BE    SD5A1                                                            
         CLC   QPROG,=C'B1'        IF NEW BILLING                               
         BNE   *+8                                                              
SD5A1    BAS   RE,TSTCLI           GET CLIENT HDR                               
*                                                                               
SD5A2    CLC   QDIV,SPACES                                                      
         BNE   SD5B                                                             
         CLC   QREGION(6),SPACES                                                
         BE    SD6                                                              
         SPACE 2                                                                
SD5B     MVC   KEYSAVE,KEY         GET PRODUCT RECORD ON A CHANGE OF            
         MVI   KEY+3,X'06'         PRODUCT IN A MULTI-PRODUCT SITUATION         
         XC    KEY+10(15),KEY+10   IF WE NEED THE DIVISION NUMBER               
         GOTO1 READ                                                             
         GOTO1 GETPROD                                                          
         MVC   KEY,KEYSAVE                                                      
         CLC   QDIV,SPACES                                                      
         BE    SD5C                                                             
         CLC   QDIV,=C'ALL'                                                     
         BE    SD5C                                                             
         CLC   QDIV,PPRDDIV        CHECK MATCH ON DIVISION IN                   
         BE    SD5C                A ONE DIVISION SITUATION                     
         MVI   KEY+10,X'FF'                                                     
         MVI   KEY+11,0                                                         
         B     SDHIGH              AND GO AND READ HIGH                         
         SPACE 2                                                                
SD5C     GOTO1 HIGH                                                             
         SPACE 2                                                                
SD6      MVI   MODE,C'S'                                                        
         BAS   RE,FILTER                                                        
         CLI   BYTE,X'FF'          SEE IF PASSES                                
         BE    SD4                 NO                                           
         BAS   RE,GETPUB                                                        
         MVI   MODE,UNSRTREC       USER HAS THE OPTION OF REJECTING             
         BAS   RE,GO               RECORDS THAT HAVE BEEN READ BEFORE           
         CLI   MODE,0              SORTING DIRECTORY                            
         BE    SD2                                                              
         SPACE 2                                                                
         XC    SORTREC,SORTREC     FILL RECORD                                  
         MVC   SORTOFF,SVCLTOFF                                                 
         MVC   SORTCLI,KEY+4       CLIENT                                       
         CLC   QDIV,SPACES                                                      
         BE    SD11                                                             
         MVC   SORTDIV,=C'999'                                                  
         OC    PPRDDIV,PPRDDIV                                                  
         BZ    SD11                NO DIVISION                                  
         MVC   SORTDIV,PPRDDIV     DIVISION (OPTIONAL)                          
         SPACE 2                                                                
SD11     EQU   *                                                                
         CLC   QPRODUCT,SPACES                                                  
         BE    *+10                                                             
         MVC   SORTPROD,KEY+7      PRODUCT                                      
*                                                                               
         CLC   QPROG,=C'54'                                                     
         BE    SD11B               54 AND L2 MUST SORT BY ESTIMATE              
         CLC   QPROG,=C'L2'                                                     
         BE    SD11B               54 AND L2 MUST SORT BY ESTIMATE              
         CLC   QPROG,=C'B1'                                                     
         BE    SD11B               BILLING MUST SORT BY ESTIMATE                
         CLC   QPROG,=C'D1'                                                     
         BE    SD11B               BILLING MUST SORT BY ESTIMATE                
         CLC   QPROG,=C'R1'                                                     
         BE    SD11B               BILLING MUST SORT BY ESTIMATE                
         CLC   QPROG,=C'RD'                                                     
         BE    SD11B               BILLING MUST SORT BY ESTIMATE                
*                                                                               
         CLC   QEST,=C'ALL'                                                     
         BE    SD11B                                                            
         CLC   QEST,SPACES                                                      
         BE    SD12                                                             
         CLC   QESTEND,SPACES                                                   
         BNE   SD12                                                             
SD11B    DS    0H                                                               
         MVC   SORTEST,KEY+19      ESTIMATE (IF EST=ALL)                        
         SPACE 2                                                                
SD12     CLI   QBILMODE,C' '       BILL YM IF BILLING MODE                      
         BE    SD14                                                             
         CLC   QPROG,=C'06'        ONLY FOR DETAIL BILLING                      
         BNE   SD14                                                             
         MVC   SORTYM,PBDBDATE                                                  
         SPACE 2                                                                
SD14     DS    0H                                                               
         XC    SORTPUB,SORTPUB     PUBLICATION                                  
         MVC   SORTPUB(6),KEY+10   NUMBER                                       
         MVC   SORTPDA(8),PUBSRTDA                                              
         CLI   SAVEQSRT,1                                                       
         BE    SD14D                                                            
         CLI   SAVEQSRT,8          PAY REP SORT                                 
         BE    SD15                                                             
         CLI   SAVEQSRT,9         PAY ADDR NAME SORT                            
         BE    SD15                                                             
         CLI   SAVEQSRT,3                                                       
         BE    SD14F                                                            
         CLI   SAVEQSRT,4                                                       
         BE    SD14H                                                            
         MVC   SORTPUB,PUBNAME                                                  
         CLC   PUBNAME(4),=C'THE '                                              
         BNE   *+10                                                             
         MVC   SORTPUB,PUBNAME+4                                                
         CLI   SAVEQSRT,7          MARKET SORT                                  
         BNE   SD15X                                                            
         CLI   QMEDIA,C'O'                                                      
         BNE   SD14C                                                            
         MVC   SORTPUB+2(15),PUBZNAME   OUTDOOR-USE ST,MKTNAME                  
         MVC   SORTPUB+17(1),KEY+15      EDITION (P,R,0)                        
         MVC   SORTPUB(2),PUBSTACD      USE STACD IF IT EXISTS                  
         CLI   SORTPUB,C' '                                                     
         BNH   SD14B                                                            
         CLI   SORTPUB,C'0'                                                     
         BL    SD14D                                                            
SD14B    DS    0H                                                               
         MVC   SORTPUB(2),PUBSTATE      OTHERWISE USE STATE                     
         B     SD14D                                                            
SD14C    DS    0H                                                               
         MVC   SORTPUB(2),PUBSTATE      FOR OTHER USE ST,CITY                   
         MVC   SORTPUB+2(16),PUBCITY                                            
         B     SD15X                                                            
*****                                                                           
SD14F    DS    0H                                                               
         ZAP   SVCIRCNM,=P'0'                                                   
         XC    SVCRDAT,SVCRDAT               CLEAR SVCRDAT                      
         LA    R2,PUBREC+33                                                     
SD14F5   CLI   0(R2),X'30'                                                      
         BE    SD14F10                                                          
         CLI   0(R2),0                                                          
         BE    SD14F15                                                          
SD14F8   ZIC   R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     SD14F5                                                           
*                                                                               
SD14F10  DS    0H                                                               
         USING PUBCIREL,R2                                                      
         CLC   SVCRDAT,PUBCDAT                                                  
         BNL   SD14F8                                                           
         MVC   SVCRDAT,PUBCDAT                                                  
         MVC   SVCIRCNM,PUBCIR1                                                 
         B     SD14F8                                                           
*                                                                               
SD14F15  DS    0H                                                               
         SP    SVCIRCNM,=P'999999999'                                           
         MVC   SORTPUB(5),SVCIRCNM                                              
         B     SD15X                                                            
*                                                                               
SD14H    DS    0H                                                               
         LA    R2,SORTPUB                                                       
         LA    R1,PUBCITY                                                       
         LA    R0,16       FOR BCT                                              
SD14H5   CLI   0(R1),C' '    DON'T SORT ON SPECIAL CHARACTERS OR BLANKS         
         BNH   SD14H10                                                          
         MVC   0(1,R2),0(R1)                                                    
         LA    R2,1(R2)                                                         
SD14H10  LA    R1,1(R1)                                                         
         BCT   R0,SD14H5                                                        
*                                                                               
         MVC   SORTPUB+16(2),PUBNAME                                            
         CLC   PUBNAME(4),=C'THE '                                              
         BNE   *+10                                                             
         MVC   SORTPUB+16(2),PUBNAME+4                                          
         B     SD15X                                                            
*                                                                               
*****                                                                           
SD14D    EQU   *                                                                
         B     SD15X                                                            
*                                                                               
SD15     CLI   SAVEQSRT,8          PAY REP SORT                                 
         BL    SD15X                                                            
         CLI   SAVEQSRT,9          PAY ADDR NAME SORT                           
         BH    SD15X                                                            
         MVC   SORTPUB+12(6),KEY+10   MOVE PUB NUMBER                           
         XC    SORTPUB(12),SORTPUB    CLEAR                                     
         MVI   SORTPUB,X'FF'       SO 'NO REP' WILL SORT LAST                   
         BAS   RE,READREPI         READ REPS ON INPUT TO SORT                   
         CLI   PREPNAME,C' '       SEE IF I FOUND REP OR PAY ADDR               
         BH    SD15A               NO                                           
         CLI   SAVEQSRT,9          SEE IF DOING PAY ADDR NAME SORT              
         BNE   SD15X               NO                                           
         CLC   QPROG,=C'NV'                                                     
         BE    SD15A0                                                           
         MVC   SORTPUB+1(17),PUBNAME  PAY PUB DIRECT IN PUBNAME ORDER           
         CLC   PUBNAME(4),=C'THE '    STRIP-OFF 'THE '                          
         BNE   SD15X                                                            
         MVC   SORTPUB+1(16),PUBNAME+4                                          
         MVI   SORTPUB+17,0                                                     
         B     SD15X                                                            
*                                                                               
SD15A0   MVC   SORTPUB(18),PUBNAME   PAY PUB DIRECT IN PUBNAME ORDER            
         CLC   PUBNAME(4),=C'THE '    STRIP-OFF 'THE '                          
         BNE   SD15X                                                            
         MVC   SORTPUB(16),PUBNAME+4                                            
         MVI   SORTPUB+16,0                                                     
         MVI   SORTPUB+17,0                                                     
         B     SD15X                                                            
*                                                                               
SD15A    CLI   SAVEQSRT,9          SEE IF DOING PAY ADDR NAME SORT              
         BNE   SD15B                                                            
         MVC   SORTPUB(12),PREPNAME                                             
*                                  PUB NUMBER WILL STILL BE IN                  
*                                  SORTPUB+12                                   
*                                  SO PUBS WITH SAME REP OR ADDR                
*                                  WILL BE SORTED BY PUB NUMBER                 
         B     SD15X                                                            
*                                                                               
SD15B    CLI   PREPKEY+3,X'11'     SEE IF I FOUND A REP                         
         BNE   SD15C                                                            
         MVC   SORTPUB(5),PREPKREP    REP CODE                                  
         B     SD15X                                                            
SD15C    MVI   SORTPUB,X'FE'         SO PAY ADDRS WILL SORT                     
         MVC   SORTPUB+1(11),PREPNAME                                           
*                                  BEFORE ** PAY PUB DIRECT **                  
*                                  BUT AFTER REPS                               
*                                  BOTH WILL BE SORTED BY PUB NUMBER            
*                                                                               
SD15X    DS    0H                                                               
         MVC   SORTDATE,KEY+16     DATE                                         
         MVC   SORTLINE,KEY+24                                                  
         MVC   SORTDISK,KEY+27     DISK ADDRESS                                 
         CLC   QREGION(6),SPACES                                                
         BNE   *+12                                                             
         BAS   RE,TOSORT                                                        
         B     SD2                                                              
         SR    R4,R4                                                            
         L     R2,ALTLREC                                                       
         LA    R2,33(R2)                                                        
         MVC   SORTREG(6),=C'999999'    PRESET REG/DST TO 999                   
         XC    SVREGDST,SVREGDST                                                
         B     SD17                                                             
         SPACE 2                                                                
SD16     IC    R4,1(R2)            LOOP FOR DISTRICT ELEMENTS                   
         AR    R2,R4                                                            
         SPACE 2                                                                
SD17     CLI   0(R2),0                                                          
         BE    SD180G                                                           
         CLI   0(R2),X'71'                                                      
         BNE   SD16                                                             
         USING PUBDSTEL,R2                                                      
         CLC   QPUB+1(3),=C'RD='        REQUESTED REG/DST OVERRIDE              
         BE    SD17B                                                            
         CLC   PUBDCLT,KEY+4       CHECK MATCH ON CLIENT                        
         BNE   SD16                                                             
         CLC   PUBDDIV,=C'999'                                                  
         BE    SD18                                                             
         CLC   PUBDDIV,PPRDDIV                    AND DIVISION                  
         BNE   SD16                                                             
         B     SD18                                                             
SD17B    DS    0H                                                               
         CLC   PUBDCLT,QPUB+4                                                   
         BNE   SD16                                                             
         CLC   PUBDDIV,=C'000'                                                  
         BNE   SD16                                                             
         SPACE 2                                                                
SD18     EQU   *                                                                
         XC    SORTREG(6),SORTREG                                               
         CLC   QREGION,SPACES                                                   
         BE    SD180A                                                           
         CLC   QREGION,=C'ALL'                                                  
         BE    SD180A                                                           
         CLC   QREGION,PUBDREG                                                  
         BNE   SD16                                                             
SD180A   MVC   SORTREG,PUBDREG                                                  
SD180B   CLC   QDIST,SPACES                                                     
         BE    SD180E                                                           
         CLC   QDIST,=C'ALL'                                                    
         BE    SD180C                                                           
         CLC   QDIST,PUBDDST                                                    
         BNE   SD16                                                             
SD180C   MVC   SORTDIST,PUBDDST                                                 
*                                                                               
SD180E   DS    0H                                                               
         CLC   SORTREG(6),SVREGDST     FOR SAME BUY DONT PASS TWO               
         BE    SD16                    IDENTICAL KEYS- RDSPLIT                  
         MVC   SVREGDST,SORTREG        WILL HANDLE ADDING UP AMOUNTS            
         BAS   RE,TOSORT                                                        
         B     SD16                                                             
*                                                                               
SD180G   DS    0H                                                               
         CLC   SORTREG(6),=C'999999'                                            
         BNE   SD2                                                              
         CLI   QREGION,C'0'                                                     
         BNL   SD2                                                              
         CLI   QDIST,C'0'                                                       
         BNL   SD2                                                              
*                                                                               
         CLC   SVREGDST,=C'999999'  CHECK FOR "LIVE" 999999 ASSIGNMENT          
         BE    SD2                  PREVENT PASSING THE INSERTION TWICE         
*                                                                               
         BAS   RE,TOSORT                                                        
         B     SD2                                                              
*                                                                               
SVREGDST DS    CL6                                                              
*****SVCLTOFF DS    CL1                                                         
SVCLTOFF DS    CL3             CLIENT OFFICE SAVE AREA                          
*                    1ST 2 BYTES=HEX VALUE OR SPACE, 1-CHARACTER CODE           
*                           BYTE 3 ALWAYS 1-CHARACTER CODE                      
         EJECT                                                                  
*                   SORT BUYS BY DISTRICT (OUTPUT) - CONTROL BREAKS             
         SPACE 3                                                                
SDOUTPUT DS    0H                                                               
         BAS   RE,FRSORT                                                        
         CLI   SRTREC,X'FF'                                                     
         BE    SDLAST                                                           
         MVC   KEY+27(4),SORTDISK                                               
         GOTO1 GETBUY                                                           
         MVC   KEY(25),PBUYKEY                                                  
         OC    WORK,WORK                                                        
         BNZ   SDOUT5                                                           
         CLI   SORTOFF,0          SEE IF OFFICE IN SORT                         
         BNE   SD25               YES - SEND OFFICE FIRST MODE                  
         B     SD26                                                             
*                                                                               
SDOUT5   CLC   0(3,R3),WORK        CHANGE OF OFFICE                             
         BE    SD18A0                                                           
         MVI   MODE,LBUYCNT                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPUB                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYVEN                                                     
         BAS   RE,GO                                                            
         CLI   SAVEQSRT,7                                                       
         BNE   *+12                                                             
         MVI   MODE,LBUYMKT                                                     
         BAS   RE,GO                                                            
         CLI   SAVEQSRT,8                 REP SORT                              
         BNE   *+12                                                             
         MVI   MODE,LBUYREP                                                     
         BAS   RE,GO                                                            
         CLI   SAVEQSRT,9                 PAY ADDR                              
         BNE   *+12                                                             
         MVI   MODE,LBUYREP                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYDST                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYREG                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYEST                                                     
         BAS   RE,GO                                                            
         BAS   RE,AFTR2                                                         
         MVI   MODE,LBUYDIV                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYCLI                                                     
         BAS   RE,GO                                                            
         MVI   MODE,OFCLAST                                                     
         BAS   RE,GO                                                            
         B     SD25                                                             
         SPACE 2                                                                
SD18A0   CLC   0(6,R3),WORK        CHANGE OF CLIENT                             
         BE    SD18A                                                            
         MVI   MODE,LBUYCNT                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPUB                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYVEN                                                     
         BAS   RE,GO                                                            
         CLI   SAVEQSRT,7                                                       
         BNE   *+12                                                             
         MVI   MODE,LBUYMKT                                                     
         BAS   RE,GO                                                            
         CLI   SAVEQSRT,8                 REP SORT                              
         BNE   *+12                                                             
         MVI   MODE,LBUYREP                                                     
         BAS   RE,GO                                                            
         CLI   SAVEQSRT,9                 PAY ADDR                              
         BNE   *+12                                                             
         MVI   MODE,LBUYREP                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYDST                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYREG                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYEST                                                     
         BAS   RE,GO                                                            
         BAS   RE,AFTR2                                                         
         MVI   MODE,LBUYDIV                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYCLI                                                     
         BAS   RE,GO                                                            
         B     SD26                                                             
         SPACE 2                                                                
SD18A    CLC   0(9,R3),WORK        CHANGE OF DIVISION                           
         BE    SD18B                                                            
         MVI   MODE,LBUYCNT                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPUB                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYVEN                                                     
         BAS   RE,GO                                                            
         CLI   SAVEQSRT,7                                                       
         BNE   *+12                                                             
         MVI   MODE,LBUYMKT                                                     
         BAS   RE,GO                                                            
         CLI   SAVEQSRT,8                 REP SORT                              
         BNE   *+12                                                             
         MVI   MODE,LBUYREP                                                     
         BAS   RE,GO                                                            
         CLI   SAVEQSRT,9                 PAY ADDR                              
         BNE   *+12                                                             
         MVI   MODE,LBUYREP                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYDST                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYREG                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYEST                                                     
         BAS   RE,GO                                                            
         BAS   RE,AFTR2                                                         
         MVI   MODE,LBUYDIV                                                     
         BAS   RE,GO                                                            
         B     SD28                                                             
         SPACE 2                                                                
SD18B    CLC   0(12,R3),WORK       CHANGE OF PRODUCT                            
         BE    SD18C                                                            
         MVI   MODE,LBUYCNT                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPUB                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYVEN                                                     
         BAS   RE,GO                                                            
         CLI   SAVEQSRT,7                                                       
         BNE   *+12                                                             
         MVI   MODE,LBUYMKT                                                     
         BAS   RE,GO                                                            
         CLI   SAVEQSRT,8                 REP SORT                              
         BNE   *+12                                                             
         MVI   MODE,LBUYREP                                                     
         BAS   RE,GO                                                            
         CLI   SAVEQSRT,9                 PAY ADDR                              
         BNE   *+12                                                             
         MVI   MODE,LBUYREP                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYDST                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYREG                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYEST                                                     
         BAS   RE,GO                                                            
         BAS   RE,AFTR2                                                         
         B     SD30                                                             
         SPACE 2                                                                
SD18C    CLC   0(14,R3),WORK       CHANGE OF ESTIMATE                           
         BE    SD20                                                             
         MVI   MODE,LBUYCNT                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPUB                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYVEN                                                     
         BAS   RE,GO                                                            
         CLI   SAVEQSRT,7                                                       
         BNE   *+12                                                             
         MVI   MODE,LBUYMKT                                                     
         BAS   RE,GO                                                            
         CLI   SAVEQSRT,8                 REP SORT                              
         BNE   *+12                                                             
         MVI   MODE,LBUYREP                                                     
         BAS   RE,GO                                                            
         CLI   SAVEQSRT,9                 PAY ADDR                              
         BNE   *+12                                                             
         MVI   MODE,LBUYREP                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYDST                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYREG                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYEST                                                     
         BAS   RE,GO                                                            
         B     SD32                                                             
         SPACE 2                                                                
SD20     CLC   0(19,R3),WORK       CHANGE OF REGION                             
         BE    SD22                                                             
         MVI   MODE,LBUYCNT                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPUB                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYVEN                                                     
         BAS   RE,GO                                                            
         CLI   SAVEQSRT,7                                                       
         BNE   *+12                                                             
         MVI   MODE,LBUYMKT                                                     
         BAS   RE,GO                                                            
         CLI   SAVEQSRT,8                 REP SORT                              
         BNE   *+12                                                             
         MVI   MODE,LBUYREP                                                     
         BAS   RE,GO                                                            
         CLI   SAVEQSRT,9                 PAY ADDR                              
         BNE   *+12                                                             
         MVI   MODE,LBUYREP                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYDST                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYREG                                                     
         BAS   RE,GO                                                            
         B     SD36                                                             
         SPACE 2                                                                
SD22     CLC   0(22,R3),WORK       CHANGE OF DISTRICT                           
         BE    SD23                                                             
         MVI   MODE,LBUYCNT                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPUB                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYVEN                                                     
         BAS   RE,GO                                                            
         CLI   SAVEQSRT,7                                                       
         BNE   *+12                                                             
         MVI   MODE,LBUYMKT                                                     
         BAS   RE,GO                                                            
         CLI   SAVEQSRT,8                 REP SORT                              
         BNE   *+12                                                             
         MVI   MODE,LBUYREP                                                     
         BAS   RE,GO                                                            
         CLI   SAVEQSRT,9                 PAY ADDR                              
         BNE   *+12                                                             
         MVI   MODE,LBUYREP                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYDST                                                     
         BAS   RE,GO                                                            
         B     SD40                                                             
         SPACE 2                                                                
SD23     DS    0H                                                               
         CLI   SAVEQSRT,7                                                       
         BNE   SD23D                                                            
         CLC   0(40,R3),WORK       CHANGE OF MKT/CITY                           
         BE    SD24                                                             
         MVI   MODE,LBUYCNT                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPUB                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYVEN                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYMKT                                                     
         BAS   RE,GO                                                            
         B     SD43                                                             
         SPACE 2                                                                
SD23D    CLI   SAVEQSRT,8          REP SORT                                     
         BE    SD23E                                                            
         CLI   SAVEQSRT,9          OR PAY ADDR SORT                             
         BNE   SD24                                                             
SD23E    CLC   0(34,R3),WORK       CHANGE OF REP/PAY OR PAYADDR                 
         BE    SD24                                                             
         MVI   MODE,LBUYCNT                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPUB                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYVEN                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYREP                                                     
         BAS   RE,GO                                                            
         B     SD43D                                                            
         SPACE 2                                                                
SD24     DS    0H                                                               
         CLC   PBUYKPUB(4),SVPUB   CHANGE OF VENDOR                             
         BE    SD24A                                                            
         MVI   MODE,LBUYCNT                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPUB                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYVEN                                                     
         BAS   RE,GO                                                            
         B     SD44                                                             
        SPACE 2                                                                 
SD24A    DS    0H                                                               
         CLC   PBUYKPUB(6),SVPUB   CHANGE OF PUB - (EDITION)                    
         BE    SD24A4                                                           
         MVI   MODE,LBUYCNT                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPUB                                                     
         BAS   RE,GO                                                            
         B     SD44                                                             
         SPACE 2                                                                
SD24A4   CLI   FCGTCONT,C'Y'                                                    
         BNE   SD50                                                             
*                                                                               
*NEW BELOW - TO ADDRESS PCONREC                                                 
         L     RF,PPWORK2C                                                      
         USING PPWORK2D,RF                                                      
         L     RE,ACONIO                                                        
         USING PCONREC,RE                                                       
*NEW ABOVE - TO ADDRESS PCONREC                                                 
*                                                                               
         CLC   KEY+16(3),PCONSTRT                                               
         BL    SD24B                                                            
         CLC   KEY+16(3),PCONEND                                                
         BNH   SD50                                                             
*                                                                               
         DROP  RE,RF                                                            
         SPACE 2                                                                
SD24B    MVI   MODE,LBUYCNT                                                     
         BAS   RE,GO                                                            
         B     SD46                                                             
         EJECT                                                                  
*                   SORT BUYS BY DISTRICT (OUTPUT) - FIRST TIMES                
         SPACE 3                                                                
AFTR2    ST    RE,AFTRPROD-4                                                    
         B     BUYX                                                             
         CNOP  0,4                                                              
         DS    F                                                                
         SPACE 2                                                                
AFTRPROD L     RE,AFTRPROD-4                                                    
         BR    RE                                                               
         SPACE 2                                                                
****SD25     MVC   RCSVOFC,SORTOFF                                              
SD25     MVC   RCSVOFC,SORTOFF+2                                                
         MVI   MODE,OFCFRST                                                     
         BAS   RE,GO                                                            
*                                                                               
SD26     XC    KEY,KEY                                                          
         MVC   KEY(7),PBUYKEY      GET A CLIENT                                 
         MVI   KEY+3,X'02'                                                      
         GOTO1 READ                                                             
         GOTO1 GETCLI                                                           
         MVC   RCSVCLI,KEY+4                                                    
*                                                                               
         MVC   PROFKEY,=CL12'P000'                                              
         MVC   PROFKEY+4(3),QAGENCY                                             
         MVC   PROFKEY+7(3),PCLTKCLT                                            
*                                                                               
         CLI   PCLTOFF,C' '                                                     
         BNH   *+14                                                             
         MVI   PROFKEY+10,C'*'                                                  
         MVC   PROFKEY+11(1),PCLTOFF                                            
*                                                                               
         GOTO1 GETPROF,DMCB,PROFKEY,SYSPROF,DATAMGR                             
*                                                                               
         MVC   PROFKEY+2(2),QPROG                                               
         GOTO1 (RF),(R1),,PROGPROF                                              
         MVI   MODE,FBUYCLI                                                     
         BAS   RE,GO                                                            
         MVI   MODE,C'S'                                                        
         SPACE 2                                                                
SD28     XC    KEY,KEY                                                          
         MVC   KEY(7),PBUYKEY      GET A DIVISION                               
         MVI   KEY+3,X'03'                                                      
         CLI   SORTDIV,0                                                        
         BZ    SD30                                                             
         MVC   KEY+7(3),SORTDIV                                                 
         GOTO1 HIGH                                                             
         GOTO1 GETDIV                                                           
         MVC   RCSVDIV,KEY+7                                                    
         MVI   MODE,FBUYDIV                                                     
         BAS   RE,GO                                                            
         SPACE 2                                                                
SD30     XC    KEY,KEY             GET A PRODUCT                                
         MVC   KEY(10),PBUYKEY                                                  
         CLI   SORTPROD,0                                                       
         BE    *+10                                                             
         MVC   KEY+7(3),SORTPROD                                                
         MVI   KEY+3,X'06'                                                      
         GOTO1 READ                                                             
         GOTO1 GETPROD                                                          
         MVC   RCSVPRD,KEY+7                                                    
         B     SRTEST              GO AND SEE IF EST/BILLS TO BE READ           
         SPACE 2                                                                
BFORPROD DS    0H                                                               
         OC    RQBILLDT,RQBILLDT      TEST BILLED TODAY FILTER                  
         BZ    SD31                                                             
         GOTO1 =A(FLTBILL),DMCB,(RC)  YES - RESTORE CHANGE DATE                 
SD31     DS    0H                                                               
         MVI   MODE,FBUYPRO                                                     
         BAS   RE,GO                                                            
         SPACE 2                                                                
SD32     XC    KEY,KEY             GET AN ESTIMATE                              
         MVC   KEY(10),PBUYKEY                                                  
         CLI   SORTPROD,0                                                       
         BE    *+10                                                             
         MVC   KEY+7(3),SORTPROD                                                
         MVI   KEY+3,X'07'                                                      
         MVC   KEY+10(2),PBUYKEY+19                                             
         GOTO1 READ                                                             
         GOTO1 GETEST                                                           
         BAS   RE,CHKES                                                         
*                                                                               
         MVI   MODE,FBUYEST                                                     
         BAS   RE,GO                                                            
         SPACE 2                                                                
SD36     CLI   FCGTREG,C'Y'                                                     
         BNE   SD38                                                             
         CLI   SORTREG,0                                                        
         BZ    SD38                                                             
         XC    KEY,KEY                                                          
         MVC   KEY(7),PBUYKEY                                                   
         MVI   KEY+3,X'04'                                                      
         MVC   KEY+7(3),PPRDDIV                                                 
         CLC   QPUB+1(3),=C'RD='   REQUESTED REG/DST OVERRIDE                   
         BNE   *+16                                                             
         MVC   KEY+4(3),QPUB+4                                                  
         MVC   KEY+7(3),=C'000'                                                 
         MVC   KEY+10(3),SORTREG                                                
         GOTO1 HIGH                                                             
         GOTO1 GETREG                                                           
         SPACE 2                                                                
SD38     MVI   MODE,FBUYREG                                                     
         BAS   RE,GO                                                            
         SPACE 2                                                                
SD40     CLI   FCGTDIST,C'Y'                                                    
         BNE   SD42                                                             
         CLI   SORTDIST,0                                                       
         BZ    SD42                                                             
         XC    KEY,KEY             GET A DISTRICT                               
         MVC   KEY(7),PBUYKEY                                                   
         MVI   KEY+3,X'05'                                                      
         MVC   KEY+7(3),PPRDDIV                                                 
         CLC   QPUB+1(3),=C'RD='   REQUESTED REG/DST OVERRIDE                   
         BNE   *+16                                                             
         MVC   KEY+4(3),QPUB+4                                                  
         MVC   KEY+7(3),=C'000'                                                 
         MVC   KEY+10(3),SORTREG                                                
         MVC   KEY+13(3),SORTDIST                                               
         GOTO1 HIGH                                                             
         GOTO1 GETDIST                                                          
         SPACE 2                                                                
SD42     MVI   MODE,FBUYDST                                                     
         BAS   RE,GO                                                            
         SPACE 2                                                                
SD43     DS    0H                                                               
         MVI   MODE,FBUYMKT                                                     
         BAS   RE,GO                                                            
         SPACE 2                                                                
SD43D    DS    0H                                                               
         MVI   MODE,FBUYREP                                                     
         BAS   RE,GO                                                            
         SPACE 2                                                                
SD44     DS    0H                                                               
         MVC   SVPUB,PBUYKPUB                                                   
         OC    SORTPDA,SORTPDA                                                  
         BZ    SD44B                                                            
         MVC   KEY+27(4),SORTPDA   HAVE PUB DA                                  
         GOTO1 GETNAME                                                          
         OC    PUBNAME(40),SPACES                                               
         L     RF,ALTLREC                                                       
         XC    0(50,RF),0(RF)                                                   
         OC    SORTLDA,SORTLDA                                                  
         BZ    SD44C                                                            
         MVC   KEY+27(4),SORTLDA   HAVE LTL DA                                  
         GOTO1 GETLTL                                                           
         B     SD44C                                                            
SD44B    DS    0H                                                               
         MVC   KEY,PBUYKEY                                                      
         BAS   RE,GETPUB                                                        
SD44C    DS    0H                                                               
         BAS   RE,READREP                                                       
         MVI   MODE,FBUYPUB                                                     
         BAS   RE,GO                                                            
         SPACE 2                                                                
SD46     DS    0H                                                               
         CLI   FCGTCONT,C'Y'                                                    
         BNE   SD50                                                             
         MVC   KEY,PBUYKEY                                                      
         BAS   RE,READCONT                                                      
         MVI   MODE,FBUYCNT                                                     
         BAS   RE,GO                                                            
         SPACE 2                                                                
SD50     MVI   MODE,PROCBUY                                                     
         MVC   KEY,PBUYKEY                                                      
         MVC   KEY+27(4),SORTDISK     BUY DISK ADDR                             
         LA    R4,PPRDKPRD                                                      
         CLI   SORTPROD,0                                                       
         BNE   *+8                                                              
         LA    R4,PBUYKPRD                                                      
         GOTO1 GETINS,DMCB,PBUYKEY,GROSS,(FCOAPRD,(R4))                         
         OC    SORTREG(6),SORTREG                                               
         BZ    SD50B                                                            
         GOTO1 =A(RDSPLIT),(R1),SORTREG                                         
*                                                                               
SD50B    DS    0H                                                               
         CLI   FCGTJOB,C'N'                                                     
         BE    *+8                                                              
         BAS   RE,READJOB                                                       
         BAS   RE,GO                                                            
         MVC   WORK,0(R3)                                                       
         B     SDOUTPUT                                                         
         EJECT                                                                  
*                   SORT BUYS BY DISTRICT (LAST)                                
         SPACE 3                                                                
SDLAST   DS    0H                                                               
         MVI   KEY,X'FF'           SET KEY TO EOF                               
         MVC   KEY+1(24),KEY                                                    
         MVI   MODE,LBUYCNT                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPUB                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYVEN                                                     
         BAS   RE,GO                                                            
         CLI   SAVEQSRT,7                                                       
         BNE   *+12                                                             
         MVI   MODE,LBUYMKT                                                     
         BAS   RE,GO                                                            
         CLI   SAVEQSRT,8         REP SORT                                      
         BNE   *+12                                                             
         MVI   MODE,LBUYREP                                                     
         BAS   RE,GO                                                            
         CLI   SAVEQSRT,9         PAY ADDR SORT                                 
         BNE   *+12                                                             
         MVI   MODE,LBUYREP                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYDST                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYREG                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYEST                                                     
         BAS   RE,GO                                                            
         BAS   RE,AFTR2                                                         
         MVI   MODE,LBUYDIV                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYCLI                                                     
         BAS   RE,GO                                                            
         CLI   QCLIENT,C'$'                                                     
         BNE   LASTREQ                                                          
         MVI   MODE,OFCLAST                                                     
         BAS   RE,GO                                                            
         B     LASTREQ                                                          
*                                                                               
         DROP  R2,R3                                                            
         EJECT                                                                  
*                   SORT BUYS BY VENDOR (CONTROL)                               
         SPACE 3                                                                
SORTBYCN DS    0H                                                               
         XC    SRTCOUNT,SRTCOUNT        CLEAR SORT TABLE                        
*****    MVI   SVCLTOFF,0                                                       
         XC    SVCLTOFF,SVCLTOFF                                                
         LA    R3,SRTREC                                                        
         MVI   FCGTPUB,C'B'        NEED ONLY BIG PUBREC                         
*        SEE IF OFFICE LIST REQUEST (QCLIENT=$X)                                
         CLI   QCLIENT,C'$'                                                     
         BNE   SC0                                                              
         GOTO1 =A(BLDOFC),DMCB,(RC)                                             
         L     R4,=V(OFCLIST)                                                   
         OC    0(6,R4),0(R4)        NO CLIENTS FOUND                            
         BZ    LASTREQ                                                          
*                                  IF SPECIFIC CLT/PRD/EST                      
*                                  READ HEADERS HOW                             
SC0      DS    0H                                                               
         CLC   QCLIENT,=C'ALL'                                                  
         BE    SC1                                                              
         CLI   QCLIENT,C'*'             OFFICE REQUEST                          
         BE    SC1                                                              
         CLI   QCLIENT,C'$'             OFFICE LIST                             
         BE    SC1                                                              
         CLI   QCLIENT,C'&&'            CLIENT GROUP                            
         BE    SC1                                                              
*                                                                               
         CLC   QPRODUCT,=C'ALL'                                                 
         BE    SC1                                                              
         CLI   QPRODUCT,C' '                                                    
         BE    SC1                                                              
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY(3),QAGENCY                                                   
         MVI   KEY+3,X'06'                                                      
         MVC   KEY+4(3),QCLIENT                                                 
         MVC   KEY+7(3),QPRODUCT                                                
         GOTO1 HIGH                                                             
         CLC   KEY(25),KEYSAVE                                                  
         BNE   NEXTREQ                                                          
         GOTO1 GETPROD                                                          
*                                                                               
         CLI   QEST,C'0'                                                        
         BL    SC1                                                              
         CLI   QESTEND,C' '                                                     
         BH    SC1                                                              
*                                                                               
         MVI   KEY+3,X'07'                                                      
         PACK  DUB,QEST                                                         
         CVB   R0,DUB                                                           
         STH   R0,HALF                                                          
         MVC   KEY+10(2),HALF                                                   
         GOTO1 HIGH                                                             
         CLC   KEY(25),KEYSAVE                                                  
         BNE   NEXTREQ                                                          
         GOTO1 GETEST                                                           
*                                                                               
SC1      DS    0H                                                               
         EJECT                                                                  
*                   SORT BUYS BY VENDOR (INPUT)                                 
         SPACE 3                                                                
         XC    KEY,KEY                                                          
         MVC   KEY(3),QAGENCY                                                   
         MVI   KEY+3,X'20'                                                      
         CLC   QCLIENT,=C'ALL'                                                  
         BE    SC1C                                                             
         CLI   QCLIENT,C'*'                                                     
         BE    SC1C                                                             
         CLI   QCLIENT,C'$'         OFFICE LIST                                 
         BE    SC1C                                                             
         CLI   QCLIENT,C'&&'        CLIENT GROUP                                
         BE    *+10                                                             
         MVC   KEY+4(3),QCLIENT                                                 
SC1C     CLC   QPRODUCT,=C'ALL'                                                 
         BE    *+10                                                             
         MVC   KEY+7(3),QPRODUCT                                                
         XC    SVPUB,SVPUB                                                      
         CLI   QPUB,C'0'                                                        
         BL    SC2                                                              
         MVC   WORK(11),QPUB                                                    
         CLC   QPUB+8(3),=C'ZZZ'                                                
         BNE   *+10                                                             
         MVC   WORK+8(3),SPACES                                                 
         GOTO1 PUBVAL,DMCB,WORK,SVPUB                                           
         MVC   KEY+10(6),SVPUB                                                  
         SPACE 2                                                                
SC2      GOTO1 HIGH                                                             
         B     SC6                                                              
         SPACE 2                                                                
SC4      GOTO1 SEQKSV                                                           
         SPACE 2                                                                
SC6      CLC   KEYSAVE(4),KEY      CHECK FOR END OF READING                     
         BNE   SC8                                                              
         CLC   KEYSAVE(7),KEY      CHK FOR NEW CLIENT                           
         BE    SC6A5                                                            
         L     RF,=A(FECOUNT)    MUST REBUILD FELIST FOR EACH CLIENT            
         XC    0(4,RF),0(RF)                                                    
*                                                                               
SC6A5    CLC   QCLIENT,=C'ALL'                                                  
         BE    SC7                                                              
         CLI   QCLIENT,C'&&'       BILLING GROUP                                
         BE    SC6A8                                                            
         CLI   QCLIENT,C'*'        OFFICE                                       
         BE    SC6A8                                                            
         CLI   QCLIENT,C'$'        OFFICE LIST                                  
         BNE   SC6D                                                             
*                                                                               
         L     R4,=V(OFCLIST)                                                   
*                                                                               
SCB14    CLC   KEY+4(3),3(R4)      TRY TO FIND CLIENT IN TABLE                  
         BE    SCB14X              FOUND - PROCESS                              
         LA    R4,6(R4)            NEXT CLIENT                                  
         CLI   0(R4),0                                                          
         BNE   SCB14                                                            
         B     SC6B                SKIP TO NEXT CLIENT                          
*                                                                               
SCB14X   MVC   SVCLTOFF,0(R4)      SAVE OFFICE CODE                             
         B     SC7                                                              
*                                                                               
SC6A8    BAS   RE,TSTCLI                                                        
         BE    SC7                                                              
SC6B     MVI   KEY+7,X'FF'                                                      
         B     SC2                 BUMP TO NEXT CLI                             
SC6D     DS    0H                                                               
         CLC   KEYSAVE(7),KEY                                                   
         BNE   SC8                                                              
*                                                                               
SC7      DS    0H                                                               
         CLI   SAVEQSRT,8           FOR REP OR ADDR SORT                        
         BE    SC7A                 MUST ALWAYS READ CLIENT                     
         CLI   SAVEQSRT,9           ON INPUT - NEEDED TO FIND REPS              
         BE    SC7A                                                             
         B     SC7A5                                                            
SC7A     BAS   RE,TSTCLI                                                        
SC7A5    CLC   QPRODUCT,=C'ALL'                                                 
         BE    SC7C                                                             
         CLI   QPRODUCT,C' '                                                    
         BE    SC7C                                                             
         CLC   KEY+7(3),QPRODUCT                                                
         BE    SC7C                                                             
         CLC   QCLIENT,=C'ALL'     IF DOING ALL CLTS OR OFFICE                  
         BE    SC7B                SEARCH OTHER CLIENTS                         
         CLI   QCLIENT,C'*'        OFFICE                                       
         BE    SC7B                                                             
         CLI   QCLIENT,C'$'        OFFICE LIST                                  
         BE    SC7B                                                             
         CLI   QCLIENT,C'&&'        CLIENT GROUP                                
         BE    SC7B                                                             
         B     SC8                                                              
*                                                                               
SC7B     CLC   KEY+7(3),QPRODUCT                                                
         BH    SC6B                PAST MY PRD SKIP TO NEXT CLIENT              
         XC    KEY+10(22),KEY+10                                                
         B     SC1C                                                             
*                                                                               
SC7C     DS    0H                                                               
         B     SC10                                                             
         SPACE 2                                                                
*****SC8      XC    WORK(36),WORK                                               
SC8      XC    WORK(38),WORK                                                    
         B     SCOUTPUT                                                         
         SPACE 2                                                                
SC10     EQU   *                                                                
         OC    SVPUB(6),SVPUB                                                   
         BZ    SC10B                                                            
         CLC   QPUB+8(3),=C'ZZZ'   SEE IF DOING ALL ZONES + EDTS                
         BNE   SC10A0                                                           
         CLC   KEY+10(4),SVPUB   TEST RIGHT BASE PUB                            
         B     SC10A5                                                           
SC10A0   CLC   KEY+10(6),SVPUB   TEST RIGHT PUB                                 
SC10A5   BE    SC10B                                                            
         BH    SC10A                                                            
         MVC   KEY+10(6),SVPUB     BUMP TO THIS PUB                             
         MVI   KEY+16,0                                                         
         B     SC2                                                              
SC10A    DS    0H                                                               
         MVI   KEY+10,X'FF'        BUMP TO NEXT PRD                             
         B     SC2                                                              
SC10B    DS    0H                                                               
         OC    KEY+21(3),KEY+21    TEST PASSIVE                                 
         BNZ   SC4                 YES - BYPASS                                 
         MVI   MODE,C'S'           SKIP READ                                    
         BAS   RE,FILTER                                                        
         CLI   BYTE,X'FF'          SEE IF PASSES                                
         BE    SC6                 NO                                           
         XC    SRTREC,SRTREC                                                    
*****                            R3 IS POINTING TO SRTREC                       
         MVC   6(6,R3),KEY+10                                                   
         CLI   SAVEQSRT,2          SORT BY PUB NUM                              
         BE    SC12                                                             
*                                  SORT BY PUB NAME                             
         BAS   RE,GETPUB                                                        
         CLI   SAVEQSRT,6                                                       
         BNE   SC10D                                                            
         MVC   6(17,R3),PUBNAME                                                 
         CLC   PUBNAME(4),=C'THE '                                              
         BNE   *+10                                                             
         MVC   6(17,R3),PUBNAME+4                                               
         MVC   23(4,R3),PUBSRTDA                                                
         B     SC12                                                             
*                                                                               
SC10D    DS    0H                                                               
         CLI   SAVEQSRT,7                                                       
         BNE   SC11                                                             
*                                                                               
         MVC   6(2,R3),PUBSTACD    USE STACD                                    
         CLI   PUBSTACD,C' '       IF THERE                                     
         BNH   SC10D2                                                           
         CLI   PUBSTACD,C'0'       UNLESS NUMERIC                               
         BL    *+10                                                             
SC10D2   DS    0H                                                               
         MVC   6(2,R3),PUBSTATE    ELSE USE STATE                               
         MVC   8(12,R3),PUBZNAME   FRO OUTDOOR USE ZONE                         
         CLI   QMEDIA,C'O'                                                      
         BE    *+10                                                             
         MVC   8(12,R3),PUBCITY                                                 
         MVC   23(4,R3),PUBSRTDA                                                
         B     SC12                                                             
*                                                                               
SC11     CLI   SAVEQSRT,8          PAY REP SORT                                 
         BL    SC12                                                             
         CLI   SAVEQSRT,9          PAY ADDR NAME SORT                           
         BH    SC12                                                             
         MVC   23(4,R3),PUBSRTDA   SAVE PUB DISK ADDR                           
         MVC   17(6,R3),6(R3)       MOVE PUB NUMBER                             
         XC    6(6,R3),6(R3)       CLEAR                                        
         MVI   6(R3),X'FF'         SO 'NO REP' WILL SORT LAST                   
         BAS   RE,READREPI         READ REPS ON INPUT TO SORT                   
         CLI   PREPNAME,C' '       SEE IF I FOUND REP OR PAY ADDR               
         BH    SC11A               NO                                           
         CLI   SAVEQSRT,9          SEE IF DOING PAY ADDR NAME SORT              
         BNE   SC12                NO                                           
         CLC   QPROG,=C'NV'                                                     
         BE    SC11A0                                                           
         MVC   7(16,R3),PUBNAME    SO PAY PUB DIRECT IN PUBNAME ORDER           
         CLC   PUBNAME(4),=C'THE '                                              
         BNE   *+10                                                             
         MVC   7(16,R3),PUBNAME+4                                               
         B     SC12                                                             
*                                                                               
SC11A0   MVC   6(17,R3),PUBNAME    SO PAY PUB DIRECT IN PUBNAME ORDER           
         CLC   PUBNAME(4),=C'THE '                                              
         BNE   SC12                                                             
         MVC   6(16,R3),PUBNAME+4                                               
         MVI   22(R3),0                                                         
         B     SC12                                                             
*                                                                               
SC11A    CLI   SAVEQSRT,9          SEE IF DOING PAY ADDR NAME SORT              
         BNE   SC11B                                                            
         MVC   6(11,R3),PREPNAME                                                
*                                  PUB NUMBER WILL STILL BE IN 14(R3)           
*                                  SO PUBS WITH SAME REP OR ADDR                
*                                  WILL BE SORTED BY PUB NUMBER                 
         B     SC12                                                             
*                                                                               
SC11B    CLI   PREPKEY+3,X'11'     SEE IF I FOUND A REP                         
         BNE   SC11C                                                            
         MVC   6(5,R3),PREPKREP    REP CODE                                     
         B     SC12                                                             
SC11C    MVI   6(R3),X'FE'         SO PAY ADDRS WILL SORT                       
         MVC   7(11,R3),PREPNAME                                                
*                                  BEFORE ** PAY PUB DIRECT **                  
*                                  BUT AFTER REPS                               
*                                  BOTH WILL BE SORTED BY PUB NUMBER            
*                                                                               
SC12     DS    0H                                                               
         CLI   CLTPUBSW,C'C'       TEST SORT ON CLIENT FIRST                    
         BNE   SC12D                                                            
         MVC   3(3,R3),KEY+4                                                    
*****    MVC   0(1,R3),SVCLTOFF                                                 
         MVC   0(3,R3),SVCLTOFF                                                 
*                                                                               
SC12D    MVC   27(6,R3),KEY+4    CLT + PRD                                      
         MVC   33(3,R3),KEY+16   DATE                                           
         MVC   36(1,R3),KEY+24   LIN                                            
         MVC   38(4,R3),KEY+27     DA                                           
         BAS   RE,TOSORT                                                        
         B     SC4                                                              
         EJECT                                                                  
*                   SORT BUYS BY VENDOR (OUTPUT) - CONTROL BREAKS               
         SPACE 3                                                                
SCOUTPUT EQU   *                                                                
         BAS   RE,FRSORT                                                        
         CLI   SRTREC,X'FF'                                                     
         BE    SCLAST                                                           
         MVC   KEY+27(4),38(R3)                                                 
         GOTO1 GETBUY                                                           
         GOTO1 GETINS,DMCB,PBUYKEY,GROSS,(FCOAPRD,PBUYKPRD)                     
         MVC   KEY(25),PBUYKEY                                                  
*****    OC    WORK(36),WORK                                                    
         OC    WORK(38),WORK                                                    
         BNZ   SC17                                                             
         CLI   0(R3),0             SEE IF OFFICE IN SORT                        
         BE    SC16                                                             
         MVC   RCSVOFC,2(R3)                                                    
         MVI   MODE,OFCFRST                                                     
         BAS   RE,GO                                                            
         B     SC31                                                             
*                                                                               
SC16     DS    0H                                                               
         B     SC30                                                             
*                                                                               
         SPACE 2                                                                
SC17     CLC   0(3,R3),WORK        CHANGE OF OFFICE (HIGH)                      
         BE    SC18                                                             
         MVI   MODE,LBUYPRO                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPUB                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYVEN                                                     
         BAS   RE,GO                                                            
         CLI   SAVEQSRT,8         SEE IF DOING REP SORT                         
         BNE   *+12                                                             
         MVI   MODE,LBUYREP                                                     
         BAS   RE,GO                                                            
         CLI   SAVEQSRT,9         OR PAYING ADDR SORT                           
         BNE   *+12                                                             
         MVI   MODE,LBUYREP                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYCLI                                                     
         BAS   RE,GO                                                            
         MVI   MODE,OFCLAST                                                     
         BAS   RE,GO                                                            
         MVC   RCSVOFC,2(R3)                                                    
         MVI   MODE,OFCFRST                                                     
         BAS   RE,GO                                                            
         B     SC31                                                             
*                                                                               
SC18     DS    0H                                                               
         CLC   0(6,R3),WORK        CHANGE OF CLT (HIGH)                         
         BE    SC19                                                             
         MVI   MODE,LBUYPRO                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPUB                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYVEN                                                     
         BAS   RE,GO                                                            
         CLI   SAVEQSRT,8         SEE IF DOING REP SORT                         
         BNE   *+12                                                             
         MVI   MODE,LBUYREP                                                     
         BAS   RE,GO                                                            
         CLI   SAVEQSRT,9         OR PAYING ADDR SORT                           
         BNE   *+12                                                             
         MVI   MODE,LBUYREP                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYCLI                                                     
         BAS   RE,GO                                                            
         B     SC31                                                             
SC19     DS    0H                                                               
         CLI   SAVEQSRT,8                                                       
         BE    SC19D                                                            
         CLI   SAVEQSRT,9                                                       
         BNE   SC19H                                                            
SC19D    CLC   0(17,R3),WORK       CHANGE OF REP OR ADDR                        
         BE    SC19H                                                            
         MVI   MODE,LBUYPRO                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPUB                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYVEN                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYREP                                                     
         BAS   RE,GO                                                            
         B     SC30D                                                            
*                                                                               
SC19H    CLC   0(25,R3),WORK       CHANGE OF VENDOR                             
         BE    SC20                                                             
         MVI   MODE,LBUYPRO                                                     
         BAS   RE,GO                                                            
         CLI   CLTPUBSW,C'C'                                                    
         BE    *+12                                                             
         MVI   MODE,LBUYCLI                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPUB                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYVEN                                                     
         BAS   RE,GO                                                            
         B     SC32                                                             
SC20     EQU   *                                                                
         CLC   0(27,R3),WORK       CHANGE OF PUB                                
         BE    SC21                                                             
         MVI   MODE,LBUYPRO                                                     
         BAS   RE,GO                                                            
         CLI   CLTPUBSW,C'C'                                                    
         BE    *+12                                                             
         MVI   MODE,LBUYCLI                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPUB                                                     
         BAS   RE,GO                                                            
         B     SC32                                                             
         SPACE 2                                                                
SC21     EQU   *                                                                
         CLC   0(30,R3),WORK       CHANGE OF CLT                                
         BE    SC22                                                             
         CLI   CLTPUBSW,C'C'                                                    
         BE    SC22                                                             
         MVI   MODE,LBUYPRO                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYCLI                                                     
         BAS   RE,GO                                                            
         B     SC36                                                             
         SPACE 2                                                                
SC22     DS    0H                                                               
         CLC   0(33,R3),WORK       CHANGE OF PRODUCT                            
         BE    SC44                                                             
         MVI   MODE,LBUYPRO                                                     
         BAS   RE,GO                                                            
         B     SC40                                                             
         EJECT                                                                  
*                   SORT BUYS BY VENDOR (OUTPUT) - FIRST TIME                   
         SPACE 3                                                                
SC30     EQU   *                                                                
SC30D    CLI   SAVEQSRT,8        REP OR PAYADDR SORT                            
         BE    SC30E                                                            
         CLI   SAVEQSRT,9                                                       
         BNE   SC31                                                             
SC30E    MVI   MODE,FBUYREP      FOR REP AND PAYADDR SORTS                      
         BAS   RE,GO                                                            
*                                                                               
SC31     EQU   *                                                                
         CLI   CLTPUBSW,C'C'                                                    
         BNE   SC32                                                             
         XC    KEY,KEY                                                          
*                                  GET CLIENT RECORD                            
         MVC   KEY(7),PBUYKEY                                                   
         MVI   KEY+3,X'02'                                                      
         GOTO1 READ                                                             
         GOTO1 GETCLI                                                           
*                                                                               
         MVC   PROFKEY,=CL12'P000'                                              
         MVC   PROFKEY+4(3),QAGENCY                                             
         MVC   PROFKEY+7(3),PCLTKCLT                                            
         CLI   PCLTOFF,C' '                                                     
         BNH   *+14                                                             
         MVI   PROFKEY+10,C'*'                                                  
         MVC   PROFKEY+11(1),PCLTOFF                                            
         GOTO1 GETPROF,DMCB,PROFKEY,SYSPROF,DATAMGR                             
         MVC   PROFKEY+2(2),QPROG                                               
         GOTO1 (RF),(R1),,PROGPROF                                              
         MVI   MODE,FBUYCLI                                                     
         BAS   RE,GO                                                            
         MVC   KEY(25),PBUYKEY      MUST RESTORE FOR GETPUB                     
*                                                                               
SC32     EQU   *                                                                
         OC    23(4,R3),23(R3)                                                  
         BZ    SC32B                                                            
         MVC   KEY+27(4),23(R3)    HAVE PUB DA                                  
         GOTO1 GETNAME                                                          
         B     SC34                                                             
SC32B    DS    0H                                                               
         BAS   RE,GETPUB                                                        
         SPACE 2                                                                
SC34     DS    0H                                                               
         CLI   SAVEQSRT,8          FOR REP                                      
         BE    SC34B                                                            
         CLI   SAVEQSRT,9          OR PAY ADDRESS                               
         BE    SC34B               MUST GET CLIENT                              
         B     SC34D                                                            
*                                                                               
SC34B    CLI   CLTPUBSW,C'P'       ONLY IF CLTPUBSW = P                         
         BNE   SC34D                                                            
         XC    KEY,KEY                                                          
*                                  GET CLIENT RECORD                            
         MVC   KEY(7),PBUYKEY      TO INSURE PROPER REP/ADDRESS                 
         MVI   KEY+3,X'02'                                                      
         GOTO1 READ                                                             
         GOTO1 GETCLI                                                           
         MVC   KEY(25),PBUYKEY                                                  
*                                                                               
SC34D    DS    0H                                                               
         BAS   RE,READREP                                                       
         MVI   MODE,FBUYPUB                                                     
         BAS   RE,GO                                                            
         SPACE 2                                                                
SC36     DS    0H                  FIRST FOR CLIENT                             
         CLI   CLTPUBSW,C'C'                                                    
         BE    *+12                                                             
         MVI   MODE,FBUYCLI                                                     
         BAS   RE,GO                                                            
         SPACE 2                                                                
SC40     DS    0H                  FIRST BUY FOR PRODUCT                        
         OC    RQBILLDT,RQBILLDT                                                
         BZ    SC42                                                             
         GOTO1 =A(FLTBILL),DMCB,(RC) RESET CHANGE CONTROL DATE                  
SC42     DS    0H                                                               
         MVI   MODE,FBUYPRO                                                     
         BAS   RE,GO                                                            
         SPACE 2                                                                
SC44     DS    0H                                                               
         CLI   FCGTJOB,C'N'                                                     
         BE    *+8                                                              
         BAS   RE,READJOB                                                       
         MVI   MODE,PROCBUY                                                     
         BAS   RE,GO                                                            
         MVC   WORK,0(R3)                                                       
         B     SCOUTPUT                                                         
         EJECT                                                                  
*                   SORT BUYS BY VENDOR (LAST TIME)                             
         SPACE 3                                                                
SCLAST   MVI   MODE,LBUYPRO                                                     
         BAS   RE,GO                                                            
         CLI   CLTPUBSW,C'C'                                                    
         BE    *+12                                                             
         MVI   MODE,LBUYCLI                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYPUB                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LBUYVEN                                                     
         BAS   RE,GO                                                            
         CLI   SAVEQSRT,8         REP SORT                                      
         BNE   *+12                                                             
         MVI   MODE,LBUYREP                                                     
         BAS   RE,GO                                                            
         CLI   SAVEQSRT,9         PAY ADDR SORT                                 
         BNE   *+12                                                             
         MVI   MODE,LBUYREP                                                     
         BAS   RE,GO                                                            
         CLI   CLTPUBSW,C'C'                                                    
         BNE   LASTREQ                                                          
         MVI   MODE,LBUYCLI                                                     
         BAS   RE,GO                                                            
         CLI   QCLIENT,C'$'       SEE I DOING OFFICE LIST                       
         BNE   LASTREQ                                                          
         MVI   MODE,OFCLAST                                                     
         BAS   RE,GO                                                            
         B     LASTREQ                                                          
         SPACE 2                                                                
         EJECT                                                                  
*                   SORT PUBLICATIONS CONTROL                                   
         SPACE 3                                                                
SORTPUBS DS    0H                                                               
         XC    SRTCOUNT,SRTCOUNT        CLEAR SORT TABLE                        
         LA    R3,SRTREC                                                        
         MVI   FCGTPUB,C'Y'                                                     
         CLC   QPROG,=C'48'        SEE IF DOING PUBLIST                         
         BNE   SP1                                                              
         CLI   QOPT1,C'D'          SEE IF SHOWING CLT/DIV/REG/DST               
         BE    SP1B                YES NEED LTLREC AND BIG                      
         CLI   QMEDIA,C'N'         OR NEWS AND PROD                             
         BNE   SP1                                                              
         CLI   QOPT1,C'P'                                                       
         BE    SP1B             LEAVE SET TO READ BIG AND LITTLE RECS           
*                                                                               
SP1      MVI   FCGTPUB,C'B'        NEED NAME REC                                
         CLI   SAVEQSRT,4          UNLESS REG/DST SORT                          
         BNE   *+8                                                              
         MVI   FCGTPUB,C'L'        THEN NEED ONLY LTL REC                       
*                                                                               
*                                                                               
SP1B     XC    PUBKEY,PUBKEY                                                    
SP2      DS    0H                                                               
         GOTO1 =A(NXTPUB)                                                       
         CLI   PUBKEY,X'FF'        END                                          
         BNE   SP4                                                              
*                                                                               
         XC    KEY,KEY                                                          
         B     SPOUTPUT                                                         
*                                                                               
SP4      DS    0H                                                               
         CLI   SAVEQSRT,4                                                       
         BE    SP16                                                             
*                                                                               
         MVC   20(4,R3),LTLSRTDA                                                
         MVC   25(4,R3),PUBSRTDA                                                
         MVC   14(6,R3),PUBKPUB                                                 
*                                                                               
         CLI   SAVEQSRT,3                                                       
         BNE   SP6                                                              
*                                       NAME SORT                               
         MVC   0(14,R3),PUBNAME                                                 
         CLC   PUBNAME(4),=C'THE '                                              
         BNE   *+10                                                             
         MVC   0(14,R3),PUBNAME+4                                               
         B     SP8                                                              
*                                                                               
*                                  MARKET SORT                                  
SP6      DS    0H                                                               
         MVC   0(2,R3),PUBSTACD    USE STACD                                    
         CLI   PUBSTACD,C' '       IF OK                                        
         BNH   SP6D                                                             
         CLI   PUBSTACD,C'0'       NUMERIC NOT OK                               
         BL    *+10                                                             
SP6D     DS    0H                                                               
         MVC   0(2,R3),PUBSTATE    ELSE USE STATE                               
         MVC   2(12,R3),PUBZNAME   FOR OUTDOOR USE ZONE                         
         CLI   QMEDIA,C'O'                                                      
         BE    *+10                                                             
         MVC   2(12,R3),PUBCITY    ELSE USE CITY                                
*                                                                               
SP8      DS    0H                                                               
         BAS   RE,TOSORT                                                        
         B     SP2                                                              
*                                  REG/DST SORT                                 
SP16     DS    0H                                                               
         L     R2,ALTLREC                                                       
         LA    R2,33(R2)                                                        
SP16B    DS    0H                                                               
         CLI   0(R2),0             EOR                                          
         BE    SP20T                                                            
         CLI   0(R2),X'71'                                                      
         BE    SP20                                                             
SP17     DS    0H                                                               
         ZIC   R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     SP16B                                                            
*                                                                               
SP20     DS    0H                                                               
         USING PUBDSTEL,R2                                                      
         CLC   QCLIENT,SPACES                                                   
         BE    SP20A                                                            
         CLC   QCLIENT,=C'ALL'                                                  
         BE    SP20A                                                            
         CLC   QCLIENT,PUBDCLT                                                  
         BNE   SP17                                                             
SP20A    CLC   QDIV,SPACES                                                      
         BE    SP20B                                                            
         CLC   QDIV,=C'ALL'                                                     
         BE    SP20B                                                            
         CLC   QDIV,PUBDDIV                                                     
         BNE   SP17                                                             
SP20B    CLC   QREGION,SPACES                                                   
         BE    SP20C                                                            
         CLC   QREGION,=C'ALL'                                                  
         BE    SP20C                                                            
         CLC   QREGION,PUBDREG                                                  
         BNE   SP17                                                             
SP20C    CLC   QDIST,SPACES                                                     
         BE    SP20D                                                            
         CLC   QDIST,=C'ALL'                                                    
         BE    SP20D                                                            
         CLC   QDIST,PUBDDST                                                    
         BNE   SP17                                                             
SP20D    DS    0H                                                               
         MVC   0(12,R3),PUBDCLT                                                 
         MVC   12(6,R3),PUBKPUB                                                 
         XC    18(2,R3),18(R3)                                                  
         MVC   20(4,R3),LTLSRTDA                                                
         MVC   25(4,R3),PUBSRTDA                                                
*                                                                               
         BAS   RE,TOSORT                                                        
         B     SP17                                                             
*                                                                               
SP20T    DS    0H                                                               
         B     SP2                 NEXT PUB                                     
         EJECT                                                                  
*                   SORT PUBLICATIONS (OUTPUT) - CONTROL BREAKS                 
         SPACE 3                                                                
SPOUTPUT DS    0H                                                               
         BAS   RE,FRSORT                                                        
         CLI   SRTREC,X'FF'                                                     
         BE    SPLAST                                                           
         OC    KEY,KEY             FIRST TIME                                   
         BZ    SP28                                                             
         CLC   QSORT,=C'04'                                                     
         BNE   SP50                                                             
         CLC   KEY(3),0(R3)        CHANGE OF CLIENT                             
         BE    SP22                                                             
         MVI   MODE,LPUBDST                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LPUBREG                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LPUBDIV                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LPUBCLI                                                     
         BAS   RE,GO                                                            
         B     SP30                                                             
         SPACE 2                                                                
SP22     CLC   KEY(6),0(R3)        CHANGE OF DIVISION                           
         BE    SP24                                                             
         MVI   MODE,LPUBDST                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LPUBREG                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LPUBDIV                                                     
         BAS   RE,GO                                                            
         B     SP34                                                             
         SPACE 2                                                                
SP24     CLC   KEY(9),0(R3)        CHANGE OF REGION                             
         BE    SP26                                                             
         MVI   MODE,LPUBDST                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LPUBREG                                                     
         BAS   RE,GO                                                            
         B     SP38                                                             
         SPACE 2                                                                
SP26     CLC   KEY(12),0(R3)       CHANGE OF DISTRICT                           
         BE    SP50                                                             
         MVI   MODE,LPUBDST                                                     
         BAS   RE,GO                                                            
         B     SP42                                                             
         EJECT                                                                  
*                   SORT PUBLICATIONS (OUTPUT) - FIRST TIME                     
         SPACE 3                                                                
SP28     DS    0H                  FIRST PUB FOR REQUEST                        
         CLC   QSORT,=C'04'        SEE IF DIV/REG/DST SORT                      
         BNE   SP50                                                             
         SPACE 2                                                                
SP30     XC    KEY,KEY             FIRST PUB FOR CLIENT                         
         MVC   KEY(3),QAGENCY                                                   
         MVI   KEY+3,X'02'                                                      
         MVC   KEY+4(3),0(R3)                                                   
         GOTO1 READ                                                             
         GOTO1 GETCLI                                                           
         MVI   MODE,FPUBCLI                                                     
         BAS   RE,GO                                                            
         SPACE 2                                                                
SP34     CLI   FCGTDIV,C'Y'        FIRST PUB FOR DIVISION                       
         BNE   SP38                                                             
         MVC   PDIVKDIV,3(R3)                                                   
         OC    3(3,R3),3(R3)                                                    
         BZ    SP36                                                             
         XC    KEY,KEY                                                          
         MVC   KEY(3),QAGENCY                                                   
         MVI   KEY+3,X'03'                                                      
         MVC   KEY+4(6),0(R3)                                                   
         GOTO1 READ                                                             
         GOTO1 GETDIV                                                           
         SPACE 2                                                                
SP36     MVI   MODE,FPUBDIV                                                     
         BAS   RE,GO                                                            
         SPACE 2                                                                
SP38     CLI   FCGTREG,C'Y'        FIRST PUB FOR REGION                         
         BNE   SP42                                                             
         MVC   PREGKREG,6(R3)                                                   
         OC    6(3,R3),6(R3)                                                    
         BZ    SP40                                                             
         XC    KEY,KEY                                                          
         MVC   KEY(3),QAGENCY                                                   
         MVI   KEY+3,X'04'                                                      
         MVC   KEY+4(9),0(R3)                                                   
         GOTO1 READ                                                             
         GOTO1 GETREG                                                           
         SPACE 2                                                                
SP40     MVI   MODE,FPUBREG                                                     
         BAS   RE,GO                                                            
         SPACE 2                                                                
SP42     CLI   FCGTDIST,C'Y'       FIRST PUB FOR DISTRICT                       
         BNE   SP46                                                             
         MVC   PDSTKDST,9(R3)                                                   
         OC    9(3,R3),9(R3)                                                    
         BZ    SP44                                                             
         XC    KEY,KEY                                                          
         MVC   KEY(3),QAGENCY                                                   
         MVI   KEY+3,X'05'                                                      
         MVC   KEY+4(12),0(R3)                                                  
         GOTO1 READ                                                             
         GOTO1 GETDIST                                                          
         SPACE 2                                                                
SP44     MVI   MODE,FPUBDST                                                     
         BAS   RE,GO                                                            
SP46     DS    0H                                                               
         EJECT                                                                  
*                   SORT PUBLICATIONS (OUTPUT) - PROCESS AND LAST               
         SPACE 3                                                                
SP50     MVC   KEY(20),0(R3)                                                    
         MVC   KEY+27(4),25(R3)                                                 
         GOTO1 GETNAME                                                          
         L     RF,ALTLREC                                                       
         XC    0(35,RF),0(RF)                                                   
         OC    20(4,R3),20(R3)                                                  
         BZ    SP51                                                             
         MVC   KEY+27(4),20(R3)                                                 
         GOTO1 GETLTL                                                           
         SPACE 2                                                                
SP51     MVI   MODE,PROCPUB                                                     
         BAS   RE,GO                                                            
         MVC   KEY,0(R3)                                                        
         B     SPOUTPUT                                                         
         SPACE 2                                                                
SPLAST   CLC   QSORT,=C'04'                                                     
         BNE   SP52                                                             
         MVI   MODE,LPUBDST                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LPUBREG                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LPUBDIV                                                     
         BAS   RE,GO                                                            
         MVI   MODE,LPUBCLI                                                     
         BAS   RE,GO                                                            
         SPACE 2                                                                
SP52     B     LASTREQ                                                          
         EJECT                                                                  
*                   SORT CONTRACT RECORDS                                       
SORTCON  DS    0H                                                               
         XC    SRTCOUNT,SRTCOUNT                                                
         LA    R3,SRTREC                                                        
         MVI   FCGTPUB,C'B'     ONLY NEED MAIN PUB REC                          
*                                                                               
         CLI   SAVEQSRT,8       SEE IF DRD SORT                                 
         BE    SCN01                                                            
*                                                                               
         CLI   SAVEQSRT,9       SEE IF DRD + NAME/MKT SORT                      
         BNE   *+8                                                              
SCN01    MVI   FCGTPUB,C'Y'     READ LTLREC AS WELL                             
*                                                                               
         MVC   BFEND,=3X'FF'                                                    
         XC    BFSTART,BFSTART                                                  
         CLI   QSTART,C' '                                                      
         BE    SCN02                                                            
         GOTO1 DATCON,DMCB,QSTART,(3,BFSTART)                                   
SCN02    DS    0H                                                               
         CLI   QEND,C' '                                                        
         BE    SCN02B                                                           
         GOTO1 DATCON,DMCB,QEND,(3,BFEND)                                       
SCN02B   DS    0H                                                               
         XC    RCSVCLI,RCSVCLI                                                  
         CLI   QCLIENT,C'*'                                                     
         BE    SCN03                                                            
         CLI   QCLIENT,C'&&'          CLIENT GROUP                              
         BE    SCN03                                                            
         CLC   QCLIENT,=C'ALL'                                                  
         BE    SCN03                                                            
         MVC   RCSVCLI,QCLIENT                                                  
*                                                                               
SCN03    DS    0H                                                               
*                             INPUT                                             
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY(3),QAGENCY                                                   
         MVI   KEY+3,X'10'                                                      
         CLC   QEST,=C'000'        IF NO CONTRACT                               
         BNE   *+8                                                              
         MVI   KEY+3,X'21'         READ 21 POINTERS                             
         MVC   KEY+4(3),RCSVCLI                                                 
SCN2     DS    0H                                                               
         GOTO1 HIGH                                                             
         B     SCN6                                                             
*                                                                               
SCN4     DS    0H                                                               
         GOTO1 SEQKSV                                                           
*                                                                               
SCN6     DS    0H                                                               
         CLC   KEY(4),KEYSAVE                                                   
         BNE   SCN8                                                             
         CLI   RCSVCLI,0                                                        
         BE    SCN6B                                                            
         CLC   RCSVCLI,KEY+4                                                    
         BNE   SCN8                                                             
         BE    SCN7                                                             
*                                                                               
SCN6B    DS    0H                                                               
         CLC   QCLIENT,=C'ALL'                                                  
         BE    SCN7                                                             
         BAS   RE,TSTCLI                                                        
         BE    SCN7                                                             
*                                                                               
SCN6D    DS    0H                                                               
         ZIC   RF,KEY+6            NEXT CLI                                     
         LA    RF,1(RF)                                                         
         STC   RF,KEY+6                                                         
         B     SCN2                                                             
*                                                                               
SCN7     DS    0H                                                               
         B     SCN10                                                            
*                                                                               
SCN8     DS    0H                                                               
         XC    WORK(48),WORK                                                    
         B     SCNOUT                                                           
*                                                                               
*                                                                               
SCN10    DS    0H                                                               
*                                                                               
         CLI   KEY+3,X'21'                                                      
         BNE   SCN10D                                                           
         CLC   KEY+16(3),BFSTART   DATE CHECK FOR 21'S                          
         BL    SCN4                                                             
         CLC   KEY+16(3),BFEND                                                  
         BH    SCN4                                                             
         B     SCN12                                                            
*                                                                               
SCN10D   DS    0H                                                               
         OC    BFSTART,BFSTART                                                  
         BNZ   SCN11                                                            
         CLI   BFEND,X'FF'                                                      
         BE    SCN12                                                            
*                                  NEED CONTRACT FOR DATE CHECK                 
SCN11    DS    0H                                                               
         GOTO1 GETCONT                                                          
*                                                                               
*NEW BELOW - TO ADDRESS PCONREC                                                 
         L     RF,PPWORK2C                                                      
         USING PPWORK2D,RF                                                      
         L     RE,ACONIO                                                        
         USING PCONREC,RE                                                       
         DROP  RF                                                               
*NEW ABOVE - TO ADDRESS PCONREC                                                 
*                                                                               
         CLC   PCONSDT,BFEND                                                    
         BH    SCN4                                                             
         CLC   PCONEDT,BFSTART                                                  
         BL    SCN4                                                             
         CLC   QPROG,=C'18'        CONTRACT ANALYSIS                            
         BE    SCN11C                                                           
         CLC   QPROG,=C'AU'        ADV CON UTIL REPORT                          
         BE    SCN11C                                                           
         CLC   QPROG,=C'AR'        AOR CON REPORT FOR BUYING AGYS               
         BNE   SCN11B                                                           
         CLI   QOPT1-1,C'A'       SEE IF ANALYSIS                               
         BE    SCN11C                                                           
         B     SCN12                                                            
*                                                                               
SCN11B   CLC   QPROG,=C'19'        CONTRACT UTILIZATION REPORT                  
         BNE   SCN12                                                            
SCN11C   CLI   QOPT1,C'Y'          SEE IF FILTERING ON END MTH                  
         BNE   SCN12               NO                                           
         CLC   PCONEDT(2),BFEND    END MTHS MUST MATCH                          
         BNE   SCN4                                                             
*                                                                               
         DROP  RE                                                               
*                                                                               
SCN12    DS    0H                                                               
         XC    0(47,R3),0(R3)                                                   
         MVC   0(3,R3),KEY+4                                                    
*                                                                               
         CLC   =C'P=ALL',SVPUBRQ   ALL PUBLISHER REQUEST ?                      
         BE    SCN12B              YES                                          
         CLI   SAVEQSRT,1                                                       
         BNH   SCN18                                                            
SCN12B   BAS   RE,GETPUB                                                        
*                                                                               
*          BYPASS CONTRACT IF PUBLISHER REQUEST AND NOT PUBLISHER PUB           
         CLC   =C'P=',SVPUBRQ      PUBLISHER REQUEST ?                          
         BNE   SCN12X              NO                                           
         CLC   =C'ALL',SVPUBRQ+2   ALL PUBLISHER PUBS ?                         
         BE    SCN12D              YES - SEE IF A PUBLISHER PUB                 
         CLC   PUBPLSH,SVPUBRQ+2   SPECIFIED PUBLISHER IN THIS PUB ?            
         BE    SCN12X              YES                                          
         B     SCN4                NO - BYPASS CONTRACT                         
SCN12D   CLI   PUBPLSH,C' '        ANY PUBLISHER IN THIS PUB ?                  
         BNH   SCN4                NO - BYPASS CONTRACT                         
*                                                                               
SCN12X   DS    0H                                                               
         CLC   =C'P=ALL',SVPUBRQ   ALL PUBLISHER REQUEST ?                      
         BNE   SCN12X5             NO                                           
*                                                                               
*****    MVC   3(4,R3),PUBPLSH     SORT BY PUBLISHER CODE                       
*****    CLI   SAVEQSRT,1                                                       
*****    BNH   SCN18                                                            
*                                  GET PUBLISHER NAME FOR SORT                  
         MVC   WORK(3),KEY                                                      
         MVI   WORK+3,X'11'                                                     
         MVC   WORK+4(4),PUBPLSH                                                
         CLC   WORK(8),PREPREC     SEE IF HAVE REP RECORD                       
         BE    SCN12X4             YES                                          
*                                                                               
         MVC   WORK(64),KEY                                                     
         XC    KEY+3(61),KEY+3                                                  
         MVI   KEY+3,X'11'                                                      
         MVC   KEY+4(4),PUBPLSH                                                 
         GOTO1 READ                                                             
         GOTO1 GETREP                                                           
         MVC   KEY(64),WORK        RESTORE PPG KEYS                             
         GOTO1 HIGH                AND SEQUENCE                                 
*                                                                               
SCN12X4  MVC   3(12,R3),PREPNAME   SORT BY PUBLISHER NAME                       
         CLC   PREPNAME(4),=C'THE '                                             
         BNE   *+10                                                             
         MVC   3(12,R3),PREPNAME+4                                              
*                                                                               
         CLI   SAVEQSRT,1                                                       
         BNH   SCN18                                                            
*                                                                               
SCN12X5  MVC   15(18,R3),PUBNAME                                                
         CLC   PUBNAME(4),=C'THE '                                              
         BNE   *+10                                                             
         MVC   15(18,R3),PUBNAME+4                                              
*                                                                               
*                      SORT 09 = DRD AND NAME FOR MAGAZINES                     
*                      SORT 09 = DRD AND MKT FOR NEWS AND OUTDOOR               
*                                                                               
         JIF   SAVEQSRT,EQ,9,AND,QMEDIA,NE,C'O',AND,QMEDIA,NE,C'N',    X        
               SCN16,JUMP=N                                                     
*                                                                               
         CLI   SAVEQSRT,9          DRD AND MKT                                  
         BE    SCN13                                                            
*                                                                               
         CLI   SAVEQSRT,7          MKT SORT                                     
         BNE   SCN16                                                            
*                                                                               
SCN13    CLI   QMEDIA,C'O'                                                      
         BNE   SCN15                                                            
         MVC   17(15,R3),PUBZNAME                                               
         MVC   32(1,R3),KEY+18          EDITION                                 
         MVC   15(2,R3),PUBSTACD                                                
         CLI   15(R3),C' '                                                      
         BNH   SCN14                                                            
         CLI   15(R3),C'0'                                                      
         BL    SCN16                                                            
SCN14    DS    0H                                                               
         MVC   15(2,R3),PUBSTATE                                                
         B     SCN16                                                            
*                                                                               
SCN15    DS    0H                                                               
         MVC   15(2,R3),PUBSTATE                                                
         MVC   17(16,R3),PUBCITY                                                
         B     SCN16                                                            
*                                                                               
SCN16    DS    0H                                                               
         CLI   SAVEQSRT,9           SEE IF DRD AND NAME/MKT                     
         BE    SCN160               IF SO LEAVE NAME OR MKT IN SORT             
         CLI   SAVEQSRT,8           DIV/REG/DST ORDER                           
         BNE   SCN18                                                            
         XC    15(18,R3),15(R3)    CLEAR NAME OR MKT                            
*                                                                               
SCN160   DS    0H                                                               
         SR    R4,R4                                                            
         L     R2,ALTLREC                                                       
         LA    R2,33(R2)                                                        
         MVC   3(9,R3),=C'999999999'    PRESET DIV/REG/DST TO 9'S               
         CLC   QPUB+1(3),=C'RD='                                                
         BNE   SCN16C                                                           
         MVC   3(3,R3),=C'000'     PRESET DIV TO 000 FOR DRD OVERRIDE           
         B     SCN16C                                                           
         SPACE 2                                                                
SCN16A   IC    R4,1(R2)            LOOP FOR DISTRICT ELEMENTS                   
         AR    R2,R4                                                            
         SPACE 2                                                                
SCN16C   CLI   0(R2),0                                                          
         BE    SCN16X                                                           
         CLI   0(R2),X'71'                                                      
         BNE   SCN16A                                                           
         USING PUBDSTEL,R2                                                      
         CLC   QPUB+1(3),=C'RD='        REQUESTED REG/DST OVERRIDE              
         BE    SCN16E                                                           
         CLC   PUBDCLT,KEY+4       CHECK MATCH ON CLIENT                        
         BNE   SCN16A                                                           
         CLC   PUBDDIV,=C'999'                                                  
         BE    SCN16G                                                           
         CLC   PUBDDIV,QDIV          NOTE - QDIV MUST BE GIVEN                  
         BNE   SCN16A                                                           
         B     SCN16G                                                           
*                                                                               
SCN16E   DS    0H                  REG/DST OVERRIDE CLIENT                      
         CLC   PUBDCLT,QPUB+4                                                   
         BNE   SCN16A                                                           
         CLC   PUBDDIV,=C'000'      DIVISION MUST BE 000                        
         BNE   SCN16A                                                           
         SPACE 2                                                                
SCN16G   EQU   *                                                                
         XC    3(9,R3),3(R3)                                                    
         MVC   3(3,R3),PUBDDIV                                                  
         CLC   QREGION,SPACES                                                   
         BE    SCN16GA                                                          
         CLC   QREGION,=C'ALL'                                                  
         BE    SCN16GA                                                          
         CLC   QREGION,PUBDREG                                                  
         BNE   SCN16A                                                           
SCN16GA  MVC   6(3,R3),PUBDREG                                                  
SCN16GB  CLC   QDIST,SPACES                                                     
         BE    SCN16GE                                                          
         CLC   QDIST,=C'ALL'                                                    
         BE    SCN16GC                                                          
         CLC   QDIST,PUBDDST                                                    
         BNE   SCN16A                                                           
SCN16GC  MVC   9(3,R3),PUBDDST                                                  
*                                                                               
SCN16GE  DS    0H                                                               
         CLC   QPUB+1(3),=C'RD='        SEE IF REG/DST OVERRIDE                 
         BNE   *+10                                                             
         MVC   12(3,R3),QPUB+4          I'LL NEED OVERRIDE CLIENT               
         B     SCN18                                                            
*                                       IS USED                                 
SCN16X   DS    0H                                                               
         CLC   6(6,R3),=C'999999'                                               
         BNE   SCN4                                                             
         CLI   QREGION,C'0'                                                     
         BNL   SCN4                                                             
         CLI   QDIST,C'0'                                                       
         BNL   SCN4                                                             
*                                                                               
SCN18    DS    0H                                                               
         MVC   33(6,R3),KEY+7      PUB NUM                                      
         CLI   KEY+3,X'21'                                                      
*NOP*    BE    *+16                                                             
         BE    SCN18B                                                           
*                                                                               
*NEW BELOW - TO ADDRESS PCONREC                                                 
         L     RF,PPWORK2C                                                      
         USING PPWORK2D,RF                                                      
         L     RE,ACONIO                                                        
         USING PCONREC,RE                                                       
*NEW ABOVE - TO ADDRESS PCONREC                                                 
*                                                                               
         MVC   41(6,R3),PCONSDT    START AND END                                
         MVC   39(2,R3),KEY+13     CON NUM                                      
*                                                                               
         DROP  RE,RF                                                            
*                                                                               
SCN18B   BAS   RE,TOSORT                                                        
*                                                                               
         CLI   KEY+3,X'21'         IF 21 POINTERS                               
         BNE   SCN4                                                             
         ZIC   RF,KEY+12           NEXT PUB                                     
         LA    RF,1(RF)                                                         
         STC   RF,KEY+12                                                        
         MVI   KEY+13,0                                                         
         B     SCN2                                                             
*                                                                               
SVPUBRQ  DS    CL6                 QPUB+1 SAVED HERE                            
         EJECT                                                                  
*                        SORT BY CONTRACT (OUTPUT)                              
*                                                                               
SCNOUT   DS    0H                                                               
         BAS   RE,FRSORT                                                        
         CLI   SRTREC,X'FF'                                                     
         BE    SCNLAST                                                          
*                                                                               
*                                  SET REQUEST FIELDS FOR APPLIC                
         MVC   QCLIENT,0(R3)                                                    
*                                                                               
         CLI   SAVEQSRT,9           DRD AND NAME OR MKT                         
         BE    SCNO3                                                            
         CLI   SAVEQSRT,8           SEE IF REG/DST SORT                         
         BNE   SCNO5                                                            
*                                                                               
SCNO3    MVC   QREGION(3),6(R3)     SET REGION IN REQUEST                       
         MVC   QDIST(3),9(R3)       AND DISTRICT IN REQUEST                     
         MVC   QDIV,3(R3)        PUT DIVISION IN QDIV                           
         OC    12(3,R3),12(R3)      SEE IF RD= CLIENT USED                      
         BZ    *+10                                                             
         MVC   QESTEND,12(R3)       PUT RD= CLIENT IN QESTEND                   
*                                                                               
SCNO5    DS    0H                                                               
         GOTO1 PUBEDIT,DMCB,33(R3),(C'Q',QPUB)                                  
         MVC   HALF,39(R3)                                                      
         LH    R0,HALF                                                          
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  QEST(3),DUB                                                      
         MVC   QSORT,SPACES                                                     
         MVI   QOPT7,C'F'                                                       
         CLI   WORK,0              TEST FIRST TIME                              
         BE    *+8                                                              
         MVI   QOPT7,C'C'          NO - THEN 'CONTINUATION' REQ                 
         CLC   QEST,=C'000'                                                     
         BE    SCNO20                                                           
         CLC   QPROG,=C'14'                                                     
         BE    SCNO20              FOR PP14 LEAVE DATES ALONE                   
*                                                                               
         CLC   QPROG,=C'AR'       AOR CON RPT FOR BUYING AGYS                   
         BNE   SCNO18                                                           
         CLI   QOPT1-1,C'L'       SEE IF LISTING                                
         BE    SCNO20             LEAVE DATES ALONE                             
*                                  IF CONTRACT REQ SET DATES                    
SCNO18   GOTO1 DATCON,DMCB,(3,41(R3)),QSTART                                    
         GOTO1 (RF),(R1),(3,44(R3)),QEND                                        
SCNO20   DS    0H                                                               
*                                                                               
         MVI   MODE,FBUYREQ                                                     
         BAS   RE,GO                                                            
         MVC   WORK,0(R3)                                                       
         B     SCNOUT                                                           
*                                                                               
SCNLAST  DS    0H                                                               
         B     LASTREQ                                                          
         SPACE 2                                                                
         EJECT                                                                  
         SPACE 2                                                                
*                                  USER SORT                                    
USERSORT DS    0H                                                               
         MVI   MODE,UNSRTREC                                                    
         XC    SORTPARS,SORTPARS                                                
         BAS   RE,GO               FIRST TIME                                   
         OC    DMCB+8(4),DMCB+8                                                 
         BZ    LASTREQ             NO SORT NEEDED                               
         PACK  DUB,DMCB+8(2)                                                    
         CVB   R0,DUB                                                           
         ST    R0,SRTRLEN                                                       
         PACK  DUB,DMCB+10(2)                                                   
         CVB   R0,DUB                                                           
         ST    R0,SRTKLEN                                                       
         XC    SRTCOUNT,SRTCOUNT        CLEAR SORT TABLE                        
*                                                                               
         LA    R2,4                                                             
         LA    R3,SRTREC                                                        
         STM   R2,R3,SORTPARS                                                   
USORT2   DS    0H                                                               
         BAS   RE,GO               GET INPUT                                    
         MVC   SORTPARS(8),DMCB+4  USERS PARS                                   
         CLI   SORTPARS+3,8        TEST END                                     
         BE    USORT3                                                           
         BAS   RE,TOSORT                                                        
         B     USORT2                                                           
*                                                                               
USORT3   DS    0H                                                               
         BAS   RE,FRSORT                                                        
         CLI   SRTREC,X'FF'        TEST END                                     
         BE    USORT4                                                           
         BAS   RE,GO               RELEASE OUTPUT                               
         B     USORT3                                                           
USORT4   DS    0H                                                               
         MVI   SORTPARS+3,12       LAST TIME                                    
         BAS   RE,GO                                                            
         B     LASTREQ                                                          
*                                                                               
*                                                                               
SORTPARS DS    2F                  PASSED TO APPL PROG                          
         EJECT                                                                  
*                   HANDLE LAST FOR REQUEST CONTROL BREAKS                      
         SPACE 3                                                                
LASTREQ  DS    0H                                                               
         CLI   MODE,FBUYCLI                                                     
         BE    RC7A                GO BACK AND START OVER                       
         MVI   MODE,LBUYREQ                                                     
         BAS   RE,GO                                                            
*                                                                               
NEXTREQ  DS    0H                                                               
         CLI   SRTCOUNT,X'FF'      CHK IF I DID A SORT WITH SORTER              
         BNE   NEXTREQ1                                                         
         GOTO1 SORTER,DMCB,=C'END'                                              
*                                                                               
*        NEEDED UNDER MVS TO INSURE ALL RECS PASSED TO SORTER                   
*        ARE PROCESSED                                                          
*                                                                               
NEXTREQ1 DS    0H                                                               
         CLI   RCMULTIQ,C'Y'       TEST MULTI-MED REQ                           
         BNE   NEXTRQ7                                                          
         LA    RF,MEDLIST                                                       
NEXTRQ2  DS    0H                                                               
         CLC   QMEDIA,0(RF)                                                     
         BE    NEXTRQ4                                                          
         LA    RF,1(RF)                                                         
         B     NEXTRQ2                                                          
*                                                                               
NEXTRQ4  DS    0H                                                               
         CLI   1(RF),X'FF'         END                                          
         BE    NEXTRQ6                                                          
         MVC   QMEDIA,1(RF)                                                     
         B     RC4B                                                             
*                                                                               
NEXTRQ6  DS    0H                                                               
         MVI   MODE,LBUYXRQ                                                     
         BAS   RE,GO                                                            
NEXTRQ7  DS    0H                                                               
         CLI   RCALLAGY,C'Y'       TEST MULTI-AGY                               
         BNE   READREQ             NO                                           
*                                                                               
         CLI   RCMULTIQ,C'Y'       IF MULTI-MED                                 
         BNE   *+8                                                              
***INT                                                                          
         MVI   QMEDIA,C'B'         RESET TO MOBILE  (WAS I=INT)                 
***INT                                                                          
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY(2),QAGENCY                                                   
         MVI   KEY+2,X'FF'         BUMP TO NEXT AGENCY                          
         GOTO1 HIGH                                                             
         CLI   KEY,X'FF'           EOF                                          
         BE    READREQ                                                          
         MVC   QAGENCY(2),KEY      SAVE NEW AGENCY                              
         XC    KEY,KEY             INIT KEY                                     
         MVC   KEY(2),QAGENCY      SET NEXT AGENCY                              
         MVC   KEY+2(1),QMEDIA     GET RIGHT MEDIA                              
         MVI   KEY+3,X'01'                                                      
         GOTO1 HIGH                                                             
         CLC   KEY(4),KEYSAVE      DID I FIND RIGHT AGY/MED?                    
         BNE   NEXTREQ                                                          
         GOTO1 GETAGY                                                           
         MVC   QAGENCY(3),KEY                                                   
         MVC   RCSVAGY(3),KEY                                                   
         B     RC4C                                                             
*                                                                               
*                                                                               
*                                                                               
***INT                                                                          
MEDLIST  DC    C'BDILMNOSTVW'                                                   
***INT                                                                          
         DC    X'FF'                                                            
*                                                                               
         EJECT                                                                  
*                  FILTER BUYS FOR REGION AND DISTRICT                          
         SPACE 3                                                                
FILTREG  NTR                                                                    
         MVI   BYTE,0                                                           
         CLI   FCGTREG,C'Y'                                                     
         BE    FILT01                                                           
         CLI   FCGTDIST,C'Y'                                                    
         BNE   FILTYES                                                          
FILT01   DS    0H                                                               
         CLC   QREGION,=C'GET'                                                  
         BE    FILT01B                                                          
         CLI   QREGION,C'0'                                                     
         BNL   FILT01B                                                          
         CLI   QDIST,C'0'                                                       
         BL    FILTYES             NO REG/DST SPECIFIED                         
FILT01B  DS    0H                                                               
         L     R2,ALTLREC                                                       
         LA    R2,33(R2)                                                        
         SR    R3,R3                                                            
         SPACE 3                                                                
FILT02   DS    0H                  LOOP FOR DISTRICT ELEMS                      
         CLI   0(R2),X'71'                                                      
         BE    FILT06                                                           
         CLI   0(R2),0                                                          
         BNE   FILT04                                                           
*                                                                               
         CLC   QREGION,=C'GET'                                                  
         BNE   FILT16B                                                          
FILT02B  DS    0H                                                               
         MVC   QREGION,SPACES                                                   
         B     FILTYES                                                          
*                                                                               
         SPACE 2                                                                
FILT04   IC    R3,1(R2)                                                         
         AR    R2,R3                                                            
         B     FILT02                                                           
         SPACE 2                                                                
FILT06   EQU   *                                                                
         USING PUBDSTEL,R2                                                      
         LA    RE,PCLTKCLT                                                      
         LA    RF,PPRDDIV                                                       
         CLC   QPUB+1(3),=C'RD='   REQUESTED REG/DST OVERRIDE                   
         BNE   *+12                                                             
         LA    RE,QPUB+4                                                        
         LA    RF,=C'000'                                                       
*                                                                               
         CLC   PUBDCLT,0(RE)       CHECK CLIENT MATCH                           
         BNE   FILT04                                                           
         CLC   PUBDDIV,=C'999'                                                  
         BE    FILT08                                                           
         CLC   PUBDDIV,0(RF)       AND DIVISION MATCH                           
         BNE   FILT04                                                           
         SPACE 2                                                                
FILT08   DS    0H                                                               
         CLC   QREGION,=C'GET'     SPECIAL TO FETCH REG/DST                     
         BNE   FILT09                                                           
         OC    PUBDSHR(2),PUBDSHR       BYPASS FETCH IF MULT REG/DST            
         BNE   FILT02B                                                          
         MVC   QREGION(6),PUBDREG                                               
FILT09   DS    0H                                                               
         CLC   QREGION,SPACES      CHECK REGION MATCH                           
         BE    FILT010                                                          
         CLC   PUBDREG,QREGION                                                  
         BNE   FILT04                                                           
         SPACE 2                                                                
FILT010  CLC   QDIST,SPACES        AND DISTRICT MATCH                           
         BE    FILT12                                                           
         CLC   QDIST,PUBDDST                                                    
         BNE   FILT04                                                           
*                                  NOW GET HEADERS                              
FILT12   EQU   *                                                                
         MVC   WORK,KEY                                                         
         XC    KEY,KEY                                                          
         MVI   WORD,0                                                           
         MVC   KEY(7),RCSVAGY                                                   
         MVI   KEY+3,X'04'                                                      
*                                                                               
         CLC   QPUB+1(3),=C'RD='    SEE IF USING REG/DST OVERRIDE CLT           
         BNE   FILT12C                                                          
         MVC   KEY+4(3),QPUB+4                                                  
*                                                                               
FILT12C  MVC   KEY+7(6),PUBDDIV    DIV + REG                                    
         CLI   QREGION,C' '                                                     
         BH    *+12                                                             
         MVI   PREGKEY,0                                                        
         B     FILT14                                                           
         CLC   KEY(25),PREGKEY                                                  
         BE    FILT14              ALREADY HAVE                                 
         GOTO1 READ                                                             
         GOTO1 GETREG                                                           
         MVI   WORD,1                                                           
FILT14   EQU   *                                                                
         CLI   QDIST,C' '                                                       
         BH    *+12                                                             
         MVI   PDSTKEY,0                                                        
         B     FILT16                                                           
         MVI   KEY+3,X'05'                                                      
         MVC   KEY+7(9),PUBDDIV    DIV + REG + DST                              
         CLC   KEY(25),PDSTKEY                                                  
         BE    FILT16              ALREADY HAVE                                 
         GOTO1 READ                                                             
         GOTO1 GETDIST                                                          
         MVI   WORD,1                                                           
FILT16   EQU   *                                                                
         MVC   KEY,WORK                                                         
         CLI   WORD,0                                                           
         BE    FILTYES                                                          
         GOTO1 HIGH                                                             
         B     FILTYES                                                          
         EJECT                                                                  
*                   FILTER BUYS FOR ESTIMATE AND DATES                          
         SPACE 3                                                                
FILTER   NTR                                                                    
         MVI   BYTE,0                                                           
         CLI   FCOAPRD,C'Y'                                                     
         BE    FLT04                                                            
         LA    RF,KEY+7                                                         
         CLI   KEY+3,X'20'                                                      
         BE    *+8                                                              
         LA    RF,KEY+13                                                        
         CLI   0(RF),C'*'                                                       
         BNE   FLT04                                                            
*                                                                               
         MVI   0(RF),C'A'          SKIP TO FIRST REAL PRODUCT                   
         MVI   1(RF),0                                                          
         B     FILT15B                                                          
*                                                                               
FLT04    DS    0H                                                               
         MVI   HAVEBUY,C'N'                                                     
*                               SEE IF PRODUCT GROUP REQUEST                    
         L     RF,PPWORK2C                                                      
         USING PPWORK2D,RF                                                      
*                                                                               
         CLI   QCONTREQ,C'*'     BE SURE 2 CARD REQUEST                         
         BNE   FLT04X                                                           
*                                                                               
         CLI   Q2USER+24,C'A'   IF ALPHA THEN SCHEME CODE                       
         BL    FLT04X                                                           
         CLI   Q2USER+24,C'Z'                                                   
         BH    FLT04X                                                           
         GOTO1 =A(FLTPGRP),DMCB,(RC)    FILTER ON GROUP                         
         BE    FLT04X           PRODUCT IN A REQUESTED GROUP                    
         B     FILTNO           NO THEN SKIP                                    
*                                                                               
         DROP  RF                                                               
*                                                                               
FLT04X   DS    0H                                                               
         CLI   QPUB,C'0'                                                        
         BL    FILT1               PUB BLANK OR ALL                             
*                                                                               
         LA    RF,KEY+10                                                        
         CLI   KEY+3,X'20'                                                      
         BE    *+8                                                              
         LA    RF,KEY+7                                                         
         CLC   QPUB+8(3),=C'ZZZ'   SEE IF DOING ALL ZONES + EDTS                
         BNE   FLT06                                                            
         CLC   SVPUB(4),0(RF)      YES ONLY CHK BASE NUMBER                     
         BNE   FILT16C                                                          
         B     FILT1                                                            
FLT06    CLC   SVPUB(6),0(RF)                                                   
         BNE   FILT16C                                                          
*                                                                               
         SPACE 2                                                                
FILT1    EQU   *                                                                
         CLC   QPROG,=C'90'        IF TRIAL BALANCE                             
         BE    FILT10              NO FILTER ON DATES                           
         CLC   KEY+16(3),BFSTART                                                
         BL    FILT15                                                           
         CLC   KEY+16(3),BFEND                                                  
         BH    FILT16B                                                          
*                                                                               
*                                                                               
FILT1A   CLI   FCRDACTV,C'Y'       TEST READING ACTIVE ONLY                     
         BNE   FILT1A1                                                          
         OC    KEY+21(3),KEY+21                                                 
         BNZ   FILTNO                                                           
FILT1A1  DS    0H                                                               
         SPACE 2                                                                
FILT4    DS    0H                                                               
         CLC   QPROG,=C'06'        IF DETAIL BILLING                            
         BNE   FILT4A                                                           
         MVI   FCBLFILT,C'P'       SET BILLABLE FILTER - PAID                   
         CLI   PCLTPROF+13,C'0'                                                 
         BE    *+8                                                              
         MVI   FCBLFILT,C'O'       ORDERED                                      
FILT4A   DS    0H                                                               
         CLI   FCPDFILT,C'N'                                                    
         BNE   FILT4B                                                           
         CLI   FCBLFILT,C'N'                                                    
         BNE   FILT4B                                                           
         CLI   QBPDATE,C' '                                                     
         BE    FILT10                                                           
         CLI   QBPDATE,C'0'        SPECIAL                                      
         BNL   FILT10                                                           
FILT4B   DS    0H                                                               
         GOTO1 GETBUY                                                           
         MVI   HAVEBUY,C'Y'                                                     
         CLI   FCPDFILT,C'N'       TEST PAID STATUS FILTER ACTIVE               
         BNE   FILT4C                                                           
         CLI   FCBLFILT,C'N'       OR BILLABLE FILTER                           
         BE    FILT4H              NO                                           
         CLC   PBUYKPRD,=C'ZZZ'    TEST NOT RELIABLE FOR ZZZ BUYS               
         BE    FILT4H                                                           
*                                                                               
FILT4C   DS    0H                                                               
         GOTO1 GETINS,DMCB,PBUYKEY,GROSS,(FCOAPRD,PBUYKPRD)                     
         TM    PBUYCNTL,X'80'                                                   
         BZ    *+10                                                             
         XC    GROSS(20),GROSS                                                  
*                                                                               
         CLI   FCPDFILT,C'N'                                                    
         BE    FILT4F                                                           
         CLI   FCPDFILT,C'P'       PAID BUYS ONLY                               
         BNE   FILT4D                                                           
         OC    PGROSS(12),PGROSS   MUST BE PAID                                 
         BNZ   FILT4F                                                           
         OC    GROSS(12),GROSS    SEE IF "FREE" BUY                             
         BNZ   FILTNO                                                           
*                                                                               
         LA    R2,PBUYREC+33      CHK FOR PAYELEM WITH DATE                     
FILT4C3  CLI   0(R2),X'25'                                                      
         BE    FILT4C5                                                          
         ZIC   R0,1(R2)                                                         
         LTR   R0,R0                                                            
         BZ    FILTNO                                                           
         AR    R2,R0                                                            
         CLI   0(R2),0          END OF REC                                      
         BE    FILTNO                                                           
         B     FILT4C3                                                          
*                                                                               
FILT4C5  DS    0H                                                               
         OC    2(3,R2),2(R2)        CHK FOR DATE                                
         BZ    FILTNO               NOT PAID                                    
         B     FILT4F                                                           
*                                                                               
FILT4D   DS    0H                  UNPAID BUYS ONLY                             
         CLC   PGROSS(12),GROSS   SEE IF FULLY PAID                             
         BNE   FILT4F                                                           
         OC    GROSS(12),GROSS    SEE IF "FREE" BUY                             
         BNZ   FILTNO                                                           
*                                                                               
         LA    R2,PBUYREC+33      CHK FOR PAYELEM WITH DATE                     
FILT4D3  CLI   0(R2),X'25'                                                      
         BE    FILT4D5                                                          
         ZIC   R0,1(R2)                                                         
         LTR   R0,R0                                                            
         BZ    FILT4F           END OF REC WITH NO DATED PAY ELEM               
         AR    R2,R0                                                            
         CLI   0(R2),0          END OF REC WITH NO DATED PAY ELEM               
         BE    FILT4F                                                           
         B     FILT4D3                                                          
*                                                                               
FILT4D5  DS    0H                                                               
         OC    2(3,R2),2(R2)        CHK FOR DATE                                
         BZ    FILT4F               NOT PAID                                    
         B     FILTNO               PAID - SO SKIP                              
*                                                                               
*                                                                               
FILT4F   DS    0H                                                               
         CLI   FCBLFILT,C'N'                                                    
         BE    FILT4H                                                           
*                                  SELECT ONLY BILLABLE BUYS                    
         LA    RE,GROSS            BILLED COMPARED TO ORDERED                   
         CLI   FCBLFILT,C'O'                                                    
         BE    *+8                                                              
         LA    RE,PGROSS           BILLED COMPARED TO PAID                      
         CLC   BGROSS(12),0(RE)                                                 
         BNE   FILT4H                                                           
         OC    GROSS(12),GROSS     SEE IF "FREE" BUY                            
         BNZ   FILTNO             NO - THEN WAS FULLY BILLED                    
*                                                                               
         LA    R2,PBUYREC+33      CHK FOR BILL ELEM WITH DATE                   
FILT4F3  CLI   0(R2),X'26'                                                      
         BE    FILT4F5                                                          
         ZIC   R0,1(R2)                                                         
         LTR   R0,R0                                                            
         BZ    FILT4H           END OF REC WITH NO DATED BILL ELEM              
         AR    R2,R0                                                            
         CLI   0(R2),0          END OF REC WITH NO DATED BILL ELEM              
         BE    FILT4H                                                           
         B     FILT4F3                                                          
*                                                                               
FILT4F5  DS    0H                                                               
         OC    5(3,R2),5(R2)        CHK FOR DATE                                
         BZ    FILT4H               NOT BILLED                                  
         B     FILTNO               BILLED - SO SKIP                            
*                                                                               
*                                                                               
FILT4H   DS    0H                                                               
         LA    R2,PBDPDATE                                                      
         CLI   QBPDATE,C'P'                                                     
         BE    FILT6                                                            
         LA    R2,PBDBDATE                                                      
         CLI   QBPDATE,C'B'                                                     
         BE    FILT6                                                            
         LA    R2,PBDCDATE                                                      
         CLI   QBPDATE,C'C'                                                     
         BE    FILT6                                                            
         LA    R2,PBDSDATE                                                      
         CLI   QBPDATE,C'S'                                                     
         BE    FILT6                                                            
         B     FILT10                                                           
*                                                                               
FILT6    DS    0H                                                               
         SPACE 2                                                                
         CLC   QPROG,=C'04'        SPECIAL CHECK FOR BILLING                    
         BE    FILT9                                                            
         CLC   QPROG,=C'06'                                                     
         BE    FILT9                                                            
         CLC   QPROG,=C'B1'        OR NEW BILLING                               
         BE    FILT9                                                            
         CLC   QPROG,=C'R1'        OR NEW REBATE BILLING                        
         BE    FILT9                                                            
         CLC   QPROG,=C'RD'        OR NEW REBATE BILLING  - DRAFT               
         BE    FILT9                                                            
         CLC   QPROG,=C'D1'        OR NEW DRAFT BILLING                         
         BE    FILT9                                                            
         CLC   QPROG,=C'E1'        OR NEW ESTIMATE                              
         BE    FILT9                                                            
         CLC   BQSTART,0(R2)                                                    
         BH    FILTNO                                                           
         SPACE 2                                                                
FILT8    EQU   *                                                                
         CLC   BQEND,0(R2)                                                      
         BL    FILTNO                                                           
         B     FILT10                                                           
         SPACE 2                                                                
FILT9    EQU   *                                                                
*                                  BILLING CHECK                                
         CLC   BQSTART(2),0(R2)                                                 
         BL    FILTNO                                                           
         BE    FILT10                                                           
         CLC   QPROG,=C'B1'        FOR NEW BILLING                              
         BE    FILT10              ALWAYS PASS PRIOR MONTHS                     
         CLC   QPROG,=C'R1'        FOR NEW REBATE BILLING                       
         BE    FILT10              ALWAYS PASS PRIOR MONTHS                     
         CLC   QPROG,=C'RD'        FOR NEW REBATE BILLING - DRAFT               
         BE    FILT10              ALWAYS PASS PRIOR MONTHS                     
         CLC   QPROG,=C'D1'        FOR NEW DRAFT BILLING                        
         BE    FILT10              ALWAYS PASS PRIOR MONTHS                     
         CLC   QPROG,=C'E1'        FOR NEW ESTIMATES                            
         BE    FILT10              ALWAYS PASS PRIOR MONTHS                     
         CLI   QBILMODE,C'P'                                                    
         BNE   FILTNO                                                           
         SPACE 2                                                                
FILT10   DS    0H                                                               
         CLI   FCRDTEST,C'Y'       SEE IF READING TEST BUYS                     
         BE    FILT10A                                                          
         GOTO1 GETBUY                                                           
         MVI   HAVEBUY,C'Y'                                                     
         CLI   PBDBFD,C'T'         SKIP TEST BUYS                               
         BE    FILTNO                                                           
*                                                                               
FILT10A  GOTO1 =A(FLTREST),DMCB,(RC)                                            
         BNE   FILTNO                                                           
         CLI   FCPDFILT,C'N'                                                    
         BNE   FILTYES                                                          
         CLI   FCBLFILT,C'N'                                                    
         BNE   FILTYES                                                          
         CLI   QBPDATE,C' '                                                     
         BE    FILT10B                                                          
         CLI   QBPDATE,C'0'                                                     
         BL    FILTYES                                                          
FILT10B  DS    0H                                                               
         CLI   MODE,C'S'           OPTION TO SKIP BUY READ                      
         BE    FILTYES                                                          
         GOTO1 GETBUY                                                           
         MVI   HAVEBUY,C'Y'                                                     
         B     FILTYES                                                          
         SPACE 2                                                                
*                                                                               
FILT15   DS    0H                  DATE BEFORE START                            
         MVC   KEY+16(3),BFSTART   JUMP TO START DATE                           
         MVC   KEY+19(2),BQEST                                                  
         XC    KEY+21(4),KEY+21                                                 
FILT15B  DS    0H                                                               
         GOTO1 HIGH                                                             
         B     FILTNO2                                                          
*                                                                               
FILT16B  DS    0H                                                               
         MVI   KEY+16,X'FF'        JUMP TO NEXT PUB                             
         B     FILT15B                                                          
         SPACE 2                                                                
FILT16C  DS    0H                                                               
         MVI   6(RF),X'FF'         JUMP TO NEXT PUB                             
         B     FILT15B                                                          
         SPACE 2                                                                
FILTNO   DS    0H                                                               
         GOTO1 SEQKSV                                                           
FILTNO2  DS    0H                                                               
         MVI   BYTE,X'FF'                                                       
         SPACE 2                                                                
FILTYES  DS    0H                                                               
         B     EXIT                                                             
*                                                                               
HAVEBUY  DS    CL1                                                              
         EJECT                                                                  
*                   FILTER BILLS FOR ESTIMATE                                   
         SPACE 3                                                                
FILTBILL NTR                                                                    
         MVI   BYTE,0                                                           
         CLI   QBPDATE,C'D'        TEST DATE OF BILL FILTER                     
         BNE   FILTB2                                                           
         LA    RF,KEY+14                                                        
         CLC   0(2,RF),BQSTART                                                  
         BL    FILTBNO                                                          
         CLC   0(2,RF),BQEND                                                    
         BH    FILTBNO                                                          
*                                                                               
FILTB2   DS    0H                                                               
         GOTO1 =A(FLTREST),DMCB,(RC)                                            
         BE    FILTBYES                                                         
*                                                                               
*                                                                               
FILTBNO  MVI   BYTE,X'FF'                                                       
         SPACE 2                                                                
FILTBYES DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
*                                  CHECK PROPER OFFICE                          
         SPACE 2                                                                
TSTCLI   NTR1  LABEL=*                                                          
*                                                                               
         XC    WORK(25),WORK                                                    
         MVC   WORK(3),RCSVAGY                                                  
         MVI   WORK+3,2                                                         
         MVC   WORK+4(3),KEY+4     SET CLT CODE                                 
*                                                                               
         CLC   WORK(25),PCLTKEY    SKIP IF CLIENT IN CORE                       
         BE    TSTCLIX                                                          
*                                                                               
         XC    WORK(64),KEY        SAVE LAST READ KEY                           
         XC    KEY(64),WORK                                                     
         XC    WORK(64),KEY                                                     
*                                                                               
         GOTO1 HIGH                                                             
*                                                                               
         CLC   KEY(7),KEYSAVE     SEE IF CLIENT ON FILE                         
         BE    TSTCLI5                                                          
*                                  NO                                           
         MVC   KEY(64),WORK        RESTORE PRIOR KEY                            
*                                  NO                                           
         GOTO1 HIGH                                                             
*                                  NO                                           
         B     TSTCLIX3          SET CC NOT =                                   
*                                                                               
TSTCLI5  GOTO1 GETCLI                                                           
*                                                                               
         MVC   KEY(64),WORK        RESTORE FILE POINTERS                        
         GOTO1 HIGH                                                             
*                                                                               
TSTCLIX  DS    0H                                                               
*                                 SET R1 TO EITHER OFFICE OR GROUP              
         LA    R1,PCLTOFF                                                       
*                                                                               
         CLI   QCLIENT,C'*'         OFFICE REQUEST                              
         BE    TSTCLIX1                                                         
*                                                                               
         LA    R1,PCLTBLGP                                                      
*                                                                               
         CLI   QCLIENT,C'&&'        OR CLIENT GROUP                             
         BNE   TSTCLIOK            OKAY IF NEITHER                              
*                                                                               
TSTCLIX1 CLI   QCLIENT+1,C'-'      'ALL BUT'                                    
         BE    TSTCLIX2                                                         
*                                                                               
         CLC   0(1,R1),QCLIENT+1   MATCH OFFICE OR GROUP                        
         BE    TSTCLIOK            = CC IS OK                                   
*                                                                               
         CLI   QCLIENT,C'*'        MUST BE OFFICE RANGE                         
         BNE   TSTCLIX3                                                         
*                                                                               
         CLI   QCLIENT+2,C' '      TEST RANGE                                   
         BNH   TSTCLIX3            NO SET CC NOT =                              
*                                                                               
         OC    SVOFCSTR,SVOFCSTR   SKIP IF RANGE ALREADY TRANSLATED             
         BNZ   TSTCLI10                                                         
*                                                                               
         XC    DMCB(4),DMCB        NEED ADDRESS OF OFFICER                      
         MVC   DMCB+4(4),=X'D9000A38'                                           
         L     RF,VCOMFACS                                                      
         L     RF,(CCALLOV-COMFACSD)(RF)                                        
         GOTO1 (RF),DMCB                                                        
         MVC   VOFFICER,DMCB                                                    
*                                                                               
         XC    OFCBLKC,OFCBLKC     ESTABLISH OFFICE CONTROL BLOCK               
         LA    R1,OFCBLKC                                                       
         USING OFFICED,R1                                                       
*                                                                               
         MVI   OFCSYS,C'P'                                                      
         MVC   OFCAGY,RCSVAGY                                                   
         MVC   OFCOFC,QCLIENT+1                                                 
*                                                                               
         GOTO1 VOFFICER,DMCB,(C'2',OFCBLKC),(0,VCOMFACS)                        
*                                                                               
         LA    R1,OFCBLKC                                                       
*                                                                               
         MVC   SVOFCSTR,OFCOFC2    SAVE STARTING OFFICE                         
*                                                                               
         XC    OFCOFC2,OFCOFC2     INIT OFFICE BLOCK                            
         MVC   OFCOFC,QCLIENT+2                                                 
*                                                                               
         GOTO1 VOFFICER,DMCB,(C'2',OFCBLKC),(0,VCOMFACS)                        
*                                                                               
         LA    R1,OFCBLKC                                                       
*                                                                               
         MVC   SVOFCEND,OFCOFC2    SAVE END OF OFFICE RANGE                     
*                                                                               
         DROP  R1                                                               
*                                                                               
TSTCLI10 DS    0H                                                               
*                                                                               
         CLC   PCLTOFF,SVOFCSTR    CLIENT OFFICE MUST BE IN RANGE               
         BL    TSTCLIX3                                                         
         CLC   PCLTOFF,QCLIENT+2                                                
         BH    TSTCLIX3                                                         
*                                                                               
         B     TSTCLIOK                                                         
*                                                                               
TSTCLIX2 DS    0H                  FOR 'ALL BUT' REVERSE CC SETTING             
*                                                                               
         CLC   0(1,R1),QCLIENT+2                                                
         BE    TSTCLIX3                                                         
*                                                                               
TSTCLIOK DS    0H                                                               
*                                                                               
         SR    R0,R0                                                            
*                                                                               
         B     TSTCLIXX                                                         
*                                                                               
TSTCLIX3 DS    0H                                                               
*                                                                               
         LTR   RE,RE               SET NE CC                                    
*                                                                               
TSTCLIXX DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
*                   ROUTINE TO READ A PUBLICATION FROM CONTRACT/BUY             
         SPACE 3                                                                
GETPUB   DS    0H                                                               
         LA    RF,KEY+10                                                        
         CLI   KEY+3,X'20'                                                      
         BE    *+8                                                              
         LA    RF,KEY+7                                                         
         CLC   PUBKMED,KEY+2                                                    
        BNE   GETP0                                                             
         CLC   PUBKPUB(6),0(RF)                                                 
         BER   RE                  HAVE PUB                                     
GETP0    DS    0H                                                               
         XC    PUBSRTDA(8),PUBSRTDA                                             
         CLI   FCGTPUB,C'N'                                                     
         BER   RE                                                               
GETP     LR    R0,RE                                                            
         MVC   DUB(6),0(RF)                                                     
         MVC   WORK,KEY                                                         
         XC    KEY,KEY                                                          
         MVC   KEY(1),WORK+2       MEDIA                                        
         MVC   KEY+1(6),DUB    BUY RECORD                                       
         MVI   KEY+9,X'81'                                                      
         MVC   KEY+7(2),WORK       AGENCY                                       
         MVC   HALF(1),KEY+6       SAVE EDITION CODE                            
         MVC   KEYSAVE,KEY                                                      
         L     RF,HIGHPUB                                                       
         CLI   PAGYPROF+16,C'0'    TEST DEFAULT TO SRDS                         
         BNE   *+8                                                              
         L     RF,READPUB                                                       
         BASR  RE,RF                                                            
         CLC   KEYSAVE(10),KEY     DID WE HIT THE AGENCY OVERRIDE               
         BE    GETPUB02                                                         
         CLC   KEY+7(2),=C'ZZ'                                                  
         BNE   GETPUB01                                                         
         CLC   KEYSAVE(7),KEY      MAYBE WE LUCKED OUT WITH THE ZZ              
         BE    GETPUB02                                                         
         SPACE 2                                                                
GETPUB01 MVC   KEY,KEYSAVE         NO - THEN WE NEED TO READ FOR                
         MVC   KEY+7(2),=C'ZZ'          THE ZZ RECORD                           
         GOTO1 READPUB                                                          
         SPACE 2                                                                
GETPUB02 DS    0H                                                               
         MVC   PUBKEY,KEY                                                       
         MVC   PUBSRTDA,KEY+27                                                  
         CLI   FCGTPUB,C'L'                                                     
         BE    GTPUB02B                                                         
         GOTO1 GETNAME                                                          
GTPUB02B DS    0H                                                               
         OC    PUBNAME(118),SPACES                                              
         L     RF,ALTLREC                                                       
         XC    0(35,RF),0(RF)                                                   
         CLI   FCGTPUB,C'B'                                                     
         BE    GETPUB03            DONT NEED LTL REC                            
         MVC   KEY+7(2),WORK       AGENCY                                       
         MVI   KEY+9,X'85'                                                      
GTPUB02D GOTO1 HIGHPUB                                                          
         CLC   KEYSAVE(10),KEY                                                  
         BNE   GTPUB02E                                                         
         MVC   LTLSRTDA,KEY+27                                                  
         GOTO1 GETLTL                                                           
         CLI   KEY,C'O'          SEE IF OUTDOOR                                 
         BNE   GETPUB03                                                         
         L     RF,ALTLREC                                                       
         CLI   33(RF),0           SEE IF IT HAS ELEMS                           
         BNE   GETPUB03                                                         
         XC    LTLSRTDA,LTLSRTDA    CLEAR DISK ADDRESS                          
         B     GTPUB02F             AND LOOK FOR BASE PUB                       
*                                                                               
GTPUB02E DS    0H                                                               
         CLI   KEYSAVE,C'O'        SEE IF LOOKED FOR OUTDOOR                    
         BNE   GETPUB03                                                         
*                                                                               
GTPUB02F CLI   KEYSAVE+6,0       SEE IF I JUST LOOKED FOR BASE PUB              
         BE    GETPUB03          YES - THEN DONE                                
*                                                                               
         MVC   KEY,KEYSAVE         RESTORE KEY I WAS LOOKING FOR                
         MVI   KEY+6,0             CLEAR 'EDIT' CODE                            
         B     GTPUB02D            EDITION NOT FOUND                            
*                                  TRY FOR BASE PUB LTLREC                      
         SPACE 2                                                                
GETPUB03 DS    0H                                                               
         MVC   PUBKED,HALF         SET EDITION CODE                             
         L     RF,ALTLREC                                                       
         MVC   LTLKED-LTLREC(1,RF),HALF                                         
         SPACE 2                                                                
GETPUB2  MVC   KEY,WORK                                                         
         LR    RE,R0                                                            
         BR    RE                                                               
         EJECT                                                                  
*                   ROUTINE TO READ A CONTRACT FROM A BUY                       
         SPACE 3                                                                
READCONT NTR1                                                                   
         XC    WORK,WORK                                                        
         XC    DUB,DUB                                                          
         CLC   RCSVAGY(3),RCONSVAM                                              
         BNE   RCON1                                                            
         CLC   RCSVCLI,RCONSVCL                                                 
         BE    RCON2                                                            
         CLC   RCONSVCL,PCLTPROF+6                                              
         BE    RCON2                                                            
RCON1    DS    0H                                                               
         XC    RCONLO(18),RCONLO                                                
         XC    RCONSADV,RCONSADV                                                
         LA    R2,PCLTREC+33                                                    
RCON1B   CLI   0(R2),X'15'                                                      
         BE    RCON1C                                                           
         CLI   0(R2),0                                                          
         BE    RCON1D                                                           
         ZIC   R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     RCON1B                                                           
*                                                                               
RCON1C   MVC   RCONSADV,2(R2)       SAVE AOR DATA                               
         CLC   RCONSADV(2),RCSVAGY    SEE IF I AM THE AOR                       
         BE    RCON1D                                                           
         TM    RCONSADV+15,X'20'   SEE IF I HAVE MY OWN CONTRACTS               
         BNO   RCON1D                                                           
         L     RF,UTL                                                           
         MVC   RCONSSYS,4(RF)       SAVE THIS SYSTEM                            
         MVC   4(1,RF),RCONSADV+14   SWITCH TO AOR                              
**************                                                                  
**************  THIS CODE DOES NOT OPEN THE AOR FILE                            
**************  IT SHOULD BE OPENED BY THE APPLICATION PROGRAM                  
**************  OR ELSEWHWERE IN PPFILCON                                       
**************                                                                  
*                                                                               
RCON1D   DS    0H                                                               
         BAS   RE,RCONRD                                                        
RCON2    DS    0H                                                               
         MVC   RCONWPB,PBUYKPUB                                                 
****                                                                            
****     OLD AOR (DUPONT STYLE) DISABLED                                        
****                                                                            
****     TM    PCLTACTL,X'80'      TEST PUB CONV. FOR CROSS-AGY ADV             
****     BZ    RCON2F               NO                                          
***                                                                             
***      LA    R2,PUBREC+33                                                     
***N2B   DS    0H                                                               
***      CLI   0(R2),X'14'                                                      
***      BE    RCON2D                                                           
***      CLI   0(R2),0                                                          
***      BE    RCON2F                                                           
***N2C   ZIC   R0,1(R2)                                                         
***      AR    R2,R0                                                            
***      B     RCON2B                                                           
***                                                                             
***N2D   DS    0H                                                               
***      USING PUBREPEL,R2                                                      
***      CLC   PUBRPOFF,RCONSVCL                                                
***      BNE   RCON2C                                                           
***      MVC   RCONWPB,PUBCVEN                                                  
***                                                                             
***      DROP  R2                                                               
***                                                                             
****     END OF OLD AOR (DUPONT STYLE)                                          
***                                                                             
*                                                                               
RCON2F   DS    0H                                                               
         OC    RCONSADV,RCONSADV      SEE IF AOR SITUATION                      
         BZ    RCON2L                                                           
         CLC   RCONSADV(2),RCSVAGY   SEE IF I AM THE AOR                        
         BE    RCON2L                                                           
         TM    RCONSADV+15,X'20'    SEE IF I HAVE MY OWN CONTRACTS              
         BNO   RCON2L                                                           
         TM    RCONSADV+15,X'01'    SEE IF PUB CONVERSION REQUIRED              
         BNO   RCON2L                                                           
         LA    R2,PUBREC+33                                                     
RCON2G   CLI   0(R2),X'80'                                                      
         BE    RCON2I                                                           
         CLI   0(R2),0                                                          
         BE    RCON2L                                                           
RCON2H   ZIC   R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     RCON2G                                                           
*                                                                               
RCON2I   CLC   RCONSADV(2),5(R2)                                                
         BNE   RCON2H                                                           
         MVC   RCONWPB,7(R2)        AOR PUB CODE                                
*                                                                               
RCON2L   DS    0H                                                               
         CLC   RCONPUB,RCONWPB                                                  
         BNE   RCON3                                                            
*                                                                               
*NEW BELOW - TO ADDRESS PCONREC                                                 
         L     RF,PPWORK2C                                                      
         USING PPWORK2D,RF                                                      
         L     RE,ACONIO                                                        
         USING PCONREC,RE                                                       
*NEW ABOVE - TO ADDRESS PCONREC                                                 
*                                                                               
         CLC   RCONPUB,PCONKPUB                                                 
         BNE   RCONX               DONT LOOK FOR SAME CONTRACT TWICE            
*                                                                               
         DROP  RE,RF                                                            
*                                                                               
         BAS   RE,ANYCONT                                                       
*                               CHECK PCONREC TO SEE IF COVERS                  
*                               THE BUY, IF NOT MUST RESEARCH                   
*                               CONLIST SINCE ANYCONT BUMPS                     
*                               CONTRACT NUMBER WHICH I                         
*                               SHOULDN'T DO IN THIS CASE                       
*                               I MUST START AGAIN WITH FIRST CONTRACT          
         MVI   DUB+6,X'01'                                                      
RCON3    DS    0H                                                               
         MVC   RCONPUB,RCONWPB                                                  
*                                                                               
         CLC   RCONHI,RCONWPB                                                   
         BNH   RCON7                                                            
         L     R1,=V(CONLIST)                                                   
         MVC   DUB(6),RCONWPB                                                   
         GOTO1 BINSRCH,(R1),(2,DUB)                                             
*                                                                               
         L     R2,0(R1)                                                         
RCON4    DS    0H                                                               
         CLC   0(6,R2),RCONWPB                                                  
         BNE   RCONX                                                            
*                                                                               
         MVC   KEY+27(4),7(R2)                                                  
*                                                                               
         CLC   RCONSADV(2),RCSVAGY    SEE IF I AM THE AOR                       
         BE    RCON6                                                            
         TM    RCONSADV+15,X'20'   SEE IF I HAVE MY OWN CONTRACTS               
         BNO   RCON6                                                            
         L     RF,UTL                                                           
         MVC   RCONSSYS,4(RF)       SAVE THIS SYSTEM                            
         MVC   4(1,RF),RCONSADV+14   SWITCH TO AOR                              
RCON6    DS    0H                                                               
         GOTO1 GETCONT                                                          
*                                                                               
         CLC   RCONSADV(2),RCSVAGY    SEE IF I AM THE AOR                       
         BE    RCON6X                                                           
         TM    RCONSADV+15,X'20'   SEE IF I HAVE MY OWN CONTRACTS               
         BNO   RCON6X                                                           
         L     RF,UTL                                                           
         MVC   4(1,RF),RCONSSYS     RETURN TO MY SYSTEM                         
*                                                                               
RCON6X   DS    0H                                                               
         BAS   RE,ANYCONT                                                       
         LA    R2,11(R2)                                                        
         B     RCON4                                                            
*                                                                               
RCON7    DS    0H                                                               
*                                  CONTRACT PAST END OF TABLE                   
*                                  READ DIRECTLY FOR IT                         
         MVC   WORK(64),KEY                                                     
         XC    KEY,KEY                                                          
         MVC   KEY(3),RCSVAGY                                                   
****                                                                            
****     OLD AOR (DUPONT STYLE) DISABLED                                        
****                                                                            
****     CLI   PCLTAGYR,C' '       TEST CROSS-AGY ADV                           
****     BNH   *+10                                                             
****     MVC   KEY(2),PCLTAGYR     YES- USE AGY OF REC                          
****                                                                            
*                                                                               
         OC    RCONSADV,RCONSADV    SEE IF AOR SITUATION                        
         BZ    RCON7C                                                           
         CLC   RCONSADV(2),RCSVAGY  SEE IF I AM THE AOR                         
         BE    RCON7C                                                           
         TM    RCONSADV+15,X'20'    SEE IF I HAVE MY OWN CONTRACTS              
         BNO   RCON7C                                                           
         MVC   KEY(2),RCONSADV       READ AOR CONTRACT                          
*                                                                               
         MVC   KEY+4(3),RCONSADV+2       USE ADV FOR CLIENT                     
*                                                                               
         L     RF,UTL                                                           
         MVC   RCONSSYS,4(RF)       SAVE THIS SYSTEM                            
         MVC   4(1,RF),RCONSADV+14   SWITCH TO AOR                              
         B     RCON7E                                                           
*                                                                               
RCON7C   MVC   KEY+4(3),RCONSVCL                                                
RCON7E   MVI   KEY+3,X'10'                                                      
         MVC   KEY+7(6),RCONWPB                                                 
         GOTO1 HIGH                                                             
         B     RCON8B                                                           
RCON8    DS    0H                                                               
         GOTO1 SEQKSV                                                           
RCON8B   DS    0H                                                               
         CLC   KEY(13),KEYSAVE                                                  
         BNE   RCONX                                                            
         GOTO1 GETCONT                                                          
         BAS   RE,ANYCONT                                                       
         B     RCON8                                                            
*                                                                               
RCONX    DS    0H                                                               
*                                                                               
         OC    RCONSADV,RCONSADV     SEE IF AOR SITUATION                       
         BZ    RCONX5                                                           
         CLC   RCONSADV(2),RCSVAGY     SEE IF I AM THE AOR                      
         BE    RCONX5                                                           
         TM    RCONSADV+15,X'20' SEE IF I DON'T HAVE MY OWN CONTRACTS           
         BNO   RCONX5                                                           
         L     RF,UTL                                                           
         MVC   4(1,RF),RCONSSYS     SWITCH BACK TO MY SYSTEM                    
*                                                                               
RCONX5   DS    0H                                                               
         OC    WORK,WORK                                                        
         BZ    RCONX2                                                           
         MVC   KEY(64),WORK                                                     
         CLI   SORTSW,C'N'                                                      
         BNE   RCONX2                                                           
         GOTO1 HIGH                                                             
RCONX2   DS    0H                                                               
*                                    THIS RETURN TO MY SYS IS NEEDED            
*                                    SINCE RCONRD RETURNS HERE                  
         OC    RCONSADV,RCONSADV     SEE IF AOR SITUATION                       
         BZ    RCONXX                                                           
         CLC   RCONSADV(2),RCSVAGY     SEE IF I AM THE AOR                      
         BE    RCONXX                                                           
         TM    RCONSADV+15,X'20' SEE IF I DON'T HAVE MY OWN CONTRACTS           
         BNO   RCONXX                                                           
         L     RF,UTL                                                           
         MVC   4(1,RF),RCONSSYS     SWITCH BACK TO MY SYSTEM                    
*                                                                               
RCONXX   B     EXIT                                                             
         SPACE 3                                                                
RCONRD   NTR1                                                                   
         MVC   RCONSVAM,RCSVAGY                                                 
         MVC   RCONSVCL,RCSVCLI                                                 
         CLI   PCLTPROF+5,C'2'                                                  
         BNE   *+10                                                             
         MVC   RCONSVCL,PCLTPROF+6      MASTER CLIENT                           
         MVC   WORK(64),KEY                                                     
         XC    KEY,KEY                                                          
         MVC   KEY(3),RCSVAGY                                                   
****                                                                            
****     OLD AOR (DUPONT STYLE) DISABLED                                        
****                                                                            
****     CLI   PCLTAGYR,C' '       TEST CROSS-AGY ADV                           
****     BNH   *+10                                                             
****     MVC   KEY(2),PCLTAGYR     YES- USE AGY OF REC                          
****                                                                            
*                                                                               
         OC    RCONSADV,RCONSADV    SEE IF AOR SITUATION                        
         BZ    RCONRD3                                                          
         CLC   RCONSADV(2),RCSVAGY  SEE IF I AM THE AOR                         
         BE    RCONRD3                                                          
         TM    RCONSADV+15,X'20'    SEE IF I HAVE MY OWN CONTRACTS              
         BNO   RCONRD3                                                          
         MVC   KEY(2),RCONSADV       READ AOR CONTRACT                          
         MVC   KEY+4(3),RCONSADV+2       USE ADV AS CLIENT                      
         B     RCONRD3C                                                         
*                                                                               
RCONRD3  DS    0H                                                               
         MVC   KEY+4(3),RCONSVCL                                                
RCONRD3C MVI   KEY+3,X'10'                                                      
         L     R2,=V(CONLIST)                                                   
         XC    8(4,R2),8(R2)       CLEAR NO, OF ENTRIES                         
         LA    R3,24(R2)           POINT TO FIRST                               
         XC    RCONLO,RCONLO                                                    
         GOTO1 HIGH                                                             
         B     RCONR4B                                                          
RCONRD4  DS    0H                                                               
         GOTO1 SEQKSV                                                           
RCONR4B  DS    0H                                                               
         CLC   KEY(7),KEYSAVE                                                   
         BE    *+8                                                              
         MVI   KEY+7,X'FF'                                                      
         MVC   0(6,R3),KEY+7                                                    
         MVC   6(1,R3),KEY+14                                                   
         MVC   7(4,R3),KEY+27                                                   
         A     R3,12(R2)                                                        
         L     RF,8(R2)                                                         
         LA    RF,1(RF)                                                         
         ST    RF,8(R2)                                                         
         CLI   KEY+7,X'FF'                                                      
         BE    RCONRD8                                                          
         CLC   8(4,R2),20(R2)        TEST END                                   
         BL    RCONRD4             NO                                           
RCONRD8  DS    0H                                                               
         MVC   RCONHI,KEY+7                                                     
         MVC   KEY(64),WORK                                                     
         B     RCONX2                                                           
         SPACE 2                                                                
ANYCONT  DS    0H                                                               
*NEW BELOW - TO ADDRESS PCONREC                                                 
         L     RF,PPWORK2C                                                      
         USING PPWORK2D,RF                                                      
         L     R1,ACONIO                                                        
         USING PCONREC,R1                                                       
         DROP  RF                                                               
*NEW ABOVE - TO ADDRESS PCONREC                                                 
*                                                                               
         OC    RCONSADV,RCONSADV    SEE IF AOR SITUATION                        
         BZ    ANYCON5                                                          
         CLC   RCONSADV(2),RCSVAGY  SEE IF I AM THE AOR                         
         BE    ANYCON5                                                          
         TM    RCONSADV+15,X'20'    SEE IF I HAVE MY OWN CONTRACTS              
         BNO   ANYCON5                                                          
*                                                                               
         CLC   RCONSADV+2(3),PCONKCLT    SEE IF RIGHT CLIENT                    
         BNER  RE                                                               
         B     ANYCON7                                                          
*                                                                               
ANYCON5  DS    0H                                                               
         CLC   RCONSVCL,PCONKCLT                                                
         BNER  RE                                                               
ANYCON7  DS    0H                                                               
         CLC   RCONWPB(6),PCONKPUB                                              
         BNER  RE                                                               
         CLI   PCONPRD,C'A'                                                     
         BL    ANYCONT2            CLIENT CONTRACT                              
         CLC   PCONPRD,RCSVPRD     PRD CONTRACT MUST BE FOR THIS PRD            
         BNER  RE                                                               
ANYCONT2 DS    0H                                                               
         SPACE 2                                                                
         CLC   PBUYKDAT(3),PCONSTRT                                             
         BL    ANYCONT4            BUY DATE LO, KEEP LOOKING                    
         CLC   PBUYKDAT(3),PCONEND                                              
         BNH   RCONX                                                            
*                                                                               
ANYCONT4 IC    RF,PCONNUM+1                                                     
         LA    RF,1(RF)                                                         
         STC   RF,DUB+6            SET TO GET NEXT CONTRACT                     
         BR    RE                                                               
*                                                                               
         DROP  R1                                                               
*                                                                               
         SPACE 3                                                                
RCONSVAM DC    CL3' '                                                           
RCONSVCL DC    CL3' '                                                           
RCONSADV DC    XL18'00'                                                         
RCONSSYS DC    X'00'                                                            
RCONLO   DC    XL6'00'                                                          
RCONHI   DC    XL6'00'                                                          
RCONPUB  DC    XL6'00'                                                          
RCONWPB  DS    CL6                                                              
         EJECT                                                                  
*                                  GET JOB RECORD                               
READJOB  NTR1                                                                   
         CLI   PBDJOB,C' '                                                      
         BNH   RJOBX                                                            
         CLC   PBUYKEY(3),RJKEY                                                 
         BNE   RJ1                                                              
         CLC   PBUYKCLT,RJKCLT                                                  
         BE    RJ2                                                              
RJ1      DS    0H                                                               
         L     RE,=A(JLIST)                                                     
         ST    RE,ALJOB                                                         
         LA    RF,L'JLIST                                                       
         XCEF                                                                   
*                                                                               
RJ2      DS    0H                                                               
         MVC   RJKEY(10),PBUYKEY                                                
         MVC   RJKJOB,PBDJOB                                                    
         MVI   RJKCOD,X'15'                                                     
         CLC   RJKEY(25),PJOBREC                                                
         BE    RJOBX               HAVE RECORD                                  
         L     R2,=A(JLIST)                                                     
RJ3      DS    0H                                                               
         CLC   RJKPRD(9),0(R2)                                                  
         BE    RJ4                 REC IN TABLE                                 
         CLI   0(R2),0                                                          
         BE    RJ5                                                              
         LA    R2,LJLISTE(R2)                                                   
         B     RJ3                                                              
RJ4      DS    0H                                                               
         MVC   KEY+27(4),9(R2)                                                  
         GOTO1 GETJOB                                                           
         B     RJOBX                                                            
*                                                                               
RJ5      DS    0H                                                               
         MVC   WORK,KEY                                                         
         MVC   KEY(25),RJKEY                                                    
         GOTO1 READ                                                             
         GOTO1 GETJOB                                                           
         L     R2,ALJOB                                                         
         MVC   0(9,R2),RJKPRD                                                   
         MVC   9(4,R2),KEY+27                                                   
         LA    R2,LJLISTE(R2)                                                   
         C     R2,=A(JLISTX)                                                    
         BL    *+8                                                              
         L     R2,=A(JLIST)                                                     
         ST    R2,ALJOB                                                         
*                                                                               
         MVC   KEY(64),WORK                                                     
         CLI   SORTSW,C'N'                                                      
         BNE   RJOBX                                                            
         GOTO1 HIGH                                                             
RJOBX    DS    0H                                                               
         B     EXIT                                                             
         SPACE 3                                                                
RJKEY    DC    XL25'00'                                                         
         ORG   RJKEY                                                            
RJKAGM   DS    CL3                                                              
RJKCOD   DS    CL1                                                              
RJKCLT   DS    CL3                                                              
RJKPRD   DS    CL3                                                              
RJKJOB   DS    CL6                                                              
         DS    CL9                                                              
*                                                                               
ALJOB    DS    F                                                                
         EJECT                                                                  
*                   ROUTINE TO READ REP RECORD FROM PUBLICATION                 
         SPACE 3                                                                
READREP  DS    0H                                                               
         MVI   DOUBLE,0                                                         
         B     *+8                                                              
READREPI MVI   DOUBLE,1            READING REPS ON INPUT                        
         XC    WORK,WORK                                                        
*****    MVI   WORK,X'08'          PAY                                          
         MVI   ADRTYP,C'P'         PAY                                          
         CLI   FCGTREP,C'Y'                                                     
         BE    READREP0                                                         
*****    MVI   WORK,X'09'          TRA                                          
         MVI   ADRTYP,C'T'         TRA                                          
         CLI   FCGTTREP,C'Y'                                                    
         BE    READREP0                                                         
*****    MVI   WORK,X'0B'          SHIPPING ADDRESS                             
         MVI   ADRTYP,C'S'         SHIPPING                                     
         CLI   FCGTTREP,C'S'                                                    
         BE    READREP0                                                         
*****    MVI   WORK,X'0A'          CON                                          
         MVI   ADRTYP,C'C'         PAY                                          
         CLI   FCGTCREP,C'Y'                                                    
         BCR   7,RE                                                             
         SPACE 2                                                                
READREP0 NTR1                                                                   
         SPACE 2                                                                
*                                  TRY FOR ADDRESS FIRST                        
*****RREP0    SR    R0,R0                                                       
*****         LA    R2,PUBREC+33                                                
*****RREP1    DS    0H                                                          
*****         CLI   0(R2),0                                                     
*****         BE    RREP6                                                       
*****         CLC   0(1,R2),WORK                                                
*****         BE    RREP3                                                       
*****RREP2    DS    0H                                                          
*****         IC    R0,1(R2)                                                    
*****         AR    R2,R0                                                       
*****         B     RREP1                                                       
*****RREP3    DS    0H                                                          
*****         USING PUBAOVEL,R2                                                 
*****         CLC   PUBAOFF,=3X'FF'                                             
*****         BE    RREP4                                                       
*****         CLC   PUBAOFF,PCLTKCLT                                            
*****         BE    RREP4                                                       
*****         CLI   PUBAOFF,X'FF'                                               
*****         BNE   RREP2                                                       
*****         CLC   PUBAOFF+1(1),PCLTOFF                                        
*****         BNE   RREP2                                                       
*****RREP4    DS    0H                                                          
*****         MVC   WORK+1(3),PUBAOFF                                           
*****         ST    R2,FULL             SAVE A(ELEM)                            
*****         CLI   WORK,X'0B'          SEE IF LOOKING FOR SHIPPING             
*****         BE    RREP20              DON'T TRY FOR REP                       
*                                                                               
*****         CLI   WORK+1,X'FF'        IF HAVE CLIENT ADDR NO NEED             
*****         BL    RREP20              TO LOOK FOR REP                         
*****         B     RREP6C                                                      
*                                                                               
*****RREP6    DS    0H                                                          
*****         CLI   WORK,X'0B'       SEE IF I WAS TRYING FOR SHIPPING           
*****         BNE   RREP6C                                                      
*****         MVI   WORK,X'09'       YES AND NOT FOUND - THEN TRY FOR           
*****         B     RREP0               TRAFFIC                                 
*                                                                               
*                                    FILL IN 7 BYTES OF CLTDATA                 
         MVC   CLTDATA(3),PCLTKAGY   CLIENT AGY/MED                             
         MVC   CLTCODE,PCLTKCLT      CLIENT CODE                                
         MVC   CLTOFF,PCLTOFF        CLIENT OFFICE                              
*                                                                               
         GOTO1 =V(PPGETADR),DMCB,(ADRTYP,CLTDATA),PUBREC,DATAMGR,0              
*                                                                               
         CLI   0(R1),X'FF'         ERROR IN CALL ?                              
         BNE   *+6                 NO                                           
         DC    H'0'                                                             
*                                                                               
         MVC   WORK(4),0(R1)       1=REC CODE, 2-4 = ADDRESS LEVEL              
*                                                                               
         L     R2,4(R1)            A(ADDRESS INFO FROM CALL)                    
         ST    R2,FULL             SAVE A(ADDRESS)                              
*                                                                               
         CLI   WORK,0              ADDRESS RECORD FOUND ?                       
         BE    RREP6               NO - TRY FOR A REP                           
         CLI   WORK,X'0B'          SHIPPING ADDRESS ?                           
         BE    RREP20              YES - ALWAYS USE IF FOUND                    
         CLI   WORK+1,X'FF'        IF HAVE CLIENT ADDR NO NEED                  
         BL    RREP20              TO LOOK FOR REP                              
*                                                                               
*                                  NOW TRY FOR A REP                            
RREP6    LA    R2,PUBREC+33                                                     
         XC    DUB,DUB                                                          
         SR    R0,R0                                                            
RREP7    DS    0H                                                               
         CLI   0(R2),0                                                          
         BE    RREP11                                                           
         CLI   0(R2),X'14'                                                      
         BE    RREP9                                                            
RREP8    DS    0H                                                               
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     RREP7                                                            
RREP9    DS    0H                                                               
         USING PUBREPEL,R2                                                      
         CLC   PUBRPOFF,=3X'FF'                                                 
         BE    RREP10                                                           
         CLC   PUBRPOFF,PCLTKCLT                                                
         BE    RREP10                                                           
         CLI   PUBRPOFF,X'FF'                                                   
         BNE   RREP8                                                            
         CLC   PUBRPOFF+1(1),PCLTOFF                                            
         BNE   RREP8                                                            
RREP10   DS    0H                                                               
         OC    PUBPAREP(12),PUBPAREP                                            
         BZ    RREP8               NO OVERIDES THIS ELEM                        
         LA    RF,PUBPAREP                                                      
         CLI   FCGTREP,C'Y'                                                     
         BE    RREP10B                                                          
         LA    RF,PUBTRREP                                                      
         CLI   FCGTTREP,C'Y'                                                    
         BE    RREP10B                                                          
         CLI   FCGTTREP,C'S'       OR SHIPPING - USE TRAFFIC REP                
         BE    RREP10B                                                          
         LA    RF,PUBCNREP                                                      
RREP10B  DS    0H                                                               
         MVC   DUB(4),0(RF)                                                     
         MVC   DUB+4(3),PUBRPOFF                                                
RREP11   DS    0H                                                               
         OC    WORK+1(3),WORK+1                                                 
         BZ    RREP12                                                           
         CLC   WORK+1(3),DUB+4     TEST 'LEVEL'                                 
         BL    RREP20              ADDR MORE SPECIFIC                           
*                                                                               
*  NOTE - THE ABOVE BL IS A BNH IN ALL PROGRAMS USING PUB ADDRESSES             
*         EXCEPT PPREP7702 AND THIS ONE. THIS MEANS THAT IF THE 'LEVEL'         
*         IS EQUAL, A REP ADDRESS WOULD BE USED IN THIS PROGRAM AND             
*         IN PPREP7702 AND A PUB ADDRESS OVERRIDE WOULD BE USED IN ALL          
*         OTHER PROGRAMS. THE MANUAL ("SPECIAL PUB ADDRESSES FOR                
*         CLIENTS/OFFICES") INDICATES THAT THE PUB ADDRESS IS THE ONE           
*         THAT SHOULD BE USED.                                                  
*                                                                               
RREP12   DS    0H                                                               
         OC    DUB(4),DUB                                                       
         BZ    RREP20              NO REP                                       
*                                                                               
         LR    R0,RE                                                            
         MVC   WORK,KEY                                                         
         XC    KEY,KEY                                                          
         MVC   KEY(2),PUBKEY+7     AGENCY                                       
         MVC   KEY+2(1),PUBKEY     MEDIA                                        
         MVI   KEY+3,X'11'                                                      
         MVC   KEY+4(4),DUB        REP CODE                                     
         CLC   KEY(10),PREPKEY     DONT READ IF ALREADY THERE                   
         BE    RREP18                                                           
         XC    PREPKEY(200),PREPKEY                                             
         GOTO1 HIGH                                                             
         CLC   KEYSAVE(10),KEY                                                  
         BNE   RREP18                                                           
         GOTO1 GETREP                                                           
         B     RREP19                                                           
*                                                                               
RREP18   DS    0H                                                               
*        DIDN'T READ ANYTHING SO RESTORE KEY AND EXIT                           
         MVC   KEY,WORK                                                         
         B     RREPX               REP ALREADY THERE                            
*                                                                               
RREP19   MVC   KEY,WORK            RESTORE KEY                                  
         CLI   DOUBLE,1            REPS ON INPUT-RESTORE SEQ READ               
         BE    RREP19C                                                          
         CLI   SORTSW,C'N'         REPS ON OUTPUT-ONLY RESTORE READ             
*                                  IF NOT SORTING                               
         BNE   RREPX                                                            
RREP19C  GOTO1 HIGH                                                             
         B     RREPX                                                            
         SPACE 3                                                                
RREP20   DS    0H                                                               
         XC    PREPREC(200),PREPREC                                             
         CLI   WORK+1,0                                                         
         BE    RREPX            NO ADDR ELEM                                    
         L     R2,FULL                                                          
         USING PGETADRD,R2                                                      
         MVC   PREPELEM(2),PGADELEM                                             
         MVC   PREPNAME(122),PGADNAME   (NAME,LIN1+2,ATTN,TELE)                 
*                                                                               
*        ALL ELEMENTS SHOULD BE THE SAME LENGTH NOW                             
*                                                                               
*****    CLI   PGADELEM+1,165      SEE IF "SHORT" ELEMENT                       
*****    BL    RREP20T             NO FAX OR EMAIL                              
*                                                                               
         OC    PGADFAX,SPACES                                                   
         CLC   PGADFAX,SPACES      ANY FAX ?                                    
         BNH   RREP20T             NO                                           
         MVC   PREPLIN3(L'PGADLIN3),PGADLIN3                                    
         MVC   PREPFAX(L'PGADFAX),PGADFAX                                       
*                                                                               
RREP20T  MVC   PREPKEY(3),RCSVAGY                                               
*                                                                               
         DROP  R2                                                               
*                                                                               
RREPX    DS    0H                                                               
         B     EXIT                                                             
*                                                                               
ADRTYP   DS    CL1          TYPE OF ADDRESS REC - PAY, TRAFFIC, ETC.            
CLTDATA  DS    0CL7                                                             
CLTAGY   DS    CL2                                                              
CLTMED   DS    CL1                                                              
CLTCODE  DS    CL3                                                              
CLTOFF   DS    CL1                                                              
         EJECT                                                                  
*                   READ ALL CLIENTS FOR MEDIA                                  
         SPACE 3                                                                
CLILIST  XC    KEY,KEY                                                          
         MVC   KEY(3),RCSVAGY                                                   
         MVI   KEY+3,X'02'                                                      
         CLC   QCLIENT,=C'ALL'                                                  
         BE    CLILIST1                                                         
         CLI   QCLIENT,C'*'                                                     
         BE    CLILIST1                                                         
         CLI   QCLIENT,C'&&'                                                    
         BE    CLILIST1                                                         
         MVC   KEY+4(3),QCLIENT                                                 
         GOTO1 HIGH                                                             
         CLC   KEY(25),KEYSAVE                                                  
         BNE   NEXTREQ                                                          
         B     CLILST2B                                                         
*                                                                               
CLILIST1 DS    0H                                                               
         GOTO1 HIGH                                                             
         B     CLIL2A                                                           
CLILIST2 DS    0H                                                               
         GOTO1 SEQKSV                                                           
CLIL2A   DS    0H                                                               
         CLC   KEY(4),KEYSAVE                                                   
         BNE   CLILIST4                                                         
CLILST2B DS    0H                                                               
         GOTO1 GETCLI                                                           
         CLI   QCLIENT,C'&&'       TEST OFFICE REQUEST                          
         BE    CLILST2D                                                         
         CLI   QCLIENT,C'*'        TEST OFFICE REQUEST                          
         BNE   CLILIST3                                                         
CLILST2D BAS   RE,TSTCLI                                                        
         BNE   CLILIST2                                                         
CLILIST3 DS    0H                                                               
         MVC   RCSVCLI,PCLTKCLT                                                 
         MVI   MODE,PROCCLI                                                     
         BAS   RE,GO                                                            
         CLC   QCLIENT,=C'ALL'                                                  
         BE    CLILIST2                                                         
         CLI   QCLIENT,C'*'                                                     
         BE    CLILIST2                                                         
         CLI   QCLIENT,C'&&'                                                    
         BE    CLILIST2                                                         
         SPACE 2                                                                
CLILIST4 MVI   MODE,LCLIREQ                                                     
         BAS   RE,GO                                                            
         B     NEXTREQ                                                          
         EJECT                                                                  
*                   READ DISTRICTS FOR A CLIENT                                 
         SPACE 3                                                                
DISTLIST XC    KEY,KEY                                                          
         MVC   KEY(7),RCSVAGY                                                   
         MVI   KEY+3,X'05'                                                      
         GOTO1 HIGH                                                             
         CLC   KEY(7),KEYSAVE                                                   
         BNE   DISTL6                                                           
         MVI   MODE,FBUYCLI                                                     
         BAS   RE,GO                                                            
         SPACE 2                                                                
DISTL2   DS    0H                                                               
         MVI   MODE,FBUYDIV                                                     
         BAS   RE,GO                                                            
         SPACE 2                                                                
DISTL4   GOTO1 GETDIST                                                          
         MVI   MODE,PROCDST                                                     
         BAS   RE,GO                                                            
         GOTO1 SEQKSV                                                           
         CLC   KEYSAVE(10),KEY                                                  
         BE    DISTL4                                                           
         MVI   MODE,LBUYDIV                                                     
         BAS   RE,GO                                                            
         CLC   KEYSAVE(7),KEY                                                   
         BE    DISTL2                                                           
         SPACE 2                                                                
DISTL6   DS    0H                                                               
         MVI   MODE,LBUYCLI                                                     
         BAS   RE,GO                                                            
         EJECT                                                                  
*                   READ PUBLICATION RECORDS FOR AGENCY                         
         SPACE 3                                                                
PUBLIST  DS    0H                                                               
*                                                                               
         CLI   QPUB,C'0'           IF PUB GIVEN                                 
         BNL   ONEPUB              JUST DO IT                                   
         XC    PUBKEY,PUBKEY                                                    
PUBL2    DS    0H                                                               
         GOTO1 =A(NXTPUB)                                                       
         CLI   PUBKEY,X'FF'                                                     
         BE    LASTREQ                                                          
         MVI   MODE,PROCPUB                                                     
         BAS   RE,GO                                                            
         B     PUBL2                                                            
         EJECT                                                                  
*              READ JUST ONE PUBLICATION                                        
         SPACE 3                                                                
ONEPUB   DS    0H                                                               
         CLC   QPUB+8(3),=C'ZZZ'     SEE IF DOING ALL ZONES/EDTS                
         BE    ONEPUB20                                                         
*                                                                               
ONEPUB5  XC    KEY,KEY             FAKE A BUY                                   
         MVC   KEY(2),QAGENCY                                                   
         MVC   KEY+2(1),QMEDIA                                                  
         MVI   KEY+3,X'20'                                                      
         GOTO1 PUBVAL,DMCB,QPUB,KEY+10                                          
         MVI   FCGTPUB,C'Y'                                                     
         BAS   RE,GETPUB                                                        
         MVI   MODE,PROCPUB                                                     
         BAS   RE,GO                                                            
         B     LASTREQ                                                          
*                                                                               
ONEPUB20 DS    0H             ALL ZONES/EDTS OF ONE PUB                         
         MVC   WORK(11),SPACES                                                  
         MVC   WORK(8),QPUB                                                     
         XC    PUBKEY,PUBKEY                                                    
         GOTO1 PUBVAL,DMCB,WORK,PUBKEY+1                                        
         MVC   PUBKEY+5(2),=H'-1'   SO NEXTPUB WILL START WITH 0                
*                                                                               
ONEPUB25 GOTO1 =A(NXTPUB)                                                       
         CLI   PUBKEY,X'FF'         END OF PUBS                                 
         BE    LASTREQ                                                          
         CLC   THISPUB(4),LASTPUB      CHECK FOR CORRECT BASE NUMBER            
         BNE   LASTREQ                                                          
         MVI   MODE,PROCPUB                                                     
         BAS   RE,GO                                                            
         B     ONEPUB25                                                         
         EJECT                                                                  
*                   READ LIST RECORDS FOR A CLIENT                              
         SPACE 3                                                                
LISTLIST XC    KEY,KEY                                                          
         MVC   KEY(7),RCSVAGY                                                   
         MVI   KEY+3,X'17'                                                      
         GOTO1 HIGH                                                             
         CLC   KEYSAVE(7),KEY                                                   
         BNE   LISTL4                                                           
         SPACE 2                                                                
LISTL2   GOTO1 GETLIST                                                          
         MVI   MODE,PROCLST                                                     
         BAS   RE,GO                                                            
         GOTO1 SEQKSV                                                           
         CLC   KEYSAVE(7),KEY                                                   
         BE    LISTL2                                                           
         SPACE 2                                                                
LISTL4   DS    0H                                                               
         MVI   MODE,LBUYREQ                                                     
         CLC   QCLIENT(3),=C'ALL'                                               
         BE    NEXTCLI                                                          
         CLI   QCLIENT,C'*'        OFFICE REQUEST                               
         BE    NEXTCLI                                                          
         CLI   QCLIENT,C'$'        OFFICE LIST                                  
         BE    NEXTCLI                                                          
         B     LASTREQ                                                          
         EJECT                                                                  
*                   ROUTINE TO GO TO APPLICATION PROGRAMS                       
         SPACE 2                                                                
GO       NTR                                                                    
         CLI   RCTRACE,C'N'                                                     
         BE    GO2                                                              
*                                                                               
         L     RF,PPWORK2C                                                      
         USING PPWORK2D,RF                                                      
         LA    R2,P                                                             
         CLI   RC2DSECT,C'Y'                                                    
         BNE   *+8                                                              
         LA    R2,P1                                                            
*                                                                               
         DROP  RF                                                               
*                                                                               
         MVC   0(132,R2),SPACES                                                 
         MVC   0(10,R2),=C'MODE=X''  '''                                        
         MVI   SKIPSPEC,C'Y'                                                    
         GOTO1 HEXOUT,WORK,MODE,7(R2),1,=C'N'                                   
         EDIT  (B1,MODE),(3,11(R2))                                             
         MVC   20(3,R2),=C'OF='                                                 
         MVC   24(1,R2),RCSVOFC                                                 
         MVC   26(3,R2),=C'CL='                                                 
         MVC   29(3,R2),RCSVCLI                                                 
         MVC   35(8,R2),=C'QCNTLDT='               ************                 
         GOTO1 HEXOUT,WORK,QCNTLDT,43(R2),3,=C'N'  ************                 
         GOTO1 REPORT                                                           
         SPACE 2                                                                
GO2      DS    0H                                                               
         MVC   DMCB+4(8),SORTPARS  PASS TO APPL PROG FOR USER SORT              
         GOTO1 PPAPPLIC,DMCB,PPWORKC                                            
         SPACE 2                                                                
GO20     DS    0H                                                               
         CLI   MODE,LBUYREQ        CHECK TO GO ON TO NEXT REQ                   
         BNE   GOEND                                                            
         SPACE 2                                                                
         L     RD,4(RD)            UNWIND WITHOUT EXIT                          
         LM    RE,RC,12(RD)                                                     
         B     NEXTREQ                                                          
         SPACE 2                                                                
         SPACE 2                                                                
GOEND    DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
*                                  SORT ROUTINES                                
         SPACE 2                                                                
TOSORT   NTR1                                                                   
         SPACE 2                                                                
         OC    SRTCOUNT,SRTCOUNT   TEST FIRST                                   
         BNZ   TS4                                                              
*                                  FIRST TIME                                   
         MVI   SORTSW,C'C'                                                      
*                                                                               
         MVC   SRTNEXT,SRTPARS+4                                                
         L     RF,=V(SRTTABLX)                                                  
         S     RF,SRTPARS+4                                                     
         SR    RE,RE                                                            
         D     RE,SRTRLEN                                                       
         ST    RF,SRTMAX           MAX RECS IN CORE                             
*                                                                               
TS4      DS    0H                                                               
         CLI   RCTRACE,C'N'                                                     
         BE    *+12                                                             
         LA    R1,=CL16'SORT TRACE (IN) '                                       
         BAS   RE,TRCESRT                                                       
         CLI   SRTCOUNT,X'FF'                                                   
         BE    TS10                USING SORT                                   
*                                                                               
*                                  ADD TO CORE TABLE                            
TS4B     DS    0H                                                               
         GOTO1 BINSRCH,SRTPARS,(1,SRTREC)                                       
*                                                                               
         OC    SRTPARS+1(3),SRTPARS+1    TEST TABLE FULL                        
         BZ    TS5                       YES                                    
*                                                                               
         CLI   0(R1),1                                                          
         BE    TSX                                                              
*                                  DUPE KEY                                     
         L     RF,0(R1)                                                         
         A     RF,16(R1)                                                        
         BCTR  RF,R0               POINT TO LAST BYTE OD EKY                    
         CLI   0(RF),255           MAX OF 255 WITH SAME KEY                     
         BE    TS5                                                              
         IC    RF,0(RF)                                                         
         LA    RE,SRTREC                                                        
         A     RE,16(R1)                                                        
         BCTR  RE,R0                                                            
         LA    RF,1(RF)                                                         
         STC   RF,0(RE)            BUMP REC NO.                                 
         B     TS4B                TRY AGAIN TO ADD IT                          
*                                                                               
TS5      DS    0H                                                               
*                                  CORE TABLE FULL - USE SORT NOW               
         L     R0,SRTRLEN                                                       
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  SRTSRLN,DUB                                                      
         L     R0,SRTKLEN                                                       
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  SRTSKLN,DUB                                                      
         MVI   SORTSW,C'S'                                                      
TS6      DS    0H                                                               
         GOTO1 SORTER,DMCB,SRTSORT,SRTRCDC,(45,SORTLOC)                         
TS7      DS    0H                  PASS CORE TABLE TO SORT                      
         L     R2,SRTPARS+4                                                     
         L     R3,SRTCOUNT                                                      
TS8      DS    0H                                                               
         GOTO1 SORTER,DMCB,=C'PUT',(R2)                                         
*                                                                               
         A     R2,SRTRLEN                                                       
         BCT   R3,TS8                                                           
         MVI   SRTCOUNT,X'FF'                                                   
TS10     DS    0H                                                               
         GOTO1 SORTER,DMCB,=C'PUT',SRTREC                                       
*                                                                               
TSX      DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
*                   LITERAL POOL FOR PROGRAM                                    
         SPACE 3                                                                
         LTORG                                                                  
*                                                                               
MYWORK   DS    CL20                                                             
OFCBLKC  DS    CL64                FOR OFFICER CALLS                            
VOFFICER DS    V                   ADDRESS OF OFFICER                           
*                                                                               
         DS    0D                                                               
LASTPUB  DC    XL6'00'                                                          
THISPUB  DC    XL6'00'                                                          
*                                                                               
SVCRDAT  DS    CL3                                                              
SVCIRCNM DS    PL5                                                              
SVOFCSTR DS    CL2                 START OF OFFICE RANGE                        
SVOFCEND DS    CL2                 END   OF OFFICE RANGE                        
         EJECT                                                                  
*                                  GET FIRST/NEXT PUB                           
         SPACE 3                                                                
FRSORT   NTR1 BASE=*,LABEL=*                                                    
         OC    SRTCOUNT,SRTCOUNT                                                
         BZ    FSEND                                                            
         CLI   SRTCOUNT,X'FF'      TEST USING SORT                              
         BE    FS4                 YES                                          
*                                  USE CORE TABLE                               
         L     R2,SRTNEXT                                                       
         MVC   SRTREC,0(R2)                                                     
         A     R2,SRTRLEN                                                       
         ST    R2,SRTNEXT                                                       
         L     R2,SRTCOUNT                                                      
         BCTR  R2,R0                                                            
         ST    R2,SRTCOUNT                                                      
         B     FSX                                                              
*                                                                               
FS4      DS    0H                  GET FROM SORT                                
         GOTO1 SORTER,DMCB,=C'GET'                                              
*                                                                               
         L     R2,DMCB+4                                                        
         LTR   R2,R2                                                            
         BZ    FSEND                                                            
         L     RF,SRTRLEN                                                       
         BCTR  RF,R0                                                            
         EX    RF,*+8                                                           
         B     FSX                                                              
         MVC   SRTREC(0),0(R2)                                                  
*                                                                               
FSEND    DS    0H                                                               
         MVI   SRTREC,X'FF'                                                     
         B     FSXX                                                             
FSX      DS    0H                                                               
         CLI   RCTRACE,C'N'                                                     
         BE    FSXX                                                             
         LA    R1,=CL16'SORT TRACE (OUT)'                                       
         BAS   RE,TRCESRT                                                       
FSXX     DS    0H                                                               
         XIT1                                                                   
*                                                                               
         DROP  RB                                                               
         SPACE 3                                                                
TRCESRT  DS    0H                                                               
*                                                                               
* THIS ROUTINE IS BASELESS!!!                                                   
*                                                                               
         LR    R0,RE               SAVE RE                                      
*                                                                               
         L     RF,PPWORK2C                                                      
         LA    RF,P1-PPWORK2D(,RF)                                              
         CLI   RC2DSECT,C'Y'                                                    
         JE    *+8                                                              
         LA    RF,P                                                             
         MVC   0(16,RF),0(R1)      R1 = STRING PASSED IN                        
*                                                                               
         LA    RF,20(RF)                                                        
         GOTOR HEXOUT,DMCB,SRTREC,(RF),48,0                                     
         MVI   SKIPSPEC,C'Y'                                                    
         GOTOR REPORT                                                           
*                                                                               
         LR    RE,R0               RESTORE RE                                   
         BR    RE                                                               
         SPACE 2                                                                
NXTPUB   DS    0D                                                               
         NMOD1 0,NXTPUB                                                         
*** NEW                                                                         
         L     RC,PPFILEC                                                       
*NOP*    L     RC,=V(PPFILCC)                                                   
*** NEW                                                                         
         MVC   LASTPUB,PUBKEY+1                                                 
         XC    PUBSRTDA(8),PUBSRTDA                                             
         L     RF,ALTLREC                                                       
         XC    0(50,RF),0(RF)                                                   
         CLI   QPUB+1,C'L'           TEST LIST MODE                             
         BE    NP40                                                             
         CLI   QPUB+1,C'P'           TEST PUBLISHER MODE                        
         BE    NP200                                                            
*                                  SEQ FILE READING                             
         LH    RF,LASTPUB+4        BUMP TO NEXT PUB                             
         LA    RF,1(RF)                                                         
         STH   RF,LASTPUB+4                                                     
         XC    KEY,KEY                                                          
         MVC   KEY(1),QMEDIA                                                    
         MVC   KEY+1(6),LASTPUB                                                 
         MVC   KEY+7(2),QAGENCY                                                 
         XC    THISPUB,THISPUB                                                  
         GOTO1 HIGHPUB                                                          
         B     NP3B                                                             
NP3      DS    0H                                                               
         GOTO1 SEQPUB                                                           
         CLC   KEY(1),KEYSAVE       SEE IF AT END OF MEDIA                      
         BE    NP4                                                              
         OC    THISPUB,THISPUB     DO I HAVE SOMETHING                          
         BZ    NP3D                NO  - GO SET END OF PUBS                     
         B     NP16                DONE WITH THIS PUB                           
*                                                                               
NP3B     DS    0H                                                               
*                                                                               
         CLC   KEY(1),KEYSAVE      END OF MEDIA                                 
         BE    NP4                                                              
NP3D     DS    0H                                                               
         MVI   PUBKEY,X'FF'        SET END                                      
         B     NP16                                                             
NP4      DS    0H                                                               
         TM    KEY+25,X'01'        TEST PASSIVE                                 
         BNZ   NP3                 YES - SKIP                                   
         OC    THISPUB,THISPUB     HAVE I FOUND ANY FOR THIS AGY                
         BZ    NP6                 NO                                           
         CLC   THISPUB,KEY+1                                                    
         BNE   NP16                DONE WITH THIS PUB NUM                       
*                                                                               
NP6      DS    0H                                                               
         CLI   KEY+9,X'81'                                                      
         BE    NP8                                                              
         CLI   KEY+9,X'85'                                                      
         BNE   NP3                                                              
*                                                                               
         CLC   KEY+7(2),QAGENCY                                                 
         BNE   NP3                                                              
         MVC   LTLSRTDA,KEY+27     SAVE DA                                      
         CLI   FCGTPUB,C'B'                                                     
         BE    NP7                                                              
         GOTO1 GETLTL                                                           
NP7      DS    0H                                                               
         CLC   THISPUB,KEY+1                                                    
         BNE   NP3                 NEED ZZ 81                                   
         B     NP16                HAVE 81 + 85 - DONE                          
NP8      DS    0H                                                               
         CLC   KEY+7(2),QAGENCY                                                 
         BNE   NP10                                                             
         MVC   THISPUB,KEY+1                                                    
         MVC   PUBKPUB(6),KEY+1                                                 
         MVC   PUBSRTDA,KEY+27     SAVE DA                                      
         CLI   FCGTPUB,C'L'        TEST NEED NAME                               
         BE    NP3                                                              
         GOTO1 GETNAME                                                          
         B     NP3                                                              
NP10     DS    0H                                                               
         CLC   KEY+7(2),=C'ZZ'                                                  
         BNE   NP3                                                              
         CLI   PAGYPROF+16,C'1'                                                 
         BNE   NP3                                                              
         CLC   THISPUB,KEY+1                                                    
         BE    NP16                                                             
         MVC   PUBKPUB(6),KEY+1                                                 
         MVC   PUBSRTDA,KEY+27                                                  
         CLI   FCGTPUB,C'L'        TEST NEED NAME                               
         BE    NP16                 NO                                          
         GOTO1 GETNAME                                                          
*                                                                               
NP16     DS    0H                                                               
         B     NPEXT                                                            
         SPACE 3                                                                
*                                  LIST RECORD MODE                             
NP40     DS    0H                                                               
         OC    LASTPUB,LASTPUB     FIRST TIME TEST                              
         BNZ   NP60                                                             
NP41     DS    0H                  FIRST/NEXT LIST RECORD                       
         BAS   RE,NPGETL                                                        
         BNE   NP3D                END                                          
NP42     DS    0H                  HAVE LIST ELEM                               
         L     R2,LISTNXT                                                       
         USING PLISPBEL,R2                                                      
         XC    KEY,KEY                                                          
         MVC   KEY(1),QMEDIA                                                    
         MVC   KEY+1(6),PLISPUB                                                 
         MVC   KEY+7(2),QAGENCY                                                 
         MVI   KEY+9,X'81'                                                      
         XC    PUBSRTDA(8),PUBSRTDA                                             
NP46     DS    0H                                                               
         GOTO1 HIGHPUB                                                          
NP46B    DS    0H                                                               
         CLC   KEY(7),KEYSAVE                                                   
         BNE   NP58                                                             
         CLC   KEY(10),KEYSAVE                                                  
         BE    NP50                                                             
         CLC   KEY(9),KEYSAVE                                                   
         BNE   NP47                                                             
         CLI   KEY+9,X'85'                                                      
         BNE   NP47                                                             
         MVC   LTLSRTDA,KEY+27          SAVE LTLREC DA                          
         CLI   FCGTPUB,C'B'        TEST NEED LTL REC                            
         BE    NP47                NO                                           
         GOTO1 GETLTL                                                           
         OC    PUBSRTDA,PUBSRTDA   TEST HAVE NAME REC                           
         BNZ   NPEXT               YES                                          
NP47     DS    0H                                                               
         CLI   PAGYPROF+16,C'1'         TEST STANDARD FILE OK                   
         BNE   NP60                                                             
         MVC   KEYSAVE+7(2),=C'ZZ'                                              
         CLC   KEY(10),KEYSAVE                                                  
         BE    NP50                                                             
         MVC   KEY,KEYSAVE              TRY FOR ZZ                              
         B     NP46                                                             
*                                                                               
NP50     DS    0H                                                               
         MVC   PUBKPUB(6),KEY+1                                                 
         MVC   PUBSRTDA,KEY+27     SAVE DA                                      
         CLI   FCGTPUB,C'L'                                                     
         BE    NP51                                                             
         GOTO1 GETNAME                                                          
NP51     DS    0H                                                               
*                                                                               
         CLI   FCGTPUB,C'B'        TEST NEED LTL REC                            
         BE    NPEXT                                                            
*                                                                               
         OC    LTLSRTDA,LTLSRTDA                                                
         BNZ   NPEXT               ALREADY HAVE LTL REC                         
         XC    KEY,KEY                                                          
         MVC   KEY(1),QMEDIA                                                    
         MVC   KEY+1(6),PLISPUB                                                 
         MVC   KEY+7(2),QAGENCY                                                 
         MVI   KEY+9,X'85'        LOOK FOR LITTLE REC                           
         GOTO1 HIGHPUB                                                          
         CLC   KEY(10),KEYSAVE                                                  
         BNE   NPEXT                                                            
         MVC   LTLSRTDA,KEY+27                                                  
         B     NPEXT                                                            
*                                                                               
*                                                                               
NP58     DS    0H                                                               
         OC    PUBSRTDA,PUBSRTDA   TEST HAVE PUB                                
         BNZ   NPEXT                                                            
*                                                                               
NP60     DS    0H                                                               
         L     R2,LISTNXT                                                       
         BAS   RE,NPNXTEL                                                       
         BNE   NP41                                                             
         ST    R2,LISTNXT                                                       
         B     NP42                                                             
*                                                                               
NPEXT    DS    0H                                                               
         XIT1                                                                   
         SPACE 3                                                                
*                                       GET LIST RECORD                         
NPGETL   NTR1                                                                   
         OC    LASTPUB,LASTPUB                                                  
         BNZ   NPGL4                                                            
*                                  FIRST TIME BUILD KEY                         
         XC    KEY,KEY                                                          
         MVC   KEY(3),RCSVAGY                                                   
         MVI   KEY+3,X'17'                                                      
         MVC   KEY+4(3),QCLIENT                                                 
         MVC   KEY+7(3),QPUB+3                                                  
         GOTO1 HIGH                                                             
         B     NPGL5                                                            
NPGL4    DS    0H                                                               
         L     RF,ALISREC                                                       
         MVC   KEY(25),0(RF)                                                    
         GOTO1 HIGH                                                             
         GOTO1 SEQKSV                                                           
*                                                                               
NPGL5    DS    0H                                                               
         CLC   KEY(10),KEYSAVE                                                  
         BE    NPGL5B                                                           
         MVI   PUBKEY,X'FF'        EOF                                          
         XC    LASTPUB,LASTPUB                                                  
         LTR   RE,RE               SET CC                                       
         B     NPGLX                                                            
*                                                                               
NPGL5B   DS    0H                                                               
         GOTO1 GETLIST                                                          
*                                                                               
         L     R2,ALISREC                                                       
         LA    R2,33(R2)                                                        
         CLI   0(R2),X'20'                                                      
         BE    NPGL6                                                            
         BAS   RE,NPNXTEL                                                       
         BNE   NPGL4                                                            
*                                                                               
NPGL6    DS    0H                                                               
         ST    R2,LISTNXT                                                       
         SR    R0,R0                                                            
NPGLX    DS    0H                                                               
         XIT1                                                                   
         SPACE 3                                                                
NPNXTEL  DS    0H                                                               
         SR    R0,R0                                                            
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         CLI   0(R2),0                                                          
         BE    NPNXTEL2                                                         
         CLI   0(R2),X'20'                                                      
         BER   RE                                                               
         B     NPNXTEL                                                          
NPNXTEL2 DS    0H                                                               
         LTR   R2,R2                                                            
         BR    RE                                                               
         SPACE 3                                                                
*                                  PUBLISHER RECORD MODE                        
NP200    DS    0H                                                               
         XC    KEY,KEY                                                          
         MVC   KEY(2),=C'0P'       REC CODE AND PUBLISHER SUB-TYPE              
         MVC   KEY+2(3),QAGENCY    AGENCY AND MEDIA                             
         MVC   KEY+5(4),QPUB+3     PUBLISHER                                    
         OC    LASTPUB,LASTPUB     FIRST TIME ?                                 
         BZ    NP210               YES                                          
         LH    RF,LASTPUB+4        NO - BUMP TO NEXT PUB                        
         LA    RF,1(RF)                                                         
         STH   RF,LASTPUB+4                                                     
*                                                                               
NP210    DS    0H                  GET PUBLISHER PASSIVE POINTER                
         MVC   KEY+10(6),LASTPUB                                                
         MVC   KEYSAVE,KEY                                                      
         GOTO1 HIGHPUB                                                          
         CLC   KEY(9),KEYSAVE                                                   
         BNE   NP3D                DONE WITH THIS PUBLISHER (END)               
*                                                                               
******                      HAVE PASSIVE POINTER                                
*                                                                               
NP240    DS    0H                                                               
         XC    PUBSRTDA(8),PUBSRTDA                                             
         XC    THISPUB,THISPUB                                                  
         MVC   THISPUB,KEY+10                                                   
         MVC   PUBSRTDA,KEY+27     SAVE DA                                      
         GOTO1 GETNAME             GET PUB RECORD                               
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY(25),AREC                                                     
         MVI   KEY+9,X'85'                                                      
         MVC   KEYSAVE,KEY                                                      
         GOTO1 HIGHPUB             GET LITTLE REC                               
         CLC   KEY(10),KEYSAVE                                                  
         BNE   NP290               DONE WITH THIS PUB                           
         MVC   LTLSRTDA,KEY+27     SAVE DA                                      
         GOTO1 GETLTL                                                           
*                                                                               
NP290    DS    0H                                                               
         B     NPEXT                                                            
         SPACE 3                                                                
         LTORG                                                                  
LISTNXT  DS    F                                                                
         SPACE 3                                                                
         LTORG                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*        GO STRAIGHT TO NEXT REQUEST                                            
         SPACE 2                                                                
ENDREQ   DS    0D                                                               
         USING *,RF                                                             
         L     RA,=V(PPWORKC)                                                   
         L     RD,BASERD                                                        
         LM    RE,RC,12(RD)        DONT CLOBBER RF - IT'S THE BASE REG          
         B     NEXTREQ                                                          
         DROP  RF                                                               
*                                                                               
BASERD   DS    F                                                                
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*                             GET SHARE FOR REGION/DISTRICT                     
*                                                                               
*                                  THIS ROUTINE FORCES THE SHARES FOR           
*                                  EACH REG/DIST TO ADD TO THE TOTAL.           
*                                  THE LOGIC PARALELLS THAT FOR POOL            
*                                  ALLOCATION IN GETINS.                        
RDSPLIT  DS    0D                                                               
         NMOD1 0,RDSPLIT                                                        
*** NEW                                                                         
         L     RC,PPFILEC                                                       
*NOP*    L     RC,=V(PPFILCC)                                                   
*** NEW                                                                         
         SPACE 2                                                                
         CLI   QPROG,C'7'          NO REG/DSTS FOR IO PROGS                     
         BE    RDSPLITX                                                         
         L     RF,0(R1)                                                         
         MVC   DOUBLE(6),0(RF)     REG/DIST                                     
         OC    DOUBLE(6),SPACES                                                 
****                                                                            
         MVC   RDDIFS(48),GROSS         GROSS AND PAID + TAX                    
         MVC   RDDIFS+48(16),BGROSS     BILLED DATA                             
         XC    RDBLSUMS,RDBLSUMS                                                
****                                                                            
         XC    RDSUMS,RDSUMS                                                    
         XC    FULL,FULL                                                        
         MVC   RDCLTDIV(3),PCLTKCLT     SET CLT/DIV                             
         MVC   RDCLTDIV+3(3),PPRDDIV                                            
         CLC   QPUB+1(3),=C'RD='        REQUESTED REG/DST OVERRIDE              
         BNE   *+16                                                             
         MVC   RDCLTDIV(3),QPUB+4                                               
         MVC   RDCLTDIV+3(3),=C'000'                                            
*                                                                               
*                                  FIRST PASS                                   
         L     R2,ALTLREC                                                       
         LA    R2,33(R2)                                                        
         CLI   0(R2),X'71'                                                      
         BE    RDSP4                                                            
RDSP3    DS    0H                                                               
         BAS   RE,RDNXTEL                                                       
         BNE   RDSP8                                                            
RDSP4    DS    0H                                                               
         USING PUBDSTEL,R2                                                      
         CLC   PUBDCLT(6),RDCLTDIV                                              
         BNE   RDSP3                                                            
         XC    FULL,FULL                                                        
         MVC   FULL+2(2),PUBDSHR                                                
         LA    R4,GROSS                                                         
         LA    R3,RDDIFS                                                        
****                                                                            
         ZAP   COUNT,=P'1'                                                      
****                                                                            
RDSP5    DS    0H                                                               
         L     R1,0(R4)                                                         
         LTR   R1,R1                                                            
         BZ    RDSP6                                                            
         M     R0,FULL                                                          
         BAS   RE,RDDIV            ROUNDED DIVISION                             
         L     R0,0(R3)                                                         
         SR    R0,R1                                                            
         ST    R0,0(R3)                                                         
RDSP6    DS    0H                                                               
****                                                                            
         CP    COUNT,=P'16'                                                     
         BE    RDSP6E                                                           
         AP    COUNT,=P'1'                                                      
         B     RDSP6C                                                           
*                                                                               
RDSP6C   LA    R4,4(R4)                                                         
         LA    R3,4(R3)                                                         
RDSP6D   B     RDSP5               NEXT AMOUNT                                  
RDSP6E   B     RDSP3               NEXT SHARE                                   
****                                                                            
*                                                                               
*                                                                               
RDSP8    DS    0H                                                               
         OC    FULL,FULL                                                        
         BZ    RDSPLITX            NO SHARES                                    
*                                  END OF PASS 1                                
*                                  NOW RDDIFS HAS DIFFERENCES                   
*                                  BETWEEN ORIGINAL AMOUNTS AND                 
*                                  SUM OF ROUNDED SHARES                        
*                                                                               
*                                                                               
*                                  PASS 2                                       
         L     R2,ALTLREC                                                       
         LA    R2,33(R2)                                                        
         CLI   0(R2),X'71'                                                      
         BE    RDSP10                                                           
RDSP9    DS    0H                                                               
         BAS   RE,RDNXTEL                                                       
         BNE   RDSP30                                                           
RDSP10   DS    0H                                                               
         CLC   PUBDCLT(6),RDCLTDIV                                              
         BNE   RDSP9                                                            
*                                  IS THIS A SELECTED REG/DST                   
         MVI   DOUBLE+6,0               NO                                      
         CLC   DOUBLE(3),=C'ALL'                                                
         BE    RDSP11                                                           
         CLC   DOUBLE(3),SPACES                                                 
         BE    RDSP11                                                           
         CLC   DOUBLE(3),PUBDREG                                                
         BH    RDSP13                                                           
         BL    RDSP30                                                           
RDSP11   DS    0H                                                               
         CLC   DOUBLE+3(3),=C'ALL'                                              
         BE    RDSP12                                                           
         CLC   DOUBLE+3(3),SPACES                                               
         BE    RDSP12                                                           
         CLC   DOUBLE+3(3),PUBDDST                                              
         BH    RDSP13                                                           
         BL    RDSP30                                                           
RDSP12   DS    0H                                                               
         MVI   DOUBLE+6,1               YES                                     
RDSP13   DS    0H                                                               
         MVC   FULL+2(2),PUBDSHR                                                
         LA    R4,GROSS                                                         
         LA    R3,RDDIFS                                                        
****                                                                            
         ZAP   COUNT,=P'1'                                                      
****                                                                            
RDSP13A  DS    0H                                                               
         OC    0(4,R4),0(R4)                                                    
         BZ    RDSP20              NO AMOUNT                                    
         OC    0(4,R3),0(R3)                                                    
         BNZ   RDSP14                                                           
*                                  NO DIFF                                      
         CLI   DOUBLE+6,0          TEST IF A SELECTED REG/DST                   
         BE    RDSP20              NO                                           
         L     R1,0(R4)                                                         
         M     R0,FULL             YES COMPUTE SHARE                            
         BAS   RE,RDDIV                                                         
*                                  AND ADD TO RDSUMS                            
RDSP13B  DS    0H                                                               
****                                                                            
         A     R1,64(R3)                                                        
         ST    R1,64(R3)                                                        
****                                                                            
         B     RDSP20                                                           
RDSP13D  DS    0H                                                               
         CLI   DOUBLE+6,0          TEST IF A SELECTED REG/DST                   
         BE    RDSP20              NO                                           
         B     RDSP13B             YES                                          
*                                  DIFFERENCE NO-ZERO                           
RDSP14   DS    0H                                                               
         L     R1,0(R4)                                                         
         M     R0,FULL             SHARE                                        
         D     R0,=F'10000'        TRUNCATED DIVISION                           
         LTR   R0,R0               TEST REMAINDER                               
         BZ    RDSP13D             ZERO                                         
         BM    RDSP17              NEG                                          
*                                                                               
*                                  AMOUNT POS                                   
         TM    0(R3),X'80'         TEST DIFF                                    
         BNZ   RDSP15                                                           
*                                  AMOUNT POS, DIFF POS                         
         C     R0,=F'5000'         TEST IF ROUNDED DOWN                         
         BNL   RDSP13D             NO                                           
         L     RF,0(R3)            YES - SUBTRACT 1 FROM DIFF                   
         BCTR  RF,R0                                                            
         ST    RF,0(R3)                                                         
         A     R1,=F'1'            ADD 1 TO AMOUNT                              
         B     RDSP13D                                                          
*                                  AMOUNT POS, DIFF NEG                         
RDSP15   DS    0H                                                               
         C     R0,=F'5000'         TEST IF ROUNDED UP                           
         BL    RDSP13D             NO                                           
         L     RF,0(R3)            YES - ADD 1 TO DIFF                          
         A     RF,=F'1'                                                         
         ST    RF,0(R3)                                                         
         B     RDSP13D                                                          
*                                                                               
*                                  AMOUNT NEG                                   
RDSP17   DS    0H                                                               
         TM    0(R3),X'80'         TEST DIFF                                    
         BZ    RDSP18                                                           
*                                  AMOUNT NEG, DIFF NEG                         
         C     R0,=F'-5000'       TEST IF ROUNDED UP                            
         BL    RDSP13D             NO                                           
         L     RF,0(R3)            YES - ADD 1 TO DIFF                          
         A     RF,=F'1'                                                         
         ST    RF,0(R3)                                                         
         BCTR  R1,R0               SUBTRACT 1 FROM AMT                          
         B     RDSP13D                                                          
*                                  AMOUNT NEG, DIFF POS                         
RDSP18   DS    0H                                                               
         C     R0,=F'-5000'        TEST IF ROUNDED DOWN                         
         BNL   RDSP13D             NO                                           
         L     RF,0(R3)            YES - SUB 1 FROM DIFF                        
         BCTR  RF,R0                                                            
         ST    RF,0(R3)                                                         
         B     RDSP13D                                                          
*                                                                               
*                                                                               
RDSP20   DS    0H                                                               
****                                                                            
         CP    COUNT,=P'16'                                                     
         BE    RDSP25                                                           
         AP    COUNT,=P'1'                                                      
*                                                                               
RDSP23   LA    R4,4(R4)                                                         
         LA    R3,4(R3)                                                         
RDSP24   B     RDSP13A             NEXT AMOUNT                                  
RDSP25   B     RDSP9               NEXT SHARE                                   
****                                                                            
*                                                                               
RDSP30   DS    0H                                                               
         MVC   GROSS(48),RDSUMS      GROSS, PAID +TAX                           
         MVC   BGROSS(16),RDBLSUMS                                              
*                                                                               
*                                                                               
RDSPLITX DS    0H                                                               
         XIT1                                                                   
         SPACE 3                                                                
RDNXTEL  DS    0H                                                               
         SR    R0,R0                                                            
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         CLI   0(R2),0                                                          
         BE    RDNXTEL2                                                         
         CLI   0(R2),X'71'                                                      
         BER   RE                                                               
         B     RDNXTEL+2                                                        
RDNXTEL2 LTR   R2,R2                                                            
         BR    RE                                                               
         SPACE 3                                                                
RDDIV    DS    0H                                                               
         SLDA  R0,1                                                             
         D     R0,=F'10000'                                                     
         LTR   R1,R1                                                            
         BNP   *+8                                                              
         A     R1,=F'1'                                                         
         SRA   R1,1                                                             
         BR    RE                                                               
*                                                                               
         DS    0F                                                               
****                                                                            
RDDIFS   DS    CL64                                                             
RDSUMS   DS    CL48                                                             
RDBLSUMS DS    CL16                                                             
****                                                                            
RDCLTDIV DS    CL6                                                              
COUNT    DC    PL2'0'                                                           
         SPACE 3                                                                
         LTORG                                                                  
         EJECT                                                                  
         EJECT                                                                  
*****************************************************                           
* SUBROUTINE TO FILTER ESTIMATES FOR  BUYS OR BILLS *                           
*****************************************************                           
         SPACE 1                                                                
FLTREST  NMOD1 0,FLTREST                                                        
         L     RC,0(R1)                                                         
*                                                                               
         LA    R2,KEY                                                           
         MVC   DUB(2),19(R2)       BUY REC EST 20 OR 21 POINTER                 
         CLI   3(R2),X'20'                                                      
         BE    FE1                                                              
         CLI   3(R2),X'21'                                                      
         BE    FE1                                                              
         MVC   DUB(2),10(R2)       BILL REC EST OR NON-BUY                      
*                                                                               
FE1      CLI   QEST,C' '                                                        
         BE    FE2                                                              
         CLC   QEST,=C'ALL'                                                     
         BE    FE2                                                              
*                                  ONE EST                                      
         CLC   DUB(2),BQEST                                                     
         BL    FENO                                                             
         BE    FE2                                                              
*                                                                               
         CLI   QESTEND,C' '        OR GROUP                                     
         BE    FENO                                                             
         CLC   DUB(2),BQESTEND                                                  
         BH    FENO                                                             
         SPACE 1                                                                
* TEST ESTIMATE IN TABLE ALREADY *                                              
         SPACE 1                                                                
FE2      DS    0H                                                               
         OC    DUB(2),DUB         MAY NOT HAVE AND EST NUMBER FOR               
         BZ    FEYES              SOME OLD BILLS, SO SKIP EST READING           
*                                                                               
         LA    R3,NEWEST                                                        
         USING FELISTD,R3                                                       
*                                 BUILD TABLE ENTRY                             
         MVC   FELPRD,7(R2)                                                     
         CLI   3(R2),X'21'                                                      
         BNE   *+10                                                             
         MVC   FELPRD,13(R2)      21 POINTER HAS PRD IN KEY+13                  
         MVC   FELEST,DUB                                                       
         MVI   FELIND,0                                                         
* INSERT ENTRY IN TABLE IF NOT THERE                                            
         GOTO1 BINSRCH,FEPARS,(X'01',NEWEST)                                    
         OC    1(3,R1),1(R1)       TEST ROOM IN TABLE                           
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     R3,0(R1)            POINT TO ENTRY                               
*                                                                               
         TM    FELIND,FELIREAD     TEST READ YET                                
         BZ    FE6                 NO - READ IT                                 
*                                                                               
FE5      CLC   QESTEND,SPACES      CHK FOR FILTER                               
         BE    FE20                                                             
         CLI   QEST,C'0'                                                        
         BNL   FE20                MUST BE ONE OR RANGE OF ESTS                 
* MUST BE ESTIMATE FILTER *                                                     
         TM    FELIND,FELIFLTR     ELSE TEST PASSED FILTER                      
         BO    FE20                YES - CONTINUE                               
         B     FENO                                                             
         SPACE 1                                                                
* READ ESTIMATE HEADER *                                                        
         SPACE 1                                                                
FE6      MVC   WORK(64),KEY        SAVE KEY/KEYSAVE                             
         XC    KEY,KEY                                                          
         MVC   KEY(7),WORK         A/M/TY/CLT                                   
         MVI   KEY+3,X'07'                                                      
         MVC   KEY+7(5),NEWEST     MOVE PRD/EST                                 
         CLC   KEY(12),PESTKEY     ALREADY HAVE RECORD                          
         BE    FE8                 YES                                          
*                                                                               
         GOTO1 READ                                                             
         GOTO1 GETEST                                                           
*                                                                               
         MVC   KEY,WORK            RESTORE DIR FOR SEQ                          
         GOTO1 HIGH                                                             
*                                                                               
FE8      MVC   KEY(64),WORK       RESTORE KEY AND KEYSAVE                       
         OI    FELIND,FELIREAD    SET EST READ FLAG                             
         TM    PESTTEST,X'80'                                                   
         BZ    *+8                                                              
         OI    FELIND,FELITEST    SET TEST EST IND                              
*                                                                               
         CLC   QESTEND,SPACES     SEE IF USING EST FILTERS                      
         BE    FE20               NO                                            
         CLI   QEST,C'0'                                                        
         BNL   FE20               NO - QESTEND IS END OF SERIES                 
* FILTER ESTIMATE *                                                             
         LA    RE,QESTEND                                                       
         LA    RF,PESTGRPS                                                      
         LA    R0,3                                                             
*                                                                               
FE10     DS    0H                                                               
         CLI   0(RE),C'*'                                                       
         BE    FE14                                                             
         CLI   0(RE),C' '                                                       
         BE    FE14                                                             
*                                                                               
         TM    0(RE),X'40'         TEST NEGATIVE FILTER                         
         BZ    FE12                YES                                          
*                                                                               
         CLC   0(1,RE),0(RF)       POSITIVE FILTER                              
         BNE   FENO                                                             
         B     FE14                                                             
*                                                                               
FE12     DS    0H                                                               
         MVC   DUB(1),0(RE)                                                     
         OI    DUB,X'40'                                                        
         CLC   DUB(1),0(RF)        NEGATIVE FILTER                              
         BE    FENO                                                             
*                                                                               
FE14     DS    0H                                                               
         LA    RE,1(RE)                                                         
         LA    RF,1(RF)                                                         
         BCT   R0,FE10                                                          
*                                                                               
         OI    FELIND,FELIFLTR     SET IND FOR PASSED FILTER                    
*                                                                               
FE20     CLC   QEST,=C'ALL'        TEST EST=ALL                                 
         BNE   FE22                                                             
         OC    RQBILLDT,RQBILLDT   TEST BILL DATE FILTER                        
         BZ    FE22                                                             
         TM    FELIND,FELIBLLD     TEST BILLED TODAY                            
         BZ    FENO                                                             
*                                                                               
FE22     TM    FELIND,FELITEST     SEE IF TEST ESTIMATE                         
         BZ    FEYES                                                            
         DROP  R3                                                               
**                                                                              
         CLI   FCRDTEST,C'Y'                                                    
         BE    FEYES               MEANS PASS TEST BUYS                         
**                                                                              
         CLC   QPROG,=C'41'        ALWAYS SHOW TEST ESTS ON PP41                
         BE    FEYES                                                            
         CLC   QPROG,=C'49'        OR PP49                                      
         BE    FEYES                                                            
         CLC   QPROG,=C'92'        OR PP92 CLOSE-OUTS                           
         BE    FEYES                                                            
         CLC   QPROG,=C'F3'        OR PPF3 BUCKET UPDATE                        
         BE    FEYES                                                            
         CLC   QPROG,=C'F4'        OR PPF4 BUCKET CREATION                      
         BE    FEYES                                                            
         CLI   QEST,C'0'           OR IF REQ FOR ONE EST                        
         BL    FENO                                                             
         CLC   QESTEND,SPACES                                                   
         BNE   FENO          ** ONLY PASS TEST ESTIMATES IF REQ                 
*                               FOR ONE ESTIMATE                                
*                            ** IF BILLING NEVER PASS TEST ESTS                 
*                                                                               
         CLC   QPROG,=C'B1'                                                     
         BE    FENO            NO TEST EST FOR BILLING                          
         CLC   QPROG,=C'R1'                                                     
         BE    FENO            NO TEST EST FOR REBATE BILLING                   
         CLC   QPROG,=C'RD'                                                     
         BE    FENO            NO TEST EST FOR REBATE BILLING                   
         CLC   QPROG,=C'D1'     OR DRAFT BILLING                                
         BE    FENO                                                             
         CLC   QPROG,=C'06'      OR OLD DETAIL BILLING                          
         BE    FENO                                                             
         CLC   QPROG,=C'04'      OR OLD ORIG BILLING                            
         BE    FENO                                                             
         CLC   QPROG,=C'72'      OR INSERTION ORDERS                            
         BE    FENO                                                             
*                                                                               
FEYES    SR    R0,R0                                                            
         B     FEX                                                              
*                                                                               
FENO     LTR   RE,RE                                                            
*                                                                               
FEX      DS    0H                                                               
         XIT1                                                                   
*                                                                               
NEWEST   DS    CL10                                                             
         SPACE 2                                                                
FEPARS   DC    A(0)                                                             
         DC    A(FELIST)                                                        
FECOUNT  DC    F'0'                NUMBER OF ENTRIES SO FAR                     
         DC    A(FELISTL)          REC LENGTH                                   
         DC    AL1(0),AL3(5)       KEY DSPL/LENGTH                              
FEMAX    DC    A(0)                MAX ENTRIES                                  
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
*  SUBROUTINE TO CHECK IF PRODUCT IS IN THE REQUESTED GROUP                     
*                                                                               
**********************************************************************          
FLTPGRP  NMOD1 0,FLTPGRP                                                        
         L     RC,0(R1)                                                         
*                                                                               
*                                                                               
         MVC   FLTPKEYS(64),KEY    SAVE KEY/KEYSAVE                             
         XC    KEY,KEY                                                          
         MVC   KEY(3),RCSVAGY                                                   
         MVI   KEY+3,X'3B'                                                      
         MVC   KEY+4(3),QCLIENT                                                 
         MVC   KEY+7(3),FLTPKEYS+7   PRODUCT                                    
*                                                                               
         CLI   FLTPKEYS+3,X'21'      SEE IF X'21' BUY POINTER                   
         BNE   *+10                                                             
         MVC   KEY+7(3),FLTPKEYS+13                                             
*                                                                               
         OC    KEY+7(6),SPACES       SPACE FILL                                 
*                                                                               
         L     RF,PPWORK2C                                                      
         USING PPWORK2D,RF                                                      
         MVC   KEY+13(1),Q2USER+24   SCHEME CODE                                
         DROP  RF                                                               
*                                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(14),KEYSAVE       CHECK THRU SCHEME                          
         BNE   FLTPNO                                                           
         MVC   WORK(4),SPACES                                                   
         LA    R1,4                                                             
*                                                                               
         L     RF,PPWORK2C                                                      
         USING PPWORK2D,RF                                                      
         LA    R2,Q2USER+25        PRDGROUP IN SECOND CARD                      
         CLI   Q2USER+25,C' '    NO GROUP - HAS PASSED SCHEME CHECK             
         BNH   FLTPYES                                                          
         CLI   Q2USER+25,C'*'    NO GROUP - HAS PASSED SCHEME CHECK             
         BNH   FLTPYES                                                          
*                                                                               
         DROP  RF                                                               
*                                                                               
         LA    R3,WORK                                                          
         MVC   WORK(5),=C'00000'                                                
FLTP3    CLI   0(R2),C'*'          STOP SCANNING                                
         BE    FLTP5                                                            
         CLI   0(R2),C'0'          STOP SCANNING                                
         BL    FLTP5                                                            
         MVC   0(1,R3),0(R2)       MOVE DIGIT INTO WORK                         
         LA    R2,1(R2)                                                         
         LA    R3,1(R3)                                                         
         BCT   R1,FLTP3                                                         
*                                                                               
FLTP5    DS    0H                                                               
         LTR   R1,R1          MEANS I HAD 4 DIGITS                              
         BNZ   FLTP10                                                           
         PACK  DUB(3),WORK(5)      DO EXTRA BYTE FOR PWOS                       
         CLC   KEY+14(2),DUB       CHECK FOR MATCH                              
         BE    FLTPYES                                                          
         B     FLTPNO                                                           
*                                                                               
FLTP10   DS    0H                                                               
         CH    R1,=H'1'      MEANS I HAD 3 DIGITS                               
         BNE   FLTP15                                                           
         MVC   PCHECK(2),KEY+14                                                 
         NI    PCHECK+1,X'F0'     TURN OFF LAST HALF                            
         PACK  DUB(3),WORK(5)     ONE EXTRA FOR PWOS                            
         CLC   PCHECK(2),DUB                                                    
         BE    FLTPYES                                                          
         B     FLTPNO                                                           
*                                                                               
FLTP15   CH    R1,=H'2'    MEANS I HAD 2 DIGITS                                 
         BNE   FLTP20                                                           
         PACK  DUB(2),WORK(3)                                                   
         CLC   KEY+14(1),DUB                                                    
         BE    FLTPYES                                                          
         B     FLTPNO                                                           
*                                                                               
FLTP20   DS    0H                                                               
         CH    R1,=H'3'      MEANS I HAD 1 DIGIT                                
         BNE   FLTP25                                                           
         MVC   PCHECK(1),KEY+14                                                 
         NI    PCHECK,X'F0'    TURN OFF LAST HALF                               
         PACK  DUB(2),WORK(3)  ONE EXTRA FOR PWOS                               
         CLC   PCHECK(1),DUB                                                    
         BE    FLTPYES                                                          
         B     FLTPNO                                                           
*                                                                               
FLTP25   DC    H'0'             SHOULDN'T HAPPEN                                
*                                                                               
FLTPYES  MVI   FLTPSW,C'Y'              PROPER GROUP                            
         B     *+8                                                              
*                                                                               
FLTPNO   MVI   FLTPSW,C'N'              NOT IN GROUP                            
*                                                                               
         MVC   KEY(32),FLTPKEYS                                                 
         GOTO1 HIGH                     RESTORE KEY                             
         MVC   KEYSAVE(32),FLTPKEYS+32  AND KEYSAVE TO ORIGINAL                 
*                                                                               
         CLI   FLTPSW,C'Y'     SETS CC CODE                                     
         XIT1                                                                   
         LTORG                                                                  
FLTPKEYS DS    CL64                                                             
FLTPSW   DS    CL1                                                              
PCHECK   DS    CL2                                                              
         EJECT                                                                  
**********************************************************************          
* SUBROUTINE TO READ BILL RECORDS TO DETERMINE IF ANY BILLING TODAY  *          
* FOR THE REQUESTED ESTIMATES AND TO SET CONTROL CHANGE DATE.        *          
* EXIT WITH CC EQ IF YES OR NEQ IF NO   -- UNLESS QEST=ALL           *          
* IF QEST=ALL THEN ESTFLTR LIST ENTRIES ARE CREATED (FELIST) INSTEAD *          
* IF QEST=ALL SETTING OF FLTBYORN IS IGNORED ON EXIT                            
**********************************************************************          
         SPACE 2                                                                
FLTBILL  NMOD1 0,FLTBILL                                                        
         L     RC,0(R1)                                                         
*                                                                               
         XC    FLTBCNTL,FLTBCNTL                                                
         MVI   FLTBYORN,C'N'                                                    
         MVI   FLTBANY,C'N'                                                     
*                                                                               
         MVC   FLTBKEYS(64),KEY    SAVE KEY/KEYSAVE                             
         XC    KEY+10(22),KEY+10   CLEAR PAST PRODUCT                           
         MVI   KEY+3,X'08'         SET FOR BILLREC                              
         CLI   QPRODUCT,C' '       TEST PRD = NO                                
         BE    FBL20                                                            
         CLC   QPRODUCT,=C'ZZZ'    OR ZZZ                                       
         BE    FBL20                                                            
         SPACE 1                                                                
* SINGLE PRODUCT *                                                              
         SPACE 1                                                                
         MVC   KEY+10(2),BQEST                                                  
FBL2     GOTO1 HIGH                                                             
         B     FBL6                                                             
*                                                                               
FBL4     GOTO1 SEQ                                                              
*                                                                               
FBL6     CLC   KEY(10),KEYSAVE     A-M/TY/C/P                                   
         BNE   FBLX                                                             
         OC    BQEST,BQEST         TEST EST = ALL/NO                            
         BZ    FBL10                                                            
         CLC   KEY(12),KEYSAVE     A/M/TY/C/P/E                                 
         BE    FBL8                                                             
         OC    BQESTEND,BQESTEND   TEST SERIES REQUEST                          
         BZ    FBLX                NO                                           
         CLC   KEY+10(2),BQESTEND  TEST PAST END OF SERIES                      
         BH    FBLX                                                             
*                                                                               
FBL8     DS    0H                                                               
         GOTO1 GETBILL                                                          
         BAS   RE,TSTBILL          TEST BILLING/CONTROL DATES                   
         B     FBL4                                                             
         EJECT                                                                  
* ESTIMATE = ALL/NO *                                                           
         SPACE 1                                                                
FBL10    DS    0H                                                               
         GOTO1 GETBILL                                                          
         CLC   QEST,=C'ALL'        TEST EST=ALL                                 
         BE    FBL14               YES                                          
         BAS   RE,FBLFLT           EST=NO - FILTER                              
         BNE   FBL12               FAILED - NEXT ESTIMATE                       
         BAS   RE,TSTBILL          TEST BILLING/CONTROL DATES                   
         B     FBL4                AND GET NEXT BILL                            
*                                                                               
FBL12    MVC   KEY+12(4),=4X'FF'   FORCE NEXT EST                               
         B     FBL2                 AND READ HIGH                               
         SPACE 1                                                                
* EST=ALL - NEED TO ADD ENTRY TO FELIST *                                       
         SPACE 1                                                                
FBL14    MVC   KEYSAVE,KEY         SAVE CURRENT KEY                             
*                                                                               
FBL16    BAS   RE,TSTBILL          TEST BILLING/CONTROL DATES                   
         GOTO1 SEQ                                                              
         CLC   KEY(12),KEYSAVE     A/M/TY/C/P/E                                 
         BE    FBL10               YES - READ BILL RECORD                       
         XC    WORK,WORK                                                        
         MVC   WORK(5),KEYSAVE+7                                                
         CLI   FLTBYORN,C'Y'       TEST BILLED TODAY                            
         BNE   *+8                                                              
         MVI   WORK+5,FELIBLLD     SET BILLED TODAY FLAG                        
*                                                                               
         L     R1,=A(FEPARS)                                                    
         GOTO1 BINSRCH,(R1),(X'01',WORK)                                        
         OC    1(3,R1),1(R1)       TEST ROOM IN TABLE                           
         BNZ   *+6                                                              
         DC    H'0'                                                             
         MVI   FLTBYORN,C'N'       RESET SWITCH                                 
         B     FBL6                 CONTINUE PROCESSING                         
         EJECT                                                                  
* READ BILLS FOR ALL PRODUCTS *                                                 
         SPACE 1                                                                
FBL20    DS    0H                                                               
         XC    KEY+7(14),KEY+7     CLEAR PAST CLIENT                            
*                                                                               
FBL22    GOTO1 HIGH                                                             
         B     FBL26                                                            
*                                                                               
FBL24    GOTO1 SEQ                                                              
*                                                                               
FBL26    CLC   KEY(7),KEYSAVE      SAME A/M/TY/CLT                              
         BNE   FBLX                                                             
         OC    BQEST,BQEST         TEST EST=ALL/NO                              
         BZ    FBL40                                                            
         CLC   KEY+10(2),BQEST     TEST REACHED LOW EST                         
         BE    FBL30                                                            
         BH    FBL28                                                            
         MVC   KEY+10(2),BQEST     READ FOR LOW EST                             
         B     FBL22                                                            
*                                                                               
FBL28    OC    BQESTEND,BQESTEND   TEST SERIES REQUEST                          
         BZ    FBL30                                                            
         CLC   KEY+10(2),BQESTEND  TEST PAST HIGH EST                           
         BNH   FBL30               NO                                           
         MVC   KEY+10(4),=4X'FF'   FORCE NEXT PRD                               
         B     FBL22                                                            
*                                                                               
FBL30    DS    0H                                                               
         GOTO1 GETBILL                                                          
         BAS   RE,TSTBILL          TEST BILLING/CONTROL DATES                   
         B     FBL24                                                            
         SPACE 1                                                                
* EST=ALL/NO *                                                                  
         SPACE 1                                                                
FBL40    DS    0H                                                               
         GOTO1 GETBILL                                                          
         CLC   QEST,=C'ALL'        TEST EST=ALL                                 
         BE    FBL44               YES                                          
         BAS   RE,FBLFLT           EST=NO - FILTER                              
         BNE   FBL42               FAILED - NEXT ESTIMATE                       
         BAS   RE,TSTBILL          TEST BILLING/CONTROL DATES                   
         B     FBL24               AND GET NEXT BILL                            
*                                                                               
FBL42    MVC   KEY+12(4),=4X'FF'   FORCE NEXT EST                               
         B     FBL22                AND READ HIGH                               
         SPACE 1                                                                
* EST=ALL - NEED TO ADD ENTRY TO FELIST *                                       
         SPACE 1                                                                
FBL44    MVC   KEYSAVE,KEY         SAVE CURRENT KEY                             
*                                                                               
FBL46    BAS   RE,TSTBILL          TEST BILLING/CONTROL DATES                   
         GOTO1 SEQ                                                              
         CLC   KEY(12),KEYSAVE      SAME A/M/TY/C/P/E                           
         BE    FBL40               YES - READ BILL RECORD                       
         MVC   DUB(5),KEYSAVE+7                                                 
         CLI   FLTBYORN,C'Y'       TEST BILLED TODAY                            
         BNE   *+8                                                              
         MVI   DUB+5,FELIBLLD      SET BILLED TODAY FLAG                        
         L     R1,=A(FEPARS)                                                    
         GOTO1 BINSRCH,(R1),(X'01',DUB)                                         
         OC    1(3,R1),1(R1)       TEST ROOM IN TABLE                           
         BNZ   *+6                                                              
         DC    H'0'                                                             
         MVI   FLTBYORN,C'N'       RESET                                        
         B     FBL26                CONTINUE PROCESSING                         
*                                                                               
FBLX     MVC   KEY(32),FLTBKEYS                                                 
         GOTO1 HIGH                     RESTORE KEY                             
         MVC   KEYSAVE(32),FLTBKEYS+32  AND KEYSAVE TO ORIGINAL                 
*                                                                               
         CLI   FLTBREQ,C'Y'        TEST TO SET CONTROL DATE                     
         BNE   FBLXIT              NO                                           
* USE COMPUTED CONTROL DATE *                                                   
         MVC   QCNTLDT,SPACES      CLEAR PREVIOUS                               
         OC    FLTBCNTL,FLTBCNTL   TEST ANY DATE FOUND                          
         BZ    FBLXIT                                                           
         GOTO1 DATCON,DMCB,FLTBCNTL,(3,QCNTLDT)                                 
FBLXIT   LA    R1,FLTBYORN         POINT TO APPROPRIATE TEST CHAR               
         CLC   QEST,=C'ALL'                                                     
         BNE   *+8                                                              
         LA    R1,FLTBANY                                                       
         CLI   0(R1),C'Y'                                                       
         XIT1                                                                   
         EJECT                                                                  
* SUBROUTINE TESTS FOR LATEST CHANGE CONTROL DATE                               
* AND SETS FLAG IF BILL RECORD WAS BILLED TODAY                                 
         SPACE 1                                                                
TSTBILL  CLC   PBILLDAT,RQBILLDT   TEST SUBSEQUENT BILLING                      
         BH    TSTBILLX            YES - IGNORE                                 
         BL    TSTBILL2                                                         
         MVI   FLTBYORN,C'Y'       SET BILLED TODAY FLAG                        
         MVI   FLTBANY,C'Y'        SET AT LEAST ONE EST FOUND                   
         B     TSTBILLX                                                         
*                                                                               
TSTBILL2 OC    FLTBCNTL,FLTBCNTL   TEST FIRST TIME                              
         BZ    *+14                                                             
         CLC   PBILLDAT,FLTBCNTL   BILL DATE TO CHANGE CONTROL                  
         BL    *+10                LOW - IGNORE                                 
         MVC   FLTBCNTL,PBILLDAT   ELSE SAVE LATEST CHANGE CONTROL              
*                                                                               
TSTBILLX CLI   RCTRACE,C'Y'                                                     
         BNER  RE                                                               
         LR    R0,RE                                                            
         GOTO1 HEXOUT,DMCB,FLTBCNTL,P,9,=C'TOG'                                 
         GOTO1 REPORT                                                           
         LR    RE,R0                                                            
         BR    RE                                                               
*                                                                               
FLTBKEYS DS    CL64                                                             
FLTBCNTL DS    CL6                                                              
FLTBYORN DS    CL1                                                              
FLTBANY  DS    CL1                                                              
FLTBREQ  DS    CL1               C'Y' IF QCNTLDT TO BE SET BY FILCON            
         EJECT                                                                  
* ESTIMATE FILTERS FOR EST=NO REQUESTS *                                        
         SPACE 1                                                                
FBLFLT   NTR1                                                                   
         OC    KEY+10(2),KEY+10    SOME OLD BILLS HAVE NO EST                   
         BZ    FBLFLTX             SO CANT READ IT                              
         MVC   WORK(32),KEY        SAVE KEY IN PROCESS                          
         MVI   KEY+3,X'07'                                                      
         XC    KEY+12(20),KEY+12   CLEAR PAST ESTIMATE                          
         GOTO1 HIGH                                                             
         CLC   KEY(25),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                MUST FIND ESTIMATE HEADER                    
         GOTO1 GETEST                                                           
         MVC   KEY(25),WORK        RESTORE BILLREC KEY                          
         GOTO1 HIGH                                                             
         SPACE 1                                                                
* READ ESTIMATE HEADER - ADD NEW ENTRY TO FELIST *                              
         SPACE 1                                                                
         LA    R3,DUB                                                           
         USING FELISTD,R3                                                       
         MVC   FELPRD(5),KEY+7     MOVE PRD/EST                                 
         MVI   FELIND,FELIREAD     SET ESTIMATE READ FLAG                       
         CLC   PBILLDAT,RQBILLDT   TEST BILLED TODAY                            
         BNE   *+8                                                              
         OI    FELIND,FELIBLLD     SET BILLED TODAY FLAG                        
*                                                                               
         L     R1,=A(FEPARS)                                                    
         GOTO1 BINSRCH,(R1),(X'01',DUB)                                         
         OC    1(3,R1),1(R1)       TEST ROOM IN TABLE                           
         BNZ   *+6                                                              
         DC    H'0'                                                             
         L     R3,0(R1)            GET POINTER TO THIS ENTRY                    
         OC    0(FELISTL,R3),DUB   'OR' ENTRIES                                 
*                                                                               
         LA    RE,QESTEND                                                       
         LA    RF,PESTGRPS                                                      
         LA    R0,3                                                             
*                                                                               
FBLFLT4  CLI   0(RE),C'*'                                                       
         BE    FBLFLT8                                                          
         CLI   0(RE),C' '                                                       
         BE    FBLFLT8                                                          
         TM    0(RE),X'40'         TEST NEGATIVE FILTER                         
         BZ    FBLFLT6                                                          
*                                                                               
         CLC   0(1,RE),0(RF)       POSITIVE FILTER                              
         BNE   FBLFLTNO            EXIT WITH CC NOT EQ                          
         B     FBLFLT8                                                          
*                                                                               
FBLFLT6  MVC   BYTE,0(RE)                                                       
         OI    BYTE,X'40'                                                       
         CLC   BYTE,0(RF)          MATCH NEGATIVE FILTER                        
         BE    FBLFLTNO            YES - FAIL                                   
*                                                                               
FBLFLT8  LA    RE,1(RE)                                                         
         LA    RF,1(RF)                                                         
         BCT   R0,FBLFLT4                                                       
         SPACE 1                                                                
         OI    FELIND,FELIFLTR     SET PASSED FILTER FLAG                       
         CR    RE,RE               SET CC EQ                                    
         B     *+6                                                              
*                                                                               
FBLFLTNO LTR   RE,RE               SET CC NOT EQ                                
*                                                                               
FBLFLTX  XIT1                                                                   
         DROP  R3                                                               
*                                                                               
         LTORG                                                                  
*                                                                               
FELISTD  DSECT                                                                  
FELPRD   DS    CL3                                                              
FELEST   DS    XL2                                                              
FELIND   DS    XL1                                                              
FELIREAD EQU   X'80'               READ                                         
FELIFLTR EQU   X'40'               PASSED FILTER                                
FELITEST EQU   X'20'               TEST ESTIMATE                                
FELIBLLD EQU   X'10'               BILLED TODAY                                 
FELISTL  EQU   *-FELISTD                                                        
*                                                                               
PPFILCON CSECT                                                                  
         EJECT                                                                  
**************************************                                          
* SUBROUTINE TO BUILD OFFICE LIST    *                                          
* LIST FORMAT IS OFFICE(3)/CLIENT(3) *                                          
*  OFFICE(2) = HEX VALUE OR SPACE,   *                                          
*                           CLTOFF   *                                          
*  OFFICE+2(1) = CLTOFF              *                                          
**************************************                                          
         SPACE 1                                                                
BLDOFC   NMOD1 0,BLDOFC                                                         
         L     RC,0(R1)                                                         
*                                                                               
         L     RE,=V(OFCLIST)                                                   
         L     RF,=V(OFCLISTX)                                                  
         SR    RF,RE                                                            
         SRL   RF,8                SET FOR BCT                                  
         XC    0(256,RE),0(RE)                                                  
         LA    RE,256(RE)                                                       
         BCT   RF,*-10                                                          
*                                                                               
         XC    SVOFCLST,SVOFCLST                                                
         MVI   RCSVOFC,0                                                        
*                                                                               
         L     R4,=V(OFCLIST)                                                   
         SR    R3,R3               CLEAR COUNTER                                
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY(3),RCSVAGY                                                   
         MVI   KEY+3,X'02'                                                      
         GOTO1 HIGH                                                             
         B     BLDOFC4                                                          
*                                                                               
BLDOFC2  GOTO1 SEQ                                                              
*                                                                               
BLDOFC4  CLC   KEY(4),KEYSAVE                                                   
         BNE   BLDOFC10                                                         
         GOTO1 GETCLI                                                           
*                                                                               
         XC    OFCBLKC,OFCBLKC                                                  
         LA    R1,OFCBLKC                                                       
         USING OFFICED,R1                                                       
*                                                                               
         MVI   OFCSYS,C'P'                                                      
         MVC   OFCAUTH,QCLIENT                                                  
         MVC   OFCAGY,RCSVAGY                                                   
         MVC   OFCOFC,PCLTOFF                                                   
*                                                                               
         DROP  R1                                                               
*                                                                               
         XC    DMCB(4),DMCB        NEED ADDRESS OF OFFICER                      
         MVC   DMCB+4(4),=X'D9000A38'                                           
         L     RF,VCOMFACS                                                      
         L     RF,(CCALLOV-COMFACSD)(RF)                                        
         GOTO1 (RF),DMCB                                                        
         MVC   VOFFICER,DMCB                                                    
*                                                                               
         CLC   QCLIENT(2),=C'$*'    IF DOING ALL OFFICE REQUEST                 
         BE    BLDOFC6              SKIP OFFICER CHECK TO INCLUDE               
*                                                                               
         GOTO1 VOFFICER,DMCB,OFCBLKC,(X'C0',VCOMFACS),SVOFCLST                  
         CLI   0(R1),0             TEST TO INCLUDE THIS CLIENT                  
         BNE   BLDOFC2             NO                                           
*                                                                               
BLDOFC6  DS    0H                  TRANSLATE OFFICE CODE TO 2 CH                
*                                                                               
         GOTO1 =V(PRNTOFC),DMCB,(0,PCLTOFF),(0,0(R4)),VOFFICER,        X        
               RCSVAGY,VCOMFACS    PRINT 2 CH EXTERNAL CODE OR                  
*                                     2 CH HEX FOR UNPRINTABLE CODES            
         MVC   2(1,R4),PCLTOFF     SET 1 CH INTERNAL OFFICE CODE                
*                                                                               
         OI    2(R4),C' '      NEEDED SINCE $* REQUESTS READ                    
*                              CLIENTS WITH NO OFFICE                           
         MVC   3(3,R4),PCLTKCLT    AND CLIENT CODE TO LIST                      
         LA    R4,6(R4)                                                         
         C     R4,=V(OFCLISTX)                                                  
         BL    *+6                                                              
         DC    H'0'                                                             
         BCTR  R3,0                BUMP COUNTER                                 
         B     BLDOFC2                                                          
*                                                                               
BLDOFC10 LPR   R3,R3               SORT CLIENT WITHIN OFFICE                    
         BZ    BLDOFCX                                                          
*                                                                               
         L     R4,=V(OFCLIST)                                                   
         GOTO1 XSORT,DMCB,(R4),(R3),6,6,0                                       
*                                                                               
BLDOFCX  XIT1                                                                   
*                                                                               
SVOFCLST DC    CL48' '                                                          
         LTORG                                                                  
         SPACE 3                                                                
*                                                                               
JLIST    DS    CL650               13 X 50                                      
JLISTX   DS    C                   X'0000'                                      
LJLISTE  EQU   13                                                               
*                                                                               
         EJECT                                                                  
*              DSECT TO COVER SORT RECORD                                       
         SPACE 2                                                                
SORTRECD DSECT              USED IN SORTBYDS                                    
SORTREC  DS    0CL56                                                            
SORTOFF  DS    CL3             CLIENT OFFICE CODE                               
*                    1ST 2 BYTES=HEX VALUE OR SPACE, 1-CHARACTER CODE           
*                           BYTE 3 ALWAYS 1-CHARACTER CODE                      
SORTCLI  DS    CL3                                                              
SORTDIV  DS    CL3                                                              
SORTPROD DS    CL3                                                              
SORTEST  DS    CL2                                                              
SORTYM   DS    CL2                                                              
SORTREG  DS    CL3                                                              
SORTDIST DS    CL3                                                              
SORTPUB  DS    CL18                                                             
SORTPDA  DS    XL4                                                              
SORTLDA  DS    XL4                                                              
SORTDATE DS    CL3                                                              
SORTLINE DS    CL1                                                              
SORTDISK DS    CL4                                                              
         EJECT                                                                  
       ++INCLUDE DDOFFICED                                                      
       ++INCLUDE DDREPMASTD                                                     
       ++INCLUDE PPREPWORK                                                      
       ++INCLUDE PPREPWORK2                                                     
       ++INCLUDE PPNEWFILE                                                      
       ++INCLUDE PPMODEQU                                                       
       ++INCLUDE DDCOMFACSD                                                     
PGETADRD DSECT                                                                  
       ++INCLUDE PPGETADRD                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'210PPFILCON  10/30/18'                                      
         END                                                                    
