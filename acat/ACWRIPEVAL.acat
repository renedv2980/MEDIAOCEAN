*          DATA SET ACWRIPEVAL AT LEVEL 002 AS OF 05/01/02                      
*CATALP WRIPEVAL                                                                
         TITLE 'WRIPEVAL - WRITER PERIOD EXPRESSION VALIDATION'                 
WRIPEVAL CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 10,**PVAL**                                                      
         LR    RA,RC                                                            
         USING PEVALD,RA                                                        
*                                  PARAMETER 1 A(PERIOD EXPRESSION)             
*                                  PARAMETER 2 A(GEND)                          
*              AFTER WRIPEVAL      DUB=0     NO GOOD                            
*                    SOFT PERIODS  DUB=1-240 PERIOD NUMBER                      
*                    HARD MONTHS   DUB=253   START-END IN DUB+1 BINARY          
*                    HARD DATES    DUB=254   START-END IN DUB+1 COMP            
*                    AGE EXPRESS   DUB=255   START-END IN DUB+1 COMP            
         SPACE 1                                                                
         L     R2,0(R1)                                                         
         L     RC,4(R1)                                                         
         USING GEND,RC                                                          
         L     R9,ASUBSYSD                                                      
         USING SUBSYSD,R9                                                       
         BAS   RE,PERDVAL                                                       
         BNE   VAL2                                                             
         ZIC   R1,KEYTYPE          1-12 TYPE CODE                               
         BCTR  R1,0                                                             
         MH    R1,=H'20'                                                        
         ZIC   R0,KEYCODE                                                       
         AR    R1,R0                                                            
         LA    R1,1(R1)                                                         
         STC   R1,DUB                                                           
         B     XIT                                                              
         SPACE 1                                                                
VAL2     BAS   RE,TRYDATE                                                       
         BNE   VAL4                                                             
         MVI   DUB,254             254=HARD DATES                               
         MVC   DUB+1(4),START                                                   
         B     XIT                                                              
         SPACE 1                                                                
VAL4     BAS   RE,TRYMONTH                                                      
         BNE   VAL6                                                             
         MVI   DUB,253             253=HARD MONTHS                              
         MVC   DUB+1(4),START                                                   
         B     XIT                                                              
         SPACE 1                                                                
VAL6     BAS   RE,TRYAGE                                                        
         BNE   MISS                                                             
         MVI   DUB,255             255=AGED DATES                               
         B     XIT                                                              
         SPACE 1                                                                
MISS     MVI   DUB,0               NO GOOD PASS BACK ZERO                       
         B     XIT                                                              
         EJECT                                                                  
*              VALIDATE FOR PERIOD CODE                                         
         SPACE 3                                                                
PERDVAL  NTR1                                                                   
         MVI   KEYTYPE,0           SET KEY TYPE 0-3 IS THIS YEAR                
         CLI   0(R2),C'-'          MAY BE PRECEDED BY MINUS (LAST YEAR)         
         BNE   *+12                                                             
         MVI   KEYTYPE,4                                                        
         LA    R2,1(R2)                                                         
         SPACE 1                                                                
         CLI   0(R2),C'+'                          OR PLUS (NEXT YEAR)          
         BNE   *+12                                                             
         MVI   KEYTYPE,8                                                        
         LA    R2,1(R2)                                                         
         SPACE 1                                                                
         CLI   0(R2),C'A'          (A=AHEAD ALLOWED TEMPORARILY)                
         BNE   *+12                                                             
         MVI   KEYTYPE,8                                                        
         LA    R2,1(R2)                                                         
         SPACE 1                                                                
PV2      LA    R1,SPECPER          LOOK FOR SPECIAL CODE                        
         SPACE 1                                                                
PV4      CLC   0(6,R1),0(R2)                                                    
         BE    PV6                                                              
         CLI   0(R1),X'FF'                                                      
         BE    PV8                                                              
         LA    R1,8(R1)                                                         
         B     PV4                                                              
         SPACE 1                                                                
SPECPER  DC    C'PERIOD',AL1(1,0)                                               
         DC    C'PER   ',AL1(1,0)                                               
         DC    C'P     ',AL1(1,0)                                               
         DC    C'YTD   ',AL1(2,0)                                               
         DC    C'YEAR  ',AL1(2,0)                                               
         DC    C'Y     ',AL1(2,0)                                               
         DC    C'ROLL  ',AL1(3,0)                                               
         DC    C'R     ',AL1(3,0)                                               
         DC    C'MONTH ',AL1(3,12)                                              
         DC    C'MON   ',AL1(3,12)                                              
         DC    C'M     ',AL1(3,12)                                              
         DC    C'FISCAL',AL1(4,0)                                               
         DC    C'F     ',AL1(4,0)                                               
         DC    X'FF'                                                            
         SPACE 1                                                                
PV6      ZIC   R0,6(R1)            FOUND                                        
         MVC   KEYCODE,7(R1)                                                    
         ZIC   R1,KEYTYPE                                                       
         AR    R1,R0                                                            
         STC   R1,KEYTYPE                                                       
         B     YES                                                              
         SPACE 1                                                                
PV8      ZIC   R1,KEYTYPE          OTHERWISE CODE IS P,Y,R,F                    
         LA    R1,1(R1)                                                         
         CLI   0(R2),C'P'                                                       
         BE    PV10                                                             
         LA    R1,1(R1)                                                         
         CLI   0(R2),C'Y'                                                       
         BE    PV10                                                             
         LA    R1,1(R1)                                                         
         CLI   0(R2),C'R'                                                       
         BE    PV10                                                             
         LA    R1,1(R1)                                                         
         CLI   0(R2),C'F'                                                       
         BE    PV10                                                             
*                                  IF OMITTED, DEFAULT TO PERIOD                
         ZIC   R1,KEYTYPE                                                       
         LA    R1,1(R1)                                                         
         STC   R1,KEYTYPE                                                       
         LA    R1,PEREXP                                                        
         B     PV12                                                             
         SPACE 1                                                                
PV10     STC   R1,KEYTYPE     FOLLOWED BY MONTH,QUARTER,SIX MONTH               
         LA    R2,1(R2)                     EXPRESSION                          
         LA    R1,PEREXP                                                        
         SPACE 1                                                                
PV12     CLC   0(3,R1),0(R2)                                                    
         BE    PV14                                                             
         CLI   0(R1),X'FF'                                                      
         BE    NO                                                               
         LA    R1,4(R1)                                                         
         B     PV12                                                             
         SPACE 1                                                                
PV14     MVC   KEYCODE,3(R1)                                                    
         B     YES                                                              
         EJECT                                                                  
*              VALIDATE FOR OTHER DATE EXPRESSIONS                              
         SPACE 3                                                                
TRYDATE  NTR1                                                                   
         GOTO1 DATVAL,DMCB,(1,(R2)),DATE                                        
         L     R3,DMCB                                                          
         LTR   R3,R3                                                            
         BZ    NO                                                               
         BAS   RE,FIGYEAR                                                       
         GOTO1 DATCON,DMCB,(0,DATE),(2,START)                                   
         MVC   END,START                                                        
         AR    R2,R3                                                            
         CLI   0(R2),C' '                                                       
         BE    YES                                                              
         CLI   0(R2),C'-'                                                       
         BNE   NO                                                               
         LA    R2,1(R2)                                                         
         GOTO1 DATVAL,DMCB,(1,(R2)),DATE                                        
         L     R3,DMCB                                                          
         LTR   R3,R3                                                            
         BZ    NO                                                               
         BAS   RE,FIGYEAR                                                       
         GOTO1 DATCON,DMCB,(0,DATE),(2,END)                                     
         CLC   START,END                                                        
         BH    NO                                                               
         B     YES                                                              
         SPACE 1                                                                
FIGYEAR  NTR1                                                                   
         MVC   DUB(2),PPERIOD                                                   
         MVI   DUB+2,1                                                          
         GOTO1 DATCON,DMCB,(1,DUB),(0,QSTART)                                   
         MVC   DUB(2),PPERIOD+2                                                 
         MVI   DUB+2,X'31'                                                      
         GOTO1 DATCON,DMCB,(1,DUB),(0,QEND)                                     
         MVC   DATE(2),QSTART      TRY START DATE YEAR                          
         CLC   DATE,QSTART                                                      
         BL    FIGYEAR2                                                         
         CLC   DATE,QEND                                                        
         BH    FIGYEAR2                                                         
         B     XIT                                                              
         SPACE 1                                                                
FIGYEAR2 MVC   DATE(2),QEND                                                     
         B     XIT                                                              
         EJECT                                                                  
*              VALIDATE FOR MONTH RANGE                                         
         SPACE 3                                                                
TRYMONTH NTR1                                                                   
         GOTO1 DATVAL,DMCB,(2,(R2)),DATE                                        
         L     R3,DMCB                                                          
         LTR   R3,R3                                                            
         BZ    NO                                                               
         GOTO1 DATCON,DMCB,(0,DATE),(1,START)                                   
         MVC   END,START                                                        
         AR    R2,R3                                                            
         CLI   0(R2),C' '                                                       
         BE    YES                                                              
         CLI   0(R2),C'-'                                                       
         BNE   NO                                                               
         LA    R2,1(R2)                                                         
         GOTO1 DATVAL,DMCB,(2,(R2)),DATE                                        
         L     R3,DMCB                                                          
         LTR   R3,R3                                                            
         BZ    NO                                                               
         GOTO1 DATCON,DMCB,(0,DATE),(1,END)                                     
         CLC   START,END                                                        
         BH    NO                                                               
         B     YES                                                              
         EJECT                                                                  
*              VALIDATE FOR AGE EXPRESSION                                      
         SPACE 3                                                                
TRYAGE   NTR1                                                                   
         CLC   0(3,R2),=C'AGE'                                                  
         BNE   NO                                                               
         LA    R2,10(R2)                                                        
         CLI   0(R2),C'-'                                                       
         BE    TA10                                                             
         CLI   0(R2),C'+'                                                       
         BE    TA20                                                             
         BAS   RE,TRYNUM           TESTING RANGE NN-NN                          
         BNE   NO                                                               
         LA    R1,1(R1)                                                         
         STC   R1,START                                                         
         CLI   0(R2),C'-'                                                       
         BNE   NO                                                               
         LA    R2,1(R2)                                                         
         BAS   RE,TRYNUM                                                        
         BNE   NO                                                               
         STC   R1,END                                                           
         B     TA30                                                             
         SPACE 1                                                                
TA10     MVI   START,0             TESTING -NN (UP TO NN DAYS)                  
         LA    R2,1(R2)                                                         
         BAS   RE,TRYNUM                                                        
         BNE   NO                                                               
         STC   R1,END                                                           
         B     TA30                                                             
         SPACE 1                                                                
TA20     MVI   END,X'FF'           TESTING +NN (MORE THAN NN DAYS)              
         LA    R2,1(R2)                                                         
         BAS   RE,TRYNUM                                                        
         BNE   NO                                                               
         LA    R1,1(R1)                                                         
         STC   R1,START                                                         
         B     TA30                                                             
         SPACE 1                                                                
TA30     CLC   START(1),END        NOW FIGURE DATES BACK FROM TODAY             
         BNL   NO                                                               
         GOTO1 DATCON,DMCB,(5,0),(0,TODAY)                                      
         MVC   DATE,=C'800101'                                                  
         CLI   END,X'FF'                                                        
         BE    TA32                                                             
         ZIC   R0,END                                                           
         SR    R1,R1                                                            
         SR    R1,R0                                                            
         ST    R1,DMCB+8                                                        
         GOTO1 ADDAY,DMCB,TODAY,DATE                                            
         SPACE 1                                                                
TA32     GOTO1 DATCON,DMCB,(0,DATE),(2,DUB+1)                                   
         MVC   DATE,TODAY                                                       
         CLI   START,0                                                          
         BE    TA34                                                             
         ZIC   R0,START                                                         
         SR    R1,R1                                                            
         SR    R1,R0                                                            
         ST    R1,DMCB+8                                                        
         GOTO1 ADDAY,DMCB,TODAY,DATE                                            
         SPACE 1                                                                
TA34     GOTO1 DATCON,DMCB,(0,DATE),(2,DUB+3)                                   
         B     YES                                                              
         SPACE 1                                                                
TRYNUM   SR    R1,R1                                                            
         CLI   0(R2),C'0'                                                       
         BLR   RE                                                               
         SPACE 1                                                                
TRYNUM2  ZIC   R0,0(R2)                                                         
         SLL   R0,28                                                            
         SRL   R0,28                                                            
         MH    R1,=H'10'                                                        
         AR    R1,R0                                                            
         LA    R2,1(R2)                                                         
         CLI   0(R2),C'0'                                                       
         BNL   TRYNUM2                                                          
         CR    R1,R1               (SET CC EQUAL)                               
         BR    RE                                                               
         SPACE 1                                                                
YES      SR    R1,R1                                                            
         B     ALL                                                              
         SPACE 1                                                                
NO       LA    R1,1                                                             
         SPACE 1                                                                
ALL      LTR   R1,R1                                                            
         SPACE 1                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
*              PERIOD KEYWORDS                                                  
         SPACE 3                                                                
PEREXP   DS    0F                                                               
         DC    C'M01',AL1(1)                                                    
         DC    C'M1 ',AL1(1)                                                    
         DC    C'M  ',AL1(1)                                                    
         DC    C'M02',AL1(2)                                                    
         DC    C'M2 ',AL1(2)                                                    
         DC    C'M03',AL1(3)                                                    
         DC    C'M3 ',AL1(3)                                                    
         DC    C'M04',AL1(4)                                                    
         DC    C'M4 ',AL1(4)                                                    
         DC    C'M05',AL1(5)                                                    
         DC    C'M5 ',AL1(5)                                                    
         DC    C'M06',AL1(6)                                                    
         DC    C'M6 ',AL1(6)                                                    
         DC    C'M07',AL1(7)                                                    
         DC    C'M7 ',AL1(7)                                                    
         DC    C'M08',AL1(8)                                                    
         DC    C'M8 ',AL1(8)                                                    
         DC    C'M09',AL1(9)                                                    
         DC    C'M9 ',AL1(9)                                                    
         DC    C'M10',AL1(10)                                                   
         DC    C'M11',AL1(11)                                                   
         DC    C'M12',AL1(12)                                                   
         DC    C'Q01',AL1(13)                                                   
         DC    C'Q1 ',AL1(13)                                                   
         DC    C'Q  ',AL1(13)                                                   
         DC    C'Q02',AL1(14)                                                   
         DC    C'Q2 ',AL1(14)                                                   
         DC    C'Q03',AL1(15)                                                   
         DC    C'Q3 ',AL1(15)                                                   
         DC    C'Q04',AL1(16)                                                   
         DC    C'Q4 ',AL1(16)                                                   
         DC    C'601',AL1(17)                                                   
         DC    C'61 ',AL1(17)                                                   
         DC    C'602',AL1(18)                                                   
         DC    C'62 ',AL1(18)                                                   
         DC    X'FF'                                                            
         EJECT                                                                  
*              LTORG AND DSECT                                                  
         LTORG                                                                  
         SPACE 3                                                                
PEVALD   DSECT                                                                  
KEYTYPE  DS    CL1                                                              
KEYCODE  DS    CL1                                                              
START    DS    CL2                                                              
END      DS    CL2                                                              
QSTART   DS    CL6                                                              
QEND     DS    CL6                                                              
DATE     DS    CL6                                                              
TODAY    DS    CL6                                                              
         SPACE 1                                                                
         PRINT OFF                                                              
       ++INCLUDE DDSPLWORKD                                                     
       ++INCLUDE ACWRIWORKD                                                     
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002ACWRIPEVAL05/01/02'                                      
         END                                                                    
