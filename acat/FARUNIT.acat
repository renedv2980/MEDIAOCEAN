*          DATA SET FARUNIT    AT LEVEL 020 AS OF 11/15/19                      
*CATALP RUNIT                                                                   
RUNIT    TITLE '- RUNNER scheduler'                                             
***********************************************************************         
* NTRY - R1=A(Parameter list as follows):-                            *         
*                                                                     *         
*        P1/B0   - Server type                                        *         
*        P1/B1-3 - AL3(XL8'Worker file key') or zero to test for      *         
*                  available server/queue entries                     *         
*        P2/B0   - X'FF'=look for MQ enabled server on test call      *         
*        P2/B1-3 - A(Agency alpha) to filter server on                *         
*        P3/B0-3 - A(Step name) or zero                               *         
*                                                                     *         
*        If HOB of RF X'80' bit is on request is to run connected     *         
*                                                                     *         
* EXIT - CC=Equal if work completed successfully (or there is an      *         
*           available server/queue entry)                             *         
*        CC=Not equal if not submitted (or there is not an available  *         
*           server queue entry)                                       *         
***********************************************************************         
                                                                                
RUNIT    CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKL,**RUNIT*,CLEAR=YES                                         
         USING WORKD,RC                                                         
         LAM   AR0,ARF,ARZERO      Clear all access registers                   
         SAC   0                                                                
         PROT  OFF                 Turn protection off                          
         ST    R1,APARM                                                         
         STCM  RF,8,MODE                                                        
         MVC   PARMS(PARMSL),0(R1)                                              
         MVC   TYPE,PTYPE          Set server type                              
         MVI   PTYPE,0                                                          
         ICM   R1,15,PASTEP                                                     
         BZ    *+10                                                             
         MVC   STEP,0(R1)          Set specific server step name                
         ICM   R1,7,PAGY                                                        
         BZ    *+10                                                             
         MVC   AGENCY,0(R1)        Save off agency alpha code                   
         MVI   MQREQ,NO                                                         
         CLI   POPT,X'FF'          Test MQ                                      
         BNE   *+8                                                              
         MVI   MQREQ,YES                                                        
                                                                                
         CLI   TYPE,0              Test want dataspace tables returned          
         BNE   *+6                                                              
         DC    H'0'                                                             
                                                                                
         SR    R1,R1                                                            
         ICM   R1,7,PAKEY          (R1=zero to test for server/queue)           
         BZ    RUNIT02                                                          
         OC    WKEY,0(R1)          Set worker file key                          
         BNZ   *+6                                                              
         DC    H'0'                No worker key prodided                       
                                                                                
         OC    WKEY(L'TQMQZERO),WKEY  MQ type request?                          
         BNZ   *+8                    No                                        
         MVI   MQREQ,YES              Yes                                       
                                                                                
         TIME  TU                                                               
         STCM  R0,15,TIME          Set time now                                 
                                                                                
         USING SSBD,RF                                                          
RUNIT02  ICM   RF,15,ASSB                                                       
         MVC   ALET,SSBTBLET       Set TABS ALET                                
         MVC   FACPAK,SSBSYSN1                                                  
         MVI   ONOROFF,ON          Set running on-line                          
         ICM   RE,15,SSBTKADR      Set/test A(Task)                             
         BNZ   RUNIT04                                                          
                                                                                
         OC    SSBCNTL,SSBCNTL     Must be off-line                             
         BZ    *+6                                                              
         DC    H'0'                                                             
         MVI   ONOROFF,OFF         Set running off-line                         
         MVI   FACPAK,C'O'         Set off-line                                 
         MVI   FACTASK,C'1'        Set Task 1                                   
         LA    RE,RETURN           BR RE                                        
         ST    RE,TICTOC                                                        
         B     RUNIT06                                                          
                                                                                
         USING TCBD,RE                                                          
RUNIT04  MVC   FACTASK,TCBTASK                                                  
         MVC   SENUMBER,TCBSEN                                                  
         DROP  RE,RF                                                            
                                                                                
RUNIT06  CLC   ASIDFLD,ARZERO      Test already established ASID                
         BNE   RUNIT08                                                          
         GOTOR TICTOC,DMCB,C'SSET',0                                            
         EXTRACT ASIDFLD,'S',FIELDS=(ASID)                                      
         GOTOR TICTOC,DMCB,C'RSET',0                                            
                                                                                
RUNIT08  MVC   FACASID,ASIDFLD+2                                                
         LAM   AR0,ARF,ARZERO                                                   
                                                                                
***********************************************************************         
* Test if any servers of the requested type exist, exit with an error *         
* if there aren't                                                     *         
***********************************************************************         
                                                                                
         LAM   AR2,AR2,ALET        Go into Tabs DATASPACE                       
         SR    R2,R2               Set to zero offset in Tabs                   
         SAC   512                                                              
                                                                                
         USING FATABSD,R2                                                       
         ICM   R2,15,TABSRUN       A(Runner table)                              
         BZ    NOSERVER                                                         
                                                                                
         OC    SENUMBER,SENUMBER   Do we have an SE                             
         JZ    RUNIT05                                                          
                                                                                
         SAC   0                                                                
         XC    DMCB(24),DMCB                                                    
         MVC   DMCB+7(L'TCBSEN),SENUMBER                                        
         GOTO1 =V(DDSTATE),DMCB,=C'ENQUIRE'                                     
         MVC   HALF,DMCB+8         Save state in half                           
         LAM   AR2,AR2,ALET                                                     
         SAC   512                                                              
                                                                                
         USING TABSRUND,R2                                                      
RUNIT05  SR    R0,R0                                                            
         ICM   R0,3,TABSNSVR       R0=N'Server entries                          
         BNZ   *+6                                                              
         DC    H'0'                                                             
                                                                                
         LLC   RF,TABSSRWD         Length of server table entry                 
         STH   RF,SERVERL          Save as half word                            
         ICM   R2,15,TABSASVR      R2=A(Server table)                           
                                                                                
         USING TABSSVRD,R2         Ensure server is available                   
RUNIT10  CLC   TSLOCKID,ARZERO     Test active server                           
         BE    RUNIT18             No  - try next                               
         CLI   TSFLAG,TSFABEND     Test abended server                          
         BE    RUNIT18             Yes - try next                               
                                                                                
***********************************************************************         
* If STEP name is set it is a command type request from within                  
***********************************************************************         
                                                                                
         OC    STEP,STEP           Test server STEP name set                    
         BZ    RUNIT12             No                                           
         CLC   TSJOB,STEP          Yes - match on name only                     
         BNE   RUNIT18             No  - try next                               
         CLC   TSAQUE,ARZERO       Test command file already posted             
         BNE   NOSERVER            Yes - server not available                   
         B     RUNIT20             This is it                                   
                                                                                
RUNIT12  CLC   TSTYPE,TYPE         Match on server type                         
         BNE   RUNIT18                                                          
                                                                                
         TM    TSTIND1,TSTIUPDT    TEST UPDATIVE                                
         JNO   RUNIT13                                                          
         OC    SENUMBER,SENUMBER   Do we have an SE                             
         JZ    RUNIT14                                                          
         CLC   HALF,=C'OO'         Is global state fully open                   
         JNE   NOSERVER            No Then return NO SERVER                     
         J     RUNIT14                                                          
                                                                                
RUNIT13  CLI   HALF+0,C'C'         Are we closed                                
         JE    NOSERVER            Then return NO SERVER                        
         CLI   HALF+1,C'C'         Are we closing                               
         JE    NOSERVER            Then return NO SERVER                        
                                                                                
RUNIT14  CLI   MQREQ,YES           MQ request ?                                 
         BNE   RUNIT15                                                          
         TM    TSTIND1,TSTIMQ      MQ Server enabled?                           
         BO    RUNIT16                                                          
         B     RUNIT18             Can't run this here                          
                                                                                
RUNIT15  CLI   TSLFACID,0          Test generic server                          
         BE    RUNIT16             Yes - it may be able to run work             
         CLC   TSLFACID,FACPAK     Test specific server runs my FACPAK          
         BNE   RUNIT18             Not this one                                 
         OI    SERVER,SFACPAK      Set specific FACPAK server found             
         B     RUNIT18                                                          
                                                                                
RUNIT16  GOTOR TAGYSVR             Test if agency server                        
         BE    *+12                No not yet                                   
         OI    SERVER,GENERIC      Set generic server found                     
         B     RUNIT18                                                          
         OI    SERVER,SAGENCY      Set specific agency server found             
                                                                                
RUNIT18  AH    R2,SERVERL          Bump to next server entry                    
         BCT   R0,RUNIT10                                                       
                                                                                
         TM    SERVER,SFACPAK+GENERIC+SAGENCY                                   
         BZ    NOSERVER            No server(s) available                       
                                                                                
***********************************************************************         
* Locate a free queue entry for the work, exit with an error if none  *         
* are available                                                       *         
***********************************************************************         
                                                                                
RUNIT20  OC    STEP,STEP           Test specific STEP name passed               
         BZ    *+8                                                              
         STCM  R2,15,ASVR          YES - SET A(SERVER ENTRY)                    
                                                                                
         LAM   AR2,AR2,ALET                                                     
         SR    R2,R2               Clear offset                                 
         SAC   512                                                              
                                                                                
         USING FATABSD,R2                                                       
         ICM   R2,15,TABSRUN       A(Runner tables)                             
         BNZ   *+6                                                              
         DC    H'0'                                                             
                                                                                
         USING TABSRUND,R2                                                      
         SR    R0,R0                                                            
         ICM   R0,3,TABSNQUE       R0=N'queue entries                           
         BNZ   *+6                                                              
         DC    H'0'                                                             
         LLC   RF,TABSJBWD         Queue entry width                            
         STH   RF,QUEUEL                                                        
                                                                                
         ICM   R2,15,TABSAQUE      R2=A(Queue table)                            
         BNZ   RUNIT22                                                          
         DC    H'0'                                                             
                                                                                
         USING TABSQUED,R2                                                      
RUNIT22  SR    RE,RE                                                            
         ICM   RF,15,FACLOCK       Replacement value is lock TOKEN              
         BNZ   *+6                                                              
         DC    H'0'                                                             
         CS    RE,RF,TQLOCKID                                                   
         BE    RUNIT24             Grabbed a queue entry                        
         AH    R2,QUEUEL           Bump to next queue entry                     
         BCT   R0,RUNIT22                                                       
         B     NOQUEUE             None available                               
                                                                                
RUNIT24  ST    R2,AQUE             Save A(Queue entry)                          
         OC    WKEY,WKEY           Test worker key provided                     
         BNZ   *+14                                                             
         XC    TQLOCKID,TQLOCKID   No - clear the lock TOKEN and exit           
         B     ALLDONE                                                          
                                                                                
         MVC   TQATIME,TIME        Set arrival time                             
         MVC   TQTYPE,TYPE         Set server type required                     
         MVC   TQWKEY,WKEY         Set worker file key                          
         MVI   TQACTION,0          Set no action                                
         MVC   TQAGY,AGENCY        Set agency code running for                  
         MVC   TQSENUM,SENUMBER    Set SE number for request                    
         LA    RF,ECB              Point to ECB in local W/S                    
         XC    TQECB,TQECB                                                      
         TM    MODE,MODECONQ       Test running connected                       
         BZ    *+8                                                              
         STCM  RF,15,TQECB         Set completion ECB                           
         MVI   TQSTATUS,TQSWAIT    Set waiting to run                           
                                                                                
***********************************************************************         
* Only use specific server if step name was specified                 *         
***********************************************************************         
                                                                                
         OC    ASVR,ASVR           Test specific server for this work           
         BZ    RUNIT30             No                                           
         LAM   AR2,AR2,ALET                                                     
         ICM   R2,15,ASVR                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         SAC   512                                                              
                                                                                
         USING TABSSVRD,R2                                                      
         CLI   TSFLAG,TSFABEND     Test not abended (active/available)          
         BE    RUNIT40                                                          
         SR    RE,RE                                                            
         ICM   RF,15,AQUE                                                       
         CS    RE,RF,TSAQUE        Can't lock the server                        
         BNE   RUNIT40                                                          
         CLI   TSFLAG,TSFACTIV     Test server active                           
         BNE   RUNIT42             No  - wake it up                             
         B     RUNIT46             Yes - just wait for completion               
         EJECT                                                                  
***********************************************************************         
* Locate a server to run this work                                    *         
*                                                                     *         
* If an available server is found his ECB will be posted complete to  *         
* run work - else the assumption is that an active server will find   *         
* the request after completing its current work                       *         
***********************************************************************         
                                                                                
RUNIT30  LAM   AR2,AR2,ALET                                                     
         SR    R2,R2               Clear offset                                 
         SAC   512                                                              
                                                                                
         USING FATABSD,R2                                                       
         ICM   R2,15,TABSRUN       R2=A(Runner tables)                          
         BNZ   *+6                                                              
         DC    H'0'                                                             
                                                                                
         USING TABSRUND,R2                                                      
         LLH   R0,TABSNSVR         R0=N'Server entries                          
         ICM   R2,15,TABSASVR      R2=A(Server table)                           
         BNZ   RUNIT32                                                          
         DC    H'0'                                                             
                                                                                
         USING TABSSVRD,R2                                                      
RUNIT32  CLC   TSTYPE,TYPE         Match on server type                         
         BNE   RUNIT38                                                          
         CLI   TSFLAG,TSFABEND     Test not abended                             
         BE    RUNIT38                                                          
         CLI   MQREQ,YES           MQ type request?                             
         BNE   *+12                                                             
         TM    TSTIND1,TSTIMQ      MQ enabled?                                  
         BZ    RUNIT38             No                                           
                                                                                
         TM    SERVER,SAGENCY      Test specific agency server found            
         BZ    RUNIT33             No, so use check SFACPAK one                 
         GOTOR TAGYSVR             Test if right server                         
         BE    RUNIT36             Yes, so try to wake it up                    
         B     RUNIT38             No, not the right one                        
                                                                                
RUNIT33  CLC   SERVERL,=AL2(TABSSVRL)                                           
         JL    RUNIT34             Agency filter not supported                  
         CLI   TSTFAGYS,C' '       Test agency specific one                     
         JH    RUNIT38             Yes - can't use this one                     
                                                                                
RUNIT34  TM    SERVER,SFACPAK      Test specific FACPAK server found            
         BZ    RUNIT35                                                          
         CLC   TSLFACID,FACPAK     Match on my FACPAK id                        
         BE    RUNIT36             Yes. so try to wake it up                    
         B     RUNIT38             No, not the right one                        
                                                                                
RUNIT35  CLI   MQREQ,YES           MQ request?                                  
         BE    RUNIT36             Yes, so don't check FACPAK id                
         CLI   TSLFACID,0          Test this is a specific one                  
         BNE   RUNIT38                                                          
                                                                                
RUNIT36  CLI   TSFLAG,TSFAVAIL     Test server available                        
         BE    RUNIT42             Yes, go wake it up                           
         CLI   TSFLAG,TSFACTIV     Test server running                          
         BE    *+6                 Yes it is busy                               
         DC    H'0'                                                             
         ST    R2,AACTSVR          Save A(active server)                        
                                                                                
RUNIT38  AH    R2,SERVERL          Bump to next server entry                    
         BCT   R0,RUNIT32                                                       
         OC    AACTSVR,AACTSVR     Test busy server found                       
         BNZ   RUNIT46                                                          
                                                                                
RUNIT40  ICM   R2,15,AQUE          Clear queue entry                            
         LH    RF,QUEUEL                                                        
         BCTR  RF,0                                                             
         XC    0(0,R2),0(R2)                                                    
         EX    RF,*-6                                                           
         B     NOSERVER                                                         
                                                                                
***********************************************************************         
* Inactive server found - post its ECB complete to wake it up         *         
***********************************************************************         
                                                                                
RUNIT42  SR    R3,R3                                                            
         ICM   R3,7,TSLPOST        Get A(wakeup ECB) in R3                      
         LH    R4,TSASID           Get ASCB for the server                      
         SAC   0                                                                
         LOCASCB ASID=(R4)                                                      
         LTR   RF,RF               ASCB returned in R1 ok?                      
         BNZ   RUNIT44             No                                           
         LR    R4,R1               Set ASCB in R4                               
         POST  (R3),99,ASCB=(R4),LINKAGE=SYSTEM,ECBKEY=8,MF=(E,POSTLF)          
         LR    R0,RF                                                            
         B     RUNIT46                                                          
                                                                                
RUNIT44  SAC   512                                                              
         MVI   TSFLAG,TSFABEND     Set server abended (so post no more)         
         OC    ASVR,ASVR           Test specific server specified               
         BNZ   RUNIT40             Yes - return no server error                 
         XC    AACTSVR,AACTSVR     Clear found entry                            
         B     RUNIT30             and go look for another server               
                                                                                
***********************************************************************         
* Now wait for the server to complete the work if running connected   *         
* Note that if running disconnected we assume to be running in FACPAK *         
* and that the PC comms code will go into polling mode until the work *         
* is complete - DDLINK will handle the clearing of the queue entry    *         
***********************************************************************         
                                                                                
RUNIT46  TM    MODE,MODECONQ       Test running connected                       
         BZ    RUNITX                                                           
         SAC   0                                                                
         CLI   ONOROFF,OFF         Only allow connected off-line                
         BE    *+6                                                              
         DC    H'0'                                                             
         LTR   R0,R0               Test post macro completion code              
         BZ    *+6                                                              
         DC    H'0'                                                             
                                                                                
         LA    R1,ECB              Wait for work to complete                    
         WAIT  ECB=(1)                                                          
                                                                                
         LAM   AR2,AR2,ALET        Go into TABS dataspace                       
         SAC   512                                                              
         ICM   R2,15,AQUE                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
                                                                                
         USING TABSQUED,R2                                                      
         MVC   ERROR,TQERROR       Set completion code                          
         MVC   WKEY,TQWKEY         Extract returned key                         
         XC    TQLOCKID,TQLOCKID   Clear queue entry lockword                   
         LAM   AR2,AR2,ARZERO                                                   
         SAC   0                                                                
                                                                                
         TIME  TU                  Get time now                                 
         S     R0,TIME             Subtract start time                          
         STCM  R0,15,ETIME         Set time difference (in TU'S)                
         B     RUNITX                                                           
         EJECT                                                                  
***********************************************************************         
* Exits from module                                                   *         
***********************************************************************         
                                                                                
NOSERVER MVI   ERROR,TQENOSVR      No table - return no server                  
         B     RUNITX                                                           
                                                                                
NOQUEUE  MVI   ERROR,TQENOQUE      No queue entries available                   
         B     RUNITX                                                           
                                                                                
ALLDONE  MVI   ERROR,0             All done                                     
                                                                                
RUNITX   LAM   AR0,ARF,ARZERO      Ensure regular addressing mode               
         SAC   0                                                                
         L     R1,APARM                                                         
         MVC   0(1,R1),ERROR       Return completion code                       
         OC    WKEY,WKEY                                                        
         BZ    EXIT                                                             
         MVC   4(4,R1),ALET        Return queue address to caller               
         MVC   8(4,R1),AQUE                                                     
                                                                                
         TM    MODE,MODECONQ       Test running connected                       
         BZ    EXIT                                                             
         MVC   4(L'ETIME,R1),ETIME Return elasped time in TU'S                  
         L     RF,0(R1)            Return output key                            
         MVC   0(L'TQWKEY,RF),WKEY                                              
                                                                                
EXIT     PROT  ON                  Turn protection on                           
         CLI   0(R1),0             Set condition code for caller                
         XIT1  ,                                                                
                                                                                
RETURN   BR    RE                                                               
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* Test for agency server                                              *         
* AR2 and R2 = Server entry                                           *         
* Must be in SAC AR mode                                              *         
***********************************************************************         
                                                                                
         USING TABSSVRD,R2         Ensure server is available                   
TAGYSVR  LA    R3,TSTFAGYS         List of agencies                             
         CLC   SERVERL,=AL2(TABSSVRL)                                           
         BL    TAGYSVRN            Not supported, so ignore                     
         LA    RF,TSTFAGY#         # of agencies attached to server             
         CPYA  AR3,AR2                                                          
TAGYSVR4 CLI   0(R3),C' '          Test end of agency list                      
         BNH   TAGYSVRN            Not agency specific                          
         CLC   AGENCY,0(R3)        Do they match?                               
         BE    TAGYSVRY            Found agency specific                        
         LA    R3,L'TSTFAGYS(,R3)                                               
         JCT   RF,TAGYSVR4         Do for all agencies in list                  
         B     TAGYSVRN            Not agency specific                          
                                                                                
TAGYSVRY SR    R3,R3               Entry found - clear R3 for exit              
TAGYSVRN LTR   R3,R3               Set condition code (zero=found)              
         LAM   AR3,AR3,ARZERO      Clear AR3                                    
         BR    RE                                                               
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* Literals and constants                                              *         
***********************************************************************         
                                                                                
YES      EQU   C'Y'                                                             
NO       EQU   C'N'                                                             
                                                                                
POSTLF   POST  ECBKEY=YES,MF=L                                                  
                                                                                
ASIDFLD  DC    D'0'                                                             
ASSB     DC    V(SSB)                                                           
TICTOC   DC    V(TICTOC)                                                        
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* Working storage                                                     *         
***********************************************************************         
                                                                                
WORKD    DSECT ,                                                                
                                                                                
ARZERO   DS    16F                                                              
                                                                                
DMCB     DS    6F                                                               
ATAB     DS    A                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
BYTE1    DS    X                                                                
                                                                                
ECB      DS    A                   ECB to post completion/wait on               
         DS    F                                                                
                                                                                
APARM    DS    A                   A(Caller's parameter list)                   
ALET     DS    A                   Local SSBTBLET                               
AQUE     DS    A                   A(Queue entry)                               
ASVR     DS    A                   A(Server entry)                              
AACTSVR  DS    A                   A(Active server)                             
                                                                                
PARMS    DS    0A                  ** Calling parameter list **                 
PTYPE    DS    C                   Server type (see TSTYPE)                     
PAKEY    DS    AL3                 A(XL10'Worker file key')                     
POPT     DS    X                   Option byte                                  
PAGY     DS    AL3                 A(Agency alpha) to set or filter             
PASTEP   DS    A                   A(Server step name)                          
PARMSL   EQU   *-PARMS                                                          
                                                                                
FACLOCK  DS    0X                  ** Locking Token **                          
FACPAK   DS    XL(L'SSBSYSN1)      FACPAK id                                    
FACTASK  DS    CL(L'TCBTASK)       FACPAK task-id                               
FACASID  DS    AL2                 FACPAK ASID                                  
                                                                                
TIME     DS    F                   Entry time                                   
ETIME    DS    F                   Elapsed time                                 
                                                                                
ONOROFF  DS    X                   ** Online/Offline flag **                    
ON       EQU   0                   Running online                               
OFF      EQU   1                   Running offline                              
                                                                                
STEP     DS    CL(L'TSJOB)         Specific server step name                    
TYPE     DS    XL(L'TQTYPE)        Server type (see tstype)                     
WKEY     DS    XL(L'TQWKEY)        Worker file key                              
AGENCY   DS    CL(L'TSTFAGYS)      Agency alpha id                              
SENUMBER DS    XL(L'TCBSEN)        SE NUMBER                                    
                                                                                
ERROR    DS    XL(L'PTYPE)         Error return byte                            
                                                                                
MQREQ    DS    C                   MQ request  (Y/N)                            
                                                                                
MODE     DS    X                   ** Calling mode **                           
MODECONQ EQU   X'80'               Run connected                                
MODEDSCQ EQU   X'40'               Run disconnected                             
                                                                                
SERVER   DS    X                   ** Server flags **                           
SFACPAK  EQU   X'01'               Specific server by FACPAK                    
GENERIC  EQU   X'02'               Generic  server any work                     
SAGENCY  EQU   X'04'               Specific server by agecny                    
                                                                                
SERVERL  DS    H                   Server entry length                          
QUEUEL   DS    H                   Queue  entry length                          
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
***********************************************************************         
* Other included books                                                *         
***********************************************************************         
                                                                                
* FASSB                                                                         
         PRINT OFF                                                              
       ++INCLUDE FASSB                                                          
         PRINT ON                                                               
* FATCB                                                                         
         PRINT OFF                                                              
       ++INCLUDE FATCB                                                          
         PRINT ON                                                               
* FATABSD                                                                       
         PRINT OFF                                                              
       ++INCLUDE FATABSD                                                        
         PRINT ON                                                               
* FATABSRUN                                                                     
         PRINT OFF                                                              
       ++INCLUDE FATABSRUN                                                      
         PRINT ON                                                               
* FAUTL                                                                         
         PRINT OFF                                                              
       ++INCLUDE FAUTL                                                          
         PRINT ON                                                               
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'020FARUNIT   11/15/19'                                      
         END                                                                    
