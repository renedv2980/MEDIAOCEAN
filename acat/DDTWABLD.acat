*          DATA SET DDTWABLD   AT LEVEL 029 AS OF 02/05/02                      
*CATALP TWABLD                                                                  
         TITLE 'TWABLD - BUILD TWA FIELDS'                                      
TWABLD   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 TWAWORKX-TWAWORKD,**TWABLD,CLEAR=YES                             
         USING TWAWORKD,RC                                                      
         LR    R2,R1                                                            
         USING TWAPARMD,R2                                                      
         L     R3,TWAPATWA         R3=A(TWA)                                    
         L     R4,TWAPAFST         R4=A(FIRST BUILD ELEMENT)                    
         LHI   R0,TWALINS          MAX ROWS = 24                                
         ST    R0,TWAMAXL          SET MAXIMUM N'LINES                          
         LHI   R0,TWACOLS          MAX COLUMNS = 80                             
         ST    R0,TWAMAXC          SET MAXIMUM N'COLUMNS                        
         OC    TWAPAMAX,TWAPAMAX                                                
         BNZ   *+12                                                             
         LHI   R0,TWASIZE          DEFAULT SIZE = 4096                          
         ST    R0,TWAPAMAX         SET DEFAULT MAXIMUM TWA SIZE                 
         MVC   TWANEXT,TWAPAOUT                                                 
*                                                                               
         LA    R1,64(R3)           POINT TO FIRST TWA FIELD                     
         USING TWAFLDD,R1                                                       
         L     RF,TWANEXT                                                       
         SR    R0,R0               ENSURE TWAPAOUT POINTS TO A FLDHDR           
TWA2     AR    R1,R0                                                            
         CR    R1,RF               TEST BUILD FROM START OF TWA                 
         BE    TWA4                                                             
         BH    TWAERR1                                                          
*                                                                               
         CLI   TWAFATB,TWAFANOP    NO-OP FIELD?                                 
         BE    *+12                YES                                          
         ST    R1,TWALAST          SAVE A(LAST FIELD HEADER)                    
         OI    TWAFOUTI,TWAFOXMT   TRANSMIT ALL TWA FIELDS                      
         ICM   R0,1,TWAFLEN                                                     
         BNZ   TWA2                                                             
         B     TWAERR2                                                          
*                                                                               
TWA4     SR    R1,R3               CALCULATE TOTAL L'TWA SO FAR                 
         ST    R1,TWALEN                                                        
         ICM   R1,15,TWALAST       R1=A(LAST TWA FIELD)                         
         BZ    TWA6                                                             
         SR    RF,RF               CALCULATE END LINE/COLUMN                    
         ICM   RF,3,TWAFADDR                                                    
         SR    RE,RE                                                            
         D     RE,TWAMAXC                                                       
         LA    RF,1(RF)            RF=CURRENT LINE                              
         LA    RE,1(RE)            RE=CURRENT COLUMN                            
         ZIC   R0,TWAFLEN                                                       
         SHI   R0,TWAFLDL                                                       
         TM    TWAFATB,TWAFAXTN    TEST EXTENDED HEADER                         
         BZ    *+8                                                              
         SHI   R0,TWAFLDL          R0=TOTAL DATA LENGTH                         
         AR    RE,R0                                                            
         LA    RE,1(RE)                                                         
         STH   RE,TWANEXTC         SET NEXT COLUMN NUMBER                       
         STH   RF,TWANEXTL         SET NEXT LINE NUMBER                         
         DROP  R1                                                               
*                                                                               
         USING TWAELEMD,R4         PROCESS A TWA BUILD ELEMENT                  
TWA6     CLI   TWAELCD,0           TEST END OF BUILD LIST                       
         BE    TWAEXIT                                                          
         CLI   TWAEFLD,0           IF FIELD NUMBER IS SET                       
         BE    *+8                                                              
         OI    TWAEATB,TWAFAXTN    SET EXTENDED HEADER ATTRIBUTE                
         TM    TWAERLN,TWAERLXT    IF NEW EXTENDED FIELD DATA                   
         BZ    *+8                   NO                                         
         OI    TWAEATB,TWAFAXTN    SET EXTENDED HEADER ATTRIBUTE                
         TM    TWAERLN,TWAERLXA    IF EXTENDED ATTRIBUTES DEFINED               
         BZ    TWA7                  NO                                         
         TM    TWAEATB,TWAFAPRT    AND FIELD IS UNPROTECTED                     
         BO    *+8                                                              
         OI    TWAEATB,TWAFAXTN    SET EXTENDED HEADER ATTRIBUTE                
*                                                                               
TWA7     ZIC   RF,TWAELLN          DERIVE LENGTH OF DATA IN EL.                 
         LA    RE,TWAELLNQ                                                      
         TM    TWAERLN,TWAERLXA    IF EXTENDED ATTRIBUTES DEFINED               
         BZ    *+8                                                              
         LA    RE,TWAELLNQ+TWAELLQ2   DERIVE L'DATA DIFFERENTLY                 
         TM    TWAERLN,TWAERLXT       IF NEW EXTENED INFO SUPPLIED              
         BZ    *+8                                                              
         LA    RE,TWAELLNQ+TWAELLQ3   DERIVE L'DATA DIFFERENTLY                 
         SR    RF,RE                                                            
         STC   RF,TWALDATA         SAVE L'DATA                                  
*                                                                               
         CLI   TWAEFLN,0           IF FIELD LENGTH NOT SET                      
         BNE   TWA8                                                             
         CLI   TWALDATA,0          THEN IF DATA LENGTH IS SET                   
         BE    TWAERR3                                                          
         MVC   TWAEFLN,TWALDATA    USE IT                                       
*                                                                               
TWA8     CLI   TWAEATB,TWAFANOP    NO-OP FIELD?                                 
         BNE   TWA9                NO                                           
         CLI   TWAERLN,0           YES -- ROW NUMBER MUST BE ZERO               
         BNE   TWAERR8                                                          
         CLI   TWAECOL,0           COLUMN NUMBER MUST ALSO BE ZERO              
         BNE   TWAERR8                                                          
         CLI   TWAEFLD,0           FIELD ID NUMBER MUST ALSO BE ZERO            
         BNE   TWAERR8                                                          
         B     TWA14                                                            
*                                                                               
TWA9     ZIC   RE,TWAERLN          TEST LINE NUMBER GIVEN                       
*                                  TURN OFF HOBS                                
         N     RE,=AL4(255-TWAERLAB-TWAERLXA-TWAERLXT)                          
         BZ    TWA10               NO                                           
         TM    TWAERLN,TWAERLAB    IF THE LINE NUMBER IS NOT ABSOLUTE           
         BO    *+8                                                              
         AH    RE,TWANEXTL         LINE IS RELATIVE TO PREVIOUS LINE            
         STH   RE,TWANEXTL         SET NEXT LINE NUMBER                         
         SR    RF,RF                                                            
         ICM   RF,1,TWAECOL        TEST COLUMN POSITION GIVEN                   
         BNZ   *+8                                                              
         LA    RF,2                NO - SET COLUMN2                             
         STH   RF,TWANEXTC                                                      
         B     TWA14                                                            
*                                                                               
TWA10    SR    RF,RF                                                            
         ICM   RF,1,TWAECOL        TEST COLUMN NUMBER GIVEN                     
         BZ    TWA12               NO                                           
         LH    R1,TWANEXTC                                                      
         BCTR  R1,0                                                             
         CR    RF,R1               TEST THIS FIELD OVERLAPS LAST                
         BNL   *+16                (NOTE UNPROTECTED FIELD CAN                  
         LH    RE,TWANEXTL         IMMEDIATELY FOLLOW A PROTECTED ONE)          
         LA    RE,1(RE)            YES - BUMP LINE NUMBER                       
         STH   RE,TWANEXTL                                                      
         STH   RF,TWANEXTC                                                      
         B     TWA14                                                            
*                                                                               
TWA12    LH    RF,TWANEXTC                                                      
         ZIC   RE,TWAEFLN                                                       
         AR    RF,RE               L'DATA+L'ATTRIBUTE                           
         BCTR  RF,0                                                             
         C     RF,TWAMAXC          TEST THIS FIELD FITS ON TWA LINE             
         BH    TWA13               NO                                           
         BL    TWA14               YES                                          
         CLC   TWANEXTL,TWAMAXL+2  FIELD FILLS UP ENTIRE LINE                   
         BNE   TWA14               IT'S OK - WE'RE NOT ON BOTTOM LINE           
         B     TWAERR7             CAN'T USE LAST POSITION OF SCREEN            
*                                                                               
TWA13    LH    RE,TWANEXTL         BUMP TO NEXT LINE                            
         LA    RE,1(RE)                                                         
         STH   RE,TWANEXTL                                                      
         LA    RF,2                SET COLUMN2                                  
         STH   RF,TWANEXTC                                                      
*                                                                               
TWA14    ZIC   RE,TWAEFLN          TEST DATA FITS ON CURRENT TWA LINE           
         LR    RF,RE                                                            
         AHI   RF,TWAFLDL          RF = L'DATA + HEADER (NOT EXTENSION)         
         CLI   TWAEATB,TWAFANOP    NO-OP FIELD?                                 
         BE    TWA14A              YES                                          
         BCTR  RE,0                                                             
         AH    RE,TWANEXTC                                                      
         C     RE,TWAMAXC                                                       
         BH    TWAERR4                                                          
         LA    RE,1(RE)                                                         
         STH   RE,TWASAVEC                                                      
         TM    TWAEATB,TWAFAXTN    TEST FIELD HAS EXTENDED HEADER               
         BZ    *+8                                                              
         AHI   RF,TWAFLDL          YES - THESE HAVE 2 HEADERS                   
*                                                                               
TWA14A   LR    RE,RF                                                            
         A     RF,TWALEN                                                        
         LA    R0,L'TWAINDS(RF)    R0=NEW LENGTH OF TWA                         
         C     R0,TWAPAMAX         TEST FIELD FITS IN TWA                       
         BH    TWAERR5                                                          
         ST    RF,TWALEN           SET NEW LENGTH OF TWA                        
         L     R1,TWANEXT          BUILD FIELD AT TWANEXT                       
         USING TWAFLDD,R1                                                       
         EX    RE,*+8                                                           
         B     *+10                                                             
         XC    TWAFLDD(0),TWAFLDD  CLEAR TWA AREA                               
         STC   RE,TWAFLEN          SET TOTAL FIELD LENGTH                       
         MVC   TWAFATB,TWAEATB     SET FIELD ATTRIBUTES                         
*                                                                               
         CLI   TWAEATB,TWAFANOP    NO-OP FIELD?                                 
         BE    TWA14B              YES                                          
         LH    RF,TWANEXTL                                                      
         C     RF,TWAMAXL          TEST TWA LINES NOT EXCEEDED                  
         BH    TWAERR6                                                          
         BCTR  RF,0                CALCULATE SCREEN ADDRESS                     
         M     RE,TWAMAXC                                                       
         AH    RF,TWANEXTC                                                      
         BCTR  RF,0                                                             
         STCM  RF,3,TWAFADDR                                                    
         OI    TWAFOUTI,TWAFOXMT   TRANSMIT THIS FIELD                          
         TM    TWAPINDS,TWAPIFLD   TEST TRANSMITTING BY FIELD                   
         BZ    *+8                                                              
         OI    TWAFOUTL,TWAFOXMT   SET TO REBUILD THIS FIELD                    
         TM    TWAERLN,TWAERLXA    IF EXTENDED ATTRIBUTES DEFINED               
         BZ    TWA14B                                                           
         TM    TWAEATB,TWAFAXTN    AND EXTENDED HDR ATTRIB NOT DEFINED          
         BO    TWA14B              (IMPLIES FIELD IS PROTECTED)                 
         OI    TWAFINPI,TWAFINXA   SET SPECIAL INPUT INDICATOR FLAG             
         MVC   TWAFINPL,TWAEXATT   SET EXTENDED ATTRIBS IN INPUT LENGTH         
*                                                                               
TWA14B   CLI   TWALDATA,0          ANY FIELD DATA GIVEN?                        
         BE    TWA16                                                            
         ZIC   RF,TWALDATA         YES - SET TO MOVE IT TO FIELD                
         CLM   RF,1,TWAEFLN                                                     
         BNH   *+8                                                              
         IC    RF,TWAEFLN          TRUNCATE DATA IF GR DEFINED LENGTH           
         BCTR  RF,0                                                             
         IC    RE,TWAEFLN                                                       
         BCTR  RE,0                                                             
         LTR   RE,RE                                                            
         BZ    TWA15                                                            
         BCTR  RE,0                                                             
         MVI   TWAFDATA,C' '       SET FIELD TO SPACES                          
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   TWAFDATA+1(0),TWAFDATA                                           
*                                                                               
TWA15    LA    RE,TWAEDTA                                                       
         TM    TWAERLN,TWAERLXA    IF EXTENDED ATTRIBUTES DEFINED               
         BZ    *+8                                                              
         LA    RE,TWAEDTA2         DATA STARTS LATER                            
         TM    TWAERLN,TWAERLXT    IF EXTRA DATA DEFINED                        
         BZ    *+8                                                              
         LA    RE,TWAEDTA3         DATA STARTS LATER                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   TWAFDATA(0),0(RE)   SET FIELD DATA                               
*                                                                               
TWA16    ZIC   RF,TWAEFLN                                                       
         LA    RF,TWAFDATA(RF)     RF=A(HEADER+L'DATA)                          
*                                                                               
         USING TWAXTHDR,RF                                                      
         MVC   TWANEXTC,TWASAVEC   SET NEXT COLUMN NUMBER                       
         CLI   TWAEATB,TWAFANOP    NO-OP FIELD?                                 
         BE    TWA18               YES                                          
         TM    TWAEATB,TWAFAXTN    TEST EXTENDED HEADER REQUIRED                
         BZ    TWA18               NO                                           
         MVC   TWAXTFD#,TWAEFLD    YES - SET FIELD NUMBER                       
         TM    TWAERLN,TWAERLXA    IF EXTENDED ATTRIBUTES DEFINED               
         BZ    *+10                                                             
         MVC   TWAXTATB,TWAEXATT   SET EXTENDED ATTRIBS IN EXTENDED HDR         
         TM    TWAERLN,TWAERLXT    NEW EXTENDED FIELD DATA                      
         BZ    *+10                                                             
         MVC   TWAXTUSR,TWAEXUSR   APPLICATION USER FIELD                       
         DROP  RF                                                               
*                                                                               
         AHI   RF,TWAFLDL                                                       
*                                                                               
TWA18    ST    RF,TWANEXT          SET A(NEXT TWA FIELD)                        
         ZIC   R0,TWAELLN          BUMP TO NEXT ELEMENT                         
         AR    R4,R0                                                            
         B     TWA6                                                             
         DROP  R1                                                               
         EJECT                                                                  
TWAERR1  MVI   TWAPERRS,TWAPEIOA                                                
         B     TWAERRX                                                          
TWAERR2  MVI   TWAPERRS,TWAPEITF                                                
         B     TWAERRX                                                          
TWAERR3  MVI   TWAPERRS,TWAPEIFL                                                
         B     TWAERRX                                                          
TWAERR4  MVI   TWAPERRS,TWAPEEMC                                                
         B     TWAERRX                                                          
TWAERR5  MVI   TWAPERRS,TWAPEEMS                                                
         B     TWAERRX                                                          
TWAERR6  MVI   TWAPERRS,TWAPEEML                                                
         B     TWAERRX                                                          
TWAERR7  MVI   TWAPERRS,TWAPEELP                                                
         B     TWAERRX                                                          
TWAERR8  MVI   TWAPERRS,TWAPEENP                                                
         B     TWAERRX                                                          
*                                                                               
TWAERRX  L     R1,TWAPAOUT                                                      
         MVC   0(L'TWAINDS,R1),TWAINDS                                          
         B     TWAXIT                                                           
*                                                                               
TWAEXIT  MVI   TWAPERRS,TWAPEOK                                                 
         L     R1,TWANEXT                                                       
         MVC   0(L'TWAINDS,R1),TWAINDS                                          
         TM    TWAPINDS,TWAPIFLD      TEST TRANSMITTING BY FIELD                
         BZ    *+10                                                             
         XC    0(L'TWAINDS,R1),0(R1)  DON'T TRANSMIT ENTIRE SCREEN              
         ST    R1,TWAPANXT                                                      
         MVC   TWAPTLEN,TWALEN+2                                                
         B     TWAXIT                                                           
*                                                                               
TWAXIT   XIT1                                                                   
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
TWAINDS  DC    X'000101'                                                        
         SPACE 1                                                                
TWALINS  EQU   24                  NUMBER OF SCREEN LINES                       
TWACOLS  EQU   80                  NUMBER OF SCREEN COLUMNS                     
TWASIZE  EQU   4096                DEFAULT MAXIMUM TWA SIZE                     
         SPACE 1                                                                
TWAWORKD DSECT                     ** TWABLD WORKING STORAGE **                 
TWAMAXL  DS    F                   MAXIMUM N'LINES                              
TWAMAXC  DS    F                   MAXIMUM N'COLUMNS                            
TWALEN   DS    F                   TOTAL TWA LENGTH SO FAR                      
TWALAST  DS    A                   A(LAST TWA FIELD HEADER)                     
TWANEXT  DS    A                   A(NEXT TWA FIELD HEADER)                     
TWANEXTL DS    H                   LAST LINE NUMBER                             
TWANEXTC DS    H                   LAST COLUMN NUMBER                           
TWASAVEC DS    H                   SAVE COLUMN NUMBER                           
TWALDATA DS    XL1                 DATA LENGTH                                  
TWAWORKX EQU   *                                                                
         EJECT                                                                  
TWAFLDD  DSECT                     ** TWA FIELD DEFINITION **                   
TWAFLEN  DS    XL1                 L'HEADER(S)+L'FIELD                          
TWAFATB  DS    XL1                 STANDARD ATTRIBUTE BYTE                      
TWAFAPRT EQU   X'20'               PROTECTED FIELD                              
TWAFAXTN EQU   X'02'               EXTENDED HEADER                              
TWAFANOP EQU   X'FF'               NO-OP FIELD                                  
TWAFADDR DS    XL2                 SCREEN ADDRESS                               
TWAFINPI DS    XL1                 INPUT INDICATORS                             
TWAFINXA EQU   X'0C'               PROT FLD HAS EXT ATTRIB IN NEXT FLD          
TWAFINPL DS    XL1                 INPUT LENGTH                                 
TWAFOUTI DS    XL1                 OUTPUT INDICATORS                            
TWAFOXMT EQU   X'80'               TRANSMIT FIELD                               
TWAFOUTL DS    XL1                 OUTPUT LENGTH                                
TWAFLDL  EQU   *-TWAFLDD                                                        
TWAFDATA DS    0C                  FIELD DATA                                   
         SPACE 1                                                                
       ++INCLUDE DDTWABLDD                                                      
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'029DDTWABLD  02/05/02'                                      
         END                                                                    
