*          DATA SET ACDISCOL   AT LEVEL 034 AS OF 05/01/02                      
*CATALP ACDISCOL                                                                
         SPACE 2                                                                
************************************************************                    
*                                                          *                    
* ACDISCOL - DISPLAY THE COLUMN NAMES FOR A COLUMN         *                    
*            LIST ENTRY                                    *                    
*                                                          *                    
* ON ENTRY, P1  BYTES 1-3 = A(COLUMN LIST ENTRY)           *                    
*           P2  BYTES 0 = 1, OUTPUT A TEN BYTE NAME        *                    
*           P2  BYTES 1-3 = A(OUTPUT AREA - 2X14 BYTES) OR *                    
*                           A(10 BYTE AREA)                *                    
*                                                          *                    
************************************************************                    
         TITLE 'ACDISCOL - DISPLAY COLUMN LIST NAME'                            
ACDISCOL CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**DICL**,RA,RR=RE,CLEAR=YES                          
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         MVC   PARMS(PARML),0(R1)                                               
         ST    RE,RELO                                                          
*                                                                               
         LM    R5,R6,ACOLS                                                      
         USING JBCLD,R5                                                         
         LA    R2,NCOLENT          R2=LOOP COUNTER                              
         LA    R3,COLTAB           R3=A(COLUMN TABLE ENTRY)                     
         USING COLTABD,R3                                                       
         MVC   HEADAREA,SPACES                                                  
         CLI   JBCLTYP,JBCLCOL                                                  
         BNE   DISP6                                                            
*                                                                               
DISP2    CLC   JBCLCN1,COLNUM      MATCH ON COLUMN NUMBER                       
         BE    DISP4                                                            
         LA    R3,COLTABL(R3)                                                   
         BCT   R2,DISP2                                                         
         B     DISPX                                                            
*                                                                               
DISP4    MVC   HEADTEN,COLTEN                                                   
         CLI   MODE,TENBYTE                                                     
         BE    DISP5                                                            
*                                                                               
         MVC   HEAD1,COLNAME1                                                   
         MVC   HEAD2,COLNAME2                                                   
*                                                                               
DISP5    OC    COLROUT,COLROUT     TEST FOR ADDITIONAL PROCESSING               
         BZ    DISPX               NO                                           
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,3,COLROUT                                                     
         LA    RF,ACDISCOL(RF)     INDEX TO ROUTINE                             
         BASR  RE,RF                                                            
         B     DISPX                                                            
*                                                                               
DISP6    CLI   JBCLTYP,JBCLSUPP    IS THIS A SUPPLEMENT (ACPRO49) ?             
         BE    DISP7               YES                                          
         CLI   JBCLTYP,JBCLFRM     NO, IS THIS A FORMULA (ACPRO46)?             
         BNE   DISPX               NO, EXIT                                     
         OC    JBCLSNV,JBCLSNV     YES, IS IT A SUPPLEMENT ?                    
         BZ    DISPX               NO, EXIT                                     
*                                                                               
         USING SUPPTABD,R3                                                      
DISP7    LA    R3,SUPPTAB                                                       
         LA    R2,NSUPENT                                                       
*                                                                               
DISP8    CLC   JBCLCN1,SUPPNUM                                                  
         BE    DISP10                                                           
*                                                                               
         LA    R3,SUPPTABL(R3)                                                  
         BCT   R2,DISP8                                                         
         B     DISPX                                                            
*                                                                               
DISP10   MVC   HEADTEN,SUPPTEN                                                  
         LA    R4,HEADTEN+4                                                     
         CLI   MODE,TENBYTE                                                     
         BE    DISP11                                                           
*                                                                               
         MVC   HEAD1(L'SUPPNAM1),SUPPNAM1                                       
         MVC   HEAD2(L'SUPPNAM2),SUPPNAM2                                       
         LA    R4,HEAD1+L'SUPPNAM1                                              
*                                                                               
DISP11   LA    R2,JBCLSNV+1                                                     
         CLI   JBCLTYP,JBCLSUPP    DIFFEREENT FIELD IF FROM ACPRO49             
         BNE   *+8                                                              
         LA    R2,JBCLCN1V                                                      
         EDIT  (B1,0(R2)),(3,0(R4)),0,ALIGN=LEFT                                
         DROP  R3                                                               
*                                                                               
DISPX    LA    R1,L'HEADTEN-1                                                   
         CLI   MODE,TENBYTE                                                     
         BE    *+8                                                              
         LA    R1,L'HEAD1+L'HEAD2-1                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R6),HEADAREA                                                 
DISPXX   XMOD1 1                                                                
*                                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
* SUB-ROUTINE FOR ADDITIONAL ESTIMATE PROCESSING                                
*        0(R6) IS OUTPUT AREA                                                   
*        R5 POINTS TO REQUEST DATA AREA                                         
*        R3 POINTS TO COLUMN SELECTED                                           
*                                                                               
         USING COLTABD,R3                                                       
EST      NTR1                                                                   
*                                                                               
         ZIC   R0,JBCLCN1V         SAVE NUMBER IN R0                            
*                                                                               
         CLI   JBCLCN1E,C'P'       PLANNING EST                                 
         BNE   ESTR                USE REVISION LOGIC                           
*                                                                               
         MVC   HEADTEN,PLANTEN                                                  
         LA    R2,HEADTEN+5        WHERE TO PRINT THE "NNN"                     
         CLI   MODE,TENBYTE                                                     
         BE    ESTP05                                                           
*                                                                               
         MVC   HEAD1,PLANL1                                                     
         MVC   HEAD2,PLANL2                                                     
         LA    R2,HEADAREA+23      WHERE TO PRINT THE "NNN"                     
*                                                                               
ESTP05   BAS   RE,EDITNUM                                                       
         CLC   JBCLCN1,=Y(JBDEST)                                               
         BE    ESTX                                                             
*                                                                               
         MVC   HEADTEN,PLANTENA                                                 
         LA    R2,HEADTEN+4        WHERE TO PRINT THE "NNN"                     
*                                                                               
         CLI   MODE,TENBYTE                                                     
         BE    ESTP05A                                                          
*                                                                               
         MVC   HEADAREA(L'PLANL1A),PLANL1A                                      
         MVC   HEADAREA+L'PLANL1A(L'PLANL2A),PLANL2A                            
*                                                                               
         LA    R2,HEADAREA+19      WHERE TO PRINT THE "NNN"                     
*                                                                               
ESTP05A  ZIC   R0,JBCLCN1V         RESTORE NUMBER                               
         BAS   RE,EDITNUM                                                       
         AR    R2,R0               BUMP LENGTH OF OUTPUT                        
         LA    R2,1(R2)            ADD A SPACE                                  
*                                                                               
         LA    R3,SUFXTAB          PRINT THE NET, GROSS, COM SUFFIX             
         LA    R1,SUFXNUM                                                       
ESTP10   CLC   JBCLCN1,0(R3)                                                    
         BE    ESTP20                                                           
*                                                                               
         LA    R3,L'SUFXTAB(R3)                                                 
         BCT   R1,ESTP10                                                        
         B     NOTOK                                                            
*                                                                               
ESTP20   MVC   0(3,R2),7(R3)                                                    
         CLI   MODE,TENBYTE                                                     
         BE    ESTX                                                             
*                                                                               
         MVC   0(5,R2),2(R3)                                                    
         B     ESTX                                                             
*                                                                               
ESTR     CLI   JBCLCN1E,C'R'       REVISION?                                    
         BNE   NOTOK               HAS TO BE P OR R                             
*                                                                               
         CLI   JBCLCN1V,1          IS THIS AN ORIGINAL ESTIMATE                 
         BNE   ESTR20              NO                                           
*                                                                               
         AH    R3,=Y(ORGEST-COLTAB) USE ORIGINAL EST PRINT                      
*                                                                               
         MVC   HEADTEN,COLTEN                                                   
         CLI   MODE,TENBYTE                                                     
         BE    ESTX                                                             
*                                                                               
         MVC   HEAD1,COLNAME1                                                   
         MVC   HEAD2,COLNAME2                                                   
         B     ESTX                DON'T NEED TO PRINT NUMBER                   
*                                                                               
ESTR20   BCTR  R0,0                DECREMENT REVISION FOR PRINT                 
         MVC   HEADTEN,COLTEN                                                   
         LA    R2,HEADTEN+4                                                     
*                                                                               
         CLM   R0,1,=AL1(100)      IS REV NUMBER GT 100                         
         BL    *+8                                                              
         LA    R2,HEADTEN+3        PRINT SHIFTED LEFT                           
*                                                                               
         CLI   MODE,TENBYTE                                                     
         BE    ESTR30                                                           
         MVC   HEAD1,COLNAME1                                                   
         MVC   HEAD2,COLNAME2                                                   
         LA    R2,HEADAREA+9       WHERE TO PRINT ESTIMATE NUMBER               
*                                                                               
ESTR30   BAS   RE,EDITNUM                                                       
*                                                                               
ESTX     B     EXIT                                                             
*                                                                               
EDITNUM  EQU   *                   RETURNS R0 AS LENGTH OF OUTPUT               
         ST    RE,SAVERE                                                        
         EDIT  (R0),(3,0(R2)),ALIGN=LEFT                                        
         L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
         EJECT                                                                  
OK       CR    RB,RB                                                            
         B     EXIT                                                             
*                                                                               
NOTOK    LTR   RB,RB                                                            
         B     EXIT                                                             
         SPACE 2                                                                
* CONSTANTS                                                                     
*                                                                               
SPACES   DC    CL80' '                                                          
PLANL1   DC    CL14'PLANNING'                                                   
PLANL2   DC    CL14'ESTIMATE'                                                   
PLANL1A  DC    CL14'PLANNING '                                                  
PLANL2A  DC    CL14'EST.'                                                       
PLANTEN  DC    CL10'PLAN'                                                       
PLANTENA DC    CL10'PLN'                                                        
*                                                                               
SUFXTAB  DS    0CL10                                                            
         DC    AL2(JBDESTN),CL5'NET',CL3'NET'                                   
         DC    AL2(JBDESTG),CL5'GROSS',CL3'GRS'                                 
         DC    AL2(JBDESTC),CL5'COMM.',CL3'COM'                                 
         DC    AL2(JBDESTH),CL5'HOURS',CL3'HRS'                                 
SUFXNUM  EQU   (*-SUFXTAB)/L'SUFXTAB                                            
         SPACE 2                                                                
* TABLE OF VALID COLUMN DATA TYPES                                              
*                                                                               
COLTAB   DS    0CL(COLTABL)                                                     
*                                                                               
         DC    AL2(JBDEST),CL14'REVISION',CL14' '                               
         DC    AL2(EST-ACDISCOL)                                                
         DC    CL10'REV'                                                        
         DC    AL2(JBDESTN),CL14'REVISION',CL14'NET'                            
         DC    AL2(EST-ACDISCOL)                                                
         DC    CL10'REV    NET'                                                 
         DC    AL2(JBDESTG),CL14'REVISION',CL14'GROSS'                          
         DC    AL2(EST-ACDISCOL)                                                
         DC    CL10'REV    GRS'                                                 
         DC    AL2(JBDESTC),CL14'REVISION',CL14'COMMISSION'                     
         DC    AL2(EST-ACDISCOL)                                                
         DC    CL10'REV    COM'                                                 
         DC    AL2(JBDESTH),CL14'REVISION',CL14'HOURS'                          
         DC    AL2(EST-ACDISCOL)                                                
         DC    CL10'REV  HOURS'                                                 
         DC    AL2(JBDESTHB),CL14'REVISION',CL14'HOURS B'                       
         DC    AL2(EST-ACDISCOL)                                                
         DC    CL10'REV  HRS B'                                                 
         DC    AL2(JBDESTHN),CL14'REVISION',CL14'HOURS N'                       
         DC    AL2(EST-ACDISCOL)                                                
         DC    CL10'REV  HRS N'                                                 
         DC    AL2(JBDESTHR),CL14'REVISION',CL14'HOURS R'                       
         DC    AL2(EST-ACDISCOL)                                                
         DC    CL10'REV  HRS R'                                                 
*                                                                               
*        HIGHEST REVISION HANDLED LIKE ESTIMATES                                
*        FOR NOW                                                                
         DC    AL2(JBDHR),CL14'HIGHEST',CL14'REVISION'                          
         DC    AL2(0)                                                           
         DC    CL10'HIGH REV.'                                                  
         DC    AL2(JBDHRG),CL14'HIGHEST REV.',CL14'GROSS'                       
         DC    AL2(0)                                                           
         DC    CL10'HI REV GRS'                                                 
         DC    AL2(JBDHRC),CL14'HIGHEST REV.',CL14'COMMISSION'                  
         DC    AL2(0)                                                           
         DC    CL10'HI REV COM'                                                 
         DC    AL2(JBDHRH),CL14'HIGHEST REV.',CL14'HOURS'                       
         DC    AL2(0)                                                           
         DC    CL10'HI REV HRS'                                                 
         DC    AL2(JBDHRHB),CL14'HIGHEST REV.',CL14'HOURS B'                    
         DC    AL2(0)                                                           
         DC    CL10'HI RV HR B'                                                 
         DC    AL2(JBDHRHN),CL14'HIGHEST REV.',CL14'HOURS N'                    
         DC    AL2(0)                                                           
         DC    CL10'HI RV HR N'                                                 
         DC    AL2(JBDHRHR),CL14'HIGHEST REV.',CL14'HOURS R'                    
         DC    AL2(0)                                                           
         DC    CL10'HI RV HR R'                                                 
*                                                                               
         DC    AL2(JBDMEST),CL14'MAX EST. ',CL14' '                             
         DC    AL2(EST-ACDISCOL)                                                
         DC    CL10'MAX EST. '                                                  
         DC    AL2(JBDMESTG),CL14'MAX EST.',CL14'GROSS'                         
         DC    AL2(EST-ACDISCOL)                                                
         DC    CL10'MAX GROSS'                                                  
         DC    AL2(JBDMESTC),CL14'MAX EST.',CL14'COMMISSION'                    
         DC    AL2(EST-ACDISCOL)                                                
         DC    CL10'MAX COM  '                                                  
*                                                                               
ORGEST   DC    AL2(JBDORG),CL14'ORIGINAL',CL14'ESTIMATE'                        
         DC    AL2(0)                                                           
         DC    CL10'ORIGINAL'                                                   
         DC    AL2(JBDORGN),CL14'ORIGINAL',CL14'ESTIMATE (NET)'                 
         DC    AL2(0)                                                           
         DC    CL10'OE NET'                                                     
         DC    AL2(JBDORGG),CL14'ORIGINAL',CL14'ESTIMATE-GROSS'                 
         DC    AL2(0)                                                           
         DC    CL10'OE GROSS'                                                   
         DC    AL2(JBDORGC),CL14'ORIGINAL',CL14'ESTIMATE-COMM'                  
         DC    AL2(0)                                                           
         DC    CL10'OE COM'                                                     
         DC    AL2(JBDORGH),CL14'ORIGINAL',CL14'ESTIMATE-HOURS'                 
         DC    AL2(0)                                                           
         DC    CL10'OE HOUR'                                                    
         DC    AL2(JBDORGHB),CL14'ORIGINAL',CL14'ESTIMATE-HRS B'                
         DC    AL2(0)                                                           
         DC    CL10'OE HOURS B'                                                 
         DC    AL2(JBDORGHN),CL14'ORIGINAL',CL14'ESTIMATE-HRS N'                
         DC    AL2(0)                                                           
         DC    CL10'OE HOURS N'                                                 
         DC    AL2(JBDORGHR),CL14'ORIGINAL',CL14'ESTIMATE-HRS R'                
         DC    AL2(0)                                                           
         DC    CL10'OE HOURS R'                                                 
*                                                                               
         DC    AL2(JBDCUR),CL14'CURRENT',CL14'ESTIMATE'                         
         DC    AL2(0)                                                           
         DC    CL10'CURRENT'                                                    
         DC    AL2(JBDCURN),CL14'CURRENT',CL14'ESTIMATE (NET)'                  
         DC    AL2(0)                                                           
         DC    CL10'CE NET'                                                     
         DC    AL2(JBDCURG),CL14'CURRENT',CL14'ESTIMATE-GROSS'                  
         DC    AL2(0)                                                           
         DC    CL10'CE GROSS'                                                   
         DC    AL2(JBDCURC),CL14'CURRENT',CL14'ESTIMATE-COMM'                   
         DC    AL2(0)                                                           
         DC    CL10'CE COM'                                                     
         DC    AL2(JBDCURH),CL14'CURRENT',CL14'ESTIMATE-HOURS'                  
         DC    AL2(0)                                                           
         DC    CL10'CE HOURS'                                                   
         DC    AL2(JBDCURHB),CL14'CURRENT',CL14'ESTIMATE-HRS B'                 
         DC    AL2(0)                                                           
         DC    CL10'CE HOURS B'                                                 
         DC    AL2(JBDCURHN),CL14'CURRENT',CL14'ESTIMATE-HRS N'                 
         DC    AL2(0)                                                           
         DC    CL10'CE HOURS N'                                                 
         DC    AL2(JBDCURHR),CL14'CURRENT',CL14'ESTIMATE-HRS R'                 
         DC    AL2(0)                                                           
         DC    CL10'CE HOURS R'                                                 
*                                                                               
         DC    AL2(JBDRATE),CL14'COMMISSION',CL14'RATE'                         
         DC    AL2(0)                                                           
         DC    CL10'COMM RATE'                                                  
*                                                                               
         DC    AL2(JBDACT),CL14'ACTUAL',CL14'CHARGES'                           
         DC    AL2(0)                                                           
         DC    CL10'ACTUAL'                                                     
         DC    AL2(JBDACTG),CL14'ACTUAL CHARGES',CL14'GROSS'                    
         DC    AL2(0)                                                           
         DC    CL10'ACT. GROSS'                                                 
         DC    AL2(JBDACTC),CL14'ACTUAL CHARGES',CL14'COMMISSION'               
         DC    AL2(0)                                                           
         DC    CL10'ACT. COM.'                                                  
         DC    AL2(JBDACTH),CL14'ACTUAL CHARGES',CL14'HOURS'                    
         DC    AL2(0)                                                           
         DC    CL10'ACT. HOURS'                                                 
*                                                                               
         DC    AL2(JBDPO),CL14'PURCHASE',CL14'ORDERS'                           
         DC    AL2(0)                                                           
         DC    CL10'PO NET'                                                     
         DC    AL2(JBDPOG),CL14'PURCHASE ORDERS',CL14'GROSS'                    
         DC    AL2(0)                                                           
         DC    CL10'PO GROSS'                                                   
         DC    AL2(JBDPOC),CL14'PURCHASE ORDERS',CL14'COMMISSION'               
         DC    AL2(0)                                                           
         DC    CL10'PO COM'                                                     
*                                                                               
         DC    AL2(JBDBAL),CL14'BALANCE',CL14' '                                
         DC    AL2(0)                                                           
         DC    CL10'BALANCE'                                                    
         DC    AL2(JBDBALG),CL14'BALANCE',CL14'GROSS'                           
         DC    AL2(0)                                                           
         DC    CL10'BAL. GROSS'                                                 
         DC    AL2(JBDBALC),CL14'BALANCE',CL14'COMMISSION'                      
         DC    AL2(0)                                                           
         DC    CL10'BAL. COM'                                                   
*                                                                               
         DC    AL2(JBDUNC),CL14'UNCOMMITTED',CL14' '                            
         DC    AL2(0)                                                           
         DC    CL10'UNC. NET'                                                   
         DC    AL2(JBDUNCG),CL14'UNCOMMITTED',CL14'GROSS'                       
         DC    AL2(0)                                                           
         DC    CL10'UNC. GROSS'                                                 
         DC    AL2(JBDUNCC),CL14'UNCOMMITTED',CL14'COMMISSION'                  
         DC    AL2(0)                                                           
         DC    CL10'UNC. COM'                                                   
*                                                                               
         DC    AL2(JBDPCOM),CL14'PERCENT',CL14'COMMITTED'                       
         DC    AL2(0)                                                           
         DC    CL10'%COM NET'                                                   
         DC    AL2(JBDPCOMG),CL14'% COMMITTED',CL14'GROSS'                      
         DC    AL2(0)                                                           
         DC    CL10'%COM GROSS'                                                 
         DC    AL2(JBDPCOMC),CL14'% COMMITTED',CL14'COMMISSION'                 
         DC    AL2(0)                                                           
         DC    CL10'%COM COM'                                                   
*                                                                               
         DC    AL2(JBDPUNC),CL14'PERCENT',CL14'UNCOMMITTED'                     
         DC    AL2(0)                                                           
         DC    CL10'%UNC NET'                                                   
         DC    AL2(JBDPUNCG),CL14'% UNCOMMITTED',CL14'GROSS'                    
         DC    AL2(0)                                                           
         DC    CL10'%UNC GROSS'                                                 
         DC    AL2(JBDPUNCC),CL14'% UNCOMMITTED',CL14'COMMISSION'               
         DC    AL2(0)                                                           
         DC    CL10'% UNC COM'                                                  
*                                                                               
         DC    AL2(JBDMORG),CL14'MAXIMUM ORIG.',CL14'ESTIMATE'                  
         DC    AL2(0)                                                           
         DC    CL10'MAX OE NET'                                                 
         DC    AL2(JBDMORGG),CL14'MAX ORIGINAL',CL14'ESTIMATE-GROSS'            
         DC    AL2(0)                                                           
         DC    CL10'MAX OE GRS'                                                 
         DC    AL2(JBDMORGC),CL14'MAX ORIGINAL',CL14'EST.-COMMISSION'           
         DC    AL2(0)                                                           
         DC    CL10'MAX OE COM'                                                 
*                                                                               
         DC    AL2(JBDMCUR),CL14'MAXIMUM CURR.',CL14'ESTIMATE'                  
         DC    AL2(0)                                                           
         DC    CL10'MAX CE NET'                                                 
         DC    AL2(JBDMCURG),CL14'MAX CURRENT',CL14'ESTIMATE-GROSS'             
         DC    AL2(0)                                                           
         DC    CL10'MAX CE GRS'                                                 
         DC    AL2(JBDMCURC),CL14'MAX CURRENT',CL14'EST.-COMMISSION'            
         DC    AL2(0)                                                           
         DC    CL10'MAX CE COM'                                                 
*                                                                               
         DC    AL2(JBDPRV),CL14'PREVIOUS',CL14'ESTIMATE'                        
         DC    AL2(0)                                                           
         DC    CL10'PREVIOUS'                                                   
         DC    AL2(JBDPRVN),CL14'PREVIOUS',CL14'ESTIMATE-NET'                   
         DC    AL2(0)                                                           
         DC    CL10'PRV NET'                                                    
         DC    AL2(JBDPRVG),CL14'PREVIOUS',CL14'ESTIMATE-GROSS'                 
         DC    AL2(0)                                                           
         DC    CL10'PRV GRS'                                                    
         DC    AL2(JBDPRVC),CL14'PREVIOUS',CL14'ESTIMATE-COMM'                  
         DC    AL2(0)                                                           
         DC    CL10'PRV COM'                                                    
*                                                                               
NCOLENT  EQU   (*-COLTAB)/L'COLTAB                                              
         SPACE 2                                                                
SUPPTAB  DS    0CL(SUPPTABL)                                                    
*                                                                               
         DC    AL2(JBDEST),CL14'SUPPLEMENT',CL14' '                             
         DC    CL10'SUP '                                                       
         DC    AL2(JBDESTN),CL14'SUPPLEMENT',CL14'NET'                          
         DC    CL10'SUP    NET'                                                 
         DC    AL2(JBDESTG),CL14'SUPPLEMENT',CL14'GROSS'                        
         DC    CL10'SUP    GRS'                                                 
         DC    AL2(JBDESTC),CL14'SUPPLEMENT',CL14'COMMISSION'                   
         DC    CL10'SUP    COM'                                                 
         DC    AL2(JBDESTH),CL14'SUPPLEMENT',CL14'HOUR'                         
         DC    CL10'SUP    HRS'                                                 
*                                                                               
NSUPENT  EQU   (*-SUPPTAB)/L'SUPPTAB                                            
         SPACE 2                                                                
*                                                                               
* LITERAL POOL                                                                  
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
* DSECT TO COVER ACJOBCOL WORKING STORAGE                                       
*                                                                               
WORKD    DSECT                                                                  
RELO     DS    A                                                                
APARM    DS    A                                                                
PARMS    DS    0A                                                               
ACOLS    DS    A                                                                
MODE     DS    0CL1                                                             
TENBYTE  EQU   1                                                                
AOUT     DS    A                                                                
PARML    EQU   *-PARMS                                                          
SAVERE   DS    A                                                                
*                                                                               
DUB      DS    D                                                                
DMCB     DS    6F                                                               
*                                                                               
WORK     DS    CL64                                                             
*                                                                               
HEADAREA DS    0CL28                                                            
HEAD1    DS    CL14                                                             
HEAD2    DS    CL14                                                             
         ORG   HEADAREA                                                         
HEADTEN  DS    CL10                                                             
         ORG   HEADAREA+L'HEADAREA                                              
*                                                                               
*                                                                               
WORKX    EQU   *                                                                
         SPACE 2                                                                
* DSECT TO COVER COLUMN TABLE                                                   
*                                                                               
COLTABD  DSECT                                                                  
COLNUM   DS    XL2                                                              
COLNAME1 DS    CL14                FIRST NAME                                   
COLNAME2 DS    CL14                SECOND NAME                                  
COLROUT  DS    AL2                 DISPLACEMENT TO OUTPUT ROUTINE               
COLTEN   DS    CL10                TEN BYTE NAME                                
COLTABL  EQU   *-COLTABD           ENTRY LENGTH                                 
         SPACE 2                                                                
* DSECT TO SUPPLEMENT COLUMN TABLE                                              
*                                                                               
SUPPTABD DSECT                                                                  
SUPPNUM  DS    XL2                                                              
SUPPNAME DS    0CL14               FIRST NAME                                   
SUPPNAM1 DS    CL11                                                             
SUPPVER  DS    CL3                                                              
SUPPNAM2 DS    CL14                SECOND NAME                                  
SUPPTEN  DS    CL10                TEN BYTE SUPPLEMENT NAME                     
SUPPTABL EQU   *-SUPPTABD          ENTRY LENGTH                                 
         SPACE 2                                                                
       ++INCLUDE ACJOBBERD                                                      
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'034ACDISCOL  05/01/02'                                      
         END                                                                    
