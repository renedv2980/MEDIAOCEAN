*          DATA SET ACCONVMOS  AT LEVEL 002 AS OF 06/26/96                      
*CATALP CONVMOS                                                                 
CONVMOS  TITLE '- LOOKS UP OR CALCULATES TRANSACTION MOS VALUE'                 
CONVMOS  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,*CONV*                                               
         USING WORKD,RC            RC=A(LOCAL WORKING STORAGE)                  
         L     R2,0(R1)                                                         
         USING TRNELD,R2           R2=A(TRANSACTION ELEMENT)                    
*                                                                               
         CLI   0(R1),X'FD'                                                      
         BE    *+12                                                             
         CLI   0(R1),X'FE'         TEST TRANSACTION RECORD LOOKUP               
         BNE   CONV4                                                            
         LA    R3,TRNELD                                                        
         SR    R0,R0                                                            
         USING TRSEL,R3            LOCATE TRANSACTION STATUS ELEMENT            
CONV2    ICM   R0,1,TRSLN          BUMP TO NEXT ELEMENT                         
         BZ    CONV4                                                            
         AR    R3,R0                                                            
         CLI   TRSEL,0             TEST E-O-R                                   
         BE    CONV4                                                            
         CLI   TRSEL,TRSELQ        TEST STATUS ELEMENT                          
         BNE   CONV2                                                            
         CLI   TRSLN,TRSPMOS+L'TRSPMOS-TRSELD                                   
         BL    CONV3                                                            
         OC    TRSPMOS,TRSPMOS     TEST POSTING MOS SET                         
         BZ    CONV3                                                            
         MVC   WORK(L'TRSPMOS),TRSPMOS                                          
         B     CONVX                                                            
*                                                                               
CONV3    OC    TRSDATE,TRSDATE     ACTIVITY DATE?                               
         BZ    CONV4                                                            
         SR    RE,RE                                                            
         ICM   RE,3,TRSDATE        COMPRESSED DATE                              
         SRL   RE,5                SHIFT OUT THE DAY                            
         SR    RF,RF                                                            
         SRDL  RE,4                SHIFT MONTH INTO RF                          
         SRL   RF,28               MONTH TO LOW ORDER OF RF                     
         CH    RF,=H'9'            TEST LAST QUARTER                            
         BNH   *+8                 DECIMAL MONTH TO RF                          
         LA    RF,6(RF)            SHIFT OUT THE SIGN                           
         STC   RF,YRMO+1           AND MONTH                                    
         CVD   RE,DUB              YEAR TO DECIMAL                              
         SRDL  RE,32               SHIFT YEAR INTO RF                           
         D     RE,=F'10'           DIVIDE BY 10                                 
         SLL   RF,4                X 16   (105 BECOMES A0)                      
         ICM   RE,3,DUB+6          DECIMAL YEAR TO RE                           
         SRL   RE,4                SHIFT OUT THE SIGN                           
         N     RE,=X'0000000F'     (105 BECOMES 5)                              
         AR    RF,RE               ADD YEAR TO DECADE                           
         STC   RF,YRMO             STORE YEAR - PWOS                            
         B     CONV5                                                            
*                                                                               
CONV4    CLI   TRNEL,TRNELQ        ENSURE THIS IS A TRANSACTION                 
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   YRMO(2),TRNDATE                                                  
*                                                                               
CONV5    SR    RF,RF                                                            
         IC    RF,YRMO             GET THE DECADE                               
         SRL   RF,4                DIVIDED BY 16                                
         MH    RF,=H'10'           X 10                                         
         CVD   RF,DUB                                                           
         ZAP   WORK(2),DUB+6(2)    SAVE DECADE (2000 = 100)                     
         IC    RF,YRMO             GET THE YEAR                                 
         N     RF,=X'0000000F'                                                  
         CVD   RF,DUB                                                           
         ZAP   WORK+2(1),DUB+7(1)  SAVE TRANSACTION YEAR                        
*                                                                               
         PACK  WORK+3(1),TRNMOS(1) SAVE MOS YEAR                                
*                                                                               
         ZAP   WORK+6(2),WORK(2)   DECADE                                       
         AP    WORK+6(2),WORK+2(1) TRANSACTION YEAR                             
         ZAP   WORK+4(2),WORK(2)   DECADE                                       
         AP    WORK+4(2),WORK+3(1) MOS  YEAR                                    
*                                                                               
         SP    WORK+6(2),WORK+4(2)                                              
         CLI   0(R1),X'FD'                                                      
         BNE   *+14                                                             
         CP    WORK+6(2),=P'5'                                                  
         B     *+10                                                             
         CP    WORK+6(2),=P'2'                                                  
         BNH   *+10                                                             
         AP    WORK(2),=P'10'                                                   
         CLI   0(R1),X'FD'                                                      
         BNE   *+14                                                             
         CP    WORK+6(2),=P'-5'                                                 
         B     *+10                                                             
         CP    WORK+6(2),=P'-8'                                                 
         BNL   *+10                                                             
         SP    WORK(2),=P'10'                                                   
*                                                                               
         ZAP   DUB,WORK(2)                                                      
         CVB   RF,DUB              MOS DECADE                                   
         SR    RE,RE                                                            
         D     RE,=F'10'           DIVIDE BY 10                                 
         SLL   RF,4                X 16                                         
         IC    RE,TRNMOS           MOS YEAR                                     
         N     RE,=X'0000000F'                                                  
         AR    RF,RE                                                            
         STC   RF,WORK             STORE MOS DECADE/YEAR                        
         IC    RF,TRNMOS+1         MOS MONTH                                    
         N     RF,=X'0000000F'                                                  
         TM    TRNMOS+1,X'F0'                                                   
         BO    *+8                                                              
         LA    RF,15(RF)                                                        
         STC   RF,WORK+1                                                        
*                                                                               
CONVX    L     R2,4(R1)            ADDRESS OF RETURN AREA                       
         MVC   0(2,R2),WORK        RETURN MOS IN CALLER'S OUTPUT AREA           
         XIT1  ,                                                                
         EJECT                                                                  
         LTORG                                                                  
         SPACE 2                                                                
WORKD    DSECT                     ** LOCAL WORKING STORAGE **                  
DUB      DS    D                                                                
WORK     DS    XL12                                                             
YRMO     DS    XL2                                                              
WORKX    EQU   *                                                                
         SPACE 2                                                                
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002ACCONVMOS 06/26/96'                                      
         END                                                                    
