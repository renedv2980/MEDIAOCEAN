*          DATA SET ACTIMETRN  AT LEVEL 025 AS OF 07/26/17                      
*CATALP TIMETRN                                                                 
         TITLE 'TIMESHEET POSTING/RECORD PROCESSING'                            
* NSHE 096 140300 PASS COMPANY STATUS 9 TO ADDTRN (EUROPE ONLY)                 
* NSHE 097 010700 CHANGES NEEDED INORDER TO ARCHIVE TRANSACTIONS                
* AMAD 101 180901 ENSURE RATE IN OCAEL IS POSITIVE                              
* NSHE 103 020103 ADD CLIENT TIME METHOD BASED BUCKETS                          
* NSHE 104 040403 ADD DIRECT TIME POINTER RECORDS FOR PROFIT AND LOSS           
* NSHE 105 050803 ADD JIM SHEA CHANGES FOR BUFFER ARRANGEMENT                   
* DON'T USE R9 AS USED TO COVER GLOBAL STORAGE FOR TMS                          
* NSHE 106 090903 FIX TO GETEURO CALL - CHECK IF BILLABLE                       
* NSHE 107 180903 FIX TO OCAEL BUG                                              
* YNGX 108 300903 FIX TO GETEURO ROUTINE                                        
* NSHE 109 071003 FIX BUG IN NEW SUMMARY RECORDS                                
* NSHE 110 221003 FIX BUG WITH NON CLIENT TIME FOR ONLINE UPDATE                
* NSHE 111 181203 SET TRAP FOR FINDING BUG WITH ONLINE UPDATE                   
* NSHE 112 130104 REWRITE STORAGE AND ROUTINES                                  
* TKLU 113 200404 COMMENTS ONLY                                                 
* NSHE 114 090704 MERGE US AND UK VERSION READY FOR DAILY TIME                  
* NSHE 115 070904 CHANGE TO ADD FFTEL WITH PERIOD END DATE                      
* NSHE 116 041104 FIX STORAGE OF METHTAB NOT IN PROGRAM CODE                    
* NSHE 117 140206 ADD CODE TO COPE WITH MCS TIME                                
* JFOS 117 150506 <LO01-4478> SET TRSSTAT4 ON SJ POSTING IF MCS TIME            
* NSHE 117 090606 <LO01-5492> CREATE POSTINGS FOR MATERIALS                     
* NSHE 118 130906 <UKCR00009077>  FIX CLIENT APPROVAL POSTING BUG               
* NSHE 119 210906 <1049525>  FIX EURO PROBLEM                                   
* NSHE 120 210906 <UKCR00009284> FIX STATUS CHANGE BUG FOR POSTINGS             
* NSHE 121 270906 IMPROVE PERFORMANCE WHEN APPROVING                            
* NSHE 122 061106 IMPROVE PERFORMANCE FOR BRANDOCEAN                            
* NSHE 123 220207 IMPROVE ERROR MESSAGING FOR BRANDOCEAN                        
* NSHE 125 060307 BUG FIX WHEN SAVING TIMESHEETS WITH NO RATE                   
* NSHE 126 080307 ENSURE REVERSALS HAPPEN                                       
* NSHE 127 190307 FIX LOOP CONCERNING 12 ACC NAME                               
* NSHE 128 210307 FIX BAD CONTRA WHEN ADJUSTMENTS MADE IN COST                  
* NSHE 129 300407 FIX MATERIALS WHEN REJECTING                                  
* NSHE 131 100807 DON'T PROCESS NON ACCOUNT TIME RECORDS                        
* NSHE 132 261107 ENHANCE LEVEL 131 CHANGE                                      
* SMAN 133 210108 <LO01-7127> REMOVE DMUNLK CALLS TO DATAMGR                    
TIMETRN  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 TIMETRNX-TIMETRND,*TIMETRN,RA,RR=R2,CLEAR=YES                    
         USING TIMETRND,RC                                                      
         MVC   SAVERD,4(RD)                                                     
         L     R8,0(R1)            PARAMETER 1 =A(CONTROL BLOCK)                
         USING TTRND,R8                                                         
         ST    R2,RELO                                                          
         LH    R1,=Y(TRNIO2-TIMETRND)                                           
         AR    R1,RC                                                            
         ST    R1,ATRNIO2                                                       
         LH    R1,=Y(METHTAB-TIMETRND)                                          
         AR    R1,RC                                                            
         ST    R1,AMETHTAB                                                      
         MVC   LITVALS(LITVALSL),GLOBALS                                        
         LA    R1,OVROUT1          SET A(ROOT ROUTINES IN W/S)                  
         LA    R0,OVROUT1N                                                      
         XR    RE,RE                                                            
         L     RF,=A(ROUTS1)                                                    
         A     RF,RELO             RF=A(GLOBAL ROUTINES)                        
TIME10   STCM  RE,1,0(R1)          SET ROUTINE NUMBER                           
         STCM  RF,7,1(R1)          SET ROUTINE ADDRESS                          
         LA    R1,4(R1)                                                         
         LA    RE,1(RE)                                                         
         BCT   R0,TIME10                                                        
*                                                                               
         LA    R1,GLOBALSL                                                      
         LA    RF,LITVALSL                                                      
         CR    RF,R1                                                            
         JE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVI   TTRETURN,TTROK                                                   
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
         LH    R1,=Y(ACTRFST-ACTRECD)                                           
         STH   R1,DATADISP                                                      
         ICM   R1,15,TTCFACS       A(COMFACS)                                   
         BNZ   *+6                                                              
         DC    H'0'                MISSING ADDRESS OF COMFACS                   
         L     RF,TTCFACS          COMPRESSED ACTIVITY DATE                     
         L     RF,CGETFACT-COMFACSD(RF)                                         
         GOTO1 (RF),DMCB,0         TEST IO COUNT                                
         L     R1,0(R1)                                                         
         SR    R0,R0                                                            
         ICM   R0,3,FATIOCNT-FACTSD(R1)                                         
         ST    R0,IOCOUNT                                                       
         XC    GOCOUNT,GOCOUNT                                                  
         XC    SVTMSMOA,SVTMSMOA                                                
         MVC   BC1RACCT,TTACKUL    VALIDATE 1R ACCOUNT                          
         TM    TTSTAT1,TTSTEXTD    USING EXTENDED TIMETRND                      
         BNZ   TIME12              YES - NAME ALREADY PASSED                    
         GOTO1 AGETACC,BC1RACCT                                                 
         MVC   BC1RNAME,BCACTNAM                                                
         B     *+10                                                             
TIME12   MVC   BC1RNAME,TT1RNAME                                                
*                                                                               
TIME20   TM    TTACTION,TTCMPARE                                                
         BO    *+12                                                             
         TM    TTACTION,TTPOST     POST TRANSACTIONS                            
         BNO   TIME40                                                           
         BAS   RE,POST                                                          
         BNE   XNO                                                              
*&&US                                                                           
         TM    TTACTION,TTCMPARE                                                
         BO    TIME30                                                           
         TM    PSTSTAT,PSTM        DID WE MAKE A POSTING?                       
         BNO   TIME30                                                           
         OI    STATUS,STALAST      LAST UPDATE                                  
         GOTO1 AGOADDTR                                                         
*&&                                                                             
TIME30   L     RF,TTCFACS          COMPRESSED ACTIVITY DATE                     
         L     RF,CGETFACT-COMFACSD(RF)                                         
         GOTO1 (RF),DMCB,0         TEST IO COUNT                                
         L     R1,0(R1)                                                         
         SR    R0,R0                                                            
         ICM   R0,3,FATIOCNT-FACTSD(R1)                                         
         B     XYES                                                             
*                                                                               
TIME40   OI    TTRETURN,TTRACTN    *** ERROR - NO ACTION SPECIFIED ***          
         B     XYES                                                             
*                                                                               
XYES     SR    RC,RC                                                            
XNO      LTR   RC,RC                                                            
EXIT     XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* CREATE TRANSACTIONS/UPDATE BUCKETS ACTION                           *         
***********************************************************************         
         SPACE 1                                                                
POST     NTR1                                                                   
         XC    STATUS,STATUS       CLEAR STATUS BYTE                            
         XC    LSTRDKY,LSTRDKY     CLEAR TSAR KEY                               
         MVI   PSTSTAT,0           CLEAR POSTING STATUS                         
         MVI   BCTRNIND,0                                                       
         XC    ATRNGLBK,ATRNGLBK                                                
*                                                                               
         OI    TTACTION,TTBLDB4    BUILD BEFORE BUFFER                          
         BAS   RE,PSTBUFF          POST TSAR RECORDS TO BUFFER                  
         MVC   LSTB4KY,RDTSRKEY    SAVE OFF BEFORE KEY FOR NEXT TIME            
         NI    TTACTION,X'FF'-TTBLDB4                                           
         BAS   RE,PSTBUFF          POST TSAR RECORDS TO BUFFER                  
         MVC   LSTAFKY,RDTSRKEY    SAVE OFF AFTER KEY FOR NEXT TIME             
*                                                                               
         USING TMSD,R4                                                          
POST10   ICM   R4,15,TTBUFF1       R4=A(ORIGINAL RECORD BUFFER)                 
         BNZ   *+6                                                              
         DC    H'0'                                                             
         CLI   0(R4),X'FF'         IF ORIGINAL BUFFER IS EMPTY                  
         BNE   POST11              THEN ADD NEW POSTINGS                        
         OI    STATUS,STADDNEW                                                  
*                                                                               
POST11   ICM   R6,15,TTBUFF2       R6=A(NEW RECORD BUFFER)                      
         BNZ   *+6                                                              
         DC    H'0'                                                             
         CLI   0(R6),X'FF'         IF NEW BUFFER IS EMPTY THEN                  
         BNE   POST14              THEN BACKOUT OLD POSTINGS                    
         OI    STATUS,STBCKOUT                                                  
*                                                                               
POST14   ST    R4,BCAORIG          SAVE A(CURRENT ORIGINAL ITEM)                
         ST    R6,BCANEW           SAVE A(CURRENT NEW ITEM)                     
         TM    STATUS,STADDNEW+STBCKOUT                                         
         BO    XYES                IF BOTH ARE SET THEN DONE                    
         TM    STATUS,STADDNEW                                                  
         BO    POSTADD                                                          
         TM    STATUS,STBCKOUT                                                  
         BO    POSTNEG                                                          
*                                                                               
*              COMPARE KEY - LINE#                                              
*                                                                               
         CLC   TMSLINE#,TMSLINE#-TMSD(R6)                                       
         BL    POSTNEG                                                          
         BH    POSTADD                                                          
*                                                                               
*              COMPARE ELEMENT DATA                                             
*                                                                               
         CLC   TMSKEY(TMSKLNQ),TMSKEY-TMSD(R6)                                  
         BNE   POSTNEG                                                          
         CLC   TMSKDATA(TMSKDL2Q),TMSKDATA-TMSD(R6)                             
         BNE   POSTNGAD                                                         
*                                                                               
*              CHECK FOR MATERIALS CHANGE                                       
*                                                                               
         MVC   BCWORK1,SPACES                                                   
*                                                                               
OLD      USING TIMELD,R5                                                        
         SR    R5,R5                                                            
         ICM   R5,15,TMSAITMS      A(ITEMS)                                     
         BZ    POST16                                                           
         SR    R1,R1                                                            
         IC    R1,1(R5)                                                         
         SHI   R1,1                                                             
         BM    POST16                                                           
         MVC   BCWORK1(0),OLD.TIMEL                                             
         EX    R1,*-6                                                           
*                                                                               
POST16   MVC   BCWORK2,SPACES                                                   
NEW      USING TIMELD,R3                                                        
         SR    R3,R3                                                            
         ICM   R3,15,TMSAITMS-TMSD(R6)                                          
         BZ    POST18                                                           
         SR    R1,R1                                                            
         IC    R1,1(R3)                                                         
         SHI   R1,1                                                             
         BM    POST18                                                           
         MVC   BCWORK2(0),NEW.TIMEL                                             
         EX    R1,*-6                                                           
         DROP  OLD,NEW                                                          
*                                                                               
POST18   CLC   BCWORK1(TIMISTAT-TIMELD),BCWORK2 - DID ITEMS CHANGE              
         BNE   POSTNGAD                                                         
         CLC   BCWORK1+TIMIPIDC-TIMELD(L'BCWORK1-TIMIPIDC+TIMELD),BCWORX        
               K2+TIMIPIDC-TIMELD                                               
         BNE   POSTNGAD                                                         
*                                                                               
*              CHECK FOR CHANGE IN NARRATIVE                                    
*                                                                               
         MVC   BCWORK1,SPACES                                                   
         USING TIMELD,R5                                                        
         SR    R5,R5                                                            
         ICM   R5,15,TMSANARR      A(NARRATIVE)                                 
         BZ    POST20                                                           
         SR    R1,R1                                                            
         IC    R1,1(R5)                                                         
         SH    R1,=Y(TIMHLNQ+1)    LENGTH OF HEADER                             
         BM    POST20                                                           
         MVC   BCWORK1(0),TIMNARR                                               
         EX    R1,*-6                                                           
         OC    BCWORK1,SPACES                                                   
*                                                                               
POST20   MVC   BCWORK2,SPACES                                                   
         USING TIMELD,R5                                                        
         SR    R5,R5                                                            
         ICM   R5,15,TMSANARR-TMSD(R6)                                          
         BZ    POST30                                                           
         SR    R1,R1                                                            
         IC    R1,1(R5)                                                         
         SH    R1,=Y(TIMHLNQ+1)    LENGTH OF HEADER                             
         BM    POST30                                                           
         MVC   BCWORK2(0),TIMNARR                                               
         EX    R1,*-6                                                           
         OC    BCWORK2,SPACES                                                   
*                                                                               
POST30   CLC   BCWORK1,BCWORK2     DID NARRATIVE CHANGE                         
         BNE   POSTNGAD                                                         
*                                                                               
*              CHECK FOR CHANGE IN TAX INFORMATION                              
*                                                                               
         XC    BCWORK1,BCWORK1                                                  
         USING TIMELD,R5                                                        
         SR    R5,R5                                                            
         ICM   R5,15,TMSATAX       A(TAX ENTRY)                                 
         BZ    POST40                                                           
         SR    R1,R1                                                            
         IC    R1,1(R5)                                                         
         SH    R1,=H'1'                                                         
         BM    POST40                                                           
         MVC   BCWORK1(0),0(R5)                                                 
         EX    R1,*-6                                                           
*                                                                               
POST40   XC    BCWORK2,BCWORK2                                                  
         USING TIMELD,R5                                                        
         SR    R5,R5                                                            
         ICM   R5,15,TMSATAX-TMSD(R6)                                           
         BZ    POST50                                                           
         SR    R1,R1                                                            
         IC    R1,1(R5)                                                         
         SH    R1,=H'1'                                                         
         BM    POST50                                                           
         MVC   BCWORK2(0),0(R5)                                                 
         EX    R1,*-6                                                           
*                                                                               
POST50   CLC   BCWORK1,BCWORK2     DID TAX INFORMATION CHANGE                   
         BNE   POSTNGAD                                                         
                                                                                
         TM    TTSTAT1,TTSTTIME    ARE WE MCS TIME?                             
         BZ    POST90              NO                                           
         CLC   TTTSSTAT,TTTSPRST   HAS APPROVAL STATUS OF TS CHANGED            
         BE    POST60              NO                                           
         OI    STATUS,STA1CBUC     YES - ONLY STATUS CHANGE                     
         B     POSTNGAD                                                         
POST60   CLC   TMSMTSS,TMSMTSS-TMSD(R6) HAS STATUS OF TIME LINE CHANGED         
         BE    POST90              NO                                           
         OI    STATUS,STA1CBUC                                                  
         B     POSTNGAD                                                         
                                                                                
POST90   BAS   RE,NEXTNEW          NOTHING CHANGED SO BUMP BOTH                 
         BAS   RE,NEXTORIG         CLUSTER POINTERS                             
         B     POST10                                                           
*                                                                               
POSTNGAD TM    TTACTION,TTCMPARE                                                
         BO    POSTCMP                                                          
         TM    STATUS,STA1CBUC     ONLY STATUS CHANGE                           
         BNZ   POSTNGA2            YES - ONLY UPDATE POSTINGS                   
         BAS   RE,BACKOUT          BACK OUT OLD POSTINGS                        
         BNE   XNO                                                              
POSTNGA2 BAS   RE,ADDPOST          ADD NEW POSTINGS                             
         BNE   XNO                                                              
         NI    STATUS,X'FF'-STA1CBUC                                            
         BAS   RE,NEXTORIG         BUMP TO NEXT CLUSTER IN ORIG RECORD          
         BAS   RE,NEXTNEW          BUMP TO NEXT CLUSTER IN NEW RECORD           
         B     POST10                                                           
*                                                                               
POSTNEG  TM    TTACTION,TTCMPARE                                                
         BO    POSTCMP                                                          
         BAS   RE,BACKOUT          BACK OUT POSTING FROM ORIG CLUSTER           
         BNE   XNO                                                              
         BAS   RE,NEXTORIG         AND ONLY BUMP BUFFER                         
         B     POST10                                                           
*                                                                               
POSTADD  TM    TTACTION,TTCMPARE                                                
         BO    POSTCMP                                                          
         BAS   RE,ADDPOST          ADD NEW POSTINGS                             
         BNE   XNO                                                              
         BAS   RE,NEXTNEW          BUMP TO NEXT CLUSTER IN NEW RECORD           
         B     POST10                                                           
*                                                                               
POSTCMP  MVI   TTRETURN,TTRUPDTE   ITEMS HAVE CHANGED - FORCE UPDATE            
         B     XYES                                                             
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* BUMP TO NEXT CLUSTER IN NEW RECORD BUFFER                           *         
***********************************************************************         
         SPACE 1                                                                
NEXTNEW  NTR1                                                                   
         NI    TTACTION,X'FF'-TTBLDB4                                           
         BAS   RE,PSTBUFF          POST TSAR RECORDS TO BUFFER                  
         MVC   LSTAFKY,RDTSRKEY    SAVE OFF AFTER KEY FOR NEXT TIME             
         B     XYES                                                             
         EJECT                                                                  
***********************************************************************         
* BUMP TO NEXT CLUSTER IN ORIGINAL RECORD BUFFER                      *         
***********************************************************************         
         SPACE 1                                                                
NEXTORIG NTR1                                                                   
         OI    TTACTION,TTBLDB4    BUILD BEFORE BUFFER                          
         BAS   RE,PSTBUFF          POST TSAR RECORDS TO BUFFER                  
         MVC   LSTB4KY,RDTSRKEY    SAVE OFF BEFORE KEY FOR NEXT TIME            
         B     XYES                                                             
         EJECT                                                                  
***********************************************************************         
* MAKE NEGATIVE POSTINGS FROM BUFFER                                  *         
***********************************************************************         
         SPACE 1                                                                
BACKOUT  NTR1                                                                   
         OI    STATUS,STORIGNL+STMINUS                                          
         BAS   RE,GOTRNS                                                        
         BNE   XNO                                                              
         NI    STATUS,X'FF'-STORIGNL-STMINUS                                    
         B     XYES                                                             
         EJECT                                                                  
***********************************************************************         
* ADD POSTINGS FROM CLUSTER                                           *         
***********************************************************************         
         SPACE 1                                                                
ADDPOST  NTR1                                                                   
         OI    STATUS,STANEW                                                    
         BAS   RE,GOTRNS           BUILD TRANACTIONS                            
         BNE   XNO                                                              
         NI    STATUS,X'FF'-STANEW                                              
         B     XYES                                                             
         EJECT                                                                  
***********************************************************************         
* CREATE POSTING BUFFERS                                              *         
*                                                                     *         
* EXIT - CC=EQU - TMS BUFFER BUILT IN BCBUFF1 (BEFORE)                *         
*                               OR IN BCBUFF2 (AFTER)                 *         
*        CC=NEQ - NO RECORDS IN TABLE                                 *         
***********************************************************************         
         SPACE 1                                                                
PSTBUFF  NTR1                                                                   
         MVC   BCABUFF,TTBUFF1     USE BUFFER#1 FOR BUILD - BEFORE              
         TM    TTACTION,TTBLDB4    BUILD BEFORE BUFFER?                         
         BO    *+10                                                             
         MVC   BCABUFF,TTBUFF2     USE BUFFER#2 FOR BUILD/POST-AFTER            
         XC    RUNINDS,RUNINDS                                                  
*                                                                               
         USING TMSD,R5                                                          
         ICM   R5,15,BCABUFF       R5=A(BUFFER)                                 
         BNZ   *+6                                                              
         DC    H'0'                INVALID BUFFER ADDRESS                       
         LH    R1,TTBUFFLN                                                      
         SH    R1,=H'1'                                                         
         AR    R1,R5                                                            
         ST    R1,AEOB             A(END OF BUFFER)                             
         ST    R1,ANEXT            A(NEXT AVAILABLE STORAGE)                    
         L     RE,BCABUFF                                                       
         LH    RF,TTBUFFLN                                                      
         SR    R0,R0                                                            
         SR    R1,R1                                                            
         MVCL  RE,R0               CLEAR BUFFER AREA                            
         MVI   0(R5),X'FF'         MARK END OF TABLE                            
         XC    TMSNUM,TMSNUM       CLEAR TABLE ENTRY COUNT                      
*                                                                               
         USING TSARRECD,R4                                                      
         L     R4,TTAIO            R4 = A(TSAR RECORD AREA)                     
*                                                                               
         LR    R1,R4                                                            
         GOTO1 AXCIO                                                            
*                                                                               
         LA    R1,LSTB4KY          ASSUME BUILDING B4 BUFFER                    
         TM    TTACTION,TTBLDB4    BUILD BEFORE BUFFER?                         
         BO    *+8                                                              
         LA    R1,LSTAFKY          NO-ACTUALLY BUILDING AFT BUFFER              
         MVC   TRKLINE(L'RDTSRKEY),0(R1)        LINE NUMBER/SUB                 
*                                                                               
         LA    R3,TSARDH                                                        
         XC    RDTSRKEY,RDTSRKEY            CLEAR KEY FOR CHECKING              
         B     PSTB12                       SAVE RDKEY INFO - IF ANY            
PSTB10   L     R1,TTAIO                                                         
         GOTO1 AXCIO                                                            
         NI    RUNINDS,X'FF'-RUNIZERO                                           
*                                                                               
PSTB12   LA    R1,DMCB             BUILD PARMLIST                               
         XC    0(12,R1),0(R1)                                                   
         MVI   7(R1),2             BEFORE BUFFER                                
         TM    TTACTION,TTBLDB4    BUILD BEFORE BUFFER?                         
         BO    *+8                                                              
         MVI   7(R1),1             AFTER BUFFER                                 
         ST    R3,0(R1)                                                         
*                                                                               
         LA    RE,PSTB14                                                        
         NTR1  LABEL=NO                                                         
         LA    R1,DMCB             R1=A(PARAMETER BLOCK)                        
         L     RE,SAVERD                                                        
         ICM   RF,15,TTATSAR                                                    
         LM    R2,RB,28(RE)                                                     
         BASR  RE,RF                                                            
         J     EXIT                                                             
PSTB14   TM    DMCB+8,X'80'         EOF?                                        
         BO    PSTBX                                                            
         LA    R3,TSANXT                                                        
         TM    TRKSTAT,TRKSSAVE    SKIP SAVED ITEMS - PRE MCS                   
         BO    PSTB10                                                           
         CLI   TTPROF3,COSAVED                                                  
         BE    PSTB38                                                           
         CLI   TTPROF3,COFUAPR     POSTINGS HIT FILE AT FULLY APPROVED          
         BNE   PSTB18                                                           
         TM    TTACTION,TTBLDB4    BUILD BEFORE BUFFER?                         
         BO    PSTB16                                                           
         TM    TTTSSTAT,TIMSFAPP   NO - IS TIME NOW FULLY APPROVED              
         BO    PSTB38              YES                                          
         B     PSTB34              NO - GET NEXT RECORD                         
PSTB16   TM    TTTSPRST,TIMSFAPP                                                
         BNO   PSTB34                                                           
*                                                                               
PSTB18   CLI   TTPROF3,COLMAPR     POSTINGS HIT FILE AT LINE MANAGER            
         BNE   PSTB22                                  APPROVAL                 
         TM    TTACTION,TTBLDB4    BUILD BEFORE BUFFER?                         
         BO    PSTB20                                                           
         TM    TTTSSTAT,TIMSFAPP   NO - IS TIME NOW FULLY APPROVED              
         BO    PSTB38              YES                                          
         TM    TTTSSTAT,TIMSMAAP   IS TIME NOW LINE MAN APPROVED                
         BO    PSTB38              YES                                          
         B     PSTB34              NO - GET NEXT RECORD                         
PSTB20   TM    TTTSPRST,TIMSFAPP   WAS PREVIOUS TIME FULLY APPROVED             
         BO    PSTB38              YES                                          
         TM    TTTSPRST,TIMSMAAP   PREV TIME LINE MAN APPROVED                  
         BNO   PSTB34              NO - NOT INTERESTED                          
*                                                                               
PSTB22   CLI   TTPROF3,COSUBMD     POSTINGS HIT FILE AT SUBMITTED               
         BE    *+12                                                             
         CLI   TTPROF3,COCLAPR     POSTINGS HIT FILE AT CLIENT APPROVAL         
         BNE   PSTB38                                                           
         TM    TTACTION,TTBLDB4    BUILD BEFORE BUFFER?                         
         BO    PSTB26                                                           
         TM    TTTSSTAT,TIMSFAPP   NO - IS TIME NOW FULLY APPROVED              
         BO    PSTB38              YES                                          
         CLI   TTPROF5,C'C'                                                     
         BNE   PSTB24                                                           
         TM    TTTSSTAT,TIMSMAAP   IS TIME NOW LINE MAN APPROVED                
         BO    PSTB32              YES                                          
         B     *+12                                                             
PSTB24   TM    TTTSSTAT,TIMSMAAP                                                
         BO    PSTB38                                                           
         TM    TTTSSTAT,TIMSPAPP   IS TIME NOW PART APPROVED                    
         BO    PSTB32              YES                                          
         TM    TTTSSTAT,TIMSSUBM   IS TIME NOW SUBMITTED                        
         BO    PSTB32              YES                                          
         B     PSTB34              NO - GET NEXT RECORD                         
PSTB26   TM    TTTSPRST,TIMSFAPP   WAS PREVIOUS TIME FULLY APPROVED             
         BO    PSTB38              YES                                          
         CLI   TTPROF5,C'C'                                                     
         BNE   PSTB28                                                           
         TM    TTTSPRST,TIMSMAAP   IS TIME NOW LINE MAN APPROVED                
         BO    PSTB32              YES                                          
         B     *+12                                                             
PSTB28   TM    TTTSPRST,TIMSMAAP                                                
         BO    PSTB38                                                           
         TM    TTTSPRST,TIMSMAAP   PREV TIME LINE MAN APPROVED                  
         BO    PSTB32              YES                                          
         TM    TTTSPRST,TIMSPAPP   IS TIME NOW PART APPROVED                    
         BO    PSTB32              YES                                          
         TM    TTTSPRST,TIMSSUBM   IS TIME NOW SUBMITTED                        
         BNO   PSTB34              NO - NOT INTERESTED                          
*                                                                               
PSTB32   CLI   TTPROF3,COCLAPR     POSTINGS HIT FILE AT CLIENT APPROVAL         
         BNE   PSTB38                                                           
         TM    TRKSTAT2,TIMESAPR   IS THE TIME CLIENT APPROVED                  
         BNZ   PSTB38              NO - GET NEXT RECORD                         
*                                                                               
PSTB34   OC    TRKMLIN,TRKMLIN     IS IT A MATERIAL                             
         BNZ   PSTB10              YES - TIME ROW WILL HAVE BUILT CNTRA         
         OI    RUNINDS,RUNIZERO    SET WE ONLY WANT CONTRA HEADERS              
         USING TIMELD,R6                                                        
PSTB38   LA    R6,TRDATA           R6=A(CURRENT X'8B' DETAIL ITEM)              
         TM    TTSTAT1,TTSTEXTD    BRANDOCEAN TIME                              
         BZ    PSTB39                                                           
         LA    R6,TRDATA2                                                       
PSTB39   CLI   TIMEL,TIMELQ                                                     
         BNE   PSTB40              NOT X'8B' ELEMENT                            
*                                                                               
         CLI   TIMETYP,TIMEITMS    MATERIALS TIMEL                              
         BE    *+12                                                             
         CLI   TIMETYP,TIMEINP     INPUT DETAIL ITEM                            
         BNE   PSTB40                                                           
         OC    RDTSRKEY,RDTSRKEY   BREAK ON INPUT DETAILS                       
         BNZ   PSTBX                                                            
         MVC   RDTSRKEY,TRKLINE    SAVE OFF KEY FOR NEXT READS                  
         SR    R1,R1                                                            
         L     R1,RDTSRKEY         BUMP TSAR REC NUM                            
         AHI   R1,1                                                             
         ST    R1,RDTSRKEY                                                      
*                                                                               
PSTB40   SR    R1,R1               INCREMENT TABLE COUNT                        
         ICM   R1,3,TMSNUM                                                      
         LA    R1,1(R1)                                                         
         STCM  R1,3,TMSNUM                                                      
         MVC   TMSOFFC,TRKOFF      OFFICE CODE                                  
         MVC   TMSCNTRA,TRKCNTRA   CONTRA ACCOUNT                               
         TM    TTSTAT1,TTSTTIME    ARE WE DOING MCS TIME                        
         BZ    PSTB42                                                           
         TM    RUNINDS,RUNIZERO    ARE WE JUST CREATING CONTRA HEADERS          
         BZ    PSTB40A             NO                                           
         MVI   TMSSTAT2,TMSSCHDR   SET WE ONLY WANT CONTRA HEADER               
PSTB40A  TM    TTSTAT1,TTSTEXTD    BRANDOCEAN TIME                              
         BZ    *+10                                                             
         MVC   TMSCNTRA,TRKACC     CONTRA ACCOUNT                               
         MVC   TMSLINE#,TRKLINE    LINE NUMBER                                  
         MVC   TMSMTSS,TRKSTAT2                                                 
         TM    TTSTAT1,TTSTEXTD    BRANDOCEAN TIME                              
         BZ    PSTB41                                                           
         MVC   TMSJNME,TRKSJNME                                                 
         MVC   TMCNNME,TRKCNNME                                                 
         MVC   TMSINME,TRKSINME                                                 
         MVC   TM12NME,TRK12NME                                                 
         MVC   TMCOST,TRKCOST                                                   
         MVC   TMANAL,TRKANAL                                                   
PSTB41   CLI   TIMETYP,TIMEITMS    MATERIALS TIMEL                              
         BNE   PSTB42                                                           
         MVI   TMSTTYP,TMSTMATR    SET MATERIALS TIME LINE                      
         MVC   TMSIND,TIMIIND                                                   
         ZAP   TMSRATE,TIMIPRCE                                                 
         ZAP   TMSMULT,TIMIMULT                                                 
         ZAP   TMSHRS,=P'0'        HOURS                                        
         MVC   TMSMOA,TIMIMOA                                                   
         MVC   TMSACC,TIMIULA      SJ OR 1N ACCOUNT                             
         MVC   TMSTSK,TIMITSK      TASK                                         
         MVC   TMSINC,TIMINULA                                                  
         L     RE,ANEXT            A(NEXT AREA)                                 
         SR    R1,R1                                                            
         IC    R1,TIMLN                                                         
         SR    RE,R1                                                            
         SH    R1,=H'1'                                                         
         MVC   0(0,RE),TIMEL       COPY ELEMENT                                 
         EX    R1,*-6                                                           
         ST    RE,ANEXT                                                         
         LA    RF,TMSAITMS                                                      
         STCM  RE,15,0(RF)                                                      
*                                                                               
PSTB42   CLI   TIMEL,TIMELQ                                                     
         BNE   PSTB60              NOT X'8B' ELEMENT                            
*                                                                               
         CLI   TIMETYP,TIMEINP     INPUT DETAIL ITEM                            
         BNE   PSTB46                                                           
*                                                                               
         MVC   TMSACC,TIMACC       SJ OR 1N ACCOUNT                             
         MVC   TMSTSK,TIMTSK       TASK                                         
         MVC   TMSTTYP,TIMTTYP     TYPE OF TIME                                 
PSTB43   TM    TTSTAT1,TTSTTIME    ARE WE DOING MCS TIME                        
         BNZ   PSTB44              YES                                          
         MVC   TMSLINE#,TIMLINE#   ITEM LINE #                                  
PSTB44   MVC   TMSIND,TIMIND       TIME INDICATOR                               
         MVC   TMSMOA,TIMMOA       MONTH OF ACTIVITY                            
         MVC   TMSSTAT,TIMSTAT     STATUS                                       
         ZAP   TMSHRS,=P'0'        HOURS                                        
         ZAP   TMSMULT,=P'0'       MULTIPLIER                                   
         TM    RUNINDS,RUNIZERO    ARE WE JUST CREATING CONTRA HEADERS          
         BNZ   PSTB45              YES - DON'T PASS VALUES                      
         ZAP   TMSHRS,TIMHRS       HOURS                                        
PSTB45   ZAP   TMSRATE,=P'0'       SALES RATE                                   
*&&UK*&& ZAP   TMSCRATE,=P'0'      COST RATE                                    
*&&UK*&& ZAP   TMSERTE,=P'0'       EURO RATE                                    
*&&UK*&& ZAP   TMSECRTE,=P'0'      EURO COST RATE                               
         CLI   TIMLN,TIMILN2Q                                                   
         BL    PSTB50                                                           
         CLC   TMSACC,SPACES       HAVE WE GOT A VALID ACCOUNT                  
         BNH   PSTB50              NO                                           
         MVC   TMSRSTA,TIMRBSTA    BILLABLE TIME STATUS                         
         MVC   TMSREFF,TIMREFF     RATE EFFECTIVE DATE                          
         MVC   TMSINC,TIMINC       INCOME ACCOUNT                               
*&&UK*&& MVC   TMSCREFF,TIMCREFF   COST RATE EFFECTIVE DATE                     
         TM    RUNINDS,RUNIZERO    ARE WE ONLY CREATING CONTRA HEADERS          
         BNZ   PSTB50              YES - DON'T PASS VALUES                      
         ZAP   TMSRATE,TIMRATE     RATE                                         
*&&UK                                                                           
         ZAP   TMSCRATE,TIMCRATE   COST RATE                                    
         CLI   TIMLN,TIMILN2Q      DOES IT HAVE EURO EXTENSION                  
         BE    PSTB50                                                           
         GOTO1 AGETEURO            GET EURO RATE - IF ANY                       
         ZAP   TMSERTE,TIMERTE     EURO RATE                                    
         ZAP   TMSECRTE,TIMECRTE   EURO COST RATE                               
*&&                                                                             
         B     PSTB50                                                           
*                                                                               
PSTB46   CLI   TIMETYP,TIMETAX     TAX DETAIL ITEM                              
         BE    *+12                                                             
         CLI   TIMETYP,TIMENAR     NARRATIVE ELEMENT                            
         BNE   PSTB50                                                           
         L     RE,ANEXT            A(NEXT AREA)                                 
         SR    R1,R1                                                            
         IC    R1,TIMLN                                                         
         SR    RE,R1                                                            
         SH    R1,=H'1'                                                         
         MVC   0(0,RE),TIMEL       COPY ELEMENT                                 
         EX    R1,*-6                                                           
         ST    RE,ANEXT                                                         
         LA    RF,TMSATAX                                                       
         CLI   TIMETYP,TIMETAX                                                  
         BE    *+8                                                              
         LA    RF,TMSANARR                                                      
         STCM  RE,15,0(RF)                                                      
*                                                                               
PSTB50   SR    R1,R1               BUMP TO NEXT ELEMENT IN CLUSTER              
         IC    R1,1(R6)                                                         
         AR    R6,R1                                                            
         B     PSTB42                                                           
*                                                                               
PSTB60   LA    R5,TMSLN2Q(R5)       BUMP TO NEXT TABLE ENTRY                    
         MVI   0(R5),X'FF'                                                      
         SR    R1,R1                                                            
         ICM   R1,3,TMSNUM                                                      
         LA    R1,1(R1)                                                         
         STCM  R1,3,TMSNUM         BUMP TABLE COUNT                             
         L     R1,ANEXT            A(END OF BUFFER)                             
         CR    R5,R1                                                            
         BL    PSTB10                                                           
         DC    H'0'                BUFFER TOO SMALL                             
*                                                                               
PSTBX    SR    R3,R3                                                            
         ICM   R3,3,TMSNUM                                                      
         BZ    EXIT                                                             
         GOTO1 TTSORT,DMCB,BCABUFF,(R3),TMSLN2Q,TMSKSRTQ,0                      
         B     EXIT                                                             
         DROP  R4,R5,R6                                                         
         EJECT                                                                  
***********************************************************************         
* BUILD TRANSACTIONS                                                  *         
***********************************************************************         
         SPACE 1                                                                
GOTRNS   NTR1                                                                   
         USING TMSD,R4             R4=A(CURRENT TABLE ENTRY)                    
         L     R4,BCAORIG                                                       
         TM    STATUS,STORIGNL                                                  
         BO    *+8                                                              
         L     R4,BCANEW                                                        
         CLC   TMSACC,SPACES       HAVE WE GOT A VALID ACCOUNT                  
         BNH   GOTRNX              NO                                           
         CLI   TMSTTYP,TMSTNC      IF NON CLIENT TIME NO BUCKETS TO ADD         
         BE    GOTRN02             FOR ONLINE UPDATE                            
         CLI   TMSTTYP,TMSTMATR    IF MATERIALS NO BUCKETS TO ADD               
         BE    GOTRN02             FOR ONLINE UPDATE                            
         TM    TMSSTAT2,TMSSCHDR   CONTRA HEADERS ONLY                          
         BNZ   GOTRN02             YES                                          
         TM    STATUS,STA1CBUC     ARE WE SKIPPING BUCKETS                      
         BNZ   GOTRN02             YES                                          
         CLC   TMSMOA,SVTMSMOA     IS THE MOA DIFFERENT                         
         BE    GOTRN02             NO - THEREFORE CURRENT TABLE OK              
         GOTO1 AMETHD              BUILD 14 CONTRA AND METHOD TABLE             
         BNE   XNO                                                              
*                                                                               
GOTRN02  MVC   BC1ROFC,SPACES      ISOLATE 1R OFFICE CODE                       
         SR    R1,R1                                                            
         IC    R1,TT1RLNQ1                                                      
         SH    R1,=H'1'                                                         
         BNM   *+6                                                              
         DC    H'0'                1R LEDGER STRUCTURE MESSED UP                
         MVC   BC1ROFC(0),TTACKACT                                              
         EX    R1,*-6                                                           
*                                                                               
         MVI   BCLNARR,0                                                        
         XC    BCNARR,BCNARR                                                    
         USING TIMELD,R5                                                        
         SR    R5,R5                                                            
         ICM   R5,15,TMSANARR                                                   
         BZ    GOTRN04                                                          
         SR    R1,R1                                                            
         IC    R1,1(R5)                                                         
         SH    R1,=Y(TIMHLNQ+1)                                                 
         MVC   BCNARR(0),TIMNARR                                                
         EX    R1,*-6                                                           
         LA    R1,1(R1)                                                         
         STC   R1,BCLNARR                                                       
*                                                                               
         USING TIMELD,R5                                                        
         SR    R5,R5                                                            
GOTRN04  ICM   R5,15,TMSAITMS                                                   
         BZ    GOTRN10                                                          
         SR    R1,R1                                                            
         ZAP   BCAMOUNT,TIMITOT                                                 
         IC    R1,1(R5)                                                         
         SH    R1,=Y(TIMITLNQ+1)                                                
         BM    GOTRN13                                                          
         MVC   BCNARR(0),TIMITEXT                                               
         EX    R1,*-6                                                           
         LA    R1,1(R1)                                                         
         STC   R1,BCLNARR                                                       
         B     GOTRN13                                                          
*                                                                               
GOTRN10  ZAP   DUB,TMSRATE         CALCULATE TOTAL POSTING AMOUNT               
*&&UK                                                                           
         LA    RE,TTPROF1          PROF1 FOR B - TIME                           
         CLI   TMSTTYP,TMSTCB      ARE WE DOING B - TIME                        
         BE    *+8                                                              
         LA    RE,TTPROF2          PROF2 FOR N AND R TIME                       
         TM    0(RE),TTPR1CST      USE COST RATE INSTEAD OF SALES RATE          
         BNO   *+10                                                             
         ZAP   DUB,TMSCRATE                                                     
*&&                                                                             
GOTRN12  MP    DUB,TMSHRS                                                       
         SRP   DUB,64-2,5                                                       
         ZAP   BCAMOUNT,DUB                                                     
GOTRN13  MVC   BCMOA,TMSMOA        MONTH OF ACTIVITY                            
*                                                                               
         MVC   BCREF,SPACES        CREATE BATCH REFERENCE #                     
         MVI   BCREF,C'T'          TIME                                         
         CLI   TMSTTYP,TMSTMATR    ARE WE DOING MATERIALS                       
         BNE   GOTRN14             NO                                           
         MVI   BCREF,C'M'          YES SET REFERENCE FOR MATERIALS              
GOTRN14  SR    R1,R1               PERIOD NUMBER INTO CHARS 2-4                 
         IC    R1,TTPERIOD         OF BATCH REFERENCE                           
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  BCREF+1(3),DUB+6(2)                                              
*                                                                               
         LA    R0,BCACCTS          CLEAR ACCOUNT/NAME AREA                      
         LA    R1,BCACCTQ                                                       
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         MVC   BCSJACCT,TMSACC     VALIDATE SJ OR 1N ACCOUNT                    
         CLC   TMSJNME,SPACES                                                   
         BH    GOTRN14A                                                         
         TM    TMSSTAT2,TMSSCHDR   CONTRA HEADERS ONLY                          
         BNZ   GOTRN14A            YES                                          
         GOTO1 AGETACC,BCSJACCT                                                 
         MVC   BCSJNAME,BCACTNAM                                                
         B     *+10                                                             
GOTRN14A MVC   BCSJNAME,TMSJNME                                                 
*                                                                               
*        CLC   TMSACC(2),=C'SJ'    GET SJ CLIENT NAME                           
*        BNE   GOTRN15                                                          
*        MVC   BCACTCDE,SPACES                                                  
*        SR    R1,R1                                                            
*        IC    R1,TTSJLNQ1         LENGTH OF 1ST LEVEL                          
*        LA    R1,1(R1)            ADD TWO FOR U/L 'SJ'                         
*        MVC   BCACTCDE(0),TMSACC                                               
*        EX    R1,*-6                                                           
*        GOTO1 AGETACC,BCACTCDE                                                 
*        MVC   BCSJCLNM,BCACTNAM                                                
*                                                                               
GOTRN15  MVC   BCCNTRA,TMSCNTRA    VALIDATE 1C ACCOUNT                          
         CLC   TMCNNME,SPACES                                                   
         BH    GOTRN16                                                          
         GOTO1 AGETACC,BCCNTRA                                                  
         MVC   BCCNTRNM,BCACTNAM                                                
         B     *+10                                                             
GOTRN16  MVC   BCCNTRNM,TMCNNME                                                 
*                                                                               
         TM    TMSSTAT2,TMSSCHDR   CONTRA HEADERS ONLY FOR TIME                 
         BNZ   GOTRN20             SKIP SI/SK/12                                
         CLI   TMSTTYP,TMSTCB      ONLY VALIDATE SI/SK/12 FOR B TIME            
         BE    GOTRN18                                                          
         CLI   TMSTTYP,TMSTMATR    OR MATERIALS                                 
         BNE   GOTRN20                                                          
GOTRN18  MVC   BCSIACCT,TMSINC     VALIDATE SI OR SK ACCOUNT                    
         CLC   TMSINME,SPACES                                                   
         BH    GOTRN19                                                          
         GOTO1 AGETACC,BCSIACCT                                                 
         MVC   BCSINAME,BCACTNAM                                                
         B     GOTRN19A                                                         
GOTRN19  MVC   BCSINAME,TMSINME                                                 
         MVC   BCACCOST,TMCOST                                                  
         MVC   BCACANAL,TMANAL                                                  
*                                                                               
GOTRN19A CLC   BCSIACCT(2),INCLDG                                               
         BNE   GOTRN20                                                          
         MVC   BC12ACCT,SPACES                                                  
         MVC   BC12ACCT(2),=C'12'  VALIDATE 12 ACCOUNT                          
         MVC   BC12ACCT+2(1),BCACCOST                                           
         CLC   BCACANAL,SPACES                                                  
         BNH   *+10                                                             
         MVC   BC12ACCT+2(12),BCACANAL                                          
         CLC   TM12NME,SPACES                                                   
         BH    GOTRN19B                                                         
         GOTO1 AGETACC,BC12ACCT                                                 
         MVC   BC12NAME,BCACTNAM                                                
         B     *+10                                                             
GOTRN19B MVC   BC12NAME,TM12NME                                                 
GOTRN20  CLI   TMSTTYP,TMSTMATR    IF MATERIALS SKIP TIME POSTINGS              
         BE    GOTRN80                                                          
         TM    STATUS,STA1CBUC     ARE WE SKIPPING BUCKETS                      
         BNZ   GOTRN60             YES                                          
*                                                                               
*              *** DR 1R/1C - ONLY UPDATE BUCKETS (CLIENT TIME) ***             
*              *** DR 1R/1N - ONLY UPDATE BUCKETS (NONCLIENT TIME) ***          
*                                                                               
         DS    0H                                                               
         GOTO1 AXCIO,TRNIO                                                      
         OI    POSTSTAT,POST1R     1R POSTING TIME RECORD BUCKETS               
*                                                                               
         USING TRNRECD,R2                                                       
         LA    R2,TRNIO                                                         
         MVC   TRNKCPY,TTACKCPY                                                 
         MVC   TRNKULA,BC1RACCT    1R ACCOUNT                                   
         MVC   TRNKOFF,TMSOFFC     CLIENT OFFICE                                
         MVC   TRNKCCPY,TTACKCPY                                                
         MVC   TRNKULC,BCCNTRA     COSTING ACCOUNT                              
         CLI   TMSTTYP,TMSTNC      IF POSTING NON-CLIENT TIME                   
         BNE   *+10                                                             
         MVC   TRNKOFF,BC1ROFC     OFFICE IS 1R OFFICE CODE                     
         TM    TTCPYST4,CPYSOFF2                                                
         BO    *+10                                                             
         MVC   TRNKOFF,SPACES      OLD OFFICES = NO OFFICE IN KEY               
         MVC   TRNKDATE,TTYYMMDD                                                
         MVC   TRNKREF,BCREF                                                    
         MVI   TRNRSTYP,49         ASSUME TYPE 49                               
         GOTO1 AELEM44                                                          
         GOTO1 AELEM50                                                          
         GOTO1 AELEM60                                                          
         OI    STATUS,STBUCKET     ONLY UPDATE BUCKETS                          
         GOTO1 AGOADDTR,BCCNTRNM                                                
*&&US                                                                           
         OC    TTTMSUPD,TTTMSUPD                                                
         BZ    GOTRN21                                                          
         GOTO1 TTTMSUPD,DMCB,(X'40',TRNIO),TTCFACS,TTTMSUBK,0                   
*&&                                                                             
GOTRN21  NI    POSTSTAT,X'FF'-POST1R                                            
         TM    TMSSTAT2,TMSSCHDR   CONTRA HEADERS ONLY                          
         BNZ   GOTRN58             NOT INTERESTED IN POSTING BUCKETS            
         CLI   TMSTTYP,TMSTNC      IF POSTING NON-CLIENT TIME                   
         BE    GOTRN58             NOT INTERESTED IN POSTING BUCKETS            
         L     R5,AMETHTAB         R5=A(METHOD TABLE)                           
         USING METHTABD,R5                                                      
GOTRN22  OC    METHNUM,METHNUM     METHOD NUMBER IS ANYTHING PRESENT            
         BZ    GOTRN32             NO - FINISHED 1R/1C AND 1C/14                
         LA    R7,METBCNT          THERE 3 POSSIBLE BUCKETS                     
         LA    R3,METPRATE         START AT TYPE 3                              
GOTRN24  CP    0(6,R3),PZERO       IS THIS ACCUMULATOR ZERO                     
         BNE   GOTRN28             NO - BUILD BUCKET FOR THIS TYPE              
GOTRN26  LA    R3,L'METSRATE(R3)   YES - GO TO NEXT ACCUMULATOR                 
         BCT   R7,GOTRN24          SUBTRACT TYPE AND REPEAT                     
         LA    R5,METHDL(R5)       END OF ACCUMULATORS GET NEXT METHOD          
         B     GOTRN22             IN TABLE AND READ THAT                       
GOTRN28  GOTO1 AXCIO,TRNIO         CLEAR IO AREA                                
         ZAP   PACK16,0(6,R3)                                                   
         MP    PACK16,TMSHRS                                                    
         MP    PACK16,METPERCT                                                  
         SRP   PACK16,64-8,5       SHIFT 8                                      
         ZAP   PACK8,PACK16                                                     
         TM    STATUS,STMINUS                                                   
         BZ    GOTRN30                                                          
         MP    PACK8,=P'-1'                                                     
GOTRN30  ZAP   AMNTDR,PACK8                                                     
         ZAP   AMNTCR,PZERO                                                     
         MVC   METHOD,METHNUM      SAVE METHOD                                  
         STC   R7,SALARY                                                        
         OI    SALARY,X'F0'        CONVERT INTO DISPLAY NUMBER                  
         OI    POSTSTAT,POSTCA     CA TYPE BUCKETS                              
*                                                                               
         USING TRNRECD,R2                                                       
         LA    R2,TRNIO                                                         
         MVC   TRNKCPY,TTACKCPY                                                 
         MVC   TRNKULA,BC1RACCT    1R ACCOUNT                                   
         MVC   TRNKOFF,TMSOFFC     CLIENT OFFICE                                
         MVC   TRNKCCPY,TTACKCPY                                                
         MVC   TRNKULC,BCCNTRA     1C ACCOUNT                                   
         TM    TTCPYST4,CPYSOFF2                                                
         BO    *+10                                                             
         MVC   TRNKOFF,SPACES      OLD OFFICES = NO OFFICE IN KEY               
         MVC   TRNKDATE,TTYYMMDD                                                
         MVC   TRNKREF,SPACES                                                   
         MVC   TRNKREF(L'METHNUM),METHNUM                                       
         GOTO1 AELEM44                                                          
         GOTO1 AELEM50                                                          
         OI    STATUS,STBUCKET     ONLY UPDATE BUCKETS                          
         GOTO1 AGOADDTR,BCCNTRNM                                                
                                                                                
         GOTO1 AXCIO,TRNIO         CLEAR IO AREA                                
         LA    R2,TRNIO                                                         
         ZAP   AMNTCR,PACK8                                                     
         ZAP   AMNTDR,PZERO                                                     
         MVC   TRNKCPY,TTACKCPY                                                 
         MVC   TRNKULA,BCCNTRA     1C ACCOUNT                                   
         MVC   TRNKOFF,TMSOFFC     CLIENT OFFICE                                
         MVC   TRNKCCPY,TTACKCPY                                                
         MVC   TRNKULC,BC142ACT    14 ACCOUNT 2 LEVEL                           
         CLI   METHSTR,METH3LVL    IS IT 3 LEVEL STRUCTURE                      
         BNE   *+10                                                             
         MVC   TRNKULC,BC143ACT    14 ACCOUNT 3 LEVEL                           
         TM    METHSTAT,METSNANL   ARE WE USING PROFILE SETTING                 
         BZ    *+10                NO                                           
         MVC   TRNKCACT(L'METHANAL),METHANAL SET ANALYSIS FROM PROFILE          
         TM    TTCPYST4,CPYSOFF2                                                
         BO    *+10                                                             
         MVC   TRNKOFF,SPACES      OLD OFFICES = NO OFFICE IN KEY               
         MVC   TRNKDATE,TTYYMMDD                                                
         MVC   TRNKREF,SPACES                                                   
         MVC   TRNKREF(L'METHNUM),METHNUM                                       
         GOTO1 AELEM44                                                          
         GOTO1 AELEM50                                                          
         OI    STATUS,STBUCKET     ONLY UPDATE BUCKETS                          
         LA    R1,BC142NME                                                      
         CLI   METHSTR,METH3LVL                                                 
         BNE   *+8                                                              
         LA    R1,BC143NME                                                      
         GOTO1 AGOADDTR                                                         
         B     GOTRN26             IN TABLE AND READ THAT                       
*                                                                               
GOTRN32  L     R5,AMETHTAB         R5=A(METHOD TABLE)                           
         USING METHTABD,R5                                                      
GOTRN34  OC    METHNUM,METHNUM     METHOD NUMBER IS ANYTHING PRESENT            
         BZ    GOTRN42             NO - FINISHED 1R/1C AND 1C/14                
                                                                                
         USING CPRRECD,R6                                                       
GOTRN36  LA    R6,KEY              READ ARATE RECORDS                           
         MVC   KEY,SPACES                                                       
         MVI   CPRKTYP,CPRKTYPQ    X'3E'                                        
         MVI   CPRKSUB,CPRKSUBQ    X'07'                                        
         MVC   CPRKCPY(L'TTACCODE),TTACCODE   COMPANY AND ACCOUNT CODE          
         MVC   KEYSAVE,KEY                                                      
         GOTO1 TTDMGR,DMCB,(X'08',DMREAD),ACCDIR,KEY,KEY,0                      
         TM    DMCB+8,X'10'        RECORD NOT FOUND                             
         BO    GOTRN38             YES - CREATE AND ADD                         
                                                                                
         LA    R3,CPRKDA           READ MASTER RECORD                           
         GOTO1 TTDMGR,DMCB,(X'08',GETREC),ACCMST,(R3),TRNIO,WORK                
         CLI   DMCB+8,0            ANY DATAMANAGER ERRORS                       
         BE    GOTRN40             NO                                           
         DC    H'0'                YES - DIE                                    
GOTRN38  GOTO1 ABLDRATR            BUILD RECORD                                 
GOTRN40  GOTO1 AADDRATE            FIND AND BUILD ELEMENT                       
         GOTO1 AWRITE              WRITE BACK OR ADD RECORD                     
         LA    R5,METHDL(R5)       END OF ACCUMULATORS GET NEXT METHOD          
         B     GOTRN34             IN TABLE AND READ THAT                       
*                                                                               
GOTRN42  L     R5,AMETHTAB         R5=A(METHOD TABLE)                           
         USING METHTABD,R5                                                      
GOTRN44  OC    METHNUM,METHNUM     METHOD NUMBER IS ANYTHING PRESENT            
         BZ    GOTRN58             NO - FINISHED 1R/1C AND 1C/14                
         LA    R7,METBCNT          THERE 3 POSSIBLE BUCKETS                     
         LA    R3,METPRATE         START AT TYPE 3                              
GOTRN46  CP    0(6,R3),PZERO       IS THIS ACCUMULATOR ZERO                     
         BNE   GOTRN50             NO - BUILD BUCKET FOR THIS TYPE              
GOTRN48  LA    R3,L'METSRATE(R3)   YES - GO TO NEXT ACCUMULATOR                 
         BCT   R7,GOTRN46          SUBTRACT TYPE AND REPEAT                     
         LA    R5,METHDL(R5)       END OF ACCUMULATORS GET NEXT METHOD          
         B     GOTRN44             IN TABLE AND READ THAT                       
GOTRN50  ZAP   PACK16,0(6,R3)                                                   
         MP    PACK16,TMSHRS                                                    
         MP    PACK16,METPERCT                                                  
         SRP   PACK16,64-8,5       SHIFT 8                                      
         ZAP   PACK8,PACK16                                                     
         TM    STATUS,STMINUS      IS IT BACK OUT                               
         BZ    GOTRN52                                                          
         MP    PACK8,=P'-1'        YES                                          
GOTRN52  STC   R7,SALARY                                                        
         OI    SALARY,X'F0'        CONVERT INTO DISPLAY NUMBER                  
         LA    R6,KEY                                                           
         USING PLDRECD,R6                                                       
         XC    PLDKEY(L'KEY),PLDKEY                                             
         MVI   PLDKTYP,PLDKTYPQ    X'18' - DIRECT TIME POINTER                  
         MVI   PLDKSUB,PLDKSUBQ    X'CD' - SUB POINTER                          
         MVC   PLDKCPY,TTACKCPY    COMPANY                                      
         MVC   PLDKMTHD,METHNUM    METHOD                                       
         MVC   PLDKCACT,BCCNTRA+L'ACTKUNT+L'ACTKLDG 1C ACCOUNT CODE             
         MVC   PLDKRACT,BC1RACCT+L'ACTKUNT+L'ACTKLDG 1R ACCOUNT CODE            
         MVC   PLDKANAL,BC142ACT+L'ACTKUNT+L'ACTKLDG SAVE ANALYSIS              
         TM    METHSTAT,METSNANL   DID ANALYSIS EXIST AT 1R                     
         BZ    *+10                YES                                          
         MVC   PLDKANAL,METHANAL   NO - USE PROFILE VALUE FOR ANALYSIS          
         MVC   PLDKYYMM,TMSMOA     YEAR/MONTH                                   
         MVC   PLDKPTYP,SALARY     PAYROLL TYPE                                 
         MVC   KEYSAVE,KEY         SAVE KEY                                     
         GOTO1 TTDMGR,DMCB,(X'08',DMREAD),ACCDIR,KEY,KEY                        
         TM    8(R1),X'10'         DID I FIND THE RECORD?                       
         BO    GOTRN56             NO BUILD A VIRGIN RECORD                     
                                                                                
         TM    PLDKSTA,ACTSDELT    IS IT MARKED DELETED?                        
         BNO   GOTRN54                                                          
         NI    PLDKSTA,X'FF'-ACTSDELT UNDELETE THE DIRECTORY RECORD             
         ZAP   PLDKAMT,PZERO       RESET AMOUNT ON RECORD                       
GOTRN54  AP    PLDKAMT,PACK8       ADD AMOUNT ON RECORD                         
         XC    PLDKDA,PLDKDA       CLEAR ANY BAD DISK ADD FIELDS                
         GOTO1 TTDMGR,DMCB,DMWRT,ACCDIR,KEY,KEY                                 
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                DIED WRITING A RECORD                        
         B     GOTRN48             GO TO NEXT ACCUMULATOR                       
*                                                                               
GOTRN56  MVC   KEY,KEYSAVE         BUILD A VIRGIN RECORD                        
         ZAP   PLDKAMT,PACK8       ADD AMOUNT ON RECORD                         
         XC    PLDKDA,PLDKDA       CLEAR ANY BAD DISK ADD FIELDS                
         GOTO1 TTDMGR,DMCB,DMADD,ACCDIR,KEY,KEY                                 
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                DIED WRITING A RECORD                        
         B     GOTRN48             GO TO NEXT ACCUMULATOR                       
*                                                                               
GOTRN58  NI    STATUS,X'FF'-STBUCKET                                            
         NI    POSTSTAT,X'FF'-POSTCA                                            
         DROP  R5                                                               
*                                                                               
*              *** DR SJ/1R ***                                                 
*                                                                               
GOTRN60  DS    0H                                                               
         CLI   TMSTTYP,TMSTCN      CONTINUE IF CLIENT TIME                      
         BH    GOTRNX                                                           
         TM    TMSSTAT2,TMSSCHDR   CONTRA HEADERS ONLY                          
         BNZ   GOTRNX              NOT INTERESTED IN ANY OTHER POSTINGS         
         SR    RE,RE               TEST IF A JOB SPECIFIED                      
         IC    RE,TTSJLNQ2                                                      
         LA    RE,BCSJCLI(RE)                                                   
         CLC   0(5,RE),SPACES                                                   
         BNH   GOTRNX                                                           
         GOTO1 AXCIO,TRNIO                                                      
         TM    STATUS,STA1CBUC     ARE WE JUST UPDATING EXISTING RECS           
         BZ    GOTRN61             NO - ADD RECORDS                             
         GOTO1 AUPDATE             UPDATE EXSITING SJ POSTING                   
         B     GOTRNX                                                           
GOTRN61  OI    POSTSTAT,POSTSJ     SJ POSTING                                   
*                                                                               
         USING TRNRECD,R2                                                       
         LA    R2,TRNIO                                                         
         MVC   TRNKCPY,TTACKCPY                                                 
         MVC   TRNKULA,BCSJACCT    SJ ACCOUNT                                   
         MVC   TRNKWORK,TMSTSK     TSK CODE                                     
         MVC   TRNKCCPY,TTACKCPY                                                
         MVC   TRNKULC,BC1RACCT    1R ACCOUNT                                   
         MVC   TRNKDATE,TTYYMMDD                                                
         MVC   TRNKREF,BCREF                                                    
         GOTO1 AELEM44                                                          
         GOTO1 AELEM2C                                                          
         GOTO1 AELEM40                                                          
         GOTO1 AELEM4C                                                          
*&&UK*&& GOTO1 AELEM50                                                          
*&&UK*&& GOTO1 AELEM50R                                                         
         GOTO1 AELEM60                                                          
         GOTO1 AELEM65                                                          
         GOTO1 AELEMC0                                                          
         GOTO1 AELEMD6                                                          
         GOTO1 AELEMD8                                                          
*&&UK*&& GOTO1 AELEMDB                                                          
*&&UK*&& GOTO1 AELEMFEJ                                                         
         GOTO1 AGOADDTR,BC1RNAME   CALL ADDTRN                                  
         NI    POSTSTAT,X'FF'-POSTSJ                                            
*                                                                               
*              *** CR SI/SJ ***                                                 
*                                                                               
GOTRN62  DS    0H                                                               
         CLI   TMSTTYP,TMSTCB      EXIT IF NOT B-TIME                           
         BNE   GOTRNX                                                           
         GOTO1 AXCIO,TRNIO                                                      
         OI    POSTSTAT,POSTSI     SI POSTING                                   
*                                                                               
         USING TRNRECD,R2                                                       
         LA    R2,TRNIO                                                         
         MVC   TRNKCPY,TTACKCPY                                                 
         MVC   TRNKULA,BCSIACCT    INCOME ACCOUNT                               
         MVC   TRNKOFF,TMSOFFC     CLIENT OFFICE                                
         TM    TTCPYST4,CPYSOFF2                                                
         BO    *+10                                                             
         MVC   TRNKOFF,SPACES      OLD OFFICES = NO OFFICE IN KEY               
         MVC   TRNKCCPY,TTACKCPY                                                
         MVC   TRNKULC,BCSJACCT    SJ ACCOUNT                                   
         MVC   TRNKDATE,TTYYMMDD                                                
         MVC   TRNKREF,BCREF                                                    
         GOTO1 AELEM44                                                          
*&&US*&& GOTO1 AELEM1A           MEDIA INTERFACE ELEMENT ('7B' IN UK)           
*&&UK*&& GOTO1 AELEM50G                                                         
         GOTO1 AELEM60                                                          
*&&UK*&& GOTO1 AELEM7B             SOURCE ELEMENT - ('1A' IN US)                
         GOTO1 AELEMC0                                                          
         GOTO1 AELEMC5                                                          
         GOTO1 AELEMD6                                                          
         GOTO1 AELEMD8                                                          
*&&UK*&& GOTO1 AELEMFES                                                         
         GOTO1 AGOADDTR,BCSJNAME                                                
         NI    POSTSTAT,X'FF'-POSTSI                                            
         CLC   BCSIACCT(2),INCLDG  ONLY MAKE COSTING POSTINGS IF                
         BNE   GOTRN70             POSTING TO SI - SKIP FOR U/L=SK              
*                                                                               
*              *** DR 1C/12 ***                                                 
*                                                                               
GOTRN66  DS    0H                                                               
         GOTO1 AXCIO,TRNIO                                                      
         OI    POSTSTAT,POST1C                                                  
*                                                                               
         USING TRNRECD,R2                                                       
         LA    R2,TRNIO                                                         
         MVC   TRNKCPY,TTACKCPY                                                 
         MVC   TRNKULA,BCCNTRA     COSTING ACCOUNT                              
         MVC   TRNKOFF,TMSOFFC     CLIENT OFFICE                                
         TM    TTCPYST4,CPYSOFF2                                                
         BO    *+10                                                             
         MVC   TRNKOFF,SPACES      OLD OFFICES = NO OFFICE IN KEY               
         MVC   TRNKCCPY,TTACKCPY                                                
         MVC   TRNKULC,BC12ACCT    ANALYSIS ACCOUNT                             
         MVC   TRNKDATE,TTYYMMDD                                                
         MVC   TRNKREF,BCREF                                                    
         GOTO1 AELEM44                                                          
         GOTO1 AELEM60                                                          
         GOTO1 AELEMC0                                                          
         GOTO1 AELEMD6                                                          
         GOTO1 AELEMD8                                                          
*&&UK*&& GOTO1 AELEMFER                                                         
         GOTO1 AGOADDTR,BC12NAME                                                
         NI    POSTSTAT,X'FF'-POST1C                                            
*                                                                               
*              *** CR 12/1C ***                                                 
*                                                                               
GOTRN68  DS    0H                                                               
         GOTO1 AXCIO,TRNIO                                                      
         OI    POSTSTAT,POST12     12 POSTING                                   
*                                                                               
         USING TRNRECD,R2                                                       
         LA    R2,TRNIO                                                         
         MVC   TRNKCPY,TTACKCPY                                                 
         MVC   TRNKULA,BC12ACCT    ANALYSIS ACCOUNT                             
         MVC   TRNKOFF,TMSOFFC     CLIENT OFFICE                                
         TM    TTCPYST4,CPYSOFF2                                                
         BO    *+10                                                             
         MVC   TRNKOFF,SPACES      OLD OFFICES = NO OFFICE IN KEY               
         MVC   TRNKCCPY,TTACKCPY                                                
         MVC   TRNKULC,BCCNTRA     COSTING ACCOUNT                              
         MVC   TRNKDATE,TTYYMMDD                                                
         MVC   TRNKREF,BCREF                                                    
         GOTO1 AELEM44                                                          
         GOTO1 AELEM60                                                          
         GOTO1 AELEMC0                                                          
         GOTO1 AELEMD6                                                          
         GOTO1 AELEMD8                                                          
*&&UK*&& GOTO1 AELEMFER                                                         
         GOTO1 AGOADDTR,BCCNTRNM                                                
         NI    POSTSTAT,X'FF'-POST12                                            
*                                                                               
*              *** TAX POSTINGS ***                                             
*                                                                               
GOTRN70  DS    0H                                                               
         USING TIMELD,R5                                                        
         SR    R5,R5                                                            
         ICM   R5,15,TMSATAX       ANY TAX POSTINGS TO MAKE                     
         BZ    GOTRNX                                                           
*                                                                               
         MVC   BCTXWC,TIMTWC      TAX WORKCODE                                  
         MVC   BCTXLOC,TIMTLOC    LOCALITY CODE                                 
         ZAP   BCTXBAS,TIMTBAS    TAX BASIS AMOUNT                              
         SR    R3,R3                                                            
         IC    R3,TIMTMINI        # TAX MINI ELEMENTS                           
*                                                                               
GOTRN75  MVC   BCTXEFF,TIMTEFF     RATE EFFECTIVE DATE                          
         ZAP   BCTXRATE,TIMTRATE   TAX RATE                                     
         ZAP   DUB,BCTXBAS         CALCULATE POSTING AMOUNT                     
         MP    DUB,BCTXRATE                                                     
         SRP   DUB,64-6,5                                                       
         ZAP   BCTXAMNT,DUB                                                     
*                                                                               
*              *** DR SJ/CREDIT ACCOUNT ***                                     
*                                                                               
         MVC   BCACTCDE,TIMTACC    CREDIT ACCOUNT                               
         GOTO1 AGETACC,BCACTCDE                                                 
         OI    POSTSTAT,POSTTXSJ   SJ TAX POSTING                               
         GOTO1 AXCIO,TRNIO                                                      
         USING TRNRECD,R2                                                       
         LA    R2,TRNIO                                                         
         MVC   TRNKCPY,TTACKCPY                                                 
         MVC   TRNKULA,BCSJACCT    SJ ACCOUNT                                   
         MVC   TRNKWORK,TMSTSK     TSK CODE                                     
         MVC   TRNKCCPY,TTACKCPY                                                
         MVC   TRNKULC,BCACTCDE    CREDIT ACCCOUNT                              
         MVC   TRNKDATE,TTYYMMDD                                                
         MVC   TRNKREF,BCREF                                                    
         GOTO1 AELEM44                                                          
         GOTO1 AELEM5F                                                          
         GOTO1 AELEM60                                                          
*&&UK*&& GOTO1 AELEMFER                                                         
         GOTO1 AGOADDTR,BCACTNAM   CALL ADDTRN                                  
         NI    POSTSTAT,X'FF'-POSTTXSJ                                          
*                                                                               
         GOTO1 AXCIO,TRNIO                                                      
         OI    POSTSTAT,POSTTXCR   CREDIT POSTING                               
         USING TRNRECD,R2                                                       
         LA    R2,TRNIO                                                         
         MVC   TRNKCPY,TTACKCPY                                                 
         MVC   TRNKULA,BCACTCDE    CREDIT ACCOUNT                               
         MVC   TRNKOFF,TMSOFFC     CLIENT OFFICE                                
         TM    TTCPYST4,CPYSOFF2                                                
         BO    *+10                                                             
         MVC   TRNKOFF,SPACES      OLD OFFICES = NO OFFICE IN KEY               
         MVC   TRNKCCPY,TTACKCPY                                                
         MVC   TRNKULC,SPACES                                                   
         SR    RE,RE                                                            
         IC    RE,TTSJLNQ1                                                      
         LA    RE,1(RE)                                                         
         MVC   TRNKULC(0),BCSJACCT                                              
         EX    RE,*-6                                                           
         MVC   TRNKDATE,TTYYMMDD                                                
         MVC   TRNKREF,BCREF                                                    
         GOTO1 AELEM44                                                          
         GOTO1 AELEM23                                                          
         GOTO1 AELEM5F                                                          
         GOTO1 AELEM60                                                          
*&&UK*&& GOTO1 AELEMFER                                                         
         GOTO1 AGOADDTR,BCACTNAM   CALL ADDTRN                                  
         NI    POSTSTAT,X'FF'-POSTTXCR                                          
         LA    R5,TIMTMINQ(R5)                                                  
         BCT   R3,GOTRN75                                                       
         B     GOTRNX                                                           
*                                                                               
*              *** MATERIALS POSTINGS ***                                       
*              *** DR SJ/CREDIT ACCOUNT ***                                     
*                                                                               
GOTRN80  DS    0H                                                               
         TM    TMSSTAT2,TMSSCHDR   CONTRA HEADERS ONLY FOR TIME                 
         BNZ   GOTRNX              NOT INTERESTED IN ANY POSTINGS               
         TM    STATUS,STA1CBUC     ARE WE JUST UPDATING EXISTING RECS           
         BZ    GOTRN81             NO - ADD RECORDS                             
         GOTO1 AUPDATE             UPDATE EXSITING SJ POSTING                   
         B     GOTRNX                                                           
GOTRN81  GOTO1 AXCIO,TRNIO                                                      
         OI    POSTSTAT,POSTSJ     SJ POSTING                                   
         USING TRNRECD,R2                                                       
         LA    R2,TRNIO                                                         
         MVC   TRNKCPY,TTACKCPY                                                 
         MVC   TRNKULA,BCSJACCT    SJ ACCOUNT                                   
         MVC   TRNKWORK,TMSTSK     TSK CODE                                     
         MVC   TRNKCCPY,TTACKCPY                                                
         MVC   TRNKULC,BC1RACCT    1R ACCOUNT                                   
         MVC   TRNKDATE,TTYYMMDD                                                
         MVC   TRNKREF,BCREF                                                    
         MVI   TRNRSTYP,8          ASSUME TYPE 8                                
         GOTO1 AELEM44                                                          
         GOTO1 AELEM2C                                                          
         GOTO1 AELEM59                                                          
         GOTO1 AELEM4C                                                          
         GOTO1 AELEM60                                                          
         GOTO1 AELEM65                                                          
         GOTO1 AELEMC0                                                          
         GOTO1 AELEMD6                                                          
         GOTO1 AELEMD8                                                          
*&&UK*&& GOTO1 AELEMDB                                                          
         GOTO1 AGOADDTR,BC1RNAME   CALL ADDTRN                                  
         NI    POSTSTAT,X'FF'-POSTSJ                                            
*                                                                               
*              *** CR SI/SJ ***                                                 
*                                                                               
GOTRN82  DS    0H                                                               
         GOTO1 AXCIO,TRNIO                                                      
         OI    POSTSTAT,POSTSI     SI POSTING                                   
*                                                                               
         USING TRNRECD,R2                                                       
         LA    R2,TRNIO                                                         
         MVC   TRNKCPY,TTACKCPY                                                 
         MVC   TRNKULA,BCSIACCT    INCOME ACCOUNT                               
         MVC   TRNKOFF,TMSOFFC     CLIENT OFFICE                                
         TM    TTCPYST4,CPYSOFF2                                                
         BO    *+10                                                             
         MVC   TRNKOFF,SPACES      OLD OFFICES = NO OFFICE IN KEY               
         MVC   TRNKCCPY,TTACKCPY                                                
         MVC   TRNKULC,BCSJACCT    SJ ACCOUNT                                   
         MVC   TRNKDATE,TTYYMMDD                                                
         MVC   TRNKREF,BCREF                                                    
         GOTO1 AELEM44                                                          
*&&US*&& GOTO1 AELEM1A           MEDIA INTERFACE ELEMENT ('7B' IN UK)           
         GOTO1 AELEM60                                                          
         GOTO1 AELEM59                                                          
*&&UK*&& GOTO1 AELEM7B             SOURCE ELEMENT - ('1A' IN US)                
         GOTO1 AELEMC0                                                          
         GOTO1 AELEMC5                                                          
         GOTO1 AELEMD6                                                          
         GOTO1 AELEMD8                                                          
         GOTO1 AGOADDTR,BCSJNAME                                                
         NI    POSTSTAT,X'FF'-POSTSI                                            
         CLC   BCSIACCT(2),INCLDG  ONLY MAKE COSTING POSTINGS IF                
         BNE   GOTRNX              POSTING TO SI - SKIP FOR U/L=SK              
*                                                                               
*              *** DR 1C/12 ***                                                 
*                                                                               
GOTRN86  DS    0H                                                               
         GOTO1 AXCIO,TRNIO                                                      
         OI    POSTSTAT,POST1C                                                  
*                                                                               
         USING TRNRECD,R2                                                       
         LA    R2,TRNIO                                                         
         MVC   TRNKCPY,TTACKCPY                                                 
         MVC   TRNKULA,BCCNTRA     COSTING ACCOUNT                              
         MVC   TRNKOFF,TMSOFFC     CLIENT OFFICE                                
         TM    TTCPYST4,CPYSOFF2                                                
         BO    *+10                                                             
         MVC   TRNKOFF,SPACES      OLD OFFICES = NO OFFICE IN KEY               
         MVC   TRNKCCPY,TTACKCPY                                                
         MVC   TRNKULC,BC12ACCT    ANALYSIS ACCOUNT                             
         MVC   TRNKDATE,TTYYMMDD                                                
         MVC   TRNKREF,BCREF                                                    
         GOTO1 AELEM44                                                          
         GOTO1 AELEM60                                                          
         GOTO1 AELEMC0                                                          
         GOTO1 AELEMD6                                                          
         GOTO1 AELEMD8                                                          
         GOTO1 AGOADDTR,BC12NAME                                                
         NI    POSTSTAT,X'FF'-POST1C                                            
*                                                                               
*              *** CR 12/1C ***                                                 
*                                                                               
GOTRN88  DS    0H                                                               
         GOTO1 AXCIO,TRNIO                                                      
         OI    POSTSTAT,POST12     12 POSTING                                   
*                                                                               
         USING TRNRECD,R2                                                       
         LA    R2,TRNIO                                                         
         MVC   TRNKCPY,TTACKCPY                                                 
         MVC   TRNKULA,BC12ACCT    ANALYSIS ACCOUNT                             
         MVC   TRNKOFF,TMSOFFC     CLIENT OFFICE                                
         TM    TTCPYST4,CPYSOFF2                                                
         BO    *+10                                                             
         MVC   TRNKOFF,SPACES      OLD OFFICES = NO OFFICE IN KEY               
         MVC   TRNKCCPY,TTACKCPY                                                
         MVC   TRNKULC,BCCNTRA     COSTING ACCOUNT                              
         MVC   TRNKDATE,TTYYMMDD                                                
         MVC   TRNKREF,BCREF                                                    
         GOTO1 AELEM44                                                          
         GOTO1 AELEM60                                                          
         GOTO1 AELEMC0                                                          
         GOTO1 AELEMD6                                                          
         GOTO1 AELEMD8                                                          
         GOTO1 AGOADDTR,BCCNTRNM                                                
         NI    POSTSTAT,X'FF'-POST12                                            
*                                                                               
GOTRNX   DS    0H                                                               
         B     XYES                                                             
         EJECT                                                                  
***********************************************************************         
* LITERALS                                                            *         
***********************************************************************         
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* CONSTANTS                                                           *         
***********************************************************************         
         SPACE 1                                                                
GLOBALS  DS    0X                                                               
         DC    CL8'ACCDIR'                                                      
         DC    CL8'ACCMST'                                                      
         DC    CL8'ACCARC'                                                      
         DC    CL8'DMRDHI'                                                      
         DC    CL8'DMREAD'                                                      
         DC    CL8'DMRSEQ'                                                      
         DC    CL8'GETREC'                                                      
         DC    CL8'PUTREC'                                                      
         DC    CL8'ADDREC'                                                      
         DC    CL8'DMWRT'                                                       
         DC    CL8'DMADD'                                                       
         DC    P'0'                                                             
         DC    P'-1'                                                            
         DC    P'100'                                                           
         DC    P'1000000'                                                       
         DC    C'SI'                                                            
GLOBALSL EQU   *-GLOBALS                                                        
*                                                                               
DELELQ   EQU   X'FF'                                                            
RECLNMAX EQU   2000                                                             
MAXIOS   DC    A(28000)                                                         
         EJECT                                                                  
***********************************************************************         
* METHOD TABLE                                                        *         
***********************************************************************         
         SPACE 1                                                                
         EJECT                                                                  
***********************************************************************         
*ROUTINES                                                             *         
***********************************************************************         
         SPACE 1                                                                
ROUTS1   NMOD1 0,**TROU**,RA                                                    
         L     RC,4(RD)                                                         
         L     RC,68(RC)                                                        
         SRL   RF,32-8                                                          
         SLL   RF,2                                                             
         B     *+4(RF)                                                          
*&&US*&& B     ELEM1A                                                           
         B     ELEM23                                                           
         B     ELEM2C                                                           
         B     ELEM40                                                           
         B     ELEM44                                                           
         B     ELEM4C                                                           
         B     ELEM50                                                           
*&&UK*&& B     ELEM50R                                                          
*&&UK*&& B     ELEM50G                                                          
         B     ELEM59                                                           
         B     ELEM5F                                                           
         B     ELEM60                                                           
         B     ELEM65                                                           
*&&UK*&& B     ELEM7B                                                           
         B     ELEMC0                                                           
         B     ELEMC5                                                           
         B     ELEMD6                                                           
         B     ELEMD8                                                           
*&&UK                                                                           
         B     ELEMDB                                                           
         B     ELEMFETR                                                         
         B     ELEMFETS                                                         
         B     ELEMFESJ                                                         
         B     GETEURO                                                          
*&&                                                                             
         B     GETACC                                                           
         B     METHD                                                            
         B     GOADDTRN                                                         
         B     ADDELEM                                                          
         B     BLDRATR                                                          
         B     ADDRATEL                                                         
         B     WRITE                                                            
         B     XCIO                                                             
         B     UPDATE                                                           
         B     UPDREC                                                           
         B     UPDEL60                                                          
                                                                                
ROUTS1L  MVI   DUB,0                                                            
         B     *+8                                                              
ROUTS1E  MVI   DUB,1                                                            
         B     *+8                                                              
ROUTS1H  MVI   DUB,2                                                            
         CLI   DUB,1                                                            
ROUTS1X  XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
* X'1A' MEDIA TRANSFER ELEMENT - US ONLY                              *         
***********************************************************************         
         SPACE 1                                                                
*&&US                                                                           
         USING TMSD,R4             R4=A(CURRENT TABLE ENTRY)                    
ELEM1A   CLC   BCSIACCT(2),INCLDG  ONLY ADD TO SI POSTING                       
         BNE   ELEM1AX             DONT ADD TO SK POSTING                       
*                                                                               
         USING MDTELD,R2                                                        
         LA    R2,ELEM             MEDIA TRANSFER ELEMENT IN US                 
         XC    ELEM,ELEM                                                        
         MVI   MDTEL,MDTELQ        X'1A' ELEMENT                                
         MVI   MDTLN,MDTLNQ                                                     
         MVI   MDTSYS,MDTSPROD     C'J' - PRODUCTION                            
         MVC   MDTMED,BCSJMED      MEDIA CODE                                   
         MVC   MDTCLI(12),BCSJCLI  SJ CLI/PRD/JOB                               
         MVC   MDTMOS,TMSMOA       MOA                                          
         MVC   MDTDSCP,BCSJNAME    SJ ACCOUNT NAME                              
         ZAP   DUB,BCAMOUNT                                                     
         CVB   R0,DUB                                                           
         STCM  R0,15,MDTCOM        INCOME (COMMISSION)                          
         STCM  R0,15,MDTINTL       INCOME (INTERNAL)                            
         GOTO1 AADDELEM                                                         
ELEM1AX  B     ROUTS1X                                                          
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* X'23' OTHERS ELEMENT                                                *         
***********************************************************************         
         SPACE 1                                                                
         USING TMSD,R4             R4=A(CURRENT TABLE ENTRY)                    
         USING OTHELD,R2                                                        
ELEM23   LA    R2,ELEM                                                          
         XC    ELEM,ELEM                                                        
         MVI   OTHEL,OTHELQ        X'23'                                        
         MVI   OTHLN,OTHLN1Q                                                    
         MVC   OTHNUM(13),SPACES                                                
         SR    RE,RE                                                            
         IC    RE,TTSJLNQ1                                                      
         SR    RF,RF                                                            
         IC    RF,TTSJLNQ2                                                      
         SR    R1,R1                                                            
         IC    R1,TTSJLNQ3                                                      
         SR    R1,RF               R1=L'JOB                                     
         SR    RF,RE               RF=L'PRODUCT                                 
         LA    RE,BCSJCLI(RE)      RE=A(PRODUCT)                                
         BCTR  RF,0                                                             
         MVC   OTHNUM(0),0(RE)     MOVE PRODUCT TO ELEMENT                      
         EX    RF,*-6                                                           
         LA    RE,1(RE,RF)         RE=A(JOB)                                    
         BCTR  R1,0                                                             
         MVC   OTHNUM+6(0),0(RE)   MOVE JOB TO ELEMENT                          
         EX    R1,*-6                                                           
         GOTO1 AADDELEM                                                         
         B     ROUTS1X                                                          
         EJECT                                                                  
***********************************************************************         
* X'2C' SPECIAL POSTING ELEMENT                                       *         
***********************************************************************         
         SPACE 1                                                                
         USING TMSD,R4             R4=A(CURRENT TABLE ENTRY)                    
         USING SPAELD,R2                                                        
ELEM2C   LA    R2,ELEM                                                          
         XC    ELEM,ELEM                                                        
         MVI   SPAEL,SPAELQ        X'2C'                                        
         MVI   SPALN,SPALNQ                                                     
         MVI   SPATYPE,SPATCCST    COSTING ACCOUNT                              
         MVC   SPAAULA,TMSCNTRA                                                 
         GOTO1 AADDELEM                                                         
         B     ROUTS1X                                                          
         EJECT                                                                  
***********************************************************************         
* X'40' PERSONNEL RATE ELEMENT                                        *         
***********************************************************************         
         SPACE 1                                                                
         USING TMSD,R4             R4=A(CURRENT TABLE ENTRY)                    
         USING PRTELD,R2                                                        
ELEM40   LA    R2,ELEM                                                          
         XC    ELEM,ELEM                                                        
         MVI   PRTEL,PRTELQ        X'40'                                        
         MVI   PRTLN,PRTLNQ                                                     
         MVC   PRTSTRT,TMSREFF     EFFECTIVE DATE FOR RATE                      
         ZAP   PRTRATE,TMSRATE     RATE                                         
         ZAP   PRTHOUR,TMSHRS      HOURS                                        
*&&US*&& MVC   PRTLINE#,TMSLINE#   TMS LINE #                                   
         TM    STATUS,STMINUS      MAKING NEGATIVE POSTING                      
         BNO   ELEM40A                                                          
         ZAP   DUB,PRTHOUR                                                      
         MP    DUB,PNEGONE                                                      
         ZAP   PRTHOUR,DUB                                                      
*                                                                               
ELEM40A  DS    0H                                                               
*&&US                                                                           
         MVI   PRTSTAT,PRTSNOTQ    N-TIME                                       
         CLI   TMSTTYP,TMSTCR      R-TIME                                       
         BNE   *+8                                                              
         MVI   PRTSTAT,PRTSRTEQ                                                 
         CLI   TMSTTYP,TMSTCB      B-TIME                                       
         BNE   *+8                                                              
         MVI   PRTSTAT,PRTSBILQ                                                 
         TM    TMSRSTA,TMSRADJ                                                  
         BNO   *+8                                                              
         OI    PRTSTAT,PRTSADJ     RATE WAS ADJUSTED                            
*&&                                                                             
         GOTO1 AADDELEM                                                         
         B     ROUTS1X                                                          
         EJECT                                                                  
***********************************************************************         
* X'44' TRANSACTION ELEMENT                                           *         
***********************************************************************         
         SPACE 1                                                                
         USING TMSD,R4             R4=A(CURRENT TABLE ENTRY)                    
         USING TRNELD,R2                                                        
ELEM44   LA    R2,ELEM                                                          
         XC    ELEM,ELEM                                                        
         MVI   TRNEL,TRNELQ        X'44'                                        
         MVC   TRNDATE,TTYYMMDD    TRANSACTION DATE                             
         TM    POSTSTAT,POSTCA     ARE WE POSTING CA TYPE BUCKETS               
         BZ    ELEM4402            NO                                           
         MVC   TRNREF,SPACES       YES                                          
         MVC   TRNREF(L'METHOD),METHOD                                          
         B     ELEM4404                                                         
ELEM4402 MVC   TRNREF,BCREF                                                     
         MVI   TRNTYPE,49          TYPE 49                                      
         CLI   TMSTTYP,TMSTMATR    IF MATERIALS IT'S A BATCH TYPE 8             
         BNE   ELEM4404                                                         
         MVI   TRNTYPE,8           TYPE 8                                       
*                                                                               
ELEM4404 MVC   TRNMOS,TMSMOA       CONVERT YYMM -> YM EDCDIC                    
         OI    TRNMOS,X'F0'        EX/  X'9402' -> C'42'                        
         SR    R1,R1                                                            
         IC    R1,TRNMOS+1                                                      
         LA    RF,X'F0'                                                         
         TM    TRNMOS+1,X'10'                                                   
         BNO   *+8                                                              
         LA    RF,X'B1'                                                         
         AR    R1,RF                                                            
         STC   R1,TRNMOS+1                                                      
*                                                                               
         TM    POSTSTAT,POSTCA     ARE WE POSTING CA TYPE BUCKETS               
         BZ    ELEM4406            NO                                           
         ZAP   TRNAMNT,PZERO       ZERO AMOUNT                                  
         SR    R1,R1                                                            
         B     ELEM44B                                                          
ELEM4406 SR    R1,R1               PERIOD NUMBER INTO LAST 4 CHARS              
         IC    R1,TTPERIOD         OF BATCH REFERENCE                           
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  TRNBREF(4),DUB+5(3)                                              
*                                                                               
         TM    POSTSTAT,POSTSJ+POSTTXSJ     SJ POSTING                          
         BZ    *+8                                                              
         OI    TRNSTAT,TRNSAUTH             ONLY AUTHORIZED USERS               
         ZAP   TRNAMNT,PZERO                                                    
         TM    POSTSTAT,POSTTXSJ+POSTTXCR   ALL TIME IS NON COMMISSION          
         BO    *+8                          EXCEPT TAX ITEMS                    
         OI    TRNSTAT,TRNSNOCM                                                 
         TM    POSTSTAT,POST1R+POSTSJ+POST1C+POSTTXSJ                           
         BZ    *+8                                                              
         OI    TRNSTAT,TRNSDR      DEBIT POSTING FOR 1R/SJ/1C                   
         TM    POSTSTAT,POST1R                                                  
         BO    ELEM44A                                                          
         CLI   TMSTTYP,TMSTCB      ONLY PUT AMOUNT IN BILLABLE POSTINGS         
         BE    ELEM4408                                                         
         CLI   TMSTTYP,TMSTMATR    AND MATERIALS                                
         BNE   ELEM44A                                                          
ELEM4408 ZAP   TRNAMNT,BCAMOUNT    AMOUNT                                       
         TM    POSTSTAT,POSTTXSJ+POSTTXCR                                       
         BZ    *+10                                                             
         ZAP   TRNAMNT,BCTXAMNT                                                 
         TM    STATUS,STMINUS      MAKING NEGATIVE POSTING                      
         BNO   ELEM44A                                                          
         ZAP   DUB,TRNAMNT                                                      
         MP    DUB,PNEGONE                                                      
         ZAP   TRNAMNT,DUB                                                      
*                                                                               
ELEM44A  MVC   TRNOFFC,TMSOFFC      EITHER OFFICE OR TASK                       
         TM    POSTSTAT,POSTSJ+POSTTXSJ                                         
         BZ    *+10                                                             
         MVC   TRNOFFC,TMSTSK                                                   
         SR    R1,R1                                                            
         ICM   R1,1,BCLNARR                                                     
         BZ    ELEM44B                                                          
         SHI   R1,1                                                             
         MVC   TRNNARR(0),BCNARR   COPY NARRATIVE INTO X'44' ELEM               
         EX    R1,*-6                                                           
         LA    R1,1(R1)                                                         
*                                                                               
ELEM44B  LA    RF,TRNLN1Q          CALCULATE ELEMENT LENGTH                     
         LA    RF,0(R1,RF)                                                      
         STC   RF,TRNLN                                                         
         GOTO1 AADDELEM                                                         
         B     ROUTS1X                                                          
         EJECT                                                                  
***********************************************************************         
* X'4C' SUBSIDIARY POSTING ELEMENT                                    *         
***********************************************************************         
         SPACE 1                                                                
         USING TMSD,R4             R4=A(CURRENT TABLE ENTRY)                    
ELEM4C   CLC   BCSIACCT,SPACES     DONT ADD MEMO ELEMENT IF NO INC ACCT         
         BNH   ELEM4CX                                                          
*                                                                               
         USING SPDELD,R2                                                        
         LA    R2,ELEM                                                          
         XC    ELEM,ELEM                                                        
         MVI   SPDEL,SPDELQ        X'4C'                                        
         MVC   SPDACCS,BCSIACCT    INCOME ACCOUNT                               
         LA    R1,SPDLN1Q                                                       
         LA    R1,L'BCSIACCT(R1)                                                
         STC   R1,SPDLN                                                         
         GOTO1 AADDELEM                                                         
ELEM4CX  B     ROUTS1X                                                          
         EJECT                                                                  
***********************************************************************         
* X'50' SUBSIDIARY CASH ELEMENT (HOURS)                               *         
***********************************************************************         
         SPACE 1                                                                
         USING TMSD,R4             R4=A(CURRENT TABLE ENTRY)                    
         USING SCIELD,R2                                                        
ELEM50   LA    R2,ELEM                                                          
         XC    ELEM,ELEM                                                        
         MVI   SCIEL,SCIELQ        X'50'                                        
         MVI   SCILN,SCILN1Q                                                    
         MVI   SCITYPE,SCITHOUR    HOURS                                        
         TM    POSTSTAT,POSTCA     ARE WE CREATING CA TYPE BUCKETS              
         BZ    ELEM5002            NO                                           
         MVI   SCILN,SCILN3Q       YES                                          
         MVI   SCITYPE,SCITNULL                                                 
         ZAP   SCIAMNT,AMNTDR      DEBIT AMOUNT                                 
         ZAP   SCIADMN,AMNTCR      CREDIT AMOUNT                                
         MVC   SCISUBTY,SPACES                                                  
         MVC   SCISUBTY(L'BUCKTYPE),BUCKTYPE                                    
         B     ELEM50C                                                          
ELEM5002 DS    0H                                                               
*&&UK                                                                           
         TM    POSTSTAT,POSTSJ                                                  
         BZ    ELEM50A                                                          
         MVI   SCITYPE,SCITSJHR                                                 
         ZAP   SCIAMNT,TMSHRS      HOURS                                        
         B     ELEM50B                                                          
*&&                                                                             
ELEM50A  CLI   TMSTTYP,TMSTNC      NON CLIENT 1N TIME                           
         BNE   *+18                DONT GENERATE HOUR BUCKETS FOR               
         CLC   TMSCNTRA+2(2),=C'L '       LEAVE OF ABSENCE                      
         BNE   *+8                                                              
         MVI   SCITYPE,SCITLOAT    C'X' - LOA BUCKET TYPE                       
         ZAP   SCIAMNT,TMSHRS                                                   
*                                                                               
ELEM50B  TM    STATUS,STMINUS                                                   
         BNO   ELEM50C                                                          
         ZAP   DUB,SCIAMNT                                                      
         MP    DUB,PNEGONE                                                      
         ZAP   SCIAMNT,DUB                                                      
*                                                                               
ELEM50C  GOTO1 AADDELEM                                                         
         B     ROUTS1X                                                          
         EJECT                                                                  
***********************************************************************         
* X'50' SUBSIDIARY CASH ELEMENT (SALES OR COST RATE MEMO)             *         
***********************************************************************         
         SPACE 1                                                                
*&&UK                                                                           
         USING TMSD,R4             R4=A(CURRENT TABLE ENTRY)                    
         USING SCIELD,R2                                                        
ELEM50R  LA    R2,ELEM                                                          
         XC    ELEM,ELEM                                                        
         MVI   SCIEL,SCIELQ        X'50'                                        
         MVI   SCILN,SCILN1Q                                                    
         MVI   SCITYPE,SCITCRAT    ALWAYS USE TYPE C EVEN IF ITS NOT            
*                                                                               
         ZAP   DUB,TMSRATE                                                      
         CLI   TMSTTYP,TMSTCB      ARE WE DOING B - TIME                        
         BE    EL50R10              CHK TTPROF1 AND TAKE OPPOSITE               
         TM    TTPROF2,TTPR1CST    PROF2 FOR N AND R TIME                       
         B     *+8                  CHK TTPROF2 AND POST AS STATED              
EL50R10  TM    TTPROF1,TTPR1SAL     (THIS IS OK THIS WAY ROUND)                 
         BNO   *+10                                                             
         ZAP   DUB,TMSCRATE                                                     
         MP    DUB,TMSHRS                                                       
         SRP   DUB,64-2,5                                                       
         ZAP   SCIAMNT,DUB                                                      
*                                                                               
         TM    STATUS,STMINUS      REVERSE AMOUNT IF BACKING OUT                
         BNO   ELEM50RX                                                         
         ZAP   DUB,SCIAMNT                                                      
         MP    DUB,PNEGONE                                                      
         ZAP   SCIAMNT,DUB                                                      
*                                                                               
ELEM50RX GOTO1 AADDELEM                                                         
         B     ROUTS1X                                                          
         EJECT                                                                  
***********************************************************************         
* X'50' SUBSIDIARY CASH ELEMENT (GROSS DOLLARS BUCKET)                *         
***********************************************************************         
         SPACE 1                                                                
         USING TMSD,R4             R4=A(CURRENT TABLE ENTRY)                    
         USING SCIELD,R2                                                        
ELEM50G  LA    R2,ELEM                                                          
         XC    ELEM,ELEM                                                        
         MVI   SCIEL,SCIELQ        X'50'                                        
         MVI   SCILN,SCILN1Q                                                    
         MVI   SCITYPE,SCITGRSS    GROSS AMOUNT                                 
         ZAP   SCIAMNT,PZERO                                                    
         GOTO1 AADDELEM                                                         
         B     ROUTS1X                                                          
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* X'59' ARTICLE TRANSACTION DATA ELEMENT                              *         
***********************************************************************         
         SPACE 1                                                                
         USING TMSD,R4             R4=A(CURRENT TABLE ENTRY)                    
         USING ARTELD,R2                                                        
ELEM59   SR    R5,R5                                                            
         ICM   R5,15,TMSAITMS      DO WE HAVE AN ITEMS ELEMENT                  
         BZ    ELEM59X             NO                                           
         USING TIMELD,R5                                                        
         OC    TIMISEQ,TIMISEQ     WAS AN ITEM ADDED                            
         BZ    ELEM59X                                                          
         LA    R2,ELEM                                                          
         XC    ELEM,ELEM                                                        
         MVI   ARTEL,ARTELQ        X'59'                                        
         MVI   ARTLN,ARTLN2Q                                                    
         MVC   ARTWC,TIMITSK       WORKCODE/TASK CODE                           
         MVC   ARTSEQ,TIMISEQ      SEQUENCE                                     
         MVC   ARTNUM,TIMINUM      NUMBER                                       
         ZAP   ARTPRICE,TMSRATE    RATE                                         
         ZAP   ARTMULT,TMSMULT     MULTIPLIER                                   
         TM    STATUS,STMINUS      MAKING NEGATIVE POSTING                      
         BNO   ELEM5902                                                         
         ZAP   DUB,ARTMULT                                                      
         MP    DUB,PNEGONE                                                      
         ZAP   ARTMULT,DUB                                                      
*                                                                               
ELEM5902 TM    TIMIIND,TIMISNPR                                                 
         BZ    *+8                                                              
         OI    ARTSTAT,ARTSTXQ                                                  
         TM    TIMIIND,TIMISPOV                                                 
         BZ    *+8                                                              
         OI    ARTSTAT,ARTSTPQ                                                  
         GOTO1 AADDELEM                                                         
ELEM59X  B     ROUTS1X                                                          
         EJECT                                                                  
***********************************************************************         
* X'5F' SALES TAX ELEMENT                                             *         
***********************************************************************         
         SPACE 1                                                                
         USING TMSD,R4             R4=A(CURRENT TABLE ENTRY)                    
         USING SUTELD,R2                                                        
ELEM5F   LA    R2,ELEM                                                          
         XC    ELEM,ELEM                                                        
         MVI   SUTEL,SUTELQ        X'5F'                                        
         MVI   SUTLN,SUTLN2Q                                                    
         MVC   SUTEFF,BCTXEFF      EFFECTIVE DATE                               
         ZAP   SUTRTE,BCTXRATE     TAX RATE                                     
         ZAP   SUTBAS,BCTXBAS      BASIS AMOUNT                                 
         MVC   SUTLOC,SPACES                                                    
         MVC   SUTLOC(L'BCTXLOC),BCTXLOC     LOCALITY CODE                      
         GOTO1 AADDELEM                                                         
         B     ROUTS1X                                                          
         EJECT                                                                  
***********************************************************************         
* X'60' TRANSACTION STATUS ELEMENT                                    *         
***********************************************************************         
         SPACE 1                                                                
         USING TMSD,R4             R4=A(CURRENT TABLE ENTRY)                    
         USING TRSELD,R2                                                        
ELEM60   LA    R2,ELEM                                                          
         XC    ELEM,ELEM                                                        
         MVI   TRSEL,TRSELQ        X'60'                                        
         MVI   TRSLN,TRSLNQ                                                     
         L     RF,TTCFACS          COMPRESSED ACTIVITY DATE                     
         L     RF,CDATCON-COMFACSD(RF)                                          
         GOTO1 (RF),DMCB,(5,0),(2,TRSDATE)                                      
         MVC   TRSEFDT,TRSDATE     EFFECTIVE DATE                               
         MVC   TRSPMOS,TMSMOA                                                   
         MVC   TRSUSER,TTUSERID    USER ID                                      
         TM    TMSIND,TMSIADJ      TIMESHEET ADJUSTED                           
         BNO   *+8                                                              
         OI    TRSSTAT2,TRSSTADJ                                                
         TM    TRSSTAT2,TRSSTMSS+TRSSTADJ                                       
         BNZ   *+8                                                              
         OI    TRSSTAT2,TRSSTIME   REGULAR                                      
         TM    TTSTAT1,TTSTTIME    TEST MCS TIME                                
         BZ    ELEM6010            NO -FINISHED                                 
         MVI   TRSSTAT3,TRSSMCS    SOURCE IS MCS FOR POSTING                    
         TM    POSTSTAT,POSTSJ+POSTSI+POST12+POST1C                             
         BZ    ELEM6010                                                         
         TM    STATUS,STORIGNL                                                  
         BO    ELEM6004                                                         
         TM    TTTSSTAT,TIMSFAPP   IS TIME FULLY APPROVED                       
         BNZ   ELEM6010            YES - STATUS TO SET                          
         OC    TTTSSTAT,TTTSSTAT   ANY STATUS                                   
         BNZ   ELEM6002            YES                                          
         MVI   TRSSTAT4,TRSSSAVT   NONE MUST BE SAVED                           
         B     ELEM6010                                                         
ELEM6002 TM    TTTSSTAT,TIMSMAAP   IS TIME LINE MANAGER APPROVED                
         BZ    *+8                 NO                                           
         OI    TRSSTAT4,TRSSMAAP   YES - SET STATUS ACCORDINGLY                 
         TM    TTTSSTAT,TIMSREJE   IS TIME LINE MANAGER REJECTED                
         BZ    *+8                 NO                                           
         MVI   TRSSTAT4,TRSSREJT   YES - SET STATUS ACCORDINGLY                 
         B     ELEM6008                                                         
*                                                                               
ELEM6004 TM    TTTSPRST,TIMSFAPP   IS TIME FULLY APPROVED                       
         BNZ   ELEM6010            YES - STATUS TO SET                          
         OC    TTTSSTAT,TTTSSTAT   ANY STATUS                                   
         BNZ   ELEM6006            YES                                          
         MVI   TRSSTAT4,TRSSSAVT   NONE MUST BE SAVED                           
         B     ELEM6010                                                         
ELEM6006 TM    TTTSPRST,TIMSMAAP   IS TIME LINE MANAGER APPROVED                
         BZ    *+8                 NO                                           
         OI    TRSSTAT4,TRSSMAAP   YES - SET STATUS ACCORDINGLY                 
         TM    TTTSPRST,TIMSREJE   IS TIME LINE MANAGER REJECTED                
         BZ    *+8                 NO                                           
         MVI   TRSSTAT4,TRSSREJT   YES - SET STATUS ACCORDINGLY                 
                                                                                
ELEM6008 TM    TMSMTSS,TIMESAPR    TEST APPROVED                                
         BZ    *+8                                                              
         OI    TRSSTAT4,TRSSSJAT   SET FURTHER STATUS BYTE                      
         TM    TMSMTSS,TIMESREJ    TEST REJECTED                                
         BZ    *+8                                                              
         MVI   TRSSTAT4,TRSSREJT                                                
         OC    TRSSTAT4,TRSSTAT4                                                
         BNZ   ELEM6009                                                         
         MVI   TRSSTAT4,TRSSSUBT   IF NEITHER, ASSUME SUBMITTED                 
         B     ELEM6010                                                         
*                                                                               
ELEM6009 TM   TRSSTAT4,TRSSSJAT+TRSSMAAP                                        
         BNO  ELEM6010                                                          
         XC   TRSSTAT4,TRSSTAT4   CLEAR AS FULLY APPROVED                       
*                                                                               
ELEM6010 GOTO1 AADDELEM                                                         
         B     ROUTS1X                                                          
         EJECT                                                                  
***********************************************************************         
* X'65' ANALYZED OFFICE ELEMENT                                       *         
***********************************************************************         
         SPACE 1                                                                
         USING TMSD,R4             R4=A(CURRENT TABLE ENTRY)                    
ELEM65   DS    0H                                                               
*                                                                               
*&&UK                                                                           
         CLI   TMSOFFC+1,C' '      ONE BYTE OR TWO BYTE OFFICE CODE             
         BE    ELEM65X             ONLY ADDING ELEM FOR TWO BYTE OFF            
*&&                                                                             
*                                                                               
         USING ANOELD,R2                                                        
         LA    R2,ELEM                                                          
         MVI   ANOEL,ANOELQ        X'65' - OFFICE ELEMENT                       
         MVI   ANOLN,ANOLNQ                                                     
         MVI   ANOTYPE,ANOTCLI     C'C' - CLIENT OFFICE                         
         MVC   ANOOFFC,TMSOFFC     CLIENT OFFICE CODE                           
         GOTO1 AADDELEM                                                         
ELEM65X  B     ROUTS1X                                                          
         EJECT                                                                  
***********************************************************************         
* X'7B' SOURCE ELEMENT - UK ONLY                                      *         
***********************************************************************         
         SPACE 1                                                                
*&&UK                                                                           
         USING TMSD,R4             R4=A(CURRENT TABLE ENTRY)                    
ELEM7B   CLC   BCSIACCT(2),INCLDG  ONLY ADD TO SI POSTING                       
         BNE   ELEM7BX             DONT ADD TO SK POSTING                       
*                                                                               
         USING SORELD,R2                                                        
         LA    R2,ELEM                                                          
         XC    ELEM,ELEM                                                        
         MVI   SOREL,SORELQ        X'7B' - SOURCE ELEMENT                       
         MVI   SORLN,SORALNQ                                                    
         MVI   SORSYS,SORSACC      C'A' - ACCOUNT SYSTEM                        
         MVC   SORAULA,BCSJACCT    SJ U/L/CLI/PRD/JOB                           
         GOTO1 AADDELEM                                                         
ELEM7BX  B     ROUTS1X                                                          
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* X'C0' ANALYSIS POINTER ELEMENT                                      *         
***********************************************************************         
         SPACE 1                                                                
         USING TMSD,R4             R4=A(CURRENT TABLE ENTRY)                    
         USING APEELD,R2                                                        
ELEMC0   LA    R2,ELEM                                                          
         XC    ELEM,ELEM                                                        
         MVI   APEEL,APEELQ        X'C0'                                        
*                                                                               
         MVI   APENUM,2            # MINI ELEMENTS                              
         OI    APENSTAT,APENSDR                                                 
         LA    R1,BC1RACCT+L'BC1RACCT-1                                         
         CLI   0(R1),X'40'                                                      
         BH    *+8                                                              
         BCT   R1,*-8                                                           
         LA    RE,BC1RACCT                                                      
         SR    R1,RE                                                            
         MVC   APENACT(0),BC1RACCT 1R ACCOUNT - DR                              
         EX    R1,*-6                                                           
         LA    R1,APELN2Q+1(R1)                                                 
         STC   R1,APENLEN                                                       
         LA    R2,0(R1,R2)                                                      
*                                                                               
         OI    APENSTAT,APENSDR                                                 
         LA    R1,BCCNTRA+L'BCCNTRA-1                                           
         CLI   0(R1),X'40'                                                      
         BH    *+8                                                              
         BCT   R1,*-8                                                           
         LA    RE,BCCNTRA                                                       
         SR    R1,RE                                                            
         MVC   APENACT(0),BCCNTRA  1C ACCOUNT - DR                              
         EX    R1,*-6                                                           
         LA    R1,APELN2Q+1(R1)                                                 
         STC   R1,APENLEN                                                       
         LA    R2,0(R1,R2)                                                      
*                                                                               
         CLC   BC12ACCT,SPACES                                                  
         BNH   ELEMC0A                                                          
         LA    RF,ELEM                                                          
         SR    R1,R1                                                            
         IC    R1,APENUM-APEELD(RF)                                             
         LA    R1,1(R1)                                                         
         STC   R1,APENUM-APEELD(RF)                                             
         LA    R1,BC12ACCT+L'BC12ACCT-1                                         
         CLI   0(R1),X'40'                                                      
         BH    *+8                                                              
         BCT   R1,*-8                                                           
         LA    RE,BC12ACCT                                                      
         SR    R1,RE                                                            
         MVC   APENACT(0),BC12ACCT 12 ACCOUNT - CR                              
         EX    R1,*-6                                                           
         LA    R1,APELN2Q+1(R1)                                                 
         STC   R1,APENLEN                                                       
         LA    R2,0(R1,R2)                                                      
*                                                                               
ELEMC0A  TM    POSTSTAT,POSTSJ     CARRY INCOME ACCOUNT ON SJ POSTINGS          
         BZ    ELEMC0B             BUT ONLY IN EUROPE                           
         CLC   BCSIACCT,SPACES                                                  
         BNH   ELEMC0B                                                          
         LA    RF,ELEM                                                          
         SR    R1,R1                                                            
         IC    R1,APENUM-APEELD(RF)                                             
         LA    R1,1(R1)                                                         
         STC   R1,APENUM-APEELD(RF)                                             
         LA    R1,BCSIACCT+L'BCSIACCT-1                                         
         CLI   0(R1),X'40'                                                      
         BH    *+8                                                              
         BCT   R1,*-8                                                           
         LA    RE,BCSIACCT                                                      
         SR    R1,RE                                                            
         MVC   APENACT(0),BCSIACCT SI ACCOUNT - CR                              
         EX    R1,*-6                                                           
         LA    R1,APELN2Q+1(R1)                                                 
         STC   R1,APENLEN                                                       
         LA    R2,0(R1,R2)                                                      
*                                                                               
ELEMC0B  LA    R2,APELN1Q(R2)                                                   
         LA    RF,ELEM                                                          
         SR    R2,RF                                                            
         STC   R2,APELN-APEELD(RF) STORE ELEMENT LENGTH                         
         GOTO1 AADDELEM                                                         
         B     ROUTS1X                                                          
         EJECT                                                                  
***********************************************************************         
* X'C5' FREEFORM DATA ELEMENT TO CARRY WORKCODE                       *         
***********************************************************************         
         SPACE 1                                                                
         USING TMSD,R4             R4=A(CURRENT TABLE ENTRY)                    
         USING RFLELD,R2                                                        
ELEMC5   LA    R2,ELEM                                                          
         XC    ELEM,ELEM                                                        
         MVI   RFLEL,RFLELQ        X'C5'                                        
         MVI   RFLLN,RFLLNQ+L'TMSTSK                                            
         MVI   RFLTYPE,RFLWC                                                    
         MVC   RFLDATA(L'TMSTSK),TMSTSK                                         
         GOTO1 AADDELEM                                                         
         B     ROUTS1X                                                          
         EJECT                                                                  
***********************************************************************         
* X'D6' TERMINAL ID ELEMENT                                           *         
***********************************************************************         
         SPACE 1                                                                
ELEMD6   OC    TTTID,TTTID                                                      
         BZ    ELEMD6X                                                          
*                                                                               
         USING TIDELD,R2                                                        
         LA    R2,ELEM                                                          
         XC    ELEM,ELEM                                                        
         MVI   TIDEL,TIDELQ        X'D6'                                        
         MVI   TIDLN,TIDLNQ                                                     
         MVC   TID,TTTID                                                        
         GOTO1 AADDELEM                                                         
ELEMD6X  B     ROUTS1X                                                          
         EJECT                                                                  
***********************************************************************         
* X'D8' PERSON ID ELEMENT                                             *         
***********************************************************************         
         SPACE 1                                                                
ELEMD8   OC    TTSECPID,TTSECPID                                                
         BZ    ELEMD8X                                                          
*                                                                               
         USING PIDELD,R2                                                        
         LA    R2,ELEM                                                          
         XC    ELEM,ELEM                                                        
         MVI   PIDEL,PIDELQ        X'D8'                                        
         MVI   PIDLN,PIDLNQ                                                     
*        MVC   PIDNO,TTPIDNO       PERSONAL PID (PERSON TS BELONGS TO)          
         MVC   PIDNO,TTSECPID      USER'S PID # (PERSON WHO ENTERED TS)         
         GOTO1 AADDELEM                                                         
ELEMD8X  B     ROUTS1X                                                          
         EJECT                                                                  
***********************************************************************         
* X'DB' FREE FORM TEXT ELEMENT FOR PERIOD END DATE - USED BY CBILL    *         
***********************************************************************         
         SPACE 1                                                                
*&&UK                                                                           
ELEMDB   TM    TTSTAT1,TTSTDALY    ARE WE ON DAILY TIME                         
         BZ    ELEMDBX             NO - NO NEED TO ADD ELEMENT AS DATE          
*                                  IS ALREADY ON TRANSACTION KEY                
         USING FFTELD,R2                                                        
         LA    R2,ELEM             ADD FFTEL WITH PERIOD END DATE               
         XC    ELEM,ELEM                                                        
         MVI   FFTEL,FFTELQ        X'DB'                                        
         MVI   FFTLN,FFTLN1Q+L'FFTDLEN+L'TTPREDTE                               
         MVI   FFTTYPE,FFTTPEDT                                                 
         MVI   FFTDLEN,L'TTPREDTE                                               
         MVC   FFTDATA(L'TTPREDTE),TTPREDTE                                     
         GOTO1 AADDELEM                                                         
ELEMDBX  B     ROUTS1X                                                          
         EJECT                                                                  
***********************************************************************         
**  BUILD OCAEL ELEMENT FOR TRANSACTION ELEMENT                       *         
***********************************************************************         
         SPACE 1                                                                
ELEMFETR CLI   TTCTRY,CTRYGER      ONLY ADD FOR GERMAN AGENCIES                 
         BNE   ELFETRX                                                          
*                                                                               
         USING OCAEL,R2                                                         
         LA    R2,ELEM                                                          
         XC    ELEM,ELEM                                                        
         MVI   OCAEL,OCAELQ                                                     
         MVI   OCAINDS,0                                                        
         LA    R1,1                INITIALIZE ENTRY NUM TO 1                    
         LA    R3,OCANTRY                                                       
         USING OCANTRY,R3                                                       
*                                                                               
         ZAP   DUB,TMSERTE                                                      
         LA    RE,TTPROF1          PROF1 FOR B - TIME                           
         CLI   TMSTTYP,TMSTCB      ARE WE DOING B - TIME                        
         BE    *+8                                                              
         LA    RE,TTPROF2          PROF2 FOR N AND R TIME                       
         TM    0(RE),TTPR1CST      CARRY OPPOSITE INFO ON JOB POSTING           
         BNO   *+10                                                             
         ZAP   DUB,TMSECRTE                                                     
         MP    DUB,TMSHRS                                                       
         SRP   DUB,64-2,5                                                       
         TM    STATUS,STMINUS      MAKING NEGATIVE POSTING                      
         BNO   *+10                                                             
         MP    DUB,PNEGONE                                                      
*                                                                               
         MVI   OCANSEQN,1                                                       
         MVI   OCANTYPE,QTRNAMNT                                                
         ZAP   OCANCASH(L'TRNAMNT),DUB                                          
         AHI   R3,OCANTR1L+L'TRNAMNT                                            
*                                                                               
         SR    R3,R2                                                            
         STC   R3,OCALN                                                         
         SR    R0,R0               NUMBER OF ENTRIES SHARES ITS FLD             
         IC    R0,OCANUM           WITH OCAINDS SO IT MUST BE ORD               
         OR    R1,R0               ON WITH INDS                                 
         STC   R1,OCANUM           NUMBER OF ENTRIES                            
         GOTO1 AADDELEM                                                         
ELFETRX  B     ROUTS1X                                                          
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
**  BUILD OCAEL ELEMENT FOR TRANSACTION/SUBSIDIARY CASH INFO          *         
***********************************************************************         
         SPACE 1                                                                
ELEMFETS CLI   TTCTRY,CTRYGER      ONLY ADD FOR GERMAN AGENCIES                 
         BNE   ELFETSX                                                          
*                                                                               
         USING OCAEL,R2                                                         
         LA    R2,ELEM                                                          
         XC    ELEM,ELEM                                                        
         MVI   OCAEL,OCAELQ                                                     
         MVI   OCAINDS,0                                                        
         SR    R1,R1               CALCULATE NUMBER OF ENTRIES IN R1            
         LA    R3,OCANTRY                                                       
         USING OCANTRY,R3                                                       
*                                                                               
         ZAP   DUB,TMSERTE                                                      
         LA    RE,TTPROF1          PROF1 FOR B - TIME                           
         CLI   TMSTTYP,TMSTCB      ARE WE DOING B - TIME                        
         BE    *+8                                                              
         LA    RE,TTPROF2          PROF2 FOR N AND R TIME                       
         TM    0(RE),TTPR1CST      CARRY OPPOSITE INFO ON JOB POSTING           
         BNO   *+10                                                             
         ZAP   DUB,TMSECRTE                                                     
         MP    DUB,TMSHRS                                                       
         SRP   DUB,64-2,5                                                       
         TM    STATUS,STMINUS      MAKING NEGATIVE POSTING                      
         BNO   *+10                                                             
         MP    DUB,PNEGONE                                                      
*                                                                               
         MVI   OCANSEQN,1                                                       
         MVI   OCANTYPE,QTRNAMNT                                                
         ZAP   OCANCASH(L'TRNAMNT),DUB                                          
         LA    R1,1(R1)            BUMP UP NUMBER OF ENTRIES                    
         AHI   R3,OCANTR1L+L'TRNAMNT                                            
*                                                                               
         MVI   OCANSEQN,1                                                       
         MVI   OCANTYPE,QSCITGRSS                                               
         ZAP   OCANCASH(L'SCIAMNT),PZERO                                        
         LA    R1,1(R1)            BUMP UP NUMBER OF ENTRIES                    
         AHI   R3,OCANTR1L+L'SCIAMNT                                            
*                                                                               
         SR    R3,R2                                                            
         STC   R3,OCALN                                                         
         SR    R0,R0               NUMBER OF ENTRIES SHARES ITS FLD             
         IC    R0,OCANUM           WITH OCAINDS SO IT MUST BE ORD               
         OR    R1,R0               ON WITH INDS                                 
         STC   R1,OCANUM           NUMBER OF ENTRIES                            
         GOTO1 AADDELEM                                                         
ELFETSX  B     ROUTS1X                                                          
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
**  BUILD OCAEL ELEMENT FOR SJ POSTINGS (PRTELD/TRNELD/SCIELD)        *         
***********************************************************************         
         SPACE 1                                                                
ELEMFESJ CLI   TTCTRY,CTRYGER      ONLY ADD FOR GERMAN AGENCIES                 
         BNE   ELFESJX                                                          
*                                                                               
         USING OCAEL,R2                                                         
         LA    R2,ELEM                                                          
         XC    ELEM,ELEM                                                        
         MVI   OCAEL,OCAELQ                                                     
         MVI   OCAINDS,0                                                        
         SR    R1,R1               INITIALIZE NUMBER OF ENTRIES                 
         LA    R3,OCANTRY                                                       
         USING OCANTRY,R3                                                       
*                                                                               
         ZAP   DUB,TMSERTE                                                      
         LA    RE,TTPROF1          PROF1 FOR B - TIME                           
         CLI   TMSTTYP,TMSTCB      ARE WE DOING B - TIME                        
         BE    *+8                                                              
         LA    RE,TTPROF2          PROF2 FOR N AND R TIME                       
         TM    0(RE),TTPR1CST      CARRY OPPOSITE INFO ON JOB POSTING           
         BNO   *+10                                                             
         ZAP   DUB,TMSECRTE                                                     
         MP    DUB,TMSHRS                                                       
         SRP   DUB,64-2,5                                                       
         TM    STATUS,STMINUS      MAKING NEGATIVE POSTING                      
         BNO   *+10                                                             
         MP    DUB,PNEGONE                                                      
*                                                                               
         MVI   OCANSEQN,1                                                       
         MVI   OCANTYPE,QTRNAMNT                                                
         ZAP   OCANCASH(L'TRNAMNT),PZERO                                        
         CLI   TMSTTYP,TMSTCB      ONLY PUT AMOUNT IN BILLABLE POSTINGS         
         BNE   *+10                                                             
         ZAP   OCANCASH(L'TRNAMNT),DUB                                          
         LA    R1,1(R1)            BUMP UP NUMBER OF ENTRIES                    
         AHI   R3,OCANTR1L+L'TRNAMNT                                            
*                                                                               
         CP    TMSERTE,PZERO                                                    
         BE    ELFESJ10                                                         
         ZAP   DUB,TMSERTE                                                      
*        TM    STATUS,STMINUS      MAKING NEGATIVE POSTING                      
*        BNO   *+10                                                             
         CP    DUB,PZERO                                                        
         BNL   *+10                                                             
         MP    DUB,PNEGONE                                                      
*                                                                               
         MVI   OCANSEQN,1                                                       
         MVI   OCANTYPE,QPRTRATE                                                
         ZAP   OCANCASH(L'PRTRATE),DUB                                          
         LA    R1,1(R1)            BUMP UP NUMBER OF ENTRIES                    
         AHI   R3,OCANTR1L+L'PRTRATE                                            
*                                                                               
ELFESJ10 DS    0H                  PUT SALES RATE OR COST RATE IN OCAEL         
         CLI   TMSTTYP,TMSTCB      ARE WE DOING B - TIME                        
         BNE   *+12                    CHOOSE RIGHT TTPROF FIELD                
         TM    TTPROF1,TTPR1SAL    IF B-TIME - CHK IF SALES RATE OPTION         
         B     *+8                     TAKE OPPOSITE                            
         TM    TTPROF2,TTPR1CST    IF NOT B-TIME - CHK COST RATE OPT            
         BO    ELFESJ20                POST AS STATED                           
*                                      (THIS IS OK THIS WAY ROUND)              
         CP    TMSERTE,PZERO                                                    
         BE    ELFESJ30                                                         
         MVI   OCANSEQN,2                                                       
         MVI   OCANTYPE,QSCITCOMM  USE COST RATE EQU TO MATCH SCIEL             
         ZAP   DUB,TMSERTE             PER NEIL SHELFORD FROM UK                
         MP    DUB,TMSHRS                                                       
         SRP   DUB,64-2,5                                                       
         TM    STATUS,STMINUS      MAKING NEGATIVE POSTING                      
         BNO   *+10                                                             
         MP    DUB,PNEGONE                                                      
         ZAP   OCANCASH(L'SCIAMNT),DUB                                          
         LA    R1,1(R1)                                                         
         AHI   R3,OCANTR1L+L'SCIAMNT                                            
         B     ELFESJ30                                                         
*                                                                               
ELFESJ20 CP    TMSECRTE,PZERO                                                   
         BE    ELFESJ30                                                         
         MVI   OCANSEQN,2                                                       
         MVI   OCANTYPE,QSCITCOMM                                               
         ZAP   DUB,TMSECRTE                                                     
         MP    DUB,TMSHRS                                                       
         SRP   DUB,64-2,5                                                       
         TM    STATUS,STMINUS      MAKING NEGATIVE POSTING                      
         BNO   *+10                                                             
         MP    DUB,PNEGONE                                                      
         ZAP   OCANCASH(L'SCIAMNT),DUB                                          
         LA    R1,1(R1)            BUMP UP NUMBER OF ENTRIES                    
         AHI   R3,OCANTR1L+L'SCIAMNT                                            
*                                                                               
ELFESJ30 SR    R3,R2                                                            
         STC   R3,OCALN                                                         
         SR    R0,R0               NUMBER OF ENTRIES SHARES ITS FLD             
         IC    R0,OCANUM           WITH OCAINDS SO IT MUST BE ORD               
         OR    R1,R0               ON WITH INDS                                 
         STC   R1,OCANUM           NUMBER OF ENTRIES                            
         GOTO1 AADDELEM                                                         
ELFESJX  B     ROUTS1X                                                          
         DROP  R3,R4                                                            
         EJECT                                                                  
*&&                                                                             
***********************************************************************         
* GET EURO RATES (CHARGE AND COST - IF ANY)                           *         
*     R5 = A(BUFFER)                                                  *         
*     R6 = A(TIMEL)                                                   *         
***********************************************************************         
         SPACE 1                                                                
*&&UK                                                                           
         USING TMSD,R5                                                          
         USING TIMELD,R6                                                        
GETEURO  MVC   SVKEY,KEY           SAVE KEY FOR LATER USE                       
         CLI   TMSTTYP,TMSTCB                                                   
         BNE   GETEX                                                            
         CLI   TTCTRY,CTRYGER      ONLY GET FOR GERMAN AGENCIES                 
         BNE   GETEX                                                            
         TM    TTACTION,TTBLDB4    BUILD BEFORE BUFFER?                         
         BNO   GETEX               NO - NEW                                     
         ZAP   TIMERTE,PZERO       INITIALIZE EURO CHARGE RATE                  
         ZAP   TIMECRTE,PZERO      INITIALIZE EURO COST RATE                    
*                                                                               
         CLC   TMSACC(2),=C'SJ'    MAKE SURE WE ARE AT LOWEST LEVEL             
         BNE   GETEX                                                            
         LA    RF,TMSACC                                                        
         LA    RF,2(RF)            BUMP PAST U/L                                
         SR    RE,RE                                                            
         IC    RE,TTSJLNQ2         LENGTH OF 1ST 2 LEVELS                       
         AR    RF,RE               BUMP TO LOWEST LEVEL                         
         LA    R1,12-1             GET LENGTH OF LOWEST LEVEL BY                
         SR    R1,RE               (LEN OF ACC-CUR POS)+1 FOR EX COM            
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   0(0,RF),SPACES                                                   
         BNH   GETEX                                                            
*                                                                               
         USING TRNRECD,R2                                                       
         LA    R2,KEY                                                           
         XC    KEY,KEY                                                          
         MVC   TRNKCPY,TTACKCPY                                                 
         MVC   TRNKULA,TMSACC      SJ ACCOUNT                                   
         MVC   TRNKWORK,TMSTSK     TSK CODE                                     
         MVC   TRNKCCPY,TTACKCPY                                                
         MVC   TRNKULC,TTACKUL     1R ACCOUNT                                   
         MVC   TRNKDATE,TTYYMMDD                                                
         MVC   TRNKREF,SPACES      CREATE BATCH REFERENCE #                     
         MVI   TRNKREF,C'T'                                                     
         SR    R1,R1               PERIOD NUMBER INTO CHARS 2-4                 
         IC    R1,TTPERIOD         OF BATCH REFERENCE                           
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  TRNKREF+1(3),DUB+6(2)                                            
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 TTDMGR,DMCB,(0,DMRDHI),ACCDIR,KEY,KEY,0                          
         CLC   TRNKEY(TRNKSBR-TRNKEY),KEYSAVE                                   
         BE    GETE20                                                           
*                                                                               
         LA    R1,KEYSAVE            TRY IT WITHOUT REFERENCE                   
         LA    R1,TRNKDATE-TRNKEY(R1)                                           
         MVC   0(L'TRNKDATE+L'TRNKREF,R1),SPACES                                
         MVC   KEY,KEYSAVE                                                      
         GOTO1 TTDMGR,DMCB,(0,DMRDHI),ACCDIR,KEY,KEY,0                          
         CLC   TRNKEY(TRNKDATE-TRNKEY),KEYSAVE                                  
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   TRNKDATE,TTYYMMDD     MAKE SURE IT WITHIN RANGE                  
         BNH   GETE20                                                           
         DC    H'0'                                                             
*                                                                               
GETE10   LA    R2,KEY                                                           
         GOTO1 TTDMGR,DMCB,(0,DMRSEQ),ACCDIR,KEY,KEY,0                          
         LA    R1,TRNKSBR-TRNKEY     ASSUME LENGTH OF CLC IS TO SUBR            
         LA    RE,KEYSAVE            CHECK IF WE HAVE A REF IN KEYSAVE          
         LA    RE,TRNKDATE-TRNKEY(RE)                                           
         CLC   0(L'TRNKDATE+L'TRNKREF,RE),SPACES                                
         BH    *+8                                                              
         LA    R1,TRNKDATE-TRNKEY      DON'T INCLUDE DATE/REF IN CLC            
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   TRNKEY(0),KEYSAVE                                                
         BNE   GETEX                                                            
*                                                                               
GETE20   LA    R3,TRNKDA                                                        
         LA    R2,ACCMST                                                        
         L     R7,ATRNIO2                                                       
         TM    TRNKSTAT,TRNSARCH                                                
         BZ    *+8                                                              
         LA    R2,ACCARC                                                        
         GOTO1 TTDMGR,DMCB,GETREC,(R2),(R3),(R7),WORK                           
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     R2,ATRNIO2                                                       
         LA    R2,TRNRFST                                                       
GETE30   CLI   0(R2),0                                                          
         BE    GETEX                                                            
         CLI   0(R2),TRNELQ           X'44' - TRANSACTION EL                    
         BE    GETE44                                                           
         CLI   0(R2),SCIELQ           X'50' - SUBSIDIARY CASH INFO EL           
         BE    GETE50                                                           
         CLI   0(R2),TRSELQ           X'60' - TRANSACTION STATUS EL             
         BE    GETE60                                                           
         CLI   0(R2),OCAELQ           X'FE' - OTHER AGENCY AMOUNT EL            
         BE    GETEFE                                                           
GETE40   SR    R1,R1                                                            
         IC    R1,1(R2)                                                         
         AR    R2,R1                                                            
         B     GETE30                                                           
*                                                                               
         USING TRNELD,R2                                                        
GETE44   DS    0H                                                               
         CLI   TMSTTYP,TMSTCB      ONLY CHECK FOR B TIME                        
         BNE   GETE40                   R TIME HAS ZERO DOLLARS                 
         CP    TRNAMNT,TIMAMNT     MAKE SURE THAT DOLLARS MATCH                 
         BNE   GETE10              IF NOT READ SEQ                              
         B     GETE40                 ELSE CONTINUE                             
         DROP  R2                                                               
*                                                                               
         USING SCIELD,R2                                                        
GETE50   CLI   SCITYPE,SCITSJHR    HOURS (UK SJ TIME)                           
         BNE   GETE40                                                           
         CP    SCIAMNT,TMSHRS      MAKE SURE THAT HOURS MATCH                   
         BNE   GETE10              IF NOT READ SEQ                              
         B     GETE40                 ELSE CONTINUE                             
         DROP  R2                                                               
*                                                                               
         USING TRSELD,R2                                                        
GETE60   DS    0H                                                               
         LA    R1,KEYSAVE            CHECK IF WE HAVE A REF IN KEYSAVE          
         LA    R1,TRNKREF-TRNKEY(R1)       IF WE DO-WE DONT NEED TO             
         CLC   0(L'TRNKREF,R1),SPACES      CHECK ACTIVITY DATE                  
         BH    GETE40                      JUST CONTINUE LOOPING                
         L     RF,TTCFACS          COMPRESSED ACTIVITY DATE                     
         L     RF,CDATCON-COMFACSD(RF)                                          
         GOTO1 (RF),DMCB,(1,TIMADAT),(2,BCWORK1)                                
         CLC   TRSDATE,BCWORK1        DO DATES MATCH?                           
         BNE   GETE10                    IF NOT-READ NEXT RECORD                
         B     GETE40                    ELSE CONTINUE                          
         DROP  R2                                                               
*                                                                               
         USING OCAELD,R2                                                        
GETEFE   SR    R0,R0                                                            
         ICM   R0,1,OCANUM         NUMBER OF ENTRIES                            
         BZ    GETEX                                                            
*                                                                               
         LA    RE,OCANTRY          1ST ENTRY                                    
         USING OCANTRY,RE                                                       
GETEFE10 CLI   OCANTYPE,QSCITSRAT  IS IT A SALES RATE?                          
         BE    GETEFE20                                                         
         CLI   OCANTYPE,QSCITCRAT  IS IT A COST RATE?                           
         BE    GETEFE30                                                         
         CLI   OCANTYPE,QPRTRATE   ONLY BUMP 6 BYTES FOR THIS                   
         BNE   GETEFE40                                                         
         ZAP   TIMERTE,OCANCASH(L'PRTRATE)                                      
         LA    RE,6(RE)                                                         
         BCT   R0,GETEFE10                                                      
         B     GETEX                                                            
*                                                                               
GETEFE20 ZAP   PACK16,OCANCASH     GET SALES AMOUNT AND BACK INTO RATE          
         MP    PACK16,=P'1000'                                                  
         DP    PACK16,TMSHRS                DIVID BY HOURS                      
         ZAP   TIMERTE,PACK16(L'PACK16-L'TMSHRS) TO GET RATE                    
         SRP   TIMERTE,64-1,5                                                   
         B     GETEFE40                                                         
*                                                                               
GETEFE30 ZAP   PACK16,OCANCASH     GET SALES AMOUNT AND BACK INTO RATE          
         MP    PACK16,=P'1000'                                                  
         DP    PACK16,TMSHRS                 DIVID BY HOURS                     
         ZAP   TIMECRTE,PACK16(L'PACK16-L'TMSHRS) TO GET RATE                   
         SRP   TIMECRTE,64-1,5                                                  
*                                                                               
GETEFE40 LA    RE,OCANTRYL(RE)                                                  
         BCT   R0,GETEFE10                                                      
         DROP  RE                                                               
*                                                                               
         USING TSWRECD,R2                                                       
GETEX    LA    R2,KEY                                                           
         MVC   KEY,SVKEY           RESTORE PREVIOUS KEY AND SEQ                 
         MVC   KEYSAVE,KEY                                                      
         GOTO1 TTDMGR,DMCB,(0,DMRDHI),ACCDIR,KEY,KEY,0                          
         CLC   TSWKEY(TSWKODS+L'TSWKODS-TSWKEY),KEYSAVE                         
         BE    *+6                                                              
         DC    H'0'                                                             
         B     ROUTS1X                                                          
         DROP  R2,R5,R6                                                         
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* VALIDATE POSTING ACCOUNTS                                           *         
***********************************************************************         
         SPACE 1                                                                
GETACC   LR    R3,R1                                                            
         GOTO1 AXCIO,TRNIO                                                      
         MVC   BCACTNAM,SPACES     ACCOUNT NAME                                 
         MVC   BCACANAL,SPACES     12 ANALYSIS ACCOUNT                          
         MVC   BCACCOST,SPACES     COSTING BYTE                                 
*                                                                               
         USING ACTRECD,R2                                                       
         LA    R2,KEY                                                           
         MVC   ACTKEY(L'KEY),SPACES                                             
         MVC   ACTKCPY,TTACKCPY                                                 
         MVC   ACTKULA,0(R3)                                                    
         GOTO1 TTDMGR,DMCB,(0,DMREAD),ACCDIR,KEY,KEY,0                          
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         LA    R3,ACTKDA                                                        
         GOTO1 TTDMGR,DMCB,GETREC,ACCMST,(R3),TRNIO,WORK                        
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R2,TRNIO                                                         
         LA    R2,ACTRFST                                                       
GETA10   CLI   0(R2),0                                                          
         BE    GETACCX                                                          
*                                                                               
         USING NAMELD,R2           *** ACCOUNT NAME ***                         
GETA20   CLI   0(R2),NAMELQ        X'20'                                        
         BNE   GETA30                                                           
         SR    R1,R1                                                            
         IC    R1,NAMLN                                                         
         SH    R1,=H'3'                                                         
         BM    GETA50                                                           
         MVC   BCACTNAM(0),NAMEREC                                              
         EX    R1,*-6                                                           
*                                                                               
         USING SPAELD,R2           *** ANALYSIS ACCOUNT ***                     
GETA30   CLI   0(R2),SPAELQ        X'2C'                                        
         BNE   GETA40                                                           
         CLI   SPATYPE,SPATANAL                                                 
         BNE   *+10                                                             
         MVC   BCACANAL,SPAAANAL                                                
*                                                                               
         USING RSTELD,R2           *** COSTING BYTE ***                         
GETA40   CLI   0(R2),RSTELQ        X'30'                                        
         BNE   *+10                                                             
         MVC   BCACCOST,RSTCOSTG                                                
*                                                                               
GETA50   SR    R1,R1                                                            
         IC    R1,1(R2)                                                         
         AR    R2,R1                                                            
         B     GETA10                                                           
*                                                                               
GETACCX  B     ROUTS1X                                                          
         EJECT                                                                  
***********************************************************************         
* BUILD 14 CONTRA ACCOUNT CODE AND HOURLY RATE METHOD TABLE           *         
*     R4 = A(BUFFER)                                                  *         
***********************************************************************         
         SPACE 1                                                                
         USING TMSD,R4                                                          
METHD    XC    METHSTAT,METHSTAT                                                
         MVC   SVTMSMOA,TMSMOA                                                  
         LA    RF,TTACKACT                                                      
         SR    R1,R1                                                            
         IC    R1,TT1RLNQ1                                                      
         SHI   R1,1                                                             
         MVC   AC1ROFFC(0),0(RF)   ISOLATE OFFICE                               
         EX    R1,*-6                                                           
         OC    AC1ROFFC,SPACES                                                  
         LA    RF,1(R1,RF)                                                      
*                                                                               
         IC    R1,TT1RLNQ2                                                      
         SR    RE,RE                                                            
         IC    RE,TT1RLNQ1                                                      
         SR    R1,RE                                                            
         SHI   R1,1                                                             
         MVC   AC1RDEPT(0),0(RF)   ISOLATE DEPT CODE                            
         EX    R1,*-6                                                           
         OC    AC1RDEPT,SPACES                                                  
         LA    RF,1(R1,RF)                                                      
*                                                                               
         IC    R1,TT1RLNQ3                                                      
         SR    RE,RE                                                            
         IC    RE,TT1RLNQ2                                                      
         SR    R1,RE                                                            
         SHI   R1,1                                                             
         MVC   AC1RSDPT(0),0(RF)   ISOLATE SUB DEPT CODE                        
         EX    R1,*-6                                                           
         OC    AC1RSDPT,SPACES                                                  
         LA    RF,1(R1,RF)                                                      
*                                                                               
         IC    R1,TT1RLNQ4                                                      
         SR    RE,RE                                                            
         IC    RE,TT1RLNQ3                                                      
         SR    R1,RE                                                            
         SHI   R1,1                                                             
         MVC   AC1RPERS(0),0(RF)   ISOLATE PERSON CODE                          
         EX    R1,*-6                                                           
         OC    AC1RPERS,SPACES                                                  
         TM    TTSTAT1,TTSTEXTD    BRANDOCEAN TIME                              
         BZ    METHD02             NO                                           
         TM    TTSTAT1,TTSTEXEC    YES - GET DETAILS FROM PREV OVERLAY          
         BZ    *+8                                                              
         OI    METHSTAT,METSPEXC   YES - SET STATUS THAT THEY ARE               
         MVC   BC142NME,TT142NME   SAVE NAME FOR 14 ACCOUNT LEVEL 2             
         MVC   BC143NME,TT143NME   SAVE NAME FOR 14 ACCOUNT LEVEL 3             
         MVC   BCACCOST,TT1RCOST                                                
         CLC   BCACCOST,SPACES                                                  
         BH    METHD16                                                          
         OI    METHSTAT,METSNANL   SET NO ANALYSIS FOUND FROM 1R                
*                                  USE DEFAULT FROM PROFILE RECORD              
         B     METHD16                                                          
*                                                                               
         USING PERRECD,R6                                                       
METHD02  LA    R6,KEY              READ PERSON RECORD                           
         MVC   KEY,SPACES                                                       
         MVI   PERKTYP,PERKTYPQ    X'0F'                                        
         MVC   PERKCPY,TTACKCPY    COMPANY CODE                                 
         MVC   PERKCODE,AC1RPERS   PERSON CODE                                  
         GOTO1 TTDMGR,DMCB,(0,DMREAD),ACCDIR,KEY,KEY,0                          
         CLI   DMCB+8,0            ANY DATAMANAGER ERRORS                       
         BE    *+6                 NO                                           
         DC    H'0'                YES - DIE                                    
         LA    R3,PERKDA                                                        
         GOTO1 TTDMGR,DMCB,GETREC,ACCMST,(R3),TRNIO,WORK                        
         CLI   DMCB+8,0            ANY DATAMANAGER ERRORS                       
         BE    *+6                 NO                                           
         DC    H'0'                YES - DIE                                    
         LA    R6,TRNIO                                                         
         LA    R3,PERRFST          POINT R3 TO FIRST ELEMENT OF RECORD          
         XR    R0,R0                                                            
         USING EMPELD,R3                                                        
METHD04  CLI   EMPEL,EMPELQ        FIND EMPLOYEE HISTORY ELEMENT                
         BE    METHD06                                                          
         CLI   EMPEL,0             EOR                                          
         BE    METHD08             GET NEXT RECORD                              
         IC    R0,EMPLN            INCREMENT TO NEXT ELEMENT                    
         AR    R3,R0                                                            
         B     METHD04             BRANCH BACK TO ELEMENT READ                  
*                                                                               
METHD06  TM    EMPSTAT,EMPSEXEC    IS THIS PERSON AN EXECUTIVE                  
         BZ    METHD08             NO                                           
         OI    METHSTAT,METSPEXC   YES - SET STATUS THAT THEY ARE               
* BUILD 14 CONTRA                                                               
METHD08  LA    R0,4                                                             
         LA    R3,TT1RLNQ4                                                      
METHD10  MVC   BCACTCDE,SPACES                                                  
         XR    RF,RF                                                            
         IC    RF,0(R3)                                                         
         AHI   RF,1                                                             
         MVC   BCACTCDE(0),TTACKUL VALIDATE 1R ACCOUNT                          
         EX    RF,*-6                                                           
         GOTO1 AGETACC,BCACTCDE                                                 
         CH    R0,=H'2'            HAVE WE GOT DOWN TO LEVEL 2                  
         BNE   *+10                NO                                           
         MVC   BC142NME,BCACTNAM   SAVE NAME FOR 14 ACCOUNT                     
         CH    R0,=H'3'            HAVE WE GOT DOWN TO LEVEL 3                  
         BNE   *+10                NO                                           
         MVC   BC143NME,BCACTNAM   SAVE NAME FOR 14 ACCOUNT                     
         CLC   BCACCOST,SPACES                                                  
         BH    METHD12                                                          
         BCTR  R3,0                                                             
         BCT   R0,METHD10                                                       
         OI    METHSTAT,METSNANL   SET NO ANALYSIS FOUND FROM 1R                
*                                  USE DEFAULT FROM PROFILE RECORD              
                                                                                
METHD12  CH    R0,=H'2'            HAVE WE GOT ACCOUNT NAME                     
         BNH   METHD16             YES                                          
         CH    R0,=H'4'            HAVE WE GOT NEITHER LEVEL 2 AND 3            
         BNE   METHD14             NO - WE HAVE LEVEL 3                         
         XR    RF,RF               YES - GET LEVEL 3 FIRST                      
         IC    RF,TT1RLNQ3                                                      
         AHI   RF,1                                                             
         MVC   BCACTCDE(0),TTACKUL VALIDATE 1R ACCOUNT                          
         EX    RF,*-6                                                           
         GOTO1 AGETACC,BCACTCDE                                                 
         MVC   BC143NME,BCACTNAM   SAVE NAME FOR 14 ACCOUNT LEVEL 3             
METHD14  XR    RF,RF                                                            
         IC    RF,TT1RLNQ2                                                      
         AHI   RF,1                                                             
         MVC   BCACTCDE(0),TTACKUL VALIDATE 1R ACCOUNT                          
         EX    RF,*-6                                                           
         GOTO1 AGETACC,BCACTCDE                                                 
         MVC   BC142NME,BCACTNAM   SAVE NAME FOR 14 ACCOUNT LEVEL 2             
METHD16  MVC   BC142ACT,SPACES                                                  
         MVC   BC142ACT(L'ACTKUNT+L'ACTKLDG),=C'14'                             
         MVC   BC142ACT+L'ACTKUNT+L'ACTKLDG(L'BCACCOST),BCACCOST                
         XR    RF,RF                                                            
         IC    RF,TT1RLNQ2                                                      
         BCTR  RF,0                                                             
         MVC   BC142ACT+L'ACTKUNT+L'ACTKLDG+L'BCACCOST(0),TTACKUL+L'ACTX        
               KUNT+L'ACTKLDG                                                   
         EX    RF,*-6                                                           
         MVC   BC143ACT,SPACES                                                  
         MVC   BC143ACT(L'ACTKUNT+L'ACTKLDG),=C'14'                             
         MVC   BC143ACT+L'ACTKUNT+L'ACTKLDG(L'BCACCOST),BCACCOST                
         XR    RF,RF                                                            
         IC    RF,TT1RLNQ3                                                      
         BCTR  RF,0                                                             
         MVC   BC143ACT+L'ACTKUNT+L'ACTKLDG+L'BCACCOST(0),TTACKUL+L'ACTX        
               KUNT+L'ACTKLDG                                                   
         EX    RF,*-6                                                           
* BUILD METHOD BASED TABLE                                                      
         L     R0,AMETHTAB                                                      
         LH    R1,=Y(L'METHTAB)                                                 
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
         USING METHTABD,R5                                                      
         L     R5,AMETHTAB         R5=A(METH TABLE)                             
         USING CAPRECD,R6                                                       
         LA    R6,KEY              READ COST ALLOCATION PROFILE RECORD          
         MVC   KEY,SPACES                                                       
         MVI   CAPKTYP,CAPKTYPQ    X'3E'                                        
         MVI   CAPKSUB,CAPKSUBQ    X'09'                                        
         MVC   CAPKCPY,TTACKCPY    COMPANY CODE                                 
         MVI   CAPKMTHD,C'1'       METHOD ONE                                   
         MVC   KEYSAVE,KEY                                                      
         GOTO1 TTDMGR,DMCB,(0,DMRDHI),ACCDIR,KEY,KEY,0                          
         B     METHD20                                                          
*                                                                               
METHD18  LA    R6,KEY                                                           
         GOTO1 TTDMGR,DMCB,(0,DMRSEQ),ACCDIR,KEY,KEY,0                          
METHD20  CLC   CAPKEY(CAPKMIN-L'CAPKMTHD),KEYSAVE                               
         BE    METHD22             SAME COMPANY SO LOOK FOR METHOD              
         L     R5,AMETHTAB         R5=A(METH TABLE)                             
         OC    METHNUM,METHNUM     HAVE WE GOT METHOD                           
         BZ    METHDX              NO                                           
         B     METHD48             YES                                          
*                                                                               
METHD22  OC    METHNUM,METHNUM                                                  
         BZ    METHD24                                                          
         CLC   METHNUM,CAPKMTHD    IS IT THE SAME METHOD AS BEFORE              
         BE    METHD24             YES                                          
         LA    R5,METHDL(R5)       NO - BUMPED TABLE TO NEXT ENTRY              
                                                                                
METHD24  CLC   CAPKOFC,SPACES                                                   
         BH    METHD26                                                          
         OI    METHSTAT,METSHLVL   HIGH LEVEL METHOD RECORD                     
         B     METHD30                                                          
METHD26  CLC   CAPKOFC,AC1ROFFC                                                 
         BNE   METHD18                                                          
         CLC   CAPKDPT,SPACES                                                   
         BNH   METHD28                                                          
         CLC   CAPKDPT,AC1RDEPT                                                 
         BNE   METHD18                                                          
         CLC   CAPKSDT,SPACES                                                   
         BNH   METHD28                                                          
         CLC   CAPKDPT,AC1RSDPT                                                 
         BNE   METHD18                                                          
         CLC   CAPKPER,SPACES                                                   
         BNH   METHD28                                                          
         CLC   CAPKPER,AC1RPERS                                                 
         BNE   METHD18                                                          
METHD28  NI    METHSTAT,X'FF'-METSHLVL                                          
                                                                                
METHD30  LA    R3,CAPKDA                                                        
         GOTO1 TTDMGR,DMCB,GETREC,ACCMST,(R3),TRNIO,WORK                        
         CLI   DMCB+8,0            ANY DATAMANAGER ERRORS                       
         BE    *+6                 NO                                           
         DC    H'0'                YES - DIE                                    
         LA    R6,TRNIO                                                         
         LA    R3,CAPRFST          POINT R3 TO FIRST ELEMENT OF RECORD          
         TM    METHSTAT,METSHLVL   ONLY FIND CONTRA AT HIGH LEVEL REC           
         BZ    METHD32                                                          
         MVI   METHSTR,METH2LVL    STRUCTURE OF 14 AS 2 LEVEL DEFAULT           
         MVC   METPERCT,=X'00001000000C' 100% IS THE DEFAULT ALLOCATION         
         MVI   METHANAL,C'Z'       Z IS THE STANDARD DEFAULT                    
METHD32  XR    R0,R0                                                            
         USING OPDELD,R3                                                        
METHD34  CLI   OPDEL,OPDELQ        FIND OPTION DATA ELEMENTS                    
         BE    METHD38                                                          
         CLI   OPDEL,0             EOR                                          
         BE    METHD18             GET NEXT RECORD                              
METHD36  IC    R0,OPDLN            INCREMENT TO NEXT ELEMENT                    
         AR    R3,R0                                                            
         B     METHD34             BRANCH BACK TO ELEMENT READ                  
*                                                                               
METHD38  CLI   OPDNUM,X'1C'        ONLINE UPDATE FOR ALLOCATION                 
         BNE   METHD40                                                          
         CLI   OPDDATA,C'Y'        YES                                          
         BE    *+14                SET METHOD UP IN TABLE                       
         XC    METHNUM,METHNUM     CLEAR METHOD NUMBER AS NOT VALID             
         B     *+10                                                             
         MVC   METHNUM,CAPKMTHD    SAVE METHOD NUMBER IN TABLE                  
         B     METHD18             READ NEXT RECORD                             
*                                                                               
METHD40  CLI   OPDNUM,X'0C'        PERCENTAGE OF SALARY TO ALLOCATE             
         BNE   METHD42             NO                                           
         ZAP   METPERCT,OPDDATA(4) SAVE PERCENTAGE OF SAL TO ALLOCATE           
         CP    METPERCT,PZERO      IF ZERO NOT INTERESTED IN THIS               
         BNE   METHD36             METHOD OTHERWISE READ OTHER PROFILES         
         XC    METHNUM,METHNUM     CLEAR METHOD NUMBER AS NOT VALID             
         B     METHD18             GET NEXT SEQUENTIAL RECORD                   
*                                                                               
METHD42  TM    METHSTAT,METSPEXC   IS THIS PERSON AN EXECUTIVE                  
         BZ    METHD44             NO - NO NEED FOR TEST                        
         CLI   OPDNUM,X'0A'        INCLUDE EXECUITVE IN ALLOCATION OPT          
         BNE   METHD44             NO - SEE IF OTHER TYPE                       
         CLI   OPDDATA,C'N'        DO WE INCLUDE                                
         BNE   METHD36             YES - GET NEXT ELEMENT                       
         XC    METHNUM,METHNUM     NO - CLEAR METHOD NUMBER                     
         B     METHD18             GET NEXT SEQUENTIAL RECORD                   
*                                                                               
METHD44  CLI   OPDNUM,X'05'        STRUCTURE OF 14 ACCOUNT OPTION ELE           
         BNE   METHD46             NO - GET NEXT ELEMENT                        
         CLI   OPDDATA,C'3'        IF IT 3 LEVEL OR 2                           
         BNE   METHD36             NO - IT'S 2 AS DEFAULT                       
         MVI   METHSTR,METH3LVL    SET STRUTURE OF 14 AS 3 LEVEL                
         B     METHD36             BRANCH BACK TO READ REST OF ELEMENTS         
*                                                                               
METHD46  CLI   OPDNUM,X'18'        GET DEFAULT ANALYSIS CODE                    
         BNE   METHD36             NO - GET NEXT ELEMENT                        
         MVC   METHANAL,OPDDATA    SAVE THIS DEFAULT                            
         B     METHD36             BACK TO READ REST OF ELEMENTS                
*                                                                               
         USING PHIRECD,R6                                                       
METHD48  LA    R6,KEY              READ PAYROLL HISTORY RECORD                  
         XC    KEY,KEY                                                          
         MVC   PHIKEY,SPACES                                                    
         MVI   PHIKTYP,PHIKTYPQ    X'3E'                                        
         MVI   PHIKSUB,PHIKSUBQ    X'05'                                        
         MVC   PHIKCPY,TTACKCPY    COMPANY CODE                                 
         LA    RF,TTACKACT                                                      
         SR    R1,R1                                                            
         IC    R1,TT1RLNQ1                                                      
         SHI   R1,1                                                             
         MVC   PHIKOFC(0),0(RF)    ISOLATE OFF/DPT/SUB                          
         EX    R1,*-6                                                           
         OC    PHIKOFC,SPACES                                                   
         LA    RF,1(R1,RF)                                                      
*                                                                               
         IC    R1,TT1RLNQ2                                                      
         SR    RE,RE                                                            
         IC    RE,TT1RLNQ1                                                      
         SR    R1,RE                                                            
         SHI   R1,1                                                             
         MVC   PHIKDPT(0),0(RF)    ISOLATE DEPT CODE                            
         EX    R1,*-6                                                           
         OC    PHIKDPT,SPACES                                                   
         LA    RF,1(R1,RF)                                                      
*                                                                               
         IC    R1,TT1RLNQ3                                                      
         SR    RE,RE                                                            
         IC    RE,TT1RLNQ2                                                      
         SR    R1,RE                                                            
         SHI   R1,1                                                             
         MVC   PHIKSBD(0),0(RF)    ISOLATE SUB DEPT CODE                        
         EX    R1,*-6                                                           
         OC    PHIKSBD,SPACES                                                   
         LA    RF,1(R1,RF)                                                      
*                                                                               
         IC    R1,TT1RLNQ4                                                      
         SR    RE,RE                                                            
         IC    RE,TT1RLNQ3                                                      
         SR    R1,RE                                                            
         SHI   R1,1                                                             
         MVC   PHIKPER(0),0(RF)    ISOLATE SUB DEPT CODE                        
         EX    R1,*-6                                                           
         OC    PHIKPER,SPACES                                                   
         LA    RF,1(R1,RF)                                                      
*                                                                               
         ICM   R1,3,TMSMOA                                                      
         LNR   R1,R1                                                            
         STCM  R1,3,PHIKMOA        MOA 2'S COMPLEMENT                           
         XC    PHIKSEQ,PHIKSEQ     CLEAR SEQUENCE NUMBER                        
         MVC   KEYSAVE,KEY                                                      
         GOTO1 TTDMGR,DMCB,(0,DMREAD),ACCDIR,KEY,KEY,0                          
         CLI   DMCB+8,0            ANY DATAMANAGER ERRORS                       
         BE    METHD50             NO - CONTINUE                                
         OI    TTRETURN,TTRHSMSG   SET RETURN BYTE                              
         TM    TTSTAT1,TTSTEXTD    IF BRANDOCEAN                                
         BNZ   ROUTS1H             RETURN WITH ERROR                            
         DC    H'0'                NO HISTORY MUST ERROR                        
*                                                                               
METHD50  LA    R3,PHIKDA                                                        
         GOTO1 TTDMGR,DMCB,GETREC,ACCMST,(R3),TRNIO,WORK                        
         CLI   DMCB+8,0            ANY DATAMANAGER ERRORS                       
         BE    *+6                 NO                                           
         DC    H'0'                YES - DIE                                    
         L     R5,AMETHTAB         R5=A(METHOD TABLE)                           
         OC    METHNUM,METHNUM     HAVE WE ANY METHODS                          
         BNZ   *+6                 YES                                          
         DC    H'0'                DIE IF NOT                                   
METHD52  ZAP   METSRATE,PZERO      ZERO ACCUMULATORS                            
         ZAP   METBRATE,PZERO      ZERO                                         
         ZAP   METPRATE,PZERO      ZERO                                         
         ZAP   METTRATE,PZERO      ZERO                                         
         LA    R6,TRNIO            R6 POINTS TO ACCMST RECORD                   
         LA    R3,PHIRFST          R3 POINTS FIRST ELEMENT OF RECORD            
         USING PDEELD,R3                                                        
METHD54  CLI   PDEEL,0             END OF RECORD                                
         BE    METHD76             YES - SET NEXT METHOD AND READ AGAIN         
         CLI   PDEEL,PDEELQ        PAYROLL DETAIL ELEMENT                       
         BE    METHD58             YES                                          
METHD56  IC    R0,PDELN            R0=LENGTH OF ELEMENT                         
         AR    R3,R0                                                            
         B     METHD54             BUMP TO NEXT PAYROLL DETAIL ELEMENT          
*                                                                               
METHD58  CLC   TMSMOA,PDEDTE       IS PAYDATE IN SAME MONTH                     
         BNE   METHD56             NO - NOT INTERESTED IN THIS                  
         TM    PDESTAT2,PDESHRTE   ONLY INTERESTED IN HOURLY RATE ELS           
         BZ    METHD56                                                          
*                                                                               
         TM    METHSTAT,METSRCAH   TEST WHETHER READ COST ALLOC HISTORY         
         BNZ   METHD64             YES DON'T NEED TO RE-READ                    
         USING CAHRECD,R6                                                       
METHD60  LA    R6,KEY              READ COST ALLOCATION HISTORY RECORD          
         MVC   KEY,SPACES                                                       
         MVI   CAHKTYP,CAHKTYPQ    X'3E'                                        
         MVI   CAHKSUB,CAHKSUBQ    X'01'                                        
         MVC   CAHKCPY,TTACKCPY    COMPANY CODE                                 
         MVC   CAHKMTHD,METHNUM    METHOD                                       
         XC    CAHKOFC,CAHKOFC     CLEAR OFFICE FIELD                           
         MVC   KEYSAVE,KEY                                                      
         GOTO1 TTDMGR,DMCB,(0,DMRDHI),ACCDIR,KEY,KEY,0                          
*                                                                               
         CLC   CAHKEY,KEYSAVE                                                   
         BE    METHD62             SAME COMPANY SO LOOK FOR METHOD              
         DC    H'0'                                                             
*                                                                               
METHD62  LA    R2,CAHKDA                                                        
         L     R6,ATRNIO2                                                       
         GOTO1 TTDMGR,DMCB,GETREC,ACCMST,(R2),(R6),WORK                         
         CLI   DMCB+8,0            ANY DATAMANAGER ERRORS                       
         BE    *+6                 NO                                           
         DC    H'0'                DIE IF THERE ARE                             
METHD64  L     R6,ATRNIO2          R6=A(COST ALLOCATION HISTORY REC)            
         LA    R2,CAHRFST          R2=A(FIRST ELEMENT ON RECORD)                
         XR    R0,R0               CLEAR R0                                     
         USING PATELD,R2                                                        
METHD66  CLI   PATEL,0             END OF RECORD                                
         BE    METHD56             GO TO NEXT ELEMENT ON PHIRECD RECORD         
         CLI   PATEL,PATELQ        PAYROLL TYPE ELEMENT                         
         BE    METHD70             YES                                          
METHD68  IC    R0,PATLN                                                         
         AR    R2,R0                                                            
         B     METHD66             BRANCH BACK TO READ REST CAHRED              
*                                                                               
METHD70  CLC   PDENUM,PATNUM       IS PAYROLL NUMBER EQUAL                      
         BNE   METHD68             NO LOOK AT NEXT PATEL                        
         CLI   PATTYPE,PATTSAL     IS IT SALARY TYPE                            
         BNE   METHD72             NO CHECK FOR OTHER TYPES                     
         AP    METSRATE,PDEAMT     ADD UP SALARY TYPES FOR METHOD               
         AP    METSRATE,PDEADJ                                                  
         AP    METTRATE,METSRATE                                                
         B     METHD56             BRANCH BACK TO NEXT PDEEL                    
*                                                                               
METHD72  CLI   PATTYPE,PATTBEN     IS IT BENEFIT TYPE                           
         BNE   METHD74             NO CHECK FOR OTHER TYPES                     
         AP    METBRATE,PDEAMT     ADD UP BENEFIT TYPES FOR METHOD              
         AP    METBRATE,PDEADJ                                                  
         AP    METTRATE,METBRATE                                                
         B     METHD56             BRANCH BACK TO NEXT PDEEL                    
*                                                                               
METHD74  CLI   PATTYPE,PATTPEN     IS IT PENSION SLARY TYPE                     
         BNE   METHD56             NO CHECK FOR OTHER TYPES                     
         AP    METPRATE,PDEAMT     ADD UP PENSION TYPES FOR METHOD              
         AP    METPRATE,PDEADJ                                                  
         AP    METTRATE,METPRATE                                                
         B     METHD56             BRANCH BACK TO NEXT PDEEL                    
*                                                                               
METHD76  NI    METHSTAT,X'FF'-METSRCAH                                          
         LA    R5,METHDL(R5)                                                    
         OC    METHNUM,METHNUM     ANYMORE METHOD TO READ                       
         BNZ   METHD52             YES - GO BACK AND READ THIS METHOD           
         DROP  R2,R3,R6                                                         
*                                                                               
METHDX   B     ROUTS1E                                                          
         DROP  R5,R4                                                            
         EJECT                                                                  
***********************************************************************         
* ADDTRNS INTERFACE                                                   *         
***********************************************************************         
         SPACE 1                                                                
GOADDTRN MVC   BCACTNAM,0(R1)      R1=A(CONTRA ACCOUNT NAME)                    
*                                                                               
         LA    RE,TRNBLK           CLEAR ADDTRN BLOCK                           
         LA    RF,TRNBLKL                                                       
         SR    R0,R0                                                            
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
         MVC   TRNGLBLK,ATRNGLBK   RESET GL BLOCK                               
         MVC   TRNGLIND,BCTRNIND   RESET GL INDICATOR                           
*                                                                               
         OI    TRNINDS,TRNICONV    NEW FILE FORMAT                              
         TM    STATUS,STALAST      ARE WE DOING THE LAST UPDATE?                
         BNO   *+12                                                             
         OI    TRNINDS,TRNILAST    SET IT AS THE LAST TIME                      
         OI    TRNINDS2,TRNIUPDG                                                
*        OI    TRNINDS1,TRNIDNMR                                                
         OI    TRNINDS2,TRNIADDG                                                
         TM    STATUS,STBUCKET     ONLY UPDATING BUCKETS?                       
         BNO   *+8                                                              
         OI    TRNINDS2,TRNIUBKO+TRNIUOFC+TRNIADDG                              
         TM    STATUS,STBUCKET     ONLY UPDATING BUCKETS?                       
         BO    *+8                                                              
         MVI   PSTSTAT,PSTM        SHOW THAT POSTINGS HAVE BEEN MADE            
         MVC   TRNCOMF,TTCFACS     A(COMFACS)                                   
         LA    R1,TRNIO                                                         
         ST    R1,TRNREC           A(TRANS RECORD)                              
         MVC   TRNBMOS,BCMOA       MOS                                          
         MVC   TRNBSEQN,=X'0000'                                                
         MVC   TRNPUSER,TTUSERID                                                
         MVC   TRNCACNM,BCACTNAM                                                
         MVC   TRNCPYS1,TTCPYST1   COMPANY STATUS BYTE #1                       
         MVC   TRNCPYS2,TTCPYST2   COMPANY STATUS BYTE #2                       
         MVC   TRNCPYS3,TTCPYST3   COMPANY STATUS BYTE #3                       
         MVC   TRNCPYS4,TTCPYST4   COMPANY STATUS BYTE #4                       
         MVC   TRNCPYS5,TTCPYST5   COMPANY STATUS BYTE #5                       
         MVC   TRNCPYS6,TTCPYST6   COMPANY STATUS BYTE #6                       
         MVC   TRNCPYS7,TTCPYST7   COMPANY STATUS BYTE #7                       
         MVC   TRNCPYS8,TTCPYST8   COMPANY STATUS BYTE #9                       
         MVC   TRNCPYS9,TTCPYST9   COMPANY STATUS BYTE #9                       
         MVC   TRNCPYSA,TTCPYSTA   COMPANY STATUS BYTE #10                      
*&&US                                                                           
*        MVC   TRNCPYSB,TTCPYSTB   COMPANY STATUS BYTE #11                      
         MVC   TRNGLMOA,TTCPYGLM   COMPANY GL MOA                               
*&&                                                                             
*&&UK                                                                           
         MVC   TRNCCCUR,TTCURCD2   PRIMARY CURRENCY                             
         MVC   TRNCCURS,TTCURCD3   SECONDARY CURRENCY                           
*&&                                                                             
         LA    RF,TTPLAREA                                                      
         STCM  RF,15,TRNPAL        SAVE PROFIT AND LOSS AREA                    
         L     RF,TTCFACS          COMPRESSED EFFECTIVE DATE                    
         L     RF,CDATCON-COMFACSD(RF)                                          
         GOTO1 (RF),DMCB,(1,TTYYMMDD),(2,TRNEFDT)                               
*                                                                               
         L     RF,TTCFACS                                                       
         L     RF,CGETFACT-COMFACSD(RF)                                         
         GOTO1 (RF),DMCB,(X'80',0),F#UTLD                                       
         L     R1,0(R1)                                                         
         USING F@UTLD,R1                                                        
         TM    F@TTEST,X'80'       TEST UPD=NO                                  
         BNO   *+8                                                              
         OI    TRNINDS,TRNIWRNO    SET WRITE=NO                                 
*                                                                               
         GOTO1 (RF),DMCB,(X'80',0),F#SELISD                                     
         L     R1,0(R1)                                                         
         USING F@SELISD,R1                                                      
         TM    F@SEIND,X'04'       TEST READ ONLY                               
         BNO   *+8                                                              
         OI    TRNINDS,TRNIWRNO    SET WRITE=NO                                 
         DROP  R1                                                               
*                                                                               
         GOTO1 TTADDTRN,TRNBLK                                                  
         CLI   TRNERRS,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   ATRNGLBK,TRNGLBLK   SAVE OFF GL BLOCK                            
         MVC   BCTRNIND,TRNGLIND   SAVE OFF GL INDICATOR                        
*        GOTO1 TTDMGR,DMCB,=C'DMUNLK',=C'ACCDIR'                                
*        GOTO1 TTDMGR,DMCB,=C'DMUNLK',=C'ACCMST'                                
         B     ROUTS1X                                                          
         EJECT                                                                  
***********************************************************************         
* ADD ELEMENT IN ELEM TO RECORD IN TRNIO                              *         
***********************************************************************         
         SPACE 1                                                                
ADDELEM  L     RF,TTCFACS                                                       
         L     RF,CHELLO-COMFACSD(RF)                                           
         GOTO1 (RF),DMCB,(C'P',ACCMST),TRNIO,ELEM,(0,=C'ADD=END')               
         CLI   DMCB+12,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         B     ROUTS1X                                                          
         EJECT                                                                  
***********************************************************************         
* BUILD ARATE RECORD                                                  *         
***********************************************************************         
         SPACE 1                                                                
*                                  CLEAR RECORD AREA TO BINARY ZEROS            
BLDRATR  LA    RE,TRNIO            RECEIVING FIELD                              
         LH    RF,=S(L'TRNIO)      RECEIVING FIELD LENGTH                       
         SR    R1,R1                                                            
         SR    R0,R0                                                            
         MVCL  RE,R0                                                            
*                                                                               
         LA    R6,TRNIO                                                         
         USING CPRRECD,R6          COSTING PERSONAL RATES RECORD                
         MVC   CPRKEY,SPACES                                                    
         MVI   CPRKTYP,CPRKTYPQ    X'3E'                                        
         MVI   CPRKSUB,CPRKSUBQ    SUB RECORD X'07'                             
         MVC   CPRKCPY(L'TTACCODE),TTACCODE                                     
*                                                                               
         LH    RE,=Y(CPRRFST-CPRRECD+1)                                         
         STCM  RE,3,CPRRLEN        RECORD LENGTH                                
         B     ROUTS1X                                                          
         DROP  R6                                                               
         EJECT                                                                  
* *********************************************************************         
* ROUTINE TO ADD A PERSONAL HOURLY RATE ELEMENT                       *         
***********************************************************************         
         USING TMSD,R4             R4=A(CURRENT TABLE ENTRY)                    
         USING METHTABD,R5                                                      
         USING CPRRECD,R6                                                       
ADDRATEL LA    R6,TRNIO                                                         
         XC    ELEM,ELEM                                                        
         LA    R7,ELEM                                                          
         USING PHRELD,R7                                                        
         MVI   PHREL,PHRELQ        SET UP HOURLY RATE ELEMENT                   
         MVI   PHRLN,PHRLN1Q       LENGTH OF ELEMENT KEY INFO                   
         MVC   PHRPER,TMSMOA       MONTH OF SERVICE                             
         MVC   PHRMTH,METHNUM      METHOD OF ALLOCATION                         
         XC    PHRNUM,PHRNUM       NUMBER OF MINI ELEMENTS                      
*                                                                               
         L     RF,TTCFACS                                                       
         L     RF,CHELLO-COMFACSD(RF)                                           
         GOTO1 (RF),DMCB,(C'G',ACCMST),('PHRELQ',CPRRECD),             X        
               (L'PHRPER+L'PHRMTH,PHRPER)                                       
         CLI   DMCB+12,0                                                        
         BNE   ADDRA02             NO MATCHING HRLY RATE ELEMENT                
         L     R7,DMCB+12                                                       
         LTR   R7,R7                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         MVI   PHREL,DELELQ        MARK IT DELETED                              
         L     RF,TTCFACS                                                       
         L     RF,CHELLO-COMFACSD(RF)                                           
         GOTO1 (RF),DMCB,(C'D',ACCMST),('DELELQ',CPRRECD),0                     
*                                                                               
ADDRA02  LA    R7,ELEM             SET R4 TO "NEW" ELEMENT                      
         LA    R3,METEBCNT         NUMBER OF HRLY RATE BUCKETS                  
         LA    R2,METTRATE         TOTAL RATE BUCKET                            
ADDRA04  SR    RE,RE                                                            
         IC    RE,PHRLN                                                         
         LA    RE,PHRELD(RE)       POINT RE TO SUB ELEMENT                      
         USING PHRNTRY,RE                                                       
*                                                                               
ADDRA06  CH    R3,=Y(METEBCNT)     IS THIS THE "TOTAL" HRLY RATE?               
         BNE   ADDRA08             YES - PUT OUT EVEN WITH 0 HRLY RATE          
         MVI   PHRTYPE,PHRTTOT                                                  
         B     ADDRA10                                                          
ADDRA08  CP    0(L'METTRATE,R2),PZERO  HRLY RATE FOR THIS SAL TYPE ZERO         
         BE    ADDRA14             NO - CONTINUE                                
         STC   R3,PHRTYPE                                                       
         OI    PHRTYPE,X'F0'       CONVERT INTO DISPLAY NUMBER                  
ADDRA10  ZAP   DUB,0(L'METTRATE,R2) HOURLY RATE                                 
         MP    DUB,P100            DECIMAL PLACES                               
         CP    METPERCT,P1MILL     IF PERCENTAGE 100% NO AMENDING OF            
         BE    ADDRA12             RATES IS NECESSARY                           
         ZAP   PACK16,DUB          USE PACK 16 FIELD                            
         MP    PACK16,METPERCT     GET PERCENTAGE                               
         SRP   PACK16,64-6,5       SHIFT 6                                      
         ZAP   DUB,PACK16                                                       
ADDRA12  ZAP   PHRRATE,DUB                                                      
         DROP  RE                                                               
         SR    RE,RE               INCREMENT ELEMENT LENGTH                     
         IC    RE,PHRLN                                                         
         AH    RE,=Y(PHRLN2Q)                                                   
         STC   RE,PHRLN                                                         
         SR    RE,RE               INCREMENT NUMBER OF SUB ELEMENTS             
         IC    RE,PHRNUM                                                        
         LA    RE,1(RE)                                                         
         STC   RE,PHRNUM                                                        
ADDRA14  LA    R2,L'METTRATE(R2)                                                
         BCT   R3,ADDRA04                                                       
*                                                                               
ADDRA16  LA    R1,CPRRFST-CPRRECD(R6)            INT TO 1ST EL                  
         SR    RE,RE               RE=A(OLDEST BUCKET ELEMENT)                  
ADDRA18  CLI   0(R1),0             TEST E-O-R                                   
         BE    ADDRA22                                                          
         CLI   0(R1),PHRELQ        TEST HOURLY RATE ELEMENT                     
         BE    ADDRA20                                                          
         SR    R0,R0                                                            
         IC    R0,PHRLN            BUMP TO NEXT ELEMENT                         
         AR    R1,R0                                                            
         B     ADDRA18                                                          
ADDRA20  LR    RE,R1               SAVE ADDR OF 1ST (OLDEST ONE)                
ADDRA22  SR    R1,R1                                                            
         ICM   R1,3,CPRRLEN        CURRENT LENGTH OF RECORD                     
         SR    RF,RF                                                            
         IC    RF,PHRLN            NEW ELEMENT TO ADD                           
         AR    RF,R1               ADD TOGETHER                                 
         C     RF,=A(RECLNMAX)                                                  
         BNH   ADDRA24             RECORD TOO BIG DELETE OLDEST ELEMENT         
         LTR   R7,RE               POINT TO OLDEST (FIRST) BUCKET               
         BNZ   *+6                                                              
         DC    H'0'                                                             
         MVI   PHREL,DELELQ                                                     
         L     RF,TTCFACS                                                       
         L     RF,CHELLO-COMFACSD(RF)                                           
         GOTO1 (RF),DMCB,(C'D',ACCMST),('DELELQ',CPRRECD),0                     
         LA    R7,ELEM             RESET R7 TO "NEW" ELEMENT                    
         B     ADDRA16             TRY AGAIN TO FIT IT                          
*                                  ADD THE NEW ELEMENT                          
ADDRA24  DS    0H                                                               
         L     RF,TTCFACS                                                       
         L     RF,CHELLO-COMFACSD(RF)                                           
         GOTO1 (RF),DMCB,(C'P',ACCMST),CPRRECD,ELEM,0                           
         CLI   DMCB+12,0                                                        
         BE    *+6                                                              
         DC    H'0'                CAN'T ADD THE ELEMENT                        
         B     ROUTS1X                                                          
         DROP  R4,R5,R6,R7                                                      
         EJECT                                                                  
***********************************************************************         
* WRITE BACK A RECORD OR ADD NEW RECORD                               *         
***********************************************************************         
WRITE    LA    R6,TRNIO            ADDR OF RECORD TO ADD OR WRITE BACK          
*                                                                               
         USING CPRRECD,R4                                                       
         LA    R4,KEY              ADDR OF DIRECTORY IO AREA                    
         MVC   CPRKEY,SPACES                                                    
         MVC   CPRKEY,0(R6)                                                     
         MVC   COMMAND,ADDREC            SET SUBSEQUENT COMMAND TO ADD          
         GOTO1 TTDMGR,DMCB,(X'88',DMREAD),ACCDIR,KEY,KEY                        
         TM    8(R1),X'10'               DID I FIND THE RECORD?                 
         BO    WRI02                     NO-THEN ADD IT                         
         MVC   DA,CPRKDA                 DISK ADDRESS                           
         L     R7,ATRNIO2                                                       
         GOTO1 TTDMGR,DMCB,(X'88',GETREC),ACCMST,DA,(R7),WORK                   
         CLI   8(R1),0                   DID I FIND THE RECORD?                 
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   COMMAND,PUTREC            YES - WRITE IT BACK                    
*                                                                               
WRI02    DS    0H                                                               
         GOTO1 TTDMGR,DMCB,COMMAND,ACCMST,DA,TRNIO,WORK                         
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                      DIED WRITING A RECORD                  
*                                                                               
         CLC   COMMAND,PUTREC            WERE WE PUTTING A RECORD               
         BNE   WRIXX                     NO- SKIP CHECK FOR DEL DIR             
         LA    R4,KEY                    ADDR OF DIRECTORY IO AREA              
         TM    CPRKSTA,X'80'             IS IT MARKED DELETED?                  
         BNO   WRIXX                                                            
         NI    CPRKSTA,X'FF'-X'80'       UNDELETE THE DIRECTORY RECORD          
         GOTO1 TTDMGR,DMCB,DMWRT,ACCDIR,KEY,KEY                                 
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                      DIED WRITING A RECORD                  
*                                                                               
WRIXX    DS    0H                                                               
         B     ROUTS1X                                                          
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* CLEAR TRANSACTION RECORD AREA                                       *         
***********************************************************************         
         SPACE 1                                                                
XCIO     LR    R0,R1                                                            
         LH    R1,=Y(L'TRNIO)                                                   
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
         B     ROUTS1X                                                          
         EJECT                                                                  
***********************************************************************         
* UPDATE SJ/SI/12 POSTINGS APPROVAL STATUS                            *         
***********************************************************************         
         USING TRNRECD,R2                                                       
         USING TMSD,R4             R4=A(CURRENT TABLE ENTRY)                    
UPDATE   LA    R2,KEY              ADDR OF DIRECTORY IO AREA                    
         MVC   TRNKEY,SPACES                                                    
         MVC   TRNKCPY,TTACKCPY                                                 
         MVC   TRNKULA,BCSJACCT    SJ ACCOUNT                                   
         MVC   TRNKWORK,TMSTSK     TSK CODE                                     
         MVC   TRNKCCPY,TTACKCPY                                                
         MVC   TRNKULC,BC1RACCT    1R ACCOUNT                                   
         MVC   TRNKDATE,TTYYMMDD                                                
         MVC   TRNKREF,BCREF                                                    
         XC    TRNKSBR,TRNKSBR                                                  
         GOTO1 AUPDREC                                                          
                                                                                
         USING TRNRECD,R2                                                       
         LA    R2,KEY                                                           
         MVC   TRNKEY,SPACES                                                    
         MVC   TRNKCPY,TTACKCPY                                                 
         MVC   TRNKULA,BCSIACCT    INCOME ACCOUNT                               
         MVC   TRNKOFF,TMSOFFC     CLIENT OFFICE                                
         TM    TTCPYST4,CPYSOFF2                                                
         BO    *+10                                                             
         MVC   TRNKOFF,SPACES      OLD OFFICES = NO OFFICE IN KEY               
         MVC   TRNKCCPY,TTACKCPY                                                
         MVC   TRNKULC,BCSJACCT    SJ ACCOUNT                                   
         MVC   TRNKDATE,TTYYMMDD                                                
         MVC   TRNKREF,BCREF                                                    
         XC    TRNKSBR,TRNKSBR                                                  
         GOTO1 AUPDREC                                                          
         CLC   BCSIACCT(2),INCLDG  ONLY MAKE COSTING POSTINGS IF                
         BNE   UPDATEX             POSTING TO SI - SKIP FOR U/L=SK              
*                                                                               
         USING TRNRECD,R2                                                       
         LA    R2,KEY                                                           
         MVC   TRNKEY,SPACES                                                    
         MVC   TRNKCPY,TTACKCPY                                                 
         MVC   TRNKULA,BCCNTRA     COSTING ACCOUNT                              
         MVC   TRNKOFF,TMSOFFC     CLIENT OFFICE                                
         TM    TTCPYST4,CPYSOFF2                                                
         BO    *+10                                                             
         MVC   TRNKOFF,SPACES      OLD OFFICES = NO OFFICE IN KEY               
         MVC   TRNKCCPY,TTACKCPY                                                
         MVC   TRNKULC,BC12ACCT    ANALYSIS ACCOUNT                             
         MVC   TRNKDATE,TTYYMMDD                                                
         MVC   TRNKREF,BCREF                                                    
         XC    TRNKSBR,TRNKSBR                                                  
         GOTO1 AUPDREC                                                          
UPDATEX  B     ROUTS1X                                                          
         DROP  R2,R4                                                            
         EJECT                                                                  
***********************************************************************         
* READING KEY AND UPDATING RECORDS                                    *         
***********************************************************************         
         SPACE 1                                                                
         USING TRNRECD,R2                                                       
UPDREC   LA    R2,KEY                                                           
         MVC   SVKEY,KEY                                                        
         GOTO1 TTDMGR,DMCB,(X'80',DMRDHI),ACCDIR,KEY,KEY                        
         B     UPR20                                                            
UPR10    LA    R2,KEY                                                           
         GOTO1 TTDMGR,DMCB,(0,DMRSEQ),ACCDIR,KEY,KEY,0                          
*                                                                               
UPR20    CLC   SVKEY(TRNKSBR-TRNRECD),KEY                                       
         BNE   UPR30                                                            
         MVC   DA,TRNKDA                 DISK ADDRESS                           
         GOTO1 TTDMGR,DMCB,(X'80',GETREC),ACCMST,DA,TRNIO,WORK                  
         CLI   8(R1),0                   DID I FIND THE RECORD?                 
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         GOTO1 AUPDEL60            UPDATE TRSEL                                 
*                                                                               
         GOTO1 TTDMGR,DMCB,PUTREC,ACCMST,DA,TRNIO,WORK                          
         CLI   8(R1),0                                                          
         BE    UPR10                                                            
         DC    H'0'                      DIED WRITING A RECORD                  
UPR30    B     ROUTS1X                                                          
         EJECT                                                                  
***********************************************************************         
* UPDATE TRANSACTION STATUS ELEMENT WITH APPROVAL STATUS              *         
***********************************************************************         
         SPACE 1                                                                
         USING TMSD,R4             R4=A(CURRENT TABLE ENTRY)                    
         USING TRNRECD,R2                                                       
UPDEL60  LA    R2,TRNIO                                                         
         USING TRSELD,R3                                                        
         LA    R3,TRNRFST                                                       
         SR    R0,R0                                                            
U6002    CLI   TRSEL,0                                                          
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLI   TRSEL,TRSELQ                                                     
         BE    U6006                                                            
U6004    IC    R0,TRSLN                                                         
         AR    R3,R0                                                            
         B     U6002                                                            
*                                                                               
U6006    XC    TRSSTAT4,TRSSTAT4   CLEAR STATUS                                 
         TM    TTTSSTAT,TIMSFAPP   IS TIME FULLY APPROVED                       
         BNZ   UPDEL60X            YES - STATUS TO SET                          
         OC    TTTSSTAT,TTTSSTAT   ANY STATUS                                   
         BNZ   U6008               YES                                          
         MVI   TRSSTAT4,TRSSSAVT   NONE MUST BE SAVED                           
         B     UPDEL60X                                                         
U6008    TM    TTTSSTAT,TIMSMAAP   IS TIME LINE MANAGER APPROVED                
         BZ    *+8                 NO                                           
         OI    TRSSTAT4,TRSSMAAP   YES - SET STATUS ACCORDINGLY                 
         TM    TTTSSTAT,TIMSREJE   IS TIME LINE MANAGER REJECTED                
         BZ    *+8                 NO                                           
         MVI   TRSSTAT4,TRSSREJT   YES - SET STATUS ACCORDINGLY                 
*                                                                               
         TM    TMSMTSS,TIMESAPR    TEST APPROVED                                
         BZ    *+8                                                              
         OI    TRSSTAT4,TRSSSJAT   SET FURTHER STATUS BYTE                      
         TM    TMSMTSS,TIMESREJ    TEST REJECTED                                
         BZ    *+8                                                              
         MVI   TRSSTAT4,TRSSREJT                                                
         OC    TRSSTAT4,TRSSTAT4                                                
         BNZ   U6010                                                            
         MVI   TRSSTAT4,TRSSSUBT   IF NEITHER, ASSUME SUBMITTED                 
         B     UPDEL60X                                                         
*                                                                               
U6010    TM   TRSSTAT4,TRSSSJAT+TRSSMAAP                                        
         BNO  UPDEL60X                                                          
         XC   TRSSTAT4,TRSSTAT4   CLEAR AS FULLY APPROVED                       
*                                                                               
UPDEL60X B    ROUTS1X                                                           
         DROP R2,R3,R4                                                          
         EJECT                                                                  
         LTORG                                                                  
***********************************************************************         
* WORKING STORAGE                                                     *         
***********************************************************************         
         SPACE 1                                                                
TIMETRND DSECT                                                                  
DUB      DS    D                                                                
DMCB     DS    6F                                                               
RELO     DS    A                                                                
AEOB     DS    A                   END OF BUFFER                                
ANEXT    DS    A                                                                
SAVERD   DS    A                                                                
IOCOUNT  DS    A                                                                
GOCOUNT  DS    A                                                                
HALF     DS    H                                                                
DATADISP DS    H                                                                
*                                                                               
BCABUFF  DS    A                   A(CURRENT BUFFER)                            
BCAORIG  DS    A                   A(ORIGINAL ITEM)                             
BCANEW   DS    A                   A(NEW ITEM)                                  
*                                                                               
AMETHTAB DS    A                   A(METHOD TABLE)                              
*                                                                               
ATRNIO2  DS    A                   A(TRANSACTION AREA 2)                        
ATRNGLBK DS    A                   GL BLOCK FROM TRNBLK                         
*                                                                               
OVROUT1  DS    0A                  ROUTINE ADDRESSES                            
*&&US                                                                           
AELEM1A  DS    A                   FORM ELEMENT 1A                              
*&&                                                                             
AELEM23  DS    A                   FORM ELEMENT 23                              
AELEM2C  DS    A                   FORM ELEMENT 2C                              
AELEM40  DS    A                   FORM ELEMENT 40                              
AELEM44  DS    A                   FORM ELEMENT 44                              
AELEM4C  DS    A                   FORM ELEMENT 4C                              
AELEM50  DS    A                   FORM ELEMENT 50                              
*&&UK                                                                           
AELEM50R DS    A                   FORM ELEMENT 50R                             
AELEM50G DS    A                   FORM ELEMENT 50G                             
*&&                                                                             
AELEM59  DS    A                   FORM ELEMENT 59                              
AELEM5F  DS    A                   FORM ELEMENT 5F                              
AELEM60  DS    A                   FORM ELEMENT 60                              
AELEM65  DS    A                   FORM ELEMENT 65                              
*&&UK                                                                           
AELEM7B  DS    A                   FORM ELEMENT 7B                              
*&&                                                                             
AELEMC0  DS    A                   FORM ELEMENT C0                              
AELEMC5  DS    A                   FORM ELEMENT C5                              
AELEMD6  DS    A                   FORM ELEMENT D6                              
AELEMD8  DS    A                   FORM ELEMENT D8                              
*&&UK                                                                           
AELEMDB  DS    A                   FORM ELEMENT DB                              
AELEMFER DS    A                   FORM ELEMENT FE                              
AELEMFES DS    A                   FORM ELEMENT FE                              
AELEMFEJ DS    A                   FORM ELEMENT FE                              
AGETEURO DS    A                   GET EURO RATES                               
*&&                                                                             
AGETACC  DS    A                   GET ACCOUNT                                  
AMETHD   DS    A                   FORMAT METHOD TABLE                          
AGOADDTR DS    A                   ADD TRANSACTION BUCKET                       
AADDELEM DS    A                   ADD ELEMENT                                  
ABLDRATR DS    A                   BUILD ARATE RECORD                           
AADDRATE DS    A                   ADD RATEL ELEMENT                            
AWRITE   DS    A                   WRITE BACK                                   
AXCIO    DS    A                   CLEAR BLOCK AREA                             
AUPDATE  DS    A                   UPDATE - PROCESS OF POSTINGS                 
AUPDREC  DS    A                   UPDATE RECORD - IO MANAGEMENT                
AUPDEL60 DS    A                   UPDATE TRSEL                                 
OVROUT1N EQU   (*-OVROUT1)/L'OVROUT1                                            
*                                                                               
DA       DS    XL4                 DISK ADDRESS OF RECORD                       
COMMAND  DS    CL8                 DATAMANAGER COMMAND                          
WORK     DS    CL96                                                             
ELCODE   DS    XL1                                                              
*                                                                               
PSTSTAT  DS    XL1                 POSTING STATUS                               
PSTM     EQU   X'80'                  POSTING HAS BEEN MADE                     
*                                                                               
TMSNUM   DS    XL2                 NUMBER ENTRIES IN TMS TABLE                  
SVTMSMOA DS    XL2                 SAVED MOA FOR TIME                           
*                                                                               
STATUS   DS    XL1                                                              
STBCKOUT EQU   X'80'               BACK OUT OLD POSTING                         
STADDNEW EQU   X'40'               ADD NEW POSTING                              
STMINUS  EQU   X'20'               USE THE NEGATIVE OF THE RATE                 
STBUCKET EQU   X'10'               UPDATE BUCKETS ONLY                          
STORIGNL EQU   X'01'               USE ORIGINAL CLUSTER                         
STANEW   EQU   X'02'               USE NEW CLUSTER                              
*STADIFF EQU   X'04'               POST DIFFERENCE AMOUNT (NOT USED)            
STALAST  EQU   X'04'               LAST CALL TO ADDTRNS                         
STA1CBUC EQU   X'08'               DON'T POST 1C/14 AND 1R/1C BUCKETS           
*                                                                               
POSTSTAT DS    XL1                                                              
POST1R   EQU   X'80'               MAKING THE 1R POSTING- BUCKETS ONLY          
POSTSJ   EQU   X'40'               MAKING THE SJ POSTING                        
POSTSI   EQU   X'20'               MAKING THE SI POSTING                        
POST1C   EQU   X'10'               MAKING THE 1C POSTING                        
POST12   EQU   X'08'               MAKING THE 12 POSTING                        
POSTTXSJ EQU   X'04'               MAKING THE SJ POSTING - TAX POSTING          
POSTTXCR EQU   X'02'               MAKING THE CR POSTING - TAX POSTING          
POSTCA   EQU   X'01'               MAKING THE 1C/1R POSTING BUCKETS             
*                                                                               
RUNINDS  DS    XL1                 RUN INDICATOR                                
RUNIZERO EQU   X'80'               CREATE ZERO HOURS BUCKETS ETC                
*                                                                               
METHSTAT DS    XL1                 METHOD STATUS                                
METSRCAH EQU   X'80'               COST ALLOCATION HISTORY READ                 
METSPEXC EQU   X'20'               PERSON IS AN EXECUTIVE                       
METSHLVL EQU   X'10'               HIGH LEVEL METHOD PROFILE RECORD             
METSNANL EQU   X'08'               NO ANALYSIS FOUND ON 1R USE DEFAULT          
BUCKTYPE DS    0CL2                                                             
METHOD   DS    CL1                 METHOD                                       
SALARY   DS    CL1                 SALARY TYPE                                  
AC1ROFFC DS    CL2                 OFFICE                                       
AC1RDEPT DS    CL6                 DEPARTMENT                                   
AC1RSDPT DS    CL6                 SUB-DEPARTMENT                               
AC1RPERS DS    CL8                 PERSON                                       
*                                                                               
BCTRNIND DS    XL1                 TRNGLIND FROM TRNBLK                         
BCACTCDE DS    CL14                ACCOUNT CODE                                 
BCACTNAM DS    CL36                ACCOUNT NAME                                 
BCAMOUNT DS    PL6                 POSTING AMOUNT                               
BC1ROFC  DS    CL2                 1R OFFICE                                    
BCACANAL DS    CL12                ANALYSIS ACCOUNT                             
BCACCOST DS    CL1                 COSTING BYTE                                 
BCMOA    DS    PL3                 MONTH OF ACTIVITY                            
BCREF    DS    CL6                 TRANSACTION REFERENCE #                      
BCTXWC   DS    CL2                 TAX WORKCODE                                 
BCTXLOC  DS    CL8                 TAX LOCALITY                                 
BCTXBAS  DS    PL6                 TAX BASIS                                    
BCTXEFF  DS    PL3                 TAX EFFECTIVE DATE                           
BCTXRATE DS    PL4                 TAX RATE                                     
BCTXAMNT DS    PL6                 TAX AMOUNT                                   
BCLNARR  DS    XL1                 L'NARRATIVE                                  
BCNARR   DS    CL200               NARRATIVE OR MATERIALS TEXT                  
*                                                                               
BC142ACT DS    CL14                14 ACCOUNT 2 LEVEL STRUCTURE                 
BC143ACT DS    CL14                14 ACCOUNT 3 LEVEL STRUCTURE                 
BC142NME DS    CL36                14 ACCOUNT 2 LEVEL NAME                      
BC143NME DS    CL36                14 ACCOUNT 3 LEVEL NAME                      
*                                                                               
BC1RACCT DS    CL14                1R ACCOUNT                                   
BC1RNAME DS    CL36                1R ACCOUNT NAME                              
BCACCTS  DS    0CL14                                                            
BCSJACCT DS    0CL14               SJ OR 1N ACCOUNT                             
BCSJUL   DS    CL2                                                              
*&&UK                                                                           
BCSJCLI  DS    0C                                                               
         ORG   BCSJACCT+L'BCSJACCT                                              
*&&                                                                             
*&&US                                                                           
BCSJCLI  DS    CL3                                                              
BCSJPRD  DS    CL3                                                              
BCSJMED  DS    0CL1                MEDIA CODE                                   
BCSJJOB  DS    CL6                                                              
*&&                                                                             
BCSJNAME DS    CL36                SJ OR 1N ACCOUNT NAME                        
BCSJCLNM DS    CL36                SJ CLIENT NAME                               
BCSIACCT DS    CL14                SI OR SK ACCOUNT                             
BCSINAME DS    CL36                SI OR SK ACCOUNT NAME                        
BCCNTRA  DS    CL14                1C OR 1N ACCOUNT                             
BCCNTRNM DS    CL36                1C OR 1N ACCOUNT NAME                        
BC12ACCT DS    CL14                12 ACCOUNT                                   
BC12NAME DS    CL36                12 ACCOUNT NAME                              
BCCRACCT DS    CL14                TAX - CREDIT ACCOUNT                         
BCCRNAME DS    CL36                TAX - CREDIT ACCOUNT NAME                    
BCACCTQ  EQU   *-BCACCTS                                                        
*                                                                               
*                                                                               
AMNTDR   DS    PL6                                                              
AMNTCR   DS    PL6                                                              
PACK6    DS    PL6                                                              
PACK8    DS    PL8                                                              
PACK16   DS    PL16                                                             
ELEM     DS    CL255                                                            
KEY      DS    CL56                                                             
KEYSAVE  DS    CL56                                                             
SVKEY    DS    CL56                                                             
*                                                                               
RDTSRKEY DS    0F                  NEXT READ TSAR KEY(LINE/SUB)                 
RDTSRTKY DS    XL2                 NEXT READ TIME KEY                           
RDTSRMKY DS    XL2                 NEXT READ MATERIALS KEY                      
*                                                                               
LSTRDKY  DS    0D                                                               
LSTB4KY  DS    0F                  LAST TSAR BEFORE KEY                         
LSTB4TKY DS    XL2                 LAST TIME BEFORE KEY                         
LSTB4MKY DS    XL2                 LAST MATERIALS BEFORE KEY                    
LSTAFKY  DS    0F                  LAST TSAR AFTER KEY                          
LSTAFTKY DS    XL2                 LAST TIME AFTER KEY                          
LSTAFMKY DS    XL2                 LAST MATERIALS AFTER KEY                     
*                                                                               
LITVALS  DS    0X                                                               
ACCDIR   DS    CL8                                                              
ACCMST   DS    CL8                                                              
ACCARC   DS    CL8                                                              
DMRDHI   DS    CL8                                                              
DMREAD   DS    CL8                                                              
DMRSEQ   DS    CL8                                                              
GETREC   DS    CL8                                                              
PUTREC   DS    CL8                                                              
ADDREC   DS    CL8                                                              
DMWRT    DS    CL8                                                              
DMADD    DS    CL8                                                              
PZERO    DS    PL1                                                              
PNEGONE  DS    PL1                                                              
P100     DS    PL2                                                              
P1MILL   DS    PL4                                                              
INCLDG   DS    CL2                                                              
LITVALSL EQU   *-LITVALS                                                        
*                                                                               
SPACES   DS    CL200                                                            
BCWORK1  DS    CL200                                                            
BCWORK2  DS    CL200                                                            
METHTAB  DS    XL(9*METHDL)                                                     
       ++INCLUDE ACADDTRND                                                      
TRNIO    DS    CL2048              TRANSACTION IO AREA - 2K                     
TRNIO2   DS    CL2048              TRANSACTION IO AREA2 - 2K                    
TIMETRNX EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
* METHOD TABLE DSECT                                                  *         
***********************************************************************         
         SPACE 1                                                                
METHTABD DSECT                                                                  
METHNUM  DS    CL1                 METHOD NUMBER                                
METPERCT DS    PL6                 METHOD PERCENTAGE OF ALLOCATION              
METHSTR  DS    XL1                 METHOD 14 ACCOUNT STRUCTURE                  
METH2LVL EQU   2                   2 LEVEL 14 ACCOUNT                           
METH3LVL EQU   3                   3 LEVEL 14 ACCOUNT                           
METHANAL DS    CL1                 DEFAULT ANALYSIS CODE                        
METTRATE DS    PL6                 TOTAL HOURLY RATE                            
METPRATE DS    PL6                 PENSION HOURLY RATE FOR THIS METHOD          
METRATEL EQU   *-METPRATE          LENGTH OF RATE BUCKET                        
METBRATE DS    PL6                 BENEFIT HOURLY RATE FOR THIS METHOD          
METSRATE DS    PL6                 SALARY HOURLY RATE FOR THIS METHOD           
METBCNT  EQU   (*-METPRATE)/METRATEL TOTAL NUMBER OF BUCKETS FOR METHOD         
METEBCNT EQU   (*-METTRATE)/METRATEL TOTAL NUMBER OF BUCKETS FOR ELEM           
METHDL   EQU   *-METHTABD                                                       
         SPACE 1                                                                
***********************************************************************         
* ++INCLUDES                                                          *         
***********************************************************************         
         SPACE 1                                                                
       ++INCLUDE ACTIMETRND                                                     
       ++INCLUDE ACTIMEBLK                                                      
       ++INCLUDE ACCAP30DST                                                     
       ++INCLUDE ACCAP30GW                                                      
COBLOCKD DSECT                                                                  
       ++INCLUDE ACCAPBLOCK                                                     
       ++INCLUDE ACTOBACCOD                                                     
       ++INCLUDE DDTSARD                                                        
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE ACGENFILE                                                      
*&&UK                                                                           
TIMELD   DSECT                                                                  
         ORG   TIMELD+TIMILN2Q                                                  
TIMERTE  DS    PL4                 EURO CHARGE RATE                             
TIMECRTE DS    PL4                 EURO COST RATE                               
TIMB2LNQ EQU   *-TIMELD            LENGTH OF BILLABLE DATA W/EURO INFO          
*&&                                                                             
       ++INCLUDE FAFACTS                                                        
       ++INCLUDE DDCTRYEQUS                                                     
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'025ACTIMETRN 07/26/17'                                      
         END                                                                    
