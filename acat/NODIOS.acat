*          DATA SET NODIOS     AT LEVEL 020 AS OF 05/01/02                      
*CATALP NODIO                                                                   
         TITLE 'NODIO- NODAL FILE IO HANDLER'                                   
*                                                                               
*        PARAMETER LIST                                                         
*                                                                               
*                                                                               
*  PARAM 1  BYTE 0-3   A(NODBLKD)      CONTROL BLOCK                            
*  PARAM 2  BYTE 0-3   A(COMMAND)                                               
*  PARAM 3  BYTE 0-3   A(INPUT)                                                 
*  PARAM 4  BYTE 0     A=AFTER,B=BEFORE                                         
*  PARAM 4  BYTE 1-3   A(POSITION CODE)  FOR MOVES OR ADDS                      
*                      CODE X'00'=ADD AT START, X'FF'=ADD AT END                
*                                                                               
         PRINT NOGEN                                                            
NODIO    CSECT                                                                  
         NMOD1 NDWKL,**NODIO,R7,R8   **THREE BASE REGISTERS                     
         SPACE 2                                                                
*                                                                               
         USING NDWKD,RC                                                         
         L     RA,0(R1)                                                         
         USING NODBLKD,RA                                                       
         USING NDLVTABD,R9         R9 USED THROUGHOUT FOR LEV TAB               
*                                                                               
         MVC   PARAMS(24),0(R1)                                                 
         MVI   NDERRMSG,C' '                                                    
         MVC   NDERRMSG+1(L'NDERRMSG-1),NDERRMSG                                
*                                                                               
         CLI   NDMODE,X'FF'        FORCE START OVER                             
         BE    ND05                                                             
*                                                                               
         ICM   RE,15,NDGORET       IF RE-ENTRY AFTER GO                         
         BNZR  RE                  RETURN TO CALLING LOCATION                   
*                                                                               
ND05     DS    0H                                                               
         MVI   DMINBTS,0                                                        
         MVI   DMOUTBTS,X'FF'                                                   
         MVC   NDAREC,NDIOA        AREC NORMALLY IOA 1                          
         XC    NDGORET,NDGORET                                                  
         MVI   NDERR,0                                                          
         CLI   NDREREAD,C'Y'                                                    
         BE    *+8                                                              
         MVI   NDREREAD,C'N'                                                    
         MVC   RERDSW,NDREREAD     SET REREAD =Y OR N                           
*                                                                               
         MVI   INPT,C' '           SET INPUT 'KEY' IN INPT                      
         MVC   INPT+1(L'INPT-1),INPT                                            
         L     R3,PARAM3                                                        
         LTR   R3,R3               IS 'KEY' GIVEN AT PARAM3                     
         BZ    ND06                NO                                           
         MVC   INPT,0(R3)          YES- SET DIRECTLY                            
         B     ND07                                                             
*                                                                               
ND06     DS    0H                  ELSE SET INPT FROM LEV TAB                   
         LH    R6,NDNLEVS          FIRST SET CODES                              
         LTR   R6,R6                                                            
         BNP   ND07                NO LEVELS                                    
         LA    R9,NDLVTAB+NDLVTABL                                              
*                                                                               
ND06B    DS    0H                                                               
         OC    NDLVRCOD,SPACES                                                  
         CLC   NDLVCOD,NDLVRCOD    IF ASKED FOR CODE                            
         BE    *+14                NOT = CURRENT                                
         MVC   NDLVCOD,NDLVRCOD    SET IT AND                                   
         OI    NDLVSTAT,X'20'      FORCE READ                                   
         LA    R9,NDLVTABL(R9)                                                  
         BCT   R6,ND06B                                                         
*                                                                               
ND06D    DS    0H                                                               
         BAS   RE,CONCAT           SET INPT                                     
*                                                                               
ND07     DS    0H                                                               
*                                  SET LEVEL 1 NODE                             
         LA    R9,NDLVTAB+NDLVTABL   POINT TO LEVEL 1                           
         MVC   NDLVNOD,NDL1NOD     NORMAL NODE FOR LEV 1                        
         CLC   INPT(1),NDLIBPRE    UNLESS LIBRARY BOOK                          
         BNE   *+10                                                             
         MVC   NDLVNOD,NDLIBNOD    THEN USE LIBRARY NODE                        
*                                                                               
         EJECT                                                                  
*                                     COMMAND PROCESSING                        
*                                     ------------------                        
         SPACE 2                                                                
         L     R2,PARAM2                                                        
         MVC   COMMAND,0(R2)                                                    
*                                                                               
         CLC   =C'LSQ',COMMAND     LOGICAL SEQ                                  
         BE    LSQ                                                              
         CLC   =C'SEQ',COMMAND     SEQ                                          
         BE    SQP                                                              
         CLC   =C'READ',COMMAND    READ                                         
         BE    RDP                                                              
         CLC   =C'HIGH',COMMAND    HIGH                                         
         BE    HGP                                                              
         CLC   =C'BSQ',COMMAND     BACK SEQ                                     
         BE    BSQ                                                              
         CLC   =C'ADD',COMMAND     ADD                                          
         BE    ADP                                                              
         CLC   =C'CHA',COMMAND     CHANGE                                       
         BE    CHAP                                                             
         CLC   =C'DEL',COMMAND     DELETE (INCLUDES DELALL)                     
         BE    RDP                 (SHARES READ ROUTINE)                        
         CLC   =C'REN',COMMAND     RENAME                                       
         BE    RDP                 (SHARES READ ROUTINE)                        
         CLC   =C'RES',COMMAND     RESTORE                                      
         BE    ADP2                (SHARES ADD ROUTINE)                         
         CLC   =C'CSQ',COMMAND     CHANGE SEQUENCE                              
         BE    RDP                 (SHARES READ ROUTINE)                        
         CLC   =C'INIT',COMMAND    INITIALIZE                                   
         BE    INIT                                                             
         MVI   NDERR,X'FF'         BAD COMMAND-**TEMP TO STOP DUMPS             
         B     NDIOXX                                                           
*                                                                               
NDIOX    DS    0H                                                               
         MVI   NDMODE,NDEND                                                     
         XC    NDGORET,NDGORET     NO RETURN                                    
         CLI   NDDUMP,C'F'         ALWAYS DUMP                                  
         BNE   *+6                                                              
         DC    H'0'                                                             
*                                                                               
NDIOX4   DS    0H                                                               
         CLI   NDERR,0             TEST ANY ERROR                               
         BE    NDIOXX              NO                                           
         MVI   NDREREAD,C'Y'       YES-FORCE RE-READ NEXT TIME                  
         BAS   RE,SETERM           SET ERRMSG                                   
         CLI   NDDUMP,0            TEST TO DUMP ON ERRRO                        
         BE    NDIOXX                                                           
         DC    H'0'                                                             
*                                                                               
NDIOXX   DS    0H                                                               
         XIT1                                                                   
*                                                                               
SETERM   DS    0H                   SET ERROR MESSAGES                          
         LA    R6,ERRMSGS                                                       
*                                                                               
SERM4    DS    0H                                                               
         CLI   0(R6),X'FF'         END                                          
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLC   NDERR,0(R6)                                                      
         BNE   *+12                                                             
         MVC   NDERRMSG(EMSGL),1(R6)                                            
         BR    RE                                                               
*                                                                               
         LA    R6,EMSGL+1(R6)                                                   
         B     SERM4                                                            
         SPACE 3                                                                
*        GO - RETURN TO USER                                                    
         SPACE 2                                                                
GO       DS    0H                                                               
         ST    RE,NDGORET          SET RETURN ADDRESS                           
         ST    R9,NDLEVPTR         POINT TO LEVEL ENTRY                         
         B     NDIOX4                                                           
*                                                                               
         EJECT                                                                  
*                             READ COMMAND PROCESSING                           
*                             (ALSO USED FOR DELETE, CSQ, AND CHANGE)           
*                             ---------------------------------------           
         SPACE 2                                                                
RDP      DS    0H                                                               
         LA    R3,INPT                                                          
         XC    NDNLEVS,NDNLEVS                                                  
         MVI   NDLEV,0                                                          
         MVI   NDERR,0                                                          
         XC    NDSQLEVS,NDSQLEVS                                                
         LA    R9,NDLVTAB+NDLVTABL  CLEAR CERTAIN LEVEL DATA                    
         LH    R0,NDMXLEVS                                                      
*                                                                               
RDP3     DS    0H                                                               
         XC    NDLVSQST,NDLVSQST   CLEAR SEQ START                              
         LA    R9,NDLVTABL(R9)                                                  
         BCT   R0,RDP3                                                          
*                                                                               
         LA    R9,NDLVTAB+NDLVTABL  POINT TO LEVEL 1                            
*                                                                               
         CLI   0(R3),C' '          IF NO KEY GIVEN                              
         BNH   RDP8                OK AS START OF GLOBAL SEQUENTIAL             
*                                                                               
RDP4     DS    0H                                                               
         BAS   RE,PARSE                                                         
*                                                                               
         TM    NDLVSTAT,X'40'      TEST NEW THIS TIME                           
         BZ    RDP5                NO- SKIP TRACE AND GO                        
*                                                                               
RDP4D    DS    0H                                                               
         MVI   NDMODE,NDVAL        MODE IS VALIDATE                             
         CLI   0(R3),C' '          UNLESS AT END OF LEVELS                      
         BH    *+8                                                              
         MVI   NDMODE,NDPROC       THEN MODE IS PROCESS                         
         BAS   RE,LVTRACE                                                       
*                                                                               
         ST    R3,NDGOSAV          SAVE POSITION IN KEY                         
         ST    R9,NDGOSAV+4        SAVE LEVEL                                   
         BAS   RE,GO               RETURN TO USER                               
         L     R3,NDGOSAV          RESTORE SAVES                                
         L     R9,NDGOSAV+4                                                     
*                                                                               
RDP5     DS    0H                                                               
         CLI   NDERR,0                                                          
         BNE   RDP8                                                             
*                                                                               
         MVC   NDLVSQST,NDLVCOD    SET STARTER FOR SEQS                         
         CLI   0(R3),C' '          TEST AT END                                  
         BNH   RDP6                YES                                          
         LA    R3,1(R3)            NO- BUMP PAST DELIMITER                      
         LA    R9,NDLVTABL(R9)                                                  
         B     RDP4                                                             
*                                                                               
RDP6     DS    0H                                                               
         LH    RF,NDNLEVS                                                       
         STH   RF,NDSQLEVS         SET TO CONTROL SUBSEQUENT SEQ                
*                                                                               
         CLC   =C'READ',COMMAND    IF READ THEN DONE                            
         BE    RDP8                                                             
         CLI   NDLIBLEV,0          ELSE                                         
         BE    RDP7                                                             
         CLC   NDLIBLEV,NDNLEVS+1  LIB CALL MUST NOT EXIST                      
         BNL   RDP7                BEFORE THIS LEVEL                            
         MVI   NDERR,NDLIBERR                                                   
         B     RDP8                                                             
*                                                                               
RDP7     DS    0H                                                               
         CLC   =C'DEL',COMMAND     DELETE                                       
         BE    DELP                                                             
         CLC   =C'CSQ',COMMAND     CHANGE SEQUENCE                              
         BE    CSQP                                                             
         CLC   =C'REN',COMMAND     RENAME                                       
         BE    RENP                                                             
         CLC   =C'CHA',COMMAND     CHANGE                                       
         BE    CHAP10                                                           
*                                                                               
RDP8     DS    0H                                                               
         MVI   NDREREAD,C'N'       NO RE-READ NEXT TIME                         
         B     NDIOX                                                            
         EJECT                                                                  
*                                   DELETE COMMAND PROCESSING                   
*                                   -------------------------                   
         SPACE 2                                                                
DELP     DS    0H                                                               
         CLC   =C'DELALL',COMMAND       UNLESS DELETING ALL BELOW               
         BE    DELP4                    MUST BE AT END OF LINE                  
*                                                                               
         CLI   NDLVFRST+NDLVTABL,C' '   TEST ANY RECORDS AT                     
         BNH   *+12                     LOWER LEVEL                             
         MVI   NDERR,NDDELERR           DELETE NOT ALLOWED                      
         B     DELP20                                                           
*                                                                               
DELP4    DS    0H                                                               
         MVC   KEY,NDLVKEY                                                      
         ZIC   RE,NDLVNPOS         NODE POS                                     
         ZIC   R5,NDNODLN          PLUS LENGTH                                  
         AR    R5,RE                                                            
         ZIC   RE,NDLVCLN          PLUS CODE LENGTH                             
         AR    R5,RE                                                            
         BCTR  R5,R0               LENGTH FOR KEY COMPARE                       
*                                                                               
         BAS   RE,HIGH                                                          
         LR    RF,R5                                                            
         BAS   RE,KEYCHK2                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R3,KEY                                                           
         AH    R3,NDKLEN           POINT TO CONTROL BYTE                        
         OI    0(R3),X'80'         SET DELETED                                  
         BAS   RE,WRITE                                                         
         MVI   SPMODE,C'D'         SETPOS MODE = DELETE                         
         BAS   RE,SETPOS           REMOVE POSITIONING                           
*                                                                               
         MVI   NDREREAD,C'Y'       FORCE RE-READ NEXT TIME                      
*                                                                               
         CLC   =C'DELALL',COMMAND  DELETE ALL RECORDS BELOW                     
         BNE   DELP20                                                           
         MVI   DMINBTS,X'08'       PASS DELETES (TO PREVENT                     
         MVI   DMOUTBTS,X'FD'      DELETING ONLY ALTERNATE RECORDS)             
         B     SQP                 USE SEQUENTIAL PROCESSING                    
*                                                                               
DELP20   DS    0H                                                               
         B     NDIOX                                                            
         EJECT                                                                  
*                                CSQ (CHANGE SEQUENCE)                          
*                                ---------------------                          
         SPACE 2                                                                
CSQP     DS    0H                                                               
         MVC   SAVKEY,KEY          SAVE KEY FOR DA                              
         MVI   SPMODE,C'D'         SETPOS MODE = DELETE                         
         BAS   RE,SETPOS           REMOVE FROM CURRENT POSITION                 
         MVI   SPMODE,C'A'         SETPOS MODE = ADD                            
         BAS   RE,SETPOS           ADD AT NEW POSITION                          
         BAS   RE,PUT              NOTE- RECORD READ IN SETPOS                  
         MVI   NDREREAD,C'Y'       FORCE RE-READ NEXT TIME                      
*                                                                               
         B     NDIOX                                                            
         EJECT                                                                  
*                                       REN (RENAME)                            
*                                       ------------                            
         SPACE 2                                                                
RENP     DS    0H                                                               
         MVC   SAVKEY,KEY          HOLD ONTO OLD KEY                            
*                                  SET NEW CODE IN NDLVCOD                      
         XC    NDLVCOD,NDLVCOD                                                  
         L     R4,PARAM4           A(NEW CODE)                                  
         LA    R4,0(R4)            STRIP HOB                                    
         LR    R5,R4                                                            
*                                                                               
RENP2    DS    0H                  GET LENGTH OF CODE                           
         CLI   0(R5),C' '                                                       
         BNH   *+12                END                                          
         LA    R5,1(R5)                                                         
         B     RENP2                                                            
*                                                                               
         SR    R5,R4                                                            
         BP    *+6                                                              
         DC    H'0'                NO NEW CODE                                  
         BCTR  R5,R0                                                            
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   NDLVCOD(0),0(R4)    SET NEW CODE IN NDLVCOD                      
*                                                                               
         MVC   WKNC,NDLVNOD        NODE AND CODE                                
         BAS   RE,SETKEY           BUILD NEW KEY AND TEST IF ON FILE            
         MVI   DMINBTS,X'08'       SET TO PASS DELETES                          
         MVI   DMOUTBTS,X'FD'                                                   
         MVI   RNDLSW,C'N'         RENAME/DELETE SWITCH                         
         BAS   RE,HIGH                                                          
         MVI   DMINBTS,0           NO MORE DELETES                              
         MVI   DMOUTBTS,X'FF'                                                   
*                                  SET LENGTH FOR KEYCHK                        
         ZIC   RE,NDLVNPOS         NODE START                                   
         ZIC   RF,NDNODLN          PLUS NODE LENGTH                             
         AR    RF,RE                                                            
         ZIC   RE,NDLVCLN          PLUS CODE LENGTH FOR LEVEL                   
         AR    RF,RE                                                            
         BCTR  RF,R0                                                            
*                                                                               
         BAS   RE,KEYCHK2                                                       
         BNE   RENP6                                                            
*                                  KEY IS ON FILE                               
         LA    R3,KEY                                                           
         AH    R3,NDKLEN           POINT TO CONTROL BYTE                        
         TM    0(R3),X'80'         TEST DELETED                                 
         BZ    RENP5               NO- ERROR                                    
         MVI   RNDLSW,C'Y'         SET RENAMING TO DELETE                       
         NI    0(R3),X'7F'         SET OFF DELETE                               
         LA    R3,KEY                                                           
         AH    R3,NDDISK                                                        
         MVC   0(4,R3),NDLVDA      SET TO CORRECT DISK ADDRESS                  
         BAS   RE,WRITE            WRITE UNDELETED POINTER                      
         B     RENP6                                                            
*                                                                               
RENP5    DS    0H                                                               
         MVI   NDERR,NDRENERR      NEW CODE ALREADY ON FILE                     
         B     NDIOX                                                            
*                                  NOW DELETE OLD CODE                          
RENP6    DS    0H                                                               
         MVC   KEY,SAVKEY          RESTORE OLD KEY                              
         BAS   RE,READ                                                          
         LA    R3,KEY                                                           
         AH    R3,NDKLEN           POINT TO CONTROL BYTES                       
         OI    0(R3),X'A0'         SET DELETE AND RENAMED                       
         BAS   RE,WRITE            DELETE CURRENT POINTER                       
*                                  SET NEW CODE IN FILE RECORD                  
         MVC   WKNOD,NDLVNOD       NODE AND CODE                                
         BAS   RE,SETKEY           BUILD NEW KEY TO PUT IN REC                  
         L     R2,NDAREC                                                        
         LH    RF,NDKLENM1                                                      
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),KEY                                                      
*                                                                               
         LA    RF,KEY              SET DISK ADDRESS IN KEY                      
         AH    RF,NDDISK                                                        
         MVC   0(4,RF),NDLVDA                                                   
*                                                                               
         BAS   RE,PUT              WRITE OLD RECORD WITH NEW KEY                
         CLI   RNDLSW,C'Y'         HAVE I RENAMED TO A DELETE                   
         BE    *+8                 YES - DONT NEED NEW POINTER                  
         BAS   RE,ADDDIR           ADD NEW DIRECTORY RECORD                     
*                                                                               
         MVI   SPMODE,C'R'         SETPOS MODE=RENAME                           
         BAS   RE,SETPOS                                                        
*                                                                               
         MVI   NDREREAD,C'Y'       FORCE RE-READ NEXT TIME                      
         B     NDIOX                                                            
         EJECT                                                                  
*                                  CHANGE COMMAND PROCESSING                    
*                                  (CHANGE OF RECORD JUST READ)                 
*                                  ----------------------------                 
         SPACE 2                                                                
CHAP     DS    0H                                                               
         BAS   RE,MOVR1            SAVE CALLERS VERSION IN IOA2                 
         B     RDP                 USE RDP TO READ RECORD                       
*                                                                               
CHAP10   DS    0H                RETURN POINT FROM RDP                          
         L     R2,NDIOA                                                         
         L     R3,NDIOA2                                                        
         LH    RF,NDKLENM1                                                      
         EX    RF,CHACLC           COMPARE KEYS OF IOA1 AND IOA2                
         BE    *+6                                                              
         DC    H'0'                KEY CHANGE NOT ALLOWED                       
*                                                                               
         BAS   RE,MOVR2            MOVE CALLERS REC BACK TO IOA1                
         MVC   SAVLDA,LASTDA       SAVE LAST DISK ADDRESS                       
         BAS   RE,SETOVR           SET ANY OVERIDES                             
         CLI   NDERR,0                                                          
         BNE   NDIOX                                                            
*                                                                               
         CLC   SAVLDA,LASTDA       TEST OK TO PUT                               
         BE    CHAP13              YES                                          
         LA    RF,KEY              IF NOT, REREAD REC TO BE CHANGED             
         AH    RF,NDDISK           (SETOVR MAY HAVE READ FOR LIB CALL)          
         MVC   0(4,RF),SAVLDA                                                   
         MVC   NDAREC,NDIOA2       DO NOT READ OVER CHANGED REC                 
         BAS   RE,GET                                                           
         MVC   NDAREC,NDIOA        RESTORE TO IOA 1                             
*                                                                               
CHAP13   DS    0H                                                               
         BAS   RE,PUT                                                           
         MVI   NDREREAD,C'Y'       FORCE RE-READ NEXT TIME                      
         B     NDIOX             DONE                                           
*                                                                               
CHACLC   CLC   0(0,R2),0(R3)                                                    
*                                                                               
         EJECT                                                                  
*                                  HIGH COMMAND PROCESSING                      
*                                  -----------------------                      
         SPACE 2                                                                
HGP      DS    0H                                                               
         LA    R3,INPT                                                          
         XC    NDNLEVS,NDNLEVS                                                  
         MVI   NDLEV,0                                                          
         MVI   NDERR,0                                                          
         XC    NDSQLEVS,NDSQLEVS                                                
         LA    R9,NDLVTAB+NDLVTABL  CLEAR CERTAIN LEVEL DATA                    
         LH    R0,NDMXLEVS                                                      
*                                                                               
HGP3     DS    0H                                                               
         XC    NDLVSQST,NDLVSQST   CLEAR SEQ START                              
         LA    R9,NDLVTABL(R9)                                                  
         BCT   R0,HGP3                                                          
*                                                                               
         LA    R9,NDLVTAB+NDLVTABL  POINT TO LEVEL 1                            
*                                                                               
         CLI   0(R3),C' '          IF NO KEY GIVEN                              
         BNH   HGP8                OK AS START OF GLOBAL SEQUENTIAL             
*                                                                               
HGP4     DS    0H                                                               
         BAS   RE,PARSE                                                         
*                                                                               
         TM    NDLVSTAT,X'40'      TEST NEW THIS TIME                           
         BZ    HGP5                NO- SKIP TRACE AND RETURN                    
*                                                                               
HGP4D    DS    0H                                                               
         MVI   NDMODE,NDVAL        MODE IS VALIDATE                             
         CLI   0(R3),C' '          UNLESS AT END OF LEVELS                      
         BH    HGP4F                                                            
         CLI   NDERR,NDRNFERR       AT END RESET NOT FOUND                      
         BNE   *+8                 ERROR TO ZERO                                
         MVI   NDERR,0                                                          
         MVI   NDMODE,NDPROC       AT END MODE IS PROCESS                       
*                                                                               
HGP4F    DS    0H                                                               
         BAS   RE,LVTRACE                                                       
*                                                                               
         ST    R3,NDGOSAV          SAVE POSITION IN KEY                         
         ST    R9,NDGOSAV+4        SAVE LEVEL                                   
         BAS   RE,GO               RETURN TO USER                               
         L     R3,NDGOSAV          RESTORE SAVES                                
         L     R9,NDGOSAV+4                                                     
*                                                                               
HGP5     DS    0H                                                               
         CLI   NDERR,0                                                          
         BNE   HGP8                                                             
*                                                                               
         MVC   NDLVSQST,NDLVCOD    SET STARTER FOR SEQS                         
         CLI   0(R3),C' '          TEST AT END                                  
         BNH   HGP8                YES                                          
         LA    R3,1(R3)            NO -BUMP PAST DELIMITER                      
         LA    R9,NDLVTABL(R9)     NEXT LEVEL                                   
         B     HGP4                                                             
*                                                                               
HGP8     DS    0H                                                               
         LH    RF,NDNLEVS                                                       
         STH   RF,NDSQLEVS         SET TO CONTROL SUBSEQUENT SEQS               
         MVI   NDREREAD,C'N'       NO RE-READ NEXT TIME                         
         B     NDIOX                                                            
         EJECT                                                                  
*                                 ADD COMMAND PROCESSING                        
*                                 (ALSO USED FOR RESTORE)                       
*                                 -----------------------                       
         SPACE 2                                                                
ADP      DS    0H                                                               
         BAS   RE,MOVR1            SAVE RECORD TO BE ADDED IN IOA2              
*                                                                               
ADP2     DS    0H                  ENTRY POINT FOR RESTORE                      
         MVI   DMINBTS,X'08'       SET TO PASS DELETES                          
         MVI   DMOUTBTS,X'FD'                                                   
         LA    R3,INPT                                                          
         XC    NDNLEVS,NDNLEVS                                                  
         MVI   NDLEV,0                                                          
         MVI   NDERR,0                                                          
         LA    R9,NDLVTAB+NDLVTABL  POINT TO LEVEL 1                            
*                                                                               
ADP4     DS    0H                                                               
         BAS   RE,PARSE                                                         
*                                                                               
         CLI   0(R3),C' '          TEST AT END                                  
         BNH   ADP5                YES                                          
*                                                                               
         TM    NDLVSTAT,X'40'      TEST NEW THIS TIME                           
         BZ    ADP4D               NO- SKIP TRACE AND RETURN                    
*                                                                               
         LA    RE,KEY              YES- TEST RECORD DELETED                     
         AH    RE,NDKLEN                                                        
         TM    0(RE),X'80'                                                      
         BZ    *+12                                                             
         MVI   NDERR,NDRNFERR                                                   
         B     ADP30                                                            
*                                                                               
         MVI   NDMODE,NDVAL                                                     
         BAS   RE,LVTRACE                                                       
*                                                                               
         ST    R3,NDGOSAV          SAVE POSITION IN KEY                         
         ST    R9,NDGOSAV+4        SAVE LEVEL                                   
         BAS   RE,GO               RETURN TO USER                               
         L     R3,NDGOSAV          RESTORE SAVES                                
         L     R9,NDGOSAV+4                                                     
*                                                                               
ADP4D    DS    0H                                                               
         CLI   NDERR,0                                                          
         BNE   ADP30                                                            
*                                                                               
         LA    R3,1(R3)            BUMP PAST DELIMITER                          
         LA    R9,NDLVTABL(R9)     NEXT LEVEL                                   
         B     ADP4                                                             
*                                                                               
ADP5     DS    0H                  HAVE END OF 'KEY'                            
         CLI   NDLIBLEV,0          OK IF NO LIB                                 
         BE    ADP6                                                             
         CLC   NDLIBLEV,NDNLEVS+1  TEST AT LEVEL BELOW                          
         BNL   ADP6                LIBRARY CALL                                 
         MVI   NDERR,NDLMXERR      MIX OF LIB AND NON-LIB                       
         B     ADP30                                                            
*                                                                               
ADP6     DS    0H                                                               
         CLC   =C'RES',COMMAND     TEST RESTORING                               
         BE    RSTP                                                             
*                                                                               
         CLI   NDERR,NDRNFERR       RECORD NOT FOUND IS OK                      
         BE    ADP7                                                             
         CLI   NDERR,0             OTHER ERRORS NO GOOD                         
         BNE   ADP30                                                            
         MVI   NDERR,NDADDERR      ADD OF KEY ALREADY ON FILE                   
         B     ADP30                                                            
*                                                                               
ADP7     DS    0H                                                               
         MVI   NDERR,0             RESET ERROR                                  
         MVI   NDLVERR,0                                                        
         OC    NDLVNOD,NDLVNOD     IS NODE ALREADY ESTABLISHED                  
         BNZ   *+8                                                              
         BAS   RE,SETNOD                                                        
         CLI   NDERR,0                                                          
         BNE   ADP30                                                            
*                                                                               
         BAS   RE,MOVR2           MOVE ADDED RECORD BACK TO IOA1                
         MVC   WKNC,NDLVNOD        NODE AND CODE                                
         BAS   RE,SETKEY                                                        
*                                                                               
         L     R2,NDAREC                                                        
         LH    RF,NDKLENM1                                                      
         EX    RF,ADPMVC           SET KEY IN RECORD                            
*                                  ADD BACK-NODE ELEMENT                        
         MVI   ELCODE,X'B5'        FIRST DELETE ANY THERE                       
         BAS   RE,DELEL                                                         
         XC    WORK,WORK                                                        
         MVI   WORK,X'B5'                                                       
         LR    R3,R9                                                            
         SH    R3,=Y(NDLVTABL)                                                  
         LA    RF,5                                                             
         OC    WORK+2(3),NDLVNOD-NDLVTABD(R3)                                   
         BZ    ADP12               NO NODE (HIGHER IS MASTER)                   
*                                                                               
         MVC   WORK+5(NDCODL),NDLVCOD-NDLVTABD(R3)                              
         LA    RF,WORK+5+NDCODL                                                 
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,*-8                                                           
         LA    R0,WORK-1                                                        
         SR    RF,R0                                                            
*                                                                               
ADP12    DS    0H                                                               
         STC   RF,WORK+1           ELEM LENGTH                                  
*                                                                               
         SR    R2,R2               ADD ELEM AT START                            
         BAS   RE,ADDEL                                                         
         BNE   ADP30                                                            
*                                                                               
         BAS   RE,SETOVR           SET OVERRIDES                                
         CLI   NDERR,0                                                          
         BNE   ADP30                                                            
*                                                                               
         MVI   SPMODE,C'A'         SETPOS MODE = ADD                            
         BAS   RE,SETPOS           POSITIONING                                  
         CLI   NDERR,0                                                          
         BNE   ADP30                                                            
*                                                                               
         BAS   RE,ADD                                                           
         MVC   NDLVDA,KEY          SET DISK ADDRESS                             
*                                                                               
ADP20    DS    0H                                                               
ADP30    DS    0H                                                               
         MVI   NDREREAD,C'Y'       FORCE RE-READ NEXT TIME                      
         B     NDIOX                                                            
*                                                                               
ADPMVC   MVC   0(0,R2),KEY                                                      
         EJECT                                                                  
*                                  RESTORE COMMAND                              
*                                  ---------------                              
         SPACE 2                                                                
RSTP     DS    0H                                                               
         CLI   NDERR,0             TEST ERROR                                   
         BNE   ADP30                                                            
*                                                                               
         LA    RF,KEY                                                           
         AH    RF,NDKLEN           POINT TO CONTROL BYTES                       
         TM    0(RF),X'80'         MUST BE A DELETE                             
         BZ    RSTP2                                                            
         TM    0(RF),X'20'         BUT NOT A RENAMED RECORD                     
         BZ    RSTP3                                                            
*                                                                               
RSTP2    DS    0H                                                               
         MVI   NDERR,NDRESERR                                                   
         B     ADP20                                                            
*                                                                               
RSTP3    DS    0H                                                               
         NI    0(RF),X'7F'         SET OFF DELETE BIT                           
         BAS   RE,WRITE                                                         
*                                                                               
         MVC   SAVKEY,KEY          SAVE KEY FOR DA                              
         MVI   SPMODE,C'A'         SETPOS MODE = ADD                            
         BAS   RE,SETPOS           POSITIONING                                  
*                                  NOTE-RECORD READ IN SETPOS                   
         L     R2,NDAREC           UNDELETE RECORD AS WELL                      
         AH    R2,NDCNTL                                                        
         NI    0(R2),X'7F'                                                      
         BAS   RE,PUT                                                           
*                                                                               
         MVI   NDREREAD,C'Y'       FORCE RE-READ NEXT TIME                      
         B     ADP20               BACK INTO ADD LOGIC                          
         EJECT                                                                  
*                                  SEQ COMMAND PROCESSING                       
*                                  ----------------------                       
         SPACE 2                                                                
SQP      DS    0H                                                               
*                                 INITIALIZE FOR SEQUENTIAL READING             
         LH    R9,NDNLEVS                                                       
         MH    R9,=Y(NDLVTABL)                                                  
         LA    R9,NDLVTAB(R9)                                                   
         MVI   SQFRST,C'Y'                                                      
*                                                                               
         CLC   NDSQLEVS,NDNLEVS    IF NOT BACKING UP BEYOND                     
         BL    SQP4                THIS LEVEL                                   
         BAS   RE,TSTLEV           THEN TEST ANY LOWER LEVEL                    
         BZ    SQP30               NO- DONE,GIVEN KEY IS END OF LINE            
         B     SQP5                                                             
*                                                                               
SQP4     DS    0H                                                               
         BAS   RE,TSTLEV           TEST ANY LOWER LEVEL                         
         BZ    SQP10               NO- GET NEXT AT THIS LEVEL                   
*                                                                               
SQP5     DS    0H                                                               
         MVI   NDMODE,NDFRST       FIRST FOR THIS LEVEL                         
         BAS   RE,SQPGO                                                         
         CLI   NDSKIP,C'Y'         TEST TO SKIP LOWER RECORDS                   
         BNE   *+12                                                             
         MVI   NDSKIP,C'N'                                                      
         B     SQP10               YES- GET NEXT AT THIS LEVEL                  
*                                                                               
         LA    R9,NDLVTABL(R9)     BUMP TO NEXT LEVEL                           
         LH    R6,NDNLEVS          BUMP LEVEL COUNT                             
         LA    R6,1(R6)                                                         
         STH   R6,NDNLEVS                                                       
         MVI   SQFRST,C'Y'                                                      
*                                                                               
SQP10    DS    0H                                                               
         CLI   SQFRST,C'Y'         TEST FIRST TIME AT THIS LEVEL                
         BNE   SQP18                                                            
         MVI   SQFRST,C'N'                                                      
*                                  INITIALIZE AT THIS LEVEL                     
         MVC   NDLVCOD,NDLVSQST    SET TO START CODE (IF ANY)                   
         XC    NDLVSQST,NDLVSQST                                                
*                                                                               
         MVC   WKNC,NDLVNOD        NODE AND CODE                                
         BAS   RE,SETKEY                                                        
         ZIC   RE,NDLVNPOS         NODE POS                                     
         ZIC   R5,NDNODLN          PLUS NODE LENGTH                             
         AR    R5,RE                                                            
         BCTR  R5,R0               LENGTH FOR KEY COMPARE                       
*                                                                               
         CLI   NDLVCOD,C' '        IF HAD SEQ START CODE                        
         BNH   SQP10D                                                           
         ZIC   RE,NDLVCLN          FORCE TO NEXT RECORD                         
         AR    RE,R5                                                            
         LA    RE,KEY(RE)          BYTE PAST CODE                               
         MVI   1(RE),X'FF'                                                      
*                                                                               
SQP10D   DS    0H                                                               
         BAS   RE,HIGH                                                          
         B     SQP12B                                                           
*                                                                               
SQP12    DS    0H                                                               
         BAS   RE,SEQ                                                           
*                                                                               
SQP12B   DS    0H                                                               
         LR    RF,R5                                                            
         BAS   RE,KEYCHK2          COMPARE THRU NODE                            
         BNE   SQP20               DONE                                         
*                                                                               
         CLC   =C'DELALL',COMMAND  TEST DELETING                                
         BNE   SQP13                                                            
         LA    RF,KEY                                                           
         AH    RF,NDKLEN           POINT TO CONTROL BYTE                        
         OI    0(RF),X'80'         SET DELETED                                  
         BAS   RE,WRITE                                                         
*                                                                               
SQP13    DS    0H                                                               
         ZIC   RF,NDLVNPOS         NOD POS                                      
         ZIC   RE,NDNODLN          PLUS NODE LENGTH                             
         AR    RF,RE                                                            
*                                                                               
         LA    R2,KEY(RF)          START OF CODE IN KEY                         
         MVC   NDLVCOD,SPACES      MOVE CODE FROM KEY TO NDLVCOD                
         LA    R3,NDLVCOD          NOTE- HANDLES LEFT AND RIGHT                 
         ZIC   R0,NDLVCLN          ALIGNED CODES                                
         SR    R1,R1                                                            
*                                                                               
SQP14    DS    0H                                                               
         CLI   0(R2),0                                                          
         BNE   SQP14B                                                           
*                                                                               
         LTR   R1,R1               0 IS END IF HAVE                             
         BP    SQP16               FOUND SIGNIFICANT BYTES                      
         B     SQP15               ELSE IGNORE                                  
*                                                                               
SQP14B   DS    0H                                                               
         MVC   0(1,R3),0(R2)                                                    
         LA    R1,1(R1)                                                         
         LA    R3,1(R3)                                                         
*                                                                               
SQP15    DS    0H                                                               
         LA    R2,1(R2)                                                         
         BCT   R0,SQP14                                                         
*                                                                               
SQP16    DS    0H                                                               
         BAS   RE,GET                                                           
         BAS   RE,EXLEV            EXTRACT DATA                                 
         BAS   RE,PARSUB           PARAMETER SUBSTITUTION                       
         BNE   SQP18               OMIT THIS RECORD                             
*                                                                               
         MVI   NDMODE,NDPROC       PROCESS                                      
         BAS   RE,SQPGO                                                         
         B     SQP4                                                             
*                                                                               
*                                  CONTINUED SEQUENTIAL READING                 
SQP18    DS    0H                                                               
         MVC   KEY,NDLVKEY         SET KEY AND                                  
         ZIC   RE,NDLVNPOS         NODE POS                                     
         ZIC   R5,NDNODLN          PLUS NODE LENGTH                             
         AR    R5,RE                                                            
         BCTR  R5,R0               LENGTH FOR KEY COMPARE                       
         BAS   RE,HIGH             RESET FOR SEQ                                
         B     SQP12               GET NEXT RECORD                              
*                                                                               
SQP20    DS    0H                                                               
         LH    R6,NDNLEVS          BACK UP 1 LEVEL                              
         BCTR  R6,R0                                                            
         STH   R6,NDNLEVS                                                       
         SH    R9,=Y(NDLVTABL)                                                  
*                                                                               
         MVI   NDMODE,NDLAST       LAST FOR LEVEL                               
         BAS   RE,SQPGO                                                         
         LH    RF,NDNLEVS                                                       
         CH    RF,NDSQLEVS         IF HAVE NOT REACHED STARTING LEVEL           
         BH    SQP10               CONTINUE AT NOW CURRENT LEVEL                
*                                  ELSE DONE                                    
SQP30    DS    0H                                                               
         B     NDIOX                                                            
         SPACE 2                                                                
SQPGO    DS    0H                  RETRUN TO USER                               
         ST    RE,NDGOSAV+8        SAVE SQPGO CALL                              
         OC    PARAM3,PARAM3       IF 'KEY' STRING GIVEN                        
         BZ    SQPGO4                                                           
         BAS   RE,CONCAT           RETURN CONCATENATED STRING TO USER           
         L     R3,PARAM3                                                        
         ZIC   R1,NDCKEYL                                                       
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),INPT                                                     
*                                                                               
SQPGO4   DS    0H                                                               
         MVC   NDLEV,NDNLEVS+1                                                  
         BAS   RE,LVTRACE                                                       
*                                                                               
         ST    R2,NDGOSAV          SAVE DIRECTORY POSITION                      
         ST    R9,NDGOSAV+4        AND LEVEL ENTRY                              
         BAS   RE,GO               RETURN TO USER                               
         L     R2,NDGOSAV          RESTORE                                      
         L     R9,NDGOSAV+4                                                     
         L     RE,NDGOSAV+8        RESTORE SQPGO CALL                           
         BR    RE                                                               
         EJECT                                                                  
*                                LOGICAL SEQUENTIAL READING                     
*                                --------------------------                     
         SPACE 2                                                                
LSQ      DS    0H                                                               
*                                 INITIALIZE FOR SEQUENTIAL READING             
         LH    R9,NDNLEVS                                                       
         MH    R9,=Y(NDLVTABL)                                                  
         LA    R9,NDLVTAB(R9)                                                   
         MVI   SQFRST,C'Y'                                                      
*                                                                               
         CLC   NDSQLEVS,NDNLEVS    IF NOT BACKING UP BEYOND                     
         BL    LSQ4                THIS LEVEL                                   
         BAS   RE,TSTLEV           THEN TEST ANY LOWER LEVEL                    
         BZ    LSQ30               NO- DONE,GIVEN KEY IS END OF LINE            
         B     LSQ5                                                             
*                                                                               
LSQ4     DS    0H                                                               
         BAS   RE,TSTLEV           TEST ANY LOWER LEVEL                         
         BZ    LSQ10               NO- GET NEXT AT THIS LEVEL                   
*                                                                               
LSQ5     DS    0H                                                               
         MVI   NDMODE,NDFRST       FIRST FOR THIS LEVEL                         
         BAS   RE,SQPGO                                                         
         CLI   NDSKIP,C'Y'         TEST TO SKIP LOWER RECORDS                   
         BNE   *+12                                                             
         MVI   NDSKIP,C'N'                                                      
         B     LSQ10               YES- GET NEXT AT THIS LEVEL                  
*                                                                               
         LA    R9,NDLVTABL(R9)     BUMP TO NEXT LEVEL                           
         LH    R6,NDNLEVS          BUMP LEVEL COUNT                             
         LA    R6,1(R6)                                                         
         STH   R6,NDNLEVS                                                       
         MVI   SQFRST,C'Y'                                                      
*                                                                               
LSQ10    DS    0H                                                               
         CLI   SQFRST,C'Y'         TEST FIRST TIME AT THIS LEVEL                
         BNE   LSQ18                                                            
         MVI   SQFRST,C'N'                                                      
*                                  INITIALIZE AT THIS LEVEL                     
         BAS   RE,SETLIB           GET LIBRARY CALL IF ANY                      
         BNE   LSQ30               QUIT ON ERROR                                
         MVC   NDLVCOD,NDLVFRST    START WITH FIRST                             
         CLI   NDLVCOD,C' '                                                     
         BH    *+6                                                              
         DC    H'0'                FIRST SHOULD ALWAYS BE THERE                 
         CLI   NDLVSQST,C' '       TEST HAVE STARTER POS                        
         BNH   *+14                NO-START WITH FIRST                          
         XC    NDLVSQST,NDLVSQST   YES-CLEAR IT                                 
         B     LSQ18               AND GET NEXT RECORD                          
*                                                                               
LSQ11    DS    0H                                                               
         MVC   WKNC,NDLVNOD        NODE AND CODE                                
         BAS   RE,SETKEY                                                        
         ZIC   RE,NDLVNPOS         NODE POS                                     
         ZIC   R5,NDNODLN          PLUS NODE LENGTH                             
         AR    R5,RE                                                            
         ZIC   RE,NDLVCLN          PLUS CODE LENGTH                             
         AR    R5,RE                                                            
         BCTR  R5,R0               LENGTH FOR KEY COMPARE                       
*                                                                               
         BAS   RE,HIGH                                                          
         LR    RF,R5                                                            
         BAS   RE,KEYCHK2          COMPARE THRU CODE                            
         BE    *+6                                                              
         DC    H'0'                NEXT RECORD IS MISSING                       
*                                                                               
         ZIC   RF,NDLVNPOS         NODE POS                                     
         ZIC   RE,NDNODLN          PLUS NODE LENGTH                             
         AR    RF,RE                                                            
*                                                                               
         LA    R2,KEY(RF)          START OF CODE IN KEY                         
         MVC   NDLVCOD,SPACES      MOVE CODE FROM KEY TO NDLVCOD                
         LA    R3,NDLVCOD          NOTE- HANDLES LEFT AND RIGHT                 
         ZIC   R0,NDLVCLN          ALIGNED CODES                                
         SR    R1,R1                                                            
*                                                                               
LSQ14    DS    0H                                                               
         CLI   0(R2),0                                                          
         BNE   LSQ14B                                                           
*                                                                               
         LTR   R1,R1               0 IS END IF HAVE                             
         BP    LSQ16               FOUND SIGNIFICANT BYTES                      
         B     LSQ15               ELSE IGNORE                                  
*                                                                               
LSQ14B   DS    0H                                                               
         MVC   0(1,R3),0(R2)                                                    
         LA    R1,1(R1)                                                         
         LA    R3,1(R3)                                                         
*                                                                               
LSQ15    DS    0H                                                               
         LA    R2,1(R2)                                                         
         BCT   R0,LSQ14                                                         
*                                                                               
LSQ16    DS    0H                                                               
         BAS   RE,GET                                                           
         BAS   RE,EXLEV            EXTRACT DATA                                 
         BAS   RE,PARSUB           PARAMETER SUBSTITUTION                       
         BNE   LSQ18               OMIT THIS RECORD                             
*                                                                               
         MVI   NDMODE,NDPROC       PROCESS                                      
         BAS   RE,SQPGO                                                         
         B     LSQ4                                                             
*                                                                               
*                                  CONTINUED SEQUENTIAL READING                 
LSQ18    DS    0H                                                               
         CLI   NDLVFWRD,C' '       TEST HAVE FWRD LINK                          
         BH    LSQ18D              YES - CONTINUE                               
         CLC   NDLVCOD,NDLVLAST    IF NOT, CURRENT CODE SHOULD = LAST           
         BE    LSQ20                                                            
         DC    H'0'                                                             
*                                                                               
LSQ18D   DS    0H                                                               
         MVC   NDLVCOD,NDLVFWRD    SET KEY AND                                  
         B     LSQ11               GET NEXT RECORD                              
*                                                                               
LSQ20    DS    0H                                                               
         LH    R6,NDNLEVS          BACK UP 1 LEVEL                              
         BCTR  R6,R0                                                            
         STH   R6,NDNLEVS                                                       
         SH    R9,=Y(NDLVTABL)                                                  
*                                                                               
         MVI   NDMODE,NDLAST       LAST FOR LEVEL                               
         BAS   RE,SQPGO                                                         
         LH    RF,NDNLEVS                                                       
         CH    RF,NDSQLEVS         IF HAVE NOT REACHED STARTING LEVEL           
         BH    LSQ10               CONTINUE AT NOW CURRENT LEVEL                
*                                  ELSE DONE                                    
LSQ30    DS    0H                                                               
         B     NDIOX                                                            
         EJECT                                                                  
*                                  BACKWARD SEQUENTIAL READING                  
*                                  ---------------------------                  
         SPACE 2                                                                
BSQ      DS    0H                                                               
*                                 INITIALIZE FOR SEQUENTIAL READING             
         LH    R9,NDNLEVS                                                       
         MH    R9,=Y(NDLVTABL)                                                  
         LA    R9,NDLVTAB(R9)                                                   
         MVI   SQFRST,C'Y'                                                      
*                                                                               
         CLC   NDSQLEVS,NDNLEVS    IF NOT BACKING UP BEYOND                     
         BL    BSQ4                THIS LEVEL                                   
         BAS   RE,TSTLEV           THEN TEST ANY LOWER LEVEL                    
         BZ    BSQ30               NO- DONE,GIVEN KEY IS END OF LINE            
         B     BSQ5                                                             
*                                                                               
BSQ4     DS    0H                                                               
         BAS   RE,TSTLEV           TEST ANY LOWER LEVEL                         
         BZ    BSQ10               NO- GET NEXT AT THIS LEVEL                   
*                                                                               
BSQ5     DS    0H                                                               
         MVI   NDMODE,NDFRST       FIRST FOR THIS LEVEL                         
         BAS   RE,SQPGO                                                         
         CLI   NDSKIP,C'Y'         TEST TO SKIP LOWER RECORDS                   
         BNE   *+12                                                             
         MVI   NDSKIP,C'N'                                                      
         B     BSQ10               YES- GET NEXT AT THIS LEVEL                  
*                                                                               
         LA    R9,NDLVTABL(R9)     BUMP TO NEXT LEVEL                           
         LH    R6,NDNLEVS          BUMP LEVEL COUNT                             
         LA    R6,1(R6)                                                         
         STH   R6,NDNLEVS                                                       
         MVI   SQFRST,C'Y'                                                      
*                                                                               
BSQ10    DS    0H                                                               
         CLI   SQFRST,C'Y'         TEST FIRST TIME AT THIS LEVEL                
         BNE   BSQ18                                                            
         MVI   SQFRST,C'N'                                                      
*                                  INITIALIZE AT THIS LEVEL                     
         BAS   RE,SETLIB           GET LIBRARY CALL IF ANY                      
         BNE   BSQ30               QUIT ON ERROR                                
         MVC   NDLVCOD,NDLVLAST    START WITH LAST                              
         CLI   NDLVCOD,C' '                                                     
         BH    *+6                                                              
         DC    H'0'                LAST SHOULD ALWAYS BE THERE                  
         CLI   NDLVSQST,C' '       TEST HAVE STARTER POS                        
         BNH   *+14                NO START WITH LAST                           
         XC    NDLVSQST,NDLVSQST   YES- CLEAR IT                                
         B     BSQ18               AND GET NEXT (BACK) RECORD                   
*                                                                               
BSQ11    DS    0H                                                               
         MVC   WKNC,NDLVNOD        NODE AND CODE                                
         BAS   RE,SETKEY                                                        
         ZIC   RE,NDLVNPOS         NODE POS                                     
         ZIC   R5,NDNODLN          PLUS NODE LENGTH                             
         AR    R5,RE                                                            
         ZIC   RE,NDLVCLN          PLUS CODE LENGTH                             
         AR    R5,RE                                                            
         BCTR  R5,R0               LENGTH FOR KEY COMPARE                       
*                                                                               
         BAS   RE,HIGH                                                          
         LR    RF,R5                                                            
         BAS   RE,KEYCHK2          COMPARE THRU CODE                            
         BE    *+6                                                              
         DC    H'0'                NEXT RECORD IS MISSING                       
*                                                                               
         ZIC   RF,NDLVNPOS         NODE POS                                     
         ZIC   RE,NDNODLN          PLUS NODE LENGTH                             
         AR    RF,RE                                                            
*                                                                               
         LA    R2,KEY(RF)          START OF CODE IN KEY                         
         MVC   NDLVCOD,SPACES      MOVE CODE FROM KEY TO NDLVCOD                
         LA    R3,NDLVCOD          NOTE- HANDLES LEFT AND RIGHT                 
         ZIC   R0,NDLVCLN          ALIGNED CODES                                
         SR    R1,R1                                                            
*                                                                               
BSQ14    DS    0H                                                               
         CLI   0(R2),0                                                          
         BNE   BSQ14B                                                           
*                                                                               
         LTR   R1,R1               0 IS END IF HAVE                             
         BP    BSQ16               FOUND SIGNIFICANT BYTES                      
         B     BSQ15               ELSE IGNORE                                  
*                                                                               
BSQ14B   DS    0H                                                               
         MVC   0(1,R3),0(R2)                                                    
         LA    R1,1(R1)                                                         
         LA    R3,1(R3)                                                         
*                                                                               
BSQ15    DS    0H                                                               
         LA    R2,1(R2)                                                         
         BCT   R0,BSQ14                                                         
*                                                                               
BSQ16    DS    0H                                                               
         BAS   RE,GET                                                           
         BAS   RE,EXLEV            EXTRACT DATA                                 
         BAS   RE,PARSUB           PARAMETER SUBSTITUTION                       
         BNE   BSQ18               OMIT THIS RECORD                             
*                                                                               
         MVI   NDMODE,NDPROC       PROCESS                                      
         BAS   RE,SQPGO                                                         
         B     BSQ4                                                             
*                                                                               
*                                  CONTINUED SEQUENTIAL READING                 
BSQ18    DS    0H                                                               
         CLI   NDLVBACK,C' '       TEST HAVE BACKWARD LINK                      
         BH    BSQ18D              YES - CONTINUE                               
         CLC   NDLVCOD,NDLVFRST    IF NOT, CURRENT CODE SHOULD = FRST           
         BE    BSQ20                                                            
         DC    H'0'                                                             
*                                                                               
BSQ18D   DS    0H                                                               
         MVC   NDLVCOD,NDLVBACK    SET KEY AND                                  
         B     BSQ11               GET 'NEXT' RECORD                            
*                                                                               
BSQ20    DS    0H                                                               
         LH    R6,NDNLEVS          BACK UP 1 LEVEL                              
         BCTR  R6,R0                                                            
         STH   R6,NDNLEVS                                                       
         SH    R9,=Y(NDLVTABL)                                                  
*                                                                               
         MVI   NDMODE,NDLAST       LAST FOR LEVEL                               
         BAS   RE,SQPGO                                                         
         LH    RF,NDNLEVS                                                       
         CH    RF,NDSQLEVS         IF HAVE NOT REACHED STARTING LEVEL           
         BH    BSQ10               CONTINUE AT NOW CURRENT LEVEL                
*                                  ELSE DONE                                    
BSQ30    DS    0H                                                               
         B     NDIOX                                                            
         SPACE 3                                                                
*        TSTLEV TEST ANY LOWER LEVEL                                            
         SPACE 2                                                                
TSTLEV   DS    0H                                                               
         CLI   NDLVFRST+NDLVTABL,C' '  WILL HAVE LOWER RECORDS                  
         BH    TL2                     IF FRST IS STATED                        
         CLI   NDLIBLEV,0                                                       
         BE    TLNO                                                             
         CLC   NDLIBLEV,NDNLEVS+1      OR IF THIS IS LIBRARY CALL               
         BNE   TLNO                                                             
*                                                                               
TL2      DS    0H                                                               
TLYES    DS    0H                                                               
         LTR   RE,RE               CC NOT 0, HAVE NEXT LEVEL                    
         BR    RE                                                               
*                                                                               
TLNO     DS    0H                                                               
         CR    RE,RE               SET CC = 0, NO NEXT LEVEL                    
         BR    RE                                                               
*                                                                               
         SPACE 3                                                                
SETLIB   DS    0H                  GET LIBRARY HEADER                           
         CLI   NDLIBLEV,0          NONE                                         
         BER   RE                                                               
         LH    R0,NDNLEVS                                                       
         BCTR  R0,R0                                                            
         STC   R0,BYTE                                                          
         CLC   NDLIBLEV,BYTE       TEST AT THIS LEVEL                           
         BE    SETLIBN                                                          
         SR    R0,R0               SET CC = (NO ERROR)                          
         BR    RE                                                               
*                                                                               
SETLIBN  NTR1                                                                   
         MVC   WKNC,NDATTNOD       ATTACHED NODE AND CODE                       
         BAS   RE,SETKEY                                                        
         BAS   RE,HIGH                                                          
         BAS   RE,KEYCHK                                                        
         BE    *+12                                                             
         MVI   NDERR,NDLNFERR                                                   
         B     SETLIBX                                                          
*                                                                               
         MVC   NDAREC,NDIOA2           USE 2ND IO AREA                          
         BAS   RE,GET                                                           
         SH    R9,=Y(NDLVTABL)         SET AT PREVIOUS LEVEL                    
         MVC   XW(NDCODL*2),NDLVFWRD   SAVE FWRD AND BACK                       
         MVC   XW+NDCODL*2(L'NDLVKEY+4),NDLVKEY  AND KEY AND DISK ADDR          
         BAS   RE,EXLEV                EXTRACT DATA                             
         MVC   NDLVFWRD(NDCODL*2),XW   RESTORE                                  
         MVC   NDLVKEY(L'NDLVKEY+4),XW+NDCODL*2                                 
         MVC   NDAREC,NDIOA            RESTORE TO  NORMAL IO AREA               
*                                                                               
SETLIBX  DS    0H                                                               
         CLI   NDERR,0             SET CC                                       
         XIT1                                                                   
         EJECT                                                                  
*        SETPOS POSITIONING LOGIC                                               
         SPACE 2                                                                
SETPOS   NTR1                                                                   
         CLC   NDLIBPRE,INPT       SKIP SET POS FOR                             
         BNE   SP08                                                             
         CLI   NDNLEVS+1,1         LIBRARY STARTS                               
         BE    SPX                                                              
*                                                                               
SP08     DS    0H                  SET LENGTHS FOR KEYCHK                       
         ZIC   RE,NDLVNPOS         NODE START                                   
         ZIC   R5,NDNODLN          PLUS NODE LENGTH                             
         AR    R5,RE                                                            
         ZIC   RE,NDLVCLN          PLUS CODE LENGTH FOR LEVEL                   
         AR    R5,RE                                                            
         BCTR  R5,R0                                                            
         STH   R5,KEYCL            LENGTH FOR THIS LEVEL                        
*                                  DO FOR HIGHER LEVEL ALSO                     
         SH    R9,=Y(NDLVTABL)                                                  
         ZIC   RE,NDLVNPOS         NODE START                                   
         ZIC   R5,NDNODLN          PLUS NODE LENGTH                             
         AR    R5,RE                                                            
         ZIC   RE,NDLVCLN          PLUS CODE LENGTH FOR LEVEL                   
         AR    R5,RE                                                            
         BCTR  R5,R0                                                            
         STH   R5,KEYCLB           LENGTH FOR HIGHER LEVEL                      
         LA    R9,NDLVTABL(R9)                                                  
*                                                                               
         CLI   SPMODE,C'A'                                                      
         BE    SPADD                                                            
         CLI   SPMODE,C'D'                                                      
         BE    SPDEL                                                            
         B     SPRENM                                                           
         EJECT                                                                  
*        SPADD - ADD POSITIONING                                                
         SPACE 2                                                                
SPADD    DS    0H                                                               
         CLC   =C'ADD',COMMAND     IF ADDING                                    
         BE    SP2                                                              
         CLC   =C'RES',COMMAND     OR RESTORING                                 
         BNE   SP2B                                                             
*                                                                               
SP2      DS    0H                  REMOVE FRST AND LAST ELEMS                   
         BAS   RE,DELFRST          BECAUSE THERE CAN BE NO                      
         BAS   RE,DELLAST          LOWER RECORDS                                
*                                                                               
SP2B     DS    0H                                                               
         OC    PARAM4,PARAM4       IF NO POSITION CODE GIVEN                    
         BNZ   *+12                                                             
         MVI   POS,X'FF'           ADD AT END                                   
         B     SP6                                                              
*                                                                               
         MVC   BASW,PARAM4         BEFORE/AFTER SW                              
         L     R4,PARAM4           A(POSITION CODE)                             
         LA    R4,0(R4)            STRIP HOB                                    
         XC    POS,POS                                                          
         LR    R5,R4                                                            
*                                                                               
SP4      DS    0H                  GET LENGTH OF CODE                           
         CLI   0(R5),C' '                                                       
         BNH   *+12                END                                          
         LA    R5,1(R5)                                                         
         B     SP4                                                              
*                                                                               
         SR    R5,R4                                                            
         BNP   SP6                 CODE OF X'00' = ADD AT START                 
         BCTR  R5,R0                                                            
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   POS(0),0(R4)        SET CODE IN POS                              
*                                                                               
SP6      DS    0H                                                               
         MVC   NDAREC,NDIOA2       USE 2ND IOAREA                               
         CLI   POS,0               ADDING AT START                              
         BE    SP20                                                             
         CLI   POS,X'FF'                                                        
         BE    SP30                ADDING AT END                                
         CLI   BASW,C'A'           ADDING AFTER                                 
         BE    SP40                                                             
         CLI   BASW,C'B'           ADDING BEFORE                                
         BE    SP50                                                             
*                                                                               
         DC    H'0'                                                             
         EJECT                                                                  
*                                  ADD AT START                                 
*                                  ------------                                 
SP20     DS    0H                                                               
*                                  FIND CURRENT FRST AND SET BACK LINK          
         MVC   WKNOD,NDLVNOD                                                    
         MVC   WKCOD,NDLVFRST                                                   
         CLI   WKCOD,C' '                                                       
         BNH   SP22                NO FRST PRESENT                              
         BAS   RE,SETKEY                                                        
         BAS   RE,HIGH                                                          
         LH    RF,KEYCL                                                         
         BAS   RE,KEYCHK2                                                       
         BE    *+6                                                              
         DC    H'0'                FRST NOT FOUND                               
*                                                                               
         BAS   RE,GET                                                           
         BAS   RE,FNDBACK                                                       
         BNE   *+6                                                              
         DC    H'0'                SHOULD NOT FIND BACK                         
*                                                                               
         MVC   WKCOD,NDLVCOD                                                    
         BAS   RE,SETBACK          SET NEW BACK                                 
         BAS   RE,PUT                                                           
*                                                                               
SP22     DS    0H                  SET NEW FRST CODE IN HIGHER LEVEL            
         SH    R9,=Y(NDLVTABL)     BACK UP ONE LEVEL                            
         MVC   WKNC,NDLVNOD        NODE AND CODE                                
         LA    RF,KEY                                                           
         AH    RF,NDDISK                                                        
         MVC   0(4,RF),NDLVDA      DISK ADDRESS OF HIGHER LEVEL                 
*                                **NO-OP DIRECTORY READ                         
*        BAS   RE,SETKEY                                                        
*        BAS   RE,HIGH                                                          
*        LH    RF,KEYCLB                                                        
*        BAS   RE,KEYCHK2                                                       
*        BE    *+6                                                              
*        DC    H'0'                HIGHER LEVEL NOT FOUND                       
*                                                                               
         BAS   RE,GET                                                           
         BAS   RE,DELFRST                                                       
         MVC   WKCOD,NDLVCOD+NDLVTABL                                           
         BAS   RE,SETFRST                                                       
*                                                                               
         BAS   RE,FNDLAST          SEE IF HAVE LAST                             
         BE    SP23                YES                                          
         CLI   NDLVFRST+NDLVTABL,C' '   NO- MUST NOT HAVE HAD FRST              
         BNH   *+6                                                              
         DC    H'0'                                                             
         MVC   WKCOD,NDLVCOD+NDLVTABL   SET LAST = NEW                          
         BAS   RE,SETLAST                                                       
         MVC   NDLVLAST+NDLVTABL,WKCOD                                          
         B     SP23D                                                            
*                                                                               
SP23     DS    0H                                                               
         CLI   NDLVFRST+NDLVTABL,C' '     IF HAVE LAST MUST                     
         BH    *+6                        HAVE HAD FRST                         
         DC    H'0'                                                             
*                                                                               
SP23D    DS    0H                                                               
         BAS   RE,PUT                                                           
         LA    R9,NDLVTABL(R9)     RESET LEVEL POINTER                          
*                                                                               
         MVC   NDAREC,NDIOA        RESET TO USE IOAREA 1                        
         CLC   =C'ADD',COMMAND     UNLESS ADDING                                
         BE    SP23F                                                            
         MVC   KEY,SAVKEY          MUST READ RECORD NOW                         
         BAS   RE,GET                                                           
*                                                                               
SP23F    DS    0H                  SET FWRD LINK ELEM IN NEW RECORD             
         CLI   NDLVFRST,C' '       BUT ONLY IF HAD FRST                         
         BNH   SP24                                                             
         MVC   WKCOD,NDLVFRST                                                   
         BAS   RE,DELFWRD                                                       
         BAS   RE,SETFWRD                                                       
         BAS   RE,DELBACK          ALSO DELETE ANY BACK                         
*                                                                               
SP24     DS    0H                                                               
         MVC   NDLVFRST,NDLVCOD    NEW IS FRST                                  
         B     SPX                                                              
         EJECT                                                                  
*                                  ADD AT END                                   
*                                  ----------                                   
SP30     DS    0H                                                               
*                                  FIND CURRENT LAST AND SET FWRD LINK          
         MVC   WKNOD,NDLVNOD                                                    
         MVC   WKCOD,NDLVLAST                                                   
         CLI   WKCOD,C' '                                                       
         BNH   SP32                NO LAST PRESENT                              
         BAS   RE,SETKEY                                                        
         BAS   RE,HIGH                                                          
         LH    RF,KEYCL                                                         
         BAS   RE,KEYCHK2                                                       
         BE    *+6                                                              
         DC    H'0'                LAST NOT FOUND                               
*                                                                               
         BAS   RE,GET                                                           
         BAS   RE,FNDFWRD                                                       
         BNE   *+6                                                              
         DC    H'0'                SHOULD NOT FIND FWRD                         
*                                                                               
         MVC   WKCOD,NDLVCOD                                                    
         BAS   RE,SETFWRD          SET NEW FWRD                                 
         BAS   RE,PUT                                                           
*                                                                               
SP32     DS    0H                  SET NEW LAST CODE IN HIGHER LEVEL            
         SH    R9,=Y(NDLVTABL)     BACK UP ONE LEVEL                            
         MVC   WKNC,NDLVNOD        NODE AND CODE                                
         LA    RF,KEY                                                           
         AH    RF,NDDISK                                                        
         MVC   0(4,RF),NDLVDA      DISK ADDRESS OF HIGHER LEVEL                 
*                                **NO-OP DIRECTORY READ                         
*        BAS   RE,SETKEY                                                        
*        BAS   RE,HIGH                                                          
*        LH    RF,KEYCLB                                                        
*        BAS   RE,KEYCHK2                                                       
*        BE    *+6                                                              
*        DC    H'0'                HIGHER LEVEL NOT FOUND                       
*                                                                               
         BAS   RE,GET                                                           
         BAS   RE,DELLAST                                                       
         MVC   WKCOD,NDLVCOD+NDLVTABL                                           
         BAS   RE,SETLAST                                                       
*                                                                               
         BAS   RE,FNDFRST          SEE IF HAVE FRST                             
         BE    SP33                YES                                          
         CLI   NDLVLAST+NDLVTABL,C' '  NO- MUST NOT HAVE HAD LAST               
         BNH   *+6                                                              
         DC    H'0'                                                             
         MVC   WKCOD,NDLVCOD+NDLVTABL   SET FRST = NEW                          
         BAS   RE,SETFRST                                                       
         MVC   NDLVFRST+NDLVTABL,WKCOD                                          
         B     SP33D                                                            
*                                                                               
SP33     DS    0H                                                               
         CLI   NDLVLAST+NDLVTABL,C' '    IF HAVE FRST MUST                      
         BH    *+6                       HAVE HAD LAST                          
         DC    H'0'                                                             
*                                                                               
SP33D    DS    0H                                                               
         BAS   RE,PUT                                                           
         LA    R9,NDLVTABL(R9)     RESET LEVEL POINTER                          
*                                                                               
         MVC   NDAREC,NDIOA        RESET TO USE IOAREA 1                        
         CLC   =C'ADD',COMMAND     UNLESS ADDING                                
         BE    SP33F                                                            
         MVC   KEY,SAVKEY          MUST READ RECORD NOW                         
         BAS   RE,GET                                                           
*                                                                               
SP33F    DS    0H                  SET BACK LINK ELEM IN NEW RECORD             
         CLI   NDLVLAST,C' '       BUT ONLY IF HAD LAST                         
         BNH   SP34                                                             
         MVC   WKCOD,NDLVLAST                                                   
         BAS   RE,DELBACK                                                       
         BAS   RE,SETBACK                                                       
         BAS   RE,DELFWRD          ALSO DELETE ANY FWRD                         
*                                                                               
SP34     DS    0H                                                               
         MVC   NDLVLAST,NDLVCOD    NEW IS LAST                                  
         B     SPX                                                              
         EJECT                                                                  
*                                  ADD AFTER                                    
*                                  ---------                                    
SP40     DS    0H                                                               
         CLC   POS,NDLVLAST        IF POS=LAST, ADD AT END                      
         BE    SP30                                                             
*                                                                               
         MVC   WKCOD,POS           READ POS TO SET NEW FWRD                     
         MVC   WKNOD,NDLVNOD                                                    
         BAS   RE,SETKEY                                                        
         BAS   RE,HIGH                                                          
         LH    RF,KEYCL                                                         
         BAS   RE,KEYCHK2                                                       
         BE    *+12                                                             
         MVI   NDERR,NDPMDERR      POSITION CODE NOT FOUND                      
         B     SPX                                                              
*                                                                               
         BAS   RE,GET                                                           
         BAS   RE,FNDFWRD          OLD FRWD                                     
         MVC   NDLVFWRD,WKCOD      SAVE OLD FWRD                                
         BAS   RE,DELFWRD                                                       
         MVC   WKCOD,NDLVCOD                                                    
         BAS   RE,SETFWRD                                                       
         BAS   RE,PUT                                                           
*                                                                               
         MVC   WKCOD,NDLVFWRD     READ OLD FWRD TO SET BACK LINK                
         BAS   RE,SETKEY                                                        
         BAS   RE,HIGH                                                          
         LH    RF,KEYCL                                                         
         BAS   RE,KEYCHK2                                                       
         BE    *+6                                                              
         DC    H'0'                OLD FWRD NOT FOUND                           
*                                                                               
         BAS   RE,GET                                                           
         BAS   RE,DELBACK                                                       
         MVC   WKCOD,NDLVCOD                                                    
         BAS   RE,SETBACK                                                       
         BAS   RE,PUT                                                           
*                                                                               
*                                  NOW ADD LINKS IN NEW RECORD                  
         MVC   NDAREC,NDIOA        RESTORE TO IOAREA 1                          
         CLC   =C'ADD',COMMAND     UNLESS ADDING                                
         BE    SP43F                                                            
         MVC   KEY,SAVKEY          MUST READ RECORD NOW                         
         BAS   RE,GET                                                           
*                                                                               
SP43F    DS    0H                                                               
         MVC   WKCOD,POS           BACK = POS                                   
         BAS   RE,DELBACK                                                       
         BAS   RE,SETBACK                                                       
*                                                                               
         MVC   WKCOD,NDLVFWRD      OLD FWRD IS NEW FWRD                         
         BAS   RE,DELFWRD                                                       
         BAS   RE,SETFWRD                                                       
*                                                                               
         B     SPX                                                              
         EJECT                                                                  
*                                  ADD BEFORE                                   
*                                  ----------                                   
SP50     DS    0H                                                               
         CLC   POS,NDLVFRST        IF POS=FRST, ADD AT START                    
         BE    SP20                                                             
*                                                                               
         MVC   WKCOD,POS           READ POS TO SET NEW BACK                     
         MVC   WKNOD,NDLVNOD                                                    
         BAS   RE,SETKEY                                                        
         BAS   RE,HIGH                                                          
         LH    RF,KEYCL                                                         
         BAS   RE,KEYCHK2                                                       
         BE    *+12                                                             
         MVI   NDERR,NDPMDERR      POSITION CODE NOT FOUND                      
         B     SPX                                                              
*                                                                               
         BAS   RE,GET                                                           
         BAS   RE,FNDBACK          OLD BACK                                     
         MVC   NDLVBACK,WKCOD      SAVE OLD BACK                                
         BAS   RE,DELBACK                                                       
         MVC   WKCOD,NDLVCOD                                                    
         BAS   RE,SETBACK                                                       
         BAS   RE,PUT                                                           
*                                                                               
         MVC   WKCOD,NDLVBACK     READ OLD BACK TO SET FWRD LINK                
         BAS   RE,SETKEY                                                        
         BAS   RE,HIGH                                                          
         LH    RF,KEYCL                                                         
         BAS   RE,KEYCHK2                                                       
         BE    *+6                                                              
         DC    H'0'                OLD BACK NOT FOUND                           
*                                                                               
         BAS   RE,GET                                                           
         BAS   RE,DELFWRD                                                       
         MVC   WKCOD,NDLVCOD                                                    
         BAS   RE,SETFWRD                                                       
         BAS   RE,PUT                                                           
*                                                                               
*                                  NOW ADD LINKS IN NEW RECORD                  
         MVC   NDAREC,NDIOA        RESTORE TO IOAREA 1                          
         CLC   =C'ADD',COMMAND     UNLESS ADDING                                
         BE    SP53F                                                            
         MVC   KEY,SAVKEY          MUST READ RECORD NOW                         
         BAS   RE,GET                                                           
*                                                                               
SP53F    DS    0H                                                               
         MVC   WKCOD,POS           FWRD = POS                                   
         BAS   RE,DELFWRD                                                       
         BAS   RE,SETFWRD                                                       
*                                                                               
         MVC   WKCOD,NDLVBACK      OLD BACK IS NEW BACK                         
         BAS   RE,DELBACK                                                       
         BAS   RE,SETBACK                                                       
*                                                                               
SPX      DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
*        SPDEL - DELETE POSITIONING                                             
         SPACE 2                                                                
SPDEL    DS    0H                                                               
         MVC   NDAREC,NDIOA2       USE 2ND IOAREA                               
*                                                                               
*                                  TEST CODE=FRST                               
*                                  --------------                               
         CLC   NDLVFRST,NDLVCOD                                                 
         BNE   DP10                                                             
*                                  READ HIGHER LEVEL TO SET NEW FRST            
         SH    R9,=Y(NDLVTABL)                                                  
         MVC   WKNC,NDLVNOD        NODE AND CODE                                
         LA    RF,KEY                                                           
         AH    RF,NDDISK                                                        
         MVC   0(4,RF),NDLVDA      DISK ADDRESS OF HIGHER LEVEL                 
*                                **NO-OP DIRECTORY READ                         
*        BAS   RE,SETKEY                                                        
*        BAS   RE,HIGH                                                          
*        LH    RF,KEYCLB                                                        
*        BAS   RE,KEYCHK2                                                       
*        BE    *+6                                                              
*        DC    H'0'                HIGHER LEVEL NOT FOUND                       
*                                                                               
         BAS   RE,GET                                                           
         BAS   RE,DELFRST          GET RID OF CURRENT FRST                      
         XC    WKCOD,WKCOD                                                      
         CLI   NDLVFWRD+NDLVTABL,C' '   TEST ANY FWRD                           
         BH    *+12                                                             
         BAS   RE,DELLAST          IF NOT-GET RID OF LAST ALSO                  
         B     DP6                 (NO LOWER RECORDS LEFT)                      
*                                                                               
         MVC   WKCOD,NDLVFWRD+NDLVTABL   SET NEW FRST                           
         BAS   RE,SETFRST                = CURRENT FWRD                         
*                                                                               
DP6      DS    0H                                                               
         BAS   RE,PUT                                                           
         LA    R9,NDLVTABL(R9)     RETURN TO CURRENT LEVEL                      
         MVC   NDLVFRST,WKCOD      SET NEW FRST (OR CLEAR)                      
         MVC   WKNOD,NDLVNOD                                                    
         CLI   NDLVFWRD,C' '       IF NOT AT END                                
         BNH   *+8                                                              
         BAS   RE,DPFWRD           REMOVE BACK LINK FROM FWRD REC               
         B     DP80                                                             
*                                                                               
*                                  TEST CODE=LAST                               
*                                  --------------                               
DP10     DS    0H                                                               
         CLC   NDLVLAST,NDLVCOD                                                 
         BNE   DP20                                                             
*                                  READ HIGHER LEVEL TO SET NEW LAST            
         SH    R9,=Y(NDLVTABL)                                                  
         MVC   WKNC,NDLVNOD        NODE AND CODE                                
         LA    RF,KEY                                                           
         AH    RF,NDDISK                                                        
         MVC   0(4,RF),NDLVDA      DISK ADDRESS OF HIGHER LEVEL                 
*                                **NO-OP DIRECTORY READ                         
*        BAS   RE,SETKEY                                                        
*        BAS   RE,HIGH                                                          
*        LH    RF,KEYCLB                                                        
*        BAS   RE,KEYCHK2                                                       
*        BE    *+6                                                              
*        DC    H'0'                HIGHER LEVEL NOT FOUND                       
*                                                                               
         BAS   RE,GET                                                           
         BAS   RE,DELLAST          GET RID OF CURRENT LAST                      
         XC    WKCOD,WKCOD                                                      
         CLI   NDLVBACK+NDLVTABL,C' '   TEST ANY BACK                           
         BH    *+12                                                             
         BAS   RE,DELFRST          IF NOT-GET RID OF FRST ALSO                  
         B     DP6                 (NO LOWER RECORDS LEFT)                      
*                                                                               
         MVC   WKCOD,NDLVBACK+NDLVTABL   SET NEW LAST                           
         BAS   RE,SETLAST                = CURRENT BACK                         
*                                                                               
DP16     DS    0H                                                               
         BAS   RE,PUT                                                           
         LA    R9,NDLVTABL(R9)     RETURN TO CURRENT LEVEL                      
         MVC   NDLVLAST,WKCOD      SET NEW LAST (OR CLEAR)                      
         MVC   WKNOD,NDLVNOD                                                    
         CLI   NDLVBACK,C' '       IF NOT AT START                              
         BNH   *+8                                                              
         BAS   RE,DPBACK           REMOVE FWRD LINK FROM BACK REC               
         B     DP80                                                             
*                                                                               
*                                  CODE NEITHER FRST NOR LAST                   
*                                  --------------------------                   
DP20     DS    0H                                                               
*                                  REMOVE THIS LINK FROM CHAIN                  
*                                                                               
         BAS   RE,DPFWRD           REPLACE BACK LINK IN FWRD REC                
         BAS   RE,DPBACK           REPLACE FWRD LINK IN BACK REC                
*                                                                               
DP80     DS    0H                                                               
         MVC   NDAREC,NDIOA        RESTORE IOAREA                               
         B     SPX                                                              
         SPACE 3                                                                
DPBACK   NTR1                      REPLACE FWRD LINK IN BACK RECORD             
         MVC   WKCOD,NDLVBACK      READ BACK TO SET NEW FWRD                    
         BAS   RE,SETKEY                                                        
         BAS   RE,HIGH                                                          
         LH    RF,KEYCL                                                         
         BAS   RE,KEYCHK2                                                       
         BE    *+6                                                              
         DC    H'0'                BACK NOT FOUND                               
*                                                                               
         BAS   RE,GET                                                           
         BAS   RE,DELFWRD          GET RID OF CURRENT FWRD                      
         MVC   WKCOD,NDLVFWRD      AND SET NEW FWRD                             
         CLI   NDLVFWRD,C' '       IF NEEDED                                    
         BNH   *+8                                                              
         BAS   RE,SETFWRD                                                       
         BAS   RE,PUT                                                           
         XIT1                                                                   
         SPACE 3                                                                
DPFWRD   NTR1                      REPLACE BACK LINK IN FWRD REC                
*                                                                               
         MVC   WKCOD,NDLVFWRD      READ FWRD TO SET NEW BACK                    
         BAS   RE,SETKEY                                                        
         BAS   RE,HIGH                                                          
         LH    RF,KEYCL                                                         
         BAS   RE,KEYCHK2                                                       
         BE    *+6                                                              
         DC    H'0'                FWRD NOT FOUND                               
*                                                                               
         BAS   RE,GET                                                           
         BAS   RE,DELBACK          GET RID OF CURRENT BACK                      
         MVC   WKCOD,NDLVBACK      AND SET NEW BACK                             
         CLI   NDLVBACK,C' '       IF NEEDED                                    
         BNH   *+8                                                              
         BAS   RE,SETBACK                                                       
         BAS   RE,PUT                                                           
         XIT1                                                                   
         EJECT                                                                  
*        SPRENM - DELETE POSITIONING                                            
         SPACE 2                                                                
SPRENM   DS    0H                                                               
         CLC   NDLVBACK,C' '       TEST ANY BACK LINK                           
         BH    RP10                YES                                          
*                                  NO- THIS MUST BE FRST                        
*                                  RESET FRST IN HIGHER LEVEL                   
         SH    R9,=Y(NDLVTABL)                                                  
         MVC   WKNC,NDLVNOD        NODE AND CODE                                
         LA    RF,KEY                                                           
         AH    RF,NDDISK                                                        
         MVC   0(4,RF),NDLVDA      DISK ADDRESS OF HIGHER LEVEL                 
*                                **NO-OP DIRECTORY READ                         
*        BAS   RE,SETKEY                                                        
*        BAS   RE,HIGH                                                          
*        LH    RF,KEYCLB                                                        
*        BAS   RE,KEYCHK2                                                       
*        BE    *+6                                                              
*        DC    H'0'                HIGHER LEVEL NOT FOUND                       
*                                                                               
         BAS   RE,GET                                                           
         BAS   RE,DELFRST          GET RID OF CURRENT FRST                      
         MVC   WKCOD,NDLVCOD+NDLVTABL   SET NEW FRST                            
         BAS   RE,SETFRST                = NEW RENAME CODE                      
         MVC   NDLVFRST+NDLVTABL,WKCOD      SET NEW FRST                        
*                                                                               
         CLI   NDLVFWRD+NDLVTABL,C' '   TEST ANY FWRD                           
         BH    RP6                 YES                                          
         BAS   RE,DELLAST          IF NOT-SET NEW LAST ALSO                     
         BAS   RE,SETLAST          (RENAMED RECORD IS ONLY ONE)                 
         MVC   NDLVLAST+NDLVTABL,WKCOD      SET NEW LAST                        
         BAS   RE,PUT                                                           
         B     SPX                                                              
*                                                                               
RP6      DS    0H                                                               
         BAS   RE,PUT                                                           
         LA    R9,NDLVTABL(R9)     RETURN TO CURRENT LEVEL                      
         B     RP20                                                             
*                                                                               
RP10     DS    0H                  HAVE BACK LINK                               
         MVC   WKCOD,NDLVBACK      READ BACK TO SET NEW FWRD                    
         BAS   RE,SETKEY                                                        
         BAS   RE,HIGH                                                          
         LH    RF,KEYCL                                                         
         BAS   RE,KEYCHK2                                                       
         BE    *+6                                                              
         DC    H'0'                BACK NOT FOUND                               
*                                                                               
         BAS   RE,GET                                                           
         BAS   RE,DELFWRD          GET RID OF CURRENT FWRD                      
         MVC   WKCOD,NDLVCOD       AND SET NEW FWRD=RENAME CODE                 
         BAS   RE,SETFWRD                                                       
         BAS   RE,PUT                                                           
*                                                                               
*                                                                               
         CLI   NDLVFWRD,C' '       TEST HAVE FWRD LINK                          
         BH    RP20                YES                                          
*                                  NO- THIS MUST BE LAST                        
*                                  RESET LAST IN HIGHER LEVEL                   
         SH    R9,=Y(NDLVTABL)                                                  
         MVC   WKNC,NDLVNOD        NODE AND CODE                                
         LA    RF,KEY                                                           
         AH    RF,NDDISK                                                        
         MVC   0(4,RF),NDLVDA      DISK ADDRESS OF HIGHER LEVEL                 
*                                **NO-OP DIRECTORY READ                         
*        BAS   RE,SETKEY                                                        
*        BAS   RE,HIGH                                                          
*        LH    RF,KEYCLB                                                        
*        BAS   RE,KEYCHK2                                                       
*        BE    *+6                                                              
*        DC    H'0'                HIGHER LEVEL NOT FOUND                       
*                                                                               
         BAS   RE,GET                                                           
         BAS   RE,DELLAST          GET RID OF CURRENT LAST                      
         MVC   WKCOD,NDLVCOD+NDLVTABL   SET NEW LAST                            
         BAS   RE,SETLAST                = NEW RENAME CODE                      
         BAS   RE,PUT                                                           
         LA    R9,NDLVTABL(R9)     RETURN TO CURRENT LEVEL                      
         MVC   NDLVFRST,WKCOD      SET NEW FRST                                 
         B     SPX                                                              
*                                                                               
RP20     DS    0H                  HAVE FWRD LINK                               
         MVC   WKNOD,NDLVNOD                                                    
         MVC   WKCOD,NDLVFWRD      READ FWRD TO SET NEW BACK                    
         BAS   RE,SETKEY                                                        
         BAS   RE,HIGH                                                          
         LH    RF,KEYCL                                                         
         BAS   RE,KEYCHK2                                                       
         BE    *+6                                                              
         DC    H'0'                FWRD NOT FOUND                               
*                                                                               
         BAS   RE,GET                                                           
         BAS   RE,DELBACK          GET RID OF CURRENT BACK                      
         MVC   WKCOD,NDLVCOD       AND SET NEW BACK=RENAME CODE                 
         BAS   RE,SETBACK                                                       
         BAS   RE,PUT                                                           
         B     SPX                                                              
*                                                                               
         EJECT                                                                  
*                                  VARIOUS POSIITONING ROUTINES                 
         SPACE 2                                                                
FNDFRST  DS    0H                                                               
         MVI   ELCODE,X'B6'        FRST ELEM                                    
         B     FNDALL                                                           
         SPACE 2                                                                
FNDLAST  DS    0H                                                               
         MVI   ELCODE,X'B7'        LAST ELEM                                    
         B     FNDALL                                                           
         SPACE 2                                                                
FNDFWRD  DS    0H                                                               
         MVI   ELCODE,X'B8'        FWRD ELEM                                    
         B     FNDALL                                                           
         SPACE 2                                                                
FNDBACK  DS    0H                                                               
         MVI   ELCODE,X'B9'        BACK ELEM                                    
         B     FNDALL                                                           
         SPACE 2                                                                
FNDALL   DS    0H                                                               
         MVC   WKCOD,SPACES                                                     
         L     R2,NDAREC                                                        
         AH    R2,NDELSTRT                                                      
         LR    RF,RE                                                            
         BAS   RE,NEXTEL2                                                       
         BNE   FNDALL4                                                          
*                                                                               
         ZIC   R1,1(R2)            MOVE TO WKCOD                                
         SH    R1,=H'3'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   WKCOD(0),2(R2)                                                   
         SR    R1,R1               SET CC                                       
*                                                                               
FNDALL4  DS    0H                                                               
         LR    RE,RF                                                            
         BR    RE                                                               
         SPACE 2                                                                
DELFRST  DS    0H                                                               
         MVI   ELCODE,X'B6'        FRST ELEM                                    
         B     DELALL                                                           
         SPACE 2                                                                
DELLAST  DS    0H                                                               
         MVI   ELCODE,X'B7'        LAST ELEM                                    
         B     DELALL                                                           
         SPACE 2                                                                
DELFWRD  DS    0H                                                               
         MVI   ELCODE,X'B8'        FWRD ELEM                                    
         B     DELALL                                                           
         SPACE 2                                                                
DELBACK  DS    0H                                                               
         MVI   ELCODE,X'B9'        BACK ELEM                                    
         B     DELALL                                                           
         SPACE 2                                                                
DELALL   DS    0H                                                               
         LR    RF,RE                                                            
         BAS   RE,DELEL                                                         
         LR    RE,RF                                                            
         BR    RE                                                               
         SPACE 2                                                                
SETFRST  DS    0H                                                               
         MVI   WORK,X'B6'          FRST ELEM                                    
         B     SETALL                                                           
         SPACE 2                                                                
SETLAST  DS    0H                                                               
         MVI   WORK,X'B7'          LAST ELEM                                    
         B     SETALL                                                           
         SPACE 2                                                                
SETFWRD  DS    0H                                                               
         MVI   WORK,X'B8'          FWRD ELEM                                    
         B     SETALL                                                           
         SPACE 2                                                                
SETBACK  DS    0H                                                               
         MVI   WORK,X'B9'          BACK ELEM                                    
         B     SETALL                                                           
         SPACE 2                                                                
SETALL   NTR1                                                                   
         MVC   WORK+2(NDCODL),WKCOD                                             
         LA    RF,WORK+1+NDCODL    GET LENGTH                                   
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,*-8                                                           
         LA    R0,WORK-1                                                        
         SR    RF,R0                                                            
         STC   RF,WORK+1           SET ELEM LENGTH                              
         SR    R2,R2               ADD AT START                                 
         BAS   RE,ADDEL                                                         
         XIT1                                                                   
         EJECT                                                                  
*        PARSE - SEPARATE KEY INTO LEVEL TABLE COMPONENTS                       
*                (R3 AT CURRENT POSITION IN 'KEY')                              
*                                                                               
*                ONE LEVEL FOR EACH CALL                                        
*                FILE IS READ AND CONTROL DATA EXTRACTED                        
         SPACE 2                                                                
PARSE    NTR1                                                                   
         SPACE 1                                                                
         MVI   NDLVERR,0                                                        
         NI    NDLVSTAT,X'BF'      SET OFF NEW STAT                             
*                                                                               
         SR    R5,R5               FOR BYTE COUNTER                             
         LR    R2,R3               SAVE START OF LEVEL                          
*                                                                               
PRS4     DS    0H                                                               
         CLI   0(R3),C' '          END                                          
         BNH   PRS8                                                             
         CLC   0(1,R3),NDDELIM     DELIMITER                                    
         BE    PRS8                                                             
*                                                                               
         LA    R3,1(R3)            BUMP POSITION                                
         B     PRS4                                                             
*                                                                               
PRS8     DS    0H                                                               
         LH    R6,NDNLEVS                                                       
         LA    R6,1(R6)            BUMP LEVEL COUNTER                           
         STH   R6,NDNLEVS                                                       
         CH    R6,NDMXLEVS         TEST VS MAX                                  
         BNH   *+12                                                             
         MVI   NDERR,NDLEVERR                                                   
         B     PRSX                                                             
*                                                                               
         LR    R5,R3                                                            
         SR    R5,R2               LENGTH                                       
         BP    *+16                                                             
*                                                                               
PRS8B    DS    0H                                                               
         MVI   NDERR,NDCDLERR      CODE LENGTH ERROR                            
         MVI   NDLVERR,NDCDLERR                                                 
         B     PRSX                                                             
*                                                                               
         ZIC   RE,NDLVCLN          TEST VS MAX                                  
         CR    R5,RE                                                            
         BH    PRS8B                                                            
*                                                                               
         STC   R6,NDLEV            SET LEVEL                                    
         MVC   XW(NDCODL),SPACES                                                
         BCTR  R5,R0                                                            
         EX    R5,PRSMVC          MOVE CODE TO XW                               
*                                                                               
         CLC   XW(1),NDLIBPRE    TEST LIBRARY START                             
         BNE   PRS8B2                                                           
         CLI   NDLEV,1             MUST BE AT LEVEL ONE                         
         BE    PRS8B2                                                           
         MVI   NDERR,NDLIBERR                                                   
         B     PRSX                                                             
*                                                                               
PRS8B2   DS    0H                                                               
         TM    NDLVSTAT,X'20'      TEST FORCE READ                              
         BNZ   PRS8C                                                            
*                                                                               
         CLI   0(R3),C' '          ALWAYS TREAT LAST AS NEW                     
         BNH   PRS8C                                                            
*                                                                               
         CLI   RERDSW,C'Y'         TEST FORCE REREAD                            
         BE    PRS8C                                                            
*                                                                               
         CLC   NDLVCOD,XW          TEST VS CURRENT                              
         BE    PRS12                                                            
*                                                                               
PRS8C    DS    0H                  MUST GET RECORD FOR THIS LEVEL               
         MVI   RERDSW,C'Y'         FORCE SUBSEQUENT READS                       
         OI    NDLVSTAT,X'40'      SET NEW THIS TIME                            
         NI    NDLVSTAT,X'DF'      SET OFF FORCE READ                           
         MVC   NDLVCOD,XW                                                       
*                                                                               
         OC    NDLVNOD,NDLVNOD     MUST HAVE NODE                               
         BZ    PRS9                                                             
*                                                                               
         MVC   WKNC,NDLVNOD        NODE AND CODE                                
         BAS   RE,SETKEY                                                        
         BAS   RE,HIGH                                                          
         BAS   RE,KEYCHK                                                        
         BE    *+16                                                             
*                                                                               
PRS9     DS    0H                                                               
         MVI   NDERR,NDRNFERR      RECORD NOT FOUND                             
         MVI   NDLVERR,NDRNFERR                                                 
         B     PRSX                                                             
*                                                                               
         BAS   RE,GET                                                           
         BAS   RE,EXLEV            EXTRACT DATA                                 
         BAS   RE,PARSUB           PARAMETER SUBSTITUTION                       
         BNE   PRS9                OMIT THIS RECORD                             
*                                                                               
PRS12    DS    0H                                                               
PRSX     DS    0H                                                               
         XIT1  REGS=(R3)                                                        
*                                                                               
PRSMVC   MVC   XW(0),0(R2)                                                      
*                                                                               
         EJECT                                                                  
*                           CONCAT - CONCATENATE KEY COMPONENTS FROM            
*                                    LEVTAB INTO INPT                           
         SPACE 2                                                                
CONCAT   NTR1                                                                   
         SPACE 2                                                                
         LA    R9,NDLVTAB+NDLVTABL  POINT TO LEVEL 1                            
         MVI   INPT,C' '           PUT KEY BACK TOGETHER                        
         MVC   INPT+1(L'INPT-1),INPT                                            
         LA    R3,INPT                                                          
         MVI   NDCKEYL,0                                                        
         SR    R6,R6                                                            
         ICM   R6,3,NDNLEVS          NO. OF LEVELS                              
         BZ    CONCX                                                            
         LR    R4,R3               SAVE OUTPUT START                            
*                                                                               
CONC4    DS    0H                                                               
         LA    RF,NDLVCOD+NDCODL                                                
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,*-8                                                           
*                                                                               
         LA    R0,NDLVCOD                                                       
         SR    RF,R0                                                            
         EX    RF,CONCMVC          SET CODE IN OUTPUT                           
*                                                                               
         LA    R3,1(RF,R3)                                                      
         MVC   0(1,R3),NDDELIM     SET DELIMITER                                
         LA    R3,1(R3)                                                         
*                                                                               
         LA    R9,NDLVTABL(R9)     NEXT LEVEL                                   
         BCT   R6,CONC4                                                         
*                                                                               
         BCTR  R3,R0                                                            
         MVI   0(R3),C' '          ERASE LAST DELIMITER                         
         SR    R3,R4                                                            
         STC   R3,NDCKEYL          SET OUTPUT KEY LENGTH                        
*                                                                               
CONCX    DS    0H                                                               
         XIT1                                                                   
*                                                                               
CONCMVC  MVC   0(0,R3),NDLVCOD                                                  
         EJECT                                                                  
*        SETNOD - ESTABLISH A NEW NODE                                          
         SPACE 2                                                                
SETNOD   NTR1                                                                   
         SPACE 1                                                                
         MVC   NDHINOD,=3X'FF'                                                  
*                                  READ FOR CURRENT HIGH NODE                   
         MVC   KEY,NDKEY                                                        
         ZIC   RF,NDNDPOS          NODE POS                                     
         ZIC   RE,NDNODLN          PLUS LENGTH                                  
         AR    RF,RE                                                            
         LA    RF,KEY-1(RF)                                                     
         MVI   0(RF),X'0A'         FIRST 10 NODES RESERVED                      
         BAS   RE,HIGH                                                          
         ZIC   RF,NDNDPOS          FOR KEY COMPARE LENGTH                       
         BCTR  RF,R0                                                            
         BAS   RE,KEYCHK2                                                       
         BNE   SN6                                                              
         LA    RF,KEY+1(RF)        POINT TO NODE                                
*                                  MOVE TO NDHINOD                              
         ZIC   R1,NDNODLN          NODE LENGTH                                  
         LR    RE,R1                                                            
         SH    R1,=H'3'                                                         
         LCR   R1,R1                                                            
         LA    R1,NDHINOD(R1)      TO ADDRESS                                   
         BCTR  RE,R0                                                            
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R1),0(RF)       MOVE NODE                                    
*                                                                               
SN6      DS    0H                                                               
         LA    R2,KEY                                                           
         AH    R2,NDDISK                                                        
         LA    R3,NDLVTAB                                                       
         MVC   0(4,R2),NDLVDA-NDLVTABD(R3)  DA OF MASTER RECORD                 
         BAS   RE,GET                                                           
         L     R2,NDAREC                                                        
         AH    R2,NDELSTRT                                                      
*                                                                               
SN8      DS    0H                                                               
         CLI   0(R2),0             EOR                                          
         BNE   *+6                                                              
         DC    H'0'                MISSING MASTER CONTROL ELEM                  
*                                                                               
         CLI   0(R2),X'D1'                                                      
         BE    SN10                                                             
         ZIC   R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     SN8                                                              
*                                                                               
SN10     DS    0H                                                               
         USING NDMASTEL,R2                                                      
         CLC   NDHINOD,NDMHINOD    SAVE 'HIGHEST'                               
         BNH   *+10                                                             
         MVC   NDHINOD,NDMHINOD    HIGH NODE                                    
*                                   DECREMENT NODE                              
         ICM   R5,7,NDHINOD                                                     
         SLL   R5,8                                                             
         SRA   R5,8                                                             
         BCTR  R5,R0                                                            
         STCM  R5,7,NDHINOD                                                     
*                                                                               
         CLC   NDHINOD,=X'00000A'  TEST END OF NODES                            
         BH    *+6                 (10 ARE  RESERVED)                           
         DC    H'0'                                                             
*                                                                               
         MVC   NDMHINOD,NDHINOD                                                 
         BAS   RE,PUT              RE-WRITE MASTER WITH NEW NODE                
*                                                                               
         XC    XW(8),XW               ALIGN NODE IN XW                          
         MVC   XW+3(3),NDHINOD                                                  
         LA    RF,XW+3+3           START OF NODE                                
         ZIC   R1,NDNODLN          NODE LENGTH                                  
         SR    RF,R1               RF IS FROM ADDRESS                           
         MVC   XW(3),0(RF)                                                      
*                                                                               
         MVC   NDLVNOD,XW          SET IN CURRENT LEVEL                         
*                                                                               
         SH    R9,=Y(NDLVTABL)     SET NODE IN BACK RECORD                      
         LA    R2,KEY                                                           
         AH    R2,NDDISK                                                        
         MVC   0(4,R2),NDLVDA                                                   
         LA    R9,NDLVTABL(R9)                                                  
         BAS   RE,GET                                                           
*                                  DELETE ANY EXISTING NODE ELEM                
         MVI   ELCODE,X'B1'                                                     
         BAS   RE,DELEL                                                         
*                                                                               
         XC    WORK(10),WORK                                                    
         MVI   WORK,X'B1'                                                       
         MVI   WORK+1,7                                                         
         MVC   WORK+2(3),NDLVNOD   NODE                                         
*                                                                               
         SR    R2,R2               ADD AT START                                 
         BAS   RE,ADDEL                                                         
         BNE   SNX                                                              
*                                                                               
SN16     DS    0H                                                               
         BAS   RE,PUT              RE-WRITE WITH NEW NODE                       
*                                                                               
SNX      DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
*        INIT - INITIALIZATION                                                  
         SPACE 2                                                                
INIT     DS    0H                                                               
         OC    NDMXLEVS,NDMXLEVS   TEST MAX LEVS SET                            
         BNZ   *+8                                                              
         MVI   NDMXLEVS+1,1        SET TO 1                                     
*                                                                               
         LH    RF,NDKLEN           KEY LENGTH                                   
         BCTR  RF,R0                                                            
         STH   RF,NDKLENM1         -1                                           
*                                                                               
         CLI   NDDELIM,0           SET DELIMETER                                
         BNE   *+8                                                              
         MVI   NDDELIM,C'.'                                                     
*                                  READ MASTER RECORD                           
         MVC   KEY,NDKEY           USE GIVEN MASTER KEY                         
         BAS   RE,HIGH                                                          
         BAS   RE,KEYCHK                                                        
         BE    *+6                                                              
         DC    H'0'                MASTER NOT FOUND                             
*                                                                               
         BAS   RE,GET                                                           
         L     R2,NDAREC                                                        
         AH    R2,NDELSTRT         POINT TO FIRST ELEM                          
         XC    XW,XW                                                            
*                                                                               
INIT8    DS    0H                                                               
         CLI   0(R2),0             EOR                                          
         BE    INIT50                                                           
         CLI   0(R2),X'D1'         MASTER CONTROL ELEM                          
         BE    INIT10                                                           
         CLI   0(R2),X'D2'         CONTROL DEFAULTS                             
         BE    INIT12                                                           
         CLI   0(R2),X'D3'         DESCRIPTION DEFAULTS                         
         BE    INIT14                                                           
*                                                                               
INIT9    DS    0H                                                               
         ZIC   R0,1(R2)            NEXT ELEM                                    
         AR    R2,R0                                                            
         B     INIT8                                                            
         SPACE 2                                                                
INIT10   DS    0H                  MASTER CONTROL ELEM                          
         USING NDMASTEL,R2                                                      
         MVC   NDHINOD,NDMHINOD    SET HIGH NODE                                
         MVC   NDNODLN,NDMNODLN    LENGTH OF NODE                               
         MVC   NDNDPOS,NDMNPOS     NODE POS                                     
         MVC   NDLIBNOD,NDMLIBND   LIBRARY NODE                                 
         MVC   NDLIBPRE,NDMLIBPR   LIBRARY PREFIX                               
         B     INIT9                                                            
         SPACE 2                                                                
INIT12   DS    0H                  CONTROLS DEFAULT ELEM                        
         USING NDDFCTEL,R2                                                      
         ZIC   R9,NDDFCLV          LEVEL NUMBER                                 
         MH    R9,=Y(NDLVTABL)     X TABLE ENTRY LENGTH                         
         LA    R9,NDLVTAB(R9)      + TABLE ADDRESS                              
         MVC   NDLVDCTL,NDDFCTL   CONTROLS                                      
         CLI   NDDFCLV,0           IF FOR LEVEL ZERO                            
         BNE   *+10                                                             
         MVC   XW(L'NDDFCTL),NDDFCTL          SET DEFAULT DEFAULT               
         B     INIT9                                                            
         SPACE 2                                                                
INIT14   DS    0H                  DESCRIPTION DEFAULT ELEM                     
         USING NDDFDSEL,R2                                                      
         ZIC   R9,NDDFDLV          LEVEL NUMBER                                 
         MH    R9,=Y(NDLVTABL)     X TABLE ENTRY LENGTH                         
         LA    R9,NDLVTAB(R9)      + TABLE ADDRESS                              
         LA    R3,NDLVDDSC                                                      
         MVI   0(R3),C' '                                                       
         MVC   1(L'NDLVDDSC-1,R3),0(R3)                                         
         ZIC   R1,1(R2)                                                         
         SH    R1,=H'4'                                                         
         EX    R1,*+8                                                           
         B     INIT9                                                            
         MVC   0(0,R3),NDDFDSC   DESCRIPTION                                    
         SPACE 2                                                                
INIT50   DS    0H                                                               
         OC    NDMXLEVS,NDMXLEVS   TEST FOUND CONTROL ELEM                      
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                              SET DEFAULTS NOT SPECIFICIED                     
*                              TO DEFAULT DEFAULTS                              
         LA    R9,NDLVTAB                                                       
         SR    R5,R5               FOR LEVEL COUNT                              
*                                                                               
INIT52   DS    0H                                                               
         OC    NDLVDCTL,NDLVDCTL   TEST CONTROLS                                
         BNZ   *+10                                                             
         MVC   NDLVDCTL,XW         NO USE MASTER                                
         MVC   NDLVCTL,NDLVDCTL                                                 
         MVC   NDLVKEY,NDKEY       SET KEY                                      
         MVC   NDLVNPOS,NDNDPOS    AND NODE POSITION                            
         MVC   NDLVDESC,NDLVDDSC                                                
*                                                                               
         EDIT  (R5),(3,NDLVLOC),FILL=0  FOR DUMP READABILITY                    
         MVI   NDLVLOC,C'L'                                                     
*                                                                               
         LA    R9,NDLVTABL(R9)      NEXT LEVEL                                  
         LA    R5,1(R5)                                                         
         CH    R5,NDMXLEVS                                                      
         BNH   INIT52                                                           
*                                                                               
         LA    R9,NDLVTAB          RESET TO START                               
         BAS   RE,EXLEV            EXTRACT DATA                                 
*                                                                               
         LA    R9,NDLVTABL(R9)                                                  
         MVC   NDL1NOD,NDLVNOD     SAVE LEVEL 1 NODE                            
         SH    R9,=Y(NDLVTABL)                                                  
*                                                                               
INITX    DS    0H                                                               
         B     NDIOX                                                            
         EJECT                                                                  
*                                  SETKEY                                       
*                                  ------                                       
         SPACE 2                                                                
SETKEY   DS    0H                                                               
         MVC   KEY,NDLVKEY         START WITH TEMPLATE                          
         ZIC   RF,NDLVNPOS         POSITION FOR NODE                            
         LA    RF,KEY(RF)                                                       
         ZIC   R1,NDNODLN          NODE LENGTH                                  
         ZIC   R0,NDLVCLN          PLUS CODE LENGTH                             
         AR    R1,R0                                                            
         BCTR  R1,R0                                                            
         EX    R1,SKCLR            CLEAR CODE AREA                              
*                                                                               
         ZIC   R1,NDNODLN                                                       
         BCTR  R1,R0                                                            
         EX    R1,SKMVC            MOVE IN NODE                                 
*                                                                               
         CLI   WKCOD,C' '          NO CODE                                      
         BNH   SKX                                                              
         LA    R1,WKCOD+NDCODL-1   GET LENGTH                                   
         CLI   0(R1),C' '                                                       
         BH    *+8                                                              
         BCT   R1,*-8                                                           
         LA    R0,WKCOD-1                                                       
         SR    R1,R0                                                            
         CLI   NDLVCALN,C'L'       LEFT ALIGN - SIMPLY MOVE                     
         BNE   SK7                                                              
         ZIC   R0,NDNODLN                                                       
         AR    RF,R0               START OF CODE                                
         B     SK8                                                              
*                                  RIGHT ALIGN-                                 
SK7      DS    0H                                                               
         ZIC   R0,NDLVCLN          MAXIMUM LENGTH                               
         AR    RF,R0               PLUS START                                   
         ZIC   R0,NDNODLN          PLUS NODE LENGTH                             
         AR    RF,R0                                                            
         SR    RF,R1               MINUS ACTUAL LEN = TO ADDRESS                
*                                                                               
SK8      DS    0H                                                               
         BCTR  R1,R0                                                            
         EX    R1,SKMVC2           MOVE CODE                                    
*                                                                               
SKX      DS    0H                                                               
         BR    RE                                                               
*                                                                               
SKCLR    XC    0(0,RF),0(RF)                                                    
SKMVC    MVC   0(0,RF),WKNOD                                                    
SKMVC2   MVC   0(0,RF),WKCOD                                                    
         SPACE 3                                                                
*                                                                               
KEYCHK   DS    0H                  KEY VS KEYSAVE                               
         LH    RF,NDKLENM1                                                      
*                                                                               
KEYCHK2  DS    0H                                                               
         EX    RF,KCCLC                                                         
         BR    RE                                                               
*                                                                               
KCCLC    CLC   KEY(0),KEYSAVE                                                   
         SPACE 3                                                                
NEXTEL   DS    0H                                                               
         SR    R0,R0                                                            
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
NEXTEL2  CLC   ELCODE,0(R2)                                                     
         BER   RE                                                               
         CLI   0(R2),0                                                          
         BNE   NEXTEL                                                           
         LTR   RE,RE                                                            
         BR    RE                                                               
         SPACE 3                                                                
*                                  DELETE ALL ELCODE ELEMS                      
*                                  -----------------------                      
DELEL    NTR1                                                                   
         SPACE 1                                                                
         L     R2,NDAREC                                                        
         AH    R2,NDELSTRT                                                      
*                                                                               
DELEL2   DS    0H                                                               
         BAS   RE,NEXTEL2                                                       
         BNE   DELELX                                                           
*                             NOTE-4TH PARM IS ADDR OF ELEM START(2),           
*                             LENGTH DISP(2), AND MAX REC SIZE(2)               
         GOTO1 NDRECUP,DMCB,(X'FE',NDAREC),(R2),,NDELSTRT                       
         B     DELEL2                                                           
*                                                                               
DELELX   DS    0H                                                               
         XIT1                                                                   
         SPACE 3                                                                
*                                  ADD ELEM IN WORK AT (R2)                     
*                                  ------------------------                     
ADDEL    DS    0H                                                               
         LR    R0,RE                                                            
         LTR   R2,R2               IF R2 NOT SET                                
         BNZ   *+12                                                             
         L     R2,NDAREC                                                        
         AH    R2,NDELSTRT         PUT AT START                                 
*                             NOTE-4TH PARM IS ADDR OF ELEM START(2),           
*                             LENGTH DISP(2), AND MAX REC SIZE(2)               
         GOTO1 NDRECUP,DMCB,(X'FE',NDAREC),WORK,(C'R',(R2)),NDELSTRT            
         CLI   DMCB+8,C'R'                                                      
         BE    *+10                OK, RETURN WITH CC=                          
         MVI   NDERR,NDOVFERR      RECORD TOO BIG                               
         LTR   RE,RE               CC NOT =                                     
*                                                                               
         LR    RE,R0                                                            
         BR    RE                                                               
         SPACE 3                                                                
*                                  MOVE FROM ONE IOA TO ANOTHER                 
*                                  ----------------------------                 
*                                                                               
MOVR1    DS    0H                  MOVE RECORD IN IOA1 TO IOA2                  
         L     RF,NDIOA2           RECIEVING                                    
         L     R0,NDIOA            SENDING                                      
         B     MOVR4                                                            
*                                                                               
MOVR2    DS    0H                  MOVE RECORD IN IOA2 TO IOA1                  
         L     RF,NDIOA                                                         
         L     R0,NDIOA2                                                        
*                                                                               
MOVR4    DS    0H                                                               
         XC    0(64,RF),0(RF)      CLEAR RECIEVING AREA                         
         LR    R1,R0                                                            
         AH    R1,NDRLEN                                                        
         OC    0(2,R1),0(R1)       TEST ANY LENGTH                              
         BNZ   *+10                                                             
         MVC   0(2,R1),NDELSTRT    NO-SET TO ELEM START                         
         LH    R1,0(R1)            RECORD LENGTH                                
         XR    R0,RE            SAVE RE AND POINT RE TO SENDING REC             
         XR    RE,R0                                                            
         XR    R0,RE                                                            
*                                                                               
         MOVE  ((RF),(R1)),(RE)                                                 
*                                                                               
         LR    RE,R0                                                            
         BR    RE                                                               
         EJECT                                                                  
*        SETOVR - SET OVERRIDES AT THIS LEVEL FOR NEXT                          
         SPACE 2                                                                
SETOVR   NTR1                                                                   
         SPACE 1                                                                
         OC    NDSETCTL,NDSETCTL   TEST OVERRIDE OF CONTROLS                    
         BZ    STO10                                                            
         MVI   ELCODE,X'B2'        DELETE ANY CURRENT ELEMS                     
         BAS   RE,DELEL                                                         
         CLI   NDSETCTL,X'FF'      FF = NONE                                    
         BE    STO10                                                            
         XC    WORK,WORK           BUILD NEW ELEM IN WORK                       
         MVI   WORK,X'B2'                                                       
         MVI   WORK+1,L'NDLVDCTL+2                                              
         MVC   WORK+2(L'NDLVDCTL),NDLVDCTL+NDLVTABL   NEXT LEVS DEFAULT         
*                                                                               
         LA    R3,L'NDLVDCTL       REPLACE DEFAULTS                             
         LA    R4,WORK+2           ON BYTE BY BYTE BASIS                        
         LA    R5,NDSETCTL                                                      
*                                                                               
STO4     DS    0H                                                               
         CLI   0(R5),0             TEST OVERRIDE THIS CONTROL                   
         BE    *+10                                                             
         MVC   0(1,R4),0(R5)                                                    
*                                                                               
         LA    R4,1(R4)                                                         
         LA    R5,1(R5)                                                         
         BCT   R3,STO4                                                          
*                                                                               
         SR    R2,R2               ADD AT START                                 
         BAS   RE,ADDEL                                                         
         BNE   SETOVRX             OVERFLOW                                     
*                                                                               
STO10    DS    0H                                                               
         CLI   NDSETDSC,C' '       TEST OVERRIDE OF DESC                        
         BNH   STO20                                                            
         MVI   ELCODE,X'B3'        DELETE ANY CURRENT ELEMS                     
         BAS   RE,DELEL                                                         
         CLI   NDSETDSC,X'FF'      FF = NONE                                    
         BE    STO20                                                            
         XC    WORK,WORK           BUILD NEW ELEM IN WORK                       
         MVI   WORK,X'B3'                                                       
         MVC   WORK+2(L'NDSETDSC),NDSETDSC                                      
         LA    R4,WORK+2+L'NDSETDSC                                             
         CLI   0(R4),C' '                                                       
         BH    *+8                                                              
         BCT   R4,*-8                                                           
*                                                                               
         LA    R0,WORK-1                                                        
         SR    R4,R0                                                            
         STC   R4,WORK+1           ELEM LENGTH                                  
*                                                                               
         SR    R2,R2               ADD AT START                                 
         BAS   RE,ADDEL                                                         
         BNE   SETOVRX             OVERFLOW                                     
*                                                                               
STO20    DS    0H                                                               
         CLI   NDSETLIB,C' '       TEST LIBRARY CALL                            
         BNH   STO30               NO                                           
         CLC   NDLIBPRE,INPT       MUST NOT BE WITHIN A LIB GROUP               
         BNE   *+12                                                             
         MVI   NDERR,NDLIBERR      LIB WITHIN LIB                               
         B     SETOVRX                                                          
*                                                                               
         CLC   NDSETAND(NDNODL+NDCODL),NDATTNOD    SAME AS NOW                  
         BE    STO30                                                            
         CLI   NDSETLIB,X'FF'      FF = NONE                                    
         BE    STO21H                                                           
         CLI   NDLIBCOD,C' '       WAS IT PREVIOUSLY  A LIB CALL                
         BH    STO21H              YES - OK                                     
*                                  IF NOT MUST REMOVE NODE                      
         LA    R9,NDLVTABL(R9)                                                  
         OC    NDLVNOD,NDLVNOD     TEST ANY NODE                                
         BZ    STO21                                                            
         MVC   NDLVCOD,SPACES                                                   
         MVC   WKNC,NDLVNOD        NODE AND CODE                                
         BAS   RE,SETKEY                                                        
         ZIC   RE,NDLVNPOS         NODE POS                                     
         ZIC   R5,NDNODLN          PLUS LENGTH                                  
         AR    R5,RE                                                            
         BCTR  R5,R0               LENGTH FOR KEY COMPARE (THRU NODE)           
*                                                                               
         BAS   RE,HIGH                                                          
         LR    RF,R5                                                            
         BAS   RE,KEYCHK2                                                       
         BNE   STO21               OK TO REMOVE NODE                            
*                                                                               
         MVI   NDERR,NDLMXERR      MIXING LIB AND REG                           
         B     SETOVRX                                                          
*                                                                               
STO21    DS    0H                                                               
         SH    R9,=Y(NDLVTABL)     RETURN TO RIGHT LEVEL                        
*                                                                               
STO21H   DS    0H                                                               
         MVC   NDATTNOD(NDNODL+NDCODL),NDSETAND   SET LIB NODE + CODE           
         CLI   NDSETLIB,X'FF'      BUT IF NONE                                  
         BNE   *+10                                                             
         MVC   NDLIBCOD,SPACES     THEN CLEAR                                   
         MVI   ELCODE,X'B4'        DELETE ANY CURRENT ELEMS                     
         BAS   RE,DELEL                                                         
         CLI   NDSETLIB,X'FF'      FF = NONE                                    
         BE    STO30                                                            
         XC    WORK,WORK           BUILD NEW ELEM IN WORK                       
         MVI   WORK,X'B4'                                                       
         MVC   WORK+2(NDNODL),NDSETAND    ATTACHMENT NODE                       
         MVC   WORK+5(L'NDSETLIB),NDSETLIB                                      
         LA    R4,WORK+5+L'NDSETLIB                                             
         CLI   0(R4),C' '                                                       
         BH    *+8                                                              
         BCT   R4,*-8                                                           
*                                                                               
         LA    R0,WORK-1                                                        
         SR    R4,R0                                                            
         STC   R4,WORK+1           ELEM LENGTH                                  
*                                                                               
         SR    R2,R2               ADD AT START                                 
         BAS   RE,ADDEL                                                         
         BNE   SETOVRX                                                          
*                                  READ LIBRARY RECORD FOR NODE                 
         MVC   WKNC,NDATTNOD       ATTACHED NODE AND CODE                       
         BAS   RE,SETKEY                                                        
         BAS   RE,HIGH                                                          
         BAS   RE,KEYCHK                                                        
         BE    *+12                                                             
*                                                                               
STO21L   DS    0H                                                               
         MVI   NDERR,NDLNFERR      LIB MEMBER NOT FOUND                         
         B     SETOVRX                                                          
*                                                                               
         MVC   NDAREC,NDIOA2       USE IOA2                                     
         BAS   RE,GET                                                           
         L     R2,NDAREC                                                        
         MVC   NDAREC,NDIOA        RESTORE TO IOA1                              
         AH    R2,NDELSTRT                                                      
*                                                                               
STO22    DS    0H                                                               
         CLI   0(R2),0             EOR                                          
         BE    STO21L              NO NODE=LIB NOT FOUND                        
*                                                                               
         CLI   0(R2),X'B1'                                                      
         BE    STO24                                                            
*                                                                               
         ZIC   R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     STO22                                                            
*                                                                               
STO24    DS    0H                                                               
         USING NDNODEL,R2                                                       
         MVC   DUB(3),NDNODE       NODE ESTABLISED BY LIBRARY                   
*                                  NOW ADD NODE ELEM THIS CURRENT REC           
*                                  DELETE ANY EXISTING NODE ELEM                
         MVI   ELCODE,X'B1'                                                     
         BAS   RE,DELEL                                                         
*                                                                               
         XC    WORK(10),WORK                                                    
         MVI   WORK,X'B1'                                                       
         MVI   WORK+1,7                                                         
         MVC   WORK+2(3),DUB       NODE                                         
*                                                                               
         SR    R2,R2               ADD AT START                                 
         BAS   RE,ADDEL                                                         
*                                                                               
STO30    DS    0H                                                               
SETOVRX  DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
*                                **DIRECTORY CALLS**                            
         SPACE 3                                                                
READ     LA    RF,=C'DMREAD'                                                    
         B     DIRCTRR                                                          
         SPACE 2                                                                
SEQ      LA    RF,=C'DMRSEQ'                                                    
         B     DIRCTRR                                                          
         SPACE 2                                                                
HIGH     LA    RF,=C'DMRDHI'                                                    
         MVC   KEYSAVE,KEY                                                      
         B     DIRCTRR                                                          
         SPACE 2                                                                
ADDDIR   LA    RF,=C'DMADD '                                                    
         B     DIRCTRW                                                          
         SPACE 2                                                                
WRITE    LA    RF,=C'DMWRT '                                                    
         B     DIRCTRW                                                          
         SPACE 2                                                                
DIRCTRR  MVI   DMGW,C'N'           NOT A WRITE                                  
         B     DIRCTRY                                                          
         SPACE 2                                                                
DIRCTRW  MVI   DMGW,C'Y'           IS A WRITE                                   
         SPACE 2                                                                
DIRCTRY  NTR1                                                                   
         MVI   TRCFTYP,C'D'                                                     
         MVC   TRCKEY,KEY                                                       
         ST    RF,DMCB                                                          
         LA    RF,NDDIRNAM                                                      
         ST    RF,DMCB+4                                                        
         MVC   DMCB(1),DMINBTS                                                  
         MVI   DMCB+8,0                                                         
         CLI   DMGW,C'N'                                                        
         BE    DIRCTRY2                                                         
         CLI   NDWRITE,C'N'        NO WRITES                                    
         BE    DMCHECK                                                          
DIRCTRY2 GOTO1 NDDMGR,DMCB,,,KEY,KEY                                            
         B     DMCHECK                                                          
         SPACE 3                                                                
*                                        **FILE CALLS**                         
         SPACE 3                                                                
GET      LA    RF,=C'GETREC'                                                    
         B     FILER                                                            
         SPACE 2                                                                
PUT      LA    RF,=C'PUTREC'                                                    
         B     FILEW                                                            
         SPACE 2                                                                
ADD      LA    RF,=C'ADDREC'                                                    
         B     FILEW                                                            
         SPACE 2                                                                
FILER    MVI   DMGW,C'N'           NOT A WRITE                                  
         B     FILE                                                             
         SPACE 2                                                                
FILEW    MVI   DMGW,C'Y'           IS A WRITE                                   
         SPACE 2                                                                
FILE     NTR1                                                                   
         MVI   TRCFTYP,C'F'                                                     
         ST    RF,DMCB                                                          
         LA    RF,NDFILNAM                                                      
         ST    RF,DMCB+4                                                        
         MVC   DMCB(1),DMINBTS                                                  
         LA    RE,KEY                                                           
         AH    RE,NDDISK                                                        
         ST    RE,DMCB+8                                                        
         MVC   TRCDISK,0(RE)                                                    
         MVC   LASTDA,0(RE)        SAVE LAST DISK ADDRESS                       
         MVI   DMCB+8,0                                                         
         CLI   DMGW,C'N'                                                        
         BE    FILE2                                                            
         CLI   NDWRITE,C'N'        NO WRITES                                    
         BE    DMCHECK                                                          
*                                                                               
FILE2    GOTO1 NDDMGR,DMCB,,,,NDAREC,DMWRK                                      
         SPACE 2                                                                
*                  DATA MANAGER ERRORS AND EXIT                                 
         SPACE 3                                                                
DMCHECK  DS    0H                                                               
         CLI   NDTRACE,C'Y'        TEST DOING TRACE                             
         BNE   DMCK20                                                           
*                                                                               
         MVI   P,C' '                                                           
         MVC   P+1(131),P                                                       
         MVC   P(3),=C'ND*'                                                     
         L     RF,DMCB                                                          
         MVC   P+4(6),0(RF)                                                     
         L     RF,DMCB+4                                                        
         MVC   P+11(8),0(RF)                                                    
         GOTO1 NDHEXOUT,TRCDMCB,DMCB+8,P+29,1,=C'N'                             
         LH    R4,NDDISK                                                        
         LA    R4,4(R4)            LENGTH INCLUDING DISK ADDR                   
         CLI   TRCFTYP,C'D'                                                     
         BNE   DMCK8                                                            
*                                  DIRECTORY TRACE                              
         L     RF,DMCB                                                          
         CLC   3(3,RF),=C'SEQ'     FOR SEQ PRINT ONLY KEY FOUND                 
         BE    DMCK4D                                                           
         LH    R1,NDKLENM1                                                      
         EX    R1,*+8          PRINT TRCKEY ONLY IF NOT = TO KEY FOUND          
         B     *+10                                                             
         CLC   TRCKEY(0),KEY                                                    
         BNE   DMCK4                                                            
         MVI   P+41,C'='                                                        
         B     DMCK4D                                                           
*                                                                               
DMCK4    DS    0H                                                               
         GOTO1 NDHEXOUT,TRCDMCB,TRCKEY,P+42,(R4)                                
         GOTO1 NDPRINT,TRCDMCB,P,=C'BL01'                                       
         MVI   P,C' '                                                           
         MVC   P+1(131),P                                                       
*                                                                               
DMCK4D   DS    0H                                                               
         GOTO1 NDHEXOUT,TRCDMCB,KEY,P+42,(R4),=C'N'                             
         GOTO1 NDPRINT,TRCDMCB,P,=C'BL01'                                       
         B     DMCK20                                                           
*                                                                               
DMCK8    DS    0H                                                               
         GOTO1 (RF),(R1),TRCDISK,P+20,4                                         
         GOTO1 (RF),(R1),NDAREC,P+42,(R4)                                       
         GOTO1 NDPRINT,TRCDMCB,P,=C'BL01'                                       
*                                                                               
*                                                                               
DMCK20   DS    0H                                                               
         MVC   BYTE,DMCB+8                                                      
         NC    BYTE,DMOUTBTS                                                    
         BZ    *+6                                                              
         DC    H'0'                                                             
         XIT1                                                                   
         SPACE 2                                                                
         EJECT                                                                  
*        LVTRACE - TRACE OF LEVEL TAB AND GO'S                                  
         SPACE 2                                                                
LVTRACE  DS    0H                                                               
         CLI   NDTRACE,C'Y'        IF NO TRACE                                  
         BNER  RE                  RETURN                                       
         SPACE 1                                                                
         NTR1                                                                   
         MVI   P,C' '                                                           
         MVC   P+1(131),P                                                       
*                                                                               
         LA    R2,P                FIRST LINE                                   
         MVC   0(3,R2),=C'ND*'                                                  
*                                                                               
         LA    R2,4(R2)                                                         
         MVC   0(2,R2),=C'L='                                                   
         EDIT  (B1,NDLEV),(2,2(R2)),FILL=0                                      
*                                                                               
         LA    R2,5(R2)                                                         
         MVC   0(5,R2),=C'MODE='                                                
         GOTO1 NDHEXOUT,DMCB,NDMODE,5(R2),1,=C'N'                               
         MVI   7(R2),C'('                                                       
         MVI   12(R2),C')'                                                      
         LA    RF,MODLST                                                        
         LA    R0,MODLSTN                                                       
*                                                                               
LVTR4    DS    0H                                                               
         CLC   NDMODE,0(RF)                                                     
         BE    LVTR6                                                            
         LA    RF,5(RF)                                                         
         BCT   R0,LVTR4                                                         
*                                                                               
LVTR6    DS    0H                                                               
         MVC   8(4,R2),1(RF)       MODE WORD                                    
*                                                                               
         LA    R2,14(R2)                                                        
         MVC   0(4,R2),=C'ERR='                                                 
         GOTO1 NDHEXOUT,DMCB,NDERR,4(R2)                                        
*                                                                               
         LA    R2,7(R2)                                                         
         MVC   0(5,R2),=C'STAT='                                                
         GOTO1 (RF),(R1),NDLVSTAT,5(R2)                                         
*                                                                               
         LA    R2,8(R2)                                                         
         MVC   0(5,R2),=C'CODE='                                                
         MVC   5(12,R2),NDLVCOD                                                 
*                                                                               
         LA    R2,18(R2)                                                        
         MVC   0(5,R2),=C'DESC='                                                
         MVC   5(15,R2),NDLVDESC                                                
*                                                                               
         LA    R2,21(R2)                                                        
         MVC   0(5,R2),=C'NODE='                                                
         GOTO1 NDHEXOUT,DMCB,NDLVNOD,5(R2),3,=C'N'                              
*                                                                               
         LA    R2,12(R2)                                                        
         MVC   0(5,R2),=C'CTLS='                                                
         GOTO1 (RF),(R1),NDLVCTL,5(R2),10,=C'N'                                 
*                                                                               
         GOTO1 NDPRINT,DMCB,P,=C'BL01'                                          
*                                          SECOND LINE                          
         MVI   P,C' '                                                           
         MVC   P+1(131),P                                                       
*                                                                               
         LA    R2,P+38                                                          
*                                                                               
         MVC   0(5,R2),=C'FRST='                                                
         MVC   5(12,R2),NDLVFRST                                                
         LA    R2,18(R2)                                                        
*                                                                               
         MVC   0(5,R2),=C'LAST='                                                
         MVC   5(12,R2),NDLVLAST                                                
         LA    R2,18(R2)                                                        
*                                                                               
         MVC   0(5,R2),=C'FWRD='                                                
         MVC   5(12,R2),NDLVFWRD                                                
         LA    R2,18(R2)                                                        
*                                                                               
         MVC   0(5,R2),=C'BACK='                                                
         MVC   5(12,R2),NDLVBACK                                                
         LA    R2,18(R2)                                                        
*                                                                               
         GOTO1 NDPRINT,DMCB,P,=C'BL01'                                          
*                                                                               
LVTX     DS    0H                                                               
         XIT1                                                                   
*                                                                               
MODLST   DS    0C                                                               
         DC    X'01',C'INIT'                                                    
         DC    X'11',C'FRST'                                                    
         DC    X'12',C'PROC'                                                    
         DC    X'13',C'LAST'                                                    
         DC    X'21',C'VAL '                                                    
         DC    X'FF',C'END '                                                    
MODLSTN  EQU   (*-MODLST)/5                                                     
         DC    X'00',C'****'                                                    
         SPACE 3                                                                
*                                  ERROR MESSAGES                               
*                                  --------------                               
EMSGL    EQU   20                                                               
ERRMSGS  DS    0C                                                               
         DC    AL1(NDLEVERR),CL20'TOO MANY ERRORS     '                         
         DC    AL1(NDCDLERR),CL20'CODE LENGTH ERROR   '                         
         DC    AL1(NDRNFERR),CL20'RECORD NOT FOUND    '                         
         DC    AL1(NDADDERR),CL20'DUPLICATE ON ADD    '                         
         DC    AL1(NDRENERR),CL20'RENAME CODE ON FILE '                         
         DC    AL1(NDPMDERR),CL20'INVALID POSITIONING '                         
         DC    AL1(NDOVFERR),CL20'RECORD TOO BIG      '                         
         DC    AL1(NDDELERR),CL20'INVALID DELETE      '                         
         DC    AL1(NDRESERR),CL20'INVALID RESTORE     '                         
         DC    AL1(NDLIBERR),CL20'INVALID LIBRARY USE '                         
         DC    AL1(NDLNFERR),CL20'LIB CALL NOT FOUND  '                         
         DC    AL1(NDLMXERR),CL20'MIXED LIB AND REG   '                         
         DC    X'FF'                                                            
*                                                                               
SPACES   DC    CL(NDCODL)' '                                                    
         EJECT                                                                  
*        EXTRACT LEVEL DATA FROM CURRENT RECORD                                 
         SPACE 2                                                                
EXLEV    NTR1                                                                   
         SPACE 1                                                                
         CLC   NDLIBLEV,NDNLEVS+1  UNLESS CURRENTLY WITHIN                      
         BL    *+14                LIBRARY CALL                                 
         MVI   NDLIBLEV,0          CLEAR LIBRARY REFERENCE                      
         XC    NDATTNOD,NDATTNOD                                                
         MVC   NDLIBCOD,SPACES                                                  
         MVC   NDLVFWRD,SPACES     CLEAR FWRD LINK                              
         MVC   NDLVBACK,SPACES     CLEAR BACK LINK                              
*                                                                               
         XC    NDLVKEY,NDLVKEY     SET KEY IN LEV TAB                           
         LH    RF,NDKLENM1                                                      
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   NDLVKEY(0),KEY                                                   
         LA    RF,KEY              SET DISK ADDRESS                             
         AH    RF,NDDISK                                                        
         MVC   NDLVDA,0(RF)                                                     
*                                                                               
         LA    R9,NDLVTABL(R9)     **NOTE-MOST DATA FOUND AT GIVEN              
*                                         LEVEL APPLIES TO NEXT LEVEL           
         XC    NDLVNOD,NDLVNOD     CLEAR NODE                                   
         MVI   NDLVERR,0                                                        
         MVI   NDLVSTAT,0                                                       
         MVC   NDLVCTL,NDLVDCTL    INITIALIZE DEFAULTS                          
         MVC   NDLVDESC,NDLVDDSC                                                
         MVC   NDLVKEY,NDKEY       SET KEY                                      
         MVC   NDLVFRST,SPACES     CLEAR FIRST                                  
         MVC   NDLVLAST,SPACES     CLEAR LAST                                   
*                                                                               
         L     R2,NDAREC                                                        
         AH    R2,NDELSTRT                                                      
*                                                                               
EXLEV8   DS    0H                                                               
         CLI   0(R2),0             EOR                                          
         BE    EXLEV50                                                          
         CLI   0(R2),X'B2'         CONTROL ELEM                                 
         BE    EXLEV10                                                          
         CLI   0(R2),X'B3'         DESCRIPTION ELEM                             
         BE    EXLEV12                                                          
         CLI   0(R2),X'B1'         NODE ELEM                                    
         BE    EXLEV16                                                          
         CLI   0(R2),X'B4'         LIBRARY ELEM                                 
         BE    EXLEV18                                                          
         CLI   0(R2),X'B6'         POINTER TO FIRST FOR NEXT LEVEL              
         BE    EXLEV20                                                          
         CLI   0(R2),X'B7'         POINTER TO LAST FOR NEXT LEVEL               
         BE    EXLEV22                                                          
         CLI   0(R2),X'B8'         POINTER TO NEXT FOR THIS LEVEL               
         BE    EXLEV24                                                          
         CLI   0(R2),X'B9'         POINTER TO BACK FOR THIS LEVEL               
         BE    EXLEV26                                                          
*                                                                               
*                                                                               
EXLEV9   DS    0H                                                               
         ZIC   R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     EXLEV8                                                           
*                                                                               
EXLEV10  DS    0H                       CONTROL ELEM                            
         USING NDCTLEL,R2                                                       
         MVC   NDLVCTL,NDCCTL                                                   
         MVC   NDLVKEY,NDKEY       SET TO KEY                                   
         B     EXLEV9                                                           
*                                                                               
EXLEV12  DS    0H                       DESCRIPTION ELEM                        
         LA    R3,NDLVDESC                                                      
         MVI   0(R3),C' '                                                       
         MVC   1(L'NDLVDESC-1,R3),0(R3)                                         
         ZIC   R1,1(R2)                                                         
         SH    R1,=H'3'                                                         
         EX    R1,EXLMVC                                                        
         B     EXLEV9                                                           
*                                                                               
EXLEV16  DS    0H                  NODE ELEM                                    
         USING NDNODEL,R2                                                       
         MVC   NDLVNOD,NDNODE                                                   
         B     EXLEV9                                                           
*                                                                               
EXLEV18  DS    0H                  LIBRARY CALL ELEM                            
         CLC   =C'DELALL',COMMAND    FOR DELALL COMMAND                         
         BE    EXLEV9              DO NOT SET LIBRARY CALL                      
         USING NDLIBEL,R2                                                       
         MVC   NDATTNOD,NDLLIBND                                                
         ZIC   RF,1(R2)            ELEM LENGTH                                  
         SH    RF,=H'3'                                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   NDLIBCOD(0),NDLLIBCD                                             
         MVC   NDLIBLEV,NDNLEVS+1  SET LEVEL OF CALL                            
         B     EXLEV9                                                           
*                                                                               
EXLEV20  DS    0H                  SET FIRST FOR NEXT LEVEL                     
         USING NDFRSTEL,R2                                                      
         ZIC   RF,1(R2)            ELEM LENGTH                                  
         SH    RF,=H'3'                                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   NDLVFRST(0),NDFRSTCD                                             
         B     EXLEV9                                                           
*                                                                               
EXLEV22  DS    0H                  SET LAST FOR NEXT LEVEL                      
         USING NDLASTEL,R2                                                      
         ZIC   RF,1(R2)            ELEM LENGTH                                  
         SH    RF,=H'3'                                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   NDLVLAST(0),NDLASTCD                                             
         B     EXLEV9                                                           
*                                                                               
EXLEV24  DS    0H                  SET NEXT FOR THIS LEVEL                      
         USING NDFWRDEL,R2                                                      
         SH    R9,=Y(NDLVTABL)     BACK UP                                      
         ZIC   RF,1(R2)            ELEM LENGTH                                  
         SH    RF,=H'3'                                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   NDLVFWRD(0),NDFWRDCD                                             
         LA    R9,NDLVTABL(R9)     RESTORE                                      
         B     EXLEV9                                                           
*                                                                               
EXLEV26  DS    0H                  SET BACK FOR THIS LEVEL                      
         USING NDBACKEL,R2                                                      
         SH    R9,=Y(NDLVTABL)     BACK UP                                      
         ZIC   RF,1(R2)            ELEM LENGTH                                  
         SH    RF,=H'3'                                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   NDLVBACK(0),NDBACKCD                                             
         LA    R9,NDLVTABL(R9)     RESTORE                                      
         B     EXLEV9                                                           
*                                                                               
EXLMVC   MVC   0(0,R3),2(R2)                                                    
         SPACE 2                                                                
EXLEV50  DS    0H                                                               
*                                                                               
EXLEVX   DS    0H                                                               
         XIT1                                                                   
         SPACE 2                                                                
         EJECT                                                                  
*        PARAMETER SUBSTITUTION ROUTINE                                         
         SPACE 2                                                                
PARSUB   DS    0H                                                               
         CLI   NDLIBLEV,0          TEST ANY LIB CALL                            
         BER   RE                                                               
         CLC   NDLIBLEV,NDNLEVS+1  TEST THERE YET                               
         BNH   PARSUBN             NO- DONE                                     
         CR    RE,RE               SET CC OK                                    
         BR    RE                                                               
*                                                                               
PARSUBN  NTR1                                                                   
*                                                                               
         BNE   *+12                                                             
         BAS   RE,MOVR1            IF AT THIS LEVEL SAVE CALLING REC            
         B     PSX                                                              
*                                  IF ABOVE THIS LEVEL TRY PARAM SUBS           
*                                  FIRST MAKE SURE STILL HAVE                   
*                                  RECORD OF LEVEL DOING CALL                   
*                                                                               
         ZIC   R9,NDLIBLEV         POINT TO LEVEL OF CALL                       
         MH    R9,=Y(NDLVTABL)                                                  
         LA    R9,NDLVTAB(R9)                                                   
*                                                                               
         L     R2,NDIOA2           POINT TO WHERE REC SHOULD BE                 
         LH    RF,NDKLENM1                                                      
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   NDLVKEY(0),0(R2)    TEST KEY OK                                  
         BE    PS2                                                              
*                                  NO- RE-READ RECORD                           
         LA    RF,KEY                                                           
         AH    RF,NDDISK                                                        
         MVC   0(4,RF),NDLVDA      DISK ADDRESS                                 
         MVC   NDAREC,NDIOA2       READ INTO 2ND IOA                            
         BAS   RE,GET                                                           
         MVC   NDAREC,NDIOA        RESTORE TO NORMAL IO AREA                    
*                                                                               
PS2      DS    0H                                                               
         BAS   RE,PSOMCK           FIRST CHECK FOR OMITS                        
         BNE   PSNOX               THIS RECORD TO BE OMITTED                    
*                                                                               
*                                  LOOK FOR PARAM ID AND REPLACEMENT            
*                                  ELEMENT PAIRS                                
         L     R2,NDIOA2           IOAREA 2 HAS LIB CALL LEVEL REC              
         AH    R2,NDELSTRT                                                      
*                                                                               
PS4      DS    0H                                                               
         CLI   0(R2),0             EOR                                          
         BE    PSX                                                              
         CLI   0(R2),X'BA'         PAR ID ELEM                                  
         BE    PS5                                                              
*                                                                               
PS4B     DS    0H                  NEXT ELEM                                    
         ZIC   R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     PS4                                                              
*                                                                               
PS5      DS    0H                                                               
         CLI   1(R2),L'PARID+2                                                  
         BNH   *+6                                                              
         DC    H'0'                BAD PARAM ID ELEM                            
*                                                                               
         ZIC   R5,1(R2)                                                         
         SH    R5,=H'3'                                                         
         XC    PARID,PARID                                                      
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   PARID+2(0),2(R2)                                                 
         MVC   PARID(2),=C'&&&&'                                                
         LA    R5,3(R5)            R5 HAS LENGTH                                
*                                                                               
         LA    R3,0(R2,R5)         POINT TO NEXT ELEM                           
         CLI   0(R3),X'BB'                                                      
         BE    *+6                                                              
         DC    H'0'                MISSING REPLACEMENT ELEM                     
         ZIC   R6,1(R3)                                                         
         SH    R6,=H'2'            R6 HAS REPLACEMENT LENGTH                    
*                                                                               
*                                  SEARCH LIB MEMBER RECORD FOR                 
*                                  PARAMS AND DO REPLACMENT                     
         L     R4,NDIOA            RECORD IS IN IOAREA 1                        
         AH    R4,NDELSTRT                                                      
*                                                                               
PSR4     DS    0H                                                               
         CLI   0(R4),0             EOR                                          
         BE    PS4B                NEXT PARAM ID ELEM                           
         CLI   0(R4),X'B0'         IGNORE NODIO CTL ELEMS B0-BF                 
         BL    PSR6                                                             
         CLI   0(R4),X'BF'                                                      
         BH    PSR6                                                             
*                                                                               
PSR4B    DS    0H                                                               
         ZIC   R0,1(R4)                                                         
         AR    R4,R0                                                            
         B     PSR4                                                             
*                                                                               
PSR6     DS    0H                                                               
         ZIC   RF,1(R4)                                                         
         BCTR  RF,R0                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   ELWRK(0),0(R4)      SET WHOLE ELEM IN ELWRK                      
         BCTR  RF,R0               RF= LENGTH OF 'TEXT'                         
         ST    RF,DMCB+4           SET AS 2ND PARAM                             
         ST    RF,FULL             SAVE OLD LENGTH                              
*                                                                               
         GOTO1 NDTSCAN,DMCB,ELWRK+2,,((R5),PARID),                     X        
               ((R6),2(R3)),0                                                   
*                                                                               
         OC    DMCB+16(4),DMCB+16  TEST ANY CHANGE                              
         BZ    PSR4B               NO-NEXT MEMBER ELEM                          
*                                                                               
         CLC   FULL,DMCB+4         TEST LENGTH CHANGE                           
         BE    PSR8                NO                                           
         CLC   DMCB+4(4),=F'253'   YES TEST TOO LARGE                           
         BNH   *+12                                                             
*                                                                               
PSR7     DS    0H                                                               
         MVI   NDERR,NDOVFERR      ELEM/RECORD OVERFLOW                         
         B     PSX                                                              
*                                                                               
         L     R6,DMCB+4           R6 HAS NEW LENGTH                            
         LA    R6,2(R6)                                                         
         STC   R6,ELWRK+1                                                       
*                                  DELETE OLD ELEM                              
         GOTO1 NDRECUP,DMCB,(X'FE',NDIOA),(R4),,NDELSTRT                        
*                                                                               
         GOTO1 (RF),(R1),,ELWRK,(C'R',(R4)),NDELSTRT                            
         CLI   DMCB+8,C'R'         TEST RECORD TOO BIG                          
         BNE   PSR7                                                             
*                                                                               
         B     PSR4B               NEXT LIB MEMBER ELEM                         
*                                                                               
PSR8     DS    0H                  NO LENGTH CHANGE                             
         ZIC   RF,ELWRK+1                                                       
         BCTR  RF,R0                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),ELWRK                                                    
         B     PSR4B               NEXT LIB MEMBER ELEM                         
*                                                                               
PSNOX    DS    0H                                                               
         LTR   RE,RE               CC - OMIT RECORD                             
         B     PSXX                                                             
*                                                                               
PSX      DS    0H                                                               
         CR    RE,RE               CC - DO NOT OMIT                             
PSXX     DS    0H                                                               
         XIT1                                                                   
         SPACE 3                                                                
*        TEST LIBRARY MEMBER TO BE OMITTED                                      
*                                                                               
PSOMCK   NTR1                                                                   
         XC    XW,XW                                                            
*                                  LOOK FOR OMIT ELEMS                          
         L     R2,NDIOA2           IOAREA 2 HAS LIB CALL LEVEL REC              
         AH    R2,NDELSTRT                                                      
*                                                                               
PSO4     DS    0H                                                               
         CLI   0(R2),0             EOR                                          
         BE    PSOX                                                             
         CLI   0(R2),X'BC'         OMIT CODE ELEM                               
         BE    PSO5                                                             
*                                                                               
PSO4B    DS    0H                  NEXT ELEM                                    
         ZIC   R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     PSO4                                                             
*                                                                               
PSO5     DS    0H                                                               
         USING NDLBOMEL,R2                                                      
         CLI   XW,0                TEST HAVE BUILT KEY TO COMPARE               
         BNZ   *+8                                                              
         BAS   RE,PSOSET           SET MEMBER PART OF 'KEY'                     
*                                                                               
         ZIC   RF,XW               LENGTH OF 'KEY'                              
         ZIC   R5,1(R2)                                                         
         SH    R5,=H'2'            LENGTH OF OMIT CODE                          
         CR    R5,RF               COMPARE LENGTHS                              
         BH    PSO4B               IF OMIT CODE LENGTH GREATER                  
*                                  DO NOT OMIT-TRY ANOTHER ELEM                 
         BCTR  R5,R0               ELSE COMPARE CODES                           
         EX    R5,*+8                                                           
         B     *+10                                                             
         CLC   NDLBOMCD(0),XW+1    IF NOT EQUAL - DO NOT OMIT                   
         BNE   PSO4B               GET NEXT OMIT ELEM                           
*                                                                               
         LA    R5,1(R5)            CODES ARE EQUAL                              
         CR    R5,RF               IF LENGTHS ALSO EQUAL                        
         BE    PSONO               OMIT THIS RECORD                             
*                                                                               
         LA    R5,XW+1(R5)         OMIT CODE LENGTH SHORTER                     
         CLC   0(1,R5),NDDELIM     NEXT POS IN 'KEY' MUST BE                    
*                                  A DELIMETER (OMIT ALL AT THIS                
*                                  LEVEL OR LOWER)                              
         BNE   PSO4B               DO NOT OMIT-GET ANOTHER ELEM                 
*                                                                               
PSONO    DS    0H                                                               
         LTR   RE,RE               SET CC- OMIT                                 
         B     PSOXX                                                            
*                                                                               
PSOX     DS    0H                                                               
         CR    RE,RE               SET CC                                       
*                                                                               
PSOXX    DS    0H                                                               
         XIT1                                                                   
         SPACE 2                                                                
PSOSET   NTR1                                                                   
         ZIC   R9,NDLIBLEV         START AT ONE BEYOND CALL LEVEL               
         MH    R9,=Y(NDLVTABL)                                                  
         LA    R9,NDLVTAB+NDLVTABL(R9)                                          
*                                                                               
         LA    R3,XW+1                                                          
         LH    R6,NDNLEVS          NUMBER OF LEVELS                             
         ZIC   RE,NDLIBLEV         LESS LEVEL AFTER CALL                        
         SR    R6,RE               IS NUMBER TO DO                              
         BP    *+6                                                              
         DC    H'0'                                                             
         LR    R4,R3               SAVE OUTPUT START                            
*                                                                               
PSOS4    DS    0H                                                               
         LA    RF,NDLVCOD+NDCODL                                                
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,*-8                                                           
*                                                                               
         LA    R0,NDLVCOD                                                       
         SR    RF,R0                                                            
         EX    RF,PSOMVC           SET CODE IN OUTPUT                           
*                                                                               
         LA    R3,1(RF,R3)                                                      
         MVC   0(1,R3),NDDELIM     SET DELIMITER                                
         LA    R3,1(R3)                                                         
*                                                                               
         LA    R9,NDLVTABL(R9)     NEXT LEVEL                                   
         BCT   R6,PSOS4                                                         
*                                                                               
         BCTR  R3,R0                                                            
         MVI   0(R3),C' '          ERASE LAST DELIMITER                         
         SR    R3,R4                                                            
         STC   R3,XW               SET OUTPUT KEY LENGTH                        
*                                                                               
         XIT1                                                                   
*                                                                               
PSOMVC   MVC   0(0,R3),NDLVCOD                                                  
         B     PSOXX                                                            
*                                                                               
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*        NODIO WORK DSECT                                                       
         SPACE 2                                                                
NDWKD    DSECT                                                                  
*                                                                               
DMCB     DS    6F                                                               
*                                                                               
         DS    0F                                                               
PARAMS   DS    0XL24                                                            
PARAM1   DS    F                                                                
PARAM2   DS    F                                                                
PARAM3   DS    F                                                                
PARAM4   DS    F                                                                
PARAM5   DS    F                                                                
PARAM6   DS    F                                                                
*                                                                               
WORK     DS    XL64                                                             
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
XW       DS    XL64                                                             
P        DS    CL132                                                            
WKNC     DS    0CL(NDNODL+NDCODL)                                               
WKNOD    DS    XL(NDNODL)                                                       
WKCOD    DS    CL(NDCODL)                                                       
*                                                                               
POS      DS    CL(NDCODL)                                                       
BASW     DS    CL1                                                              
SPMODE   DS    CL1                                                              
KEYCL    DS    H                                                                
KEYCLB   DS    H                                                                
*                                                                               
         DS    0D                                                               
KEY      DS    XL64                                                             
KEYSAVE  DS    XL64                                                             
TRCKEY   DS    XL64                                                             
SAVKEY   DS    XL64                                                             
TRCDMCB  DS    6F                                                               
TRCDISK  DS    XL4                                                              
TRCFTYP  DS    C                                                                
LASTDA   DS    XL4                                                              
SAVLDA   DS    XL4                                                              
DMGW     DS    C                                                                
ELCODE   DS    X                                                                
RNDLSW   DS    C                                                                
*                                                                               
ELWRK    DS    XL256                                                            
PARID    DS    CL20                                                             
*                                                                               
NDWKL    EQU   *-NDWKD                                                          
*                                                                               
MAXCDLN  EQU   20                                                               
         EJECT                                                                  
*                                                                               
       ++INCLUDE NODIOELS                                                       
         EJECT                                                                  
*                                                                               
       ++INCLUDE NODBLKD                                                        
*                                                                               
NODBLKD  DSECT                     RETURN TO NODBLKD                            
         ORG   NDWORK              FOR FIELDS THAT NEED TO BE SAVED             
*                                  BUT CLUTTER UP THE USER DSECT                
DMWRK    DS    12D                                                              
NDAREC   DS    A                                                                
NDGORET  DS    F                   A(RETURN AFTER GO)                           
NDGOSAV  DS    4F                  TO BE RESTORED AFTER RETURN                  
INPT     DS    CL64                                                             
COMMAND  DS    CL8                                                              
DMINBTS  DS    X                                                                
DMOUTBTS DS    X                                                                
RERDSW   DS    C                                                                
SQFRST   DS    C                                                                
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'020NODIOS    05/01/02'                                      
         END                                                                    
