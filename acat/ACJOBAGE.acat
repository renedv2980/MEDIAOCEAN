*          DATA SET ACJOBAGE   AT LEVEL 007 AS OF 12/20/11                      
         TITLE 'ACJOBAGE - JOB AGEING SUBROUTINE'                               
*                                                                               
*CATALP ACJOBAGE                                                                
*                                                                               
***********************************************************************         
*  JOB AGEING SUBROUTINE (ACJOBAGE)                                   *         
*                                                                     *         
*      CURRENTLY SUPPORTED AGEING METHODS:                            *         
*        SYSTEM DEFAULT                                               *         
*        FIFO                                                         *         
*        ACTIVITY DATE                                                *         
*        OPEN ITEM AGEING                                             *         
*        UNBILLED                                                     *         
*                                                                     *         
*      FOR DEFINITIONS OF THE AGEING METHODS, SEE THE PRODPAK         *         
*      REFERENCE MANUAL, VOLUME 2, 'JOB AGEING REPORT (AC61)'.        *         
*                                                                     *         
*      CALL THIS MODULE VIA:                                          *         
*        GOTO1    ACJOBAGE,JOBAGEID                                   *         
*                                                                     *         
*      HISTORICAL NOTE:                                               *         
*        ACJOBAGE WAS BASED ON AC61 (MODULE ACREP6102).               *         
*                                                                     *         
*      THE FOLLOWING OPTIONS ARE IN THE CURRENT JOB AGEING REPORT     *         
*      PROGRAM, AC61, BUT ARE NOT YET SUPPORTED IN THIS SUBPROGRAM,   *         
*      ACJOBAGE.  THESE RESTRICTIONS ARE VALID EVEN THOUGH THE        *         
*      INTERFACE BLOCK FOR ACJOBAGE, ACJOBAGEID, CONTAINS A GENERATE  *         
*      TRIAL BALANCE FIELD, JAITRIAL AND SOME CODE TO SUPPORT TRIAL   *         
*      EXISTS:                                                        *         
*                                                                     *         
*      . TRIAL BALANCE REPORT                                         *         
*      . PRINT CLIENT TYPE TABLE     (REQUIRES TRIAL BALANCE REPORT)  *         
*      . TYPE TABLE BY CONTRA-LEDGER (REQUIRES TRIAL BALANCE REPORT)  *         
*                                                                     *         
*      THE FOLLOWING PROFILE OPTION IS INCLUDED IN THE CURRENT JOB    *         
*      AGEING REPORT, AC61, DOCUMENTATION BUT ARE NOT SUPPORTED BY    *         
*      AC61 NOR BY ACJOBAGE:                                          *         
*                                                                     *         
*      . SUPPRESS BILLED CHG - OPEN AGE (Y/N DEFAULT N)               *         
*                                                                     *         
*      THE FOLLOWING OPTION IS SUPPORTED IN THE CURRENT JOB AGEING    *         
*      REPORT, AC61, VIA A PROFILE OPTION AND BY SCRIBE VIA A REQUEST *         
*      SCREEN AND REQUEST CARD OPTION:                                *         
*                                                                     *         
*      . INCLUDE REVERSALS              (Y/N DEFAULT Y)               *         
*                                                                     *         
***********************************************************************         
         EJECT ,                                                                
ACJOBAGE CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,**JAGE**,R9,RR=R5                                              
         SPACE 1                                                                
         USING JOBAGEID,RA         INTERFACE  BLOCK FROM CALLER                 
         USING JOBAGED,RC          AGEING     WORK AREA                         
         SPACE 1                                                                
         LR    RA,R1               INPUT BLOCK                                  
         ST    R5,JAIRELO          RELOCATION FACTOR                            
*                                                                               
         CLI   JAIMODE,JAIINIT     INITIALIZE                                   
         BE    INITIAL                                                          
         L     RC,JAIWORKA         ->    MODULE    WORK AREA                    
         MVC   MODE,JAIMODE        SAVE  MODE                                   
         CLI   MODE,TRANS          TRANSACTION                                  
         BE    TRANSACT                                                         
         CLI   MODE,TOTAL          TOTAL    => RETURN TOTALS                    
         BE    TOTALS                                                           
         CLI   MODE,FINI           FINISHED => FREE   STORAGE                   
         BE    FINISH                                                           
         DC    H'00'                                                            
         SPACE 1                                                                
XIT      XIT1                      EXIT                                         
         EJECT ,                                                                
**********************************************************************          
*        INITIALIZATION                                              *          
**********************************************************************          
         SPACE 1                                                                
INITIAL  DS    0H                                                               
*                                                                               
*                                  ***   INIT THE  MOD  WORK AREA ***           
*                                                                               
         ICM   RC,15,JAIWORKA      ->    WORK AREA                              
         BNZ   INIT10              AVAILABLE, YES, USE  IT                      
         BAS   RE,GETWORKA         GET   MODULE    WORK AREA                    
*                                                                               
INIT10   DS    0H                                                               
         L     RC,JAIWORKA         ->    MODULE    WORK AREA                    
         LR    R0,RC               ->    MODULE    WORK AREA                    
         L     R1,JAIWORKL         SIZE  OF   WORK AREA                         
*        LA    R2,0                FROM  ANY  WHERE                             
         LA    R3,0                SIZE  OF   FROM AREA                         
         MVCL  R0,R2               CLEAR WORK AREA                              
*                                                                               
*                                  MOVE  EYE  CATCHER                           
         MVC   IDJOBAGD,=CL8'JOBAGED'                                           
         MVI   MODE,INIT           INITIALIZE THE  MODE                         
*                                                                               
         ST    RA,AJOBAGEI         SAVE  ADDR OF   JOBAGEI                      
         SPACE 1                                                                
*                                                                               
*                                  ***   INIT ROUTINE   ADDRESSES ***           
*                                                                               
         MVC   ADCOMFAC,JAICMFAC   ADDR  OF   COMFACS                           
         MVC   BINSRCH,JAIBSRCH    ADDR  OF   BINSRCH   ROUTINE                 
         MVC   DATCON,JAIDTCON     ADDR  OF   DATCON    ROUTINE                 
         MVC   ADDAY,JAIADDAY      ADDR  OF   ADDAY     ROUTINE                 
         MVC   APRINT,JAIPRINT     ADDR  OF   PRINT     ROUTINE                 
         MVC   APRNTBL,JAIPRNTB    ADDR  OF   PRNTBL    ROUTINE                 
*                                                                               
*                                  ***   RUN  CONSTANTS ***                     
*                                                                               
*                                  TODAY'S    DATE      (COMPRESSED)            
         GOTO1 DATCON,DMCB,(5,0),(2,TODAYDAT)                                   
         MVC   MOAENDDT,JAIMOAED   MOA   END  DATE      (PACKED   YYMM)         
         MVC   ACTENDDT,JAIACTED   ACTIVITY   END  DATE (COMPRESSED)            
         EJECT ,                                                                
*                                                                               
*                                  ***   INIT TABLE     ADDRESSES ***           
*                                                                               
         L     R1,=A(SEDATES-JOBAGED)                                           
         AR    R1,RC                                                            
         ST    R1,ASEDATES         SAVE  ADDR OF   START/END DATES              
*                                                                               
         L     R1,=A(TIMETAB-JOBAGED)                                           
         AR    R1,RC                                                            
         ST    R1,ATIMETAB         SAVE  ADDR OF   TIME TABLE                   
*                                                                               
         L     R1,=A(BILLTAB-JOBAGED)                                           
         AR    R1,RC                                                            
         ST    R1,ABILLTAB         SAVE  ADDR OF   BILL TABLE                   
*                                                                               
         L     R1,=A(CRTAB-JOBAGED)                                             
         AR    R1,RC                                                            
         ST    R1,ACRTAB           SAVE  ADDR OF   CR   TABLE                   
*                                                                               
         L     R1,=A(DRTAB-JOBAGED)                                             
         AR    R1,RC                                                            
         ST    R1,ADRTAB           SAVE  ADDR OF   DR   TABLE                   
*                                                                               
         L     R1,=A(ODRTB-JOBAGED)                                             
         AR    R1,RC                                                            
         ST    R1,AODRTB           SAVE  ADDR OF   OPEN DR   TABLE              
*                                                                               
         L     R1,=A(OFFSTB-JOBAGED)                                            
         AR    R1,RC                                                            
         ST    R1,AOFFSTB          SAVE  ADDR OF   OFFSET    TABLE              
*                                                                               
         L     R1,=A(OCRTB-JOBAGED)                                             
         AR    R1,RC                                                            
         ST    R1,AOCRTB           SAVE  ADDR OF   OPEN CR   TABLE              
*                                                                               
         L     R1,=A(OCRATB-JOBAGED)                                            
         AR    R1,RC                                                            
         ST    R1,AOCRATB          SAVE  ADDR OF   OPEN CR   ADJUSTED           
*                                                                               
         L     R1,=A(TOTTB-JOBAGED)                                             
         AR    R1,RC                                                            
         ST    R1,ATOTTB           SAVE  ADDR OF   TOTALS    TABLE              
*                                                                               
         L     R1,=A(ASOFTAB-JOBAGED)                                           
         AR    R1,RC                                                            
         ST    R1,AASOFTAB         SAVE  ADDR OF   AS   OF   TABLE              
         EJECT ,                                                                
*                                                                               
*                                  ***   SET  TABLE     MAXIMUMS  ***           
*                                                                               
         L     R1,ATIMETAB         ->    TIME TABLE                             
         LA    R2,MXTIMTAB         GET   MAXIMUM                                
*                                  SAVE  MAXIMUM                                
         ST    R2,TIMTABMX-TIMETAB(,R1)                                         
*                                                                               
         L     R1,ABILLTAB         ->    BILL TABLE                             
         LA    R2,MXBILTAB         GET   MAXIMUM                                
*                                  SAVE  MAXIMUM                                
         ST    R2,BILTABMX-BILLTAB(,R1)                                         
*                                                                               
         L     R1,ACRTAB           ->    CR   TABLE                             
         LA    R2,MXCRTAB          GET   MAXIMUM                                
*                                  SAVE  MAXIMUM                                
         ST    R2,CRTABMAX-CRTAB(,R1)                                           
*                                                                               
         L     R1,ADRTAB           ->    DR   TABLE                             
         LA    R2,MXDRTAB          GET   MAXIMUM                                
*                                  SAVE  MAXIMUM                                
         ST    R2,DRTABMAX-DRTAB(,R1)                                           
*                                                                               
         L     R1,AODRTB           ->    OPEN DR   TABLE                        
         LA    R2,MXODRTB          GET   MAXIMUM                                
*                                  SAVE  MAXIMUM                                
         ST    R2,ODRTBMAX-ODRTB(,R1)                                           
*                                                                               
         L     R1,AOFFSTB          ->    OFFSET    TABLE                        
         LH    R2,=Y(MXOFFSTB)         GET   MAXIMUM                            
*                                  SAVE  MAXIMUM                                
         ST    R2,OFFSTBMX-OFFSTB(,R1)                                          
*                                                                               
         L     R1,AOCRTB           ->    OPEN CR   TABLE                        
         LA    R2,MXOCRTB          GET   MAXIMUM                                
*                                  SAVE  MAXIMUM                                
         ST    R2,OCRTBMAX-OCRTB(,R1)                                           
*                                                                               
         L     R1,AOCRATB          ->    OPEN CR   ADJUSTED  TABLE              
         LA    R2,MXOCRATB         GET   MAXIMUM                                
*                                  SAVE  MAXIMUM                                
         ST    R2,OCRTBMAX-OCRTB(,R1)                                           
*                                                                               
         L     R1,ATOTTB           ->    TOTALS    TABLE                        
         LA    R2,MXTOTTB          GET   MAXIMUM                                
*                                  SAVE  MAXIMUM                                
         ST    R2,TOTTBMAX-TOTTB(,R1)                                           
         EJECT ,                                                                
*                                                                               
*                                  ***   GET  THE  PARAMETERS ***               
*                                                                               
         MVC   METHOD,JAIMTHD      METHOD                                       
         CLI   METHOD,SPACE        BLANK ?                                      
         BNE   *+8                 NO,   SKIP                                   
         MVI   METHOD,MTHDEFLT     YES,  USE  DEFAULT                           
*                                                                               
*        MVC   SUPLASTN,JAISUPLN   SUPPRESS   LAST NIGHT'S BILLED ITEMS         
*        CLI   SUPLASTN,YES        OPTION     YES  ?                            
*        BE    *+8                 YES,  SKIP                                   
*        MVI   SUPLASTN,NO         NO,   DEFAULT   =    NO                      
*                                                                               
*        MVC   REVERSAL,JAIREVER   INCLUDE    REVERSALS                         
*        CLI   REVERSAL,NO         OPTION     NO   ?                            
*        BE    *+8                 YES,  SKIP                                   
*        MVI   REVERSAL,YES        NO,   DEFAULT   =    YES                     
*                                                                               
         MVC   TRIAL,JAITRIAL      GENERATE   TRIAL     BALANCE                 
         CLI   TRIAL,YES           OPTION     YES  ?                            
         BE    *+8                 YES,  SKIP                                   
         MVI   TRIAL,NO            NO,   DEFAULT   =    NO                      
*                                                                               
         MVC   TIMCHRGS,JAITIMCH   INCLUDE    TIME CHARGES                      
*                                                                               
         MVC   AGEBYTXD,JAIAGETX   AGE   BY   TRANSACTION    DATE               
         CLI   AGEBYTXD,YES        OPTION     YES  ?                            
         BE    INIT40              YES,  SKIP                                   
         CLI   AGEBYTXD,JAIAGEBT   OPTION     BOTH ?                            
         BE    INIT40              YES,  SKIP                                   
         MVI   AGEBYTXD,NO         NO,   DEFAULT   =    NO                      
*                                                                               
INIT40   DS    0H                                                               
         MVC   DEBUGSW,JAIDEBUG    DEBUG RUN                                    
         CLI   DEBUGSW,YES         OPTION     YES  ?                            
         BE    *+8                 YES,  SKIP                                   
         MVI   DEBUGSW,NO          NO,   DEFAULT   =    NO                      
*                                                                               
         EJECT ,                                                                
         CLI   JAISEDT#,0          ANY   START/END DATES                        
         BNE   *+6                 YES,  CONTINUE                               
         DC    H'00'               NO,   ABEND                                  
         CLI   JAISEDT#,MAX#DATS   TOO   MANY START/END DATES                   
         BNH   *+6                 YES,  CONTINUE                               
         DC    H'00'               NO,   ABEND                                  
*                                                                               
         L     R2,ASEDATES         OUR   ADDR START/END DATE PAIRS              
         L     R3,JAISEDAT         USER  ADDR START/END DATE PAIRS              
         LA    R3,0(,R3)           CLEAR HIGH ORDER     BYTE                    
         LTR   R3,R3               CHECK FOR  ZERO                              
         BNZ   *+6                 HIGH, CONTINUE                               
         DC    H'00'               NO,   ABEND                                  
*                                                                               
         ST    R3,AUSRDATS         SAVE  USER'S    START/END DATE AREA          
         MVC   DATPAIRS,JAISEDT#   SAVE  NUM  OF   START/END DATE PAIRS         
         ZIC   R4,DATPAIRS         LOAD  NUM  OF   START/END/DATE PAIRS         
         MVC   LOWDATE,HIGHVALU    INIT  LOW  DATE                              
*        MVC   HIGHDATE,LOWVALUE   CLEAR HIGH DATE                              
*                                                                               
         USING DATESD,R2           OUR   START/END DATE PAIRS                   
         USING JAIDATED,R3         USER  START/END DATE PAIRS     AREA          
*                                                                               
INIT50   DS    0H                                                               
         CLC   JAISTADT,LOWVALUE   START DATE =    X'000000' ?                  
         BNE   INIT55              NO,   SKIP                                   
         MVC   STARTDT,LOWVALUE    SAVE  DATE VALUE                             
         B     INIT65              DO    END  DATE                              
*                                                                               
INIT55   DS    0H                                                               
         GOTO1 DATCON,DMCB,(1,JAISTADT),(2,STARTDT)                             
         CLC   LOWDATE,STARTDT     NEW   LOW  DATE ?                            
         BNH   INIT60              NO,   SKIP                                   
*                                  YES,  LOW  DATE IS   DATE-1,   SO            
*                                        CONVERT   TO   EBCDIC                  
*                                        SUBTRACT  1    DAY                     
*                                        CONVERT   TO   COMPRESSED              
         GOTO1 DATCON,DMCB,(2,STARTDT),(0,WORK)                                 
         GOTO1 ADDAY,DMCB,WORK,WORK+6,F'-1'                                     
         GOTO1 DATCON,DMCB,(0,WORK+6),(2,LOWDATE)                               
*                                                                               
INIT60   DS    0H                                                               
         CLC   HIGHDATE,STARTDT    NEW   HIGH DATE ?                            
         BNL   INIT65              NO,   SKIP                                   
         MVC   HIGHDATE,STARTDT    SAVE  NEW  HIGH DATE                         
*                                                                               
INIT65   DS    0H                                                               
         CLC   JAIENDDT,HIGHVALU   END   DATE =    X'FFFFFF' ?                  
         BNE   INIT70              NO,   SKIP                                   
         MVC   ENDDT,HIGHVALU      SAVE  DATE VALUE                             
         B     INIT75              DO    NEXT DATE PAIR                         
*                                                                               
INIT70   DS    0H                                                               
         GOTO1 DATCON,DMCB,(1,JAIENDDT),(2,ENDDT)                               
         CLC   LOWDATE,ENDDT       NEW   LOW  DATE ?                            
         BNH   *+10                NO,   SKIP                                   
         MVC   LOWDATE,ENDDT       SAVE  NEW  LOW  DATE                         
         CLC   HIGHDATE,ENDDT      NEW   HIGH DATE ?                            
         BNL   INIT75              NO,   SKIP                                   
*                                  YES,  HIGH DATE IS   DATE+1,   SO            
*                                        CONVERT   TO   EBCDIC                  
*                                        ADD       1    DAY                     
*                                        CONVERT   TO   COMPRESSED              
         GOTO1 DATCON,DMCB,(2,ENDDT),(0,WORK)                                   
         GOTO1 ADDAY,DMCB,WORK,WORK+6,F'1'                                      
         GOTO1 DATCON,DMCB,(0,WORK+6),(2,HIGHDATE)                              
*                                                                               
INIT75   DS    0H                                                               
         CLC   STARTDT,ENDDT       START DATE >    END  DATE ?                  
         BNH   *+6                 NO,   CONTINUE                               
         DC    H'00'               YES,  ABEND                                  
*                                                                               
         ZAP   JAISMMOA,=P'0'      ZERO  MOA  DATE TOTAL                        
         ZAP   JAISMTXD,=P'0'      ZERO  TX   DATE TOTAL                        
*                                                                               
         LA    R2,DATESLNQ(,R2)    OUR   NEXT START/END DATE PAIR               
         LA    R3,JAIDATEQ(,R3)    USER  NEXT START/END DATE PAIR               
         BCT   R4,INIT50           LOOP  NEXT START/END DATE PAIR               
*                                                                               
         B     XIT                 EXIT                                         
*                                                                               
         DROP  R2,R3               KEEP  IT   CLEAN                             
         EJECT ,                                                                
**********************************************************************          
*        TRANSACTION                                                 *          
**********************************************************************          
         SPACE 1                                                                
         USING TRNELD,R4           TRANSACTION     ELEMENT                      
         USING TRNRECD,R5          TRANSACTION     RECORD                       
         SPACE 1                                                                
TRANSACT DS    0H                                                               
         L     R4,JAIATX           ->    TRANSACTION    ELEMENT                 
         ST    R4,ATX              SAVE  ADDRESS                                
         MVC   AJOBRCD,JAIJOBR     ADDR  OF   JOB  RECORD                       
*                                                                               
*                                  ***   GET  PARAMETERS     ***                
*                                                                               
         MVC   SKCNTRAT,JAISKCNT   TREAT CONTRA    AS   TIME                    
         CLI   SKCNTRAT,YES        OPTION     YES  ?                            
         BE    *+8                 YES,  SKIP                                   
         MVI   SKCNTRAT,NO         NO,   DEFAULT   =    NO                      
*                                                                               
         MVC   ZEROBAL,JAI0BAL     INCLUDE    ZERO BALANCE   JOBS               
         CLI   ZEROBAL,YES         OPTION     YES  ?                            
         BE    *+8                 YES,  SKIP                                   
         MVI   ZEROBAL,NO          NO,   DEFAULT   =    NO                      
*                                                                               
         MVC   DATADISP,JAIDDISP   DISP  OF   1ST  ELEMENT   IN   RCD           
*                                                                               
         MVC   APRORATA,JAIPRATA   ADDR  OF   PRORATA   BLOCK                   
*                                                                               
         MVC   TRANMOA,JAITXMOA    TRANSACTION     MOA                          
*                                                                               
         TM    JAIEXJOB,JAIEXXJ    JOB   IS   AN   EXPENSE   JOB  ?             
         BZ    *+8                 NO,   SKIP                                   
         OI    EXPJOB,EXPJB        YES,  SET  INDICATOR                         
*                                                                               
*                                  ***   LOCAL     INIT ***                     
*                                                                               
         LR    R5,R4               FIND  TRANSACTION    RECORD                  
         SH    R5,DATADISP               ADDRESS                                
*                                                                               
         CLC   JOBID,TRNKCULA      SAME  JOB ?                                  
         BE    *+8                 YES,  SKIP                                   
         BAS   RE,CLEAR            CLEAR JOB  RELATED   FLDS AND TABLES         
         MVC   JOBID,TRNKCULA      SAVE  JOB  ID                                
*                                                                               
         CLC   CONTRAID,TRNKCULC   SAME  CONTRA-COMPANY ID ?                    
         BE    *+8                 YES,  SKIP                                   
*                                  UPDATE     OPEN DR   TABLE     OR            
         BAS   RE,UPDATOTB                    OPEN CR   ADJUSTED  TABLE         
         MVC   CONTRAID,TRNKCULC   SAVE  CONTRA-COMPANY ID                      
*                                                                               
         MVI   TPUTSTAT,SPACE      CLEAR TIMEPUT   FLAG                         
         MVI   TRANSTAT,X'00'      CLEAR TRANSACTION    STATUS                  
*                                                                               
         LR    R5,R4               FIND  TRANSACTION    RECORD                  
         SH    R5,DATADISP               ADDRESS                                
*                                                                               
         CLI   TRNEL,TRNELQ        TRANSACTION     ELEMENT  (X'44') ?           
         BNE   TRANEX              NO,   EXIT                                   
*                                                                               
         CLC   TRNKWORK,=C'**'     PURCHASE   ORDER     TRANSACTION ?           
         BE    TRANEX              YES,  EXIT                                   
         EJECT ,                                                                
*                                                                               
*                                  ***   PROCESS   THE  TRANSACTION ***         
*                                                                               
         ZAP   TRANAMNT,TRNAMNT    GET   THE  TRANSACTION    AMOUNT             
         TM    EXPJOB,EXPJB        IS    THIS AN   EXPENSE   JOB  ?             
         BZ    TRAN10              NO,   SKIP                                   
*                                                                               
         USING SCIELD,R4           SUBSIDIARY CASH INFO ELEMENT                 
*                                                                               
TRAN05   DS    0H                                                               
         MVI   ELCODE,SCIELQ       SUBSIDIARY CASH ELEMENT  (X'50')             
         BAS   RE,NEXTEL           GET   THE  ELEMENT                           
         BNE   TRAN10              NONE, CONTINUE                               
*                                                                               
         CLI   SCITYPE,SCITSJXP    AMOUNT     POSTED    TO   EXPENSES ?         
         BNE   TRAN05              NO,   GET  NEXT ELEMENT                      
         ZAP   TRANAMNT,SCIAMNT    YES,  GET  THE  AMOUNT                       
*                                                                               
         USING TRNELD,R4           TRANSACTION     ELEMENT                      
*                                                                               
TRAN10   DS    0H                                                               
         L     R4,ATX              GET   TRANSACTION    ADDRESS                 
         MVC   POSTMOAD,TRANMOA    GET   TRANSACTION    MOA                     
*                                                                               
         ZAP   POSTTRAN,TRANAMNT   BALANCE=   DEBITS -  CREDITS                 
         TM    TRNSTAT,TRNSDR      IS    THIS TX   A    DEBIT ?                 
         BO    TRAN15              YES,  SKIP                                   
         EJECT ,                                                                
*                                                                               
*                                  ***   CREDITS   PROCESSING ***               
*                                                                               
         CLC   MOAENDDT,POSTMOAD   MOA   END  DATE <    TX    MOA               
         BL    TRANEX              YES,  EXIT                                   
*                                                                               
*                                  SAVE  CREDIT    AMOUNT     IN                
         ZAP   POSTTRAN,TRANAMNT         POST OF   TX   AMOUNT                  
         MP    POSTTRAN,=P'-1'     REVERSE    SIGN OF   CREDIT                  
         BAS   RE,TIMEGET          CHECK TBL  FOR  BILLED AMOUNT                
*                                        OF   CHARGES   THAT HAVE BEEN          
*                                        ISOLATED                               
         B     TRAN40              CONTINUE   WITH COMMON    CODE               
         EJECT ,                                                                
*                                                                               
*                                  ***   DEBITS    PROCESSING     ***           
*                                                                               
TRAN15   DS    0H                                                               
         CLI   TRIAL,YES           TRIAL ?                                      
         BE    TRAN20              YES,  SKIP                                   
         CLI   TIMCHRGS,TIMBOTH    INCLUDE    ALL  TIME  CHARGES ?              
         BE    TRAN40              YES,  I    DON'T      NEED TIME              
*                                                                               
TRAN20   DS    0H                                                               
         CLC   TRNKCUNT(2),=C'1R'  CONTRA     U/L  OF    C'1R' ?                
         BE    TRAN25              YES,  IT   IS   TIME                         
         CLI   SKCNTRAT,YES        TREAT SK   CONTRA     AS   TIME ?            
         BNE   TRAN30              NO,   IT   IS   NOT   CLIENT    TIME         
*                                                                               
TRAN25   DS    0H                                                               
         OI    TRANSTAT,TIME       FLAG  TRAN FOR  TRIAL      BAL               
         CLI   TIMCHRGS,TIMBOTH    INCLUDE    ALL  TIME  CHARGES ?              
         BE    TRAN40              YES,  I    DON'T      NEED TIME              
*                                                                               
         MVI   TPUTSTAT,TPUTBILL   SAY   BILL TO   TIMEPUT                      
         BAS   RE,TIMEPUT          SAVE  THE  AMOUNT     BILLED                 
*                                                                               
         CLI   TIMCHRGS,TIMCOOP    OUT   OF   POCKET     ONLY ?                 
         BE    TRANEX              YES,  EXIT                                   
         B     TRAN40              NO,   PASS TIME CHARGES    ALONG             
*                                                                               
TRAN30   DS    0H                                                               
         CLI   TIMCHRGS,TIMBOTH    INCLUDE    ALL  TIME  CHARGES ?              
         BE    TRAN40              YES,  I    DON'T      NEED TIME              
         CLI   TIMCHRGS,TIMTIMEO   TIME  ONLY ?                                 
         BE    TRANEX              YES,  EXIT                                   
         B     TRAN40              NO,   PASS TIME CHARGES    ALONG             
         EJECT ,                                                                
*                                                                               
*                                  ***   COMMON    CR/DR  CODE     ***          
*                                                                               
TRAN40   DS    0H                                                               
         CLI   TRIAL,YES           TRIAL REQUESTED                              
         BE    TRIALTX             YES,  PROCESS   TRIAL     TRANSACT           
*                                                                               
         MVC   POSTTXDT,TRNDATE    USE   YYMMDD    OF    TRAN DATE              
*                                                                               
         CLI   METHOD,MTHDEFLT     DEFAULT    AGEING ?                          
         BNE   TRAN45              NO    SKIP                                   
*                                                                               
*---------------------------------------------------------------------*         
*        DEFAULT AGEING METHOD - PUT  DR'S AND -CR'S IN ONE TABLE AND *         
*                                PUT  CR'S AND -DR'S IN ANOTHER       *         
*                                                                     *         
*        WHY?, SO THAT OFFSET DR'S OFFSET THEIR ORIGINAL CHARGES, AND *         
*        REVERSED BILLS ARE APPLIED TO THEIR REVERSALS, NOT CHARGES   *         
*                                                                     *         
*        NOTE: REMEMBER THAT BY THE TIME WE GET HERE, A CREDITS IS A  *         
*              NEGATIVE NUMBER; THEREFORE, IN THIS METHOD, ALL THE    *         
*              POSITIVE NUMBERS (DR'S AND -CR'S) ARE IN ONE TABLE     *         
*              (DRTAB), AND ALL THE NEGATIVE NUMBERS (CR'S AND -DR'S) *         
*              ARE IN ONE TABLE (CRTAB).                              *         
*----------------------------------------------------------------------         
*                                                                               
         CLC   TRANMOA,MOAENDDT    TRAN  MOA  >    MOA  END  DATE               
         BH    TRANEX              YES,  EXIT                                   
*                                                                               
         CP    TRANAMNT,=P'0'      TRANSACTION     AMOUNT    <    ZERO          
         BNL   *+8                                                              
         OI    TRANSTAT,NEGATVE    YES,  TURN ON   INDICATOR                    
*                                                                               
         TM    TRNSTAT,TRNSDR      DEBIT ?                                      
         BO    *+8                 YES,  SKIP                                   
         OI    TRANSTAT,CREDIT     NO,   CREDIT                                 
*                                                                               
         L     R6,ADRTAB           ->    DR   TABLE                             
*                                  +CREDIT    OR   -DEBIT ?                     
         TM    TRANSTAT,NEGATVE+CREDIT                                          
         BNM   TRAN50               NO,  CONTINUE                               
         L     R6,ACRTAB            YES, ->   CR   TABLE                        
         B     TRAN50               CONTINUE                                    
         EJECT ,                                                                
*                                                                               
*---------------------------------------------------------------------*         
*        NOT DEFAULT AGEING METHODS -                                 *         
*                                PUT DR'S AND -DR'S IN ONE TABLE AND  *         
*                                PUT CR'S AND -CR'S IN ANOTHER TABLE  *         
*---------------------------------------------------------------------*         
*                                                                               
TRAN45   DS    0H                                                               
         CLC   TRANMOA,MOAENDDT    TRAN  MOA  >    MOA  END  DATE               
         BH    TRAN55              YES,  SKIP                                   
         L     R6,ADRTAB           ->    DR   TABLE                             
         TM    TRNSTAT,TRNSDR      DEBIT ?                                      
         BO    *+8                 YES,  SKIP                                   
         L     R6,ACRTAB           NO,   ->   CR   TABLE                        
*                                                                               
*---------------------------------------------------------------------*         
*        TABLE THE TRANSACTION                                        *         
*---------------------------------------------------------------------*         
*                                                                               
TRAN50   DS    0H                                                               
         ST    R6,POSTTBL          SAVE  ADDR OF   TBL  TO   BE  POSTED         
         BAS   RE,TBLIT            TABLE THE  TRANSACTION                       
*                                                                               
TRAN55   DS    0H                                                               
         CLI   METHOD,MTHDOPEN     OPEN  ITEM AGEING ?                          
         BE    TRAN60              YES,  CONTINUE                               
         CLI   METHOD,MTHDUBIL     UNBILLED  ITEM AGEING                        
         BNE   TRANEX              NO,   EXIT                                   
         EJECT ,                                                                
*                                                                               
*---------------------------------------------------------------------*         
*        OPEN ITEM AGEING AND UNBILLED AGEING BOTH CALL ACASOF,       *         
*        WHICH WILL ZERO OUT UNBILLED ITEMS, IF THE TRANSACTION IS    *         
*        PAST THE ASOF DATE                                           *         
*---------------------------------------------------------------------*         
*                                                                               
TRAN60   DS    0H                                                               
         TM    TRNSTAT,TRNSDR      DEBIT ?                                      
         BZ    TRAN70              NO,   SKIP                                   
*                                  YES,  TELL TIMEPUT   WE   WANT TO            
*                                        SUBTRACT  THE  BILLED                  
         MVI   TPUTSTAT,TPUTSUB          PORTION   OF   THE CHARGES             
         BAS   RE,TIMEPUT          CALL  TIMEPUT                                
*                                                                               
         TM    TRNSTAT,TRNSREV     REVERSAL   TRANSACTION ?                     
         BZ    TRAN65              NO,   SKIP                                   
         BAS   RE,PUTOFFS          SAVE  REVERSAL  IN   OFFSET    TBL           
         B     TRANEX              EXIT                                         
*                                                                               
TRAN65   DS    0H                                                               
         L     R6,AODRTB           ->    OPEN DEBITS    TABLE                   
         ST    R6,POSTTBL          SAVE  ADDR OF   TBL  TO   BE  POSTED         
         BAS   RE,TBLIT            TABLE THE  TRANSACTION                       
         B     TRANEX              EXIT                                         
*                                                                               
TRAN70   DS    0H                  CREDIT                                       
         CLI   METHOD,MTHDUBIL     UNBILLED   ITEM AGEING ?                     
         BE    TRANEX              YES,  EXIT                                   
*                                  NO,   OPEN ITEM AGEING                       
*                                                                               
*                                  ***   SAVE OPEN CREDITS   ***                
*                                                                               
         MVI   ELCODE,TRSELQ       TRANSACTION     STATUS    ELEMENT            
         BAS   RE,NEXTEL           GET   THE  ELEMENT                           
         BE    *+6                 FOUND,     CONTINUE                          
         DS    H'0'                NO,   ABEND                                  
*                                                                               
         USING TRSELD,R4           TRANSACTION     STATUS    ELEMENT            
*                                                                               
         MVC   POSTDADT,TRSDATE    DATE  ADDED     DATE A.K.A.                  
*                                        THE  ACTIVITY  DATE)                   
*                                                                               
         USING TRNELD,R4           TRANSACTION     ELEMENT                      
         USING TIMEDATD,R2         TIME  TBL  AND  BILL TBL  DATA               
         USING TIMEHDRD,R3         TIME  TBL  AND  BILL TBL  HDR                
*                                                                               
         L     R4,ATX              GET   TRANSACTION    ADDRESS                 
         L     R3,ABILLTAB         ->    BILL TABLE                             
         ICM   R5,15,TIME#ENT      GET   NUM  IN   TABLE                        
         BZ    TRAN85              NONE, BILL DID  NOT  MARK CHARGES            
*                                                                               
         LA    R2,TIMEDSTA         START OF   DATA IN   TABLE                   
*                                  CONVERT    PACKED    TO   COMPRESSED         
         GOTO1 DATCON,DMCB,(1,TRNDATE),(2,WORK)                                 
*                                                                               
TRAN75   DS    0H                                                               
         CLC   TIMEINV,TRNREF      SAME  INVOICE   NUMBER ?                     
         BNE   TRAN80              NO,   SKIP IT                                
*                                                                               
         CLC   TIMERUN,TRN2DAY     MATCH ON   RUN  DATE ?                       
         BE    TRAN95              YES,  FOUND     MATCH,    CONTINUE           
*                                                                               
         CLC   TIMEBIL,WORK        MATCH ON   BILL DATE ?                       
         BE    TRAN95              YES,  FOUND     MATCH,    CONTINUE           
*                                                                               
TRAN80   DS    0H                  LOOP  TO   GET  NEXT ELEMENT                 
         LA    R2,TIMELNQ(,R2)     ELEMENT    LENGTH                            
         BCT   R5,TRAN75           LOOP  TILL END  OF   TABLE                   
*                                                                               
         DROP  R2,R3               KEEP  IT   CLEAN                             
*                                                                               
TRAN85   DS    0H                  THIS  BILL DID  NOT  MARK CHANGES;           
*                                        SO,  SAVE IT                           
         CLI   TIMCHRGS,TIMTIMEO   TIME  ONLY ?                                 
         BE    TRANEX              YES,  FORGET    THIS BILL                    
         MVI   OFFSTAT,YES         OFFSET     ITEM                              
*                                                                               
         TM    TRNSTAT,TRNSREV     REVERSAL   TRANSACTION ?                     
         BZ    TRAN90              NO,   SKIP                                   
         BAS   RE,PUTOFFS          SAVE  REVERSAL  IN   OFFSET    TBL           
         B     TRANEX              EXIT                                         
*                                                                               
TRAN90   DS    0H                  SAVE  OPEN CREDITS   BY                      
         BAS   RE,SAVEOCR                ACTIVITY  DATE (DA  DATE)              
         B     TRANEX              EXIT                                         
*                                                                               
*        PROGRESSIVE OR CLIENT BILLING ONLY !                                   
*        THIS  BILL MARKED CHARGES.                                             
*              IF IT IS A TOTAL BILL, THEN                                      
*                 SAVE THE AMOUNT OF NON-MARKING BILLS IT REVERSED, SO          
*                 TOTAL BILL AMOUNT = CHARGES MARKED - PREVIOUS BILLING         
*              THUS:                                                            
*                 PREVIOUS BILLING = CHARGES MARKED - TOTAL BILL AMOUNT         
TRAN95   DS    0H                                                               
         CLC   =C'TOTAL',TRNNARR   TOTAL BILL ?                                 
         BNE   TRANEX              NO,   EXIT                                   
*                                  YES,  SAVE OPEN CREDITS   BY                 
         BAS   RE,SAVEOCR                ACTIVITY  DATE (DA  DATE)              
*        B     TRANEX              EXIT                                         
*                                                                               
TRANEX   DS    0H                                                               
         B     XIT                 EXIT                                         
*                                                                               
         DROP  R4,R5               KEEP  IT   CLEAN                             
         EJECT ,                                                                
**********************************************************************          
*        PROCESS TRIAL TRANSACTION                                   *          
**********************************************************************          
         SPACE 1                                                                
TRIALTX  DS    0H                                                               
*                                  NOT   YET  SUPPORTED                         
*                                                                               
*                                                                               
*                                                                               
TRIALEX  DS    0H                                                               
         B     XIT                 EXIT                                         
         EJECT ,                                                                
**********************************************************************          
*        RETURN TOTALS                                               *          
**********************************************************************          
         SPACE 1                                                                
TOTALS   DS    0H                                                               
         CLI   AGEBYTXD,AGBYBOTH   AGE   BY   TX   AND  MOA  DATES ?            
         BE    TOTAL02             YES,  SKIP                                   
         MVC   TAGEBYTX,AGEBYTXD   NO,   USE  AGE  BY   TX   DATE REPLY         
         B     TOTAL05             CONTINUE                                     
*                                                                               
TOTAL02  DS    0H                  AGE   BY   TX   AND  MOA  DATES              
         CLI   TAGEBYTX,YES        COMPLETED  AGE  BY   TX   DATE ?             
         BE    TOTAL03             YES,  DO   AGE  BY   MOA  DATE               
         MVI   TAGEBYTX,YES        NO,   DO   AGE  BY   TX   DATE               
         B     TOTAL05             CONTINUE                                     
*                                                                               
TOTAL03  DS    0H                                                               
         MVI   TAGEBYTX,NO         DO         AGE  BY   MOA  DATE               
*                                                                               
         USING JAIDATED,R1         USER'S     START/END DATES      AREA         
*                                                                               
TOTAL05  DS    0H                                                               
         L     R1,AUSRDATS         ->    USER'S    START/END DATES AREA         
         ZIC   R2,DATPAIRS         NUM   OF   START/END DATE PAIRS              
*                                                                               
TOTAL07  DS    0H                                                               
         CLI   TAGEBYTX,YES        AGE   BY   TRANSACTION    DATE ?             
         BE    *+10                YES,  SKIP                                   
         ZAP   JAISMMOA,=P'0'      ZERO  THE  SUM  BY   MOA  DATE FIELD         
         CLI   TAGEBYTX,NO         AGE   BY   TRANSACTION    DATE ?             
         BE    *+10                NO,   SKIP                                   
         ZAP   JAISMTXD,=P'0'      ZERO  THE  SUM  BY   TX   DATE FIELD         
         LA    R1,JAIDATEQ(,R1)    NEXT  USER'S    START/END DATES AREA         
         BCT   R2,TOTAL07          LOOP  TO   ZERO ALL  FLDS IN   TABLE         
*                                                                               
         DROP  R1                  KEEP  IT   CLEAN                             
*                                                                               
         USING ACTRECD,R1          ACCOUNT/JOB     RECORD                       
*                                                                               
         L     R1,JAIJOBS          ADDR  OF   JOB  RECORD                       
         ST    R1,AJOBRCDS         SAVE  THE  JOB  RECORD    ADDR               
*                                                                               
         CLC   JOBID,ACTKCULA      SAME  JOB  ?                                 
         BNE   TOTALEX             NO    EXIT                                   
*                                                                               
         DROP  R1                  KEEP  IT   CLEAN                             
*                                                                               
*                                  UPDATE     OPEN DR   TABLE     OR            
         BAS   RE,UPDATOTB                    OPEN CR   ADJUSTED  TABLE         
*                                                                               
*                                  APPLY TOTAL     BILLS     TO                 
         BAS   RE,APPLTOTS               OPEN CR   TABLE                        
*                                                                               
         MVI   SWAPSTAT,NO         INITIALIZE SWAP STATUS                       
*                                                                               
         CLI   TRIAL,YES           TRIAL REQUESTED ?                            
         BE    TRIALTOT            YES,  PROCESS   TRIAL     TOTALS             
*                                                                               
         CLI   ZEROBAL,YES         INCLUDE    ZERO BALANCE   JOBS ?             
         BNE   TOTAL10             NO,   CHECK     BALANCE                      
*                                                                               
         CLI   METHOD,MTHDUBIL     UNBILLED   ITEMS     AGEING ?                
         BE    TOTAL30             YES,  ONLY CHK  FOR  OPEN DEBITS             
*                                                                               
TOTAL10  DS    0H                                                               
         L     R3,ADRTAB           ->    DRTAB                                  
         L     R5,ACRTAB           ->    CRTAB                                  
*                                  GET   SUM  OF   DEBITS                       
         ZAP   JOBBAL,DRTABTOT-DRTAB(PKDAMNTQ,R3)                               
*                                  MINUS SUM  OF   CREDITS (REMEMBER            
*                                        500  CREDIT =  -500 IN   TBL)          
         AP    JOBBAL,CRTABTOT-CRTAB(PKDAMNTQ,R5)                               
         BNO   *+6                 NOT   OVERFLOW, CONTINUE                     
         DC    H'00'               ON    OVERFLOW, ABEND                        
*                                                                               
         CP    JOBBAL,=P'0'        ZERO  BALANCE   FOR  JOB  ?                  
         BE    TOTALEX             YES,  EXIT                                   
         EJECT ,                                                                
*                                                                               
*---------------------------------------------------------------------*         
*        CALL  THE AGEING SUBROUTINES                                 *         
*---------------------------------------------------------------------*         
*                                                                               
         CLI   METHOD,MTHDFIFO     FIFO  AGEING ?                               
         BNE   TOTAL15             NO,   SKIP                                   
         BAS   RE,SUMFIFO          YES,  USE  FIFO AGEING                       
         LR    R1,R3               ->    DR   TABLE                             
         BAS   RE,RPTTOTS                REPORT    THE  TOTALS                  
         LR    R1,R5               ->    CR   TABLE                             
         BAS   RE,RPTTOTS                REPORT    THE  TOTALS                  
         B     TOTALEX             EXIT                                         
*                                                                               
TOTAL15  DS    0H                                                               
         CLI   METHOD,MTHDOPEN     OPEN  ITEM AGEING ?                          
         BNE   TOTAL20             NO,   SKIP                                   
         BAS   RE,SUMOPEN          YES,  USE  OPEN ITEM AGEING                  
         LR    R1,R3               ->    OPEN DR   TABLE                        
         BAS   RE,RPTTOTS                REPORT    THE  TOTALS                  
         LR    R1,R5               ->    OPEN CR   TABLE                        
         BAS   RE,RPTTOTS                REPORT    THE  TOTALS                  
*                                  REVERSE    OPEN CR   ADJUSTED  TABLE         
         BAS   RE,REVADJVL               SPECIAL   ADJUSTMENT     VALUE         
         B     TOTALEX             EXIT                                         
*                                                                               
TOTAL20  DS    0H                                                               
         CLI   METHOD,MTHDACTD     ACTIVITY   DATE AGEING ?                     
         BNE   TOTAL25             NO,   SKIP                                   
         LR    R1,R3               ->    DR   TABLE                             
         BAS   RE,RPTTOTS                REPORT    THE  TOTALS                  
         LR    R1,R5               ->    CR   TABLE                             
         BAS   RE,RPTTOTS                REPORT    THE  TOTALS                  
         B     TOTALEX             EXIT                                         
*                                                                               
TOTAL25  DS    0H                                                               
         CLI   METHOD,MTHDUBIL     UNBILLED   ITEM AGEING ?                     
         BNE   TOTAL35             NO,   SKIP                                   
*                                                                               
TOTAL30  DS    0H                  UNBILLED   ITEM AGEING                       
         L     R3,AODRTB           ->    OPEN DEBITS    TABLE                   
*                                  ANY   BALANCE   ?                            
         CP    ODRTBTOT-ODRTB(PKDAMNTQ,R3),=P'0'                                
         BE    TOTALEX             NO,   EXIT                                   
         LR    R1,R3               ->    OPEN DR   TABLE                        
         BAS   RE,RPTTOTS          YES,  REPORT    THE  TOTALS                  
         B     TOTALEX             EXIT                                         
*                                                                               
TOTAL35  DS    0H                  DEFAULT    AGEING                            
         BAS   RE,DFLTRSET         RESET TABLES    FOR  DEFAULT AGEING          
*                                        IN   ORDER     THAT WE CAN             
*                                        USE  FIFO AGEING    ALGORITHM          
         BAS   RE,SUMFIFO          USE   FIFO AGEING    ALGORITHM               
         LR    R1,R3               ->    DR   TABLE                             
         BAS   RE,RPTTOTS                REPORT    THE  TOTALS                  
         LR    R1,R5               ->    CR   TABLE                             
         BAS   RE,RPTTOTS          REPORT     THE  TOTALS                       
         B     TOTALEX             EXIT                                         
*                                                                               
TOTALEX  DS    0H                                                               
         CLI   AGEBYTXD,AGBYBOTH   AGE   BY   TX   AND  MOA  DATES ?            
         BNE   TOTALEXT            NO,   EXIT                                   
         CLI   TAGEBYTX,YES        AGE   BY   TX   DATE JUST COMPLETED?         
         BE    TOTALS              YES,  DO   AGE  BY   MOA  DATE               
*                                                                               
TOTALEXT DS    0H                                                               
         B     XIT                 EXIT                                         
*                                                                               
         EJECT ,                                                                
**********************************************************************          
*        PROCESS TRIAL TOTALS                                        *          
**********************************************************************          
         SPACE 1                                                                
TRIALTOT DS    0H                                                               
*                                  NOT   YET  SUPPORTED                         
*                                                                               
*                                                                               
*                                                                               
TRIALTEX DS    0H                                                               
         B     XIT                 EXIT                                         
         EJECT ,                                                                
**********************************************************************          
*        FINISHED => FREE STORAGE                                    *          
**********************************************************************          
         SPACE 1                                                                
FINISH   DS    0H                                                               
         BAS   RE,FREEWORK         FREE  MODULE    WORK AREA                    
*                                                                               
         B     XIT                 EXIT                                         
*                                                                               
         EJECT ,                                                                
**********************************************************************          
*        CLEAR JOB RELATED FIELDS AND TABLES                         *          
**********************************************************************          
         SPACE 1                                                                
CLEAR    NTR1                                                                   
*                                                                               
*                                  ***   CLEAR     TABLES    ***                
*                                                                               
         SR    R2,R2               CLEAR REGISTER                               
*                                                                               
         L     RE,ATIMETAB         ->    TIME TABLE                             
*                                  CLEAR NUM  ITEMS     IN   TABLE              
         ST    R2,TIMTAB#-TIMETAB(,RE)                                          
*                                  ->    DATA AREA                              
         LA    RE,TIMTABST-TIMETAB(,RE)                                         
         L     RF,=A(TIMTABLQ)     DATA  SIZE                                   
         XCEFL ,                   CLEAR IT                                     
*                                                                               
         L     RE,ABILLTAB         ->    BILL TABLE                             
*                                  CLEAR NUM  ITEMS     IN   TABLE              
         ST    R2,BILTAB#-BILLTAB(,RE)                                          
*                                  ->    DATA AREA                              
         LA    RE,BILTABST-BILLTAB(,RE)                                         
         L     RF,=A(BILTABLQ)     DATA  SIZE                                   
         XCEFL ,                   CLEAR IT                                     
*                                                                               
         L     RE,AOFFSTB          ->    OFFSET    TABLE                        
*                                  CLEAR NUM  ITEMS     IN   TABLE              
         ST    R2,OFFSTB#-OFFSTB(,RE)                                           
*                                  ->    DATA AREA                              
         LA    RE,OFFSTBST-OFFSTB(,RE)                                          
         L     RF,=A(OFFSTBLQ)     DATA  SIZE                                   
         XCEFL ,                   CLEAR IT                                     
*                                                                               
         L     RE,AOCRTB           ->    OPEN CR   TABLE                        
*                                  CLEAR NUM  ITEMS     IN   TABLE              
         ST    R2,OCRTB#-OCRTB(,RE)                                             
*                                  ->    DATA AREA                              
         LA    RE,OCRTBSTA-OCRTB(,RE)                                           
         L     RF,=A(OCRTBLNQ)     DATA  SIZE                                   
         XCEFL ,                   CLEAR IT                                     
*                                                                               
         L     RE,ATOTTB           ->    TOTAL     BILLS     TABLE              
*                                  CLEAR NUM  ITEMS     IN   TABLE              
         ST    R2,TOTTB#-TOTTB(,RE)                                             
*                                  ->    DATA AREA                              
         LA    RE,TOTTBSTA-TOTTB(,RE)                                           
         L     RF,=A(TOTTBLNQ)     DATA  SIZE                                   
         XCEFL ,                   CLEAR IT                                     
*                                                                               
         EJECT ,                                                                
         L     RE,ACRTAB           ->    CR   TABLE                             
*                                  CLEAR NUM  ITEMS     IN   TABLE              
         ST    R2,CRTAB#-CRTAB(,RE)                                             
*                                  CLEAR SUM  OF   TABLE     AMOUNTS            
         ZAP   CRTABTOT-CRTAB(L'CRTABTOT,RE),=P'0'                              
*                                  ->    DATA AREA                              
         LA    RE,CRTABSTA-CRTAB(,RE)                                           
         L     RF,=A(CRTABLNQ)     DATA  SIZE                                   
         XCEFL ,                   CLEAR IT                                     
*                                                                               
         L     RE,ADRTAB           ->    DR   TABLE                             
*                                  CLEAR NUM  ITEMS     IN   TABLE              
         ST    R2,DRTAB#-DRTAB(,RE)                                             
*                                  CLEAR SUM  OF   TABLE     AMOUNTS            
         ZAP   DRTABTOT-DRTAB(L'DRTABTOT,RE),=P'0'                              
*                                  ->    DATA AREA                              
         LA    RE,DRTABSTA-DRTAB(,RE)                                           
         L     RF,=A(DRTABLNQ)     DATA  SIZE                                   
         XCEFL ,                   CLEAR IT                                     
*                                                                               
         L     RE,AODRTB           ->    OPEN DR   TABLE                        
*                                  CLEAR NUM  ITEMS     IN   TABLE              
         ST    R2,ODRTB#-ODRTB(,RE)                                             
*                                  CLEAR SUM  OF   TABLE     AMOUNTS            
         ZAP   ODRTBTOT-ODRTB(L'ODRTBTOT,RE),=P'0'                              
*                                  ->    DATA AREA                              
         LA    RE,ODRTBSTA-ODRTB(,RE)                                           
         L     RF,=A(ODRTBLNQ)     DATA  SIZE                                   
         XCEFL ,                   CLEAR IT                                     
*                                                                               
         L     RE,AOCRATB          ->    OPEN CR   ADJUSTED  TABLE              
*                                  CLEAR NUM  ITEMS     IN   TABLE              
         ST    R2,OCRATB#-OCRATB(,RE)                                           
*                                  CLEAR SUM  OF   TABLE     AMOUNTS            
         ZAP   OCRATBTO-OCRATB(L'OCRATBTO,RE),=P'0'                             
*                                  ->    DATA AREA                              
         LA    RE,OCRATBST-OCRATB(,RE)                                          
         L     RF,=A(OCRATBLQ)     DATA  SIZE                                   
         XCEFL ,                   CLEAR IT                                     
*                                                                               
         BAS   RE,ASOFINIT         INIT  AS   OF   TABLE                        
         SPACE 1                                                                
*                                  ***   CLEAR     LOCAL     FIELDS ***         
         MVI   TAGEBYTX,X'00'      RESET AGE  BY   TRANSACTION    DATE          
         MVI   OFFSTAT,NO          RESET OFFSET    ITEM STATUS                  
         XC    CONTRAID,CONTRAID   RESET CONTRA-COMPANY ID                      
*                                  RESET OPEN CR   ADJUSTMENT     TABLE         
         ZAP   OCRAADJV,=P'0'            SPECIAL   ADJUSTMENT     VALUE         
*                                                                               
         B     XIT                 EXIT                                         
*                                                                               
         EJECT ,                                                                
**********************************************************************          
*        TRANSACTION PROCESSING ROUTINES                             *          
**********************************************************************          
         SPACE 3                                                                
*--------------------------------------------------------------------*          
*                                                                    *          
*        USER HAS ASKED TO SEGREGATE CLIENT TIME                     *          
*        WE SAVE THE BILLED AMOUNT ON THE DEBITS TO SUBTRACT IT FROM *          
*        THE BILLING.                                                *          
*                                                                    *          
*--------------------------------------------------------------------*          
         SPACE 1                                                                
         USING TIMEDATD,R2         TIME  TBL  AND  BILL TBL  DATA AREA          
         USING TRNELD,R7           TRANSACTION     ELEMENT                      
         USING TRNRECD,R5          TRANSACTION     RECORD                       
         SPACE 1                                                                
TIMEPUT  NTR1                                                                   
         L     R7,ATX              GET   TRANSACTION    ADDRESS                 
         LR    R5,R7               FIND  TRANSACTION    RECORD                  
         SH    R5,DATADISP               ADDRESS                                
         LA    R2,TIMEKEY          SEARCH     AREA FOR  TBL  ENTRY              
*                                                                               
         XC    TIMEKEY,TIMEKEY     CLEAR THE  KEY  FOR  BINSRCH                 
*                                                                               
         ICM   R4,15,APRORATA      ->    PRORATA   BLOCK                        
         BZ    TIMEPX              NONE, EXIT                                   
*                                                                               
         USING PTAELD,R4           PROD  TRANSACTION    ACTIVITY                
*                                                                               
         CLI   0(R4),PTAELQ        X'77' ELEMENT ?                              
         BNE   TIMEP70             NO,   SKIP X'77'     EL   PROCESSING         
         B     TIMEP20             PROCESS    IT                                
*                                                                               
TIMEP10  DS    0H                                                               
         MVI   ELCODE,PTAELQ       X'77' ELEMENT                                
         BAS   RE,NEXTEL           GET   NEXT EL                                
         BNE   TIMEP70             NO    MORE ELS, ALL  DONE                    
*                                                                               
TIMEP20  DS    0H                                                               
         TM    PTASTAT1,PTASPEND   SKIP  ALL  PENDING                           
         BO    TIMEP10                                                          
*                                                                               
         CLI   PTATYPE,PTATRAL     ALLOCATE   TO   BILLING ?                    
         BE    TIMEBL              YES,  PROCESS                                
         CLI   PTATYPE,PTATWOF     WRITE-OFF ?                                  
         BE    TIMEWO              YES,  PROCESS                                
         CLI   PTATYPE,PTATWOFR    WRITE-OFF  RECOVERY ?                        
         BNE   TIMEP10             NO,   GET  NEXT ELEMENT                      
*                                                                               
TIMEWO   DS    0H                  WRITE-OFF                                    
         CLI   TPUTSTAT,TPUTSUB    RETURNING  BILLABLE  AMOUNT ?                
         BNE   TIMEP10             NO,   GET  NEXT ELEMENT                      
         MVC   TIMEINV,PTAWREF     YES,  GET  THE  INVOICE   NUMBER             
*                                  CONVERT    PACKED    TO   COMPRESSED         
*                                  TRANSACTION     DATE                         
         GOTO1 DATCON,DMCB,(1,PTAWDAT),(2,TIMERUN)                              
         MVC   TIMEBIL,TIMERUN     BILLING         DATE (COMPRESSED)            
         ZAP   TIMEAMNT,PTANET     NET   AMOUNT    IN   AGENCY CURRENCY         
         BAS   RE,TIMEADD          ADD   TO   TABLE                             
         B     TIMEP10             GET   NEXT                                   
*                                                                               
TIMEBL   DS    0H                  BILLING    AMOUNT                            
         MVC   TIMEINV,PTARBLNO    INVOICE    NUMBER                            
         MVC   TIMERUN,PTARDATE    RUN   DATE (COMPRESSED)                      
         MVC   TIMEBIL,PTARBLDT    BILL  DATE (COMPRESSED)                      
         ZAP   TIMEAMNT,PTANET     AMOUNT                                       
*                                                                               
* THIS CODE HANDLES OLD ITEMS WHERE 4B ELEMENT DID NOT CONTAIN                  
* THE AMOUNT BILLED.                                                            
*                                                                               
         CP    PTANET,=P'0'        AMOUNT =   0 ?                               
         BNE   TIMEBL20            NO,   SKIP                                   
*                                  IS    IT   FULLY     BILLED ?                
         OC    ACCOUSED(ACCOULEN,R5),ACCOUSED(R5)                               
         BZ    TIMEBL20            NO,   TAKE WHAT YOU  HAVE                    
*                                  BEFORE     05/18/92 (NEW  AC21)              
         CLC   ACCOUSED(ACCOULEN,R5),=X'B8B3'                                   
         BNL   TIMEBL20            NO,   SKIP                                   
         ZAP   TIMEAMNT,TRANAMNT   YES,  USE  TRANSACTION    AMOUNT             
*                                                                               
TIMEBL20 DS    0H                                                               
         BAS   RE,TIMEADD          ADD   TO   TABLE                             
         B     TIMEP10             GET   NEXT                                   
*                                                                               
TIMEP70  DS    0H                                                               
*                                  SUBTRACT   BILLED    AMOUNT                  
         CLI   TPUTSTAT,TPUTSUB          FROM CHARGE ?                          
         BNE   TIMEPX              NO                                           
         MVI   BYTE,DEBUGTX        PRINT TRANSACTION                            
         BAS   RE,DEBUGIT          ANY   DEBUGGING OUTPUT    REQUIRED ?         
         MVI   BYTE,DEBUGPRO       PRINT PRORATA   BLOCK                        
         BAS   RE,DEBUGIT          ANY   DEBUGGING OUTPUT    REQUIRED ?         
         BAS   RE,ASOFTRN          SET   POSTTRAN  VIA  ACASOF                  
*                                                                               
TIMEPX   DS    0H                                                               
         B     XIT                 EXIT                                         
*                                                                               
         DROP  R2,R4,R5,R7         KEEP  IT   CLEAN                             
         EJECT ,                                                                
*--------------------------------------------------------------------*          
*                                                                    *          
*        ADD TIME WHICH HAS BEEN BILLED TO TABLE                     *          
*        FOR OPEN ITEM AGEING, SUBTRACT THE BILLED PORTION FROM THIS *          
*        DEBIT AMOUNT.                                               *          
*                                                                    *          
*--------------------------------------------------------------------*          
         SPACE 1                                                                
         USING TIMEDATD,R2         TIME  TBL  AND  BILL TBL  DATA AREA          
         USING TIMEHDRD,R3         TIME  TBL  AND  BILL TBL  HDR                
         SPACE 1                                                                
TIMEADD  NTR1                                                                   
         LA    R2,TIMEKEY          SEARCH     AREA FOR  TBL  ENTRY              
         CLI   TPUTSTAT,TPUTSUB    CALLED     FOR  OPEN CHARGES ?               
         BNE   TIMEA10             NO,   ADD  BILLED    AMOUNT TO   TBL         
*                                                                               
*                                  COMPRESSED TO PACKED                         
         GOTO1 DATCON,DMCB,(2,TIMERUN),(1,WORK)                                 
*                                  WAS   THIS BILLED   WITHIN                   
         CLC   MOAENDDT,WORK             REQUIRED  RANGE ?                      
         BL    XIT                 NO,   CONSIDER  IN   UNBILLED                
*                                                                               
         SP    POSTTRAN,TIMEAMNT   SUBTRACT   AMOUNT    BILLED                  
*                                                                               
         L     R3,ABILLTAB         ->    BILL TABLE                             
         B     TIMEA20                                                          
*                                                                               
TIMEA10  DS    0H                                                               
         L     R3,ATIMETAB         ->    TIMETABLE                              
*                                                                               
TIMEA20  DS    0H                                                               
         L     R5,TIME#ENT         NUMBER     IN   TABLE                        
         L     R6,TIMEMAX          MAXIMUM    NUM  OF   ENTRIES                 
         LA    R7,TIMEDSTA         START OF   DATA IN   TABLE                   
*                                                                               
*                                  SEARCH/ADD TABLE                             
         GOTO1 BINSRCH,DMCB,(1,TIMEKEY),(R7),(R5),TIMELNQ,             X        
               (0,TIMSKYLQ),(R6)                                                
         MVC   TIME#ENT,DMCB+8 INSERT     NUM  OF   RCDS SO   FAR               
         OC    DMCB(4),DMCB        TABLE FULL ?                                 
         BNZ   *+6                 NO,   CONTINUE                               
         DC    H'00'               YES,  ABEND                                  
*                                                                               
         CLI   DMCB,1              NOT   FOUND,    I.E. ADDED ?                 
         BE    XIT                 YES,  EXIT                                   
*                                  RECORD     FOUND                             
         L     R5,DMCB                  (INVOICE   NUM  OR   DATE)              
*                                  SO    ADD  AMOUNT    TO   IT                 
         AP    TIMEAMNT-TIMEDATD(PKDAMNTQ,R5),TIMEAMNT                          
         BNO   *+6                 NOT   OVERFLOW, CONTINUE                     
         DC    H'00'               ON    OVERFLOW, ABEND                        
*                                                                               
         XC    TIMEKEY,TIMEKEY     CLEAR KEY  FOR  NEXT TIME                    
         ZAP   TIMEAMNT,=P'0'      CLEAR DATA FOR  NEXT TIME                    
         B     XIT                                                              
*                                                                               
         DROP  R2,R3               KEEP  IT   CLEAN                             
         EJECT ,                                                                
*--------------------------------------------------------------------*          
*       LOOK IN TIMETAB FOR INVOICE NUMBERS, A MATCH INDICATES PART  *          
*       OF THE BILL IS TIME BILLING (ALSO MATCH DATES)               *          
*                                                                    *          
*       TIMCHRGS = O - SUBTRACT TABLE AMOUNT FROM TRANSACTION AMOUNT *          
*       TIMCHRGS = T - USE TABLE AMOUNT AS BILL AMOUNT               *          
*--------------------------------------------------------------------*          
         SPACE 1                                                                
         USING TIMEDATD,R2         TIME  TBL  AND  BILL TBL  DATA AREA          
         USING TIMEHDRD,R3         TIME  TBL  AND  BILL TBL  HDR                
         USING TRNELD,R7           TRANSACTION     ELEMENT                      
         SPACE 1                                                                
TIMEGET  NTR1                                                                   
         L     R7,ATX              GET   TRANSACTION    ADDRESS                 
         L     R3,ATIMETAB         GET   TIME TABLE     ADDRESS                 
         ICM   R5,15,TIME#ENT      NUMBER     IN   TABLE                        
         BZ    TIMEGEX             TABLE EMPTY,    EXIT                         
*                                                                               
         LA    R2,TIMEDSTA         START OF   DATA IN   TABLE                   
*                                  CONVERT    PACKED    TO   COMPRESSED         
         GOTO1 DATCON,DMCB,(1,TRNDATE),(2,WORK)                                 
*                                                                               
TIMEG10  DS    0H                                                               
         CLC   TIMEINV,TRNREF      SAME  INVOICE   NUMBER ?                     
         BNE   TIMEG15             NO,   SKIP IT                                
*                                                                               
         CLC   TIMERUN,TRN2DAY     MATCH ON   RUN  DATE ?                       
         BE    TIMEG20             YES,  FOUND     MATCH,    CONTINUE           
*                                                                               
         CLC   TIMEBIL,WORK        MATCH ON   BILL DATE ?                       
         BE    TIMEG20             YES,  FOUND     MATCH,    CONTINUE           
*                                                                               
TIMEG15  DS    0H                  LOOP  TO   GET  NEXT ELEMENT                 
         LA    R2,TIMELNQ(,R2)     ELEMENT    LENGTH                            
         BCT   R5,TIMEG10          LOOP  TILL END  OF   TABLE                   
         B     TIMEGEX             NOT   FOUND,    EXIT                         
*                                                                               
TIMEG20  DS    0H                  FOUND CLIENT    TIME IS   THIS BILL          
         CLI   TIMCHRGS,TIMTIMEO   TIME  ONLY ?                                 
         BE    TIMEG30             YES,  SKIP                                   
*                                  ADD   AMOUNT    OF   EXCLUDED  TO            
*                                        THIS INVOICE  (BECAUSE                 
         AP    POSTTRAN,TIMEAMNT         CREDITS   ARE  NEGATIVE)               
         BNO   *+6                 NOT   OVERFLOW, CONTINUE                     
         DC    H'00'               ON    OVERFLOW, ABEND                        
*                                                                               
         B     TIMEG40             CONTINUE                                     
*                                                                               
TIMEG30  DS    0H                                                               
*                                  USE   TABLE     AMOUNT AS                    
         ZAP   POSTTRAN,TIMEAMNT         BILL      AMOUNT                       
         MP    POSTTRAN,=P'-1'     REVERSE    SIGN OF   BILLING                 
*                                                                               
TIMEG40  DS    0H                                                               
         MVC   TIMEINV,SPACES      ONLY  USE  INVOICE   NUMBER    ONCE          
         B     XIT                                                              
*                                                                               
TIMEGEX  DS    0H                                                               
         CLI   TIMCHRGS,TIMTIMEO   TIME  ONLY ?                                 
         BNE   XIT                 NO,   EXIT                                   
         ZAP   POSTTRAN,=P'0'      ZERO  BILL AMOUNT,   ITS  NOT  TIME          
         B     XIT                 EXIT                                         
*                                                                               
         DROP  R2,R3,R7            KEEP  IT   CLEAN                             
         EJECT ,                                                                
*--------------------------------------------------------------------*          
*        WHEN OPEN ITEM AGEING OR UNBILLED ITEM AGEING, COMPARE THE  *          
*        REVERSAL TRANSACTION TO THE OFFSET TABLE ITEMS BY:          *          
*                                                                    *          
*          .  TX-DATE MONTH, AND                                     *          
*          .  AMOUNT                                                 *          
*                                                                    *          
*        IF THE TRANSACTION MATCHES AN ITEM IN THE OFFSET TABLE,     *          
*        I.E. SAME REFERENCE NUMBER, DATE, AND A REVERSAL OF THE     *          
*        AMOUNT, THEN ZERO OUT THE AMOUNT FIELD; ELSE, ADD AN ENTRY  *          
*        TO THE OFFSET TABLE.                                        *          
*                                                                    *          
*        NOTE: WHENEVER THE CONTRA-COMPANY CHANGES, THEN WE CALL     *          
*              UPDATOTB TO UPDATE THE OPEN DR TABLE OR THE OPEN CR   *          
*              ADJUSTED TABLE WITH THE OFFSET TABLE. THIS AVOIDS THE *          
*              NEED FOR KEEPING THE CONTRA-COMPANY ID IN THE OFFSET  *          
*              TABLE.                                                *          
*--------------------------------------------------------------------*          
         SPACE 1                                                                
         USING OFFSTABD,R2         OFFSET     TABLE     ENTRY                   
         USING TRNELD,R3           TRANSACTION     ELEMENT                      
         USING TRNRECD,R4          TRANSACTION     RECORD                       
         SPACE 1                                                                
PUTOFFS  NTR1                                                                   
         L     R3,ATX              ->    TRANSACTION    ELEMENT                 
         LR    R4,R3               FIND  TRANSACTION    RECORD                  
         SH    R4,DATADISP               ADDRESS                                
*                                                                               
         LA    R2,OFFSTKEY         ->    OFFSET    SEARCH    AREA               
         MVC   OFFSREF,TRNREF      REFERENCE  NUMBER                            
         MVC   OFFSDATE,TRNDATE    TRANSACTION     DATE                         
         ZAP   OFFSAMNT,POSTTRAN   TRANSACTION     AMOUNT                       
         MP    OFFSAMNT,=P'-1'     TX    AMOUNT    REVERSED                     
         MVC   OFFSMOAD,POSTMOAD   MOA   DATE                                   
         MVC   OFFSTXDT,POSTTXDT   TX    DATE                                   
*                                                                               
         DROP  R3,R4               KEEP  IT   CLEAN                             
*                                                                               
         L     R3,AOFFSTB          ->    OFFSET    TABLE                        
*                                  CURRENT    NUM  OF   ENTRIES                 
         L     R4,OFFSTB#-OFFSTB(,R3)                                           
*                                  MAX        NUM  OF   ENTRIES                 
         L     R5,OFFSTBMX-OFFSTB(,R3)                                          
*                                  ->         DATA START                        
         LA    R6,OFFSTBST-OFFSTB(,R3)                                          
*                                  SEARCH/ADD TABLE                             
         GOTO1 BINSRCH,DMCB,(1,OFFSTKEY),(R6),(R4),OFFSLNQ,            X        
               (0,OFFSSKYQ),(R5)                                                
*                                  UPDATE     CURR NUM  OF   ENTRIES            
         MVC   OFFSTB#-OFFSTB(L'OFFSTB#,R3),DMCB+8                              
         OC    DMCB,DMCB           TABLE FULL ?                                 
         BNZ   *+6                 NO,   CONTINUE                               
         DC    H'00'               YES,  ABEND                                  
*                                                                               
         L     RE,DMCB             ->    TABLE     ENTRY                        
         CLI   DMCB,1              NOT   FOUND,    I.E. ADDED ?                 
         BE    PUTO10              YES,  REVERSE   AMOUNT                       
*                                  NO,   CLEAR     ENTRY                        
         ZAP   OFFSAMNT-OFFSKEY(PKDAMNTQ,RE),=P'0'                              
         B     PUTO20              CONTINUE                                     
*                                                                               
PUTO10   DS    0H                  ADDED      TO   TABLE                        
*                                  RESET TO   TX   AMOUNT                       
         MP    OFFSAMNT-OFFSKEY(PKDAMNTQ,RE),=P'-1'                             
*                                                                               
PUTO20   DS    0H                                                               
*                                  CLEAR KEY  AREA FOR  NEXT TIME               
         XC    OFFSTKEY(OFFSLNQ),OFFSTKEY                                       
         ZAP   OFFSAMNT,=P'0'      CLEAR AMOUNT    FOR  NEXT TIME               
*                                                                               
PUTOFFSX DS    0H                                                               
         B     XIT                                                              
*                                                                               
         DROP  R2                  KEEP  IT   CLEAN                             
         EJECT ,                                                                
*--------------------------------------------------------------------*          
*        SAVE OPEN CREDITS BY DATE ADDED DATE (DA DATE=ACTIVITY DATE)*          
*        IN THE OPEN CREDITS TABLE OR IN THE TOTAL BILLS TABLE.      *          
*                                                                    *          
*        SAVE POSTMOAD, POSTTXDT AND POSTTRAN IN THE TABLE OF CHOICE.*          
*--------------------------------------------------------------------*          
         SPACE 1                                                                
         USING TRNELD,R3           TRANSACTION     ELEMENT                      
         SPACE 1                                                                
SAVEOCR  NTR1                                                                   
         L     R3,ATX              ->    TRANSACTION    ELEMENT                 
*                                                                               
         CLC   =C'TOTAL',TRNNARR   TOTAL BILL ?                                 
         BE    SAVEOC50            YES,  PROCESS   TOTAL     BILLS              
*                                                                               
         USING OCRENTD,R2          OPEN  CR   DATA AREA                         
         USING TIMEHDRD,R4         MAP   TABLE     HEADER    AREA               
*                                                                               
         LA    R2,OCRBSKEY         ->    BINSRCH   AREA                         
         L     R4,AOCRTB           ->    OPEN CR   TABLE                        
*                                                                               
         XC    OCRBSKEY,OCRBSKEY   CLEAR BINSRCH   AREA                         
         MVC   OCRDADAT,POSTDADT   DATE  ADDED     DATE (ACTIVITY DATE)         
*                                                                               
         MVC   OCRMOAD,POSTMOAD    MOA   DATE                                   
         MVC   OCRTXDT,POSTTXDT    TX    DATE                                   
         ZAP   OCRAMNT,POSTTRAN    AMOUNT                                       
*                                                                               
         L     R5,TIME#ENT         CURRENT    NUM  OF   ENTRIES                 
         STCM  R5,3,OCRCTR         UNIQUE     COUNTER                           
         L     R6,TIMEMAX          MAXIMUM    NUM  OF   ENTRIES                 
         LA    R7,TIMEDSTA         DATA  START                                  
*                                                                               
         GOTO1 BINSRCH,DMCB,(1,(R2)),(R7),(R5),OCRLNQ,(0,OCRSKYLQ),(R6)         
         MVC   TIME#ENT,DMCB+8     CURRENT    NUM  OF   ENTRIES                 
         OC    DMCB(4),DMCB        TABLE FULL ?                                 
         BNZ   *+6                 NO,   CONTINUE                               
         DC    H'0'                YES,  ABEND                                  
*                                                                               
         CLI   DMCB,1              NOT   FOUND,    I.E. ADDED ?                 
         BE    SAVEOCRX            YES,  TABLE     ENTRY     COMPLETE           
*                                  FOUND IN   TABLE                             
         DC    H'0'                ABEND                                        
*                                                                               
         USING TOTBENTD,R2         TOTAL BILLS     DATA AREA                    
         USING TIMEHDRD,R4         MAP   TABLE     HEADER    AREA               
*                                                                               
SAVEOC50 DS    0H                                                               
         LA    R2,TOTBSKEY         ->    BINSRCH   AREA                         
         L     R4,ATOTTB           ->    TOTALS    TABLE                        
*                                                                               
         XC    TOTBSKEY,TOTBSKEY   CLEAR BINSRCH   AREA                         
         MVC   TOTBDADT,POSTDADT   DATE  ADDED     DATE (ACTIVITY DATE)         
*                                                                               
         MVC   TOTBMOAD,POSTMOAD   MOA   DATE                                   
         MVC   TOTBTXDT,POSTTXDT   TX    DATE                                   
         ZAP   TOTBAMNT,POSTTRAN   AMOUNT                                       
         MVC   TOTB2DAY,TRN2DAY    TODAY'S    DATE                              
         MVC   TOTBUBIL,TRNUNBIL   UNBILLED   DATE                              
*                                                                               
         L     R5,TIME#ENT         CURRENT    NUM  OF   ENTRIES                 
         STCM  R5,3,TOTBCTR        UNIQUE     COUNTER                           
         L     R6,TIMEMAX          MAXIMUM    NUM  OF   ENTRIES                 
         LA    R7,TIMEDSTA         DATA  START                                  
*                                                                               
         GOTO1 BINSRCH,DMCB,(1,(R2)),(R7),(R5),TOTBLNQ,(0,TOTBSKYQ),   X        
               (R6)                                                             
         MVC   TIME#ENT,DMCB+8     CURRENT    NUM  OF   ENTRIES                 
         OC    DMCB(4),DMCB        TABLE FULL ?                                 
         BNZ   *+6                 NO,   CONTINUE                               
         DC    H'0'                YES,  ABEND                                  
*                                                                               
         CLI   DMCB,1              NOT   FOUND,    I.E. ADDED ?                 
         BE    SAVEOCRX            YES,  TABLE     ENTRY     COMPLETE           
*                                  FOUND IN   TABLE                             
         DC    H'0'                YES,  ABEND                                  
*                                                                               
*                                                                               
SAVEOCRX DS    0H                                                               
         B     XIT                 EXIT                                         
*                                                                               
         DROP  R2,R3,R4            KEEP  IT   CLEAN                             
         EJECT ,                                                                
*--------------------------------------------------------------------*          
*        UPDATE THE OPEN DR TABLE OR THE OPEN CR ADJUSTED TABLE WITH *          
*        THE OFFSET TABLE DATA                                       *          
*--------------------------------------------------------------------*          
         SPACE 1                                                                
         USING TIMEHDRD,R2         MAP   TABLE     HEADER    AREA               
         USING OFFSTABD,R4         OFFSET     TABLE     ENTRY                   
         SPACE 1                                                                
UPDATOTB NTR1                                                                   
         CLI   METHOD,MTHDOPEN     OPEN  ITEM AGEING ?                          
         BE    UPDATO10            YES,  CONTINUE                               
         CLI   METHOD,MTHDUBIL     UNBILLED   ITEM AGEING ?                     
         BNE   UPDATOEX            NO,   EXIT                                   
*                                                                               
UPDATO10 DS    0H                                                               
         L     R2,AOFFSTB          ->    OFFSET    TABLE                        
         ICM   R3,15,TIME#ENT      CURRENT    NUM  OF   ENTRIES                 
         BZ    UPDATOEX            NONE, EXIT                                   
         LA    R4,TIMEDSTA         DATA  START                                  
*                                                                               
         L     R5,AODRTB           ->    OPEN DR   TABLE                        
         CLI   OFFSTAT,YES         DOING BILLING ?                              
         BNE   *+8                 NO,   SKIP                                   
         L     R5,AOCRATB          YES,  ->   OPEN CR   ADJUSTED  TABLE         
         ST    R5,POSTTBL          SAVE  TABLE     ADDRESS                      
*                                                                               
UPDATO20 DS    0H                                                               
         CP    OFFSAMNT,=P'0'      AMOUNT =   ZERO ?                            
         BE    UPDATO30            YES,  SKIP THIS ENTRY                        
*                                  NO,   ADD  THIS ENTRY                        
         ZAP   POSTTRAN,OFFSAMNT   GET   TX   AMOUNT                            
         MVC   POSTTXDT,OFFSTXDT   GET   TX   DATE                              
         MVC   POSTMOAD,OFFSMOAD   GET   MOA  DATE                              
         BAS   RE,TBLIT            TABLE THE  TRANSACTION                       
*                                                                               
UPDATO30 DS    0H                  CLEAR THIS ENTRY,    GET  NEXT ENTRY         
*                                  CLEAR CURR ENTRY                             
         XC    OFFSTABD(OFFSLNQ),OFFSTABD                                       
         MVC   OFFSAMNT,=P'0'      SET   AMOUNT    TO   ZERO                    
         LA    R4,OFFSLNQ(,R4)     NEXT  OFFSET    TBL  ITEM                    
         BCT   R3,UPDATO20         TEST  OFFSET    TBL  ENTRY                   
*                                                                               
*                                  ALL   ENTRIES   DONE                         
         XC    TIME#ENT,TIME#ENT   CLEAR NUM  OF   ENTRIES                      
         MVI   OFFSTAT,NO          RESET OFFSET    ITEM STATUS                  
*                                                                               
UPDATOEX DS    0H                                                               
         B     XIT                 EXIT                                         
*                                                                               
         DROP  R2,R4               KEEP  IT   CLEAN                             
         EJECT ,                                                                
*--------------------------------------------------------------------*          
*        APPLY TOTAL BILLS ELEMENTS TO OPEN CREDITS TABLE.           *          
*                                                                    *          
*        ENTER IN THE OPEN CREDITS ADJUSTED TABLE ONLY THE           *          
*        OPEN BILLS WHICH DO NOT HAVE A SUBSEQUENT TOTAL BILL WITHIN *          
*        THE DATE PARAMETERS OF THE REQUEST; I.E. ALL THE OPEN       *          
*        BILLS THAT PRECEDE THE LATEST (NON-REVERSED) TOTAL BILL     *          
*        ARE ALREADY ACCOUNTED FOR, SO THEY ARE ELIMINATED.          *          
*                                                                    *          
*        THIS ROUTINE APPLIES TO THE OPEN AGEING METHOD ONLY.        *          
*        THIS ROUTINE APPLIES ONLY TO THE FIRST PASS THRU THE TOTALS *          
*        ROUTINE FOR EACH USER CALL TO THE TOTALS ROUTINE.           *          
*                                                                    *          
*        REGISTER USAGE:                                             *          
*          R2 = ADDRESS OF THE OPEN CR TABLE                         *          
*          R3 = NUMBER  OF THE OPEN CR TABLE ELEMENTS LEFT TO CHECK  *          
*          R4 = ADDRESS OF THE OPEN CR TABLE ELEMENT                 *          
*          R5 = ADDRESS OF THE TOTALS  TABLE                         *          
*          R6 = NUMBER  OF THE TOTALS  TABLE ELEMENTS LEFT TO CHECK  *          
*          R7 = ADDRESS OF THE TOTALS  TABLE ELEMENT                 *          
*--------------------------------------------------------------------*          
         SPACE 1                                                                
         USING OCRENTD,R4          MAP   OPEN      CREDITS   TBL  ENTRY         
         USING TOTBENTD,R7         MAP   TOTAL     BILLS     TBL  ENTRY         
         SPACE 1                                                                
APPLTOTS NTR1                                                                   
         CLI   METHOD,MTHDOPEN     OPEN  AGEING ?                               
         BNE   APPLTOEX            NO,   EXIT                                   
         L     R2,AOCRTB           ->    OPEN CR   TABLE                        
*                                                                               
*                                  TABLE EMPTY ?                                
*                                        NOTE:     ON   2ND  PASS               
*                                                  THIS FLD  WILL BE 0          
         ICM   R3,15,OCRTB#-OCRTB(R2)                                           
         BZ    APPLTOEX            YES,  EXIT                                   
*                                  NO,   ->   DATA AREA                         
         LA    R4,OCRTBSTA-OCRTB(,R2)                                           
*                                                                               
         L     R5,ATOTTB           ->    TOTAL     BILLS     TABLE              
*                                  TABLE EMPTY ?                                
         ICM   R6,15,TOTTB#-TOTTB(R5)                                           
         BZ    APPLT50             YES,  MOVE OPEN CR   TO                      
*                                             OPEN CR   ADJUSTED                
*                                  NO,   ->   DATA AREA                         
         LA    R7,TOTTBSTA-TOTTB(,R5)                                           
*                                                                               
         BAS   RE,OFFTOTTB         OFFSET     TOTAL     BILL ENTRIES            
*                                                                               
*                                  ***   ELIMINATE OPEN BILLS      ***          
*                                  ***   PRECEDING THE  LAST VALID ***          
*                                  ***   TOTAL     BILL            ***          
*                                                                               
*                                  COMPARE    DATE ADDED     DATE               
*                                        OF   TOTAL     BILL ENTRY              
*                                        WITH THE  DATE ADDED     DATE          
APPLT20  DS    0H                        OF   THE  OPEN CR   ELEMENT            
         CLC   TOTBDADT,OCRDADAT   COMPARE    DATE ADDED     DATES              
*                                  TOTAL TBL  ENT  LOW, GET  NEXT TOT           
         BL    APPLT30                   TBL  ENTRY                             
*                                  OPEN  CR   TBL  ENT  NOT  HIGH,              
         ZAP   OCRAMNT,=P'0'       CLEAR OPEN CR   TBL  ENT  AMOUNT             
         LA    R4,OCRLNQ(,R4)      GET   NEXT OPEN CR   TBL  ENTRY              
         BCT   R3,APPLT20          CHECK THIS ENTRY                             
         B     APPLT90             NONE  LEFT,     EXIT SINCE                   
*                                        OPEN CREDITS   ADJUSTED  TABLE         
*                                        ALREADY   INITIALIZED                  
*                                                                               
APPLT30  DS    0H                  TOTAL BILL TABLE     ENT  LOW                
         LA    R7,TOTBLNQ(,R7)     GET   NEXT TOTAL     BILL TBL  ENTRY         
         SH    R6,=H'01'           UPDATE     NUM  OF   ELEMENTS  LEFT          
*                                  TOTAL BILL TBL  ELEMENTS  LEFT,              
         BH    APPLT20                   CONTINUE  CHECKING                     
*                                  NONE  LEFT,     FINISHED  CHECKING           
*                                                                               
         DROP  R7                  KEEP  IT   CLEAN                             
*                                                                               
*                                  ***   ADD  ALL  REMAINING OPEN  ***          
*                                  ***   BILLS     TO   THE  OPEN  ***          
*                                  ***   CR   ADJUSTED  TABLE      ***          
*                                                                               
*                                  CONVERT    REMAINING OPEN CR   TABLE         
*                                        ELEMENTS  TO   OPEN CR                 
APPLT50  DS    0H                        ADJUSTED  TABLE     ELEMENTS           
         L     R5,AOCRATB          ->    OPEN CR   ADJUSTED  TABLE              
         ST    R5,POSTTBL          SAVE  TABLE     ADDRESS                      
*                                                                               
*                                  MOVE  THE  OPEN CR   TBL  ENT  TO            
APPLT60  DS    0H                        THE  OPEN CR   ADJUSTED  TABLE         
         ZAP   POSTTRAN,OCRAMNT    GET   AMOUNT                                 
         MVC   POSTTXDT,OCRTXDT    GET   TX   DATE                              
         MVC   POSTMOAD,OCRMOAD    GET   MOA  DATE                              
         BAS   RE,TBLIT            TABLE      THE  ENTRY                        
*                                                                               
         LA    R4,OCRLNQ(,R4)      GET   THE  NEXT OPEN CR   TBL  ENTRY         
         BCT   R3,APPLT60          LOOP  FOR  NEXT OPEN CR   TBL  ENTRY         
*                                                                               
APPLT90  DS    0H                                                               
*                                  CLEAR NUM  OF   ENTRIES   IN   OCRTB         
         XC    OCRTB#-OCRTB(L'OCRTB#,R2),OCRTB#-OCRTB(R2)                       
*                                                                               
APPLTOEX DS    0H                                                               
         B     XIT                 EXIT                                         
*                                                                               
         DROP  R4                  KEEP  IT   CLEAN                             
         EJECT ,                                                                
*--------------------------------------------------------------------*          
*        OFFSET TOTAL BILL TABLE ENTRIES WITH OTHER TOTAL BILL       *          
*        ENTRIES WHEN:                                               *          
*                                                                    *          
*          THE UNBILL DATE MATCHES THE "TODAY'S DATE", AND           *          
*          THE AMOUNTS ARE EXACTLY REVERSED                          *          
*                                                                    *          
*        AN ELEMENT THAT IS OFFSET WILL HAVE ITS DATE ADDED DATE SET *          
*        TO ZERO.  DATE ADDED DATE = DA DATE = ACTIVITY DATE.        *          
*                                                                    *          
*        REGISTER USAGE:                                             *          
*          R2 = ADDRESS OF TOTAL    BILLS    TABLE                   *          
*          R3 = NUMBER  OF ELEMENTS IN   THE TABLE                   *          
*          R4 = ADDRESS OF ELEMENT  THAT WE  ARE     CHECKING        *          
*          R5 = NUMBER  OF ELEMENTS LEFT TO  CHECK                   *          
*          R6 = ADDRESS OF ELEMENT       TO  COMPARE WITH            *          
*          R7 = NUMBER  OF ELEMENTS LEFT TO  COMPARE WITH            *          
*--------------------------------------------------------------------*          
         SPACE 1                                                                
         USING TIMEHDRD,R2         MAP   TABLE     HEADER    AREA               
         USING TOTBENTD,R4         MAP   TOTAL     BILLS     TBL  ENTRY         
CMPREL   USING TOTBENTD,R6         MAP   TOTAL     BILLS     TBL  ENTRY         
         SPACE 1                                                                
OFFTOTTB NTR1                                                                   
         L     R2,ATOTTB           ->    TOTAL     BILLS     TABLE              
         L     R3,TIME#ENT         CURRENT    NUM  OF   ELEMENTS                
         LA    R4,TIMEDSTA         ->    DATA START                             
         LR    R5,R3               NUM   OF   ELEMENTS  LEFT                    
*                                                                               
OFFTOT10 DS    0H                  SEE   IF   TOTAL     WAS  UNBILLED           
         OC    TOTBUBIL,TOTBUBIL   ANY   UNBILLED  DATE ?                       
         BZ    OFFTOT50            NO,   GET  NEXT CHECK     ELEMENT            
*                                  YES,  TOTAL     WAS  UNBILLED                
*                                                                               
*                                  FIND  TOTAL     THAT DID  THE                
*                                        DID  THE  UNBILLING                    
         LA    R6,TOTBLNQ(,R4)     START COMPARISON     WITH NEXT EL            
         LR    R7,R5               FIND  NUM  OF   ELEMENTS  LEFT               
         SH    R7,=H'01'           ANY   COMPARE   ELEMENTS  ?                  
         BNZ   OFFTOT20            YES,  CONTINUE                               
*        DC    H'00'               NO,   ABEND                                  
         B     OFFTOT50                                                         
*                                                                               
OFFTOT20 DS    0H                                                               
*                                  HAS   THIS ELEMENT BEEN   OFFSET ?           
         OC    CMPREL.TOTBDADT,CMPREL.TOTBDADT                                  
         BZ    OFFTOT30            YES,  GET  NEXT COMPARE   ELEMENT            
*                                                                               
*                                  COULD THIS BE   THE  UNBILL    EL ?          
         CLC   TOTBUBIL,CMPREL.TOTB2DAY                                         
         BNE   OFFTOT30            NO,   GET  NEXT COMPARE   ELEMENT            
*                                                                               
         ZAP   PCK,TOTBAMNT        AMOUNT     OF   CHECKED   ELEMENT            
*                                  DOES  THE  COMPARE   ELEMENT                 
         AP    PCK,CMPREL.TOTBAMNT       HAVE THE  REVERSE   AMOUNT ?           
         BNZ   OFFTOT30            NO,   GET  NEXT COMPARE   ELEMENT            
*                                  YES,  CLEAR     DATE ADDED     DATE          
         XC    TOTBDADT,TOTBDADT   IN    BOTH ENTRIES,  I.E. MARK BOTH          
*                                        ENTRIES   AS   UNBILLED                
         XC    CMPREL.TOTBDADT,CMPREL.TOTBDADT                                  
         B     OFFTOT50            GET   NEXT CHECK     ELEMENT                 
*                                                                               
OFFTOT30 DS    0H                                                               
         LA    R6,TOTBLNQ(,R6)     ->    NEXT COMPARE   ELEMENT                 
         BCT   R7,OFFTOT20         LOOP  TO   TEST IT                           
*                                  NONE  LEFT -                                 
*                                  COULD WE   BE   IN   THE  MIDDLE             
         CLC   TOTBUBIL,TODAYDAT         OF   A    DATA BASE UPDATE ?           
*                                  YES,  ASSUME    UNBILL    WILL BE            
         BE    OFFTOT40                  ENTERED   LATER     TODAY              
*        DC    H'00'               NO,   DATA BASE ERROR,    ABEND              
         B     OFFTOT50                                                         
*                                                                               
*                                  ASSUME     UNBILL    WILL BE                 
OFFTOT40 DS    0H                        ENTERED   LATER     TODAY              
         XC    TOTBDADT,TOTBDADT   MARK  ENTRY     AS   UNBILLED                
         B     OFFTOT50            GET   NEXT CHECK     ELEMENT                 
*                                                                               
OFFTOT50 DS    0H                  GET   NEXT CHECK     ELEMENT                 
         LA    R4,TOTBLNQ(,R4)     ->    NEXT CHECK     ELEMENT                 
         SH    R5,=H'01'           UPDATE     NUM  OF   ELEMENTS  LEFT          
         BZ    OFFTOTEX            NONE  LEFT,     THEN DONE                    
         OC    TOTBDADT,TOTBDADT   HAS   THIS ELEMENT   BEEN OFFSET ?           
         BZ    OFFTOT50            YES,  SKIP      IT                           
         B     OFFTOT10            NO,   CHECK     IT                           
*                                                                               
OFFTOTEX DS    0H                                                               
         B     XIT                 EXIT                                         
*                                                                               
         DROP  CMPREL,R2,R4        KEEP  IT   CLEAN                             
         EJECT ,                                                                
*--------------------------------------------------------------------*          
*        SAVE TRANSACTION IN A TABLE SO THAT THE TRANSACTIONS CAN BE *          
*        AGED WHEN WE ARE RAEDY TO GET THE TOTALS                    *          
*                                                                    *          
*        INPUT:                                                      *          
*              POSTTBL  - THE TABLE TO BE UPDATED                    *          
*              POSTTRAN - THE TRANSACTION AMOUNT                     *          
*              POSTTXDT - THE TRANSACTION AMOUNT                     *          
*              POSTMOAD - THE MOA DATE                               *          
*              AGEBYTXD - AGE BY TRANSACTION DATE                    *          
*                                                                    *          
*--------------------------------------------------------------------*          
         SPACE 1                                                                
         USING CRDRHDRD,R2         CR    AND  DR   TBL  HDR  AREA               
         USING CRDRDATD,R3         CR    AND  DR   TBL  DATA AREA               
         SPACE 1                                                                
TBLIT    NTR1                                                                   
         L     R2,POSTTBL          ->    THE  TABLE     TO   BE   USED          
         LA    R3,CRDRKEY          ->    THE  BINARY    SEARCH    KEY           
         AP    CRDRTOT,POSTTRAN    ADD   AMOUNT    TO   SUM  FOR  TABLE         
         BNO   *+6                 NOT   OVERFLOW, CONTINUE                     
         DC    H'00'               ON    OVERFLOW, ABEND                        
*                                                                               
         LA    R6,CRDRDSTA         ->    THE  DATA START                        
*                                                                               
         CLI   AGEBYTXD,NO         AGE   BY   TRANSACTION    DATE ?             
         BE    TBLIT50             NO,   SKIP TX   TBL  ENTRIES                 
*                                                                               
         MVI   CRDRDTYP,CRDRDTX    DATE  TYPE TRANSACTION                       
*                                  COMPRESS   THE  TRANSACTION    DATE          
         GOTO1 DATCON,DMCB,(1,POSTTXDT),(2,CRDRDATE)                            
         ZAP   CRDRAMNT,POSTTRAN   AMOUNT                                       
*                                                                               
         CLC   CRDRDATE,LOWDATE    DATE  <    LOW  DATE ?                       
         BH    TBLIT10             NO,   SKIP                                   
         MVC   CRDRDATE,LOWDATE    YES,  USE  LOW  DATE                         
*                                                                               
TBLIT10  DS    0H                                                               
         CLC   CRDRDATE,HIGHDATE   DATE  >    HIGH DATE ?                       
         BL    TBLIT20             NO,   SKIP                                   
         MVC   CRDRDATE,HIGHDATE   YES,  USE  HIGH DATE                         
*                                                                               
TBLIT20  DS    0H                                                               
         L     R4,CRDR#ENT         CURRENT    NUM  OF   ENTRIES                 
         L     R5,CRDRMAX          MAXIMUM    NUM  OF   ENTRIES                 
*                                  SEARCH/ADD TABLE                             
         GOTO1 BINSRCH,DMCB,(1,CRDRKEY),(R6),(R4),CRDRLNQ,             X        
               (0,CRDRSKYQ),(R5)                                                
         MVC   CRDR#ENT,DMCB+8     UPDATE     CURR NUM  OF   ENTRIES            
         OC    DMCB(4),DMCB        TABLE      FULL ?                            
         BNZ   *+6                 NO,   CONTINUE                               
         DC    H'00'               YES,  ABEND                                  
*                                                                               
         CLI   DMCB,1              NOT   FOUND,    I.E. ADDED ?                 
         BE    TBLIT50             YES,  TABLE     ENTRY     COMPLETE           
*                                  NO,   OLD       ENTRY                        
         L     RE,DMCB             ->    TABLE     ENTRY                        
*                                  ADD   AMOUNT                                 
         AP    CRDRAMNT-CRDRDATD(PKDAMNTQ,RE),POSTTRAN                          
         BNO   *+6                 NOT   OVERFLOW, CONTINUE                     
         DC    H'00'               ON    OVERFLOW, ABEND                        
*                                                                               
TBLIT50  DS    0H                                                               
         CLI   AGEBYTXD,YES        AGE   BY   MOA  DATE ?                       
         BE    TBLIT90             NO,   SKIP MOA  TBL  ENTRIES                 
*                                                                               
         MVI   CRDRDTYP,CRDRDMOA   DATE  TYPE MOA                               
*                                  GET   MOA  DATE (PACKED YYMM)                
         MVC   WORK(L'POSTMOAD),POSTMOAD                                        
*                                  SET   DAY (DD)  TO   01                      
         MVI   WORK+L'POSTMOAD,X'01'                                            
*                                  COMPRESS   THE  MOA  DATE                    
         GOTO1 DATCON,DMCB,(1,WORK),(2,CRDRDATE)                                
         ZAP   CRDRAMNT,POSTTRAN   AMOUNT                                       
*                                                                               
         CLC   CRDRDATE,LOWDATE    DATE  <    LOW  DATE ?                       
         BH    TBLIT60             NO,   SKIP                                   
         MVC   CRDRDATE,LOWDATE    YES,  USE  LOW  DATE                         
*                                                                               
TBLIT60  DS    0H                                                               
         CLC   CRDRDATE,HIGHDATE   DATE  >    HIGH DATE ?                       
         BL    TBLIT70             NO,   SKIP                                   
         MVC   CRDRDATE,HIGHDATE   YES,  USE  HIGH DATE                         
*                                                                               
TBLIT70  DS    0H                                                               
         L     R4,CRDR#ENT         CURRENT    NUM  OF   ENTRIES                 
         L     R5,CRDRMAX          MAXIMUM    NUM  OF   ENTRIES                 
*                                  SEARCH/ADD TABLE                             
         GOTO1 BINSRCH,DMCB,(1,CRDRKEY),(R6),(R4),CRDRLNQ,             X        
               (0,CRDRSKYQ),(R5)                                                
         MVC   CRDR#ENT,DMCB+8     UPDATE     CURR NUM  OF   ENTRIES            
         OC    DMCB(4),DMCB        TABLE      FULL ?                            
         BNZ   *+6                 NO,   CONTINUE                               
         DC    H'00'               YES,  ABEND                                  
*                                                                               
         CLI   DMCB,1              NOT   FOUND,    I.E. ADDED ?                 
         BE    TBLIT90             YES,  TABLE     ENTRY     COMPLETE           
*                                  NO,   OLD       ENTRY                        
         L     RE,DMCB             ->    TABLE     ENTRY                        
*                                  ADD   AMOUNT                                 
         AP    CRDRAMNT-CRDRDATD(PKDAMNTQ,RE),POSTTRAN                          
         BNO   *+6                 NOT   OVERFLOW, CONTINUE                     
         DC    H'00'               ON    OVERFLOW, ABEND                        
*                                                                               
TBLIT90  DS    0H                                                               
*                                  CLEAR KEY  AREA FOR  NEXT TIME               
         XC    CRDRKEY(CRDRKYLQ),CRDRKEY                                        
         ZAP   CRDRAMNT,=P'0'      CLEAR AMOUNT    FOR  NEXT TIME               
*                                                                               
TBLITEX  DS    0H                                                               
         B     XIT                 EXIT                                         
*                                                                               
         DROP  R2,R3               KEEP  IT   CLEAN                             
         EJECT ,                                                                
*--------------------------------------------------------------------*          
*        AS OF TABLE INITIALIZATION                                  *          
*--------------------------------------------------------------------*          
         SPACE 1                                                                
         USING ACASOFD,R4          AS    OF   DSECT                             
         SPACE 1                                                                
ASOFINIT NTR1                                                                   
         LA    R4,ASOFBLK          ->    AS   OF   BLOCK                        
         L     RE,AASOFTAB         ->    AS   OF   TABLE                        
         ST    RE,ACABILLS         STORE IT                                     
*                                                                               
         L     RE,AJOBRCD          ->    JOB  RECORD                            
         ST    RE,ACAATRN                                                       
*                                                                               
         L     RE,ADCOMFAC         ->    COMFACS                                
         ST    RE,ACACOMF                                                       
*                                                                               
         MVC   ACASAEND,ACTENDDT   ACTIVITY   END  DATE (COMPRESSED)            
         L     RF,APRORATA         ->    PRORATA   BLOCK                        
         ST    RF,ACAPTABF                                                      
*                                                                               
         MVC   ACASOFDT,MOAENDDT   MOA   END  DATA (YYMM PACKED)                
         MVI   ACAMODE,ACAMINIT    RUN   MODE IS   INITIALIZE                   
*                                                                               
         GOTO1 =V(ACASOF),(R4)                                                  
         B     XIT                                                              
*                                                                               
         DROP  R4                  KEEP  IT   CLEAN                             
         EJECT ,                                                                
*--------------------------------------------------------------------*          
*        AS OF TRANSACTION ROUTINE                                   *          
*--------------------------------------------------------------------*          
         SPACE 1                                                                
         USING TRNELD,R7           TRANSACTION     ELEMENT                      
         USING TRNRECD,R5          TRANSACTION     RECORD                       
         USING ACASOFD,R4          AS    OF   DSECT                             
         SPACE 1                                                                
ASOFTRN  NTR1                                                                   
         LA    R4,ASOFBLK          ->    AS   OF   BLOCK                        
*                                                                               
         L     R7,ATX              GET   TRANSACTION    ADDRESS                 
         LR    R5,R7               FIND  TRANSACTION    RECORD                  
         SH    R5,DATADISP               ADDRESS                                
         ST    R5,ACAATRN          STORE IN   BLOCK                             
*                                                                               
         MVI   ACAMODE,ACAMTRN                                                  
         MVI   ACAFROM,ACAFPTA                                                  
*                                                                               
         GOTO1 =V(ACASOF),(R4)                                                  
         ZAP   POSTTRAN,ACAUNBLD                                                
         B     XIT                                                              
*                                                                               
         DROP  R4,R5,R7            KEEP  IT   CLEAN                             
         EJECT ,                                                                
**********************************************************************          
*        TOTALS PROCESSING ROUTINES                                  *          
**********************************************************************          
         SPACE 3                                                                
*--------------------------------------------------------------------*          
*                                                                    *          
*        RESET THE TABLES AND THE TABLE POINTERS FOR THE DEFAULT     *          
*        AGEING METHOD IN ORDER THAT WE WILL BE ABLE TO USE THE      *          
*        FIFO AGEING ALGORITHM.                                      *          
*                                                                    *          
*        INPUT:                                                      *          
*              R3 = ADDRESS OF DEBIT  TABLE (ONLY POSITIVE VALUES)   *          
*              R5 = ADDRESS OF CREDIT TABLE (ONLY NEGATIVE VALUES)   *          
*                                                                    *          
*--------------------------------------------------------------------*          
         SPACE 1                                                                
DFLTRSET NTR1                                                                   
         MVI   SWAPSTAT,NO         INIT  SWAP STATUS                            
         CP    JOBBAL,=P'0'        IS    THE  BALANCE   POSITIVE ?              
         BNM   DFLTREX             YES,  READY     TO   USE FIFO AGEING         
         XR    R3,R5               NO,   SWAP R3   AND  R5                      
         XR    R5,R3                                                            
         XR    R3,R5                                                            
*                                                                               
         LR    R1,R3               SWAP  SIGNS     OF   ALL                     
         BAS   RE,NEGATE                 CR   TBL  ENTRIES                      
         LR    R1,R5               SWAP  SIGNS     OF   ALL                     
         BAS   RE,NEGATE                 DR   TBL  ENTRIES                      
         MVI   SWAPSTAT,YES        INDICATE   TABLES    SWAPED                  
*                                                                               
DFLTREX  DS    0H                                                               
         XIT1  REGS=(R3,R5)        EXIT  RETURNING REGS R3   AND  R5            
         EJECT ,                                                                
*--------------------------------------------------------------------*          
*                                                                    *          
*        AGE OPEN DEBITS AGEING ALGORITHM                            *          
*                                                                    *          
*--------------------------------------------------------------------*          
         SPACE 1                                                                
ODRHDR   USING CRDRHDRD,R3         OPEN  DR   TBL  HDR  AREA                    
OCRAHDR  USING CRDRHDRD,R5         OPEN  CR   ADJUSTED  TBL  HDR  AREA          
         SPACE 1                                                                
SUMOPEN  NTR1                                                                   
         L     R3,AODRTB           ->    OPEN DR   TABLE                        
         SR    R5,R5               CLEAR REGISTER                               
*                                                                               
*                                  JOB   BALANCE   SAME AS   SUM  OF            
         CP    JOBBAL,ODRHDR.CRDRTOT     OPEN DR   TBL  AMOUNTS ?               
         BE    SUMOPEX             YES,  EXIT                                   
*                                                                               
         L     R5,AOCRATB          ->    OPEN CR   ADJUSTED  TABLE              
*                                                                               
*                                  JOB   BALANCE   SAME AS   SUM  OF            
*                                        OPEN CR   ADJUSTED  TBL                
         CP    JOBBAL,OCRAHDR.CRDRTOT    AMOUNTS ?                              
         BE    SUMOPEX             YES,  EXIT                                   
*                                                                               
*                                  CALCULATE  SUM  OF   OPEN DR   ENT.S         
         ZAP   OCRAADJV,ODRHDR.CRDRTOT                                          
*                                  PLUS  SUM  OF   OPEN CR   ADJUSTED           
*                                        ENTRIES                                
         AP    OCRAADJV,OCRAHDR.CRDRTOT                                         
         SP    OCRAADJV,JOBBAL     MINUS JOB  BALANCE                           
         MP    OCRAADJV,=P'-1'     TIMES MINUS     ONE                          
         CP    OCRAADJV,=P'0'      SPECIAL    VALUE    =     0 ?                
         BE    SUMOP10             YES,  SKIP                                   
*                                                                               
*                                  ***   ADD  OPEN CR  ADJUSTED    ***          
*                                  ***   TBL  ENTRY    FOR  THE    ***          
*                                  ***   SPECIAL   ADJUSTMENT      ***          
*                                  ***   VALUE                     ***          
*                                                                               
         ST    R5,POSTTBL          SAVE  TABLE     ADDRESS                      
         ZAP   POSTTRAN,OCRAADJV   SAVE  SPECIAL   VALUE                        
*                                  USE   LOW  DATE FOR  TX   DATE AND           
         GOTO1 DATCON,DMCB,(2,LOWDATE),(1,POSTTXDT)                             
         MVC   POSTMOAD,POSTTXDT         FOR  MOA  DATE                         
         BAS   RE,TBLIT            TABLE THE  TRANSACTION                       
*                                                                               
SUMOP10  DS    0H                                                               
         LR    R1,R3               ->    OPEN DR   TABLE                        
         BAS   RE,TEMPTOTS         TOTAL IT                                     
         MVI   TMPTOTSW,ZERO       INIT  THE  INDICATOR                         
*                                  SEE   IF   ALL  TOTALS    HAVE THE           
         BAS   RE,CHKSIGNS               SAME SIGN                              
         CLI   TMPTOTSW,MIXED      MIXED      SIGNS ?                           
         BE    SUMOP20             YES,  USE  FIFO AGEING                       
*                                                                               
         LR    R1,R5               ->    OPEN CR   ADJUSTED  TABLE              
         BAS   RE,TEMPTOTS         TOTAL IT                                     
*                                  SEE   IF   ALL  TOTALS    HAVE THE           
*                                        SAME SIGN AS   IN   THE                
         BAS   RE,CHKSIGNS               OPEN DR   TABLE                        
         CLI   TMPTOTSW,MIXED      MIXED      SIGNS ?                           
         BNE   SUMOPEX             NO,   JUST ADD  THE  SUMS                    
*                                                                               
SUMOP20  DS    0H                  USE   FIFO AGEING                            
*                                                                               
*                                  ***   SPECIAL   CASE WHEN:       ***         
*                                  ***   SUM  ODR  <    SUM  OCRA   ***         
*                                  ***   AND  ABSOLUTE  VALUE     OF***         
*                                  ***   SUM  ODR  >    SUM  OCRA   ***         
*                                                                               
*                                  SUM   OPEN DR'S <    SUM  OPEN CR'S?         
         CP    ODRHDR.CRDRTOT,OCRAHDR.CRDRTOT                                   
         BNL   SUMOP40             NO,   DO   NOT  NEGATE                       
*                                                                               
*                                  GET   ABSOLUTE  VALUE     OF                 
         ZAP   PCK,ODRHDR.CRDRTOT        SUM  OPEN DR'S                         
         BNM   *+10                      >=   0,   SKIP                         
         MP    PCK,=P'-1'                <    0,   MAKE POSITIVE                
         CP    PCK,OCRAHDR.CRDRTOT ABS   VALUE     >    OPEN CRA'S ?            
         BNH   SUMOP40             NO,   DO   NOT  NEGATE                       
*                                  YES,  NEGATE                                 
         LR    R1,R3               SWAP  SIGNS     OF   ALL                     
         BAS   RE,NEGATE                 CR   TBL  ENTRIES                      
         LR    R1,R5               SWAP  SIGNS     OF   ALL                     
         BAS   RE,NEGATE                 DR   TBL  ENTRIES                      
         MVI   SWAPSTAT,YES        INDICATE   TABLES    SWAPED                  
*                                                                               
SUMOP40  DS    0H                  USE   FIFO AGEING    ALGORITHM               
         BAS   RE,SUMFIFO                                                       
*                                                                               
SUMOPEX  DS    0H                                                               
         XIT1  REGS=(R3,R5)        EXIT  RETURNING REGS R3   AND  R5            
*                                                                               
         DROP  ODRHDR,OCRAHDR      KEEP  IT   CLEAN                             
         EJECT ,                                                                
*--------------------------------------------------------------------*          
*        REVERSE SPECIAL ADJUSTMENT VALUE IN THE OPEN CR ADJUSTED    *          
*        TABLE.                                                      *          
*--------------------------------------------------------------------*          
         SPACE 1                                                                
REVADJVL NTR1                                                                   
         CP    OCRAADJV,=P'0'      ANY   SPECIAL   ADJUSTMENT     VALUE         
         BE    REVADJEX            NO,   EXIT                                   
*                                                                               
         L     R5,AOCRATB          ->    OPEN CR   ADJUSTED  TABLE              
         ST    R5,POSTTBL          SAVE  TABLE     ADDRESS                      
         ZAP   POSTTRAN,OCRAADJV   GET   SPECIAL   VALUE                        
         MP    POSTTRAN,=P'-1'     MULTIPLY   BY   MINUS     ONE                
*                                  USE   LOW  DATE FOR  TX   DATE AND           
         GOTO1 DATCON,DMCB,(2,LOWDATE),(1,POSTTXDT)                             
         MVC   POSTMOAD,POSTTXDT         FOR  MOA  DATE                         
         BAS   RE,TBLIT            TABLE THE  TRANSACTION                       
*                                                                               
REVADJEX DS    0H                                                               
         B     XIT                 EXIT                                         
         EJECT ,                                                                
*--------------------------------------------------------------------*          
*        CHECK IF ALL TEMPORARY TOTALS VALUES HAVE THE SAME SIGN,    *          
*        I.E. ALL POSITIVE OR ALL NEGATIVE.                          *          
*--------------------------------------------------------------------*          
         SPACE 1                                                                
CHKSIGNS NTR1                                                                   
         LA    R2,TEMPTOTA         ->    TEMPORARY TOTALS    AREA               
         ZIC   R3,DATPAIRS         NUMBER     OF   TOTALS                       
*                                                                               
CHKS10   DS    0H                                                               
*                                  SIGN  OF   THIS TOTAL     FIELD              
         CP    0(PKDAMNTQ,R2),=P'0'                                             
         BE    CHKS50              ZERO,      GET  NEXT TOTAL     FIELD         
         BP    CHKS30              POSITIVE,  PROCESS   POSITIVE                
*                                  NEGATIVE,  PROCESS   NEGATIVE                
*                                                                               
*                                  PROCESS    NEGATIVE                          
         CLI   TMPTOTSW,ZERO       HAS   SIGN BEEN SET  ?                       
         BNE   CHKS20              YES,  TEST FOR  SAME SIGN                    
         MVI   TMPTOTSW,NEGATIVE   NO,   SET  SIGN TO   NEGATIVE                
         B     CHKS50              GET   NEXT TOTAL     FIELD                   
*                                                                               
CHKS20   DS    0H                                                               
         CLI   TMPTOTSW,NEGATIVE   DO    WE   HAVE THE  SAME SIGN ?             
         BE    CHKS50              YES,  GET  NEXT TOTAL     FIELD              
         B     CHKSIGNM            NO,   SET  TO   MIXED                        
*                                                                               
CHKS30   DS    0H                  PROCESS    POSITIVE                          
         CLI   TMPTOTSW,ZERO       HAS   SIGN BEEN SET  ?                       
         BNE   CHKS40              YES,  TEST FOR  SAME SIGN                    
         MVI   TMPTOTSW,POSITIVE   NO,   SET  SIGN TO   POSITIVE                
         B     CHKS50              GET   NEXT TOTAL     FIELD                   
*                                                                               
CHKS40   DS    0H                                                               
         CLI   TMPTOTSW,POSITIVE   DO    WE   HAVE THE  SAME SIGN ?             
         BE    CHKS50              YES,  GET  NEXT TOTAL     FIELD              
         B     CHKSIGNM            NO,   SET  TO   MIXED                        
*                                                                               
CHKS50   DS    0H                  GET   NEXT TOTAL     FIELD                   
         LA    R2,PKDAMNTQ(,R2)    ->    NEXT FIELD                             
         BCT   R3,CHKS10           TEST  NEXT FIELD                             
         B     CHKSIGNX            ALL   DONE,     EXIT                         
*                                                                               
CHKSIGNM DS    0H                                                               
         MVI   TMPTOTSW,MIXED      SET   SIGN TO   MIXED                        
*                                                                               
CHKSIGNX DS    0H                                                               
         B     XIT                 EXIT                                         
         EJECT ,                                                                
*--------------------------------------------------------------------*          
*                                                                    *          
*        FIFO AGEING ALGOTITHM                                       *          
*                                                                    *          
*        INPUT:                                                      *          
*              R3 = ADDRESS OF DEBIT  OR CREDIT TABLE ASSUME DEBIT   *          
*              R5 = ADDRESS OF CREDIT OR DEBIT  TABLE ASSUME CREDIT  *          
*                                                                    *          
*        WORK REGISTERS:                                             *          
*              R2 = ADDRESS OF DEBIT  TABLE ENTRY                    *          
*              R3 = ADDRESS OF DEBIT  TABLE                          *          
*              R4 = ADDRESS OF CREDIT TABLE ENTRY                    *          
*              R5 = ADDRESS OF CREDIT TABLE                          *          
*              R6 = DEBIT   TBL ENTRY NUMBER                         *          
*              R7 = CREDIT  TBL ENTRY NUMBER                         *          
*                                                                    *          
*        SIMPLIFIED LOGIC:                                           *          
*              1. GET   FIRST CR ELEMENT                             *          
*              2. GET   FIRST DR ELEMENT                             *          
*              3. ADD   CR ELEMENT AMOUNT TO DR ELEMENT AMOUNT       *          
*              4. CLEAR CR ELEMENT AMOUNT                            *          
*              5. IF    DR ELEMENT AMOUNT = 0 AND                    *          
*                       CR ELEMENT AMOUNT = 0 THEN                   *          
*              6.          GET NEXT CR ELEMENT AND                   *          
*              7.          GET NEXT DR ELEMENT AND                   *          
*              8.          GOTO 3.                                   *          
*              9. IF    DR ELEMENT AMOUNT > 0 THEN                   *          
*             10.       GET NEXT CR ELEMENT                          *          
*             11.       GOTO 3.                                      *          
*             12. IF    DR ELEMENT AMOUNT < 0 THEN                   *          
*             13.       MOVE DR ELEMENT AMOUNT TO CR ELEMENT AMOUNT  *          
*             14.       CLEAR DR ELEMENT AMOUNT                                 
*             15.       GOTO 2.                                      *          
*             16. WHEN  NO MORE DR ELEMENTS THEN                     *          
*             17.       MOVE SUM OF TABLE AMOUNTS TO CURR CR ELEMENT *          
*             18.       EXIT                                         *          
*             19. WHEN  NO MORE CR ELEMENTS THEN                     *          
*             20.       IF DR ELEMENT AMOUNT > 0 THEN EXIT           *          
*             21.       IF DR ELEMENT AMOUNT = 0 GET NEXT DR ELEMENT *          
*             22.          IF NONE EXISTS THEN EXIT                  *          
*             23.          IF ONE  EXISTS THEN GOTO 20.              *          
*             24.       IF DR ELEMENT AMOUNT < 0 THEN                *          
*             25,          ADD A NEW TABLE ENTRY WITH THE DATA IN    *          
*                              THE DR ELEMENT                        *          
*             26.          CLEAR DR ELEMENT AMOUNT                   *          
*             27.          GOTO 1.                                   *          
*                                                                    *          
*--------------------------------------------------------------------*          
         EJECT ,                                                                
         SPACE 1                                                                
         USING DRENTRYD,R2         DR    TABLE     ENTRY                        
         USING CRENTRYD,R4         CR    TABLE     ENTRY                        
         SPACE 1                                                                
SUMFIFO  NTR1                                                                   
*                                  FIND  SUM  OF   DR   TBL +CR   TBL           
         ZAP   DRPLUSCR,DRTABTOT-DRTAB(,R5)                                     
         AP    DRPLUSCR,CRTABTOT-CRTAB(,R3)                                     
         BNO   *+6                 NOT   OVERFLOW, CONTINUE                     
         DC    H'00'               ON    OVERFLOW, ABEND                        
*                                                                               
         CLI   SWAPSTAT,YES        WERE  ENTRIES   NEGATED ?                    
         BNE   SUMFI05             NO,   SKIP                                   
         MP    DRPLUSCR,=P'-1'     MULTIPLY   SUM  BY   -1                      
*                                                                               
SUMFI05  DS    0H                  FIND  1ST  CR   ENTRY                        
         LA    R4,CRTABSTA-CRTAB(,R5)                                           
         LA    R7,1                FIRST      CR   ENTRY                        
*                                  FIND  1ST  DR   ENTRY                        
         LA    R2,DRTABSTA-DRTAB(,R3)                                           
         LA    R6,1                FIRST      DR   ENTRY                        
*                                  GET   NUM  OF   CR   TBL  ENTRIES            
         MVC   CRNUMENT,CRTAB#-CRTAB(R5)                                        
*                                  GET   NUM  OF   DR   TBL  ENTRIES            
         MVC   DRNUMENT,DRTAB#-DRTAB(R3)                                        
         C     R7,CRNUMENT         ANY   CR   TBL  ENTRIES ?                    
         BH    SUMFI60             NO,   RAN  OUT  OF   CR   ENTRIES            
         C     R6,DRNUMENT         ANY   DR   TBL  ENTRIES ?                    
         BH    SUMFI40             NO,   RAN  OUT  OF   DR   ENTRIES            
*                                                                               
SUMFI10  DS    0H                  TEST  FOR  GOOD CR   AND  DR   ENTRY         
*                                  NOTE: WE   WILL GET  HERE EVEN WHEN          
*                                        WE   KNOW THAT ONE IS    GOOD          
         CLC   CRDTTYPE,TAGEBYTX   THIS  PASS'S    AGE  BY   TX   ENTRY         
         BE    SUMFI15             YES,  CONTINUE                               
         LA    R4,CRLNQ(,R4)       NEXT  CR   ENTRY                             
         LA    R7,1(,R7)           NEXT  CR   ENTRY     NUMBER                  
         C     R7,CRNUMENT         PROCESSED  ALL  CR   TBL  ENTRIES ?          
         BH    SUMFI60             YES,  RAN  OUT  OF   CR   ENTRIES            
         B     SUMFI10             TEST  THIS ENTRY                             
*                                                                               
SUMFI15  DS    0H                  TEST  FOR  GOOD DR   ENTRY                   
         CLC   DRDTTYPE,TAGEBYTX   THIS  PASS'S    AGE  BY   TX   ENTRY         
         BE    SUMFI20             YES,  CONTINUE                               
         LA    R2,DRLNQ(,R2)       NEXT  DR   ENTRY                             
         LA    R6,1(,R6)           NEXT  DR   ENTRY     NUMBER                  
         C     R6,DRNUMENT         PROCESSED  ALL  DR   TBL  ENTRIES ?          
         BH    SUMFI40             YES,  MOVE BALANCE   TO   CR   ENTRY         
         B     SUMFI15             TEST  THIS ENTRY                             
*                                                                               
SUMFI20  DS    0H                                                               
         AP    DRAMNT,CRAMNT       ADD   CREDITS   TO   DEBITS                  
         BNO   *+6                 NOT   OVERFLOW, CONTINUE                     
         DC    H'00'               ON    OVERFLOW, ABEND                        
*                                                                               
         ZAP   CRAMNT,=P'0'        CLEAR CREDITS   AMOUNT                       
         CP    DRAMNT,=P'0'        BALANCE    IS   ZERO ?                       
         BNZ   SUMFI25             NO,   CONTINUE                               
*                                                                               
         LA    R4,CRLNQ(,R4)       NEXT  CR   ENTRY                             
         LA    R7,1(,R7)           NEXT  CR   ENTRY     NUMBER                  
         LA    R2,DRLNQ(,R2)       NEXT  DR   ENTRY                             
         LA    R6,1(,R6)           NEXT  DR   ENTRY     NUMBER                  
         B     SUMFI10             FIND  NEXT GOOD CR & DR   ENTRIES            
*                                                                               
SUMFI25  DS    0H                                                               
*        CP    DRAMNT,=P'0'        BALANCE    IS   NEGATIVE ?                   
         BM    SUMFI30             YES,  MOVE SUM  TO   CR   ENTRY              
*                                                                               
*                                  BALANCE    IS   POSITIVE                     
         LA    R4,CRLNQ(,R4)       NEXT  CR   ENTRY                             
         LA    R7,1(,R7)           NEXT  CR   ENTRY     NUMBER                  
         C     R7,CRNUMENT         PROCESSED  ALL  CR   TBL  ENTRIES ?          
         BH    SUMFI60             YES,  RAN  OUT  OF   CR   ENTRIES            
         B     SUMFI10             TEST  FOR  GOOD CR   ENTRY                   
*                                                                               
SUMFI30  DS    0H                  BALANCE    IS   NEGATIVE                     
         ZAP   CRAMNT,DRAMNT       MOVE  DEBITS    TO   CREDITS                 
         ZAP   DRAMNT,=P'0'        CLEAR DEBITS    AMOUNT                       
         LA    R2,DRLNQ(,R2)       NEXT  DR   ENTRY                             
         LA    R6,1(,R6)           NEXT  DR   ENTRY     NUMBER                  
         C     R6,DRNUMENT         PROCESSED  ALL  DR   TBL  ENTRIES ?          
         BH    SUMFI40             YES,  MOVE BALANCE   TO   CR   ENTRY         
         B     SUMFI10             TEST  FOR  GOOD DR   ENTRY                   
*                                                                               
         EJECT ,                                                                
SUMFI40  DS    0H                  RAN   OUT  OF   DR   ENTRIES                 
*                                  INSERT     SUM  OF   TBL  ENTRIES            
*                                        INTO LAST NON-ZERO  CR   ENTRY         
*                                  CLEAR ALL  OTHER     ENTRIES                 
*                                                                               
         SR    RF,RF               INIT  PTR  TO   LAST ENTRY                   
*                                                                               
SUMFI45  DS    0H                  MAKE  SURE WE   HAVE A    GOOD ENTRY         
         CLC   CRDTTYPE,TAGEBYTX   THIS  PASS'S    AGE  BY   TX   ENTRY         
         BNE   SUMFI50             NO,   GET  NEXT CR   ENTRY                   
*                                                                               
         CP    CRAMNT,=P'0'        IS    THIS AMOUNT    ZERO ?                  
         BE    SUMFI50             YES,  GET  NEXT CR   ENTRY                   
*                                                                               
         ZAP   CRAMNT,=P'0'        SET   ENTRY     AMOUNT    TO   ZERO          
         LR    RF,R4               SAVE  PTR  TO   ENTRY                        
*                                                                               
SUMFI50  DS    0H                  GET   NEXT CR   ENTRY                        
         LA    R4,CRLNQ(,R4)       NEXT  CR   ENTRY                             
         LA    R7,1(,R7)           NEXT  CR   ENTRY     NUMBER                  
         C     R7,CRNUMENT         PROCESSED  ALL  CR   TBL  ENTRIES ?          
         BNH   SUMFI45             NO,   TEST THIS ENTRY                        
*                                                                               
         LTR   R4,RF               WERE  ALL  ENTRIES   ZERO ?                  
         BZ    SUMFIFOX            YES,  COMPLETED FIFO AGEING;   EXIT          
*                                        BECAUSE   ALL  CR   ENTRIES            
*                                        AND       ALL  DR   ENTRIES            
*                                        ARE  ZERO.     I.E. NOTHING            
*                                        TO   NEGATE                            
*                                                                               
         ZAP   CRAMNT,DRPLUSCR     NO,   INSERT    TABLES    SUM                
         B     SUMFI90             COMPLETED  FIFO AGEING                       
*                                                                               
         EJECT ,                                                                
SUMFI60  DS    0H                  RAN   OUT  OF   CR   ENTRIES                 
         CLC   DRDTTYPE,TAGEBYTX   THIS  PASS'S    AGE  BY   TX   ENTRY         
         BNE   SUMFI75             NO,   GET  NEXT DR   ENTRY                   
*                                                                               
         CP    DRAMNT,=P'0'        IS    DR   AMOUNT    NEGATIVE ?              
         BZ    SUMFI75             ZERO, GET  NEXT DR   ENTRY                   
         BP    SUMFI90             NO,   COMPLETED FIFO AGEING                  
*                                                                               
*                                  YES,  ADD  AMOUNT    TO   CR   TBL           
*                                  CURRENT    NUM  OF   CR   ENTRIES            
         L     R6,CRTAB#-CRTAB(,R5)                                             
*                                  MAXIMUM    NUM  OF   CR   ENTRIES            
         L     R7,CRTABMAX-CRTAB(,R5)                                           
*                                  ->    CR   TBL  DATA START                   
         LA    R4,CRTABSTA-CRTAB(,R5)                                           
         GOTO1 BINSRCH,DMCB,(1,(R2)),(R4),(R6),CRLNQ,(0,CRSKYLNQ),(R7)          
*                                  UPDATE     CURR NUM  OF   ENTRIES            
         MVC   CRTAB#-CRTAB(4,R5),DMCB+8                                        
         OC    DMCB(4),DMCB        TABLE      FULL ?                            
         BNZ   *+6                 NO,   CONTINUE                               
         DC    H'00'               YES,  ABEND                                  
*                                                                               
         CLI   DMCB,1              NOT   FOUND,    I.E. ADDED ?                 
         BE    SUMFI70             YES,  TABLE     ENTRY     COMPLETE           
*                                  NO,   OLD       ENTRY                        
         L     R5,DMCB             ->    TABLE     ENTRY                        
         ZAP   CRAMNT,DRAMNT       MOVE  AMOUNT                                 
*                                                                               
SUMFI70  DS    0H                                                               
         ZAP   DRAMNT,=P'0'        CLEAR DR   ENTRY     AMOUNT                  
         B     SUMFI05             START ALL  OVER AGAIN                        
*                                                                               
SUMFI75  DS    0H                                                               
         LA    R2,DRLNQ(,R2)       NEXT  DR   ENTRY                             
         LA    R6,1(,R6)           NEXT  DR   ENTRY     NUMBER                  
         C     R6,DRNUMENT         PROCESSED  ALL  DR   TBL  ENTRIES ?          
         BNH   SUMFI60             NO,   TEST THIS ENTRY                        
*        B     SUMFI90             YES,  COMPLETED FIFO AGEING                  
*                                                                               
         EJECT ,                                                                
SUMFI90  DS    0H                  COMPLETED  FIFO AGEING                       
         CLI   SWAPSTAT,YES        WERE  THE  TBLS SWAPPED ?                    
         BNE   SUMFIFOX            NO,   EXIT                                   
         LR    R1,R3               SWAP  SIGNS     OF   ALL                     
         BAS   RE,NEGATE                 DR  (REALLY    CR)                     
*                                        TBL ENTRIES                            
         LR    R1,R5               SWAP  SIGNS     OF   ALL                     
         BAS   RE,NEGATE                 CR  (REALLY    DR)                     
*                                        TBL ENTRIES                            
         MVI   SWAPSTAT,NO         NO    LONGER    NEGATED                      
*                                                                               
SUMFIFOX DS    0H                                                               
         B     XIT                 EXIT                                         
*                                                                               
         DROP  R2,R4               KEEP  IT   CLEAN                             
         EJECT ,                                                                
*--------------------------------------------------------------------*          
*                                                                    *          
*        NEGATE ALL VALUES IN THE TABLE.                             *          
*                                                                    *          
*        NOTE: ONLY NEGATES AMOUNT VALUES FOR ENTRIES WHOSE DATE     *          
*              TYPE MATCHES THE DATE TYPE IN TAGEBYTX                *          
*                                                                    *          
*        INPUT:                                                      *          
*              R1 = ADDRESS OF THE TABLE TO BE NEGATED               *          
*                                                                    *          
*--------------------------------------------------------------------*          
         SPACE 1                                                                
         USING CRDRHDRD,R1         CR    AND  DR   TBL  HDR  AREA               
         USING CRDRDATD,R2         CR    AND  DR   TBL  DATA AREA               
         SPACE 1                                                                
NEGATE   NTR1                                                                   
         LA    R2,CRDRDSTA         FIND  1ST  TBL  ENTRY                        
         LA    R3,1                FIRST      TBL  ENTRY                        
         MVC   FULL,CRDR#ENT       GET   NUM  OF   TBL  ENTRIES                 
         C     R3,CRDR#ENT         ANY   ENTRIES ?                              
         BH    NEGATEX             NO,   SKIP                                   
*                                                                               
NEGAT10  DS    0H                                                               
         CLC   CRDRDTYP,TAGEBYTX   THIS  PASS'S    AGE  BY   TX   ENTRY         
         BNE   NEGAT20             NO,   SKIP THIS ENTRY                        
         MP    CRDRAMNT,=P'-1'     MULTIPLY   ENTRY     BY   -1                 
*                                                                               
NEGAT20  DS    0H                                                               
         LA    R2,CRDRLNQ(,R2)     NEXT  TBL  ENTRY                             
         LA    R3,1(,R3)           NEXT  TBL  ENTRY     NUMBER                  
         C     R3,FULL             PROCESSED  ALL  TABLE     ENTRIES ?          
         BNH   NEGAT10             NO,   TEST THIS ENTRY                        
*                                  YES,  EXIT                                   
*                                                                               
NEGATEX  DS    0H                                                               
         B     XIT                 EXIT                                         
*                                                                               
         DROP  R1,R2               KEEP  IT   CLEAN                             
         EJECT ,                                                                
*--------------------------------------------------------------------*          
*                                                                    *          
*        SUM ALL THE VALUES IN THE TABLE TO REPORT THE TABLE TOTALS  *          
*                                                                    *          
*        INPUT:                                                      *          
*              R1 = ADDRESS OF TABLE                                 *          
*                                                                    *          
*--------------------------------------------------------------------*          
         SPACE 1                                                                
         USING CRDRDATD,R2         CR    AND  DR   TBL  ENTRY                   
         USING CRDRHDRD,R3         CR    AND  DR   TBL  HDR  AREA               
         SPACE 1                                                                
RPTTOTS  NTR1                                                                   
         LTR   R3,R1               ->    TABLE     ADDRESS                      
         BZ    RPTTOTEX            NONE, EXIT                                   
         LA    R2,CRDRDSTA         FIND  1ST  ENTRY                             
         LA    R6,1                FIRST      ENTRY                             
         MVC   NUMTBENT,CRDR#ENT   GET   NUM  OF   TBL  ENTRIES                 
         C     R6,NUMTBENT         ANY   ENTRIES ?                              
         BH    RPTTOTEX            NO,   EXIT                                   
*                                                                               
RPTTOT10 DS    0H                  TEST  FOR  GOOD TBL  ENTRY                   
         CLC   CRDRDTYP,TAGEBYTX   THIS  PASS'S    AGE  BY   TX   ENTRY         
         BNE   RPTTOT20            NO,   SKIP                                   
*                                  YES,  FOUND     TBL  ENTRY                   
         CP    CRDRAMNT,=P'0'      AMOUNT     =    ZERO ?                       
         BE    RPTTOT20            YES,  SKIP THIS ENTRY                        
         BAS   RE,RPTADENT         ADD   THIS ENTRY                             
*                                                                               
RPTTOT20 DS    0H                  FIND  NEXT ENTRY                             
         LA    R2,CRDRLNQ(,R2)     NEXT  TBL  ENTRY                             
         LA    R6,1(,R6)           NEXT  TBL  ENTRY     NUMBER                  
         C     R6,NUMTBENT         PROCESSED  ALL  TABLE     ENTRIES ?          
         BNH   RPTTOT10            NO,   TEST THIS ENTRY                        
*                                  YES,  PROCESSING     COMPLETE                
*                                                                               
RPTTOTEX DS    0H                                                               
         B     XIT                 EXIT                                         
*                                                                               
         DROP  R2,R3               KEEP  IT   CLEAN                             
         EJECT ,                                                                
*--------------------------------------------------------------------*          
*                                                                    *          
*        ADD THE AMOUNT IN CRDRAMNT TO THE PROPER TOTALS FIELDS      *          
*                                                                    *          
*        INPUT:                                                      *          
*              R2 = ADDRESS OF THE TABLE ENTRY                       *          
*                                                                    *          
*--------------------------------------------------------------------*          
         SPACE 1                                                                
         USING CRDRDATD,R2         CR    AND  DR   TBL  DATA AREA               
         USING DATESD,R3           START/END  DATE PAIRS                        
         USING JAIDATED,R4         USER  START/END  DATES                       
         SPACE 1                                                                
RPTADENT NTR1                                                                   
         L     R3,ASEDATES         ->    START/END DATE TABLE                   
         L     R4,AUSRDATS         ->    USER'S    START/END DATES              
         ZIC   R5,DATPAIRS         NUM   OF   START/END DATE PAIRS              
*                                                                               
RPTADD10 DS    0H                  SEE   IF   ENTRY     DATE IN   RANGE         
         CLC   CRDRDATE,STARTDT    ENTRY DATE <    START     DATE ?             
         BL    RPTADD30            YES,  TRY  NEXT START/END DATE               
         CLC   CRDRDATE,ENDDT      ENTRY DATE >    END       DATE ?             
         BH    RPTADD30            YES,  TRY  NEXT START/END DATE               
*                                  NO,   IN        START/END DATE RANGE         
*                                                                               
*                                  ADD   AMOUNT TO   THE  TOTAL   FIELD         
         CLI   TAGEBYTX,YES        TOTAL BY   TRANSACTION ?                     
         BE    RPTADD20            YES,  SKIP                                   
         AP    JAISMMOA,CRDRAMNT   NO,   ADD  TO   SUM  BY   MOA  DATE          
         BNO   *+6                 NOT   OVERFLOW, CONTINUE                     
         DC    H'00'               ON    OVERFLOW, ABEND                        
*                                                                               
         B     RPTADD30            CONTINUE                                     
*                                                                               
RPTADD20 DS    0H                                                               
         AP    JAISMTXD,CRDRAMNT   NO,   ADD  TO   SUM  BY   TX   DATE          
         BNO   *+6                 NOT   OVERFLOW, CONTINUE                     
         DC    H'00'               ON    OVERFLOW, ABEND                        
*                                                                               
RPTADD30 DS    0H                  GET   NEXT START/END DATE PAIR               
         LA    R3,DATESLNQ(,R3)    ->    NEXT START/END DATE PAIR               
         LA    R4,JAIDATEQ(,R4)    ->    NEXT START/END DATE (USER'S)           
         BCT   R5,RPTADD10         TEST  NEXT START/END DATE RANGE              
*                                                                               
RPTADDEX DS    0H                                                               
         B     XIT                 EXIT                                         
*                                                                               
         DROP  R2,R3,R4            KEEP  IT   CLEAN                             
         EJECT ,                                                                
*--------------------------------------------------------------------*          
*                                                                    *          
*        TEMPORARY SUM OF ALL THE VALUES IN A TABLE TO REPORT THE    *          
*        CURRENT TABLE TOTALS                                        *          
*                                                                    *          
*        INPUT:                                                      *          
*              R1 = ADDRESS OF TABLE                                 *          
*                                                                    *          
*--------------------------------------------------------------------*          
         SPACE 1                                                                
         USING CRDRDATD,R2         CR    AND  DR   TBL  ENTRY                   
         USING CRDRHDRD,R3         CR    AND  DR   TBL  HDR  AREA               
         SPACE 1                                                                
TEMPTOTS NTR1                                                                   
         LTR   R3,R1               ->    TABLE     ADDRESS                      
         BZ    TEMPTOTX            NONE, EXIT                                   
*                                                                               
         LA    R2,TEMPTOTA         TEMPORARY  TOTALS    AREA                    
         LA    R4,MAX#DATS         NUMBER     OF   TOTALS    SLOTS              
*                                                                               
TEMPTO10 DS    0H                  CLEAR THE  TEMPORARY TOTALS    AREA          
         ZAP   0(PKDAMNTQ,R2),=P'0'                                             
         LA    R2,PKDAMNTQ(,R2)    NEXT  TOTALS    FIELD                        
         BCT   R4,TEMPTO10         CLEAR NEXT FIELD                             
*                                                                               
         LA    R2,CRDRDSTA         FIND  1ST  ENTRY                             
         LA    R6,1                FIRST      ENTRY                             
         MVC   NUMTBENT,CRDR#ENT   GET   NUM  OF   TBL  ENTRIES                 
         C     R6,NUMTBENT         ANY   ENTRIES ?                              
         BH    TEMPTOTX            NO,   EXIT                                   
*                                                                               
TEMPTO20 DS    0H                  TEST  FOR  GOOD TBL  ENTRY                   
         CLC   CRDRDTYP,TAGEBYTX   THIS  PASS'S    AGE  BY   TX   ENTRY         
         BNE   TEMPTO30            NO,   SKIP                                   
*                                  YES,  FOUND     TBL  ENTRY                   
         CP    CRDRAMNT,=P'0'      AMOUNT     =    ZERO ?                       
         BE    TEMPTO30            YES,  SKIP THIS ENTRY                        
         BAS   RE,TEMPADD          ADD   THIS ENTRY                             
*                                                                               
TEMPTO30 DS    0H                  FIND  NEXT ENTRY                             
         LA    R2,CRDRLNQ(,R2)     NEXT  TBL  ENTRY                             
         LA    R6,1(,R6)           NEXT  TBL  ENTRY     NUMBER                  
         C     R6,NUMTBENT         PROCESSED  ALL  TABLE     ENTRIES ?          
         BNH   TEMPTO20            NO,   TEST THIS ENTRY                        
*                                  YES,  PROCESSING     COMPLETE                
*                                                                               
TEMPTOTX DS    0H                                                               
         B     XIT                 EXIT                                         
*                                                                               
         DROP  R2,R3               KEEP  IT   CLEAN                             
         EJECT ,                                                                
*--------------------------------------------------------------------*          
*                                                                    *          
*        ADD THE AMOUNT IN CRDRAMNT TO THE PROPER TEMPORARY TOTALS   *          
*        FIELDS                                                      *          
*                                                                    *          
*        INPUT:                                                      *          
*              R2 = ADDRESS OF THE TABLE ENTRY                       *          
*                                                                    *          
*--------------------------------------------------------------------*          
         SPACE 1                                                                
         USING CRDRDATD,R2         CR    AND  DR   TBL  DATA AREA               
         USING DATESD,R3           START/END  DATE PAIRS                        
         SPACE 1                                                                
TEMPADD  NTR1                                                                   
         L     R3,ASEDATES         ->    START/END DATE TABLE                   
         LA    R4,TEMPTOTA         ->    TEMPORARY TOTALS    AREA               
         ZIC   R5,DATPAIRS         NUM   OF   START/END DATE PAIRS              
*                                                                               
TEMPAD10 DS    0H                  SEE   IF   ENTRY     DATE IN   RANGE         
         CLC   CRDRDATE,STARTDT    ENTRY DATE <    START     DATE ?             
         BL    TEMPAD20            YES,  TRY  NEXT START/END DATE               
         CLC   CRDRDATE,ENDDT      ENTRY DATE >    END       DATE ?             
         BH    TEMPAD20            YES,  TRY  NEXT START/END DATE               
*                                  NO,   IN        START/END DATE RANGE         
*                                                                               
*                                  ADD   AMOUNT TO   THE  TOTALS  FIELD         
         AP    0(PKDAMNTQ,R4),CRDRAMNT                                          
         BNO   TEMPAD20            NOT   OVERFLOW, CONTINUE                     
         DC    H'00'               ON    OVERFLOW, ABEND                        
*                                                                               
TEMPAD20 DS    0H                  GET   NEXT START/END DATE PAIR               
         LA    R3,DATESLNQ(,R3)    ->    NEXT START/END DATE PAIR               
         LA    R4,PKDAMNTQ(,R4)    ->    NEXT TOTALS    FIELD                   
         BCT   R5,TEMPAD10         TEST  NEXT START/END DATE RANGE              
*                                                                               
TEMPADEX DS    0H                                                               
         B     XIT                 EXIT                                         
*                                                                               
         DROP  R2,R3               KEEP  IT   CLEAN                             
         EJECT ,                                                                
**********************************************************************          
*        GET MODULE WORK AREA                                        *          
**********************************************************************          
         SPACE 1                                                                
GETWORKA DS    0H                                                               
         NTR1                                                                   
         L     R2,=A(JOBAGELQ)     MODULE     WORK AREA SIZE                    
         LA    R2,7(,R2)           ROUND      TO                                
         SRL   R2,3                           DOUBLE    WORD                    
         SLL   R2,3                           BOUNDARY                          
*                                                                               
*        IF WE ADD CODE TO GET SPACE FOR TABLES DEPENDENT ON                    
*        THE NUMBER OF DATE PAIRS, REMEMBER TO ROUND EACH TO                    
*        A DOUBLE WORD BOUNDARY.  ALSO, REMEMBER TO UPDATE                      
*        SCRIBE LOGIC AND THE SCRIBE INITIALIZATION TABLE TO                    
*        FREE THIS AREA AFTER EACH REQUEST.                                     
*                                                                               
         ST    R2,JAIWORKL         SAVE  IT                                     
         LR    R0,R2                                                            
         GETMAIN R,LV=(R0)         GET   IT                                     
         ST    R1,JAIWORKA         SAVE  WORK AREA ADDRESS                      
         LR    RC,R1               MAKE  WORK AREA AVAILABLE                    
*                                                                               
         XC    JAIADMPP,JAIADMPP   CLEAR ADDR OF   DUMP POINTERS                
*                                                                               
         ICM   R2,15,JAIVXTRA      ->    VXTRAS                                 
         BZ    GETWKEX             NOT   PASSED,   EXIT                         
*                                                                               
         USING RUNXTRAD,R2                                                      
*                                                                               
         L     R2,ADMASTD          ->    MASTER    DSECT                        
*                                                                               
         USING MASTD,R2                                                         
*                                                                               
         LA    R3,MX#DUMPP         MAXIMUM    NUM  OF   DUMP POINTERS           
         LA    R4,MCUSRDMP         EXTRA DUMP AREA                              
*                                                                               
GETWK20  DS    0H                                                               
         NC    4(4,R4),4(R4)       EXTRA DUMP AREA PTRS AVAILABLE ?             
         BZ    GETWK30             YES,  GO   TO   USE  IT                      
         LA    R4,8(,R4)           NO,   TRY  NEXT AREA                         
         BCT   R3,GETWK20          TEST  NEXT AREA                              
         B     GETWKEX             NONE  AVAILABLE,     EXIT                    
*                                                                               
GETWK30  DS    0H                                                               
         STCM  RC,15,0(R4)         SAVE  IN   EXTRA     DUMP AREA PTRS          
         LR    RF,RC               GET   END  OF   DUMP AREA +1                 
         A     RF,JAIWORKL                                                      
         STCM  RF,15,4(R4)         SAVE  IN   EXTRA     DUMP AREA PTRS          
         ST    R4,JAIADMPP         SAVE  ADDR OF   DUMP POINTERS                
*                                                                               
GETWKEX  DS    0H                                                               
         B     XIT                 RETURN                                       
*                                                                               
         DROP  R2                  KEEP  IT   CLEAN                             
         EJECT ,                                                                
**********************************************************************          
*        FREE MODULE WORK AREA                                       *          
**********************************************************************          
         SPACE 1                                                                
FREEWORK DS    0H                                                               
         NTR1                                                                   
         L     R0,JAIWORKL         LNG   MODULE    WORK AREA                    
         LR    R1,RC               ->    MODULE    WORK AREA                    
         FREEMAIN R,A=(R1),LV=(R0) FREE  THE  STORAGE                           
*                                                                               
         ICM   R2,15,JAIADMPP      ->    DUMP AREA POINTERS                     
         BZ    FREEWEX             NONE, EXIT                                   
         XC    0(8,R2),0(R2)       CLEAR EXTRA     DUMP AREA POINTERS           
         XC    JAIADMPP,JAIADMPP   CLEAR ADDR OF   DUMP AREA POINTERS           
*                                                                               
FREEWEX  DS    0H                                                               
         XC    JAIWORKA,JAIWORKA   CLEAR MODULE    WORK AREA ADDRESS            
         XC    JAIWORKL,JAIWORKL   CLEAR MODULE    WORK AREA LENGTH             
         B     XIT                 RETURN                                       
         EJECT ,                                                                
**********************************************************************          
*        GENERATE DEBUGGING OUTPUT                                   *          
**********************************************************************          
         SPACE 1                                                                
DEBUGIT  NTR1                                                                   
         CLI   DEBUGSW,YES         DEBUG REQUESTED ?                            
         BNE   DEBUGITX            NO,   EXIT                                   
*                                  TEST  MAXIMUM   DEBUG  PRINTOUTS             
         CLC   DEBUGCNT,=YL2(MAXDEBUG)                                          
         BNL   DEBUGITX            YES,  EXIT                                   
         LH    R2,DEBUGCNT         GET   DEBUG     PRINTOUTS                    
         LA    R2,1(,R2)           ADD   ONE                                    
         STH   R2,DEBUGCNT         SAVE  DEBUG     PRINTOUTS                    
*                                                                               
         CLI   BYTE,DEBUGTX        PRINT TRANSACTION ?                          
         BNE   DEBUG20             NO,   SKIP                                   
         LA    R2,L'CTRANS         HEADING    LENGTH                            
         LA    R3,CTRANS           HEADING    CONSTANT                          
         L     R4,ATX              TRANSACTION     ELEMENT                      
*                                  TRANSACTION     LENGTH                       
         ZIC   R5,TRNLN-TRNELD(,R4)                                             
         SH    R4,DATADISP         TRANSACTION     RECORD                       
         AH    R5,DATADISP         DISPLAY         BOTH                         
         B     DEBUGPRT            PRINT IT                                     
*                                                                               
DEBUG20  DS    0H                                                               
         CLI   BYTE,DEBUGPRO       PRINT PRORATA ?                              
         BNE   DEBUGITX            NO,   SKIP                                   
         LA    R2,L'CPRORATA       HEADING    LENGTH                            
         LA    R3,CPRORATA         HEADING    CONSTANT                          
         L     R4,APRORATA         PRORATA    ELEMENT                           
         LA    R5,1                PRORATA    LENGTH                            
         LR    RE,R4               ->         PRORATA   ELEMENT                 
         SR    RF,RF               CLEAR REGISTER                               
*                                                                               
DEBUG25  DS    0H                                                               
*                                  ANY   MORE ?                                 
         CLI   PTAEL-PTAELD(RE),X'00'                                           
         BE    DEBUGPRT            NO    PRINT    IT                            
*                                  ELEMENT    LENGTH                            
         IC    RF,PTALN-PTAELD(,RE)                                             
         AR    R5,RF               ADD   EL   LENGTH                            
         AR    RE,R5               ->    NEXT ELEMENT                           
         B     DEBUG25             TEST  NEXT ELEMENT                           
*                                                                               
DEBUGPRT DS    0H                                                               
         LA    R6,=C'2D'           DEBUG FORMAT                                 
*                                        SKIP TWO  LINES                        
*                                        DUMP FORMAT                            
         GOTO1 APRNTBL,DMCB,((R2),(R3)),(R4),C'DUMP',(R5),(R6),        X        
               (C'P',APRINT)                                                    
*                                                                               
DEBUGITX DS    0H                                                               
         B     XIT                 RETURN                                       
         EJECT ,                                                                
**********************************************************************          
*        CONSTANTS                                                   *          
**********************************************************************          
         SPACE 1                                                                
SPACES   DC    CL132' '            SPACES                                       
*                                                                               
LOWVALUE DC    XL16'00'            LOW   VALUE                                  
HIGHVALU DC    16XL1'FF'           HIGH  VALUE                                  
*                                                                               
*                                  DEBUG CONSTANTS                              
CTRANS   DC    C' ACJOBAGE - TRANSACTION'                                       
CPRORATA DC    C' ACJOBAGE - PRORATA'                                           
         SPACE 3                                                                
         GETEL R4,DATADISP,ELCODE  GETEL/NEXTEL CODE                            
         EJECT ,                                                                
         LTORG                                                                  
         EJECT ,                                                                
       ++INCLUDE ACJOBAGED                                                      
         EJECT ,                                                                
       ++INCLUDE ACJOBAGEI                                                      
         EJECT ,                                                                
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
* DDREPXTRAD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDREPXTRAD                                                     
         PRINT ON                                                               
* DDMASTD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DDMASTD                                                        
         PRINT ON                                                               
* ACASOFD                                                                       
         PRINT OFF                                                              
       ++INCLUDE ACASOFD                                                        
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'007ACJOBAGE  12/20/11'                                      
         END                                                                    
