*          DATA SET PPGETSPC   AT LEVEL 016 AS OF 07/29/09                      
*CATALP PPGETSPC                                                                
         TITLE 'PPGETSPC-MODULE TO FIND VALUES FROM PSPACEREC'                  
*                                                                               
*        THIS ROUTINE RETURNS A CALCULATED MATERIALS CLOSING DATE               
*        USING THE FORMULA FOUND IN A PRINTPAK SPACE RECORD (PSPACEREC)         
*        IT ALSO RETURNS THE OVERAGE PERCENTAGE                                 
*                                                                               
*        NOTE THAT THIS MODULE READS THE PRINT FILE                             
*        AND MAY DESTROY SEQUENCIAL READING                                     
*                                                                               
*        CHANGE LOG                                                             
*                                                                               
*      BOBY JUN/09   Y2K PROBLEM IN CVBIN                                       
*                                                                               
*      BPLA JAN/95   IF BUG - IF PBDMDATE ALREADY THERE- NO LOOKUP              
*                                                                               
*      BPLA 9/1/94   IF SPACES RECORD NOT FOUND FOR OUTDOOR SHOW                
*                    THEN CHECK LOOKING FOR OUTSPACE 2                          
*                    FIRST X'66' ELEM (17 BYTES)                                
*                                                                               
*        PARAMETER LIST                                                         
*                                                                               
*        INPUT                                                                  
*                                                                               
*        1       C'B'  A(BUYREC)                                                
*             OR C'S'  A(AGY,MED,CLT,17 BYTE SPACE,DATE- 3-BYTE BINARY)         
*                                                                               
*        2       A(DATAMGR)                                                     
*                                                                               
*        3       A(ADDAY)   IF NOT PRESENT ONLY OVERAGE IS RETURNED             
*                                                                               
*        OUTPUT                                                                 
*                                                                               
*        1       X'FF' IF ERROR                                                 
*                OTHERWISE WILL CONTAIN CALCULATED MATERIALS CLOSING            
*                DATE                                                           
*                                                                               
*        2       FIRST BYTE WILL CONTAIN LOOKED-UP OVERAGE PERCENTAGE           
*                IF X'00' - NO OVERAGE WAS ENTERED OR NO SPACE                  
*                           RECORD FOUND                                        
*                                                                               
*                IF X'FF' - ZERO WAS ENTERED - MEANS DO NOT APPLY               
*                OVERAGE TO THIS SPACE                                          
*                                                                               
*                                                                               
*         3      UNCHANGED                                                      
*                                                                               
PPGETSPC CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,PPGETSPC,CLEAR=YES                                   
         USING WORKD,RC                                                         
         LR    R8,R1               R8=A(PARAMETER LIST)                         
         L     R2,0(R1)         A(PBUYREC) (OR SPACE AREA), A(DATAMGE)          
*                                                                               
         MVC   DATAMGR,4(R8)  V(DATAMGR)                                        
         MVC   ADDAY,8(R8)    V(ADDAY)  (NOT PRESENT IF ONLY OVERAGE            
*                                        IS TO BE RETURNED)                     
*                                                                               
         MVC   BUYSW,0(R1)    SAVE BUYREC OR SPACE INDICATOR                    
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
*                                                                               
*                                                                               
         USING PBUYRECD,R2                                                      
*                                                                               
         CLI   BUYSW,C'B'         SEE IF I HAVE A BUYREC                        
         BNE   GETSPC2                                                          
         MVC   MYAGY,PBUYKAGY                                                   
         MVC   MYMED,PBUYKMED                                                   
         MVC   MYCLT,PBUYKCLT                                                   
         MVC   MYDATE,PBUYKDAT                                                  
         XC    0(4,R1),0(R1)  USED TO RETURN MATERIALS CLOSING DATE             
         MVI   4(R1),0        USED TO RETURN OVERAGE PERCENTAGE                 
*                                                                               
         MVC   0(3,R1),PBDMDATE    SAVE IF I ALREADY HAVE                       
         B     GETSPC3                                                          
*                                                                               
GETSPC2  CLI   BUYSW,C'S'         SEE IF I HAVE A SPACE DESC                    
         BNE   GETSPCER           INVALID PARAMETER LIST                        
         MVC   MYAGY,0(R8)                                                      
         MVC   MYMED,2(R8)                                                      
         MVC   MYCLT,3(R8)                                                      
         MVC   MYDATE,23(R8)        DATE                                        
         MVC   WORK(17),6(R8)       GIVEN SPACE DESC                            
         OC    WORK(17),SPACES                                                  
         CLC   WORK(17),SPACES                                                  
         XC    0(4,R1),0(R1)  USED TO RETURN MATERIALS CLOSING DATE             
         MVI   4(R1),0        USED TO RETURN OVERAGE PERCENTAGE                 
*                                                                               
         BE    GETSPCER           NO SPACE GIVEN                                
         B     GETALL                                                           
*                                                                               
GETSPC3  DS    0H                                                               
         MVC   WORK(17),SPACES                                                  
*                                                                               
GETSPC3C CLI   PBUYKMED,C'N'       SEE IF NEWSPAPERS                            
         BE    GETN                                                             
         CLI   PBUYKMED,C'O'       SEE IF OUTDOOR                               
         BE    GETO                                                             
*                                                                               
GETM     DS    0H           MAGAZINES/SUPPLEMENTS/TRADE                         
         CLI   PBDSPACE,C'#'                                                    
         BNE   GETM5                                                            
         MVC   WORK(16),PBDSPACE+1                                              
         B     GETM7                                                            
*                                                                               
GETM5    MVC   WORK(17),PBDSPACE                                                
*                                                                               
GETM7    OC    WORK(17),SPACES                                                  
         B     GETALL                                                           
*                                                                               
GETN     DS    0H             NEWSPAPERS                                        
         CLI   PBDSPACE,C'#'                                                    
         BNE   GETN5                                                            
         MVC   WORK(7),PBDSPACE+1                                               
         B     GETN7                                                            
*                                                                               
GETN5    MVC   WORK(8),PBDSPACE                                                 
GETN7    OC    WORK(17),SPACES                                                  
         B     GETALL                                                           
*                                                                               
GETO     DS    0H             OUTDOOR                                           
*                                                                               
*        ONLY OUTSPACE 2 IS USED FOR SPACE LOOK-UP                              
*        CODE BELOW NO-OPED FOR NOW                                             
*        THIS CODE WOULD TRY TO FIND AND SRI MATCH FIRST                        
*                                                                               
***      MVC   WORK(10),PBDSPACE                                                
***      CP    PBDSHOW,=P'0'     SEE IF SHOW GIVEN                              
***      BNE   GETALL                                                           
***      CP    PBDREG,=P'0'     SEE IF REG GIVEN                                
***      BNE   GETALL                                                           
***      CP    PBDILLUM,=P'0'     SEE IF ILLUM GIVEN                            
***      BNE   GETALL                                                           
*********************************************                                   
GETO3    LA    R6,PBUYREC+33                                                    
         MVI   ELCODE,X'66'                                                     
         BAS   RE,NEXTEL                                                        
         BNE   GETSPCX        NO SPACE ELEM FOUND                               
         CLI   1(R6),20       TO ALLOW FOR 17 PLUS # OR *                       
         BH    GETSPCX                                                          
         BNE   GETO3C                                                           
         CLI   2(R6),C'#'                                                       
         BE    GETO3C                                                           
         CLI   2(R6),C'*'                                                       
         BE    GETO3C                                                           
         B     GETSPCX                                                          
*                                                                               
GETO3C   DS    0H                                                               
         ZIC   R4,1(R6)                                                         
         SH    R4,=H'3'                                                         
         BM    GETSPCX                                                          
         LA    R7,2(R6)                                                         
         CLI   2(R6),C'#'                                                       
         BE    GETO4                                                            
         CLI   2(R6),C'*'                                                       
         BNE   GETO5                                                            
GETO4    LA    R7,1(R7)                                                         
         SH    R4,=H'1'                                                         
         BM    GETSPCX                                                          
*                                                                               
GETO5    EX    R4,*+8                                                           
         B     *+10                                                             
*                                                                               
         MVC   WORK(0),0(R7)                                                    
         OC    WORK(17),SPACES                                                  
*                                                                               
GETALL   DS    0H             WORK HAS SPACE DESCRIPTION                        
*                                                                               
         LA    R9,KEY                                                           
         USING PSPCREC,R9                                                       
         XC    PSPCKEY,PSPCKEY                                                  
         MVC   PSPCKAGY(3),MYAGY      (AGY/MEDIA)                               
         MVI   PSPCKTYP,X'2B'                                                   
         MVC   PSPCKPUB,PBUYKPUB                                                
         CLI   BUYSW,C'S'                                                       
         BNE   *+10                                                             
         MVC   PSPCKPUB,=6X'FF'                                                 
         MVC   PSPCKCLT,MYCLT                                                   
*                                                                               
GETSPC4  MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,(0,DMRDHI),PRTDIR,KEY,KEY                           
         B     GETSPC7                                                          
*                                                                               
GETSPC5  GOTO1 DATAMGR,DMCB,(0,DMRSEQ),PRTDIR,KEY,KEY                           
*                                                                               
GETSPC7  CLC   KEY(4),KEYSAVE      CHK THROUGH RECORD TYPE                      
         BNE   GETSPCN                                                          
         CLI   KEY+4,X'FF'         SEE IF "ALL" PUB RECORD FOUND                
         BE    GETSPC7H            GO CHK CLIENT                                
         CLC   KEY+4(6),KEYSAVE+4  CHECK FOR FULL PUB MATCH                     
         BE    GETSPC7H                                                         
         CLC   KEY+4(4),KEYSAVE+4  CHK FOR BASE PUB NUMBER                      
         BNE   GETSPC7C            NO - GO READ FOR "ALL" PUBREC                
         CLC   KEY+8(2),=X'FFFF'   SEE IF ALL/ZONE/EDT RECORD FOUND             
         BE    GETSPC7H            YES GO CHK CLIENT                            
*                                                                               
         MVC   KEY+8(2),=2X'FF'    TRY FOR ALL ZONE/EDT REC                     
         MVC   KEY+10(3),MYCLT                                                  
         B     GETSPC4                                                          
*                                                                               
         B     GETSPC5                                                          
*                                                                               
GETSPC7C XC    KEY+4(28),KEY+4     GO TRY FOR "ALL" PUB PSPACEREC               
         MVC   KEY+4(6),=6X'FF'                                                 
         MVC   KEY+10(3),MYCLT                                                  
         B     GETSPC4                                                          
*                                                                               
GETSPC7H DS    0H                                                               
         CLC   KEY+10(3),KEYSAVE+10    SEE IF RIGHT CLT FOUND                   
         BE    GETSPC10                YES - PROCESS                            
         CLC   KEY+10(3),=3X'FF'    SEE IF "ALL" CLT REC FOUND                  
         BE    GETSPC10             YES - PROCESS                               
         CLI   KEYSAVE+10,X'FF'  SEE IF I WAS LOOKING FOR ALL CLT REC           
         BE    GETSPCN             IF SO STOP LOOKING                           
         MVC   KEY+10(3),=3X'FF'                                                
         B     GETSPC4                                                          
*                                                                               
*                                                                               
         DROP  R9                                                               
*                                                                               
GETSPC10 DS    0H                  READ THIS RECORD                             
*                                                                               
         GOTO1 DATAMGR,DMCB,GETREC,PRTFIL,KEY+27,IO,DMWORK                      
*                                                                               
         LA    R7,IO                                                            
         USING PSPCREC,R7                                                       
         LA    R6,PSPCELEM                                                      
         USING PSPCEL01,R6                                                      
         MVI   ELCODE,X'01'                                                     
         CLI   0(R6),0           NO ELEMENTS                                    
         BE    GETSPC50                                                         
         CLC   0(1,R6),ELCODE                                                   
         BE    GETSPC20                                                         
GETSPC15 BAS   RE,NEXTEL                                                        
         BNE   GETSPC50                                                         
GETSPC20 CLC   PSPCDESC,WORK                                                    
         BE    GETSPC30                                                         
         B     GETSPC15                                                         
*                                                                               
GETSPC30 MVC   MYMCLOM,PSPCMCM       MATERIALS CLOSING MONTH                    
         MVC   MYMCLOD,PSPCMCD       DAY                                        
         MVC   4(1,R8),PSPCORV       SAVE OVERAGE PERCENTAGE                    
         OC    PBDMDATE,PBDMDATE   SEE IF BUY HAS A CLOSING DATE                
         BZ    GETSPC40                                                         
         MVC   0(3,R8),PBDMDATE   ALWAYS USE IT                                 
         B     GETSPCX                                                          
*                                                                               
*        CALUCULATE MATERIALS CLOSING DATE                                      
GETSPC40 DS    0H                                                               
         OC    ADDAY,ADDAY       SEE IF I WAS PASSED ADDRESS OF ADDAY           
         BZ    GETSPCX           IF NOT NO MATERIALS DATE IS RETURNED           
*                                                                               
         LA    R3,MYDATE                                                        
         LA    R4,MYMCLOM                                                       
*                                                                               
         CP    MYMCLOM,=P'999'   CHECK FOR SPECIAL CODE                         
         BNE   *+10                                                             
         ZAP   MYMCLOM,=P'0'     SET TO ZERO                                    
*                                                                               
         CP    MYMCLOD,=P'999'   CHECK SPECIAL CODE                             
         BNE   *+10                                                             
         ZAP   MYMCLOD,=P'0'     SET TO ZERO                                    
*                                                                               
         CP    MYMCLOM,=P'0'                                                    
         BNE   GETSPC43                                                         
         CP    MYMCLOD,=P'0'    IF BOTH NOW 0-THEN NO CALCULATION               
         BE    GETSPCX                                                          
*                                                                               
GETSPC43 DS    0H                                                               
         LA    R5,0(R8)         OUTPUT                                          
         L     RF,ADDAY                                                         
         BAS   RE,DATCOMP                                                       
*                                                                               
         B     GETSPCX                                                          
*                                                                               
GETSPC50 DS    0H               SPACE NOT FOUND IN THE PSPACEREC                
         CLC   PSPCKPUB(9),=9X'FF'    SEE IF I WAS PROCESSING THE               
         BE    GETSPCN                ALL PUB/ALL CLT RECORD                    
         CLC   PSPCKCLT,=3X'FF'        SEE IF I WAS PROCESSING AN               
         BE    GETSPC60               "ALL" CLT REC                             
         MVC   KEY+10(3),=3X'FF'      TRY FOR "ALL" CLT RECORD                  
         B     GETSPC4                LEAVING PUB ALONE                         
*                                                                               
GETSPC60 DS    0H                                                               
         CLC   PSPCKPUB+4(2),=2X'FF' SEE IF I WAS PROCESSING  ALL Z/E           
         BE    GETSPC65                                                         
         MVC   KEY+8(2),=2X'FF'      TRY FOR ALL Z/E RECORD                     
         MVC   KEY+10(3),MYCLT       FOR MY CLIENT                              
         B     GETSPC4                                                          
*                                                                               
GETSPC65 MVC   KEY+4(6),=6X'FF'    TRY FOR ALL PUB CLT RECORD                   
         MVC   KEY+10(3),MYCLT      MY CLIENT                                   
         B     GETSPC4            GO TRY FOR IT                                 
*                                                                               
GETSPCER MVI   0(R8),X'FF'                                                      
         B     GETSPCX                                                          
*                                                                               
GETSPCN  DS    0H                                                               
*                                                                               
*        CODE BELOW NO-OPED FOR NOW                                             
*        IT WOULD TRY FOR A SPACE MATCH ON OUTSPACE 2                           
*        IF I DIDN'T FIND A MATCH ON SRI                                        
*                                                                               
****     CLI   BUYSW,C'B'        SEE IF PROCESSING A BUY                        
****     BNE   GETSPCX                                                          
****     CLI   MYMED,C'O'         SEE IF OUTDOOR                                
****     BNE   GETSPCX                                                          
****     CLI   WORK,X'FF'         SEE IF I WAS LOOKING FOR A SRI                
****     BNE   GETSPCX                                                          
****     MVC   WORK(17),SPACES                                                  
****     B     GETO3             NOW GO TRY FOR OUTSPACE 2                      
*                                                                               
GETSPCX  DS    0H                                                               
*                                                                               
         XIT                                                                    
*                                                                               
         EJECT                                                                  
*                                                                               
NEXTEL   DS    0H                                                               
         ZIC   R0,1(R6)                                                         
         AR    R6,R0                                                            
         CLI   0(R6),0            END OF RECORD                                 
         BE    NEXTELX                                                          
         CLC   0(1,R6),ELCODE                                                   
         BER   RE                                                               
         B     NEXTEL                                                           
*                                                                               
NEXTELX  LTR   RE,RE                                                            
         BR    RE                                                               
         EJECT                                                                  
*                                  COMPUTE DATE BASED ON INPUT                  
*                                  DATE AND ALGORITHM                           
*                                  R3 = A(INPUT DATE)                           
*                                  R4 = A(ALGORITHM)                            
*                                  R5 = A(OUTPUT)                               
DATCOMP  NTR1                                                                   
         SPACE 1                                                                
         MVC   0(3,R5),0(R3)                                                    
         CP    0(2,R4),=P'0'       ALG0 MO                                      
         BNE   DC10                                                             
*                                  ALGO MO = 0                                  
         TM    2(R4),X'F0'         IS IT 'PLUS'NUMBER                           
         BZ    DC1                                                              
         NI    2(R4),X'0F'                                                      
         ZAP   DUB,2(2,R4)                                                      
         B     DC4                                                              
         SPACE                                                                  
DC1      ZAP   DUB,2(2,R4)         ALGO DAY                                     
         BM    DC4                 IF NEG SUBTRACT DAYS                         
         BZ    DC6                 IF 0 USE FOM                                 
*                                  IF POS                                       
         SPACE                                                                  
         CVB   R0,DUB              NO/JUST MOVE IT                              
         STC   R0,2(R5)                                                         
DC2      EQU   *                                                                
         SR    R1,R1                                                            
         IC    R1,1(R5)                                                         
         LA    R1,MOTAB-1(R1)                                                   
         CLC   2(1,R5),0(R1)       CHECK VS HI DAY IN MONTH                     
         BNH   *+10                                                             
         MVC   2(1,R5),0(R1)                                                    
         B     DCEXT                                                            
DC4      EQU   *                                                                
         CVB   R0,DUB                                                           
         BAS   RE,CVEBC            CV TO EBCDIC FOR ADDAY                       
         GOTO1 (RF),DMCB,DUB,DUB,(R0)                                           
         SPACE 1                                                                
         BAS   RE,CVBIN            BACK TO BINARY                               
         B     DCEXT                                                            
DC6      EQU   *                                                                
         MVI   2(R5),1             USE FOM                                      
         B     DCEXT                                                            
*                                  MONTH NOT ZERO                               
DC10     EQU   *                                                                
         SR    R6,R6                                                            
         IC    R6,1(R3)            MO                                           
         ZAP   DUB,0(2,R4)         ALGO MO                                      
         CVB   R0,DUB                                                           
         SR    R1,R1                                                            
         IC    R1,0(R3)            YR                                           
         AR    R6,R0                                                            
         BP    DC12                                                             
         BCTR  R1,R0               DECR YR                                      
         AH    R6,=H'12'                                                        
         BNP   *-6                                                              
         B     DC14                                                             
DC12     EQU   *                                                                
         CH    R6,=H'12'                                                        
         BNH   DC14                                                             
         LA    R1,1(R1)                                                         
         SH    R6,=H'12'                                                        
         B     DC12                                                             
DC14     EQU   *                                                                
         STC   R1,0(R5)                                                         
         STC   R6,1(R5)                                                         
         ZAP   DUB,2(2,R4)                                                      
         BNP   DC16                                                             
         CVB   R0,DUB                                                           
         STC   R0,2(R5)                                                         
         B     DC2                                                              
DC16     EQU   *                                                                
         MVI   2(R5),31            USE EOM                                      
         B     DC2                                                              
DCEXT    EQU   *                                                                
         XIT1                                                                   
         SPACE 3                                                                
CVEBC    EQU   *                                                                
         SR    R1,R1                                                            
         IC    R1,0(R5)                                                         
         CVD   R1,DUB2                                                          
         OI    DUB2+7,X'0F'                                                     
         UNPK  DUB(2),DUB2                                                      
         IC    R1,1(R5)                                                         
         CVD   R1,DUB2                                                          
         OI    DUB2+7,X'0F'                                                     
         UNPK  DUB+2(2),DUB2                                                    
         IC    R1,2(R5)                                                         
         CVD   R1,DUB2                                                          
         OI    DUB2+7,X'0F'                                                     
         UNPK  DUB+4(2),DUB2                                                    
         BR    RE                                                               
         SPACE 3                                                                
CVBIN    EQU   *                   CONVERT EBCIDIC DATE TO BINARY               
         MVC   DUB2(2),DUB         YEAR                                         
         TR    DUB2(2),HEXTABL-C'0'   CVB BY TABLE TO HANDLE 21ST               
*                                  CENTURY DATES - FAF8 EG                      
*                                                                               
         SR    R1,R1                                                            
         IC    R1,DUB2             DECADE                                       
         MHI   R1,10               *10                                          
         SR    R0,R0                                                            
         IC    R0,DUB2+1           YEAR                                         
         AR    R1,R0               BINARY YEAR                                  
         STC   R1,0(R5)                                                         
*                                                                               
         PACK  DUB2,DUB+2(2)                                                    
         CVB   R1,DUB2                                                          
         STC   R1,1(R5)                                                         
*                                                                               
         PACK  DUB2,DUB+4(2)                                                    
         CVB   R1,DUB2                                                          
         STC   R1,2(R5)                                                         
         BR    RE                                                               
         LTORG                                                                  
         SPACE 1                                                                
DMRDHI   DC    C'DMRDHI  '                                                      
DMRSEQ   DC    C'DMRSEQ  '                                                      
GETREC   DC    C'GETREC  '                                                      
PRTDIR   DC    C'PRTDIR  '                                                      
PRTFIL   DC    C'PRTFILE '                                                      
         SPACE 3                                                                
HEXTABL  DC    AL1(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15)                       
*                                                                               
MOTAB    DC    AL1(31,28,31,30,31,30,31,31,30,31,30,31)                         
         SPACE 1                                                                
         EJECT                                                                  
WORKD    DSECT                     ** WORKING STORAGE **                        
WORK     DS    CL20                                                             
SPACES   DS    CL20                                                             
MYMCLOM  DS    PL2      MATERIALS CLOSING DATE FORMULA  - MONTH                 
MYMCLOD  DS    PL2                                        DAY                   
*                                                                               
DUB      DS    D                                                                
DUB2     DS    D                                                                
DMCB     DS    6F                                                               
DATAMGR  DS    V                                                                
ADDAY    DS    V                                                                
DMWORK   DS    12D                                                              
ELCODE   DS    CL1                                                              
*                                                                               
BUYSW    DS    CL1       B= PUBREC, S= AGY/MED/CLT/SPACE                        
MYAGY    DS    CL2       SAVED FROM INPUT PARAMETER 1                           
MYMED    DS    CL1                                                              
MYCLT    DS    CL3                                                              
MYDATE   DS    XL3                                                              
*                                                                               
KEY      DS    CL32                                                             
KEYSAVE  DS    CL32                                                             
IO       DS    4000C                                                            
WORKX    EQU   *                                                                
         EJECT                                                                  
PBUYRECD DSECT                                                                  
       ++INCLUDE PBUYREC                                                        
       ++INCLUDE PBDELEM                                                        
         EJECT                                                                  
*                                                                               
       ++INCLUDE PSPACEREC                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'016PPGETSPC  07/29/09'                                      
         END                                                                    
