*          DATA SET PXB12E     AT LEVEL 009 AS OF 05/01/02                      
*      COPY OF PPB12E     AT LEVEL 005 AS OF 10/20/94                           
*CATALP PPB12E                                                                  
         TITLE 'PPB12E - PRINTPAK BILLING/SOURCE BOOK E'                        
*                                                                               
*      CHANGE LOG                                                               
*   BPLA 9/18/96  PPB12EO SOURCE MADE LIVE - NOW INCLUDES PPREPB1WRK            
*                 NEEDS PPB1ACC                                                 
*                                                                               
*   BPLA 10/19/94  IN VBPRNT - IF AOR BILL DO NOT SHOW                          
*                  ZERO PROVINCIAL VATS FOR CODE S                              
*                                                                               
*                                                                               
*   BPLA 5/94   IF REVERSAL - AND REVESAL DATE IS BEFORE                        
*               JUNE 1/94 SET QST TO 4 PCT (IF APPLICABLE)                      
*                                                                               
*                                                                               
*   BPLA 3/94   NEW SOURCE BOOK - PST                                           
*                                                                               
***********************************************************************         
*                                                                               
*        SETCPVC  SETS CLIENT/PRODUCT VAT CODES (CVATCDS)                       
*                 AND 'MIXED PROD' SWITCHES     (CVATPSWS)                      
*                                                                               
*        PARM1  BYTE  0   PRODUCT CODE                                          
*               BYTES 1-3 NOTE USED                                             
*                                                                               
*        NOTE- MAYBE SHOULD TABLE PRODUCT INFO TO SAVE RE-READS                 
*        NOTE- MAYBE IT SHOULD BE POSSIBLE TO HAVE PST WITHOUT                  
*              GST BUT THAT WOULD NEED SOME WORK. (TOO MUCH RELIANCE            
*              ON CVATSW (NOT CVATSWS) TO CONTROL VAT LOGIC?)                   
*                                                                               
***********************************************************************         
         SPACE 2                                                                
         PRINT NOGEN                                                            
SETCPVC  CSECT                                                                  
         NMOD1 SPCWRKL,SETCPVC                                                  
         USING PPWORKD,RA                                                       
         USING PPWORK2D,R9                                                      
         LR    R8,RC                                                            
         USING SPCWRKD,R8                                                       
         USING BILWRKD,RC                                                       
         LA    RC,SPACEND                                                       
*                                                                               
         CLI   CVATCOD,0           IF GST CODE WAS SET TO NULLS                 
         BE    SCPVX               DONT PICK UP ANY NOW                         
*                                                                               
         LA    R2,CVATCDS          VAT CODES                                    
         XC    0(NPROVS+1,R2),0(R2)                                             
         LA    R3,CVATPSWS         'MIXED PROD' SWITCHES                        
         XC    0(NPROVS+1,R3),0(R3)                                             
*                                                                               
         L     R5,ADCLT                                                         
         LA    R5,33(R5)                                                        
         MVI   ELCODE,X'25'          SEARCH FOR PST ELEM                        
         BAS   RE,SNEXTEL                                                       
         BNE   SCPV2                                                            
         MVC   1(NPROVS,R2),2(R5)        MOVE FROM ELEM                         
SCPV2    DS    0H                                                               
         L     RF,ADCLT                                                         
         MVC   0(1,R2),PCLTGST-PCLTREC(RF)                                      
         CLI   0(R2),C' '                                                       
         BH    *+8                                                              
         MVI   0(R2),C'S'          GST DEFAULT IS S                             
*                                                                               
SCPV6    DS    0H                                                               
         OC    0(NPROVS+1,R2),0(R2)              ANY VATS AT ALL?               
         BZ    SCPVX                             NO, DONE                       
*                                                                               
         L     RE,APRD              SEE IF I HAVE A PRODUCT                     
         BZ    SCPVX                                                            
         L     RF,ADCLT                                                         
*                                                                               
         MVC   SPCWORK(7),0(RF)                                                 
         MVC   SPCWORK+7(3),0(RE)   3 CHAR PRD CODE                             
         MVI   SPCWORK+3,X'06'                                                  
         L     RF,ADPRD                                                         
         CLC   SPCWORK(10),0(RF)    TEST ALREADY HAVE PRDHDR                    
         BE    SCPV8                                                            
*                                                                               
         MVC   SCPSVKEY,KEY        SAVE KEY                                     
         XC    KEY,KEY                                                          
         MVC   KEY(10),SPCWORK                                                  
         GOTO1 READ                                                             
         L     RF,ADPRD                                                         
         ST    RF,AREC                                                          
         GOTO1 GETPRT                                                           
         MVC   KEY,SCPSVKEY        RESTORE KEY                                  
         GOTO1 HIGH                AND SEQ                                      
*                                                                               
SCPV8    DS    0H                                                               
         XC    SPCWORK(11),SPCWORK                                              
         L     RF,ADPRD                                                         
         MVC   SPCWORK(1),PPRDGST-PPRDREC(RF)                                   
         LR    R5,RF                                                            
         LA    R5,33(R5)                                                        
         MVI   ELCODE,X'25'                                                     
         BAS   RE,SNEXTEL                                                       
         BNE   SCPV8C                                                           
         MVC   SPCWORK+1(NPROVS),2(R5)                                          
*                                                                               
SCPV8C   LA    R4,SPCWORK                                                       
         LA    R5,NPROVS+1                                                      
*                                                                               
SCPV9    DS    0H                                                               
         CLI   0(R4),C'0'          ZERO                                         
         BNE   *+8                                                              
         MVI   0(R4),0             BECOMES NULL                                 
         CLI   0(R4),C' '               SET FROM PRDHDR                         
         BNH   SCPV12                   IF PRESENT                              
         CLC   0(1,R2),0(R4)            IF PRODUCT NOT = CLIENT                 
         BE    *+14                                                             
         MVI   0(R3),C'M'          SET HAVE MIXED PRD VAT CODES                 
         MVC   0(1,R2),0(R4)       SET VALUE FROM PRODUCT                       
*                                                                               
SCPV12   DS    0H                                                               
         LA    R2,1(R2)            NEXT PROVINCE                                
         LA    R3,1(R3)                                                         
         LA    R4,1(R4)                                                         
         BCT   R5,SCPV9                                                         
         B     SCPVX                                                            
*                                                                               
SCPVX    DS    0H                                                               
         XIT1                                                                   
*                                                                               
*                                                                               
SNEXTEL  DS    0H                                                               
         CLI   0(R5),0         SEE IF AT END OF RECORD                          
         BE    SNXTELX                                                          
         ZIC   R0,1(R5)                                                         
         AR    R5,R0                                                            
         CLC   0(1,R5),ELCODE                                                   
         BER   RE                                                               
         B     SNEXTEL                                                          
*                                                                               
SNXTELX  LTR   RE,RE              RETURN WITH CC NOT EQUAL                      
         BR    RE                                                               
*                                                                               
         LTORG                                                                  
*                                                                               
SPCWRKD  DSECT                                                                  
SPCWORK  DS    XL64                                                             
SCPSVKEY DS    XL32                                                             
SPCWRKL  EQU   *-SPCWRKD                                                        
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        VBADD    ADD TO VAT BASIS TABLE                                        
*                                                                               
*        PARM1  BYTE  0   C=CLEAR                                               
*               BYTES 1-3 A(RDBUFF BUFFALO RECORD)                              
*        PARM2  BYTE  0   NOT USED                                              
*               BYTES 1-3 A(BRKTAB ENTRY)                                       
*                                                                               
*        NOTE-   PST LOGIC (SUCH AS IT IS) IS BUILT AROUND THERE                
*              BEING ONLY ONE EFFECTIVE VAT CODE FOR EACH PROVINCE              
*              (INCLUDING NATIONAL). THIS CODE IS S. OTHER CODES                
*              SHOULD HAVE 0%.                                                  
*                IF THIS IS A PROBLEM IT CAN BE AT LEAST PARTIALLY              
*              ADDRESSED BY RESTRUCTURING THE VBATAB TO INCLUDE                 
*              THE VAT CODE.                                                    
*                THE VBATAB IS NOW INDEXED BY PROVINCE NUMBER (0=GST).          
*              NOTE THAT CVATCDS IS SET FROM CLT/PRD AND NOT AFFECTED           
*              BY PUBL VAT CODES, WHICH ARE USED JUST TO BUILD THE              
*              VBTAB.                                                           
*                                                                               
***********************************************************************         
         SPACE 2                                                                
         PRINT NOGEN                                                            
VBADD    CSECT                                                                  
         NMOD1 VBAWRKL,VBADD                                                    
         USING PPWORKD,RA                                                       
         USING PPWORK2D,R9                                                      
         LR    R8,RC                                                            
         USING VBAWRKD,R8                                                       
*                                                                               
         USING BILWRKD,RC                                                       
         LA    RC,SPACEND                                                       
*                                                                               
         L     R3,4(R1)                                                         
         ST    R3,VBABRK           SAVE A(BREAK TABLE ENTRY)                    
         MVC   VBABRKC,BRKCODE-BRKTABD(R3)                                      
         ST    R1,VBASVR1                                                       
         SPACE 2                                                                
         CLI   0(R1),C'C'          CLEAR COMMAND                                
         BNE   VB2                                                              
*                                                                               
         L     R0,=V(VBTAB)                                                     
         L     R1,=A(VTOTSIZE*MAXLEVS)                                          
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
         B     VBADDX                                                           
*                                                                               
VB2      DS    0H                                                               
         SR    R7,R7                                                            
         ICM   R7,7,1(R1)          A(RDBUFF BUFFALO RECORD)                     
         L     RF,ASORTDTA                                                      
         LA    R0,SORTREC                                                       
         SR    RF,R0                                                            
         AR    R7,RF               R7 NOW POINTS TO DATA AREA                   
*                                                                               
         L     RE,APRD             PRODUCT CODE                                 
         CLC   LASTPRD,0(RE)       SAME AS LAST?                                
         BE    VB2C                YES, SKIP GETTING VAT CODES FOR PROD         
         MVC   LASTPRD,0(RE)                                                    
*                                  GET PROPER PRODUCT CODE                      
         GOTO1 =V(SETCPVC),VBADMCB                                              
*                                                                               
VB2C     DS    0H                                                               
         CLI   PROFB1A+0,C'Y'      TEST TO DO PSTS FOR ALL PUBS                 
         BNE   VB2C4                                                            
VB2CXX   MVI   SVPPSTS,C'S'        YES, ACTIVATE ALL PSTS                       
         MVC   SVPPSTS+1(L'SVPPSTS-1),SVPPSTS                                   
         B     VB2F                AND SKIP PUB READ                            
*                                                                               
VB2C4    DS    0H                                                               
         MVI   SVPPSTS,C'S'        YES, ACTIVATE ALL PSTS                       
         MVC   SVPPSTS+1(L'SVPPSTS-1),SVPPSTS                                   
         L     R4,APUB              CHK FOR PUB                                 
         LTR   R4,R4                                                            
         BZ    VB2F                                                             
*                                                                               
         CLC   XXPUB,0(R4)          MANUAL BILLS                                
         BE    VB2F                                                             
         CLC   0(6,R4),=X'FFFFFFFFFFFF'    PREVIOUSLY BILLED INS                
         BE    VB2F                 LUMPED TOGETHER                             
*                                                                               
         MVC   VBAWRK2(6),0(R4)                                                 
*                                                                               
         XC    SVPPSTS,SVPPSTS      CLEAR                                       
*                                                                               
         MVC   VBASAVK,KEY                                                      
         XC    KEY,KEY                                                          
         MVC   KEY(1),QMEDIA                                                    
         OC    AMED,AMED         SEE IF MULTI-MEDIA REQUEST                     
         BZ    VB2C4C                                                           
         L     RF,AMED           USE MEDIA FROM AMED                            
         MVC   KEY(1),0(RF)                                                     
*                                                                               
VB2C4C   DS    0H                                                               
         MVC   KEY+1(6),VBAWRK2         PUB                                     
         MVC   KEY+7(2),QAGENCY                                                 
         MVI   KEY+9,X'81'                                                      
         GOTO1 HIGHPUB                                                          
         CLC   KEY(25),KEYSAVE                                                  
         BE    VB2C4E                                                           
         L     RF,ADAGY                                                         
         CLI   PAGYPROF+16-PAGYREC(RF),C'0'    SEE IF USING DEFAULT             
         BNE   *+6                                                              
         DC    H'0'                 PUB NOT FOUND                               
         MVC   KEYSAVE+7(2),=C'ZZ'                                              
         CLC   KEYSAVE(25),KEY                                                  
         BE    VB2C4E                                                           
         MVC   KEY,KEYSAVE                                                      
         GOTO1 HIGHPUB                                                          
         CLC   KEY(25),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                 PUB NOT FOUND                               
*                                                                               
VB2C4E   DS    0H                                                               
         L     R4,ADPUB                                                         
         CLC   KEY(25),0(R4)       SEE IF ALREADY THERE                         
         BE    VB2C4F                                                           
         GOTO1 GETNAME                                                          
VB2C4F   DS    0H                                                               
*                                                                               
VB2D     DS    0H                                                               
         MVC   KEY,VBASAVK                                                      
         L     R5,ADPUB                                                         
         LA    R5,33(R5)                                                        
         MVI   ELCODE,X'90'                                                     
         BAS   RE,VNEXTEL                                                       
         BNE   VB2F                NOT FOUND                                    
         MVC   SVPPSTS,2(R5)       SAVE PUB PST CODES                           
*                                                                               
VB2F     DS    0H                                                               
         MVC   VBASVCDS,CVATCDS    SAVE VAT CODES                               
         LA    R3,SVPPSTS          PUB PST CODES                                
         LA    RE,VBASVCDS+1                                                    
         LA    R0,NPROVS                                                        
*                                                                               
VB3      DS    0H                  R3 POINTS TO PUB VAT LIST                    
         CLI   0(R3),C'0'          ZERO                                         
         BNE   *+8                                                              
         MVI   0(R3),0             BECOMES NULL                                 
         CLI   0(R3),C' '          IF NO PUB PST CODE                           
         BH    *+8                                                              
         MVI   0(RE),0             THEN NO PST - (BUT IF THERE IS PST           
*                                  THE CLT/PRD CODE IS PRESERVED ??'            
*                               ** NOTE THAT CODES THEMSELVES ARE               
*                                  NOT SET FROM PUB!                            
*                                                                               
VB4      DS    0H                                                               
         LA    RE,1(RE)                                                         
         LA    R3,1(R3)                                                         
         BCT   R0,VB3                                                           
*                                                                               
         LA    R2,VBASVCDS                                                      
         LA    R0,NPROVS+1                                                      
*                                                                               
VB6      DS    0H                                                               
         CLI   0(R2),C' '                                                       
         BNH   VB9                                                              
*                                                                               
         L     RF,APER             PERIOD (MOS)                                 
         ZIC   R5,0(RF)                                                         
         SLL   R5,3                X 8                                          
         LA    R5,PERLIST(R5)                                                   
         L     R4,=V(VCTAB)        VAT CODE TABLE                               
         USING VCTABD,R4                                                        
         LA    RE,NPROVS+1                                                      
         SR    RE,R0               PROVINCE NUMBER                              
*                                                                               
VB7      DS    0H                                                               
         CLM   RE,1,VCTPRV         PROVINCE                                     
         BNE   VB8                                                              
         CLC   VCTVCD,0(R2)        CODE                                         
         BNE   VB8                                                              
         CLC   0(2,R5),VCTSTRT     START MOS                                    
         BL    VB8                                                              
         CLC   0(2,R5),VCTEND      END MOS                                      
         BNH   VB8B                                                             
*                                                                               
VB8      DS    0H                                                               
         LA    R4,VCTABL(R4)       NEXT ENTRY                                   
         CLI   0(R4),X'FF'         END OF TABLE                                 
         BNE   VB7                                                              
         B     VB9                                                              
*                                                                               
VB8B     DS    0H                                                               
*                                 SAVE ENTRY IN MY WORK                         
         MVC   VBAPCTW(VCTABL),0(R4)                                            
*                                                                               
         CLI   0(R4),06            SEE IF QUEBEC                                
         BNE   VB8B5                                                            
         CLI   MANRVDTP,0              SEE IF REVERSAL                          
         BZ    VB8B5                                                            
         CLC   MANRVDTP,=X'5E0601'     SEE IF ORIG. BILL WAS BEFORE             
         BNL   VB8B5                   JUN01/94                                 
         OC    VBAPCTW+VCTABL-4(4),VBAPCTW+VCTABL-4                             
         BZ    VB8B5                                                            
         MVC   VBAPCTW+VCTABL-4(4),=AL4(4000)                                   
*                                                                               
VB8B5    DS    0H                                                               
         DROP  R4                                                               
         LA    R4,VBAPCTW        POINT R4 TO MY ENTRY                           
         USING VCTABD,R4                                                        
*                                                                               
         L     RE,APER             PERIOD NUMBER                                
         ZIC   RF,0(RE)                                                         
         SR    RE,RE                                                            
         MH    RF,=Y(NPROVS+1)                                                  
*                                                                               
         LA    RE,NPROVS+1                                                      
         SR    RE,R0                                                            
         AR    RF,RE                                                            
         MH    RF,=Y(VBTABL)                                                    
*                                                                               
         L     RE,VBABRKC          POINT TO TOTALS FOR THIS LEVEL               
         L     RE,ATOTSV(RE)                                                    
         AR    RF,RE               PLUS DISP INTO TOTALS                        
         USING VBTABD,RF            VBTAB ENTRY                                 
         MVC   VBTPCT,VCTPCT       SET PERCENT                                  
         MVC   VBTCOD,VCTVCD       AND CODE                                     
*                                                                               
         CLI   UPASS,C'S'          FOR RETAIL SUMMARY PASS                      
         BE    VB8E                AMOUNTS ARE DIFFERENT                        
*                                                                               
         LA    RE,VBTBLBN          FOR BCT                                      
         LR    R6,R7               A(ORD/BILLED $)                              
*                                                                               
         CLI   FINANSW,X'01'      SEE IF FINANCIAL                              
         BNE   *+8                                                              
         LA    R6,ROWTL(R6)       POINT TO OPEN ACCUMS                          
*                                                                               
VB8D     DS    0H                                                               
         L     R1,0(RF)                                                         
         A     R1,0(R6)            +ORDERED                                     
         S     R1,ROWHL(R6)        -BILLED                                      
         ST    R1,0(RF)                                                         
         LA    R6,4(R6)                                                         
         LA    RF,4(RF)                                                         
         BCT   RE,VB8D                                                          
         B     VB8F                                                             
*                                                                               
VB8E     DS    0H                  RETAIL SUMMARY PASS                          
*              **RETAIL SUMMARIES NOT HANDLED PROPERLY                          
*              **BUT NO ONE IN CANADA USES THEM                                 
         L     R1,0(RF)            GROSS                                        
         A     R1,4(R7)                                                         
         ST    R1,0(RF)                                                         
         L     R1,8(RF)            NET                                          
         A     R1,8(R7)                                                         
         ST    R1,8(RF)                                                         
*                                                                               
VB8F     DS    0H                  NOW DO FOR 'TOTAL' MONTH                     
         LA    RF,MAXMOS                                                        
         MH    RF,=Y(NPROVS+1)                                                  
*                                                                               
         LA    RE,NPROVS+1                                                      
         SR    RE,R0                                                            
         AR    RF,RE                                                            
         MH    RF,=Y(VBTABL)                                                    
*                                                                               
         L     RE,VBABRKC          POINT TO TOTALS FOR THIS LEVEL               
         L     RE,ATOTSV(RE)                                                    
         AR    RF,RE               PLUS DISP INTO TOTALS                        
         USING VBTABD,RF            VBTAB ENTRY                                 
         MVC   VBTPCT,VCTPCT       SET PERCENT                                  
         MVC   VBTCOD,VCTVCD       AND CODE                                     
*                                                                               
         CLI   UPASS,C'S'          FOR RETAIL SUMMARY PASS                      
         BE    VB8I                AMOUNTS ARE DIFFERENT                        
*                                                                               
         DROP  R4                                                               
         LA    RE,VBTBLBN          FOR BCT                                      
         LR    R6,R7               A(ORD/BILLED $)                              
*                                                                               
         CLI   FINANSW,X'01'      SEE IF FINANCIAL                              
         BNE   *+8                                                              
         LA    R6,ROWTL(R6)       POINT TO OPEN ACCUMS                          
*                                                                               
VB8H     DS    0H                                                               
         L     R1,0(RF)                                                         
         A     R1,0(R6)            +ORDERED                                     
         S     R1,ROWHL(R6)        -BILLED                                      
         ST    R1,0(RF)                                                         
         LA    R6,4(R6)                                                         
         LA    RF,4(RF)                                                         
         BCT   RE,VB8H                                                          
         B     VB9                                                              
*                                                                               
VB8I     DS    0H                  RETAIL SUMMARY PASS                          
*              **CODE WILL SURELY HAVE TO BE CHANGED                            
*              **TO BETTER HANDLE RETAIL SUMMARIES PST                          
         L     R1,0(RF)            GROSS                                        
         A     R1,4(R7)                                                         
         ST    R1,0(RF)                                                         
         L     R1,8(RF)            NET                                          
         A     R1,8(R7)                                                         
         ST    R1,8(RF)                                                         
*                                                                               
VB9      DS    0H                                                               
         LA    R2,1(R2)                                                         
         BCT   R0,VB6                                                           
*                                                                               
         MVC   VBAMODE,=C'A '       VBADD MODE                                  
         GOTO1 =A(VBATRC),VBADMCB,(R8) TRACE                                    
         DROP  RF                                                               
*                                                                               
VBADDX   DS    0H                                                               
         XIT1                                                                   
*                                                                               
VNEXTEL  DS    0H                                                               
         CLI   0(R5),0         SEE IF AT END OF RECORD                          
         BE    VNXTELX                                                          
         ZIC   R0,1(R5)                                                         
         AR    R5,R0                                                            
         CLC   0(1,R5),ELCODE                                                   
         BER   RE                                                               
         B     VNEXTEL                                                          
*                                                                               
VNXTELX  LTR   RE,RE              RETURN WITH CC NOT EQUAL                      
         BR    RE                                                               
*                                                                               
         LTORG                                                                  
*                                                                               
VBAPCTW  DS    CL20               WORK AREA VCTAB ENTRY                         
*                                                                               
VBAWRKD  DSECT                                                                  
VBADMCB  DS    6F                                                               
VBAWORK  DS    XL64                                                             
VBAWRK2  DS    XL64                                                             
VBASAVK  DS    XL64                                                             
VBAPARMS DS    XL24                                                             
VBASVR1  DS    F                                                                
VBABRKC  DS    F                                                                
VBABRK   DS    F                                                                
VBASVGST DS    F                                                                
VBASVGSP DS    F                                                                
VBALINE  DS    A                                                                
VBADUB   DS    D                                                                
VBAFULL  DS    F                                                                
VBAHALF  DS    H                                                                
VBABYTE  DS    XL1                                                              
VBBBYTE  DS    XL1                                                              
VBASVCDS DS    XL11                                                             
VBAPASS  DS    XL1                                                              
VBAMODE  DS    CL2                                                              
VBPRMODE DS    C                                                                
VBAWRKL  EQU   *-VBAWRKD                                                        
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        VBATRC   TRACE VBTAB                                                   
*                                                                               
*                                                                               
***********************************************************************         
         SPACE 2                                                                
         PRINT NOGEN                                                            
VBATRC   CSECT                                                                  
         NMOD1 VBAWRKL,VBATRC                                                   
         USING PPWORKD,RA                                                       
         USING PPWORK2D,R9                                                      
         USING BILWRKD,RC                                                       
         LA    RC,SPACEND                                                       
*                                                                               
         L     R8,0(R1)            VBATRC CALLERS VBAWRKD                       
         USING VBAWRKD,R8                                                       
         ST    R1,VBASVR1                                                       
         SPACE 2                                                                
         L     RF,APATCH           TRACE?                                       
         TM    0(RF),X'40'                                                      
         BZ    VBATRCX                                                          
*                                                                               
         MVC   P1,SPACES                                                        
         GOTO1 APRNT                                                            
*                                                                               
         CLC   VBAMODE,=C'A '        VBADD MODE                                 
         BNE   VBATR3                                                           
         MVC   P1(2),=C'VB'                                                     
         MVC   P1+3(5),VBAWRK2     PUB                                          
         GOTO1 HEXOUT,VBADMCB,(R5),P1+9,2,=C'N'                                 
         GOTO1 (RF),(R1),CVATCDS,P1+14,11,=C'N'                                 
         GOTO1 (RF),(R1),SVPPSTS,P1+38,10,=C'N'                                 
         GOTO1 APRNT                                                            
*                                                                               
VBATR3   DS    0H                                                               
         LA    R2,P1                                                            
         L     R4,VBABRKC                                                       
         L     R4,ATOTSV(R4)                                                    
         SR    R5,R5                                                            
         LA    R3,(NPROVS+1)*(MAXMOS+1)                                         
         MVC   0(2,R2),=C'VB'                                                   
         MVC   2(2,R2),VBAMODE                                                  
         CLC   VBAMODE,=C'RU'        FOR ROLLUP SKIP IF                         
         BNE   VBATR4                                                           
         OC    VBABRKC,VBABRKC     AT FINAL ONE BECAUSE ITS MEANINGLESS         
         BNZ   VBATR4                                                           
         MVC   11(2,R2),=C'00'                                                  
         GOTO1 APRNT                                                            
         B     VBATR9                                                           
*                                                                               
VBATR4   DS    0H                                                               
         OC    0(VBTABL,R4),0(R4)                                               
         BZ    VBATR8                                                           
         LR    RF,R5                                                            
         SR    RE,RE                                                            
         D     RE,=A(NPROVS+1)                                                  
         LA    RF,1(RF)                      RF = PERIOD                        
         EDIT  (RF),(2,5(R2)),ZERO=NOBLANK                                      
         EDIT  (RE),(2,8(R2)),ZERO=NOBLANK   RE=PROVINCE                        
         GOTO1 HEXOUT,VBADMCB,VBABRKC+3,12(R2),1,=C'N' BRK CODE                 
         GOTO1 (RF),(R1),(R4),15(R2),VBTABL                                     
         GOTO1 APRNT                                                            
*                                                                               
VBATR8   DS    0H                                                               
         LA    R5,1(R5)                                                         
         LA    R4,VBTABL(R4)                                                    
         BCT   R3,VBATR4                                                        
*                                                                               
VBATR9   DS    0H                                                               
*                                                                               
VBATRCX  DS    0H                                                               
         XIT1                                                                   
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        VBADJ    SET COMMISSION ADJUSTMENTS AND                                
*                 AMOUNT DUE IN VBTAB                                           
*                                                                               
*        PARM1  BYTE  0   NOT USED                                              
*               BYTES 1-3 A(BILL FORMULA) 5 BYTES                               
*        PARM2  BYTE  0   NOT USED                                              
*               BYTES 1-3 A(PERLIST ENTRY)                                      
*        PARM3  BYTE  0   NOT USED                                              
*               BYTES 1-3 A(BRKTAB ENTRY)                                       
*                                                                               
***********************************************************************         
         SPACE 2                                                                
         PRINT NOGEN                                                            
VBADJ    CSECT                                                                  
         NMOD1 VBAWRKL,VBADJ                                                    
         USING PPWORKD,RA                                                       
         USING PPWORK2D,R9                                                      
         LR    R8,RC                                                            
         USING VBAWRKD,R8                                                       
         USING BILWRKD,RC                                                       
         LA    RC,SPACEND                                                       
*                                                                               
         L     R3,8(R1)                                                         
         ST    R3,VBABRK           SAVE A(BREAK TABLE ENTRY)                    
         MVC   VBABRKC,BRKCODE-BRKTABD(R3)                                      
         ST    R1,VBASVR1          SAVE R1                                      
         SPACE 2                                                                
*                                                                               
         L     RF,0(R1)            A(BILL FORMULA)                              
         MVC   VBABYTE,0(RF)         BASIS                                      
         MVC   VBBBYTE,1(RF)         ADJ BASIS                                  
         XC    VBAFULL,VBAFULL                                                  
         MVC   VBAFULL+1(3),2(RF)      ADJUSTMENT PERCENT                       
         TM    2(RF),X'F0'             SEE IF NEGATIVE                          
         BNO   *+8                                                              
         MVI   VBAFULL,X'FF'                                                    
*                                                                               
         L     RF,=F'1000000'                                                   
*                                                                               
VBJ3     DS    0H                                                               
         L     R1,VBASVR1                                                       
         L     R1,4(R1)            A(PERLIST ENTRY)                             
         LA    R0,PERLIST                                                       
         SR    R1,R0                                                            
         SR    R0,R0                                                            
         D     R0,=F'8'            8 BYTES PER PERLIST ENTRY                    
*                                                                               
VBJ3B    DS    0H                                                               
         MH    R1,=Y((NPROVS+1)*VBTABL)     X NO. OF PROVINCES AND NAT          
         L     R2,VBABRKC                                                       
         L     R2,ATOTSV(R2)                                                    
         AR    R2,R1               R2 POINTS TO MONTH                           
         USING VBTABD,R2                                                        
*                                                                               
         LA    R1,MAXMOS                                                        
         MH    R1,=Y((NPROVS+1)*VBTABL)     X NO. OF PROVINCES AND NAT          
         L     R6,VBABRKC                                                       
         L     R6,ATOTSV(R6)                                                    
         AR    R6,R1               R6 POINTS TO TOTAL MONTH ROW                 
*                                                                               
         LA    R3,NPROVS+1                                                      
*                                                                               
VBJ4     DS    0H                                                               
*                                                                               
         CLI   CDSEP,C'Y'          SEE IF CD ON SEPERATE BILL                   
         BNE   VBJ4C                                                            
         NI    VBABYTE,X'FB'      SET OFF CD IN FORMULA                         
         B     VBJ4X                                                            
*                                                                               
VBJ4C    CLI   SCBNCSW,C'C'        SEE IF UFC COMM BILL                         
         BNE   VBJ4X                                                            
         CLI   PROFB2B+4,C'Y'      SEE IF CD IS ON NET BILL                     
         BNE   VBJ4X                                                            
         NI    VBABYTE,X'FB'       SET CD OFF                                   
*                                                                               
VBJ4X    DS    0H                                                               
         L     R4,VBTGRS           BASIS IS GROSS                               
         TM    VBABYTE,X'01'                                                    
         BO    VBJ4X5                                                           
         L     R4,VBTNET           OR NET                                       
         TM    VBABYTE,X'02'                                                    
         BO    VBJ4X5                                                           
         L     R4,VBTAC            MUST BE AC                                   
*                                                                               
VBJ4X5   DS    0H                                                               
         TM    VBABYTE,X'04'       SEE IF SUBTRACTING CD                        
         BZ    VBJ6                                                             
         S     R4,VBTCD                                                         
*                                                                               
VBJ6     L     R1,VBTGRS           ADJUSTMENT BASIS IS GROSS                    
         TM    VBBBYTE,X'01'                                                    
         BO    VBJ6C                                                            
         L     R1,VBTNET           OR NET                                       
         TM    VBBBYTE,X'02'                                                    
         BO    VBJ6C                                                            
         L     R1,VBTAC            MUST BE CD                                   
*                                                                               
VBJ6C    DS    0H                                                               
         TM    VBBBYTE,X'04'       SEE IF SUBTRACTING CD FROM BASIS             
         BZ    *+8                                                              
         S     R1,VBTCD                                                         
*                                                                               
         CLI   TAXOPT,C'N'         OPTION TO REMOVE TAX                         
         BE    VBJ6F                                                            
         TM    VBBBYTE,X'08'      SEE IF AC                                     
         BO    VBJ6F              YES - THEN DON'T SUBTRACT TAX                 
         S     R1,VBTTAX                                                        
*                                                                               
VBJ6F    DS    0H                                                               
         M     R0,VBAFULL                                                       
         BAS   RE,VBJDIV                                                        
         ST    R1,VBTADJ           CALCULATED ADJUSTMENT AMOUNT                 
         AR    R4,R1               BASIS PLUS ADJ = DUE                         
         ST    R4,VBTDUE                                                        
*                                                                               
         A     R1,VBTADJ-VBTABD(R6)  ADD TO TOTAL ROW                           
         ST    R1,VBTADJ-VBTABD(R6)                                             
         A     R4,VBTDUE-VBTABD(R6)                                             
         ST    R4,VBTDUE-VBTABD(R6)                                             
*                                                                               
VBJ8     DS    0H                                                               
         LA    R2,VBTABL(R2)       NEXT PROVINCE                                
         LA    R6,VBTABL(R6)                                                    
         BCT   R3,VBJ4                                                          
*                                                                               
VBJ10    DS    0H                                                               
         MVC   VBAMODE,=C'J '        VBADJ MODE                                 
         GOTO1 =A(VBATRC),VBADMCB,(R8)                                          
*                                                                               
VBJX     DS    0H                                                               
         XIT1                                                                   
*                                                                               
VBJDIV   DRNDR (R0),(RF)                                                        
         BR    RE                                                               
         DROP  R2                                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        VBCALC   CALC VAT AMOUNTS AND ADD INTO WKROW                           
*                                                                               
*        PARM1  BYTE  0   NOT USED                                              
*               BYTES 1-3 A(PERLIST ENTRY)                                      
*        PARM2  BYTE  0   NOT USED                                              
*               BYTES 1-3 A(MONTH CUME SLOT - ATOTS)                            
*        PARM3  BYTE  0   NOT USED                                              
*               BYTES 1-3 A(BRKTAB ENTRY)                                       
*                                                                               
*        NOTE - VAT IS CALCULATED FOR THE GIVEN MONTH AND                       
*               TOTALED IN WKROW TO STAY COMPATIBLE WITH OLD BILCALC.           
*               IT IS ALSO SET AT VATDSP IN THE REGULAR ACCUME (AT R7).         
*                                                                               
***********************************************************************         
         SPACE 2                                                                
         PRINT NOGEN                                                            
VBCALC   CSECT                                                                  
         NMOD1 VBAWRKL,VBCALC                                                   
         USING PPWORKD,RA                                                       
         USING PPWORK2D,R9                                                      
         LR    R8,RC                                                            
         USING VBAWRKD,R8                                                       
         USING BILWRKD,RC                                                       
         LA    RC,SPACEND                                                       
*                                                                               
         L     R7,4(R1)            A(MONTH CUME CLOT)                           
         L     R3,8(R1)                                                         
         ST    R3,VBABRK           SAVE A(BREAK TABLE ENTRY)                    
         MVC   VBABRKC,BRKCODE-BRKTABD(R3)                                      
         ST    R1,VBASVR1                                                       
*                                                                               
*                                                                               
         XC    VBASVGSP,VBASVGSP                                                
*                                                                               
         L     RF,0(R1)                                                         
         LA    R0,PERLIST                                                       
         SR    RE,RE                                                            
         SR    RF,R0                                                            
         D     RE,=F'8'            PERLIST ENTRY IS 8 LONG                      
         MH    RF,=Y((NPROVS+1)*VBTABL)                                         
         L     R2,VBABRKC                                                       
         L     R2,ATOTSV(R2)                                                    
         AR    R2,RF                                                            
         USING VBTABD,R2                                                        
*                                                                               
         LA    R3,NPROVS+1                                                      
         LA    R5,WKROW                                                         
         L     RF,=F'100000'                                                    
*                                                                               
VBC4     DS    0H                                                               
         CLI   VBTCOD,C' '         SKIP IF NO VAT CODE                          
         BE    VBC8                                                             
         L     R1,VBTDUE           DUE AMOUNT                                   
         CLI   SCBNCSW,C'C'        IF SCB COMMISSIION BILL                      
         BNE   VBC6                                                             
         S     R1,VBTNET           THEN LESS NET                                
         B     VBC6A               (BUT DON'T SUBTRACT TAX FROM DUE)            
*                                                                               
VBC6     DS    0H                                                               
         S     R1,VBTTAX           LESS TAX (SALES)                             
VBC6A    CLI   ADJSEP,C'Y'         IF ADJ ON SEP INV                            
         BNE   *+8                                                              
         S     R1,VBTADJ           THEN LESS ADJ                                
*                                                                               
         CH    R3,=Y(NPROVS+1)     UNLESS DOING GST NOW                         
         BE    VBC6B                                                            
         ICM   R0,15,VBASVGSP      FIRST APPLY GST TO GET BASIS                 
         A     R0,=F'100000'                                                    
         MR    R0,R0                                                            
         BAS   RE,VBCDIV                                                        
*                                                                               
VBC6B    DS    0H                                                               
         ST    R1,VBTVATB          STORE BASIS IN VBTAB                         
         LR    RE,R1                    VAT BASIS                               
         ST    RE,VATBDSP(R7)          SET IN MONTH CUME ROW                    
         A     RE,VATBDSP-EXTDSP(R5)   AND TOTAL IN WKROW                       
         ST    RE,VATBDSP-EXTDSP(R5)                                            
*                                                                               
VBC6D    DS    0H                                                               
         ICM   R0,15,VBTPCT        TIMES VAT PCT (N.NNN)                        
         MR    R0,R0                                                            
         BAS   RE,VBCDIV                                                        
         ST    R1,VBTVAT                                                        
         CH    R3,=Y(NPROVS+1)     IF DOING GST NOW                             
         BNE   *+10                                                             
         MVC   VBASVGSP,VBTPCT     SAVE ACTUAL GST %                            
         ST    R1,VATDSP(R7)       SET IN MONTH CUME ROW                        
         A     R1,VATDSP-EXTDSP(R5)    AND TOTAL IN WKROW                       
         ST    R1,VATDSP-EXTDSP(R5)                                             
*                                                                               
VBC8     DS    0H                                                               
         LA    R2,VBTABL(R2)       NEXT PROVINCE                                
         LA    R5,4(R5)            NEXT WKROW SLOT                              
         LA    R7,4(R7)            NEXT ACCUM SLOT                              
         BCT   R3,VBC4                                                          
*                                                                               
         MVC   VBAMODE,=C'C '        VBCALC MODE                                
         GOTO1 =A(VBATRC),VBADMCB,(R8)                                          
*                                                                               
VBCALCX  DS    0H                                                               
         XIT1                                                                   
*                                                                               
VBCDIV   DRNDR (R0),(RF)                                                        
         BR    RE                                                               
         DROP  R2                                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        VBCALCJ  CALC VAT AMOUNTS FOR SEP COMM BILLS                           
*                 AND ADD INTO WKROW                                            
*                                                                               
*        PARM1  BYTE  0   NOT USED                                              
*               BYTES 1-3 A(PERLIST ENTRY)                                      
*        PARM2  BYTE  0   NOT USED                                              
*               BYTES 1-3 A(MONTH CUME SLOT - ATOTS)                            
*        PARM3  BYTE  0   NOT USED                                              
*               BYTES 1-3 A(BRKTAB ENTRY)                                       
*                                                                               
*        NOTE - VAT IS CALCULATED FOR THE GIVEN MONTH AND                       
*               TOTALED IN WKROW TO STAY COMPATIBLE WITH OLD BILCALC.           
*               IT IS ALSO SET AT VATCDSP IN THE REGULAR CUME (AT R7).          
*             **IT IS NOT STORED IN THE VBTAB                                   
*                                                                               
***********************************************************************         
         SPACE 2                                                                
         PRINT NOGEN                                                            
VBCALCJ CSECT                                                                   
         NMOD1 VBAWRKL,VBCALCJ                                                  
         USING PPWORKD,RA                                                       
         USING PPWORK2D,R9                                                      
         LR    R8,RC                                                            
         USING VBAWRKD,R8                                                       
         USING BILWRKD,RC                                                       
         LA    RC,SPACEND                                                       
*                                                                               
         L     R7,4(R1)            A(MONTH CUME CLOT)                           
         L     R3,8(R1)                                                         
         ST    R3,VBABRK           SAVE A(BREAK TABLE ENTRY)                    
         MVC   VBABRKC,BRKCODE-BRKTABD(R3)                                      
         ST    R1,VBASVR1                                                       
         SPACE 2                                                                
*                                                                               
         L     RF,0(R1)                                                         
         LA    R0,PERLIST                                                       
         SR    RE,RE                                                            
         SR    RF,R0                                                            
         D     RE,=F'8'            PERLIST ENTRY IS 8 LONG                      
         MH    RF,=Y((NPROVS+1)*VBTABL)                                         
         L     R2,VBABRKC                                                       
         L     R2,ATOTSV(R2)                                                    
         AR    R2,RF                                                            
         USING VBTABD,R2                                                        
*                                                                               
         LA    R3,NPROVS+1                                                      
         LA    R5,WKROW                                                         
         L     RF,=F'100000'                                                    
*                                                                               
VBCJ4    DS    0H                                                               
         L     R1,VBTADJ           CALC VAT OF COMM ADJ                         
         CH    R3,=Y(NPROVS+1)     UNLESS DOING GST NOW                         
         BE    VBCJ4B                                                           
         ICM   R0,15,VBASVGSP      APPLY GST TO GET PROV BASIS                  
         A     R0,=F'100000'                                                    
         MR    R0,R0                                                            
         BAS   RE,VBCJDIV                                                       
*                                                                               
VBCJ4B   DS    0H                                                               
         LR    RE,R1                    VAT BASIS                               
         ST    RE,VATCBDSP(R7)          SET IN MONTH CUME ROW                   
         A     RE,VATCBDSP-EXTDSP(R5)   AND TOTAL IN WKROW                      
         ST    RE,VATCBDSP-EXTDSP(R5)                                           
*                                                                               
         ICM   R0,15,VBTPCT                                                     
         MR    R0,R0                                                            
         BAS   RE,VBCJDIV                                                       
*                   **NOTE-SEP COMM VAT NOT IN VBTAB                            
         CH    R3,=Y(NPROVS+1)     IF DOING GST NOW                             
         BNE   *+10                                                             
         MVC   VBASVGSP,VBTPCT     SAVE GST AMOUNT                              
         ST    R1,VATCDSP(R7)      SET IN MONTH CUME ROW                        
         A     R1,VATCDSP-EXTDSP(R5)   AND TOTAL IN WKROW                       
         ST    R1,VATCDSP-EXTDSP(R5)                                            
*                                                                               
VBCJ8    DS    0H                                                               
         LA    R2,VBTABL(R2)       NEXT PROVINCE                                
         LA    R5,4(R5)            NEXT WKROW SLOT                              
         LA    R7,4(R7)            NEXT ACCUM SLOT                              
         BCT   R3,VBCJ4                                                         
*                                                                               
         MVC   VBAMODE,=C'CJ'        VBCALC MODE - SEP ADJ                      
         GOTO1 =A(VBATRC),VBADMCB,(R8)                                          
*                                                                               
VBCJALCX DS    0H                                                               
         XIT1                                                                   
*                                                                               
VBCJDIV  DRNDR (R0),(RF)                                                        
         BR    RE                                                               
         DROP  R2                                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        VBCALCD  CALC VAT AMOUNTS FOR SEP CD BILLS                             
*                 AND ADD INTO WKROW                                            
*                                                                               
*        PARM1  BYTE  0   NOT USED                                              
*               BYTES 1-3 A(PERLIST ENTRY)                                      
*        PARM2  BYTE  0   NOT USED                                              
*               BYTES 1-3 A(MONTH CUME SLOT - ATOTS)                            
*        PARM3  BYTE  0   NOT USED                                              
*               BYTES 1-3 A(BRKTAB ENTRY)                                       
*                                                                               
*        NOTE - VAT IS CALCULATED FOR THE GIVEN MONTH AND                       
*               TOTALED IN WKROW TO STAY COMPATIBLE WITH OLD BILCALC.           
*               IT IS ALSO SET AT VATDDSP IN THE REGULAR CUME (AT R7).          
*             **IT IS NOT STORED IN THE VBTAB                                   
*                                                                               
***********************************************************************         
         SPACE 2                                                                
         PRINT NOGEN                                                            
VBCALCD CSECT                                                                   
         NMOD1 VBAWRKL,VBCALCD                                                  
         USING PPWORKD,RA                                                       
         USING PPWORK2D,R9                                                      
         LR    R8,RC                                                            
         USING VBAWRKD,R8                                                       
         USING BILWRKD,RC                                                       
         LA    RC,SPACEND                                                       
*                                                                               
         L     R7,4(R1)            A(MONTH CUME CLOT)                           
         L     R3,8(R1)                                                         
         ST    R3,VBABRK           SAVE A(BREAK TABLE ENTRY)                    
         MVC   VBABRKC,BRKCODE-BRKTABD(R3)                                      
         ST    R1,VBASVR1                                                       
         SPACE 2                                                                
*                                                                               
         L     RF,0(R1)                                                         
         LA    R0,PERLIST                                                       
         SR    RE,RE                                                            
         SR    RF,R0                                                            
         D     RE,=F'8'            PERLIST ENTRY IS 8 LONG                      
         MH    RF,=Y((NPROVS+1)*VBTABL)                                         
         L     R2,VBABRKC                                                       
         L     R2,ATOTSV(R2)                                                    
         AR    R2,RF                                                            
         USING VBTABD,R2                                                        
*                                                                               
         LA    R3,NPROVS+1                                                      
         LA    R5,WKROW                                                         
         L     RF,=F'100000'                                                    
*                                                                               
VBDJ4    DS    0H                                                               
         L     R1,VBTCD            CALC VAT OF CD                               
         CH    R3,=Y(NPROVS+1)     UNLESS DOING GST NOW                         
         BE    VBDJ4B                                                           
         ICM   R0,15,VBASVGSP      APPLY GST TO GET PROV BASIS                  
         A     R0,=F'100000'                                                    
         MR    R0,R0                                                            
         BAS   RE,VBDJDIV                                                       
*                                                                               
VBDJ4B   DS    0H                                                               
         LR    RE,R1                    VAT BASIS                               
         ST    RE,VATDBDSP(R7)          SET IN MONTH CUME ROW                   
         A     RE,VATDBDSP-EXTDSP(R5)   AND TOTAL IN WKROW                      
         ST    RE,VATDBDSP-EXTDSP(R5)                                           
*                                                                               
         ICM   R0,15,VBTPCT                                                     
         MR    R0,R0                                                            
         BAS   RE,VBDJDIV                                                       
*                   **NOTE-SEP CD VAT NOT IN VBTAB                              
         CH    R3,=Y(NPROVS+1)     IF DOING GST NOW                             
         BNE   *+10                                                             
         MVC   VBASVGSP,VBTPCT     SAVE GST AMOUNT                              
         ST    R1,VATDDSP(R7)      SET IN MONTH CUME ROW                        
         A     R1,VATDDSP-EXTDSP(R5)   AND TOTAL IN WKROW                       
         ST    R1,VATDDSP-EXTDSP(R5)                                            
*                                                                               
VBDJ8    DS    0H                                                               
         LA    R2,VBTABL(R2)       NEXT PROVINCE                                
         LA    R5,4(R5)            NEXT WKROW SLOT                              
         LA    R7,4(R7)            NEXT ACCUM SLOT                              
         BCT   R3,VBDJ4                                                         
*                                                                               
         MVC   VBAMODE,=C'CD'        VBCALC MODE - SEP CD                       
         GOTO1 =A(VBATRC),VBADMCB,(R8)                                          
*                                                                               
VBDJALCX DS    0H                                                               
         XIT1                                                                   
*                                                                               
VBDJDIV  DRNDR (R0),(RF)                                                        
         BR    RE                                                               
         DROP  R2                                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        VBCALCA  CALC VAT AMOUNTS FOR AOR BILLS                                
*                 AND ADD INTO WKROW                                            
*                                                                               
*        PARM1  BYTE  0   NOT USED                                              
*               BYTES 1-3 A(PERLIST ENTRY)                                      
*        PARM2  BYTE  0   NOT USED                                              
*               BYTES 1-3 A(MONTH CUME SLOT - ATOTS)                            
*        PARM3  BYTE  0   NOT USED                                              
*               BYTES 1-3 A(BRKTAB ENTRY)                                       
*                                                                               
*        NOTE - VAT IS CALCULATED FOR THE GIVEN MONTH AND                       
*               TOTALED IN WKROW TO STAY COMPATIBLE WITH OLD BILCALC.           
*               IT IS ALSO SET AT VATADSP IN THE REGULAR CUME (AT R7).          
*             **IT IS NOT STORED IN THE VBTAB                                   
*                                                                               
***********************************************************************         
         SPACE 2                                                                
         PRINT NOGEN                                                            
VBCALCA CSECT                                                                   
         NMOD1 VBAWRKL,VBCALCA                                                  
         USING PPWORKD,RA                                                       
         USING PPWORK2D,R9                                                      
         LR    R8,RC                                                            
         USING VBAWRKD,R8                                                       
         USING BILWRKD,RC                                                       
         LA    RC,SPACEND                                                       
*                                                                               
         L     R7,4(R1)            A(MONTH CUME CLOT)                           
         L     R3,8(R1)                                                         
         ST    R3,VBABRK           SAVE A(BREAK TABLE ENTRY)                    
         MVC   VBABRKC,BRKCODE-BRKTABD(R3)                                      
         ST    R1,VBASVR1                                                       
*                                                                               
         XC    VBASVGST,VBASVGST                                                
*                                                                               
         SPACE 2                                                                
*                                                                               
         L     RF,0(R1)            A(PERLIST ENTRY)                             
         MVC   HALF,0(RF)          SAVE MOS                                     
*                                                                               
         SR    R3,R3                                                            
         LA    R5,WKROW                                                         
         LA    R4,SVAORVCS         AOR VAT CODES                                
         MVC   VBAFULL,AORDSP(R7)  ALL PROVS HAVE SAME AOR VAT BASIS            
*                                                                               
VBCA4    DS    0H                                                               
         CLI   0(R4),C'S'          % ONLY FOR CODE S?                           
         BNE   VBCA8                                                            
         GOTO1 =V(VBGETPCT),VBADMCB,((R3),HALF),(R4) GET VCTAB ENTRY            
         SR    R0,R0               VAT %                                        
         ICM   RF,15,4(R1)          A(VCTAB ENTRY)                              
         BNZ   VBCA5                                                            
         B     VBCA8               IF NOT FOUND SKIP TO NEXT PROV               
*                                                                               
VBCA5    L     R0,VCTPCT-VCTABD(RF)   VAT %                                     
VBCA6    L     R1,VBAFULL          ALL PROVS HAVE SAME AOR VAT BASIS            
*                                                                               
         LTR   R3,R3               IF NOT DOING GST NOW                         
         BZ    *+8                                                              
         A     R1,VBASVGST         ADD GST TO GET PROV BASIS                    
         MR    R0,R0                                                            
         L     RF,=F'100000'                                                    
         BAS   RE,VBCADIV                                                       
*                   **NOTE-AOR VAT NOT IN VBTAB                                 
         LTR   R3,R3               IF DOING GST NOW                             
         BNZ   *+8                                                              
         ST    R1,VBASVGST         SAVE GST AMOUNT                              
         ST    R1,VATADSP(R7)      SET IN MONTH CUME ROW                        
         A     R1,VATADSP-EXTDSP(R5)   AND TOTAL IN WKROW                       
         ST    R1,VATADSP-EXTDSP(R5)                                            
*                                                                               
VBCA8    DS    0H                                                               
         CH    R3,=Y(NPROVS)                                                    
         BNL   VBCA9                                                            
         LA    R3,1(R3)                                                         
         LA    R5,4(R5)            NEXT WKROW SLOT                              
         LA    R7,4(R7)            NEXT ACCUM SLOT                              
         LA    R4,1(R4)            NEXT PROVINCE                                
         B     VBCA4                                                            
*                                                                               
VBCA9    DS    0H                                                               
         MVC   VBAMODE,=C'CA'        VBCALC MODE - AOR                          
         GOTO1 =A(VBATRC),VBADMCB,(R8)                                          
*                                                                               
VBCAALCX DS    0H                                                               
         XIT1                                                                   
*                                                                               
VBCADIV  DRNDR (R0),(RF)                                                        
         BR    RE                                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        VBCALCR  CALC VAT AMOUNTS FOR RETAIL BILLS                             
*                 AND ADD INTO WKROW                                            
*                                                                               
*        PARM1  BYTE  0   NOT USED                                              
*               BYTES 1-3 A(PERLIST ENTRY)                                      
*        PARM2  BYTE  0   NOT USED                                              
*               BYTES 1-3 A(MONTH CUME SLOT - ATOTS)                            
*        PARM3  BYTE  0   NOT USED                                              
*               BYTES 1-3 A(BRKTAB ENTRY)                                       
*        PARM4  BYTE  0   NOT USED                                              
*               BYTES 1-3 A(RETAIL SHARE PCT)                                   
*                                                                               
*        NOTE - VAT IS CALCULATED FOR THE GIVEN MONTH AND                       
*               TOTALED IN WKROW TO STAY COMPATIBLE WITH OLD BILCALC.           
*               IT IS ALSO SET AT VATRDSP IN THE REGULAR CUME (AT R7).          
*               ALSO TAX IS SET AT RETTDSP.                                     
*             **IT IS NOT STORED IN THE VBTAB                                   
*                                                                               
***********************************************************************         
         SPACE 2                                                                
         PRINT NOGEN                                                            
VBCALCR CSECT                                                                   
         NMOD1 VBAWRKL,VBCALCR                                                  
         USING PPWORKD,RA                                                       
         USING PPWORK2D,R9                                                      
         LR    R8,RC                                                            
         USING VBAWRKD,R8                                                       
         USING BILWRKD,RC                                                       
         LA    RC,SPACEND                                                       
*                                                                               
         L     R7,4(R1)            A(MONTH CUME SLOT)                           
         L     R3,8(R1)                                                         
         ST    R3,VBABRK           SAVE A(BREAK TABLE ENTRY)                    
         MVC   VBABRKC,BRKCODE-BRKTABD(R3)                                      
         L     RF,12(R1)           RETAIL SHARE PCT                             
         MVC   VBAFULL,0(RF)                                                    
         ST    R1,VBASVR1                                                       
         SPACE 2                                                                
*                                                                               
         L     RF,0(R1)                                                         
         LA    R0,PERLIST                                                       
         SR    RE,RE                                                            
         SR    RF,R0                                                            
         D     RE,=F'8'            PERLIST ENTRY IS 8 LONG                      
         MH    RF,=Y((NPROVS+1)*VBTABL)                                         
         L     R2,VBABRKC                                                       
         L     R2,ATOTSV(R2)                                                    
         AR    R2,RF                                                            
         USING VBTABD,R2                                                        
*                                                                               
         LA    R3,NPROVS+1                                                      
         LA    R5,WKROW                                                         
*                                                                               
VBCR4    DS    0H                                                               
         L     R1,VBTTAX           TAX AMOUNT FROM VBTAB ENTRY                  
         L     R0,VBAFULL          X SHARE % (TAX WILL NOT ADD                  
         MR    R0,R0               UP TO THE PENNY LIKE OTHER RETAIL            
         L     RF,=F'10000'        FIELDS BUT THATS OK BECAUSE IT IS            
         BAS   RE,VBCRDIV          ONLY USED FOR THE VAT CALC)                  
*                                                                               
         LR    RE,R1                                                            
         ST    RE,RETTDSP(R7)      SET TAX IN ACCUM                             
         A     RE,RETTDSP-EXTDSP(R5)    AND ROW FOR TOTALS                      
         ST    RE,RETTDSP-EXTDSP(R5)                                            
*                                                                               
         L     R1,VBTVATB          VAT BASIS                                    
         L     R0,VBAFULL          X SHARE % (TOTAL MAY BE DIFFER               
         MR    R0,R0               SLIGHTLY FROM RETADSP BECAUSE                
         L     RF,=F'10000'        OF ROUNDING)                                 
         BAS   RE,VBCRDIV                                                       
         ST    R1,VBTVATB          STORE BASIS BACK IN VBTAB                    
*                                                                               
VBCR6    DS    0H                                                               
         LR    RE,R1                    VAT BASIS                               
         ST    RE,VATRBDSP(R7)          SET IN MONTH CUME ROW                   
         A     RE,VATRBDSP-EXTDSP(R5)   AND TOTAL IN WKROW                      
         ST    RE,VATRBDSP-EXTDSP(R5)                                           
*                                                                               
         ICM   R0,15,VBTPCT                                                     
         MR    R0,R0                                                            
         L     RF,=F'100000'                                                    
         BAS   RE,VBCRDIV                                                       
         ST    R1,VBTVAT                                                        
*                                                                               
         ST    R1,RETVDSP(R7)      SET IN MONTH CUME ROW                        
         A     R1,RETVDSP-EXTDSP(R5)   AND TOTAL IN WKROW                       
         ST    R1,RETVDSP-EXTDSP(R5)                                            
*                                                                               
VBCR8    DS    0H                                                               
         LA    R2,VBTABL(R2)       NEXT PROVINCE                                
         LA    R5,4(R5)            NEXT WKROW SLOT                              
         LA    R7,4(R7)            NEXT ACCUM SLOT                              
         BCT   R3,VBCR4                                                         
*                                                                               
         MVC   VBAMODE,=C'CR'        VBCALC MODE - RETAIL                       
         GOTO1 =A(VBATRC),VBADMCB,(R8)                                          
*                                                                               
VBCRX    DS    0H                                                               
         XIT1                                                                   
*                                                                               
VBCRDIV  DRNDR (R0),(RF)                                                        
         BR    RE                                                               
         DROP  R2                                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        VBGETPCT  - GET VCTAB ENTRY                                            
*                                                                               
*                    PARM1 BYTE  0     PROVINCE NUMBER                          
*                          BYTES 1-3   A(MOS)                                   
*                    PARM2 BYTE  0     NOT USED                                 
*                          BYTES 1-3   A(VAT CODE)                              
*                                      ON OUTPUT = A(TABLE ENTRY)               
*                                                                               
***********************************************************************         
         SPACE 2                                                                
         PRINT NOGEN                                                            
VBGETPCT CSECT                                                                  
         NMOD1 VBAWRKL,VBGPC                                                    
         USING PPWORKD,RA                                                       
         USING PPWORK2D,R9                                                      
         LR    R8,RC                                                            
         USING VBAWRKD,R8                                                       
         USING BILWRKD,RC                                                       
         LA    RC,SPACEND                                                       
*                                                                               
         L     R2,=V(VCTAB)        VAT CODE TABLE                               
         USING VCTABD,R2                                                        
*                                                                               
VBGPC4   DS    0H                                                               
         CLC   0(1,R1),VCTPRV       PROVINCE                                    
         BNE   VBGPC8                                                           
         L     RF,4(R1)                                                         
         CLC   0(1,RF),VCTVCD       CODE                                        
         BNE   VBGPC8                                                           
         SR    RF,RF                                                            
         ICM   RF,7,1(R1)          MOS                                          
         BZ    VBGPC8B             IF 0, IGNORE                                 
         OC    0(2,RF),0(RF)                                                    
         BZ    VBGPC8B                                                          
         CLC   0(2,RF),VCTSTRT        START MOS                                 
         BL    VBGPC8                                                           
         CLC   0(2,RF),VCTEND         END MOS                                   
         BNH   VBGPC8B                                                          
*                                                                               
VBGPC8   DS    0H                                                               
         LA    R2,VCTABL(R2)       NEXT ENTRY                                   
         CLI   0(R2),X'FF'         END OF TABLE                                 
         BNE   VBGPC4                                                           
         XC    4(4,R1),4(R1)       NO ENTRY                                     
         B     VBGPCX                                                           
*                                                                               
VBGPC8B  DS    0H                                                               
         MVC   VBPCTWK(VCTABL),0(R2)                                            
         CLI   0(R2),06                 SEE IF QUEBEC                           
         BNE   VBGPC10                                                          
         CLI   MANRVDTP,0               SEE IF REVERSAL                         
         BZ    VBGPC10                                                          
         CLC   MANRVDTP,=X'5E0601'      SEE IF BEFORE JUN1/94                   
         BNL   VBGPC10                                                          
         OC    VBPCTWK+VCTABL-4(4),VBPCTWK+VCTABL-4                             
         BZ    VBGPC10                                                          
         MVC   VBPCTWK+VCTABL-4(4),=AL4(4000)   USE 4.000                       
*                                                                               
VBGPC10  DS    0H                                                               
         LA    RE,VBPCTWK                                                       
         ST    RE,4(R1)            RETURN A(ENTRY)  NOW VBPCTWK                 
******   ST    R2,4(R1)            RETURN A(ENTRY)                              
*                                                                               
VBGPCX   DS    0H                                                               
         XIT1                                                                   
         DROP  R2                                                               
         LTORG                                                                  
VBPCTWK  DS    CL20                                                             
*                                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        VBPRNT   CREATE GST/PST PRINT LINES                                    
*                                                                               
*        PARM1  BYTE  0   MODE- N=NORMAL,J=SEP ADJ,A=AOR                        
*               BYTES 1-3 A(BRKTAB ENTRY)                                       
*        PARM2  BYTE  0   NOT USED                                              
*               BYTES 1-3 A(DISP INTO LINE OF WHERE VAT$ TO PRINT)              
*        PARM3  BYTE  0   NOT USED                                              
*               BYTES 1-3 A(VAT CODES) FOR OTHER AGY  ** AOR MODE ONLY          
*        PARM4  BYTE  0   NOT USED                                              
*               BYTES 1-3 A(AOR VAT BASIS)    = AOR AMOUNT                      
*        PARM5  BYTE  0   NOT USED                                              
*               BYTES 1-3 A(AOR VAT AMOUNTS)                                    
*        PARM6  BYTES 1-4 ON RETURN = SUM OF ALL VATS                           
*                                                                               
*              NOTE - 'NORMAL' MODE INCLUDES RETAIL                             
*                                                                               
***********************************************************************         
         SPACE 2                                                                
         PRINT NOGEN                                                            
VBPRNT   CSECT                                                                  
         NMOD1 VBAWRKL,VBPRNT                                                   
         USING PPWORKD,RA                                                       
         USING PPWORK2D,R9                                                      
         LR    R8,RC                                                            
         USING VBAWRKD,R8                                                       
         USING BILWRKD,RC                                                       
         LA    RC,SPACEND                                                       
*                                                                               
         SR    R3,R3                                                            
         ICM   R3,7,1(R1)                                                       
         ST    R3,VBABRK           SAVE A(BREAK TABLE ENTRY)                    
         MVC   VBABRKC,BRKCODE-BRKTABD(R3)                                      
         MVC   VBPRMODE,0(R1)      MODE                                         
         ST    R1,VBASVR1                                                       
         MVC   VBAPARMS,0(R1)      SAVE PARMS                                   
         XC    VBASVGSP,VBASVGSP                                                
         SPACE 2                                                                
*                                                                               
         CLI   VBPRMODE,C'N'       IF 'NORMAL' MODE                             
         BNE   *+8                                                              
         BAS   RE,VBPRMTOT         ADD UP TO 'TOTAL MONTH'                      
*                                                                               
         L     R2,VBABRKC                      RIGHT LEVEL                      
         L     R2,ATOTSV(R2)                                                    
         LH    RF,=Y(MAXMOS*(NPROVS+1)*VBTABL)  POINT TO 'TOTAL MONTH'          
         AR    R2,RF                                                            
         USING VBTABD,R2                                                        
*                                                                               
         SR    R3,R3               PROVINCE INDEX                               
         L     R4,4(R1)            A(DISPL WHERE VAT $ TO PRINT)                
         CLI   VBPRMODE,C'A'       SEE IF AOR                                   
         BNE   VBPR2                                                            
         CLI   AORLOPT,C'N'        AND DOING CLT/PRD LIST                       
         BNE   VBPR2A              DON'T ADJUST R4                              
*                                                                               
VBPR2    SH    R4,=H'1'            ADJUST BY ONE BYTE                           
VBPR2A   LA    R4,P1(R4)                                                        
         ST    R4,VBALINE                                                       
         L     R5,8(R1)            A(AOR VAT CODES)                             
         MVC   VBASVGSP,VBTPCT     SAVE GST %                                   
         XC    VBADUB,VBADUB                                                    
*                                                                               
VBPR4    DS    0H                                                               
         CLI   VBPRMODE,C'A'       FOR AOR                                      
         BNE   VBPR4D                                                           
         CLI   0(R5),C' '          CHECK AOR VAT CODE ACTIVE                    
         BH    VBPR4E                                                           
*                                                                               
VBPR4C   DS    0H                                                               
         LTR   R3,R3               DO GST EVEN IF NO CODE??                     
         BZ    VBPR4E              (WILL ONLY BE HERE IF                        
         B     VBPR21               CVATSW NOT = 0)                             
*                                                                               
VBPR4D   DS    0H                  NOT AOR                                      
         CLI   VBTCOD,C' '         ACTIVE PROVINCE?                             
         BNH   VBPR4C                                                           
*                                                                               
VBPR4E   DS    0H                                                               
         CLI   VBPRMODE,C'A'        SEE IF AOR                                  
         BNE   VBPR4E5                                                          
         LTR   R3,R3                SEE IF DOING GST                            
         BZ    VBPR4E5                                                          
*                                                                               
         CLI   0(R5),C'S'       ALL NON-'S' CODES ARE NOW ZERO PCT.             
         BNE   VBPR4E5              SHOW THEM EVEN IF VAT IS ZERO               
*                                                                               
         LR    RF,R3                                                            
         SLL   RF,2                  X 4                                        
         L     R1,VBAPARMS+16        A(AOR VAT AMOUNTS)                         
         L     RE,0(R1,RF)                                                      
         LTR   RE,RE                                                            
         BZ    VBPR21                SKIP TO NEXT PROVINCE IF NO VAT            
*                                                                               
VBPR4E5  MVC   VBAWRK2,SPACES                                                   
         LA    RE,VBAWRK2                                                       
         LR    RF,R3                                                            
         MH    RF,=H'14'                                                        
         A     RF,=V(PSTNAMS)      'NAME' OF GST/PST                            
         MVC   0(7,RE),0(RF)       ENGLISH                                      
         CLI   RCLANG,4                                                         
         BNE   *+10                                                             
         MVC   0(7,RE),7(RF)       OR FRENCH                                    
         LA    RE,8(RE)                                                         
         CLI   0(RE),C' '                                                       
         BH    *+8                                                              
         BCT   RE,*-8                                                           
         LA    RE,3(RE)                                                         
*                                                                               
         LR    RF,R3                                                            
         MH    RF,=H'16'                                                        
         A     RF,=V(VATACCS)      ACCOUNT # TABLE                              
         CLI   0(RF),C' '          ANY # PRESENT?                               
         BNH   VBPR4F                                                           
         MVI   0(RE),C'#'                                                       
         MVC   1(16,RE),0(RF)                                                   
         LA    RE,16(RE)                                                        
         CLI   0(RE),C' '                                                       
         BH    *+8                                                              
         BCT   RE,*-8                                                           
*                                                                               
VBPR4F   DS    0H                                                               
         SH    R4,=H'27'           BACK UP 27 SPACES                            
         LA    R0,VBAWRK2                                                       
         SR    RE,R0               RE = LEN(NAME + ACCT -1)                     
         LR    RF,R4                                                            
         SR    RF,RE                                                            
         SH    RF,=H'3'                                                         
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RF),VBAWRK2                                                  
*                                                                               
         MVC   VBAFULL,VBTPCT      %                                            
*                                                                               
         CLI   VBPRMODE,C'A'       FOR AOR, GET % FROM VCTAB                    
         BNE   VBPR4H                                                           
         XC    VBAFULL,VBAFULL                                                  
         CLI   0(R5),C'S'          % ONLY FOR CODE S?                           
         BNE   VBPR4H                                                           
         GOTO1 =V(VBGETPCT),VBADMCB,((R3),0),(R5)                               
         ICM   RF,15,4(R1)                                                      
         BNZ   *+6                                                              
         DC    H'0'                                                             
         MVC   VBAFULL,VCTPCT-VCTABD(RF)  SET %                                 
*                                                                               
VBPR4H   DS    0H                                                               
         LTR   R3,R3               IF DOING GST NOW                             
         BNZ   *+10                                                             
         MVC   VBASVGSP,VBAFULL    SAVE GST %                                   
         CLC   VBAFULL,=X'FFFFFFFF'    MIXED %'S  (SHOULDN'T HAPPEN)?           
         BE    VBPR8                                                            
         MVI   0(R4),C'('                                                       
         LA    R4,1(R4)                                                         
         OC    VBAFULL,VBAFULL     IF NO %                                      
         BNZ   VBPR4H2                                                          
         MVC   0(5,R4),=C'0.000'                                                
         LA    R4,5(R4)                                                         
         B     VBPR4H3                                                          
*                                                                               
VBPR4H2  DS    0H                                                               
         EDIT  (B4,VBAFULL),(7,0(R4)),3,ALIGN=LEFT   %                          
         AR    R4,R0                                                            
VBPR4H3  DS    0H                                                               
         MVI   0(R4),C'%'                                                       
         MVI   1(R4),C')'                                                       
*                                                                               
*                                  GET BASIS AMOUNTS                            
*                                  -----------------                            
*                                                                               
         OC    VBAFULL,VBAFULL                                                  
         BZ    VBPR8               SKIP IF NO PCT.                              
*                                                                               
         CLI   VBPRMODE,C'A'       AOR MODE                                     
         BNE   VBPR4I                                                           
         L     R1,VBAPARMS+12      A(AMOUNT) IN PARM4                           
         L     R1,0(R1)                                                         
         LTR   R3,R3               IF NOT DOING GST NOW                         
         BZ    VBPR4H4                                                          
         ICM   R0,15,VBASVGSP      FIRST APPLY GST TO GET BASIS                 
         A     R0,=F'100000'                                                    
         MR    R0,R0                                                            
         L     RF,=F'100000'                                                    
         BAS   RE,VBPRDIV                                                       
*                                                                               
VBPR4H4  DS    0H                                                               
         B     VBPR6B                                                           
*                                                                               
VBPR4I   DS    0H                                                               
         CLI   VBPRMODE,C'J'       SEP ADJ BILL MODE                            
         BNE   VBPR4K                                                           
         LR    RF,R3               PROVINCE INDEX                               
         SLL   RF,2                X4 FOR DISP                                  
         L     R1,VATCBDSP(R7,RF)  ADJ VAT BASIS                                
*                                                                               
VBPR4J   DS    0H                                                               
         B     VBPR6B                                                           
*                                                                               
VBPR4K   DS    0H                                                               
         CLI   VBPRMODE,C'C'       SEP CD BILL MODE                             
         BNE   VBPR4L                                                           
         LR    RF,R3               PROVINCE INDEX                               
         SLL   RF,2                X4 FOR DISP                                  
         L     R1,VATDBDSP(R7,RF)  CD VAT BASIS                                 
*                                                                               
VBPR4K4  DS    0H                                                               
         B     VBPR6B                                                           
*                                                                               
VBPR4L   DS    0H                  'NORMAL' MODE                                
         CLI   RETPROF,0           IF RETAIL                                    
         BE    VBPR4R                                                           
         L     R1,VBTVATB          BASIS FROM VBTAB                             
         B     VBPR6B                                                           
*                                                                               
VBPR4R   DS    0H                  NON-RETAIL                                   
         L     R1,VBTVATB          BASIS FROM VBTAB                             
*                                                                               
VBPR6B   DS    0H                                                               
         LTR   R1,R1               ANY BASIS ?                                  
         BZ    VBPR8               NO, DONT SHOW IT                             
         MVI   1(R4),C' '                                                       
         MVC   2(2,R4),=C'OF'                                                   
         CLI   RCLANG,4            FRENCH                                       
         BNE   *+10                                                             
         MVC   2(2,R4),=C'DE'                                                   
         LA    R4,5(R4)                                                         
*                                  BASIS AMOUNT                                 
         LR    R0,R1                                                            
*                                                                               
         CLI   VBPRMODE,C'C'       SEE IF DOING CD SEP BILL                     
         BNE   VBPR6D                                                           
         LCR   R0,R0              REVERSE SIGN                                  
*                                                                               
VBPR6D   CURED (R0),(14,0(R4)),CURTAB,CR=YES,COMMAS=YES,ALIGN=LEFT              
         AR    R4,R0                                                            
         MVI   0(R4),C')'                                                       
*                                                                               
*                                  ACTUAL VAT AMOUNT                            
*                                  **NOTE- COULD GET SOME FROM VBTAB            
*                                  **NOTE- 'TOTAL MONTH' CUMES                  
VBPR8    DS    0H                                                               
         L     R7,VBABRKC                                                       
         L     R7,ATOTS(R7)        POINT TO ACCUME                              
         AH    R7,=Y(MAXMOS*ROWL)  TOTAL MONTH ROW                              
         LR    RF,R3               PROVINCE INDEX                               
         SLL   RF,2                X4 FOR DISP INTO VAT AMOUNTS                 
         L     RE,RETVDSP(R7,RF)   USE RETAIL VAT                               
         CLI   RETPROF,0           IF RETAIL                                    
         BNE   *+8                                                              
         L     RE,VATDSP(R7,RF)                                                 
*                                                                               
         CLI   VBPRMODE,C'J'       IF SEP ADJ BILL NOW                          
         BNE   *+8                                                              
         L     RE,VATCDSP(R7,RF)   USE ADJ VAT                                  
*                                                                               
         CLI   VBPRMODE,C'C'       IF SEP CD BILL NOW                           
         BNE   *+8                                                              
         L     RE,VATDDSP(R7,RF)   USE CD VAT                                   
*                                                                               
         CLI   VBPRMODE,C'A'       IF AOR                                       
         BNE   VBPR9                                                            
         L     R1,VBAPARMS+16      PARM5 HAS A(AMOUNTS)                         
         L     RE,0(R1,RF)         USE AOR VAT                                  
*                                                                               
VBPR9    DS    0H                                                               
         L     R4,VBALINE          WHERE TO PRINT VAT $                         
         LR    R0,RE                                                            
*                                                                               
         CLI   VBPRMODE,C'C'       SEE IF DOING CD SEP BILL                     
         BNE   VBPR9C                                                           
         LCR   R0,R0              REVERSE SIGN                                  
*                                                                               
VBPR9C   CURED (R0),(16,0(R4)),CURTAB,CR=YES,COMMAS=YES                         
*                                                                               
         A     R0,VBADUB           CUME VAT AMOUNTS                             
         ST    R0,VBADUB                                                        
*                                                                               
         L     R4,VBALINE          BUMP TO NEXT LINE                            
         LA    R4,132(R4)                                                       
         ST    R4,VBALINE                                                       
*                                                                               
VBPR21   DS    0H                                                               
         LA    R2,VBTABL(R2)       NEXT PROVINCE                                
         LA    R5,1(R5)                                                         
         LA    R3,1(R3)                                                         
         CH    R3,=Y(NPROVS)                                                    
         BNH   VBPR4                                                            
         GOTO1 APRNT                                                            
*                                                                               
VBPRX    DS    0H                                                               
         L     R1,VBASVR1          RESTORE R1                                   
         MVC   20(4,R1),VBADUB     RETURN VAT TOTAL IN PARM 6                   
         XIT1                                                                   
*                                                                               
VBPRDIV  DRNDR (R0),(RF)                                                        
         BR    RE                                                               
         SPACE 2                                                                
***********************************************************************         
*   VBPRMTOT - ADD UP TO 'TOTAL MONTH'                                          
***********************************************************************         
*                                                                               
VBPRMTOT NTR1                                                                   
         L     R2,VBABRKC                      FIRST MONTH/PROV                 
         L     R2,ATOTSV(R2)                                                    
         LH    R3,=Y(MAXMOS*(NPROVS+1)*VBTABL)  POINT TO 'TOTAL MONTH'          
         AR    R3,R2                                                            
*                                                                               
         LA    R0,NPROVS+1         FIRST CLEAR TOTAL                            
         LR    R4,R3                                                            
         XC    0(VBTABL,R4),0(R4)                                               
         LA    R4,VBTABL(R4)                                                    
         BCT   R0,*-10                                                          
*                                  NOW ROLL UP TOTALS                           
         LA    R5,MAXMOS           PROVINCES WITHIN MONTHS                      
*                                                                               
VBMT2    DS    0H                                                               
         LA    R0,NPROVS+1                                                      
         LR    R4,R3                                                            
*                                                                               
VBMT3    DS    0H                                                               
         LA    R1,VBTBLTN          COUNT OF FIELDS                              
*                                                                               
VBMT4    DS    0H                  ROLL UP DOLLAR FIELDS                        
         L     RF,0(R2)                                                         
         A     RF,0(R4)                                                         
         ST    RF,0(R4)                                                         
         LA    R2,4(R2)                                                         
         LA    R4,4(R4)                                                         
         BCT   R1,VBMT4                                                         
*                                  R2 AND R4 NOW AT %,CODE,ETC                  
         OC    0(4,R4),0(R4)       DO WE ALREADY HAVE %                         
         BNZ   VBMT7                                                            
         MVC   0(4,R4),0(R2)       NO, JUST SET %                               
         B     VBMT7B                                                           
*                                                                               
VBMT7    DS    0H                                                               
         OC    0(4,R2),0(R2)    BUT DON'T OVERRIDE WITH NULLS (NO PST)          
         BZ    VBMT7B                                                           
         CLC   0(4,R4),0(R2)       ELSE, ARE THEY THE SAME                      
         BE    VBMT7B                                                           
         MVC   0(4,R4),=X'FFFFFFFF'  NO, SET MIXED (SHOULDNT HAPPEN??)          
*                                                                               
VBMT7B   DS    0H                                                               
         CLI   4(R4),0             DO WE ALREADY HAVE CODE                      
         BNE   VBMT7D                                                           
         MVC   4(1,R4),4(R2)       NO, JUST SET %                               
         B     VBMT7F                                                           
*                                                                               
VBMT7D   DS    0H                                                               
         CLI   4(R2),0        BUT DON'T OVERRIDE WITH NULLS (NO PST)            
         BE    VBMT7F                                                           
         CLC   4(1,R4),4(R2)       ELSE, ARE THEY THE SAME                      
         BE    VBMT7F                                                           
         MVI   4(R4),X'FF'         NO, SET MIXED (SHOULDNT HAPPEN??)            
*                                                                               
VBMT7F   DS    0H                                                               
VBMT8    DS    0H                                                               
         LA    R2,VBTABL-(VBTBLTN*4)(R2)          BUMP PAST %, ETC.             
         LA    R4,VBTABL-(VBTBLTN*4)(R4)                                        
         BCT   R0,VBMT3                                                         
*                                                                               
         BCT   R5,VBMT2            NEXT MONTH  (NOTE- R2                        
*                                  AUTOMATICALLY POSITIONED)                    
VBPRMX   DS    0H                                                               
         XIT1                                                                   
         DROP  R2                                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        VBROLL   ROLL-UP VBTAB                                                 
*                                                                               
*        PARM1  BYTE  0   NOT USED                                              
*               BYTES 1-3 A(BRKTAB ENTRY)                                       
*                                                                               
***********************************************************************         
         SPACE 2                                                                
         PRINT NOGEN                                                            
VBROLL   CSECT                                                                  
         NMOD1 VBAWRKL,VBROLL                                                   
         USING PPWORKD,RA                                                       
         USING PPWORK2D,R9                                                      
         LR    R8,RC                                                            
         USING VBAWRKD,R8                                                       
         LA    RC,SPACEND                                                       
         USING BILWRKD,RC                                                       
*                                                                               
         ICM   R3,15,0(R1)                                                      
         ST    R3,VBABRK           SAVE A(BREAK TABLE ENTRY)                    
         MVC   VBABRKC,BRKCODE-BRKTABD(R3)  BRKTAB ENTRY                        
         ST    R1,VBASVR1                                                       
         SPACE 2                                                                
*                                                                               
         L     R2,VBABRKC          R2 IS THIS LEVEL                             
         L     R2,ATOTSV(R2)                                                    
         LR    R3,R2                                                            
         AH    R3,=Y(VTOTSIZE)     R3 IS NEXT LEVEL                             
*                                                                               
         USING VBTABD,R2                                                        
         LA    R4,(NPROVS+1)*(MAXMOS+1)                                         
*                                                                               
VBR4     DS    0H                                                               
         LA    R5,VBTBLTN          COUNT OF FIELDS                              
*                                                                               
VBR6     DS    0H                                                               
         L     RF,0(R2)                                                         
         A     RF,0(R3)                                                         
         ST    RF,0(R3)                                                         
         XC    0(4,R2),0(R2)       CLEAR OLD                                    
         LA    R2,4(R2)                                                         
         LA    R3,4(R3)                                                         
         BCT   R5,VBR6             NEXT FIELD ENTRY                             
*                                  NOW POINTED AT % AND CODE                    
         OC    0(4,R3),0(R3)       DO WE ALREADY HAVE %                         
         BNZ   VBR7                                                             
         MVC   0(4,R3),0(R2)       NO, JUST SET %                               
         B     VBR7B                                                            
*                                                                               
VBR7     DS    0H                                                               
         OC    0(4,R2),0(R2)    BUT DON'T OVERRIDE WITH NULLS (NO PST)          
         BZ    VBR7B                                                            
         CLC   0(4,R3),0(R2)       ELSE, ARE THEY THE SAME                      
         BE    VBR7B                                                            
         MVC   0(4,R3),=X'FFFFFFFF'  NO, SET MIXED (SHOULDNT HAPPEN??)          
*                                                                               
VBR7B    DS    0H                                                               
         CLI   4(R3),0             DO WE ALREADY HAVE CODE                      
         BNE   VBR7D                                                            
         MVC   4(1,R3),4(R2)       NO, JUST SET %                               
         B     VBR7F                                                            
*                                                                               
VBR7D    DS    0H                                                               
         CLI   4(R2),0        BUT DON'T OVERRIDE WITH NULLS (NO PST)            
         BE    VBR7F                                                            
         CLC   4(1,R3),4(R2)       ELSE, ARE THEY THE SAME                      
         BE    VBR7F                                                            
         MVI   4(R3),X'FF'         NO, SET MIXED (SHOULDNT HAPPEN??)            
*                                                                               
VBR7F    DS    0H                                                               
VBR8     DS    0H                                                               
         XC    0(VBTABL-(VBTBLTN*4),(R2)),0(R2)          CLEAR OLD              
         LA    R2,VBTABL-(VBTBLTN*4)(R2)          BUMP PAST %, ETC.             
         LA    R3,VBTABL-(VBTBLTN*4)(R3)                                        
         BCT   R4,VBR4             NEXT PROVINCE/MONTH                          
*                                                                               
         MVC   VBAMODE,=C'RU'       VBCALC MODE - ROLLUP                        
         L     RF,VBABRK           POINT TO NEXT LEVEL FOR TRACE DESC           
         SH    RF,=Y(BRKL)                                                      
         MVC   VBABRKC,0(RF)                                                    
         GOTO1 =A(VBATRC),VBADMCB,(R8)                                          
*                                                                               
VBROLLX  DS    0H                                                               
         XIT1                                                                   
         DROP  R2                                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
       ++INCLUDE PPREPB1WRK                                                     
         EJECT                                                                  
*                                                                               
*INCLUDE DDBUFFALOD                                                             
         PRINT OFF                                                              
       ++INCLUDE DDBUFFALOD                                                     
         PRINT ON                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'009PXB12E    05/01/02'                                      
         END                                                                    
