*          DATA SET CXSPE2C    AT LEVEL 038 AS OF 01/20/04                      
*CATALP CXSPE2C                                                                 
         TITLE 'CXSPE2C - CONTROL SECURITY EXTRACT - PERS. PASSWORD V2'         
**********************************************************************          
* AL4 A(EXTRACT RECORD)                                              *          
* AL4 A(CONFILE RECORD)                                              *          
* AL4 0 OR A(PASSWORD ENCRYPTION KEY)                                *          
* AL4 N/A                                                            *          
* AL4 A(EXTRACT CONTROL BLOCK)                                       *          
**********************************************************************          
         SPACE 1                                                                
         PRINT NOGEN                                                            
CXSPE2C  CSECT                                                                  
         NMOD1 WORKX-WORKD,*CXSPE2C,RR=RA                                       
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RA,RELO                                                          
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         L     R7,ADXBLOCK                                                      
         USING DXBLOCKD,R7                                                      
         L     R6,DXSTPTR                                                       
         USING SXDTABD,R6                                                       
         MVC   VERSION,SXDTVER                                                  
*                                                                               
         LM    R2,R3,0(R1)                                                      
         USING CXSPED,R2           R2=A(EXTRACT RECORD)                         
         USING SAPEREC,R3          R3=A(CONFILE RECORD)                         
*                                                                               
CXSLEN   XC    CXSPELEN,CXSPELEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   CXSPELEN(2),=AL2(CXSPEDL)                                        
         MVC   CXSPETYP,CXSPEDQ                                                 
         MVI   CXSPEDX,MXTRTQ                                                   
         MVI   CXSPEDX+1,MXTRTQ                                                 
*                                                                               
CXSAID   MVI   CXSPEAID-1,MXTRTQ   AGENCY                                       
         MVC   CXSPEAID,SAPEAGY                                                 
*                                                                               
CXSPID   MVI   CXSPEPID-1,MXTRTQ   PERSON ID                                    
         MVC   CXSPEPID,SAPEPID                                                 
*                                                                               
CXPEFD   MVI   CXSPEEFD-1,MXTRTQ   EFFECTIVE DATE                               
         MVC   EFFDATE,SAPEDEF                                                  
         XC    EFFDATE,FFILL                                                    
         GOTO1 VDATCON,DMCB,(2,EFFDATE),(20,CXSPEEFD)                           
*                                                                               
         LA    R4,SAPEDATA         R4=A(FIRST ELEMENT)                          
*                                                                               
CXSELM   CLI   0(R4),0             GET ELEMENT FROM RECORD                      
         BE    CXSNXTX                                                          
         CLI   0(R4),SAPWDELQ      GET PASSWORD POINTER ELEMENT                 
         BE    CXSPWD                                                           
         CLI   0(R4),SANAMELQ      GET PERSON NAME ELEMENT                      
         BE    CXSNAM                                                           
         CLI   0(R4),SAPERELQ      GET PERSON DETAILS ELEMENT                   
         BE    CXSPER                                                           
         CLI   0(R4),SAADRELQ      GET PERSON ADDRESS ELEMENT                   
         BE    CXSADR                                                           
         CLI   0(R4),SAAGCELQ      GET ACCESS GROUP ELEMENT                     
         BE    CXSAGC                                                           
*                                                                               
CXSNXT   SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R4)                                                         
         CH    RF,=H'1'                                                         
         BNH   EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R4,RF                                                            
         B     CXSELM                                                           
*                                                                               
         USING SAPWDD,R4                                                        
CXSPWD   MVI   CXSPEPWD-1,MXTRTQ   PASSWORD CODE                                
         MVC   CXSPEPWD,SAPWDCOD                                                
         ICM   RF,15,AENCKEY                                                    
         BZ    CXSPWD10                                                         
         LA    R5,CXSPEPWD                                                      
         GOTO1 VCRYPT,DMCB,L'CXSPEPWD,(R5),(RF)                                 
         CLI   0(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
CXSPWD10 MVI   CXSPENUM-1,MXTRTQ   PERSON NUMBER                                
         SR    RF,RF                                                            
         ICM   RF,3,SAPWDNUM                                                    
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  CXSPENUM,DUB+5(3)                                                
         B     CXSNXT                                                           
         DROP  R4                                                               
*                                                                               
         USING SANAMD,R4                                                        
CXSNAM   MVI   CXSPEFNA-1,MXTRTQ   FIRST NAME                                   
         MVI   CXSPEMNA-1,MXTRTQ   MIDDLE NAME                                  
         MVI   CXSPELNA-1,MXTRTQ   LAST NAME                                    
         MVI   CXSPEFNA,C' '                                                    
         MVC   CXSPEFNA+1(L'CXSPEFNA-1),CXSPEFNA                                
         MVI   CXSPEMNA,C' '                                                    
         MVC   CXSPEMNA+1(L'CXSPEMNA-1),CXSPEMNA                                
         MVI   CXSPELNA,C' '                                                    
         MVC   CXSPELNA+1(L'CXSPELNA-1),CXSPELNA                                
         SR    RF,RF                                                            
         LA    RE,SANAMELN                                                      
CXSNAMF  TM    SANAMIND,SANAMIFN                                                
         BZ    CXSNAMM                                                          
         IC    RF,0(RE)                                                         
         LA    RE,1(RE)                                                         
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   CXSPEFNA(0),0(RE)                                                
         TR    CXSPEFNA,MXTRT                                                   
         LA    RF,1(RF)                                                         
         AR    RE,RF                                                            
CXSNAMM  TM    SANAMIND,SANAMIMN                                                
         BZ    CXSNAML                                                          
         IC    RF,0(RE)                                                         
         LA    RE,1(RE)                                                         
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   CXSPEMNA(0),0(RE)                                                
         TR    CXSPEMNA,MXTRT                                                   
         LA    RF,1(RF)                                                         
         AR    RE,RF                                                            
CXSNAML  TM    SANAMIND,SANAMILN                                                
         BZ    CXSNAMX                                                          
         IC    RF,0(RE)                                                         
         LA    RE,1(RE)                                                         
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   CXSPELNA(0),0(RE)                                                
         TR    CXSPELNA,MXTRT                                                   
CXSNAMX  B     CXSNXT                                                           
         DROP  R4                                                               
*                                                                               
         USING SAPERD,R4                                                        
CXSPER   MVI   CXSPEOFF-1,MXTRTQ   PERSON OFFICE CODE                           
         MVC   CXSPEOFF,SAPEROFF                                                
*                                                                               
         MVI   CXSPEDEP-1,MXTRTQ   PERSON DEPARTMENT CODE                       
         MVC   CXSPEDEP,SAPERDID                                                
*                                                                               
         MVI   CXSPEDHI-1,MXTRTQ   PERSON HIRE DATE                             
         GOTO1 VDATCON,DMCB,(2,SAPERDHI),(20,CXSPEDHI)                          
*                                                                               
         MVI   CXSPEDHI-1,MXTRTQ   PERSON TERMINATION DATE                      
         GOTO1 VDATCON,DMCB,(2,SAPERDTE),(20,CXSPEDTE)                          
*                                                                               
         CLI   VERSION,3           TEST VERSION >= 3                            
         BL    CXSNXT              NO                                           
*                                                                               
         MVI   CXSPEIPE-1,MXTRTQ   SET DELIMITER                                
         MVI   CXSPEIPE,C'Y'       SET PASSWORD EXPIRATION TO Y                 
         CLI   SAPERPCN,SAPERPNQ   TEST PASSWORD DOES NOT EXPIRE                
         BE    CXSPER10            YES                                          
         CLI   SAPERPCN,SAPERPAQ   TEST AUTOMATIC REMOTE PROCESS                
         BNE   CXSNXT                                                           
*                                                                               
CXSPER10 MVI   CXSPEIPE,C'N'       PASSWORD DOES NOT EXPIRE                     
         B     CXSNXT                                                           
         DROP  R4                                                               
*                                                                               
         USING SAAGCD,R4                                                        
CXSAGC   MVI   CXSPEAGC-1,MXTRTQ   PERSON ACCESS GROUP CODE                     
         GOTO1 VHEXOUT,DMCB,SAAGCNUM,CXSPEAGC,2,=C'TOG'                         
         B     CXSNXT                                                           
         DROP  R4                                                               
*                                                                               
         USING SAADRD,R4                                                        
CXSADR   EQU   *                   PERSON ADDRESS                               
         B     CXSNXT                                                           
         DROP  R4                                                               
*                                                                               
CXSNXTX  B     EXIT                                                             
*                                                                               
DELREC   L     R1,APARM            DELETE THIS RECORD                           
         MVI   8(R1),X'80'                                                      
*                                                                               
EXIT     EQU   *                                                                
         XMOD1 1                                                                
*                                                                               
         EJECT                                                                  
         DS    0D                                                               
SPACES   DC    32C' '                                                           
FFILL    DC    80X'FF'                                                          
SAVEACTN DC    CL1' '                                                           
         DS    0D                                                               
         LTORG                                                                  
         SPACE 1                                                                
       ++INCLUDE MXTRT                                                          
       ++INCLUDE CXSRECID                                                       
         SPACE 1                                                                
         DS    0D                                                               
VHEXOUT  DC    V(HEXOUT)                                                        
VCRYPT   DC    V(CRYPT)                                                         
VDATCON  DC    V(DATCON)                                                        
         DS    0D                                                               
         DC    C'CXSPECC '                                                      
CXSPECC  DS    0XL4                CLIENT SAVE DATA AREA                        
         DC    1000XL4'00'         A/M,CLI                                      
         DC    8X'FF'                                                           
         EJECT                                                                  
* INCLUDE CXTRACT LOCAL DSECTS                                                  
         SPACE 1                                                                
       ++INCLUDE CXTRACTD                                                       
         EJECT                                                                  
WORKD    DSECT                                                                  
PARM     DS    0XL20               PARAMETER LIST                               
AXREC    DS    A                   A(EXTRACT RECORD)                            
CFREC    DS    A                   A(CONFILE RECORD)                            
AENCKEY  DS    A                   A(PASSWORD ENCRYPTION KEY, 10 BYTES)         
         DS    A                   N/A                                          
ADXBLOCK DS    A                   A(EXTRACT CONTROL BLOCK)                     
*                                                                               
DMCB     DS    6F                                                               
DUB      DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
RELO     DS    A                                                                
*                                                                               
EFFDATE  DS    XL2                                                              
VERSION  DS    XL1                                                              
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE CXSPE2D                                                        
         EJECT                                                                  
       ++INCLUDE SEACSFILE                                                      
         EJECT                                                                  
       ++INCLUDE DXDSECTS                                                       
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'038CXSPE2C   01/20/04'                                      
         END                                                                    
