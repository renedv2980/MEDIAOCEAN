*          DATA SET DDWORKER   AT LEVEL 028 AS OF 05/01/02                      
*CATALP HARDWORK                                                                
         TITLE 'DDWORKER - WORK FILE DATA MANAGER'                              
WORKER   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 20,**WRKR**                                                      
         USING WORKD,RC                                                         
*                                                                               
         LR    RA,R1               SAVE DMCB POINTER                            
         USING DMCBD,RA                                                         
*                                                                               
         L     R2,DM2              GET DTF ADDRESS                              
         USING WRKFILED,R2                                                      
*                                                                               
         MVI   DM3,0               RESET ERROR BYTE                             
         XC    P1(24),P1                                                        
         ST    R2,P4               SET A(DTF)                                   
         LA    RE,P6                                                            
         ST    RE,P5               SET A(DISK ADDR)                             
*                                                                               
* TEST FILE OPEN                                                                
         OC    WRKMTX(14),WRKMTX                                                
         BNZ   WRKR2                                                            
* NOT OPEN                                                                      
*                                                                               
         GOTO1 =V(DADDS),P1,V(DAOPEN)                                           
         OC    WRKRELO,WRKRELO     DID USER SPECIFY A BUFFER                    
         BZ    SRCH                                                             
         LA    R1,WRKRELO          RELOCATE ADDRESS                             
         S     R1,WRKRELO                                                       
         A     R1,WRKABUFF                                                      
         ST    R1,WRKABUFF                                                      
         XC    0(16,R1),0(R1)                                                   
         MVC   16(4,R1),=F'19254'                                               
         MVC   20(4,R1),=F'185'                                                 
         TM    29(R2),X'08'        FOR 3340                                     
         BNO   SRCH                                                             
         MVC   16(4,R1),=F'8534'   SET TRACK CAPACITY                           
         MVC   20(4,R1),=F'167'        AND RECORD OVERHEAD                      
         B     SRCH                                                             
         SPACE 2                                                                
WRKR2    L     RE,DM1                                                           
         CLC   0(3,RE),=C'SRCH' (SIC)  TEST SRCH E-O-F COMMAND                  
         BNE   WRKCMD                                                           
         MVC   P6,WRKINEXT         SET LAST KNOWN INDEX REC                     
         B     SRCH3                                                            
         EJECT                                                                  
* READ INDEX RECORDS TO FIND LAST USED                                          
*                                                                               
SRCH     MVC   P6,=X'00010000'     SET FIRST REC-1                              
         MVC   WRKNEXT,DATASTRT    PRESET FILE START TTB0                       
*                                                                               
SRCH2    IC    RE,P6+2             NEXT REC                                     
         LA    RE,1(RE)                                                         
         STC   RE,P6+2                                                          
SRCH3    GOTO1 =V(DADDS),P1,V(RDID),WRKIX                                       
*                                                                               
         TM    P3+1,X'08'          TEST REC NOT FOUND                           
         BZ    SRCH4                                                            
*                                                                               
         CLC   P6,=X'00010100'     TEST FIRST REC                               
         BE    SRCH10              CREATE NEW INDEX                             
* NEXT TRACK                                                                    
         LH    RE,P6                                                            
         LA    RE,1(RE)                                                         
         SLL   RE,16                                                            
         ST    RE,P6                                                            
         CLC   P6,DATASTRT         TEST END OF INDEX                            
         BL    SRCH2               NOT YET                                      
         B     SRCHX                                                            
*                                                                               
SRCH4    BAS   RE,CHECK                                                         
         OC    WRKIX,WRKIX         TEST ENTRY USED                              
         BZ    SRCHX               NO-THIS IS LAST                              
         MVC   WRKNEXT(3),WRKIXEND  SAVE DATA END                               
         LH    RE,WRKNEXT          SET TO START ON NEXT TRACK                   
         LA    RE,1(RE)                                                         
         SLL   RE,16                                                            
         ST    RE,WRKNEXT          SET TT00                                     
         B     SRCH2                                                            
*                                                                               
SRCHX    MVC   WRKINEXT,P6         SAVE TTB0 OF FIRST FREE INDEX                
         B     WRKCMD                                                           
         EJECT                                                                  
* NO INDEX RECORDS - ADD NEW                                                    
*                                                                               
SRCH10   MVC   WRKNEXT,=X'00010000'                                             
         XC    WRKIX,WRKIX                                                      
         GOTO1 =A(RESERVE)                                                      
         BNE   SRCH                FILE WAS HELD - SEARCH INDEX AGAIN           
*                                                                               
SRCH12   GOTO1 =V(DADDS),P1,V(WTCKD),WRKIX,16                                   
         BAS   RE,CHECK                                                         
*                                                                               
         CLC   P6,DATASTRT         TEST END OF INDEX                            
         BL    SRCH12                                                           
*                                                                               
         MVC   WRKNEXT,DATASTRT    RESTORE START DATA ADDRESS                   
         MVC   WRKINEXT,=X'00010100' SET FREE INDEX ADDRESS                     
         GOTO1 =A(RELEASE)                                                      
         B     WRKCMD                                                           
         EJECT                                                                  
* COMMAND ANALYSIS                                                              
*                                                                               
WRKCMD   L     RE,DM1                                                           
*                                                                               
         CLC   0(3,RE),=C'INDEX'                                                
         BE    RDINDX                                                           
         CLC   0(3,RE),=C'READ'                                                 
         BE    RDFILE                                                           
         CLC   0(3,RE),=C'WRI'                                                  
         BE    WTFILE                                                           
         CLC   0(3,RE),=C'ADD'                                                  
         BE    ADD                                                              
         CLC   0(3,RE),=C'CLO'                                                  
         BE    CLOSE                                                            
         CLC   0(3,RE),=C'DEL'                                                  
         BE    DELETE                                                           
         CLC   0(3,RE),=C'REST'                                                 
         BE    RESTORE                                                          
         CLC   0(3,RE),=C'ERA'                                                  
         BE    ERASE                                                            
         CLC   0(3,RE),=C'SRCH'  (SIC)                                          
         BE    EXIT                                                             
         DC    H'0'                                                             
         SPACE 2                                                                
EXIT     XMOD1 1                                                                
         EJECT                                                                  
*                                                                               
RDINDX   XC    WRKFLDA,WRKFLDA                                                  
*                                                                               
         L     R4,DM3              GET ID ADDRESS                               
         MVC   DUB,0(R4)           MOVE TO SRCH ARG                             
         MVC   P6,=X'00010000'                                                  
         OC    DUB,DUB             TEST 'FIRST' REQ                             
         BZ    RDIX2               YES                                          
         CLC   WRKIXID,DUB         TEST 'NEXT' REQUEST                          
         BNE   RDIX2                                                            
         XC    DUB,DUB             SET FOR 'FIRST' REQ, STARTING HERE           
         MVC   P6,WRKIXDA                                                       
* GET NEXT INDEX REC                                                            
RDIX2    IC    RE,P6+2                                                          
         LA    RE,1(RE)                                                         
         STC   RE,P6+2                                                          
         CLC   P6,WRKINEXT         TEST AGAINST FIRST FREE INDEX                
         BNL   RDIXNF              IF HIGH OR EQ, DONE                          
*                                                                               
         GOTO1 =V(DADDS),P1,V(RDID),WRKIX                                       
*                                                                               
         TM    P3+1,X'08'          TEST REC NOT FOUND                           
         BZ    RDIX4                                                            
* NEXT TRK                                                                      
         LH    RE,P6                                                            
         LA    RE,1(RE)                                                         
         SLL   RE,16                                                            
         ST    RE,P6                                                            
         B     RDIX2                                                            
*                                                                               
RDIX4    TM    DM1,X'08'           TEST USER WANTS DELETES                      
         BO    *+12                                                             
         CLI   WRKIXST,0           CHECK STATUS                                 
         BNZ   RDIX2               NO GOOD                                      
*                                                                               
         OC    DUB,DUB             TEST SPECIFIC SEARCH                         
         BZ    *+14                                                             
         CLC   WRKIXID,DUB                                                      
         BNE   RDIX2                                                            
* GOT IT                                                                        
         MVC   WRKIXDA,P6          SAVE DISK ADDRESS                            
         L     R4,DM3                                                           
         MVC   0(16,R4),WRKIX      INDEX REC TO USER                            
         B     EXIT                                                             
         SPACE 2                                                                
RDIXNF   OI    DM3,X'90'           SET NOT FOUND                                
         XC    WRKIX,WRKIX                                                      
         B     EXIT                                                             
         EJECT                                                                  
RDFILE   MVC   P6(3),WRKIXBEG                                                   
         OC    WRKFLDA,WRKFLDA     TEST FIRST TIME                              
         BZ    RD2                 YES                                          
*                                                                               
         MVC   P6,WRKFLDA                                                       
RD1      IC    RE,P6+2                                                          
         LA    RE,1(RE)                                                         
         STC   RE,P6+2                                                          
*                                                                               
RD2      CLC   P6(3),WRKIXEND                                                   
         BNH   RD3                                                              
         OI    DM3,X'80'                                                        
         XC    WRKFLDA,WRKFLDA     CLEAR TO ALLOW RE-READ                       
         B     EXIT                                                             
*                                                                               
RD3      OC    WRKRELO,WRKRELO                                                  
         BNZ   RD4                                                              
         GOTO1 =V(DADDS),P1,V(RDID),DM4                                         
         B     RD5                                                              
*                                                                               
RD4      MVC   P7,P6                                                            
         LA    RF,P7                                                            
         ST    RF,P5                                                            
         L     R1,WRKABUFF                                                      
         LA    R1,24(R1)                                                        
         ST    R1,P6                                                            
         GOTO1 =V(DADDS),P1,V(RDTRK),DM4                                        
         MVC   P6,P7                                                            
         LA    RF,P6                                                            
         ST    RF,P5                                                            
*                                                                               
RD5      TM    P3+1,X'08'          TEST NO REC FOUND                            
         BO    RD6                                                              
         BAS   RE,CHECK                                                         
         MVC   WRKFLDA,P6          SAVE LAST ADDRESS                            
         B     EXIT                                                             
*                                                                               
RD6      LH    RE,P6               NEXT TRACK                                   
         LA    RE,1(RE)                                                         
         SLL   RE,16                                                            
         ST    RE,P6                                                            
         B     RD1                                                              
         EJECT                                                                  
WTFILE   L     R5,DM4              REC ADDRESS                                  
         LH    R0,0(R5)            LENGTH                                       
         MVC   P6,WRKFLDA                                                       
*                                                                               
         GOTO1 =V(DADDS),P1,V(WTID),(R5),(R0)                                   
*                                                                               
         BAS   RE,CHECK                                                         
         B     EXIT                                                             
         SPACE 2                                                                
DELETE   MVI   WRKIXST,X'FF'                                                    
*                                                                               
DELX     MVC   P6,WRKIXDA                                                       
*                                                                               
         GOTO1 =V(DADDS),P1,V(WTID),WRKIX,16                                    
         BAS   RE,CHECK                                                         
         B     EXIT                                                             
         SPACE 2                                                                
RESTORE  MVI   WRKIXST,0                                                        
         B     DELX                                                             
         EJECT                                                                  
* BEFORE ADD, CHECK CONCURRENT UPDATING                                         
*             CHECK DUPLICATE INDEX ENTRY                                       
*                                                                               
ADD      L     R6,=A(RSVTAB)       CHECK FIRST ADD                              
ADD2     CLC   0(7,R6),22(R2)                                                   
         BE    ADD20                                                            
         LA    R6,7(R6)                                                         
         CLI   0(R6),X'FF'                                                      
         BNE   ADD2                                                             
* THIS IS FIRST TIME                                                            
         GOTO1 =A(RESERVE)                                                      
* SEARCH FOR E-O-F                                                              
         MVC   WORK(16),DM1                                                     
         GOTO1 =A(WORKER),WORK,=C'SRCH'                                         
*                                                                               
* CHECK THE INDEX TO DELETE DUPLICATES                                          
*                                                                               
ADD4     MVC   WORK(16),DM1                                                     
         GOTO1 =A(WORKER),WORK,=C'INDEX'                                        
         CLI   8(R1),0             NON-ZERO MEANS NOT THERE                     
         BNE   ADD10                                                            
*                                                                               
         MVI   WRKIXST,X'FF'       SET DELETE FLAG                              
* WRITE INDEX REC                                                               
         MVC   P6(3),WRKIXDA                                                    
         GOTO1 =V(DADDS),P1,V(WTID),WRKIX,16                                    
         BAS   RE,CHECK                                                         
*                                                                               
         XC    WRKIX,WRKIX                                                      
         B     ADD4                CHECK FOR MORE DUPS                          
         EJECT                                                                  
ADD10    CLC   WRKINEXT,DATASTRT   TEST ROOM IN INDEX                           
         BL    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         XC    WRKIX,WRKIX                                                      
         L     R4,DM3                                                           
         MVC   WRKIXID,0(R4)                                                    
         B     ADD22                                                            
*                                                                               
* NOT FIRST ADD                                                                 
*                                                                               
ADD20    L     R4,DM3                                                           
         CLC   WRKIXID,0(R4)       ID MUST AGREE                                
         BE    ADD22                                                            
* CHANGE OF ID - CLOSE PREVIOUS, THEN ADD                                       
         MVC   WORK(16),DM1                                                     
         GOTO1 =A(WORKER),WORK,=C'CLOSE'                                        
         B     ADD                                                              
*                                                                               
ADD22    DS    0H                                                               
         L     R5,DM4              GET REC ADDRESS                              
         LH    R0,0(R5)            GET REC LEN                                  
         OC    WRKRELO,WRKRELO                                                  
         BZ    ADD24                                                            
         BAS   RE,ADDTOBUF                                                      
         B     EXIT                                                             
*                                                                               
ADD24    GOTO1 =V(DADDS),P1,V(WTCKD),(R5),(R0)                                  
         MVC   WRKIXEND,P6                                                      
         OC    WRKIXBEG,WRKIXBEG                                                
         BNZ   *+10                                                             
         MVC   WRKIXBEG,P6                                                      
         B     EXIT                                                             
         EJECT                                                                  
*                                                                               
CLOSE    OC    WRKRELO,WRKRELO                                                  
         BZ    *+8                                                              
         BAS   RE,ADDTRACK                                                      
         MVC   P6(3),WRKINEXT      FIRST FREE INDEX                             
*                                                                               
         GOTO1 =V(DADDS),P1,V(WTID),WRKIX,16                                    
         BAS   RE,CHECK                                                         
* FIND NEXT INDEX REC                                                           
CLS2     IC    RE,P6+2                                                          
         LA    RE,1(RE)                                                         
         STC   RE,P6+2                                                          
         GOTO1 =V(DADDS),P1,V(RDID),WRKIX                                       
         TM    P3+1,X'08'                                                       
         BZ    CLS4                                                             
* NEXT TRK                                                                      
         LH    RE,P6                                                            
         LA    RE,1(RE)                                                         
         SLL   RE,16                                                            
         ST    RE,P6                                                            
         CLC   P6,DATASTRT                                                      
         BL    CLS2                                                             
         B     CLSX                                                             
*                                                                               
CLS4     BAS   RE,CHECK                                                         
         MVC   WRKINEXT,P6                                                      
*                                                                               
CLSX     GOTO1 =A(RELEASE)                                                      
         LH    RE,WRKNEXT                                                       
         LA    RE,1(RE)                                                         
         SLL   RE,16                                                            
         ST    RE,WRKNEXT                                                       
         B     EXIT                                                             
         EJECT                                                                  
* CLEAR ALL INDEX ENTRIES                                                       
*                                                                               
ERASE    MVC   P6,=X'00010000'                                                  
         GOTO1 =A(RESERVE)                                                      
*                                                                               
ERS2     IC    RE,P6+2                                                          
         LA    RE,1(RE)                                                         
         STC   RE,P6+2                                                          
*                                                                               
         GOTO1 =V(DADDS),P1,V(RDID),WRKIX                                       
*                                                                               
         TM    P3+1,X'08'          TEST REC NOT FOUND                           
         BZ    ERS4                                                             
*                                                                               
         LH    RE,P6               NEXT TRACK                                   
         LA    RE,1(RE)                                                         
         SLL   RE,16                                                            
         ST    RE,P6                                                            
         CLC   P6,WRKINEXT         COMPARE TO FIRST FREE INDEX                  
         BL    ERS2                                                             
* RESET FILE POINTERS                                                           
         MVC   WRKINEXT,=X'00010100'                                            
         MVC   WRKNEXT,DATASTRT                                                 
         GOTO1 =A(RELEASE)                                                      
         B     EXIT                                                             
*                                                                               
ERS4     XC    WRKIX,WRKIX                                                      
         GOTO1 (RF),(R1),V(WTID)                                                
         BAS   RE,CHECK                                                         
         B     ERS2                                                             
         EJECT                                                                  
*              ADD A RECORD INTO BUFFER IF ANY SPACE                            
*                                                                               
         SPACE 2                                                                
ADDTOBUF NTR1                                                                   
         L     R7,WRKABUFF                                                      
         L     R3,0(R7)            R3=BYTES SO FAR                              
         AR    R3,R0                  + LENGTH OF THIS RECORD                   
         A     R3,20(R7)              + OVERHEAD                                
         C     R3,16(R7)           WILL IT FIT                                  
         BNH   ADDBUF2                                                          
         BAS   RE,ADDTRACK         NO - RELEASE A TRACK                         
         LR    R3,R0                                                            
         SPACE 2                                                                
ADDBUF2  LA    R4,24(R7)           FIND NEXT SLOT IN BUFFER                     
         A     R4,4(R7)                                                         
         XC    0(8,R4),0(R4)       BUILD COUNT                                  
         STH   R0,6(R4)                                                         
         LA    R4,8(R4)                                                         
         LR    R1,R0               THEN MOVE IN DATA                            
         MOVE  ((R4),(R1)),(R5)                                                 
         ST    R3,0(R7)            UPDATE COUNTERS - TRACK CAPACITY             
         L     R4,4(R7)                                                         
         AR    R4,R0                                                            
         LA    R4,8(R4)                                                         
         ST    R4,4(R7)            SIGMA L'COUNT+DATA                           
         L     R4,8(R7)                                                         
         AR    R4,R0                                                            
         ST    R4,8(R7)            SIGMA L'DATA                                 
         XIT1                                                                   
         SPACE 2                                                                
ADDTRACK NTR1                                                                   
         XC    W1(28),W1                                                        
         L     R7,WRKABUFF         SET UP FOR WRITE TRACK                       
         OC    0(4,R7),0(R7)                                                    
         BZ    XIT                                                              
         MVC   W3,4(R7)            SIGMA RECORD AND COUNT LENGTH (P3)           
         MVC   W4,DM2              A(DTF)                                       
         LA    R3,W7                                                            
         ST    R3,W5                                                            
         LA    R3,24(R7)           A(BUFFER)                                    
         ST    R3,W6                                                            
         GOTO1 =V(DADDS),W1,V(WTTRK),A(WRKTKWRK)                                
         OC    W3(2),W3                                                         
         BZ    *+6                                                              
         DC    H'0'                                                             
         XC    0(16,R7),0(R7)                                                   
         MVC   WRKIXEND,WRKNEXT                                                 
         OC    WRKIXBEG,WRKIXBEG                                                
         BNZ   *+14                                                             
         MVC   WRKIXBEG,WRKNEXT                                                 
         MVI   WRKIXBEG+2,1        RECORD 1                                     
*                                                                               
XIT      XIT1                                                                   
         EJECT                                                                  
CHECK    OC    P3(2),P3                                                         
         BCR   8,RE                                                             
         DC    H'0'                                                             
*                                                                               
*&&US                                                                           
DATASTRT DC    X'000B0000'         FIRST DATA TRACK                             
*&&                                                                             
*&&UK                                                                           
DATASTRT DC    X'00020000'         FIRST DATA TRACK                             
*&&                                                                             
         LTORG                                                                  
         TITLE 'DDWORKER - CONCURRENT UPDATE LOGIC'                             
RESERVE  CSECT                                                                  
         NMOD1 0,**RSRV**                                                       
         MVI   RSVSW,0             CLEAR WAIT SWITCH                            
*                                                                               
         GOTO1 =V(DADDS),P1,V(DACPUID)                                          
         TM    P3+1,X'80'          TEST WRONG CPU                               
         BO    BADCPU              YES - ERROR                                  
*                                                                               
         LA    R6,RSVTAB           TEST FILE NAME IN TABLE                      
RSV2     CLI   0(R6),0                                                          
         BE    RSV4                                                             
         CLC   0(7,R6),22(R2)      R2 HAS DTF ADDRESS                           
         BE    RSV4                                                             
         LA    R6,7(R6)                                                         
         CLI   0(R6),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'0'                                                             
         B     RSV2                                                             
RSV4     MVC   0(7,R6),22(R2)      MOVE FILE NAME TO LIST                       
*&&UK                                                                           
         B     RSVX                                                             
*&&                                                                             
*                                                                               
RSV5     COMRG                                                                  
         STH   R1,MYCOM            SAVE THIS COMRG ADDRESS                      
*                                                                               
         LH    R5,124(R1)          A(2ND PART PIB TABLE)                        
         L     R1,X'80'            GET NUMBER OF PARTITIONS                     
         LH    R0,44(R1)                                                        
RSV6     LA    R5,16(R5)           POINT TO NEXT PIB ENTRY                      
         LH    R1,0(R5)            COMRG ADDRESS                                
         CH    R1,MYCOM            IS IT ME                                     
         BE    RSV10               YES-SKIP                                     
         CLC   RSVID(2),18(R1)     IS WORKER THERE                              
         BNE   RSV10               NO                                           
* CHECK FILE LIST IN THIS PARTITION                                             
         MVC   RSVFULL+1(3),20(R1) GET A(HIS RESERVE TABLE)                     
         L     R6,RSVFULL                                                       
RSV8     CLC   0(7,R6),22(R2)      IS THIS FILE THERE                           
         BE    RSVWAIT             YES - WAIT                                   
         LA    R6,7(R6)                                                         
         CLI   0(R6),X'FF'         TEST E-O-L                                   
         BNE   RSV8                NO                                           
*                                                                               
RSV10    BCT   R0,RSV6                                                          
* WEVE GOT IT                                                                   
         MVCOM 18,5,RSVID                                                       
*                                                                               
RSVX     CLI   RSVSW,0             SET CC EQ IF NO WAIT OCCURED                 
         XMOD1 1                                                                
RSVSW    DC    X'00'                                                            
         EJECT                                                                  
RSVWAIT  MVI   RSVSW,1             INDICATE WAIT OCCURRED                       
*                                                                               
         SETIME 30,RSVTECB                                                      
         WAIT  RSVTECB                                                          
         B     RSV5                                                             
         SPACE 2                                                                
BADCPU   DS    0H                                                               
         L     RF,=V(LOGIO)                                                     
         LTR   RF,RF                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         GOTO1 (RF),P1,=C'** WORKER ** WRONG CPU ID - ABORT'                    
         CANCEL                                                                 
         SPACE 2                                                                
*                                                                               
RSVFULL  DS    F                                                                
RSVID    DC    C'WR',AL3(RSVTAB)                                                
*                                                                               
RSVTAB   DC    5XL7'00'                                                         
         DC    X'FF'                                                            
MYCOM    DC    H'0'                                                             
RSVTECB  TECB                                                                   
         LTORG                                                                  
         EJECT                                                                  
RELEASE  CSECT                                                                  
         NMOD1 0,**RLSE**                                                       
*                                                                               
         L     R6,=A(RSVTAB)                                                    
REL2     CLC   0(7,R6),22(R2)      R2 HAS DTF ADDRESS                           
         BE    REL4                                                             
         LA    R6,7(R6)                                                         
         CLI   0(R6),X'FF'                                                      
         BNE   REL2                                                             
         DC    H'0'                                                             
REL4     CLI   7(R6),X'FF'                                                      
         BE    REL6                                                             
         MVC   0(7,R6),7(R6)                                                    
         LA    R6,7(R6)                                                         
         B     REL4                                                             
REL6     XC    0(7,R6),0(R6)                                                    
RELX     XMOD1 1                                                                
         LTORG                                                                  
         TITLE 'WORKER - WORK AREAS'                                            
       ++INCLUDE DDWRKFILED                                                     
         EJECT                                                                  
WORKD    DSECT                                                                  
*                                                                               
P1       DS    F                                                                
P2       DS    F                                                                
P3       DS    F                                                                
P4       DS    F                                                                
P5       DS    F                                                                
P6       DS    F                                                                
P7       DS    F                                                                
P8       DS    F                                                                
W1       DS    F                                                                
W2       DS    F                                                                
W3       DS    F                                                                
W4       DS    F                                                                
W5       DS    F                                                                
W6       DS    F                                                                
W7       DS    F                                                                
W8       DS    F                                                                
*                                                                               
DUB      DS    D                                                                
FULL     DS    F                                                                
WORK     DS    CL24                                                             
         SPACE 2                                                                
DMCBD    DSECT                                                                  
DM1      DS    A                   A(=C'COMMAND)                                
DM2      DS    A                   A(DTF)                                       
DM3      DS    A                   A(ID)                                        
DM4      DS    A                   A(I/O AREA)                                  
DM5      DS    A                                                                
DM6      DS    A                                                                
         SPACE 2                                                                
WRKTKWRK CSECT                                                                  
         DS    1000C                                                            
 END                                                                            
