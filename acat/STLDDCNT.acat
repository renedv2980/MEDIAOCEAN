*          DATA SET STLDDCNT   AT LEVEL 007 AS OF 06/04/20                      
*CATALP STLDDCNT                                                                
         TITLE 'LDCOUNT - STATION - REC TYPE COUNT/PRINT'                       
*        PARAMS VIA R1                                                          
*        XL1   COUNT/PRINT  X'00'=COUNT  X'FF'=PRINT                            
*              RETURN X'FF' TO INHIBIT LOAD OF RECORD                           
*        AL3   A(RECORD)                                                        
*        AL4   A(FILE NAME)                                                     
*                                                                               
         PRINT NOGEN                                                            
LDCOUNT  CSECT                                                                  
         NMOD1 0,STLDDCNT                                                       
         L     R3,0(R1)            POINT TO RECORD                              
         LR    R2,R1               SAVE PARAM PTR                               
*                                                                               
FIRST    NOP   FIRSTX                                                           
         OI    FIRST+1,X'F0'                                                    
         L     RE,=V(UPSIVAL)                                                   
         MVC   UPSI,0(RE)                                                       
FIRSTX   DS    0H                                                               
*                                                                               
         CLI   0(R1),X'FF'                                                      
         JE    PRINT                                                            
*                                                                               
TALLY    CLI   0(R3),C'S'          STATION                                      
         JE    ST1                                                              
         CLI   0(R3),C'A'          ADDRESS                                      
         JE    AD1                                                              
         CLI   0(R3),C'R'          REP                                          
         JE    RP1                                                              
         CLI   0(R3),C'B'          REP NAME                                     
         JE    RN1                                                              
         CLI   0(R3),C'M'          MARKET                                       
         JE    MK1                                                              
         CLI   0(R3),C'N'          PASSIVE PTR - MARKET/STATION                 
         JE    MS1                                                              
         CLI   0(R3),C'L'          PASSIVE PTR - ALPHA/NUMERIC MKT              
         JE    AN1                                                              
         CLI   0(R3),C'F'          PASSIVE PTR - FORMAT                         
         JE    FT1                                                              
         CLI   0(R3),C'Y'          CABLE INFORMATION                            
         JE    CB1                                                              
         CLI   0(R3),C'K'          PASSIVE PTR - MARKET/STATIONS                
         JE    KN1                                                              
         CLI   0(R3),C'X'          PASSIVE KEY FOR CANADA TV/NET                
         JE    TN1                                                              
*                                                                               
* THIS DON'T EXIST ON STATION FILE                                              
         CLI   0(R3),0             CONTROL                                      
         JE    CT1                                                              
         CLI   0(R3),255           CONTROL                                      
         JE    CT1                                                              
*                                                                               
         CLC   LASTUNK,0(R3)       DID WE SEE THIS TYPE ALREADY?                
         JE    UK1                 YES, DON'T NEED TO PRINT IT AGAIN            
*                                                                               
         MVC   LASTUNK,0(R3)       ELSE SAVE IT NOW AND PRINT IT                
         L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
         MVC   P(16),=CL16'UNKNOWN KEY: '                                       
         GOTO1 =V(HEXOUT),DMCB,(R3),P+16,15                                     
         GOTO1 =V(PRINTER)                                                      
         J     UK1                 MUST BE UNKNOWN                              
         DROP  RA                                                               
*                                                                               
LASTUNK  DC    X'00'               LAST UNKNOWN KEY TYPE                        
*                                                                               
CT1      LA    RF,CT                                                            
         J     BUMP                                                             
ST1      LA    RF,ST                                                            
         J     BUMP                                                             
AD1      LA    RF,AD                                                            
         J     BUMP                                                             
RP1      LA    RF,RP                                                            
         J     BUMP                                                             
RN1      LA    RF,RN                                                            
         J     BUMP                                                             
MK1      LA    RF,MK                                                            
         J     BUMP                                                             
MS1      LA    RF,MS                                                            
         J     BUMP                                                             
AN1      LA    RF,AN                                                            
         J     BUMP                                                             
CB1      LA    RF,CB                                                            
         J     BUMP                                                             
FT1      LA    RF,FT                                                            
         J     BUMP                                                             
UK1      LA    RF,UK                                                            
         J     BUMP                                                             
KN1      LA    RF,KN                                                            
         J     BUMP                                                             
TN1      LA    RF,TN                                                            
         J     BUMP                                                             
*                                                                               
BUMP     L     RE,0(RF)                                                         
         LA    RE,1(RE)            BUMP COUNTER                                 
         ST    RE,0(RF)                                                         
*                                                                               
         TM    17(R3),X'80'        DELETED                                      
         BZ    XIT                 NO                                           
         L     RE,4(RF)                                                         
         LA    RE,1(RE)                                                         
         ST    RE,4(RF)                                                         
         TM    UPSI,X'01'          PURGE DELETES                                
         BZ    *+8                 NO                                           
         MVI   0(R2),255           SET PURGE RETURN                             
*                                                                               
XIT      XMOD1 1                                                                
         EJECT                                                                  
PRINT    L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
         ZAP   LINE,=P'99'                                                      
         L     RE,4(R1)                                                         
         MVC   HL+8(8),0(RE)       FILE NAME                                    
*                                                                               
         LA    RE,HLL                                                           
         LA    RF,HL                                                            
         BAS   R9,PUT              HEADLINE                                     
         LA    RE,ULL                                                           
         LA    RF,UL                                                            
         BAS   R9,PUT              UNDERLINE                                    
*                                                                               
         SR    R4,R4               TOTAL                                        
         SR    R5,R5               DELETES                                      
         LA    R2,NUMENTQ          LOOP THROUGH # OF ENTRIES                    
         LA    R3,STARTENT                                                      
         USING RECTABD,R3                                                       
PR1      BAS   R8,PTOT             PRINT A LINE                                 
         LA    R3,UKLQ(R3)         BUMP TO NEXT ENTRY                           
         BCT   R2,PR1              LOOP                                         
*                                                                               
         ST    R4,0(R3)            TOTAL RECS                                   
         ST    R5,4(R3)            TOTAL DELS                                   
         LA    RE,ULL                                                           
         LA    RF,UL                                                            
         BAS   R9,PUT                                                           
         BAS   R8,PTOT                                                          
         LA    RE,ULL                                                           
         LA    RF,UL                                                            
         BAS   R9,PUT                                                           
         B     XIT                                                              
*                                                                               
PTOT     A     R4,RECTCNTR         ADD TO TOT                                   
         A     R5,RECTDLTD         ADD TO DEL                                   
*                                                                               
         L     RE,RECTCNTR                                                      
         BAS   RF,CVD                                                           
         MVC   DCNT,CN                                                          
*                                                                               
         L     RE,RECTDLTD                                                      
         BAS   RF,CVD                                                           
         MVC   DDEL,CN                                                          
*                                                                               
         MVC   DL,RECTDESC         MOVE REC NAME                                
         MVC   DPBOOK,RECTPBOK                                                  
         MVC   DDSECT,RECTDSCT                                                  
*                                                                               
         LA    RE,DLL                                                           
         LA    RF,DL                                                            
         BAS   R9,PUT                                                           
         BR    R8                                                               
         DROP  R3                                                               
*                                                                               
CVD      CVD   RE,PK                                                            
         UNPK  CN,PK                                                            
         OI    CN+9,X'F0'                                                       
         CLI   CN,C'0'                                                          
         BNER  RF                                                               
         MVI   CN,C' '                                                          
         BR    RF                                                               
         EJECT                                                                  
PUT      STM   RE,RF,DUB                                                        
         MVC   P,SPACES                                                         
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   P(0),0(RF)                                                       
         GOTO1 =V(PRINTER)                                                      
         LM    RE,RF,DUB                                                        
PUTX     BR    R9                                                               
*                                                                               
DUB      DS    D                                                                
PK       DS    D                                                                
CN       DS    CL10                                                             
*                                                                               
STARTENT DS    0F                                                               
CT       DC    F'0',F'0',CL16'CONTROL'             X'00' OR X'FF'               
         DC    CL10'          ',CL10'          '                                
ST       DC    F'0',F'0',CL16'STATION MASTER'      C'S'                         
         DC    CL10'SPGENSTA  ',CL10'STAREC    '                                
AD       DC    F'0',F'0',CL16'ADDRESS'             C'A'                         
         DC    CL10'SPGENADD  ',CL10'ADDRREC   '                                
RP       DC    F'0',F'0',CL16'REPRESENTATIVE'      C'R'                         
         DC    CL10'SPGENREP  ',CL10'REPREC    '                                
RN       DC    F'0',F'0',CL16'REPNAME RECS'        C'B'                         
         DC    CL10'SPGENREP  ',CL10'REBKEY    '                                
MK       DC    F'0',F'0',CL16'MARKET'              C'M'                         
         DC    CL10'SPGENMKT  ',CL10'MKTREC    '                                
CB       DC    F'0',F'0',CL16'CABLE'               C'Y'                         
         DC    CL10'SPGENCBL  ',CL10'CBLREC    '                                
MS       DC    F'0',F'0',CL16'N PASSV MKT/STA '    C'N'                         
         DC    CL10'SPGENSTA  ',CL10'STNKTYPE  '                                
AN       DC    F'0',F'0',CL16'ALPHA/NUM MKT'       C'L'                         
         DC    CL10'SPGENANMK ',CL10'ANMRECD   '                                
FT       DC    F'0',F'0',CL16'FORMAT'              C'F'                         
         DC    CL10'SPGENSTA  ',CL10'STFKTYPE  '                                
KN       DC    F'0',F'0',CL16'K PASSV MKT/STA '    C'K'                         
         DC    CL10'SPGENSTA  ',CL10'STKKTYPE  '                                
TN       DC    F'0',F'0',CL16'PASSV CAN TV/NET'    C'X'                         
         DC    CL10'SPGENSTA  ',CL10'STXKTYPE  '                                
UK       DC    F'0',F'0',CL16'UNKNOWN'                                          
         DC    CL10'          ',CL10'          '                                
UKLQ     EQU   *-UK                                                             
NUMENTQ  EQU   (*-CT)/UKLQ        NUMBER OF RECORD TYPES                        
TO       DC    F'0',F'0',CL16'TOTALS'                                           
*                                                                               
HL       DC    CL37'REC TYP XXXXXXX  COUNT      DELETES  '                      
HLL      EQU   *-HL                                                             
UL       DC    CL37'---------------  ---------  ---------'                      
ULL      EQU   *-UL                                                             
*                                                                               
DL       DC    CL15' '                                                          
         DC    CL1' '                                                           
DCNT     DC    CL10' '                                                          
         DC    CL1' '                                                           
DDEL     DC    CL10' '                                                          
         DC    CL1' '                                                           
DPBOOK   DC    CL10' '                                                          
         DC    CL1' '                                                           
DDSECT   DC    CL10' '                                                          
DLL      EQU   *-DL                                                             
*                                                                               
UPSI     DC    X'00'                                                            
*                                                                               
         LTORG                                                                  
DMCB     DS    6F                                                               
         EJECT                                                                  
RECTABD  DSECT                                                                  
RECTCNTR DS    F                    COUNT OF THIS RECORD TYPE                   
RECTDLTD DS    F                    COUNT OF DELETED FOR THIS RECORD            
RECTDESC DS    CL16                 DESCRIPTION                                 
RECTPBOK DS    CL10                 PANBOOK                                     
RECTDSCT DS    CL10                 DESCT                                       
         DS    0F                                                               
RECTABLQ EQU   *-RECTABD                                                        
         EJECT                                                                  
*DDDPRINT                                                                       
       ++INCLUDE DDDPRINT                                                       
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'007STLDDCNT  06/04/20'                                      
         END                                                                    
