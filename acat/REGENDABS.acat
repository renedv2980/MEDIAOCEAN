*          DATA SET REGENDABS  AT LEVEL 061 AS OF 09/11/98                      
*CATALP REGENDAB                                                                
***********************************************************************         
*                                                                     *         
*         REGENDAB --- DARE BUY LIST ROUTINES                         *         
*                                                                     *         
* ------------------------------------------------------------------- *         
* UPDATE HISTORY:                                                     *         
*                                                                     *         
* 02JUN94 (SKU) --- DATE OF BIRTH                                     *         
*                                                                     *         
* 26JAN95 (SKU) --- INCREASE MAX BUY COMMENT LENGTH TO 70             *         
*                   SUPPORT DAILY BUY                                 *         
*                   DISPLAY SINGLE TIME FOR IDENTICAL START-END TIMES *         
*                                                                     *         
* 07APR95 (SKU) --- SPECIFY BUYS MARKED DELETED AS 'C' AND REMOVE     *         
*                   NPW/RATE/SPT                                      *         
*                                                                     *         
* 22JUL96 (SKU) --- FIX OOWR BUG                                      *         
*                                                                     *         
* 02OCT97 (SKU) --- ACCEPT ZERO SPOT AGENCY BUYS                      *         
*                                                                     *         
* 12NOV97 (JRD) --- TAKE OVER CHOPPING                                *         
*                                                                     *         
* 17FEB98 (JRD) --- MG PRINTING                                       *         
*                                                                     *         
* 05MAY98 (SKU) --- SUPPORT AGENCY DEMOS                              *         
*                                                                     *         
* 11SEP98 (SKU) --- TAKE OVER BUG FIX                                 *         
*                                                                     *         
***********************************************************************         
*  INPUT:                                                                       
*              PARAMETER 1 = BYTE 1 - X'80' PRINT OPTION                        
*                                     PROCESS ONLY ONE BUY PER PASS             
*                                     X'40' PROCESS TKO'S                       
*                                     X'20' SHOW MG INDICATOR                   
*                                     X'10' DON'T PRINT DEMOS                   
*                          = BYTES 2-4 - A(AGENCY DARE ORDER KEY)               
*                        2 = BYTE 1 - MAX ROW AT 1ST BYTE                       
*                            BYTES 2-4 - A(BUY LIST AREA)                       
*                              ASSUMES MULTIPLES OF 81 COLUMNS                  
*                        3 = BYTE 1 - BUYLINE NUMBER TO START                   
*                            BYTES 2-4 - A(IOAREA)                              
*                        4 = A(ROUTINE ADDRESS BLOCK)                           
*                                  WORD 1 = A(COMFACS)                          
*                                  WORD 2 = A(OUTDAY)                           
*                                  WORD 3 = A(UNTIME)                           
*                                  WORD 4 = A(DEMOCON)                          
*                        5 = A(TAKEOVER BLOCK)                                  
*                                 A(REDARTKO)                                   
*                                 A(PERVERT)                                    
*                                 XL3 TKODATE (YMD BINARY)                      
*  OUTPUT:                                                                      
*              PARAMETER 3 = BYTE 1 - NUMBER OF ROWS USED                       
*                            BYTE 2 - NEXT BUY NUMBER OR X'00' NO MORE          
*                            BYTE 3 - FOR PRINT, X'08' IF BUY IS DAILY          
***********************************************************************         
         TITLE 'REGENDAB - DARE BUY LIST ROUTINES'                              
REGENDAB CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,REGENDAB                                             
         USING WORKD,RC                                                         
         ST    R1,SVDMCB                                                        
                                                                                
         MVC   PRINTOPT,0(R1)      FIRST BYTE IS PRINT OPTION                   
                                                                                
         L     R6,0(R1)                                                         
         ST    R6,ABUYKEY                                                       
         MVC   BUYKEY,0(R6)                                                     
                                                                                
         MVC   BLISTNUM,4(R1)      FIRST BYTE IS MAX ROW                        
         L     R4,4(R1)                                                         
         USING LBUYD,R4                                                         
         LR    RE,R4                                                            
         ZIC   RF,BLISTNUM                                                      
         MH    RF,=Y(LBUYLENQ)                                                  
         XCEF                                                                   
                                                                                
         MVC   BBUYNUM,8(R1)       FIRST BYTE IS START BUY NUMBER               
         MVC   AIOAREA,8(R1)                                                    
         L     R6,12(R1)                                                        
         MVC   VCOMFACS(16),0(R6)                                               
                                                                                
         L     RE,16(R1)                                                        
         MVC   VREDARTK(11),0(RE)                                               
                                                                                
         MVC   DATADISP,=H'34'                                                  
         MVI   DMINBTS,0           DO NOT PASS DELETED RECORDS                  
*                                                                               
         BAS   RE,INIT             SET ADDRESSES                                
*                                                                               
         B     DISPAGE                                                          
*                                                                               
EXIT     DS    0H                                                               
         XMOD1                                                                  
         EJECT                                                                  
***********************************************************************         
* SET ADDRESSES                                                                 
* RETRIEVE DEMO VALUES, IF ANY                                                  
***********************************************************************         
INIT     NTR1                                                                   
         L     R6,VCOMFACS                                                      
         USING COMFACSD,R6                                                      
         MVC   VDATAMGR,CDATAMGR                                                
         MVC   VDATCON,CDATCON                                                  
         MVC   VADDAY,CADDAY                                                    
         MVC   VGETDAY,CGETDAY                                                  
         DROP  R6                                                               
*                                                                               
* RETRIEVE DEMO CATEGORIES FROM HEADER RECORD                                   
*                                                                               
         XC    DEMOCATS,DEMOCATS                                                
         MVC   KEY(L'RDARKEY),BUYKEY                                            
         LA    R6,KEY                                                           
         USING RDARKEY,R6                                                       
         XC    RDARKRT(3),RDARKRT                                               
         MVI   RDARKRT,X'10'                                                    
         DROP  R6                                                               
*                                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(L'RDARKEY),KEYSAVE                                           
         BNE   DISPAGEX                                                         
*                                                                               
         GOTO1 GETREC                                                           
*                                                                               
         L     R6,AIOAREA                                                       
         USING RDARREC,R6                                                       
         CLC   =C'$EDI$',RDARAGAD  EDI DOES NOT SUPPORT DEMOS                   
         BE    INITX                                                            
         DROP  R6                                                               
*                                                                               
         MVI   ELCODE,X'03'        GET DEMO CATEGORIES                          
         BAS   RE,GETEL                                                         
         BNE   INITX                                                            
         USING RDARDMEL,R6                                                      
         CLC   RDARDEM1(16),MYSPACES                                            
         BE    INITX                                                            
*                                                                               
         LA    R3,1                                                             
         LA    R6,RDARDEM1                                                      
         LA    R4,WORK                                                          
         XC    WORK,WORK                                                        
         DROP  R6                                                               
*                                                                               
INIT10   DS    0H                                                               
         MVC   1(1,R4),0(R6)                                                    
         CLI   1(R4),C'T'          FUDGE FOR DEMOCON                            
         BNE   *+8                                                              
         MVI   1(R4),C'I'                                                       
         PACK  DUB(8),1(3,R6)                                                   
         CVB   RF,DUB                                                           
         STC   RF,2(R4)                                                         
         LA    R6,L'RDARDEM1(R6)                                                
         LA    R4,3(R4)                                                         
         CLC   0(4,R6),MYSPACES                                                 
         BE    INIT20                                                           
         AHI   R3,1                                                             
         CHI   R3,4                                                             
         BNH   INIT10                                                           
*                                                                               
INIT20   DS    0H                                                               
         L     R6,AIOAREA                                                       
         USING DBLOCKD,R6                                                       
         XC    DBLOCK,DBLOCK                                                    
         MVC   DBFILE,=C'INV'                                                   
         MVC   DBCOMFCS,VCOMFACS                                                
         MVI   DBSELMED,C'T'                                                    
                                                                                
         GOTO1 VDEMOCON,DMCB,((R3),WORK),(2,DEMOCATS),(0,DBLOCKD)               
         DROP  R6                                                               
*                                                                               
INITX    DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* DISPLAY ONE PAGE OF BUYS                                                      
***********************************************************************         
DISPAGE  DS    0H                                                               
         MVC   KEY(L'RDARKEY),BUYKEY                                            
         XC    BROWUSED,BROWUSED                                                
         XC    LBUYMC(LBUYLENQ),LBUYMC                                          
         XC    BUYDAILY,BUYDAILY   CLEAR RETURN STATUS BYTE                     
                                                                                
         GOTO1 HIGH                                                             
         CLC   KEY(RDARKSEQ-RDARKEY),KEYSAVE                                    
         BNE   DISPAGEX                                                         
                                                                                
DISP10   DS    0H                                                               
         XC    SPTCOST,SPTCOST                                                  
         XC    BUYORBIT(204),BUYORBIT                                           
         XC    ORBITSTT,ORBITSTT                                                
         XC    ORBITEND,ORBITEND                                                
         XC    BSTRTDAY,BSTRTDAY                                                
         XC    BENDDAY,BENDDAY                                                  
         XC    BSTATUS,BSTATUS     CLEAR STATUS BYTE                            
                                                                                
         LA    R6,KEY                                                           
         USING RDARKEY,R6                                                       
         OC    RDARKSRT,RDARKSRT                                                
         BNZ   DISPAGEX            MUST BE BUY HEADER                           
         DROP  R6                                                               
                                                                                
         GOTO1 GETREC                                                           
                                                                                
         L     R6,AIOAREA                                                       
         MVI   ELCODE,X'01'        HEADER DESCRIPTION ELEMENT                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         USING RDARBYEL,R6                                                      
         TM    PRINTOPT,X'01'      IF PRINT, IS THIS JUST TO GET NEXT           
         BO    DISP15               BUY #?                                      
         CLI   RDARBYLN,RDARBYOL                                                
         BNH   DISP15                                                           
         TM    RDARBYDL,X'40'      SKIP FOR HARD DELETE                         
         BZ    DISP15                                                           
         MVI   KEY+RDARKSRT-RDARKEY,X'FF'                                       
         GOTO1 HIGH                                                             
         CLC   KEY(RDARKSEQ-RDARKEY),KEYSAVE                                    
         BE    DISP13                                                           
         MVI   BBUYNUM,0           NO MORE BUYS TO DISPLAY                      
         B     DISPAGEX                                                         
                                                                                
DISP13   DS    0H                                                               
         L     RF,ABUYKEY          UPDATE BUYKEY IN CALLER                      
         MVC   0(L'BUYKEY,RF),KEY                                               
         B     DISP10                                                           
                                                                                
DISP15   DS    0H                                                               
         MVC   BUYHEAD,RDARBYEL    SAVE OFF BUY HEADER X'01' ELEMENT            
         DROP  R6                                                               
                                                                                
         XC    BUYDEMOS,BUYDEMOS                                                
         L     R6,AIOAREA                                                       
         MVI   ELCODE,X'20'        BUY DEMO VALUES                              
         BAS   RE,GETEL                                                         
         BNE   DISP18                                                           
         USING RDARBMEL,R6                                                      
         MVC   BUYDEMOS,RDARBDM1   SAVE OFF BUY DEMO VALUES                     
         DROP  R6                                                               
                                                                                
DISP18   DS    0H                                                               
         NI    BSTATUS,X'FF'-BSTMGOOD                                           
                                                                                
         L     R6,AIOAREA                                                       
         MVI   ELCODE,X'10'        REVISION ELEMENT                             
         BAS   RE,GETEL                                                         
         BNE   DISP20                                                           
                                                                                
         USING RDARRVEL,R6                                                      
         CLI   RDARRVBY,0          MAKE GOOD?                                   
         BE    *+14                NO                                           
         OI    BSTATUS,BSTMGOOD                                                 
         MVC   MGLINK,RDARRVBY                                                  
         DROP  R6                                                               
                                                                                
DISP20   DS    0H                                                               
         GOTO1 SEQ                                                              
         LA    R6,KEY                                                           
         USING RDARKEY,R6                                                       
         CLC   KEY(RDARKSRT-RDARKEY),KEYSAVE                                    
         BNE   DISP300             FOR THE SAME BUYLINE, CHECK:                 
         CLI   RDARKSRT,X'10'      IF ORBIT FOUND, PROCESS                      
         BE    DISP100                                                          
         CLI   RDARKSRT,X'20'      IF BUY COMMENT FOUND, SKIP FOR NOW           
         BE    DISP20                                                           
         CLI   RDARKSRT,X'30'      IF DETAIL, PROCESS                           
         BE    DISP200                                                          
         B     DISP300             IF NONE OF THE ABOVE, GET NEXT               
         DROP  R6                  BUYLINE                                      
         EJECT                                                                  
**********************************************************************          
* PROCESS BUY ORBITS                                                            
**********************************************************************          
DISP100  DS    0H                  PROCESS ORBITS                               
         GOTO1 GETREC                                                           
                                                                                
         L     R6,AIOAREA                                                       
         USING RDAROEEL,R6                                                      
         MVI   ELCODE,X'02'        GET ORBIT ELEMENT                            
         BAS   RE,GETEL                                                         
         BNE   DISP20              NONE FOUND                                   
                                                                                
         LA    R3,BUYORBIT         SAVE ORBIT INFO IN ARRAY                     
DISP110  MVC   0(L'BUYORBIT,R3),RDAROERO                                        
         LA    R3,L'BUYORBIT(R3)                                                
         MVC   BUYROTAT(12),RDAROERO                                            
         XC    BUYSTEN,BUYSTEN                                                  
         XC    BSTRTDAY,BSTRTDAY                                                
         XC    BENDDAY,BENDDAY                                                  
         GOTO1 STARTEND,DMCB,BUYROTAT,BUYSTEN                                   
         OC    ORBITSTT,ORBITSTT                                                
         BNZ   *+10                                                             
         MVC   ORBITSTT,BSTRTDAY    FIRST ORBIT FOR EFFECTIVE DATE CAL          
         BAS   RE,NEXTEL                                                        
         BE    DISP110                                                          
         MVC   ORBITEND,BENDDAY     LAST ORBIT FOR EFFECTIVE DATE CAL           
         B     DISP20                                                           
         DROP  R6                                                               
         EJECT                                                                  
**********************************************************************          
* PROCESS BUY DETAILS                                                           
**********************************************************************          
DISP200  DS    0H                  PROCESS BUY DETAILS                          
         GOTO1 GETREC                                                           
         L     R6,AIOAREA                                                       
         USING RDARKEY,R6          BUYLINE NUMBER                               
         MVC   BBUYNUM,RDARKSEQ                                                 
                                                                                
         TM    PRINTOPT,X'01'      IF PRINT, IS THIS JUST TO GET NEXT           
         BO    DISPAGEX             BUY #?                                      
                                                                                
         EDIT  RDARKSEQ,(3,LBUYLINE)                                            
         DROP  R6                                                               
                                                                                
         TM    PRINTOPT,X'20'      SHOW MG INDICATOR?                           
         BZ    DISP202             NO                                           
         TM    BSTATUS,BSTMGOOD    MAKEGOOD LINE?                               
         BZ    DISP202             NO                                           
                                                                                
         MVC   LBUYMGI,=C'MG'                                                   
         EDIT  (B1,MGLINK),LBUYMGL,ALIGN=LEFT,ZERO=NOBLANK                      
                                                                                
DISP202  DS    0H                                                               
         MVC   BBUYSTRW,BROWUSED                                                
                                                                                
         NI    BSTATUS,X'FF'-BSTDAILY                                           
         L     R6,AIOAREA                                                       
         MVI   ELCODE,X'01'        GET DESCRIPTION ELEMENT                      
         BAS   RE,GETEL                                                         
         BNE   DISP205             NONE FOUND, NEXT BUYLINE                     
         USING RDARBDEL,R6                                                      
         TM    RDARBDFL,X'80'      FLAG STATUS IF DAILY BUY                     
         BZ    DISP205                                                          
         OI    BSTATUS,BSTDAILY                                                 
         OI    BUYDAILY,BSTDAILY                                                
         DROP  R6                                                               
                                                                                
DISP205  DS    0H                                                               
         L     R6,AIOAREA                                                       
         MVI   ELCODE,X'02'        GET DETAIL ELEMENT                           
         BAS   RE,GETEL                                                         
         BNE   DISP300             NONE FOUND, NEXT BUYLINE                     
                                                                                
         LA    R3,BUYORBIT                                                      
                                                                                
DISP210  DS    0H                                                               
         XC    BSTRTDAY,BSTRTDAY                                                
         XC    BENDDAY,BENDDAY                                                  
         LA    RF,BUYHEAD          DEFAULT TO USE BUY HEADER DATA               
         USING RDARBYEL,RF                                                      
         MVC   BUYROTAT(12),RDARBYRO                                            
         DROP  RF                                                               
                                                                                
         OC    0(L'BUYORBIT,R3),0(R3)                                           
         BZ    DISP220             IF ORBIT INFO PRESENT, USE IT                
                                                                                
DISP215  DS    0H                                                               
         MVC   BUYROTAT(12),0(R3)                                               
         GOTO1 BUYROT,DMCB,BUYROTAT                                             
         XC    BUYSTEN,BUYSTEN                                                  
         XC    BSTRTDAY,BSTRTDAY                                                
         XC    BENDDAY,BENDDAY                                                  
         GOTO1 STARTEND,DMCB,BUYROTAT,BUYSTEN                                   
         B     DISP260                                                          
                                                                                
DISP220  DS    0H                                                               
         TM    BSTATUS,BSTDAILY                                                 
         BO    DISP260                                                          
         GOTO1 BUYROT,DMCB,BUYROTAT                                             
         XC    BUYSTEN,BUYSTEN                                                  
         GOTO1 STARTEND,DMCB,BUYROTAT,BUYSTEN                                   
                                                                                
DISP260  DS    0H                                                               
         TM    PRINTOPT,X'40'      NEED TKO PROCESSING?                         
         BNO   DISP270             NO                                           
*                                                                               
         MVC   DMCB(4),AIOAREA     SETUP CHOPPING CALL                          
         MVC   DMCB+4(3),TKODATE                                                
         MVC   DMCB+8(1),BSTRTDAY                                               
         MVC   DMCB+9(1),BENDDAY                                                
         MVC   DMCB+10(1),BRTSTDAY                                              
         MVC   DMCB+11(1),ORBITSTT                                              
         XC    WORK,WORK                                                        
         MVC   WORK(4),VDATCON                                                  
         MVC   WORK+4(4),VGETDAY                                                
         MVC   WORK+8(4),VADDAY                                                 
         MVC   WORK+12(4),VPERVERT                                              
         LA    RE,WORK                                                          
         ST    RE,DMCB+12                                                       
*                                                                               
         GOTO1 VREDARTK,DMCB                                                    
         BNE   DISP300             BUY DELETED, EXIT                            
*                                                                               
         CLI   0(R6),X'02'                                                      
         BE    DISP270                                                          
         L     R6,AIOAREA                                                       
         MVI   ELCODE,X'02'        GET DETAIL ELEMENT                           
         BAS   RE,GETEL                                                         
         BE    DISP270             NONE FOUND, NEXT BUYLINE                     
         DC    H'0'                                                             
                                                                                
DISP270  DS    0H                                                               
         GOTO1 BUYLINE             SEND A BUYLINE TO OUTPUT                     
                                                                                
         ZIC   RF,BROWUSED         NEXT PRINT LINE                              
         LA    RF,1(RF)                                                         
         STC   RF,BROWUSED                                                      
         CLC   BROWUSED,BLISTNUM   NEW LINE EXCEEDED DISPLAY LINES              
         BL    DISP280                                                          
         ZIC   RF,BROWUSED                                                      
         BCTR  RF,0                                                             
         STC   RF,BROWUSED                                                      
         TM    BSTATUS,BSTSAME     SAME BUYLINE?                                
         BZ    DISPAGEX            NO, NEW BUYLINE SO DON'T SHOW                
         CLI   BLISTNUM,15         **** FOR DISPLAY ONLY                        
         BNL   *+14                                                             
         MVC   BROWUSED,BBUYSTRW   REST OF BUY DIDN'T FIT                       
         B     DISPAGEX                                                         
                                                                                
*****                                                                           
***** WE SHOULD CONTINUE FOR PRINT, SET FLAG FOR DISPLAY                        
*****                                                                           
         OI    PRINTOPT,X'01'      GET NEXT BUY NUM AND EXIT                    
         B     DISP310                                                          
                                                                                
DISP280  DS    0H                                                               
         LA    R4,81(R4)                                                        
                                                                                
         OI    BSTATUS,BSTSAME     SAME BUYLINE                                 
         LA    R3,L'BUYORBIT(R3)                                                
         BAS   RE,NEXTEL                                                        
         BE    DISP210                                                          
         OI    BSTATUS,BSTORBIT    IF NO MORE BUYLINES BUT                      
         OC    0(L'BUYORBIT,R3),0(R3) ORBIT INFO PRESENT, DISPLAY DAY           
         BNZ   DISP215             AND TIMES ONLY                               
                                                                                
* SHOW PROGRAM NAME                                                             
         LA    RF,BUYHEAD                                                       
         USING RDARBYEL,RF                                                      
         CLC   RDARBYPN,MYSPACES                                                
         BE    DISP290                                                          
         MVC   9(2,R4),=C'P='                                                   
         MVC   11(L'RDARBYPN,R4),RDARBYPN                                       
         DROP  RF                                                               
                                                                                
         LA    R4,81(R4)                                                        
                                                                                
         ZIC   RF,BROWUSED         NEXT PRINT LINE                              
         LA    RF,1(RF)                                                         
         STC   RF,BROWUSED                                                      
                                                                                
         CLC   BROWUSED,BLISTNUM                                                
         BNH   DISP290                                                          
*                                                                               
* INSERT A BETTER CHECK HERE !!!!!!!!!!!!!                                      
*                                                                               
         TM    BSTATUS,BSTSAME     SAME BUYLINE?                                
         BZ    DISPAGEX            NO, NEW BUYLINE SO DON'T SHOW                
         CLI   BLISTNUM,15         **** FOR DISPLAY ONLY                        
         BNL   DISPAGEX                                                         
         MVC   BROWUSED,BBUYSTRW   REST OF BUY DIDN'T FIT                       
         B     DISPAGEX                                                         
                                                                                
* SHOW BUY COMMENTS                                                             
DISP290  DS    0H                                                               
         BAS   RE,GETCOMMT                                                      
         CLC   BROWUSED,BLISTNUM                                                
         BNH   DISP295                                                          
         TM    BSTATUS,BSTSAME     SAME BUYLINE?                                
         BZ    DISPAGEX            NO, NEW BUYLINE SO DON'T SHOW                
         CLI   BLISTNUM,15         **** FOR DISPLAY ONLY                        
         BH    DISPAGEX                                                         
         MVC   BROWUSED,BBUYSTRW   REST OF BUY DIDN'T FIT                       
         B     DISPAGEX                                                         
                                                                                
* SHOW DEMO VALUES                                                              
DISP295  DS    0H                                                               
         TM    PRINTOPT,X'10'      OPTION TO PRINT DEMO VALUES??                
         BNZ   DISP300                                                          
         BAS   RE,DISDEMO                                                       
         ZIC   RF,BROWUSED         NEXT PRINT LINE                              
         LA    RF,1(RF)                                                         
         STC   RF,BROWUSED                                                      
         CLC   BROWUSED,BLISTNUM                                                
         BNH   DISP300                                                          
         TM    BSTATUS,BSTSAME     SAME BUYLINE?                                
         BZ    DISPAGEX            NO, NEW BUYLINE SO DON'T SHOW                
         CLI   BLISTNUM,15         **** FOR DISPLAY ONLY                        
         BH    DISPAGEX                                                         
         MVC   BROWUSED,BBUYSTRW   REST OF BUY DIDN'T FIT                       
         B     DISPAGEX                                                         
                                                                                
DISP300  DS    0H                                                               
         TM    PRINTOPT,X'80'      ARE WE PRINTING?                             
         BZ    DISP310                                                          
         OI    PRINTOPT,X'01'      GET NEXT BUY# AND EXIT                       
                                                                                
DISP310  DS    0H                                                               
         GOTO1 SEQ                 GET NEXT BUYLINE                             
         CLC   KEY(RDARKSEQ-RDARKEY),KEYSAVE                                    
         BE    DISP10                                                           
         MVI   BBUYNUM,0           NO MORE BUYS TO DISPLAY                      
                                                                                
DISPAGEX DS    0H                                                               
         L     R1,SVDMCB                                                        
         XC    8(4,R1),8(R1)                                                    
         MVC   8(1,R1),BROWUSED                                                 
         MVC   9(1,R1),BBUYNUM                                                  
         TM    BUYDAILY,BSTDAILY   RETURN IF DAILY FLAG                         
         BZ    *+8                                                              
         MVI   10(R1),BSTDAILY                                                  
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* DISPLAY DEMO VALUES                                                           
***********************************************************************         
DISDEMO  NTR1                                                                   
         LA    R6,DEMOCATS                                                      
         LA    R2,BUYDEMOS                                                      
         OC    BUYDEMOS,BUYDEMOS                                                
         BZ    DDEMX                                                            
         CLI   0(R6),0                                                          
         BE    DDEMX                                                            
         LR    R8,R4                                                            
         MVC   9(7,R8),=C'DEMOS: '                                              
         AHI   R8,16                                                            
         LA    R4,81(R4)                                                        
*                                                                               
DDEM05   DS    0H                                                               
         MVC   0(7,R8),0(R6)                                                    
         AHI   R8,7                                                             
         MVI   0(R8),C'/'                                                       
         AHI   R8,1                                                             
         EDIT  (C7,0(R2)),(8,(R8)),1,ALIGN=LEFT                                 
         AR    R8,R0                                                            
         AHI   R2,7                                                             
         AHI   R6,7                                                             
         CLI   0(R6),0                                                          
         BE    DDEMX                                                            
         MVC   0(2,R8),=C', '                                                   
         AHI   R8,2                                                             
         B     DDEM05                                                           
*                                                                               
DDEMX    DS    0H                                                               
         XIT1  REGS=(R4)                                                        
         EJECT                                                                  
***********************************************************************         
* GET BUY COMMENTS                                                              
***********************************************************************         
GETCOMMT NTR1                                                                   
         MVC   SVKEY,KEY                                                        
         LA    R6,KEY                                                           
         USING RDARKEY,R6                                                       
         MVI   RDARKSRT,X'20'      GET BUY COMMENTS                             
         DROP  R6                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(L'RDARKEY),KEYSAVE                                           
         BNE   GETCX                                                            
         GOTO1 GETREC                                                           
                                                                                
         L     R6,AIOAREA                                                       
         USING RDARCTCD,R6                                                      
         MVI   ELCODE,X'02'        COMMENTS                                     
         BAS   RE,GETEL                                                         
         BNE   GETCX                                                            
                                                                                
GETC10   DS    0H                                                               
         CLI   RDARCTLN,2          SKIP IF NO COMMENT                           
         BNH   GETC100                                                          
         CLI   RDARCTLN,72         SHORTEN TO MAX 70 IF TOO LONG                
         BL    *+12                                                             
         LA    R1,69                                                            
         B     GETC20                                                           
                                                                                
         ZIC   R1,RDARCTLN                                                      
         SH    R1,=H'3'            SUBTRACT OVERHEAD LENGTH                     
GETC20   DS    0H                                                               
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   9(0,R4),RDARCTCM                                                 
                                                                                
         ZIC   RF,BROWUSED         NEXT PRINT LINE                              
         LA    RF,1(RF)                                                         
         STC   RF,BROWUSED                                                      
                                                                                
         CLC   BROWUSED,BLISTNUM                                                
         BH    GETCX                                                            
                                                                                
GETC50   DS    0H                                                               
         LA    R4,81(R4)                                                        
                                                                                
GETC100  DS    0H                                                               
         BAS   RE,NEXTEL                                                        
         BE    GETC10                                                           
                                                                                
GETCX    DS    0H                                                               
         MVC   KEY,SVKEY                                                        
         GOTO1 HIGH                                                             
         XIT1  REGS=(R4)                                                        
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
* CALULATE ONE-BYTE DAYS OF WEEK                                                
***********************************************************************         
BUYROT   NTR1                                                                   
         L     R2,0(R1)            ROTATION                                     
                                                                                
         LA    R1,7                                                             
         ZIC   RE,=X'40'                                                        
         SR    R0,R0                                                            
                                                                                
BUYR30   DS    0H                                                               
         CLI   0(R2),C'X'                                                       
         BNE   *+6                                                              
         OR    R0,RE                                                            
         LA    R2,1(R2)                                                         
         SRL   RE,1                                                             
         BCT   R1,BUYR30                                                        
                                                                                
         STC   R0,BUYDAYS                                                       
         B     EXIT                                                             
         EJECT                                                                  
**********************************************************************          
*   STARTEND:  CONVERTS ROTATION MATRIX AND START DAY TO ONE-BYTE               
*        START/END DAY, INSERTS INTO ADDRESS                                    
**********************************************************************          
STARTEND NTR1                                                                   
         L     R2,0(R1)            A(INPUT: ROTATION+START DAY)                 
         L     R3,4(R1)            A(RECEIVING FIELD)                           
*                                                                               
         ZIC   RF,7(R2)            ROTATION START DAY                           
         SLL   RF,28               STRIP OFF ZONE BITS                          
         SRL   RF,28               SHIFT START DAY BACK                         
*                                                                               
         LA    R8,0(R2)            A(ROTATION FIELD)                            
         LR    RE,R8               CALCULATE END OF ROTATION FIELD              
         LA    RE,7(RE)                                                         
         AR    R8,RF               GET A(1ST DAY+1)                             
         BCTR  R8,0                BACK OFF TO A(1ST ENTRY)                     
         LR    RF,R8               SAVE A(1ST ENTRY)                            
*                                     FOR WHEN 1ST ENTRY IS ONLY ENTRY          
         LA    R0,7                SET LOOP CONTROL TO SIX DAYS                 
STEN0040 EQU   *                                                                
         CR    R8,RE               END OF ROTATION FIELD REACHED?               
         BNE   STEN0080            NO                                           
         LA    R8,0(R2)            YES  - GO BACK TO FIRST LOCATION             
STEN0080 EQU   *                                                                
         CLI   0(R8),C'X'          ARRAY POSITION USED?                         
         BNE   STEN0200            NO  - SKIP IT                                
         CLI   BSTRTDAY,0          ANYTHING IN START DAY?                       
         BNE   STEN0120            YES - DON'T REPLACE                          
         LA    R4,7                NO  - CALCULATE STARTDAY                     
         SR    R4,R0               SUBTRACT REMAINING DAYS                      
                                                                                
         ZIC   R5,7(R2)            ROTATION START DAY                           
         SLL   R5,28               STRIP OFF ZONE BITS                          
         SRL   R5,28               SHIFT START DAY BACK                         
                                                                                
         AR    R4,R5               ADD ROTATION START DAY                       
         CH    R4,=H'7'            OUT OF WEEK ROTATOR?                         
         BNH   STEN0090                                                         
         SH    R4,=H'7'                                                         
*                                                                               
STEN0090 DS    0H                                                               
         STC   R4,BSTRTDAY         SAVE CALCULATED STARTDAY                     
STEN0120 EQU   *                                                                
         OC    0(1,R3),0(R3)       RECEIVING FIELD ENTRY MADE?                  
         BNZ   STEN0160            YES - START DAY ENTERED                      
         SLL   R4,4                NO  - MOVE START DAY TO HIGH NYBBLE          
         STC   R4,0(R3)            INSERT INTO RECORD                           
STEN0160 EQU   *                                                                
         LR    RF,R8               YES - SAVE NEW ARRAY POSITION                
STEN0200 EQU   *                                                                
         LA    R8,1(R8)            BUMP TO NEXT ARRAY LOCATION                  
         BCT   R0,STEN0040         GO BACK AND CHECK NEXT                       
         LA    R8,0(R2)            A(START OF ARRAY)                            
         BCTR  R8,0                BACK UP 1 POSITION                           
         SR    RF,R8               CALCULATED DISPLACEMENT                      
         STC   RF,BENDDAY          SAVE END DAY FOR EFF DATE SETTING            
         ZIC   RE,0(R3)            RETRIEVE START DAY                           
         AR    RF,RE               ADD START TO END                             
         STC   RF,0(R3)            PUT IT BACK IN RECORD                        
         LA    R8,0(R2)            COUNT NUMBER OF DAYS                         
         SR    RF,RF                                                            
         LA    R0,7                                                             
STEN0240 EQU   *                                                                
         CLI   0(R8),C'X'          DAY ACTIVE?                                  
         BNE   STEN0280            NO                                           
         LA    RF,1(RF)            YES - ADD 1                                  
STEN0280 EQU   *                                                                
         LA    R8,1(R8)            BUMP TO NEXT POSITION                        
         BCT   R0,STEN0240         GO BACK AND CHECK NEXT                       
         C     RF,=F'1'            COUNT = 1?                                   
         BH    STEN0320            NO  - HIGHER - EXIT                          
         BE    *+6                 YES - SET START=END IN RECORD                
         DC    H'0'                SHOULD NEVER HAPPEN                          
*                                     MEANS EMPTY ARRAY!!!                      
         ZIC   RF,0(R3)            RETRIEVE START/END DAY                       
         SLL   RF,28               DROP START DAY                               
         SRL   RF,24               MOVE END DAY BACK TO HI NYBBLE               
         NI    0(R3),X'0F'         CLEAR START DAY                              
         ZIC   RE,0(R3)            RETRIEVE 0/END DAY                           
         AR    RE,RF               ADD NEW START DAY                            
         STC   RE,0(R3)            MOVE IT BACK                                 
STEN0320 EQU   *                                                                
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* BUYROTAT HAS BUY ROTATION INFO                                                
* R6 IS POINTING TO BUY DETAIL X'02' ELEMENT                                    
* BUYHEAD HAS BUY HEADER X'01' ELEMENT                                          
***********************************************************************         
BUYLINE  NTR1                                                                   
         LA    R3,BUYHEAD                                                       
         USING RDARBYEL,R3                                                      
*                                                                               
* CHECK IF RECORD IS MARKED AS SOFT DELETED                                     
*                                                                               
         CLI   RDARBYLN,RDARBYOL   NEW LENGTH ONLY                              
         BNH   BUYL10                                                           
                                                                                
         TM    RDARBYDL,X'80'                                                   
         BZ    *+10                                                             
         MVC   LBUYMC,=C' C'       SOFT DELETE                                  
         DROP  R3                                                               
                                                                                
         USING RDARBUEL,R6                                                      
                                                                                
* DISPLAY START-END TIMES                                                       
BUYL10   DS    0H                  IF SAME BUY AND DAILY, SKIP TIME             
         TM    BSTATUS,BSTSAME+BSTDAILY                                         
         BO    BUYL30                                                           
                                                                                
         CLC   BUYSTTIM,BUYENTIM   DISPLAY SINGLE TIME FOR                      
         BNE   *+10                IDENTICAL START AND END TIMES                
         XC    BUYENTIM,BUYENTIM                                                
         GOTO1 VUNTIME,DMCB,BUYSTTIM,LBUYTIME                                   
                                                                                
* DISPLAY START-END DAYS                                                        
         TM    BSTATUS,BSTDAILY    CHECK IF DAILY BUY                           
         BZ    BUYL40                                                           
*                                                                               
* DAILY BUYS DISPLAY SINGLE DAY AND EFFECTIVE DATE                              
*                                                                               
BUYL30   DS    0H                                                               
         GOTO1 VDATCON,DMCB,(2,RDARBUSD),(0,WORK)                               
         GOTO1 VGETDAY,DMCB,WORK,LBUYDAYS                                       
         GOTO1 VDATCON,DMCB,(2,RDARBUSD),(4,LBUYDATE)                           
         B     BUYL120                                                          
                                                                                
BUYL40   DS    0H                                                               
         GOTO1 VOUTDAY,DMCB,BUYDAYS,BUYSTEN,LBUYDAYS                            
                                                                                
         TM    BSTATUS,BSTORBIT    ORBIT BUY, DON'T SHOW REST                   
         BO    BUYLX                                                            
*                                                                               
* CALULATE EFFECTIVE DATES                                                      
*                                                                               
         GOTO1 VDATCON,DMCB,(2,RDARBUSD),(0,WORK)                               
*                                  CONVERT START DATE TO EBCDIC                 
         ZIC   RF,RDARBUWK         CALCULATE NUMBER OF DAYS                     
         LTR   RF,RF               AGENCY CAN SEND ZERO WEEKS                   
         BZ    *+6                                                              
         BCTR  RF,0                MAKE WEEKS ZERO RELATIVE                     
         MH    RF,=H'7'            MULTIPLY WEEKS BY 7                          
         GOTO1 VADDAY,DMCB,WORK,WORK+6,(RF)                                     
*                                                                               
         GOTO1 VGETDAY,DMCB,WORK,WORK+12                                        
*                                  GET DAY OF START WEEK                        
         ZIC   RF,DMCB             GET DAY OF WEEK                              
         BCTR  RF,0                MAKE DAY ZERO RELATIVE                       
         LTR   RF,RF               MONDAY?                                      
         BZ    BUYL100             YES - LEAVE                                  
         LNR   RF,RF               NEGATE REGISTER                              
         GOTO1 VADDAY,DMCB,WORK,WORK+12,(RF)                                    
*                                  BACK UP TO MONDAY                            
*                                                                               
* IN CASE OF OUT OF WEEK ROTATORS, COMPARE BUY START DAY AND ROTATION           
* START DAY TO GET THE CORRECT MONDAY DATE                                      
*                                                                               
         XC    BRTSTDAY,BRTSTDAY                                                
         MVN   BRTSTDAY,BUYROTST   CONVERT ROTATION START DAY TO BINARY         
         CLC   BSTRTDAY,BRTSTDAY                                                
         BNL   BUYL50                                                           
         MVC   WORK(6),WORK+12                                                  
         LA    RF,7                                                             
         GOTO1 VADDAY,DMCB,WORK,WORK+12,(RF)                                    
*                                                                               
BUYL50   DS    0H                                                               
         MVC   WORK(6),WORK+12                                                  
                                                                                
BUYL100  DS    0H                                                               
         OC    ORBITSTT,ORBITSTT   IF ORBIT, USE 1ST DAY OF 1ST ORBIT           
         BZ    *+10                                                             
         MVC   BSTRTDAY,ORBITSTT                                                
                                                                                
         ZIC   RF,BSTRTDAY         ADD START DAY                                
         BCTR  RF,0                MAKE ZERO RELATIVE                           
         GOTO1 VADDAY,DMCB,WORK,WORK+12,(RF)                                    
*                                  BUMP TO START DAY IN WEEK                    
         GOTO1 VDATCON,DMCB,(0,WORK+12),(4,LBUYDATE)                            
         MVC   BUYEFFST,WORK+12                                                 
*                                                                               
*INSERT EFFECTIVE START DATE                                                    
*                                                                               
         GOTO1 VGETDAY,DMCB,WORK+6,WORK+12                                      
*                                  GET DAY OF END   WEEK                        
         ZIC   RF,DMCB             GET DAY OF WEEK                              
         BCTR  RF,0                MAKE DAY ZERO RELATIVE                       
         LTR   RF,RF               MONDAY?                                      
         BZ    BUYL110             YES - LEAVE                                  
         LNR   RF,RF               NEGATE REGISTER                              
         CLC   BSTRTDAY,BENDDAY    START/END ON SAME DAY?                       
*                                    (SINGLE-DAY BUY?)                          
         BNH   BUYL105             YES - DON'T BUMP TO NEXT WEEK                
*                                  START < END DAY: NOT AN OOWR                 
*                                   DON'T BUMP                                  
         LA    RF,7(RF)            BUMP IT INTO NEXT WEEK                       
                                                                                
BUYL105  DS    0H                                                               
         GOTO1 VADDAY,DMCB,WORK+6,WORK+12,(RF)                                  
*                                  BACK UP TO MONDAY                            
*                                                                               
* IN CASE OF OUT OF WEEK ROTATORS, COMPARE BUY START DAY AND ROTATION           
* START DAY TO GET THE CORRECT MONDAY DATE                                      
*                                                                               
         XC    BRTSTDAY,BRTSTDAY                                                
         MVN   BRTSTDAY,BUYROTST   CONVERT ROTATION START DAY TO BINARY         
         CLC   BSTRTDAY,BRTSTDAY                                                
         BNL   BUYL108                                                          
         MVC   WORK+6(6),WORK+12                                                
         LA    RF,7                                                             
         GOTO1 VADDAY,DMCB,WORK+6,WORK+12,(RF)                                  
*                                                                               
BUYL108  DS    0H                                                               
         MVC   WORK+6(6),WORK+12                                                
                                                                                
BUYL110  DS    0H                                                               
         OC    ORBITEND,ORBITEND   IF ORBIT, USE LAST DAY OF LAST ORBIT         
         BZ    *+10                                                             
         MVC   BENDDAY,ORBITEND                                                 
                                                                                
         ZIC   RF,BENDDAY          ADD END   DAY                                
         BCTR  RF,0                MAKE ZERO RELATIVE                           
         GOTO1 VADDAY,DMCB,WORK+6,WORK+12,(RF)                                  
*                                  BUMP TO END   DAY IN WEEK                    
*INSERT EFFECTIVE END DATE                                                      
*                                                                               
         CLC   BUYEFFST,WORK+12                                                 
         BE    BUYL120                                                          
         MVI   LBUYDATE+5,C'-'                                                  
         LA    R3,LBUYDATE                                                      
         GOTO1 VDATCON,DMCB,(0,WORK+12),(4,6(R3))                               
                                                                                
BUYL120  DS    0H                                                               
* NUMBER OF WEEKS                                                               
         EDIT  RDARBUWK,(2,LBUYNW),ZERO=NOBLANK                                 
         MVI   LBUYNW+2,C'W'                                                    
                                                                                
* NUMBER PER WEEK                                                               
         CLC   =C' C',LBUYMC       DON'T SHOW NPW/RATE/SPT IF DELETED           
         BE    BUYLX                                                            
         EDIT  RDARBUSW,(3,LBUYNPW),ZERO=NOBLANK                                
                                                                                
         ZIC   RF,RDARBUWK                                                      
         XC    HALF,HALF                                                        
         MVC   HALF+1(1),RDARBUSW                                               
         MH    RF,HALF                                                          
                                                                                
* NUMBER OF SPOTS                                                               
         EDIT  (RF),(4,LBUYSPT),ZERO=NOBLANK                                    
                                                                                
         TM    BSTATUS,BSTSAME     IF SAME BUYLINE, SKIP SPOT LENGTH            
         BZ    BUYL125                                                          
         CLC   SPTCOST,RDARBU$$    SAME BUYLINE, BUT CHK IF MULTI-RATE          
         BNE   BUYL140                                                          
         B     BUYLX                                                            
                                                                                
* SPOT LENGTH                                                                   
BUYL125  DS    0H                                                               
         LA    R3,BUYHEAD                                                       
         USING RDARBYEL,R3                                                      
                                                                                
         CLI   RDARBYSU,C'M'       IF UNIT IS MINUTES                           
         BNE   BUYL130                                                          
         EDIT  RDARBYSL,(3,LBUYLENM)                                            
         MVI   LBUYLEN+2,C'M'                                                   
         B     BUYL140                                                          
                                                                                
BUYL130  DS    0H                                                               
         EDIT  RDARBYSL,(3,LBUYLEN)                                             
                                                                                
* RATE                                                                          
BUYL140  DS    0H                                                               
         EDIT  RDARBU$$,(10,LBUYRATE),2,FLOAT=-,COMMAS=YES                      
         MVC   SPTCOST,RDARBU$$                                                 
                                                                                
BUYLX    DS    0H                                                               
         B     EXIT                                                             
         DROP  R3,R4,R6                                                         
         EJECT                                                                  
***********************************************************************         
* DATAMGR CALLS                                                                 
***********************************************************************         
HIGH     NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         GOTO1 VDATAMGR,DMCB,(DMINBTS,=C'DMRDHI'),=C'REPDIR',          X        
               KEYSAVE,KEY,0                                                    
         XIT1                                                                   
*                                                                               
SEQ      NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         GOTO1 VDATAMGR,DMCB,(DMINBTS,=C'DMRSEQ'),=C'REPDIR',          X        
               KEYSAVE,KEY,0                                                    
         XIT1                                                                   
*                                                                               
GETREC   NTR1                                                                   
         GOTO1 VDATAMGR,DMCB,(DMINBTS,=C'GETREC'),=C'REPFILE',KEY+28,  X        
               AIOAREA,DMWORK                                                   
         XIT1                                                                   
         EJECT                                                                  
         GETEL R6,DATADISP,ELCODE                                               
         LTORG                                                                  
         EJECT                                                                  
MYSPACES DC    40C' '                                                           
*                                                                               
WORKD    DSECT                                                                  
DMWORK   DS    12D                                                              
*                                                                               
VCOMFACS DS    A                   A(COMFACS)                                   
VOUTDAY  DS    A                   A(OUTDAY)                                    
VUNTIME  DS    A                   A(VUNTIME)                                   
VDEMOCON DS    A                   A(DEMOCON)                                   
*                                                                               
VDATAMGR DS    A                   A(DATAMGR)                                   
VDATCON  DS    A                   A(VDATCON)                                   
VADDAY   DS    A                   A(VADDAY)                                    
VGETDAY  DS    A                   A(VGETDAY)                                   
*                                                                               
VREDARTK DS    A                   A(REDARTK)                                   
VPERVERT DS    A                   A(PERVERT)                                   
TKODATE  DS    CL3                 TAKODATE(YMD BINARY)                         
MGLINK   DS    X                   AGENCY MAKEGOOD TARGET LINE                  
*                                                                               
ABUYKEY  DS    A                                                                
BUYKEY   DS    CL32                                                             
KEY      DS    CL32                                                             
KEYSAVE  DS    CL32                                                             
SVKEY    DS    CL32                                                             
DMCB     DS    6F                                                               
DUB      DS    D                                                                
HALF     DS    H                                                                
DMINBTS  DS    X                                                                
AIOAREA  DS    A                                                                
SVDMCB   DS    A                                                                
DATADISP DS    H                                                                
ELCODE   DS    X                                                                
WORK     DS    CL64                                                             
DEMOCATS DS    CL64                                                             
*                                                                               
BUYORBIT DS    17CL12              BUY ORBIT ARRAY                              
*                                                                               
BUYROTAT DS    CL7                 BUY ROTATION                                 
BUYROTST DS    CL1                 BUY ROTATION START DAY                       
BUYSTTIM DS    CL2                 BUY START TIME                               
BUYENTIM DS    CL2                 BUY END TIME                                 
*                                                                               
BUYHEAD  DS    CL(RDARBYLE)        BUY HEADER X'01' DESCRIPTIVE ELEMT           
*                                                                               
BUYDEMOS DS    CL28                BUY DEMOS                                    
*                                                                               
BUYDAYS DS     X                   1-BYTE DAYS OF WEEK   X'40' - MON            
*                                                        X'20' - TUE            
*                                                        ...     ...            
*                                                        X'01' - SUN            
BUYSTEN DS     X                   1-BYTE START-END DAYS                        
*                                    BITS 7-4 START      X'01' - MON            
*                                    BITS 3-0 END        X'02' - TUE            
*                                                        ...     ...            
*                                                        X'07' - SUN            
BSTRTDAY DS    X                   BUY START DAY                                
BENDDAY  DS    X                   BUY END DAY                                  
BRTSTDAY DS    X                   BUY ROTATION START DAY                       
BUYEFFST DS    CL6                 BUY EFFECTIVE START DATE                     
ORBITSTT DS    X                   FIRST DAY OF FIRST ORBIT                     
ORBITEND DS    X                   LAST DAY OF LAST ORBIT                       
BSTATUS  DS    X                   DISPLAY FLAG                                 
BSTSAME  EQU   X'02'               SAME BUY DON'T PRINT BUY#/RATE/LEN           
BSTORBIT EQU   X'04'               ORBIT BUY, PRINT DAY/TIME ONLY               
BSTMGOOD EQU   X'40'               BUY IS MAKEGOOD                              
BSTDAILY EQU   X'80'               BUY IS DAILY                                 
BLISTNUM DS    X                   MAX ROWS FREE TO LIST                        
BROWUSED DS    X                   ACTUALLY NUMBER OF ROWS USED                 
BBUYSTRW DS    X                   START ROW NUMBER OF CURRENT BUY              
BBUYNUM  DS    X                   LAST BUY NUMBER                              
PRINTOPT DS    X                   PRINT OPTION                                 
*                                  X'80' PRINT ONLY ONE BUY AND EXIT            
*                                  X'40' DO TAKEOVER CHOPPING                   
*                                  X'20' SHOW MG INDICATOR                      
*                                  X'01' GET NEXT BUYLINE # AND EXIT            
SPTCOST  DS    XL4                 PREVIOUS BUY'S SPOT COST                     
BUYDAILY DS    X                   RETURN BUY DAILY STATUS                      
EFFDATES DS    CL12                AGENCY HEADER EFFECTIVE DATES EBCDIC         
WORKX    EQU   *                                                                
         EJECT                                                                  
*                                                                               
* BUY LIST LINE                                                                 
*                                                                               
LBUYD    DSECT                                                                  
LBUYMC   DS    CL2                                                              
         DS    CL1                                                              
LBUYLINE DS    CL3                                                              
LBUYTYPE DS    CL1                                                              
LBUYDAYS DS    CL12                                                             
         DS    CL1                                                              
LBUYTIME DS    CL11                                                             
LBUYLENM DS    CL1                                                              
LBUYLEN  DS    CL3                                                              
         DS    CL1                                                              
LBUYDATE DS    CL12                                                             
         DS    CL1                                                              
LBUYNW   DS    CL3                                                              
         DS    CL2                                                              
LBUYMGI  DS    CL2                                                              
         DS    C                                                                
LBUYMGL  DS    CL3                                                              
         DS    CL1                                                              
LBUYNPW  DS    CL3                                                              
         DS    CL1                                                              
LBUYRATE DS    CL10                                                             
LBUYSPT  DS    CL4                                                              
         DS    CL2                                                              
LBUYLENQ EQU   *-LBUYMC                                                         
         ORG   LBUYDAYS                                                         
LBUYPROG DS    CL34                                                             
         ORG   LBUYDAYS                                                         
LBUYCMMT DS    CL71                                                             
*                                                                               
* REGENCON                                                                      
* REGENDAR                                                                      
*                                                                               
       ++INCLUDE REDARDSECT                                                     
       ++INCLUDE DDCOMFACS                                                      
DBLOCKD  DSECT                                                                  
       ++INCLUDE DEDBLOCK                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'061REGENDABS 09/11/98'                                      
         END                                                                    
