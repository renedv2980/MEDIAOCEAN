*          DATA SET SPWRI27X   AT LEVEL 058 AS OF 05/01/02                      
*CATALP SPWRI27A                                                                
         TITLE 'T20427 - SPOTPAK WRITER BUY RULE REPORT'                        
T20427   CSECT                                                                  
*&&DO                                                                           
         ORG   *+9214                                                           
         DC    XL2'00'                                                          
         ORG   *-9216                                                           
*&&                                                                             
         PRINT NOGEN                                                            
         NMOD1 WORKL,T20427,RA,RR=R2                                            
         LR    R6,RC                                                            
         USING WORKD,R6                                                         
         ST    R2,RELO                                                          
         MVC   SAVERD,4(RD)        SAVE A(CALLING PROGRAM'S SAVE AREA)          
         L     RC,0(R1)                                                         
         USING GEND,RC                                                          
         ST    RC,AGEND                                                         
         L     R8,ASPOOLD                                                       
         USING SPOOLD,R8                                                        
         L     R9,ASYSD                                                         
         USING SYSD,R9             SYSTEM SPECIFIC WORK                         
         L     R7,ATWA                                                          
         USING CONHEADH-64,R7                                                   
*                                                                               
         L     RF,TWADCONS               GET A WORK AREA                        
         L     R2,TSPFUSER-TWADCOND(RF)  THAT IS GUARANTEED TO BE               
         ST    R2,ARULWK                 SAVED ACROSS REQUESTS                  
*                                                                               
         L     RE,TRECUP-TWADCOND(RF)                                           
         ST    RE,VRECUP                                                        
         L     RE,TBINSRCH-TWADCOND(RF)                                         
         ST    RE,VBINSRCH                                                      
         L     RE,TLOADER-TWADCOND(RF)                                          
         ST    RE,VLOADER                                                       
*                                                                               
         L     RF,ACOMFACS                                                      
         MVC   VCALLOV,CCALLOV-COMFACSD(RF)                                     
*                                                                               
         LA    R0,AXTRAN           SET EXTENSION ROUTINES                       
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         L     R1,=A(T20427X)                                                   
         A     R1,RELO                                                          
         ST    R1,AXTRA(RE)                                                     
         STC   RF,AXTRA(RE)                                                     
         LA    RF,1(RF)                                                         
         LA    RE,4(RE)                                                         
         BCT   R0,*-16                                                          
*                                  REPORT CALLING MODE                          
         CLI   RPMODE,RPINIT       INITIALIZATION                               
         BE    INIT                                                             
         CLI   RPMODE,RPVAL        VALIDATION                                   
         BE    VALID                                                            
         CLI   RPMODE,RPINPUT      INPUT                                        
         BE    INPUT                                                            
         CLI   RPMODE,RPDRHOOK     DRIVER HOOK                                  
         BE    DRHOOK                                                           
         CLI   RPMODE,RPRUNLST     RUN LAST                                     
         BE    RUNLST                                                           
         B     XIT                                                              
*                                                                               
XIT      XIT1  ,                                                                
         EJECT                                                                  
* INITIALIZATION                                                                
*                                                                               
INIT     MVI   SVPRD,0                                                          
         MVI   SVEST,0                                                          
         XC    RCOUNT,RCOUNT                                                    
         MVI   FRSTLAST,C'Y'       NEED RUNFRST AND RUNLAST                     
         MVI   TRCSW,C'N'                                                       
         MVI   ERRSW,C'N'          CLEAR ERROR SWITCH                           
*                                                                               
         MVI   NDEMOS,1                                                         
         OI    DATAIND,DIDEMP                                                   
*                                                                               
         XC    BGPROF,BGPROF                                                    
         XC    WORK,WORK           READ BG PROFILE                              
         MVC   WORK(4),=C'SOBG'                                                 
         MVC   WORK+4(2),AGYALPHA                                               
         MVC   WORK+6(1),WRIMED                                                 
         MVC   WORK+7(3),WRICLT                                                 
         GOTO1 GETPROF,DMCB,WORK,BGPROF,DATAMGR                                 
*                                                                               
         L     R1,=A(MYDEMOS)                                                   
         A     R1,RELO                                                          
         XC    0(L'MYDEMOS,R1),0(R1)                                            
         XC    SVBUYKEY,SVBUYKEY                                                
         OI    SBQSKIP,SBQSKBIL+SBQSKGL   SKIP STABUCKS AND GOALS               
         OI    SBQDATA,SBQDPUR     SET DATA                                     
         OI    SBQDPTLN,SBQDLSGR   SUPPRESS DAYPART GROUP TOTALS                
         OI    COLIND,COLIDEM      INDICATE DEMOS IS THIS RPT                   
         OI    COLIND,COLIRND      ROUND OUT PENNIES                            
         OI    COLIND,COLINDR      NO DEMO ROUNDING                             
         CLI   WGTOPT,0            IF MARKET WEIGHTING OPTION NOT SET,          
         BNE   *+8                 TURN IT OFF                                  
         MVI   WGTOPT,C'N'                                                      
*                                                                               
         CLI   RPTSCRN,0           TEST USER REPORT SCREEN                      
         BE    INIT20                                                           
*                                                                               
INIT16   DS    0H                                                               
         XC    AUPGHDR,AUPGHDR                                                  
*                                                                               
INIT17F  TM    DOWNOPT,DOWNON      REPORT OPTION MANDATORY FOR                  
         BZ    INIT18              DOWNLOADING                                  
         CLI   SINGRPT,0                                                        
         BE    ENOREP                                                           
*                                                                               
INIT18   LA    R2,PBETITH          TITLE                                        
         MVC   TITLE,SPACES                                                     
         MVC   TITLE(26),=C'GUIDELINE EXCEPTION REPORT'                         
         CLI   5(R2),0                                                          
         BE    INIT19                                                           
         GOTO1 ANY                                                              
         MVC   TITLE,WORK                                                       
*                                                                               
INIT19   GOTO1 CENTER,DMCB,TITLE,63                                             
*                                                                               
INIT20   DS    0H                  ONE STATION OR ALL?                          
         MVI   ONESTA,C'N'                                                      
         LA    R2,PBESTAH                                                       
         CLI   5(R2),0                                                          
         BE    INIT20F                                                          
         CLC   =C'ALL',8(R2)                                                    
         BE    INIT20F                                                          
         MVI   ONESTA,C'Y'                                                      
*                                                                               
INIT20F  DS    0H                  ERROR MARKETS ONLY OPTIONS                   
         LA    R2,PBEOPT1H                                                      
         MVC   MKERROPT,8(R2)                                                   
         CLI   MKERROPT,C' '                                                    
         BH    *+12                                                             
         MVI   MKERROPT,C'N'                                                    
         B     INIT20H                                                          
         CLI   MKERROPT,C'N'                                                    
         BE    INIT20H                                                          
         CLI   MKERROPT,C'Y'                                                    
         BNE   EINV                                                             
*                                                                               
INIT20H  DS    0H                  CREATE STATUS RECORDS OPTION                 
         ICM   RF,15,TWAMASTC      NOT IF RUNNING SOON                          
         BZ    *+14                                                             
         USING MASTD,RF                                                         
         OC    MCREMPQK,MCREMPQK                                                
         BNZ   INIT20H2                                                         
         DROP  RF                                                               
*                                                                               
         LA    R2,PBEOPT2H                                                      
         MVC   MKBGROPT,8(R2)                                                   
         CLI   MKBGROPT,C' '       IF NO SCREEN INPUT                           
         BH    INIT20H4                                                         
         CLI   BGPROF+0,C'Y'       USE PROFILE OPTION                           
         BNE   *+12                                                             
         MVI   MKBGROPT,C'Y'                                                    
         B     INIT20H4                                                         
INIT20H2 DS    0H                                                               
         MVI   MKBGROPT,C'N'                                                    
         B     INIT20I                                                          
*                                                                               
INIT20H4 DS    0H                                                               
         CLI   MKBGROPT,C'N'                                                    
         BE    INIT20I                                                          
         CLI   MKBGROPT,C'Y'                                                    
         BNE   EINV                                                             
*                                                                               
INIT20I  DS    0H                                                               
*                                                                               
INIT22   DS    0H                                                               
         CLI   OFFLINE,C'Y'        DO WE NEED OFF-LINE MODULES?                 
         BNE   INIT23                                                           
         L     R2,ARULWK                                                        
         USING SPRULERD,R2                                                      
         OC    VRULER,VRULER       DO WE ALREADY HAVE THEM?                     
         BNZ   INIT23                                                           
*                                                                               
         GOTO1 VLOADER,DMCB,=CL8'T20431  ',0                                    
         ICM   RF,15,4(R1)                                                      
         BNZ   *+6                                                              
         DC    H'0'                                                             
         MVC   VRULER,0(RF)                                                     
         MVC   VBGSTAT,4(RF)                                                    
         DROP  R2                                                               
                                                                                
******   NO-OP FAILED ATTEMPT TO USE CALLOFF ****                               
*                                                                               
*        GOTO1 VCALLOV,DMCB,(X'31',0),0                                         
*        CLI   4(R1),X'FF'                                                      
*        BNE   *+6                                                              
*        DC    H'0'                                                             
*        ICM   RF,15,0(R1)                                                      
*        BNZ   *+6                                                              
*        DC    H'0'                                                             
*        MVC   VRULER,0(RF)                                                     
*        MVC   VBGSTAT,4(RF)                                                    
*        DROP  R2                                                               
*                                                                               
INIT23   DS    0H                                                               
         SR    R1,R1               FIND EXTRA LENGTH OF RECORD                  
         CLI   SBQPGRD,C' '        DUE TO PRODUCT AND MARKET GROUPS             
         BNH   INIT24              AND MARKETS RANKED                           
         LA    R1,2(R1)                                                         
         CLC   SBPGR1LN,SBPGR2LN                                                
         BE    INIT24                                                           
         LA    R1,2(R1)                                                         
*                                                                               
INIT24   CLI   SBQMGRD,C' '                                                     
         BNH   INIT26                                                           
         LA    R1,2(R1)                                                         
         CLC   SBMGR1LN,SBMGR2LN                                                
         BE    INIT26                                                           
         LA    R1,2(R1)                                                         
         CLC   SBMGR2LN,SBMGR3LN                                                
         BE    INIT26                                                           
         LA    R1,2(R1)                                                         
*                                                                               
INIT26   ST    R1,LGRPS            EXTRA LENGTH FOR PRD AND MKT GROUPS          
         CLI   MORDER,C'R'         TEST MARKETS RANKED                          
         BNE   *+8                                                              
         LA    R1,2(R1)            YES-ADD 2 FOR MKT RANK NUMBER                
         CLI   MORDER,C'A'         TEST MARKETS RANKED                          
         BNE   *+8                                                              
         LA    R1,8(R1)            YES-ADD 8 FOR MKT NAME                       
         CLI   DPTOPT,C'Y'         TEST DAYPART REPORT                          
         BNE   *+8                                                              
         LA    R1,8(R1)            YES-ADD 8 FOR DAYPART ROW                    
         ST    R1,EXTRALEN         EXTRA LENGTH FOR REPORTS 2 AND 3             
*                                                                               
         LA    R4,RPTLEVS          SET LEVELS                                   
         CLI   DPTOPT,C'Y'                                                      
         BNE   *+8                                                              
         LA    R4,RPTLEVS2                                                      
         CLI   SBQPGRD,C' '                                                     
         BH    *+14                                                             
         XC    1(2,R4),1(R4)       SET PRODUCT GROUPS                           
         B     INIT28                                                           
         CLC   SBPGR1LN,SBPGR2LN                                                
         BNE   INIT28                                                           
         MVI   2(R4),0                                                          
*                                                                               
INIT28   CLI   SBQMGRD,C' '        SET MARKET GROUPS                            
         BH    *+14                                                             
         XC    5(3,R4),5(R4)                                                    
         B     INIT30                                                           
         CLC   SBMGR1LN,SBMGR2LN                                                
         BNE   *+14                                                             
         XC    6(2,R4),6(R4)                                                    
         B     INIT30                                                           
         CLC   SBMGR2LN,SBMGR3LN                                                
         BNE   INIT30                                                           
         MVI   7(R4),0                                                          
*                                                                               
INIT30   LA    R1,LEVELS           SET THE LEVELS                               
         LA    RF,1                                                             
*                                                                               
INIT31   CLI   0(R4),X'FF'                                                      
         BE    INIT36                                                           
         CLI   0(R4),0                                                          
         BE    INIT34                                                           
         MVC   0(1,R1),0(R4)                                                    
         CLI   0(R1),QMKT                                                       
         BNE   INIT32                                                           
         STC   RF,MKTLEV           MARKET LEVEL                                 
         CLI   DPTOPT,C'Y'         TEST DAYPART REPORT                          
         BE    *+12                                                             
         STC   RF,LSTHEDLV         NO-LAST HEADLINE LEVEL                       
         B     INIT33                                                           
         CLI   MORDER,C'R'         YES-MARKET COULD BE MARKET RANKED            
         BNE   *+12                    OR MARKET NAME                           
         MVI   0(R1),QMKTR                                                      
         B     INIT33                                                           
         CLI   MORDER,C'A'                                                      
         BNE   INIT33                                                           
         MVI   0(R1),QMKTNM                                                     
         B     INIT33                                                           
*                                                                               
INIT32   CLI   0(R1),QDPT                                                       
         BNE   INIT33                                                           
         CLI   DPTOPT,C'Y'                                                      
         BNE   INIT33                                                           
         STC   RF,LSTHEDLV         LAST HEADLINE LEVEL FOR DAYPART              
         STC   RF,DPTLEV           VERSION OF PBE                               
*                                                                               
INIT33   CLI   0(R1),QSTA                                                       
         BNE   *+8                                                              
         STC   RF,MIDLEV           MIDLINE LEVEL                                
         LA    R1,1(R1)                                                         
         LA    RF,1(RF)                                                         
*                                                                               
INIT34   LA    R4,1(R4)                                                         
         B     INIT31                                                           
*                                                                               
INIT36   MVI   MYFIRSTH,12         SET DRIVER'S FIRST HEADLINE                  
*                                                                               
INITX    B     XIT                                                              
         SPACE 2                                                                
*              RUN LAST                                                         
RUNLST   DS    0H                                                               
         CLI   MKBGROPT,C'Y'                                                    
         BNE   RUNL2                                                            
         L     RF,ARULWK                                                        
         L     RF,VBGSTAT-SPRULERD(RF)                                          
         GOTO1 (RF),DMCB,(RC),(R2),VRECUP  POST TO STATUS RECS                  
RUNL2    DS    0H                                                               
         B     XIT                                                              
*                                                                               
         SPACE 2                                                                
* REPORT LEVELS BASED ON REPORT 2 (DETAIL REPORT)                               
*                                                                               
RPTLEVS  DC    AL1(QCLT)           HEADLINES                                    
         DC    AL1(QPRDGR1)                                                     
         DC    AL1(QPRDGR2)                                                     
         DC    AL1(QPRD)                                                        
         DC    AL1(QEST)                                                        
         DC    AL1(QMKTGR1)                                                     
         DC    AL1(QMKTGR2)                                                     
         DC    AL1(QMKTGR3)                                                     
         DC    AL1(QMKT)                                                        
         DC    AL1(QSTA)           MIDLINE                                      
         DC    AL1(QBUY)           DETAIL                                       
         DC    X'FF'                                                            
         SPACE 1                                                                
* REPORT LEVELS BASED ON REPORT 2 (DETAIL REPORT)                               
* FOR DAYPART VERSION OF THE PBE                                                
*                                                                               
RPTLEVS2 DC    AL1(QCLT)           HEADLINES                                    
         DC    AL1(QPRDGR1)                                                     
         DC    AL1(QPRDGR2)                                                     
         DC    AL1(QPRD)                                                        
         DC    AL1(QEST)                                                        
         DC    AL1(QMKTGR1)                                                     
         DC    AL1(QMKTGR2)                                                     
         DC    AL1(QMKTGR3)                                                     
         DC    AL1(QMKT)                                                        
         DC    AL1(QRPTSEQ)                                                     
         DC    AL1(QDPT)                                                        
         DC    AL1(QRPTSEQ)                                                     
         DC    AL1(QSTA)           MIDLINE                                      
         DC    AL1(QBUY)           DETAIL                                       
         DC    X'FF'                                                            
         EJECT                                                                  
* FURTHER REQUEST VALIDATION                                                    
*                                                                               
VALID    OC    SBBCLT,SBBCLT       ALL CLIENTS IS INVALID                       
         BZ    ECLT                                                             
*                                                                               
         CLC   PBEPRD(3),=C'POL'   POL IS INVALID                               
         BE    EPRD                                                             
*                                                                               
         CLI   PBEFLTH+5,0         NO FILTERS ALLOWED                           
         BNE   EFILT                                                            
*                                                                               
         LA    R2,PBEOPTH          NO OPTIONS ALLOWED                           
         CLI   PBEOPTH+5,0                                                      
         BNE   EOPT                                                             
         LA    R2,PBEOTHH          OR 'OTHER' OPTIONS                           
         CLI   PBEOTHH+5,0                                                      
         BNE   EOPT                                                             
*                                                                               
VALX     B     XIT                                                              
         SPACE 2                                                                
* ERROR EXITS AND MESSAGES                                                      
* NOTE - MESSAGES ARE IN SYSTEM 24                                              
*                                                                               
EBKS     MVC   CONHEAD(L'EBKSM),EBKSM                                           
         B     MYCURSOR                                                         
*                                                                               
ENOREP   MVC   CONHEAD(L'ENOREPM),ENOREPM                                       
         B     MYCURSOR                                                         
*                                                                               
EINV     MVI   ERROR,INVALID                                                    
         B     CURSOR                                                           
*                                                                               
ECLT     MVI   ERROR,INVCLT                                                     
         LA    R2,PBECLTH                                                       
         B     CURSOR                                                           
*                                                                               
EPRD     MVI   ERROR,INVPROD                                                    
         LA    R2,PBEPRDH                                                       
         B     CURSOR                                                           
*                                                                               
ESTA     MVI   ERROR,INVSTA                                                     
         LA    R2,PBESTAH                                                       
         B     CURSOR                                                           
*                                                                               
EFILT    MVI   ERROR,109                                                        
         LA    R2,PBEFLTH                                                       
         B     CURSOR                                                           
*                                                                               
EOPT     MVI   ERROR,78                                                         
         B     CURSOR                                                           
*                                                                               
MYCURSOR MVI   ERROR,X'FE'                                                      
CURSOR   GOTO1 CURSERR                                                          
*                                                                               
EBKSM    DC    C'**ERROR** MUST SPECIFY 3 BOOKS IN MMMYY FORMAT'                
ENOREPM  DC    C'REPORT OPTION MISSING'                                         
         EJECT                                                                  
INPUT    L     R4,AGLOBAL          ** INPUT ROUTINE **                          
         USING GLOBALD,R4                                                       
*                                                                               
         CLI   SBMODE,SBPROCSP     HOOK MODE PASSED FROM SPOTIO                 
         BE    PROCBUY             PROC MARKET                                  
         CLI   SBMODE,SBPROCMK     DO GLOBALS (ALSO AT RPFINAL?)                
         BE    PRLAST                                                           
         B     XIT                                                              
         EJECT                                                                  
* PROCESS BUY RECORD                                                            
*                                                                               
PROCBUY  L     R3,SBAIO1                                                        
         USING BUYRECD,R3                                                       
*                                                                               
BY28     DS    0H                                                               
BY32     MVI   SBBPRD,0                                                         
         MVI   SBBPRD2,0                                                        
         XC    SBPRD,SBPRD                                                      
         XC    SBPRD2,SBPRD2                                                    
*                                                                               
*******************************************************************             
*                                                                               
*      SPRULER CALL                                                             
*                                                                               
*******************************************************************             
*                                                                               
         MVC   SBBEST,BUYKEST      ESTIMATE                                     
*                                  NOTE- SBPRD SET BY RULER                     
         MVC   SVBMKT,SBBMKT       SAVE MARKET                                  
         MVC   SVBMGR,SBBMGR       SAVE MARKET GROUP                            
         MVI   MKTIND,X'FF'        3RD BYTE OF MKT (SPILL+NON SPILL)            
         MVI   SBEDEMTY,C'P'       PURCHASED LOOKUP                             
         LA    RF,PBSPHOOK         SPOT HOOK ROUTINE                            
         ST    RF,SBSPHOOK                                                      
*                                                                               
         L     R2,ARULWK                                                        
         USING SPRULERD,R2                                                      
*                                                                               
         OC    0(4,R2),0(R2)       FIRST TIME                                   
         BNZ   PROC06                                                           
*                                                                               
         MVC   SPRUABUY,SBAIO1     A(BUY RECORD)                                
         LA    RF,RULHOOK                                                       
         ST    RF,SPRUHOOK         CALLERS HOOK ROUTINE                         
         MVC   SPRUACOM,ACOMFACS   A(COMFACS)                                   
         MVC   SPRUUNTM,UNTIME     A(UNTIME)                                    
         MVC   SPRUMSUN,MSUNPK     A(MSUNPK)                                    
         MVC   SPRUMAST,TWAMASTC   A(MASTD)                                     
         MVC   SPRUBINS,VBINSRCH   A(BINSRCH)                                   
         LA    RF,SBLOCK           A(SBLOCK)                                    
         ST    RF,SPRUASBL         A(SBLOCK)                                    
*                                                                               
         MVC   SPRUAGY,AGYALPHA                                                 
         MVC   SPRUMED,SBMED                                                    
         LA    RF,SBAGYREC                                                      
         MVC   SPRUCTRY,AGYPROF+7-AGYHDR(RF)                                    
*                                                                               
         MVC   SPRUBGPR,BGPROF     PASS PROFILE                                 
         MVI   SPRUCTL,X'FF'   *** CONTROL BYTE                                 
         MVI   SPRUTRCE,0      *** TRACE CONTROL                                
*                                                                               
PROC06   DS    0H                                                               
         L     RF,ACOMFACS          SET ADDRESSES FOR MOBILE                    
         XC    WORK(4),WORK         DONT NEED GETBROAD                          
         MVC   WORK+04(4),CADDAY-COMFACSD(RF)                                   
         MVC   WORK+08(4),CGETDAY-COMFACSD(RF)                                  
         MVC   WORK+12(4),CDATCON-COMFACSD(RF)                                  
         LA    RF,PRCDTLST                                                      
         ST    RF,SBADATE                                                       
         GOTO1 MOBILE,DMCB,(53,QSTART),(5,SBADATE),WORK,0                       
         XC    SBNDATES,SBNDATES                                                
         MVC   SBNDATES+3(1),DMCB      NUMBER OF DATES                          
*                               READ STATION RECORD FOR STATION TYPE            
         MVI   WORK,C'S'                                                        
         MVC   WORK+1(1),SBMED                                                  
         MVC   WORK+2(5),SBSTA                                                  
         CLI   SBSTA+4,C' '                                                     
         BH    *+10                                                             
         MVC   WORK+6(1),SBMED                                                  
         MVC   WORK+7(2),SBAGY                                                  
         MVC   WORK+9(3),SBCLT                                                  
         MVC   WORK+12(3),=3C'0'                                                
         GOTO1 DATAMGR,DMCB,=CL8'DMREAD',=CL8'STATION',WORK,STAIOA              
         LA    RF,STAIOA                                                        
         MVC   SBSTYPE,STYPE-STAREC(RF)                                         
*                                                                               
         GOTO1 SPOTBUY,DMCB,SBLOCK   SPOTBUY CALL                               
         MVI   SPRUMODE,C'B'       PROCESS BUY RECORD                           
         GOTO1 VRULER,DMCB,SPRULERD                                             
*                                                                               
***********************************************************************         
***********************************************************************         
*                                                                               
         L     R2,SBASPTTB         R2=A(SPOT TABLE)                             
         L     R3,SBNSPTEN         R3=N'ENTRIES                                 
         L     R5,SBLSPTEN         R5=L'ENTRY                                   
         LTR   R3,R3                                                            
         BZ    BYX                                                              
         USING SPTTABD,R2                                                       
*                                                                               
BY34     TM    SPTIND,SPTDUMMY     TEST DUMMY ENTRY                             
         BO    BY40                                                             
         OC    SPTADATE(4),SPTADATE     TEST AFFID DATE/TIME                    
         BZ    BY40                                                             
         CLI   SBEPRD,0            YES-TEST PRODUCT FILTER                      
         BE    *+14                                                             
         CLC   SPTPRD1,SBEPRD      YES-CHECK THE PRODUCT                        
         BNE   BY40                                                             
         CLI   SBQBPRD,X'FF'       TEST POL REQUEST                             
         BNE   BY35                                                             
         CLI   SBBPRD,X'FF'        YES-TEST POL DETAILS SET YET                 
         BE    BY38                                                             
         MVI   SBBPRD,X'FF'                                                     
         B     BY36                                                             
*                                                                               
BY35     CLC   SPTPRD1,SBBPRD      TEST CHANGE OF PRODUCT                       
         BE    BY37                                                             
         MVC   SBBPRD,SPTPRD1      YES-SET PRODUCT DETAILS                      
*                                                                               
BY36     ZIC   RE,SBBPRD                                                        
         BCTR  RE,0                                                             
         MH    RE,=Y(PRDBUFFL)                                                  
         L     R1,SBAPRDBF                                                      
         AR    R1,RE                                                            
         USING PRDBUFFD,R1                                                      
         MVC   SBPRD,PBALPH                                                     
         MVC   SBPRDNM,PBNAME                                                   
         MVC   SBBPGR,PBGROUP      SET PRODUCT GROUP                            
         OC    SBBPGR,SBBPGR                                                    
         BNZ   BY37                                                             
         MVC   SBBPGR,=X'9999'                                                  
*                                                                               
BY37     CLI   SBQBPRD,X'FF'       IF NOT POL REQUEST,                          
         BE    BY38                                                             
         CLC   SPTPRD2,SBBPRD2     TEST CHANGE OF SECOND PRD                    
         BE    BY38                                                             
         MVC   SBBPRD2,SPTPRD2     YES                                          
         XC    SBPRD2,SBPRD2                                                    
         CLI   SBBPRD2,0                                                        
         BE    BY38                                                             
         ZIC   RE,SBBPRD2                                                       
         BCTR  RE,0                                                             
         MH    RE,=Y(PRDBUFFL)                                                  
         L     R1,SBAPRDBF                                                      
         AR    R1,RE                                                            
         USING PRDBUFFD,R1                                                      
         MVC   SBPRD2,PBALPH                                                    
         DROP  R1                                                               
*                                                                               
BY38     CLI   MORDER,C'R'         TEST MARKETS SHOULD BE RANKED                
         BNE   BY39                                                             
         GOTO1 SPOTMKRK,DMCB,SBLOCK     YES-GET MARKET'S RANK                   
*                                                                               
BY39     ST    R2,SBACURCH         SBACURCH=A(SPOT TABLE ENTRY)                 
         B     *+8                 ***NOOP DRIVER CALL***                       
         BAS   RE,DRIVIN           CALL DRIVER FOR INPUT                        
*                                                                               
BY40     LA    R2,0(R5,R2)                                                      
         BCT   R3,BY34                                                          
*                                                                               
BYX      MVI   GLOPTS+4,C'N'       TELL DRIVER NOT AFFIDS REPORT NOW            
         CLI   SINGRPT,0                                                        
         BE    XIT                                                              
         MVI   GLOPTS+4,0                                                       
         B     XIT                                                              
*                                                                               
PRCDTLST DS    XL256                                                            
STAIOA   DS    XL512                                                            
*                                                                               
         SPACE 2                                                                
GETDEMOS LR    R0,RE                                                            
         ZIC   RE,0(R1)                                                         
         BCTR  RE,0                                                             
         SLL   RE,8                                                             
         ZIC   RF,BUYKEST                                                       
         BCTR  RF,0                                                             
         AR    RE,RF                                                            
         A     RE,SBAESTTB                                                      
         SR    R1,R1                                                            
         ICM   R1,1,0(RE)          POINTER TO ESTIMATE BUFFER ENTRY             
         BNZ   *+6                                                              
         DC    H'0'                                                             
         BCTR  R1,0                                                             
         MH    R1,=Y(ESTBUFFL)                                                  
         A     R1,SBAESTBF                                                      
         USING ESTBUFFD,R1                                                      
         MVC   DEMOS,EBDEMOS                                                    
         LR    RE,R0                                                            
         BR    RE                                                               
         DROP  R1,R3                                                            
*                                                                               
         SPACE 2                                                                
***********************************************************************         
*                                                                               
*        PBSPHOOK - SPOT HOOK FOR SPOTBUY                                       
*                                                                               
***********************************************************************         
         SPACE 1                                                                
         DS    0D                                                               
PBSPHOOK NTR1                                                                   
         L     R2,ARULWK                                                        
         USING SPRULERD,R2                                                      
         MVI   SPRUMODE,C'S'       SPOT MODE                                    
         GOTO1 VRULER,DMCB,SPRULERD                                             
*                                                                               
PBSPHX   DS    0H                                                               
         XIT1                                                                   
         DROP  R2                                                               
         SPACE 2                                                                
***********************************************************************         
*                                                                               
*        RUHOOK - CALLBACK ROUTINE FOR SPRULER                                  
*                                                                               
*                 SPRUMSGA(4)    A(MESSAGE)                                     
*                 SPRUMSGL(2)    MESSAGE LENGTH                                 
*                 SPRUMSGT(1)    MESSAGE TYPE                                   
*                                                                               
***********************************************************************         
         SPACE 1                                                                
         DS    0D                                                               
RULHOOK  NTR1                                                                   
         L     R2,ARULWK                                                        
         USING SPRULERD,R2                                                      
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,3,SPRUMSGL      LENGTH OF MESSAGE                             
         BZ    RUHX                                                             
         L     RE,SPRUMSGA        A(MESSAGE)                                    
         LA    R0,P+2                                                           
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         TM    SPRUTRCE,X'08'      PRINT BEFORE SORT?                           
         BZ    RUH04                                                            
         GOTO1 VPRINT,DMCB,P-1,=C'BL01'                                         
         MVC   P,SPACES                                                         
*                                                                               
RUH04    DS    0H                                                               
         B     RUH05                                                            
         DC    64X'00'             *** PATCH ***                                
*                                                                               
RUH05    DS    0H                                                               
         CLI   SPRUMSGT,SPRUMRUQ   DON'T DRIVER RULE OR TRACE RECS              
         BNH   RUH06                                                            
*                                                                               
         CLI   ONESTA,C'Y'         IF DOING ONLY ONE STATION                    
         BNE   RUH05D                                                           
         CLI   SPRUMSGT,SPRUMGLQ    SKIP MARKET LEVEL ERRORS                    
         BE    RUH06                                                            
*                                                                               
RUH05D   DS    0H                                                               
         CLI   MKERROPT,C'Y'       IF SHOWING ONLY ERROR MARKETS                
         BNE   RUH05H              KEEP TRACK OF ERROR STATUS                   
         CLI   SPRUMSGT,SPRUMBSQ   BUY/SPOT ERROR                               
         BNE   *+8                                                              
         MVI   ERRSW,C'Y'          SET HAVE ERROR                               
         CLI   SPRUMSGT,SPRUMGLQ   GLOBAL (MARKET) ERROR                        
         BNE   *+8                                                              
         MVI   ERRSW,C'Y'          SET HAVE ERROR                               
*                                                                               
         CLI   SPRUMSGT,SPRUMSUQ   DAYPART/STATION TYPE TABLE                   
         BL    RUH05H              AND OTHER REPORTS OK                         
         CLI   ERRSW,C'Y'          ONLY IF HAVE HAD ERROR                       
         BNE   RUH06               ELSE SKIP                                    
*                                                                               
RUH05H   DS    0H                                                               
         MVI   ERRSW,C'N'          RESET ERROR SWITCH                           
*                                                                               
         ZIC   R1,SBBPRD            GET PRODUCT GROUP                           
         BCTR  R1,0                                                             
         MH    R1,=Y(PRDBUFFL)                                                  
         A     R1,SBAPRDBF                                                      
         USING PRDBUFFD,R1                                                      
         MVC   SBBPGR,PBGROUP                                                   
         OC    SBBPGR,SBBPGR                                                    
         BNZ   *+10                                                             
         MVC   SBBPGR,=X'9999'                                                  
         DROP  R1                                                               
*                                                                               
         MVI   MYDRIN,C'Y'         SET DOING MY OWN DRIVER INPUTS               
         MVI   RPTNO,1             SET FOR REPORT 1                             
         MVI   GLOPTS+10,C'Y'                                                   
         MVI   GLOPTS+11,C'N'                                                   
         BAS   RE,DRIVIN           EACH LINE IS INPUT TO DRIVER                 
         MVI   MYDRIN,C'N'         NOT MY DRIVER INPUT                          
*                                                                               
RUH06    DS    0H                                                               
RUHX     DS    0H                                                               
         XIT1                                                                   
*                                                                               
***********************************************************************         
*        PRLAST - LASTS                                                         
***********************************************************************         
         SPACE 2                                                                
PRLAST   DS    0H                                                               
         L     R2,ARULWK                                                        
         USING SPRULERD,R2                                                      
         OC    0(4,R2),0(R2)       ANY BUYS?                                    
         BZ    PRLASTX             NO, NO GLOBALS                               
         MVI   SPRUMODE,C'G'                                                    
         MVC   SVBMKTS,SBBMKT      SAVE CURRENT MKT                             
         MVC   SVBMGRS,SBBMGR      AND CURRENT MKT GROUP                        
         MVC   SBBMKT,SVBMKT       RESTORE PREVIOUS MARKET                      
         MVC   SBBMGR,SVBMGR       AND PREVIOUS MARKET GROUP                    
         MVI   MKTIND,X'FF'        3RD BYTE OF MKT (SPILL+NON SPILL)            
         GOTO1 VRULER,DMCB,SPRULERD                                             
*                                                                               
         MVI   ERRSW,C'N'          CLEAR ERROR SWITCH                           
         B     PRLAST4             NOOP REPORT 2 (RECAP)                        
*                                                                               
         MVI   ERRSW,C'Z'          DUMMY SETTING FOR RECAP ERROR TEST           
*                                                                               
         MVI   MYDRIN,C'Y'         SET DOING MY OWN DRIVER INPUTS               
         MVI   RPTNO,2             SET FOR REPORT 2 - RECAP                     
         MVI   GLOPTS+11,C'Y'                                                   
         MVI   GLOPTS+10,C'N'                                                   
         BAS   RE,DRIVIN           EACH LINE IS INPUT TO DRIVER                 
         MVI   MYDRIN,C'N'         NOT MY DRIVER INPUT                          
*                                                                               
PRLAST4  DS    0H                                                               
         MVC   SBBMKT,SVBMKTS      RESTORE CURRENT MARKET                       
         MVC   SBBMGR,SVBMGRS      AND CURRENT MARKET GROUP                     
*                                                                               
PRLASTX  DS    0H                                                               
         XIT1                                                                   
         DROP  R2                                                               
         EJECT                                                                  
* DRIVER INPUT ROUTINE                                                          
*                                                                               
         L     R4,AGLOBAL                                                       
         USING GLOBALD,R4                                                       
DRIVIN   LR    R0,RE                                                            
         MVI   GLMODE,GLINPUT                                                   
         BAS   RE,GODRIVER                                                      
*                                                                               
DRIVINX  LR    RE,R0                                                            
         BR    RE                                                               
         EJECT                                                                  
* DRIVER HOOK                                                                   
*                                                                               
DRHOOK   L     R4,AGLOBAL                                                       
         USING GLOBALD,R4                                                       
         L     R5,ARULWK                                                        
         USING SPRULDD,R5                                                       
*                                                                               
         CLI   GLHOOK,GLINIT       INITIALIZATION                               
         BE    DRVINIT                                                          
         CLI   GLHOOK,GLRESOLV     RESOLVE ADDRESSES                            
         BE    RESOLVE                                                          
         CLI   GLHOOK,GLROUT       EXECUTE ROUTINES                             
         BE    EXEC                                                             
         CLI   GLHOOK,GLINCOMP     INTERNAL COMPUTES                            
         BE    INTCOMP                                                          
         CLI   GLHOOK,GLPRINT      PRINT A LINE                                 
         BE    PRINT                                                            
         CLI   GLHOOK,GLFIRST      FIRSTS                                       
         BE    FIRSTS                                                           
         CLI   GLHOOK,GLLAST       LASTS                                        
         BE    LASTS                                                            
         CLI   GLHOOK,GLHEAD       HEADHOOK                                     
         BE    HEAD                                                             
         CLI   GLHOOK,GLPUTSRT     PUT RECORD OR NOT                            
         BE    PUTSRT                                                           
         CLI   GLHOOK,GLOUTPUT     BEFORE START OF OUTPUT                       
         BE    PRLAST              FINISH UP LAST MARKET                        
*                                                                               
DRHOOKX  B     XIT                                                              
         SPACE 2                                                                
* DRIVER INITIALIZATION                                                         
*                                                                               
DRVINIT  DS    0H                                                               
         MVC   GLTRACE,TRCSW       TRACE SWITCH                                 
         MVI   GLBOXOPT,C'N'       NO BOXES                                     
         OI    GLINDS3,GLNSPAM     NO SPACE AFTER MIDLINES                      
*                                                                               
         MVI   GLOPTS+10,C'Y'      ACTIVATE REPORTS FOR INIT                    
         MVI   GLOPTS+11,C'Y'                                                   
*                                                                               
         B     XIT                                                              
* DRIVER HOOK ROUTINE TO RESOLVE ADDRESSES                                      
*                                                                               
RESOLVE  LA    R1,RTNLIST          SEARCH LIST FOR ROUTINE NAME                 
*                                                                               
RESOLVE2 CLI   0(R1),FF                                                         
         BE    XIT                                                              
         CLC   0(8,R1),GLLABEL                                                  
         BE    *+12                                                             
         LA    R1,12(R1)                                                        
         B     RESOLVE2                                                         
         MVC   GLAROUT,8(R1)       YES-RETURN ADDRESS                           
         B     XIT                                                              
         SPACE 1                                                                
RTNLIST  DS    0F                                                               
         DC    CL8'OMGR1USR',A(OMGR1)                                           
         DC    CL8'OMGR2USR',A(OMGR2)                                           
         DC    CL8'OMGR3USR',A(OMGR3)                                           
         DC    CL8'OMKTUSR1',A(OMKT1)                                           
         DC    CL8'ITXT    ',A(ITXT)                                            
         DC    CL8'OTXT    ',A(OTXT)                                            
         DC    CL8'IRCOUNT ',A(IRCOUNT)                                         
         DC    CL8'ILINKEY ',A(ILINKEY)                                         
         DC    CL8'OLINKEY ',A(OLINKEY)                                         
         DC    CL8'ILTYP1  ',A(ILTYP1)                                          
         DC    CL8'ILTYP2  ',A(ILTYP2)                                          
         DC    CL8'IRCPERR ',A(IRCPERR)                                         
         DC    CL8'ORCPERR ',A(ORCPERR)                                         
         DC    X'FF'                                                            
         EJECT                                                                  
* INTERNAL COMPUTE HOOKS                                                        
*                                                                               
INTCOMP  DS    0H                                                               
         B     XIT                                                              
         SPACE 2                                                                
* PUT RECORD TO SORT OR NOT                                                     
*                                                                               
PUTSRT   DS    0H                                                               
         MVI   GLHOOK,0                                                         
         CLI   MYDRIN,C'Y'         ONLY MY OWN DRIVER INS                       
         BE    *+12                                                             
         MVI   GLHOOK,GLDONT                                                    
         B     XIT                                                              
         CLC   GLRECNO,RPTNO       TEST RIGHT REQUEST                           
         BE    XIT                                                              
         MVI   GLHOOK,GLDONT                                                    
         B     XIT                                                              
         SPACE 2                                                                
* DRIVER HOOK TO EXECUTE ROUTINES                                               
*                                                                               
EXEC     L     R2,GLAIFLD          R2=A(INPUT)                                  
         L     R3,GLAOFLD          R3=A(OUTPUT)                                 
         L     RF,GLAROUT          BRANCH TO ROUTINE                            
         BR    RF                                                               
         EJECT                                                                  
* MARKET GROUP OUTPUT ROUTINES                                                  
*                                                                               
OMGR1    LA    R4,MGR1HEAD                                                      
         MVC   MGR1HEAD,SPACES                                                  
         MVC   0(12,R4),SBMGR1BK                                                
         MVC   13(1,R4),SBQMGRD                                                 
         LA    R1,SBMGR1LN                                                      
         BAS   RE,OGRPCODE                                                      
         CLC   19(L'UNKNOWN,R4),UNKNOWN                                         
         BE    XIT                                                              
         MVC   SBBMGR,0(R2)                                                     
         GOTO1 GETMGRNM,SBMGR1NM                                                
         MVC   19(24,R4),SBMGR1NM                                               
         B     XIT                                                              
*                                                                               
OMGR2    LA    R4,MGR2HEAD                                                      
         MVC   MGR2HEAD,SPACES                                                  
         MVC   0(12,R4),SBMGR2BK                                                
         MVC   13(1,R4),SBQMGRD                                                 
         LA    R1,SBMGR2LN                                                      
         BAS   RE,OGRPCODE                                                      
         CLC   19(L'UNKNOWN,R4),UNKNOWN                                         
         BE    XIT                                                              
         MVC   SBBMGR,0(R2)                                                     
         GOTO1 GETMGRNM,SBMGR2NM                                                
         MVC   19(24,R4),SBMGR2NM                                               
         B     XIT                                                              
*                                                                               
OMGR3    LA    R4,MGR3HEAD                                                      
         MVC   MGR3HEAD,SPACES                                                  
         MVC   0(12,R4),SBMGR3BK                                                
         MVC   13(1,R4),SBQMGRD                                                 
         LA    R1,SBMGR3LN                                                      
         BAS   RE,OGRPCODE                                                      
         CLC   19(L'UNKNOWN,R4),UNKNOWN                                         
         BE    XIT                                                              
         MVC   SBBMGR,0(R2)                                                     
         GOTO1 GETMGRNM,SBMGR3NM                                                
         MVC   19(24,R4),SBMGR3NM                                               
         B     XIT                                                              
*                                                                               
OGRPCODE UNPK  DUB(5),0(3,R2)                                                   
         ZIC   RF,0(R1)                                                         
         BCTR  RF,0                                                             
         EX    RF,OGRPMVC                                                       
         EX    RF,OGRPCLC                                                       
         BNE   *+10                                                             
         MVC   19(L'UNKNOWN,R4),UNKNOWN                                         
         BR    RE                                                               
*                                                                               
OGRPMVC  MVC   14(0,R4),DUB                                                     
OGRPCLC  CLC   14(0,R4),=C'9999'                                                
UNKNOWN  DC    C'** UNKNOWN **'                                                 
         EJECT                                                                  
* MARKET OUTPUT ROUTINE FOR REPORTS                                             
*                                                                               
OMKT1    MVC   SBBMKT,0(R2)                                                     
         EDIT  SBBMKT,(4,SBMKT),FILL=0                                          
         GOTO1 GETMKTNM                                                         
         MVC   0(6,R3),=C'MARKET'                                               
         MVC   9(4,R3),SBMKT                                                    
         MVC   16(24,R3),SBMKTNM                                                
         B     XIT                                                              
         SPACE 2                                                                
* TEXT INPUT ROUTINE                                                            
*                                                                               
ITXT     DS    0H                                                               
         MVC   0(2,R2),SPRUMTKY    RULE TYPE, TABLE LEVEL ETC                   
         L     R1,SPRUMSGA                                                      
         MVC   2(100,R2),0(R1)                                                  
         B     ITXT2                                                            
         DC    64X'00'             *** PATCH ***                                
*                                                                               
ITXT2    DS    0H                                                               
**                                                                              
         B     XIT                                                              
         SPACE 2                                                                
* LINE TYPE 1                                                                   
*                                                                               
ILTYP1   DS    0H                                                               
         MVC   0(1,R2),SPRUMSGT    MESSAGE (LINE) TYPE                          
         B     XIT                                                              
         SPACE 2                                                                
* LINE TYPE 2                                                                   
*                                                                               
ILTYP2   DS    0H                                                               
         MVC   0(2,R2),SPRUMTKY    TYPE 2, 'KEY' FOR TABLE ENTRIES              
         B     XIT                                                              
         SPACE 2                                                                
* TEXT OUTPUT ROUTINE                                                           
*                                                                               
OTXT     DS    0H                                                               
         CLI   MKBGROPT,C'Y'                                                    
         BNE   OTXT2                                                            
         L     RF,ARULWK                                                        
         L     RF,VBGSTAT-SPRULERD(RF)                                          
         GOTO1 (RF),DMCB,(RC),(R2),VRECUP  POST TO STATUS RECS                  
OTXT2    DS    0H                                                               
         MVC   0(100,R3),2(R2)      TEXT AT +2                                  
         B     XIT                                                              
         SPACE 2                                                                
* RECORD COUNT INPUT ROUTINE                                                    
*                                                                               
IRCOUNT  DS    0H                                                               
         LH    R1,RCOUNT           BUMP COUNTER                                 
         LA    R1,1(R1)                                                         
         STH   R1,RCOUNT                                                        
         MVC   0(2,R2),RCOUNT      THEN PASS TO DRIVER                          
         B     XIT                                                              
         SPACE 2                                                                
* LINE KEY INPUT ROUTINE                                                        
*                                                                               
* NOTE- CHANGES HERE MAY REQUIRE CHANGE IN LENGTHS IN 05 PHASE                  
*                                                                               
ILINKEY  DS    0H                                                               
         L     R5,SBAIO1                                                        
         USING BUYRECD,R5                                                       
         L     RF,ARULWK                                                        
         MVC   0(5,R2),XFF              FF'S FOR GLOBALS                        
         MVC   5(1,R2),SPRUMSGT-SPRULERD(RF)   LINE TYPE                        
         CLI   SPRUMSGT-SPRULERD(RF),SPRUMGLQ                                   
         BNL   ILK04                                                            
*                                                                               
         USING ILINKD,R2                                                        
         MVC   ILEST,BUYKEST      ESTIMATE                                      
         MVC   ILSTAT,BUYMSTA+2   STATION                                       
         MVC   ILLINE,BUYKEY+10   LINE                                          
         MVC   ILSTDT,BDSTART     START DATE                                    
         MVC   ILENDT,BDEND       END DATE                                      
         MVC   ILNOWK,BDNOWK      NPW                                           
         MVC   ILDPT,BDDAYPT      DAYPART                                       
         MVC   ILSEC,BDSEC        SPOT LENGTH                                   
         MVC   ILDAY,BDDAY        DAY                                           
         MVC   ILSEDAY,BDSEDAY    START-END DAY                                 
         MVC   ILTIMES,BDTIMST    START-END TIMES                               
         MVC   ILPROG,BDPROGRM    PROG                                          
         MVC   ILDISK,SBRECDA     DISK ADDRESS                                  
         DROP  R2                                                               
*                                                                               
ILK04    DS    0H                                                               
         B     XIT                                                              
         DROP  R5                                                               
         SPACE 2                                                                
* LINE KEY OUTPUT ROUTINE                                                       
*                                                                               
* NOTE- CHANGES HERE MAY REQUIRE CHANGE IN LENGTHS IN 05 PHASE                  
*                                                                               
OLINKEY  DS    0H                                                               
         LA    RF,198*2            CLEAR 2 LINES                                
         LR    RE,R3                                                            
         SR    R0,R0                                                            
         L     R1,=X'40000000'     PAD=SPACES                                   
         MVCL  RE,R0                                                            
*                                                                               
         CLI   5(R2),SPRUMGLQ   MARKET LEVEL ERROR                              
         BNE   OLK03                                                            
         MVC   0(19,R3),=C'** MARKET ERRORS **'                                 
         CLI   GLBOXOPT,C'N'       IF NO BOXES, UNDERLINE                       
         BNE   *+10                                                             
         MVC   198(19,R3),DASHES                                                
         B     XIT                                                              
*                                                                               
OLK03    DS    0H                                                               
         CLI   5(R2),SPRUMSUQ   'MARKET SUMMARY'                                
         BNE   OLK04                                                            
         MVC   0(20,R3),=C'** MARKET SUMMARY **'                                
         CLI   GLBOXOPT,C'N'       IF NO BOXES, UNDERLINE                       
         BNE   *+10                                                             
         MVC   198(20,R3),DASHES                                                
         CLI   MKBGROPT,C'Y'                                                    
         BNE   XIT                                                              
         L     RF,ARULWK                                                        
         L     RF,VBGSTAT-SPRULERD(RF)                                          
         GOTO1 (RF),DMCB,(RC),(R3),VRECUP   STATUS RECORDS HERE                 
*                                        (BGSTAT WILL KNOW WHAT TO DO)          
         B     XIT                                                              
*                                                                               
OLK04    DS    0H                                                               
         CLI   5(R2),SPRUMDSQ   DPT/STYPE TABLE                                 
         BNE   OLK06                                                            
         BAS   RE,GETDNAM          GET DEMO NAME INTO WORK                      
         MVC   00(43,R3),DSTHEAD                                                
         MVC   22(7,R3),WORK                                                    
         CLI   GLBOXOPT,C'N'       IF NO BOXES, UNDERLINE                       
         BNE   XIT                                                              
         MVC   198+00(07,R3),DASHES                                             
         MVC   198+09(12,R3),DASHES                                             
         MVC   198+22(07,R3),WORK+7                                             
         MVC   198+32(05,R3),DASHES                                             
         MVC   198+38(05,R3),DASHES                                             
         B     XIT                                                              
*                                                                               
DSTHEAD  DC    CL43'DAYPART  STATION TYPE RATING      %   SPOTS'                
*                                                                               
OLK06    DS    0H                                                               
         CLI   5(R2),SPRUMPGQ   PROGRAM TABLE                                   
         BNE   OLK07                                                            
         BAS   RE,GETDNAM          GET DEMO NAME INTO WORK                      
         MVC   00(07,R3),=C'PROGRAM'                                            
*                                                                               
         MVC   22(7,R3),WORK                                                    
         MVC   34(09,R3),=C'%   SPOTS'                                          
         CLI   GLBOXOPT,C'N'       IF NO BOXES, UNDERLINE                       
         BNE   XIT                                                              
         MVC   198+00(20,R3),DASHES                                             
         MVC   198+22(07,R3),WORK+7                                             
         MVC   198+32(05,R3),DASHES                                             
         MVC   198+38(05,R3),DASHES                                             
         B     XIT                                                              
*                                                                               
OLK07    DS    0H                                                               
         CLI   5(R2),SPRUMPSQ   PROGRAM/STATION TABLE                           
         BNE   OLK08                                                            
         MVC   0(29,R3),=C'PROGRAM               STATION'                       
         CLI   GLBOXOPT,C'N'       IF NO BOXES, UNDERLINE                       
         BNE   XIT                                                              
         MVC   198+00(20,R3),DASHES                                             
         MVC   198+22(07,R3),DASHES                                             
         B     XIT                                                              
*                                                                               
OLK08    DS    0H                  OTHERWISE SPOT/BUY ERROR                     
         USING ILINKD,R2                                                        
         USING OLINKD,R3                                                        
         ST    R3,SVOUT            SAVE OUTPUT POINTER                          
*                                                                               
OLK10    DS    0H                                                               
         MVC   OLDESC,=C'BUY='                                                  
         MVC   DUB(3),ILSTAT                STATION                             
         GOTO1 MSUNPK,DMCB,(X'80',DUB-2),WORK,WORK+4                            
         MVC   OLSTAT,WORK+4               STATION                              
         LA    R3,15(R3)                                                        
         CLI   0(R3),C' '                                                       
         BH    *+8                                                              
         BCT   R3,*-8                                                           
         LA    R3,2(R3)                                                         
         USING OLESTLIN,R3                                                      
         LA    R5,OLESTLIN                                                      
         EDIT  (B1,0(R2)),(3,0(R5)),FILL=0  ESTIMATE                            
         MVI   03(R5),C'-'                                                      
         EDIT  (B1,4(R2)),(3,4(R5)),FILL=0   LINE NUMBER                        
*                                                                               
         LA    R5,OLSTENDT                                                      
         GOTO1 DATCON,DMCB,(3,ILSTDT),(4,0(R5))                                 
         CLC   ILENDT,ILSTDT       IF START AND END THE SAME                    
         BE    OLK12               DON'T SHOW END                               
         MVI   5(R5),C'-'                                                       
         GOTO1 (RF),(R1),(3,ILENDT),(4,6(R5))                                   
OLK12    DS    0H                                                               
         EDIT  (B1,ILNOWK),(2,OLNOWK),ZERO=NOBLANK                              
         MVC   OLNOWK+2(3),=C'/WK'                                              
*                                                                               
         GOTO1 DAYUNPK,(R1),(ILSEDAY,ILDAY),(0,OLDAYS)                          
         GOTO1 UNTIME,(R1),ILTIMES,OLTIMES                                      
         MVC   OLDPTSL(1),ILDPT                                                 
         MVI   OLDPTSL+1,C'/'                                                   
         LA    R5,OLDPTSL+2                                                     
         EDIT  (B1,ILSEC),(2,0(R5)),ALIGN=LEFT                                  
         MVC   OLPROG,ILPROG                                                    
*                                                                               
         CLI   GLBOXOPT,C'N'       IF NO BOXES, UNDERLINE                       
         BNE   XIT                                                              
         L     R3,SVOUT                                                         
         LA    R5,OLINKL(R3)                                                    
         CLI   0(R5),C' '                                                       
         BH    *+8                                                              
         BCT   R5,*-8                                                           
         SR    R5,R3                                                            
         MVI   198(R3),C'-'                                                     
         BCTR  R5,0                                                             
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   198+1(0,R3),198(R3)                                              
*                                                                               
         CLI   MKBGROPT,C'Y'                                                    
         BNE   XIT                                                              
         L     RF,ARULWK                                                        
         L     RF,VBGSTAT-SPRULERD(RF)                                          
         GOTO1 (RF),DMCB,(RC),(R3),VRECUP  POST TO STATUS RECS                  
*                                                                               
         B     XIT                                                              
         DROP  R2,R3                                                            
*                                                                               
***********************************************************************         
*        IRCPERR - RECAP ERROR INPUT                                            
***********************************************************************         
         SPACE 1                                                                
IRCPERR  DS    0H                                                               
         MVC   0(1,R2),ERRSW       FOR NOW JUST SIMPLE YES/NO                   
         B     XIT                                                              
*                                                                               
***********************************************************************         
*        ORCPERR - RECAP ERROR OUTPUT                                           
***********************************************************************         
         SPACE 1                                                                
ORCPERR  DS    0H                                                               
         MVC   0(1,R3),0(R2)    FOR NOW JUST MOVE INTERNAL TO OUTPUT            
         B     XIT                                                              
*                                                                               
         EJECT                                                                  
* PRINT A LINE                                                                  
*                                                                               
PRINT    DS    0H                                                               
         B     XIT                 PATCH TO ACTIVATE                            
*                                                                               
         EJECT                                                                  
* DRIVER HOOK FIRSTS                                                            
*                                                                               
FIRSTS   DS    0H                                                               
         CLC   GLARGS(1),MKTLEV                                                 
         BE    MKTFST                                                           
         B     XIT                                                              
         SPACE 2                                                                
* DRIVER HOOK LASTS                                                             
*                                                                               
LASTS    DS    0H                                                               
         B     XIT                                                              
         SPACE 2                                                                
* DRIVER HEADHOOK                                                               
*                                                                               
HEAD     DS    0H                                                               
         L     R1,AH4              MKT GROUPS IN HEAD 5, 6, AND 7               
         LA    R1,50(R1)           AT COLUMN 50                                 
         A     R1,PWIDTH                                                        
         MVC   0(43,R1),MGR1HEAD                                                
         A     R1,PWIDTH                                                        
         MVC   0(43,R1),MGR2HEAD                                                
         A     R1,PWIDTH                                                        
         MVC   0(43,R1),MGR3HEAD                                                
*                                                                               
         B     XIT                                                              
         SPACE 2                                                                
* ROUTINE TO HOOK TO CALLING PROGRAM TO CALL DRIVER                             
*                                                                               
GODRIVER NTR1                                                                   
         L     RF,ADRIVER                                                       
         L     RE,SAVERD                                                        
         LM    R0,RC,20(RE)                                                     
         BASR  RE,RF                                                            
         XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
*        MKTFST - MARKET FIRST ROUTINE (OUTPUT)                       *         
***********************************************************************         
         SPACE 2                                                                
MKTFST   DS    0H                                                               
         BAS   RE,CALLAUTH         CALL SPAUTH                                  
         B     XIT                                                              
         EJECT                                                                  
*        CALLAUTH - CALL SPAUTH TO PUT THE DATE                                 
*                                                                               
CALLAUTH NTR1                                                                   
         CLC   MYEST,SBBEST         DID EST CHANGE                              
         BE    CALL10               NO, GOTO SPAUTH ROUTINE                     
*                                                                               
         MVC   MYEST,SBBEST                                                     
         XC    SVEKEY,SVEKEY        YES, CHECK THE FLAG                         
         LA    R2,SVEKEY                                                        
         USING ESTHDRD,R2                                                       
         MVC   EKEYAM,SBBAGYMD                                                  
         MVC   EKEYCLT,SBBCLT                                                   
         MVC   EKEYPRD,SBPRD                                                    
         MVC   EKEYEST,SBBEST                                                   
*                                                                               
         GOTO1 DATAMGR,DMCB,=C'DMRDHI',=C'SPDIR',SVEKEY,SBAIO2                  
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R2,SBAIO2                                                        
         CLC   SVEKEY,EKEY                                                      
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         GOTO1 DATAMGR,DMCB,=C'GETREC',=C'SPFIL',SVEKEY,SBAIO2                  
         TM    EFLAG1,EF1SDE       PIGGYBACK AUTHORIZATION OPEN?                
         BZ    CALLX               NO, GET OUT                                  
         DROP  R2                                                               
*                                                                               
         PUSH  USING                                                            
         USING SPAUTHD,WORK                                                     
CALL10   XC    SPAUTHD(SPAUTHLQ),SPAUTHD                                        
         MVC   SPACOM,ACOMFACS                                                  
         MVC   SPAKAM,SBBAGYMD                                                  
         MVC   SPAKCLT,SBBCLT                                                   
         MVC   SPAKPRD,SBBPRD                                                   
         MVC   SPAKPRD2,SBBPRD2                                                 
         MVC   SPAKEST,SBBEST                                                   
         MVC   SPAKMKT,SBBMKT                                                   
         MVC   SPAIO,SBAIO2        USE ADDR OF BUY REC                          
         MVI   SPAUPDT,SPAUPDBG    UPDATE  BG REPORT DATE                       
         GOTO1 DATCON,DMCB,SBQSTART,(2,SPASDTE)                                 
         GOTO1 DATCON,DMCB,SBQEND,(2,SPAEDTE)                                   
*                                                                               
         GOTO1 =V(SPAUTH),WORK,RR=YES                                           
         CLI   SPAERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
CALLX    XIT1                                                                   
         POP   USING                                                            
         EJECT                                                                  
*        GETDNAM - GET DMEO NAME                                                
*                                                                               
GETDNAM  NTR1                                                                   
         ZIC   RE,SBBPRD           PROD                                         
         BCTR  RE,0                MINU 1                                       
         SLL   RE,8                TIMES 256                                    
         ZIC   R0,SBBEST                                                        
         BCTR  R0,0                                                             
         AR    RE,R0               PLUST EST MINUS 1                            
         A     RE,SBAESTTB         PLUS A(TABLE) FINDS POINTER                  
         CLI   0(RE),0             TEST PRESENT                                 
         BNE   GDNM04                                                           
         LA    RE,1(RE)            IF NOT FOUND AT ONCE SHOULD BE ABLE          
         B     *-12                TO FIND LOWEST ACTIVE ESTIMATE               
*                                  (IN EST=NO OR RANGE SITUATIONS)              
GDNM04   DS    0H                                                               
         L     R1,SBAESTBF         A(EST BUFFER)                                
         ZIC   RF,0(RE)            INDEX                                        
         BCTR  RF,0                                                             
         MH    RF,=Y(ESTBUFFL)                                                  
         AR    R1,RF                                                            
*                                                                               
         MVC   FULL(3),EBDEMOS-ESTBUFFD(R1)   GET FIRST DEMO                    
*                                  NOW LOOK FOR NAME                            
         LA    R0,NMYDEMOS         MIGHT HAVE FOUND NAME ALREADY                
         L     R2,=A(MYDEMOS)                                                   
*                                                                               
GDNM2    OC    0(3,R2),0(R2)                                                    
         BZ    GDNM4                                                            
         CLC   0(3,R2),FULL                                                     
         BE    GDNM6               NAME FOUND                                   
         LA    R2,10(R2)                                                        
         BCT   R0,GDNM2                                                         
         L     R2,=A(MYDEMOS)                                                   
         XC    0(L'MYDEMOS,R2),0(R2)                                            
*                                                                               
GDNM4    MVC   0(3,R2),FULL        GET THE NAME                                 
*                                                                               
         L     R5,=A(DBAREA)                                                    
         USING DBLOCKD,R5                                                       
         XC    DBLOCK,DBLOCK                                                    
         MVI   DBFUNCT,DBGETDEM                                                 
         MVI   DBTPTT,C'T'                                                      
         MVC   DBCOMFCS,SBCOMFAC                                                
         MVC   DBFILE,=C'TP '                                                   
         MVI   DBSELMED,C'T'                                                    
         LA    R1,SBAGYREC                                                      
         USING AGYHDR,R1                                                        
         CLI   AGYPROF+7,C'C'                                                   
         BNE   *+8                                                              
         MVI   DBSELMED,C'C'                                                    
         GOTO1 DEMOCON,DMCB,(1,FULL),(2,3(R2)),(C'S',(R5)),0                    
         DROP  R1,R5                                                            
*                                                                               
GDNM6    LA    R5,3(R2)            R5=A(DEMO NAME)                              
*                                                                               
GDNM10   MVC   WORK+20(7),0(R5)                                                 
         LA    R2,WORK             ALIGN IN WORK(7) AND UNDERLINE               
         LA    R3,WORK+26          IN WORK+7(7)                                 
         MVC   WORK(7),SPACES                                                   
*                                                                               
GDNM12   DS    0H                                                               
         CLI   0(R3),C' '                                                       
         BH    *+12                                                             
         LA    R2,1(R2)                                                         
         BCT   R3,*-12                                                          
*                                                                               
         MVC   0(7,R2),WORK+20                                                  
         MVC   WORK+7,SPACES                                                    
         MVC   7(7,R2),DASHES                                                   
*                                                                               
GDNMX    DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
* WORKING STORAGE                                                               
*                                                                               
*                                                                               
NSPOTS   DS   F                                                                 
EXTRALEN DS   F                                                                 
LGRPS    DS   F                                                                 
RCOUNT   DS   H                                                                 
PUTSW    DS   CL1                                                               
TRCSW    DS    CL1                                                              
MYDRIN   DS    CL1                                                              
RPTNO    DS    XL1                                                              
ERRSW    DS    CL1                                                              
MKERROPT DS    CL1                                                              
MKBGROPT DS    CL1                                                              
ONESTA   DS    CL1                                                              
*                                                                               
SUPPRESS DS    CL1                                                              
WITHIN   DS    CL1                                                              
SINGRPT  DS    XL1                                                              
MORDER   DS    CL1                                                              
VAROPT   DS    CL1                                                              
DPTOPT   DS    CL1                                                              
DPTLEV   DS    CL1                                                              
*                                                                               
AUPGHDR  DS    F                   A(UPGRADE FIELD HEADER)                      
*                                                                               
SVPRD    DS    XL1                                                              
SVEST    DS    XL1                                                              
SVBMKT   DS    XL2                                                              
SVBMKTS  DS    XL2                                                              
SVBMGR   DS    XL2                                                              
SVBMGRS  DS    XL2                                                              
SVMKTIND DS    CL(L'MKTIND)                                                     
SVMAXTLV DS    CL(L'MAXTOTLV)                                                   
SVBUYKEY DS    XL(L'BUYKEY)                                                     
*                                                                               
MYEST    DS    XL1                                                              
SVEKEY   DS    XL13                                                             
*                                                                               
DEMOS    DS    XL(L'EBDEMOS)                                                    
POLDEMOS DS    XL12                                                             
NPOLDEMS DS    XL1                                                              
BGPROF   DS    XL16                                                             
*                                                                               
DASHES   DC    CL20'--------------------'                                       
XFF      DC    XL16'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'                           
FF       EQU   X'FF'                                                            
*                                                                               
MGR1HEAD DS    CL43                                                             
MGR2HEAD DS    CL43                                                             
MGR3HEAD DS    CL43                                                             
*                                                                               
MYDEMOS  DS    XL80                                                             
NMYDEMOS EQU   8                                                                
*                                                                               
DBAREA   DS    XL(L'DBLOCK)                                                     
*                                                                               
*                                                                               
         SPACE 1                                                                
T20427   CSECT                                                                  
         DS    0F                                                               
         SPACE 2                                                                
* EXTENSION ROUTINES                                                            
*                                                                               
T20427X  NMOD1 0,**427X**,RA                                                    
         L     RC,AGEND                                                         
         USING GEND,RC                                                          
         SRL   RF,24                                                            
         SLL   RF,2                                                             
         B     *+4(RF)                                                          
*                                                                               
         B     VALBOOK                                                          
         B     GETDPTTB                                                         
         B     HEDDEM                                                           
*                                                                               
XIT2     XIT1  ,                                                                
         EJECT                                                                  
*                                                                               
* VALIDATE BOOK FIELD                                                           
* INPUT :  R2=A(FIELD HEADER)                                                   
*          R3=A(OUTPUT BOOK YYMM)                                               
* OUTPUT:  FULL=A(UPGRADE FIELD HEADER)                                         
*                                                                               
VALBOOK  MVC   0(4,R3),=C'ACT '                                                 
         CLI   5(R2),0                                                          
         BE    VBOOKX                                                           
         CLC   8(3,R2),=C'ACT '                                                 
         BNE   VBOOK2                                                           
         CLI   5(R2),3                                                          
         BE    VBOOKX                                                           
         CLI   5(R2),6             TEST ACT/YY                                  
         BNE   EBK                                                              
         CLI   11(R2),C'/'                                                      
         BNE   EBK                                                              
         CLI   12(R2),C'0'                                                      
         BL    EBK                                                              
         CLI   13(R2),C'0'                                                      
         BL    EBK                                                              
         PACK  DUB,12(2,R2)        YES-                                         
         CVB   RE,DUB                                                           
         STC   RE,3(R3)                                                         
         OI    3(R3),X'80'         INDICATE YEAR                                
         B     VBOOKX                                                           
*                                                                               
VBOOK2   GOTO1 DATVAL,DMCB,(2,8(R2)),DUB                                        
         OC    0(4,R1),0(R1)                                                    
         BZ    VBOOK4                                                           
         MVC   0(4,R3),DUB                                                      
         B     VBOOKX                                                           
*                                                                               
VBOOK4   OC    SBQUPGRD,SBQUPGRD   TEST UPGRADE ALREADY REFINED                 
         BNZ   EBK                 YES-INVALID BOOK                             
         BAS   RE,VALUPG           VALIDATE FOR UPGRADE EXPRESSION              
         ST    R2,FULL             PASS A(UPGRADE FIELD HEADER)                 
         XC    0(4,R3),0(R3)       CLEAR THE BOOK FIELD                         
*                                                                               
VBOOKX   B     XIT2                                                             
         EJECT                                                                  
* VALIDATE UPGRADE EXPRESSION                                                   
* R2=A(FIELD HEADER)                                                            
*                                                                               
VALUPG   NTR1  ,                                                                
         XC    SBQUPVAL(SBQUPL),SBQUPVAL                                        
         GOTO1 SCANNER,DMCB,(20,(R2)),BLOCK,C',=,='                             
         SR    R5,R5                                                            
         ICM   R5,1,4(R1)          R5=N'SCANNER LINES                           
         BZ    EBK                                                              
         MVI   BYTE,0              FIELD INPUT INDICATORS                       
         LA    R3,BLOCK                                                         
*                                                                               
VALUPG1  SR    R1,R1                                                            
         ICM   R1,1,0(R3)          TEST L'FIRST HALF OF ENTRY                   
         BZ    EBK                                                              
         CLI   0(R3),L'UPGNAME                                                  
         BH    EBK                                                              
         CLI   1(R3),0             TEST L'SECOND HALF OF ENTRY                  
         BE    EBK                                                              
         BCTR  R1,0                R1=L'FIRST HALF OF INPUT-1                   
         LA    R4,UPGTAB                                                        
         USING UPGTABD,R4          R4=A(UPGRADE TABLE)                          
*                                                                               
VALUPG2  CLI   UPGNAME,0           TEST E-O-T                                   
         BE    EBK                                                              
         CLI   UPGSHRT,C' '        TEST ENTRY HAS SHORT KEYWORD                 
         BE    VALUPG4                                                          
         LA    RE,2                                                             
         CLI   UPGSHRT+2,C' '                                                   
         BNE   *+6                                                              
         BCTR  RE,0                                                             
         CLI   UPGSHRT+1,C' '                                                   
         BNE   *+6                                                              
         BCTR  RE,0                                                             
         CR    R1,RE               TEST INPUT LEN = SHORT NAME LEN              
         BNE   VALUPG4                                                          
         EX    R1,*+8              YES - MATCH ON SHORT NAME                    
         B     *+10                                                             
         CLC   UPGSHRT(0),12(R3)                                                
         BE    VALUPG6                                                          
*                                                                               
VALUPG4  EX    R1,*+8              MATCH ON LONG NAME                           
         B     *+10                                                             
         CLC   UPGNAME(0),12(R3)                                                
         BE    VALUPG6                                                          
         LA    R4,UPGTABL(R4)      BUMP TO NEXT UPGRADE TABLE ENTRY             
         B     VALUPG2                                                          
*                                                                               
VALUPG6  MVC   HALF(1),BYTE        TEST KEYWORD INPUT PREVIOUSLY                
         NC    HALF(1),UPGIND1                                                  
         BNZ   EUPG                                                             
         XC    WORK,WORK                                                        
         ZIC   R1,1(R3)                                                         
         EX    R1,*+4                                                           
         MVC   WORK+8(0),22(R3)                                                 
         STC   R1,WORK+5                                                        
         LA    R1,8(R1)                                                         
         STC   R1,WORK                                                          
         OC    BYTE,UPGIND1        SET THIS KEYWORD INPUT                       
         SR    RF,RF                                                            
         ICM   RF,3,UPGROUT                                                     
         LA    RF,T20427X(RF)      RF=A(VALIDATION ROUTINE)                     
         BASR  RE,RF                                                            
         LA    R3,22+20(R3)                                                     
         BCT   R5,VALUPG1                                                       
*                                                                               
         OC    SBQUPGRD,SBQUPGRD   TEST FOR UPGRADE FORMULA                     
         BZ    EUPG                                                             
         OC    SBQUPFBK,SBQUPFBK   MUST HAVE SHARE BOOK                         
         BZ    ENOFRBK                                                          
*                                                                               
VALUPGX  B     XIT2                                                             
         SPACE 2                                                                
UPGTAB   DS    0H               ** UPGRADE VALIDATION TABLE **                  
         DC    C'PUT     ',C'   ',X'1000',AL2(VALUPA-T20427X)                   
         DC    C'SHR     ',C'   ',X'0800',AL2(VALUPA-T20427X)                   
         DC    C'RTG     ',C'   ',X'0800',AL2(VALUPA-T20427X)                   
         DC    C'RP      ',C'   ',X'1800',AL2(VALUPA-T20427X)                   
         DC    C'TUPGRADE',C'UPT',X'8000',AL2(VALUPE-T20427X)                   
         DC    C'PUPGRADE',C'UPP',X'8000',AL2(VALUPE-T20427X)                   
         DC    C'BOOK    ',C'BK ',X'4000',AL2(VALUPB-T20427X)                   
UPGTABX  DC    AL1(0)                                                           
*                                                                               
UPGTABD  DSECT                                                                  
UPGNAME  DS    CL8                 KEYWORD NAME                                 
UPGSHRT  DS    CL3                 SHORT KEYWORD NAME                           
UPGIND1  DS    XL1                 INDICATORS                                   
UPGIND2  DS    XL1                 INDICATORS                                   
UPGROUT  DS    AL2                 DISPLACEMENT OF VALIDATION ROUTINE           
UPGTABL  EQU   *-UPGTABD                                                        
*                                                                               
T20427   CSECT                                                                  
         DS    0H                                                               
         SPACE 2                                                                
VALUPE   LR    R0,RE               ** VALIDATE UPGRADE EXPRESSION **            
         MVC   SBQUPFIL,UPGSHRT+2  SET FILE INDICATOR                           
         GOTO1 UPVAL,DMCB,WORK,PARAS,(C'/',ACOMFACS)                            
         CLI   PARAS+1,0                                                        
         BE    EUPG                                                             
         MVC   SBQUPGRD,PARAS+4    UPGRADE EXPRSSN FROM UPGRADE ELEM            
         LR    RE,R0                                                            
         BR    RE                                                               
         SPACE 1                                                                
VALUPB   LR    R0,RE               ** VALIDATE OVERRIDE BOOK **                 
         GOTO1 BOOKVAL,DMCB,(C'N',WORK),(1,FULL),SCANNER                        
         SR    RF,RF                                                            
         ICM   RF,1,4(R1)          RF=N'BOOKS                                   
         BZ    EBK                                                              
         TM    FULL,X'BF'          TEST FOR FUNNY BOOK FORMATS                  
         BNZ   EBK                                                              
         MVC   SBQUPFBK,FULL+1                                                  
         LR    RE,R0                                                            
         BR    RE                                                               
         SPACE 1                                                                
VALUPA   LR    R0,RE               ** VALIDATE PUT/SHR AVERAGING **             
         CLI   WORK+5,1                                                         
         BNE   EUPG                                                             
         CLI   WORK+8,C'1'                                                      
         BE    *+12                                                             
         CLI   WORK+8,C'2'                                                      
         BNE   EUPG                                                             
         TM    UPGIND1,X'10'                                                    
         BZ    *+10                                                             
         MVC   SBQUPPUT,WORK+8                                                  
         TM    UPGIND1,X'08'                                                    
         BZ    *+10                                                             
         MVC   SBQUPSHR,WORK+8                                                  
         LR    RE,R0                                                            
         BR    RE                                                               
         EJECT                                                                  
* GET DAYPART TABLE                                                             
* INPUT  : SBDPTMEN = DAYPART MENU                                              
* OUTPUT : SBDPTTAB SET                                                         
*                                                                               
GETDPTTB OC    SBADPTTB,SBADPTTB   TEST DAYPART TABLES BUFFER                   
         BZ    GDX                                                              
         CLC   SBDPTMEN,SVDPTMEN   YES-TEST DAYPART MENU CHANGE                 
         BE    GDX                                                              
         MVC   SVDPTMEN,SBDPTMEN   YES-GET DAYPART TABLE                        
         LA    R1,ALPHATAB                                                      
         SR    RE,RE                                                            
*                                                                               
GD10     CLI   0(R1),0                                                          
         BNE   *+12                                                             
         LA    RE,25                                                            
         B     GD20                                                             
         CLC   SBDPTMEN,0(R1)                                                   
         BE    *+12                                                             
         LA    R1,1(R1)                                                         
         BCT   RE,GD10                                                          
         LPR   RE,RE                                                            
*                                                                               
GD20     MH    RE,=H'180'                                                       
         L     R1,SBADPTTB                                                      
         LA    R1,0(RE,R1)                                                      
         XC    SBDPTTAB,SBDPTTAB                                                
         MVC   SBDPTTAB(180),0(R1)                                              
*                                                                               
GDX      B     XIT2                                                             
         SPACE 1                                                                
ALPHATAB DC    C'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789',X'00'                    
         EJECT                                                                  
* DEMO HEADING ROUTINE                                                          
*                                                                               
HEDDEM   DS    0H                                                               
         L     R4,AGLOBAL                                                       
         USING GLOBALD,R4                                                       
         MVC   0(7,R3),SPACES                                                   
         ZIC   R5,GLARGS                                                        
         BCTR  R5,0                                                             
         MH    R5,=H'7'                                                         
         LA    R5,DEMNAMES(R5)                                                  
         CLI   GLARGS+1,C'R'       TEST DEMO MODIFIER IS RATING                 
         BE    HDEM10                                                           
         L     R1,ADEMLST                                                       
         ZIC   R2,GLARGS                                                        
         BCTR  R2,0                                                             
         MH    R2,=H'3'                                                         
         LA    R2,0(R1,R2)                                                      
         CLI   2(R2),0                                                          
         BE    XIT2                                                             
         MVC   FULL(3),0(R2)                                                    
         MVC   FULL+1(1),GLARGS+1    MODIFIER IS S OR P                         
         LA    R0,NMYDEMOS         MIGHT HAVE FOUND NAME ALREADY                
         L     R2,=A(MYDEMOS)                                                   
*                                                                               
HDEM2    OC    0(3,R2),0(R2)                                                    
         BZ    HDEM4                                                            
         CLC   0(3,R2),FULL                                                     
         BE    HDEM6               NAME FOUND                                   
         LA    R2,10(R2)                                                        
         BCT   R0,HDEM2                                                         
         L     R2,=A(MYDEMOS)                                                   
         XC    0(L'MYDEMOS,R2),0(R2)                                            
*                                                                               
HDEM4    MVC   0(3,R2),FULL        GET THE NAME                                 
*                                                                               
         L     R5,=A(DBAREA)                                                    
         USING DBLOCKD,R5                                                       
         XC    DBLOCK,DBLOCK                                                    
         MVI   DBFUNCT,DBGETDEM                                                 
         MVI   DBTPTT,C'T'                                                      
         MVC   DBCOMFCS,SBCOMFAC                                                
         MVC   DBFILE,=C'TP '                                                   
         MVI   DBSELMED,C'T'                                                    
         LA    R1,SBAGYREC                                                      
         USING AGYHDR,R1                                                        
         CLI   AGYPROF+7,C'C'                                                   
         BNE   *+8                                                              
         MVI   DBSELMED,C'C'                                                    
         GOTO1 DEMOCON,DMCB,(1,FULL),(2,3(R2)),(C'S',(R5)),0                    
         DROP  R1,R5                                                            
*                                                                               
HDEM6    LA    R5,3(R2)            R5=A(DEMO NAME)                              
*                                                                               
HDEM10   OC    0(7,R5),0(R5)                                                    
         BZ    XIT2                                                             
         MVC   0(7,R3),0(R5)                                                    
         B     XIT2                                                             
         EJECT                                                                  
* ERROR EXITS AND MESSAGES                                                      
*                                                                               
EPCT     MVC   CONHEAD(L'EPCTM),EPCTM                                           
         B     MYCURS                                                           
*                                                                               
EBK      MVC   CONHEAD(L'EBKM),EBKM                                             
         B     MYCURS                                                           
*                                                                               
EUPG     MVC   CONHEAD(L'EUPGM),EUPGM                                           
         B     MYCURS                                                           
*                                                                               
ENOFRBK  MVC   CONHEAD(L'ENOFRBKM),ENOFRBKM                                     
         B     MYCURS                                                           
*                                                                               
MYCURS   MVI   ERROR,X'FE'                                                      
CURS     GOTO1 CURSERR                                                          
*                                                                               
EPCTM    DC    C'INVALID PERCENT'                                               
EBKM     DC    C'INVALID BOOK'                                                  
EUPGM    DC    C'INVALID UPGRADE EXPRESSION'                                    
ENOFRBKM DC    C'MUST SPECIFY SHARE BOOK'                                       
         EJECT                                                                  
SVDPTMEN DS    CL1                                                              
*                                                                               
         LTORG                                                                  
*                                                                               
*&&DO                                                                           
T20427Z  DS    0X                                                               
         DS    (8192-24-(T20427Z-T20427))X'00'                                  
*&&                                                                             
         EJECT                                                                  
WORKD    DSECT                                                                  
*                                                                               
AXTRA    DS    0F               ** EXTENSION ROUTINE ADDRESSES **               
AVALBOOK DS    A                                                                
AGETDPTB DS    A                                                                
AHDEM    DS    A                                                                
AXTRAN   EQU   (*-AXTRA)/L'AXTRA                                                
*                                                                               
SAVERD   DS    A                                                                
RELO     DS    A                                                                
AGEND    DS    A                                                                
SVOUT    DS    A                                                                
         DS    0D                                                               
PRDLST   DS    XL256                                                            
ARULWK   DS    A                                                                
*                                                                               
VRECUP   DS    V                                                                
VLOADER  DS    V                                                                
VCALLOV  DS    V                                                                
VBINSRCH DS    V                                                                
*                                                                               
WORKL    EQU   *-WORKD                                                          
         SPACE 2                                                                
ILINKD   DSECT                     DSECT FOR BUY LINE (INTERNAL)                
ILEST    DS    XL1                 ESTIMATE                                     
ILSTAT   DS    XL3                 STATION                                      
ILLINE   DS    XL1                 LINE NUMBER                                  
ILSTDT   DS    XL3                 START DATE                                   
ILENDT   DS    XL3                 END DATE                                     
ILNOWK   DS    XL1                 NPW                                          
ILDPT    DS    CL1                 DAYPART                                      
ILSEC    DS    XL1                 SPOT LENGTH                                  
ILDAY    DS    XL1                 DAY                                          
ILSEDAY  DS    XL1                 START-END DAY                                
ILTIMES  DS    XL4                 START-END TIMES                              
ILPROG   DS    CL17                PROGRAM                                      
ILDISK   DS    XL4                 DISK ADDRESS                                 
ILINKX   DS    0X                                                               
ILINKL   EQU   *-ILINKD                                                         
         SPACE 2                                                                
OLINKD   DSECT                     DSECT FOR BUY LINE (EXTERNAL)                
OLDESC   DS    CL4                 DESCRIPTION (BUY=)                           
         DS    CL1                                                              
OLSTAT   DS    CL8                 STATION                                      
         DS    CL1                                                              
OLESTLIN DS    CL7                 EST-LIN                                      
         DS    CL1                                                              
OLSTENDT DS    CL11                START-END DATES                              
         DS    CL1                                                              
OLNOWK   DS    CL5                 NPW                                          
         DS    CL1                                                              
OLDAYS   DS    CL8                 DAYS                                         
         DS    CL1                                                              
OLTIMES  DS    CL11                START-END TIME                               
         DS    CL1                                                              
OLDPTSL  DS    CL5                 DPT/SLN                                      
         DS    CL1                                                              
OLPROG   DS    CL17                                                             
OLINKX   DS    0X                                                               
OLINKL   EQU   *-OLINKD                                                         
         EJECT                                                                  
SPRULDD  DSECT                                                                  
       ++INCLUDE SPRULERD                                                       
*                                                                               
*              OTHER DATA TO BE SAVED ACROSS REQUEST                            
*              (CONVENIENTLY ADDRESSABLE)                                       
SVDCB    DS    XL128               SAVED DCB                                    
         DS    XL128               SPARE                                        
VRULER   DS    V                   A(SPRULER)                                   
VBGSTAT  DS    V                   A(SPBGSTAT)                                  
*                                                                               
         EJECT                                                                  
* OTHER INCLUDES                                                                
*                                                                               
*SPWRIWORKD                                                                     
*SPOTTABD                                                                       
*DDSPOOLD                                                                       
*DDSPLWORKD                                                                     
*DDBIGBOX                                                                       
*DRGLOBAL                                                                       
*DRIVETABLE                                                                     
*DRINTRECD                                                                      
*DDMSTD                                                                         
*DDCOMFACSD                                                                     
*SPGENAGY                                                                       
*SPGENBUY                                                                       
*SPGENSTA                                                                       
*SPAUTHD                                                                        
*SPWRIFFD                                                                       
         PRINT OFF                                                              
       ++INCLUDE SPWRIWORKD                                                     
       ++INCLUDE SPOTTABD                                                       
       ++INCLUDE DDSPOOLD                                                       
       ++INCLUDE DDSPLWORKD                                                     
       ++INCLUDE DDBIGBOX                                                       
       ++INCLUDE DRGLOBAL                                                       
       ++INCLUDE DRIVETABLE                                                     
       ++INCLUDE DRINTRECD2                                                     
       ++INCLUDE DDMASTD                                                        
       ++INCLUDE DDCOMFACSD                                                     
DBLOCKD  DSECT                                                                  
       ++INCLUDE DEDBLOCK                                                       
AGYHDRD  DSECT                                                                  
       ++INCLUDE SPGENAGY                                                       
BUYRECD  DSECT                                                                  
       ++INCLUDE SPGENBUY                                                       
ESTHDRD  DSECT                                                                  
       ++INCLUDE SPGENEST                                                       
       ++INCLUDE SPGENSTA                                                       
       ++INCLUDE SPAUTHD                                                        
       ++INCLUDE SPWRIFFD                                                       
         PRINT ON                                                               
         ORG   CONTAGH                                                          
         EJECT                                                                  
       ++INCLUDE SPWRIF1D                                                       
         EJECT                                                                  
         ORG   CONTAGH                                                          
       ++INCLUDE SPWRID4D                                                       
         EJECT                                                                  
       ++INCLUDE DDGENTWA                                                       
         SPACE 2                                                                
       ++INCLUDE DDTWADCOND                                                     
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'058SPWRI27X  05/01/02'                                      
         END                                                                    
