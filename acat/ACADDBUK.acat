*          DATA SET ACADDBUK   AT LEVEL 006 AS OF 11/02/06                      
*CATALP ACADDBUK                                                                
         TITLE '- Add bucket element to record'                                 
ACADDBUK CSECT                                                                  
***********************************************************************         
* Add bucket element to record                                        *         
*                                                                     *         
* NTRY - P1    XL1        New File                 X'80' BKINEWF                
*              AL3        Record                                                
*        P2    XL1        Replace bucket           X'80' BKIRPLC                
*              AL3        BUKELD element to add                                 
*              or         CACELD element to add                                 
*        P3    A          HELLO                                                 
***********************************************************************         
                                                                                
         PRINT NOGEN                                                            
         USING GLOBALS,RA          RA=A(GLOBALS)                                
         USING WORKD,RC            RC=A(LOCAL WORKING STORAGE)                  
         NMOD1 WORKL,**ABUK**,CLEAR=YES,RR=RE                                   
         MVC   PARMS,0(R1)         Save off parameters                          
         ST    RE,RELO             SAVE RELOCATION FACTOR                       
         BASR  RA,0                                                             
         AHI   RA,GLOBALS-*                                                     
                                                                                
         MVC   FILE,ACCMST                                                      
         LA    RF,ACCRFST-ACCRECD                                               
         TM    BUKRIND,BUKINEWF    New File                                     
         BO    ADDBUK02                                                         
         MVC   FILE,ACCFIL                                                      
         LA    RF,ACCORFST                                                      
                                                                                
ADDBUK02 STH   RF,DATADISP                                                      
                                                                                
         USING BUKELD,RE                                                        
BUK1     USING BUKELD,BUKELEM1                                                  
BUK2     USING BUKELD,BUKELEM2                                                  
         L     RE,ABUKELEM         Move element to local area                   
         CLI   0(RE),BUKELQ        X'45' Bucket element                         
         BE    ADDBUK08            Add bucket to record                         
         CLI   0(RE),CACELQ        X'43' Contra name element                    
         BNE   ADDBUKX             Something was wrong                          
*                                  Get existing CACELQ, if any                  
         GOTOR HELLO,ELIST,(C'G',FILE),('CACELQ',ABUKREC),0,0,0                 
         CLI   ELERR,0                                                          
         BNE   ADDBUK07            No element found so add new one              
                                                                                
OLD      USING CACELD,R2                                                        
NEW      USING CACELD,R3                                                        
         L     R2,ELADDR4          A(CACELD after get)                          
         L     R3,ACACELEM         A(CACELD passed)                             
         CLC   OLD.CACLN,NEW.CACLN                                              
         BNE   ADDBUK06            Different length so delete and add           
         LLGC  RF,OLD.CACLN        Same length so just replace                  
         BCTR  RF,0                                                             
         EXMVC RF,OLD.CACELD,NEW.CACELD                                         
         B     ADDBUKX                                                          
         DROP  OLD,NEW                                                          
                                                                                
ADDBUK06 GOTOR HELLO,ELIST,(C'D',FILE),('CACELQ',ABUKREC),0,0,0                 
                                                                                
ADDBUK07 GOTOR HELLO,ELIST,(C'P',FILE),ABUKREC,ACACELEM,0,0                     
         CLI   ELERR,0                                                          
         BE    ADDBUKX                                                          
         DC    H'0'                Can't add the element                        
                                                                                
ADDBUK08 MVC   BUK1.BUKEL(BUKDR-BUKELD),0(RE)                                   
         CLI   BUKLN,BUKLN7Q       7 instead of 6                               
         BE    ADDBUK09                                                         
         BL    *+6                                                              
         DC    H'00'               Not valid                                    
         ZAP   BUK1.BUKDR7,BUKDR                                                
         ZAP   BUK1.BUKCR7,BUKCR                                                
         B     ADDBUK10                                                         
                                                                                
ADDBUK09 ZAP   BUK1.BUKDR7,BUKDR7                                               
         ZAP   BUK1.BUKCR7,BUKCR7                                               
         DROP  RE                                                               
                                                                                
REC      USING BUKELD,R4                                                        
ADDBUK10 SR    R6,R6               R6 = # of bucket elements                    
         L     R4,ABUKREC                                                       
         AH    R4,DATADISP                                                      
ADDBUK12 CLI   0(R4),EOR           End of record                                
         BE    ADDBUK18                                                         
         CLI   0(R4),BUKELQ                                                     
         BNE   ADDBUK16                                                         
         AHI   R6,1                Found one                                    
         CLC   REC.BUKMOS,BUK1.BUKMOS                                           
         BNE   ADDBUK16                                                         
         SHI   R6,1                Losing one                                   
         MVI   REC.BUKEL,DELELQ    Mark for deletion                            
         TM    BUKEIND,BUKIRPLC    Replace values instead of adding             
         BO    ADDBUK16                                                         
         AP    BUK1.BUKDR7,REC.BUKDR                                            
         AP    BUK1.BUKCR7,REC.BUKCR                                            
                                                                                
ADDBUK16 LLGC  RF,REC.BUKLN                                                     
         AR    R4,RF                                                            
         B     ADDBUK12                                                         
         DROP  REC                                                              
                                                                                
ADDBUK18 GOTOR HELLO,ELIST,(C'D',FILE),('DELELQ',ABUKREC),0,0,0                 
                                                                                
ADDBUK20 XC    BUK2.BUKELD(BUKLN7Q),BUK2.BUKELD                                 
         CLI   BUK1.BUKDR7,0       Test values fit in PL6                       
         BNE   ADDBUK22            No                                           
         CLI   BUK1.BUKCR7,0                                                    
         BE    ADDBUK40            Yes - Don't split                            
                                                                                
ADDBUK22 MVC   BUK2.BUKELD(BUKDR-BUKELD),BUK1.BUKELD                            
         ZAP   BUK2.BUKDR7,PZERO                                                
         ZAP   BUK2.BUKCR7,PZERO                                                
                                                                                
         CLI   BUK1.BUKDR7,0       Test debit bucket overflow                   
         BE    ADDBUK30            No                                           
         ZAP   BUK2.BUKDR7,BUK1.BUKDR7                                          
         ZAP   DUB,PL6MAX                                                       
         CP    BUK2.BUKDR7,PZERO                                                
         BH    *+10                                                             
         MP    DUB,PMINUS1                                                      
         ZAP   BUK1.BUKDR7,DUB                                                  
         SP    BUK2.BUKDR7,DUB                                                  
                                                                                
ADDBUK30 CLI   BUK1.BUKCR7,0       Test credit bucket overflow                  
         BE    ADDBUK40            No                                           
         ZAP   BUK2.BUKCR7,BUK1.BUKCR7                                          
         ZAP   DUB,PL6MAX                                                       
         CP    BUK2.BUKCR7,PZERO   Neg or Pos number                            
         BH    *+10                                                             
         MP    DUB,PMINUS1                                                      
         ZAP   BUK1.BUKCR7,DUB                                                  
         SP    BUK2.BUKCR7,DUB                                                  
                                                                                
ADDBUK40 CHI   R6,BUKMAXN          Test enough room for new bucket(s)           
         BNH   ADDBUK56            Yes                                          
                                                                                
         L     R4,ABUKREC          No                                           
         AH    R4,DATADISP                                                      
ADDBUK42 CLI   0(R4),EOR                                                        
         BNE   *+6                                                              
         DC    H'00'                                                            
                                                                                
         CLI   0(R4),BUKELQ        X'45' bucket element                         
         BE    ADDBUK44            POINT TO OLDEST (FIRST) BUCKET               
         LLGC  RF,1(,R4)                                                        
         AR    R4,RF                                                            
         B     ADDBUK42                                                         
                                                                                
REC      USING BUKELD,R4                                                        
SAV      USING BUKELD,BUKSAVE                                                   
                                                                                
ADDBUK44 MVC   SAV.BUKELD(BUKDR-BUKELD),REC.BUKELD                              
         ZAP   SAV.BUKDR7,REC.BUKDR                                             
         ZAP   SAV.BUKCR7,REC.BUKCR                                             
                                                                                
ADDBUK50 MVI   REC.BUKEL,DELELQ    MARK ELEMENT FOR DELETION                    
         SHI   R6,1                Losing an element                            
         LLGC  RF,REC.BUKLN        Bump to next element                         
         AR    R4,RF                                                            
         CLI   0(R4),EOR           End of record                                
         BE    ADDBUK52            Yes                                          
         CLC   REC.BUKELD(BUKDR-BUKELD),SAV.BUKELD                              
         BNE   ADDBUK52                                                         
         AP    SAV.BUKDR7,REC.BUKDR                                             
         AP    SAV.BUKCR7,REC.BUKCR                                             
         B     ADDBUK50                                                         
         DROP  REC                                                              
                                                                                
ADDBUK52 GOTOR HELLO,ELIST,(C'D',FILE),('DELELQ',ABUKREC),0,0,0                 
                                                                                
         USING PBKELD,R3                                                        
         GOTOR HELLO,ELIST,(C'G',FILE),('PBKELQ',ABUKREC),0,0,0                 
         CLI   ELERR,0                                                          
         BNE   ADDBUK54            Element not found                            
         L     R3,ELADDR4          Update prior bucket element                  
         CLC   PBKLOW,SAV.BUKYEAR                                               
         BL    *+10                                                             
         MVC   PBKLOW,SAV.BUKYEAR                                               
         CLC   PBKHI,SAV.BUKYEAR                                                
         BH    *+10                                                             
         MVC   PBKHI,SAV.BUKYEAR                                                
         AP    PBKDR,SAV.BUKDR7                                                 
         AP    PBKCR,SAV.BUKCR7                                                 
         B     ADDBUK56                                                         
                                                                                
ADDBUK54 XC    ELEMENT,ELEMENT     Create prior bucket element                  
         LA    R3,ELEMENT                                                       
         MVI   PBKEL,PBKELQ                                                     
         MVI   PBKLN,PBKLNQ                                                     
         MVC   PBKLOW,SAV.BUKYEAR                                               
         MVC   PBKHI,SAV.BUKYEAR                                                
         ZAP   PBKDR,SAV.BUKDR7                                                 
         ZAP   PBKCR,SAV.BUKCR7                                                 
         GOTOR HELLO,ELIST,(C'P',FILE),ABUKREC,PBKELD,0,0                       
         CLI   ELERR,0                                                          
         BE    *+6                                                              
         DC    H'0'                Can't add the element                        
         DROP  R3,SAV                                                           
                                                                                
NEW      USING BUKELD,WORK                                                      
ADDBUK56 MVI   NEW.BUKEL,BUKELQ                                                 
         MVI   NEW.BUKLN,BUKLNQ                                                 
         MVC   NEW.BUKMOS,BUK1.BUKMOS                                           
         ZAP   NEW.BUKDR,BUK1.BUKDR7                                            
         ZAP   NEW.BUKCR,BUK1.BUKCR7                                            
         GOTOR HELLO,ELIST,(C'P',FILE),ABUKREC,NEW.BUKEL,0,0                    
         CLI   ELERR,0                                                          
         BE    *+6                                                              
         DC    H'0'                Can't add the element                        
         AHI   R6,1                Added a new BUKELQ                           
         DROP  NEW                                                              
                                                                                
ADDBUK58 CLI   BUK2.BUKEL,0        Test any remaining amount in BUK2            
         BE    ADDBUKX                                                          
         MVC   BUK1.BUKELD(BUKLN7Q),BUK2.BUKELD                                 
         B     ADDBUK20            Keep spliting                                
         DROP  BUK2                                                             
                                                                                
ADDBUKX  SR    RE,RE                                                            
         XIT1                                                                   
         DROP  RB                                                               
         EJECT                                                                  
***********************************************************************         
* Global area and constants                                                     
***********************************************************************         
GLOBALS  DS    0D                                                               
         LTORG                                                                  
                                                                                
*&&UK                                                                           
BUKMAXN  EQU   54                  MAXIMUM N'BUCKET ELEMENTS ON RECORD          
*&&                                                                             
*&&US                                                                           
BUKMAXN  EQU   108                 MAXIMUM N'BUCKET ELEMENTS ON RECORD          
*&&                                                                             
EOR      EQU   0                   End of record                                
FF       EQU   X'FF'                                                            
DELELQ   EQU   X'FF'               FOR ELEMENT DELETION                         
PZERO    DC    P'0'                                                             
PMINUS1  DC    P'-1'                                                            
PL6MAX   DC    PL6'99999999999'                                                 
ACCFIL   DC    C'ACCFIL  '                                                      
ACCMST   DC    C'ACCMST  '                                                      
         EJECT                                                                  
***********************************************************************         
* Local working storage                                                         
***********************************************************************         
WORKD    DSECT                     ** LOCAL WORKING STORAGE **                  
RELO     DS    A                                                                
PARMS    DS    0XL(6*4)                                                         
         DS    6A                                                               
         ORG   PARMS                                                            
BUKRIND  DS    0XL1                Record indicator                             
BUKINEWF EQU   X'80'               .  New file ACCMST                           
ABUKREC  DS    A                   A(Record to add to)                          
BUKEIND  DS    0XL1                Element indicator                            
BUKIRPLC EQU   X'80'               .  Replace elements                          
ACACELEM DS    0A                  A(CACELD element to add/change)              
ABUKELEM DS    A                   A(BUKELD element to add/replace)             
HELLO    DS    A                   A(Hello)                                     
                                                                                
ELIST    DS    6A                  HELLO PARAMETER LIST                         
         ORG   ELIST                                                            
ELADDR1  DS    A                                                                
ELADDR2  DS    A                                                                
ELADDR3  DS    A                                                                
ELERR    DS    0X                  HELLO ERROR RETURN BYTE                      
ELADDR4  DS    A                   A(element) - GET                             
ELADDR5  DS    A                   A(element) - after ADD                       
         ORG                                                                    
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
WORK     DS    XL64                                                             
FILE     DS    CL8                 FILE NAME (ACCFIL/ACCMST)                    
DATADISP DS    H                   DISPLACEMENT TO FIRST ELEMENT                
ELEMENT  DS    XL256                                                            
                                                                                
BUKELEM1 DS    XL(BUKLN7Q)         BUCKET ELEMENT 1 BUILT HERE                  
BUKELEM2 DS    XL(BUKLN7Q)         BUCKET ELEMENT 2 BUILT HERE                  
BUKSAVE  DS    XL(BUKLN7Q)         BUCKET ELEMENT SAVE AREA                     
                                                                                
WORKX    DS    0D                                                               
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
BUKELD   DSECT                                                                  
         ORG   BUKDR                                                            
BUKDR7   DS    PL7                                                              
BUKCR7   DS    PL7                                                              
BUKLN7Q  EQU   *-BUKELD                                                         
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006ACADDBUK  11/02/06'                                      
         END                                                                    
