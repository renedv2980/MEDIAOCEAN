*          DATA SET CXSEFC     AT LEVEL 004 AS OF 03/25/08                      
*CATALP CXSEFC                                                                  
         TITLE 'CXSEFC - CONTROL SECURITY EXTRACT - PER PW EFF DATE'            
**********************************************************************          
* AL4 A(EXTRACT RECORD)                                              *          
* AL4 A(CONFILE RECORD)                                              *          
* AL4 N/A                                                            *          
* AL4 N/A                                                            *          
* AL4 A(EXTRACT CONTROL BLOCK)                                       *          
**********************************************************************          
         PRINT NOGEN                                                            
CXSEFC   CSECT                                                                  
         NMOD1 WORKX-WORKD,*CXSEFC*,RR=RA                                       
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RA,RELO                                                          
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         USING CXSEFD,R2           R2=A(EXTRACT RECORD)                         
         USING SA0REC,R3           R3=A(CONFILE RECORD)                         
*                                                                               
CXSLEN   XC    CXSEFLEN,CXSEFLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   CXSEFLEN(2),=AL2(CXSEFDL)                                        
         MVC   CXSEFTYP,CXSEFDQ                                                 
         MVI   CXSEFDX,MXTRTQ                                                   
         MVI   CXSEFDX+1,MXTRTQ                                                 
*                                  GET KEY INFO                                 
CXSAID   MVI   CXSEFAID-1,MXTRTQ   AGENCY                                       
         MVC   CXSEFAID,SA0KAGY                                                 
*                                                                               
         MVI   CXSEFNUM-1,MXTRTQ                                                
         SR    RF,RF                                                            
         ICM   RF,3,SA0KNUM        PERSON NUMBER                                
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  CXSEFNUM,DUB+4(4)                                                
*                                                                               
         MVI   CXSEFEFF-1,MXTRTQ                                                
         XC    HIVALS(HIVLNQ),HIVALS ZERO HIGHEST DATE/TIME VALUES              
         LA    R4,SA0DATA            R4=A(FIRST ELEMENT)                        
*                                                                               
         USING SAPWHD,R4                                                        
CXSPWH   CLI   SAPWHEL,0           GET ELEMENT FROM RECORD                      
         BE    CXSPWH30            AT EOR-GO TO FINAL LOGIC                     
         CLI   SAPWHEL,SAPWHELQ    TEST FOR PASSWORD HISTORY ELEMENT            
         BE    CXSPWH20                                                         
*                                                                               
CXSPWH10 ZIC   RF,SAPWHLN          BUMP TO NEXT ELEMENT                         
         CHI   RF,1                                                             
         BNH   EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R4,RF                                                            
         B     CXSPWH                                                           
*                                                                               
CXSPWH20 CLC   SAPWHDTE,MAXDATE    FOR NOW, SKIP OVER MAX DATE                  
         BE    CXSPWH10                                                         
*                                                                               
         CLC   HIDATE,SAPWHDTE     TEST IF EFFECTIVE DATE IS HIGHER             
         BL    CXSPWH22                                                         
         BH    CXSPWH10                                                         
         CLC   HITIME,SAPWHTME     IF EQUAL, COMPARE TIME                       
         BH    CXSPWH10                                                         
CXSPWH22 MVC   HIDATE,SAPWHDTE                                                  
         MVC   HITIME,SAPWHTME                                                  
         MVC   HIFLAG,SAPWHFLG                                                  
         B     CXSPWH10            BUMP TO NEXT ELEMENT                         
*                                                                               
CXSPWH30 OC    HIDATE,HIDATE       TEST FOR A HIGHEST DATE VALUE                
         BZ    CXSNXTX             PASSWORD EFFECTIVE DATE                      
*                                                                               
         TM    HIFLAG,SAPWHEXP          AUTOMATIC EXPIRE?                       
         BZ    CXSPWH40                                                         
         MVC   CXSEFEFF(8),=C'20010101' IF SO, USE THIS DATE SO IT              
         B     CXSNXTX                  WORKS LIKE THE OLD WAY                  
*                                                                               
CXSPWH40 GOTO1 VDATCON,DMCB,(2,HIDATE),(20,CXSEFEFF)                            
         DROP  R4                                                               
*                                                                               
CXSNXTX  B     EXIT                                                             
*                                                                               
DELREC   L     R1,APARM            DELETE THIS RECORD                           
         MVI   8(R1),X'80'                                                      
*                                                                               
EXIT     XMOD1 1                                                                
         LTORG                                                                  
*                                                                               
VDATCON  DC    V(DATCON)                                                        
MAXDATE  DC    X'FFFF'                                                          
*                                                                               
       ++INCLUDE MXTRT                                                          
       ++INCLUDE CXSRECID                                                       
         EJECT                                                                  
                                                                                
* INCLUDE CXTRACT LOCAL DSECTS                                                  
       ++INCLUDE CXTRACTD                                                       
         EJECT                                                                  
                                                                                
***********************************************************************         
* WORKING STORAGE                                                               
***********************************************************************         
WORKD    DSECT                                                                  
PARM     DS    0XL20               PARAMETER LIST                               
AXREC    DS    A                   A(EXTRACT RECORD)                            
CFREC    DS    A                   A(CONFILE RECORD)                            
         DS    A                                                                
         DS    A                                                                
ADXBLOCK DS    A                   A(EXTRACT CONTROL BLOCK)                     
*                                                                               
DUB      DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
RELO     DS    A                                                                
DMCB     DS    6F                                                               
*                                                                               
HIVALS   DS    0X                                                               
HIDATE   DS    XL(L'SAPWHDTE)      HIGHEST DATE                                 
HIFLAG   DS    XL(L'SAPWHFLG)      HIGH DATE/TIME FLAG                          
HITIME   DS    XL(L'SAPWHTME)      HIGHEST TIME                                 
HIVLNQ   EQU   *-HIVALS                                                         
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE CXSEFD                                                         
         EJECT                                                                  
       ++INCLUDE SEACSFILE                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004CXSEFC    03/25/08'                                      
         END                                                                    
