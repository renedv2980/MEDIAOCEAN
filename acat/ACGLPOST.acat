*          DATA SET ACGLPOST   AT LEVEL 007 AS OF 03/13/12                      
*CATALP ACGLPOST                                                                
***********************************************************************         
*  Figure out the general ledger account and office to post to,       *         
*  creating element, GLDELD and adding it to Unit S tranaction        *         
*  This will update unit G                                            *         
*       (a) Updating TRSEL                                            *         
*       (b) Updating GLDEL                                            *         
*       (c) Adding or updating bucket record GLBRECD                  *         
*       (d) Calling ADDTRANS to add unit G transactions               *         
*                                                                     *         
*  Note: Must pass minimum of 2K as GLBLOCK                           *         
*                                                                     *         
*        When making postings.                                        *         
*        On-line this module is called via ADDTRNS                    *         
*            (a) ADDTRNS save of #k of TIA and restores it in the     *         
*                first part of MINIO2 block, this is for GLPOST       *         
*                This is saved and restored upon re-entry and exit              
*            (b) GLPOST will use MINIO area 2 to make postings        *         
*                1. MINIO2 is used to store posting information using *         
*                   BIN31. Two pass process.                          *         
*                   (I)  First pass GLBREC posting is made. Could be  *         
*                        multiple entries for one GLBREC. Also re-use *         
*                        MINIO2 while countinuing to add GL records   *         
*                        transacionts to new BIN31 table.             *         
*                  (II)  Second pass calls ADDTRNS to make UNIT G     *         
*                        postings. Code added to ADDTRNS to make DR   *         
*                        even sequence and accumilated to same        *         
*                        transaction, that is transaction is updated  *         
*                        with new amount if it exsist already.        *         
*                        CR is odd sequence #.                        *         
*                 (III)  Used to store unit S names to post on to the *         
*                        GLBRECD records in CACEL element             *         
*                                                                     *         
*                                                                     *         
*        Off-line if the module is called via ADDTRN                  *         
*            (a) ADDTRNS will do a #K getmain low core                *         
*            (b) GLPOST  will do a high core getmain for 500K. This   *         
*                is instead of the MINIO buffer                       *         
*                                                                     *         
*        Off-line called directly you will need to pass 2K minimum.   *         
*             Up to 16K possible.                                     *         
*                                                                     *         
*        ACGLPOST can be called directly from program to add          *         
*         a GLDEL element as draft to posting file. The udpate will   *         
*         make it live by calling program with GLUPDTE and GLPOST     *         
*                                                                     *         
*  GLACTION = GLADDEL (Add GLDEL)                                     *         
*    GLDELD element added to transaction as draft or live             *         
*                                                                     *         
*  GLACTION = GLUPDTE (Update records to buffer)                      *         
*    Update GL Specail buckets and records to GL, must call GLPOST    *         
*    TRSELD element, X'60' GL date is updated                         *         
*                                                                     *         
*  GLACTION = GLPOST                                                  *         
*    Update records in buffer to ACC file.                            *         
*                                                                     *         
*  PARAMETERS                                                         *         
*                                                                     *         
*  ON ENTRY:  P1 = A(GLBLKD) See ACGLPOSTD (Min 2K area)              *         
*                                                                     *         
*  RETURNS:   If transaction was update or okay then exit equal else  *         
*             not equal on exit, ERROR# set                           *         
*                                                                     *         
***********************************************************************         
         TITLE 'ACGLPOST - General ledger posting element'                      
ACGLPOST RSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 GLWRKLNQ,**GLPO**,R8,R9,RR=R2,CLEAR=YES                          
         USING LGRD,R5                                                          
         USING GLBLKD,RA                                                        
         USING GLWORKD,RC                                                       
ACGL     ST    R2,GLRELO                                                        
         L     RA,0(,R1)           Load block                                   
         ST    RA,GLBLOCK                                                       
         LA    RA,0(,RA)           Clear HOB - clear for 24 bit not 31          
                                                                                
         BRAS  RE,GLINIT                                                        
         CLI   GLACTION,GLPOST     Post the GL accounts now in buffer           
         JNE   ACGL100                                                          
         BRAS  RE,GLUPDATE         Called via addtrans in TRNILAST              
         J     EXIT                                                             
                                                                                
         USING TRNRECD,R4                                                       
ACGL100  MVI   ERROR#,ERRNOTRN                                                  
         LA    RE,*                                                             
         ST    RE,AGLERROR         For debugging                                
         ICM   R4,15,@TRANACT                                                   
         JZ    EXITCC              Error                                        
         MVI   ERROR#,ERRNONE      Reset                                        
         CLI   TRNKUNT,C'S'        Unit S only                                  
         JNE   EXITCC                                                           
         CLI   TRNKLDG,C'9'        G/L posting controls                         
         JE    EXITCC              No need to add element                       
         CLI   TRNKLDG,C'J'        Production ?                                 
         JNE   *+8                                                              
         OI    LDGIND,LDGPROD      Yes                                          
*                                  No                                           
         MVC   TRNCULA,0(R4)                                                    
         AH    R4,DATADISP                                                      
         MVI   ERROR#,ERRNF44      Bad tranaction record                        
         CLI   0(R4),TRNELQ        X'44' Transaction element                    
         JNE   EXITCC              Error                                        
         MVI   ERROR#,ERRNONE                                                   
                                                                                
         USING TRNELD,R4                                                        
         CLC   TRNOFFC,=C'**'                                                   
         JE    EXITCC              Purchase order so skip                       
         DROP  R4                                                               
                                                                                
         NI    GLIND1,TURNOFF-GLILIVE                                           
         CLC   TRNCPY,GLLSTCPY     Same company code                            
         JE    ACGL210             Yes                                          
         NI    PRGIND,TURNOFF-PRGBYCPY                                          
                                                                                
         TM    GLACTION,GLUPDTE    Need to post whats in buffer                 
         BZ    ACGL210             No                                           
         MVC   SVACTION,GLACTION   Yes, force GLPOST                            
         MVI   GLACTION,GLPOST                                                  
         BRAS  RE,GLUPDATE                                                      
         MVC   GLACTION,SVACTION   Restore action                               
                                                                                
ACGL210  TM    PRGIND,PRGBYCPY     Initialized ?                                
         JO    ACGL212             Yes                                          
         BRAS  RE,CPYINIT                                                       
                                                                                
         USING TRSELD,R4                                                        
ACGL212  CLC   GLLSTULA,TRNULA     Same account ?                               
         JE    ACGL214             No                                           
         MVI   GLTORULE,0          Reset rule                                   
         J     ACGL218             No                                           
                                                                                
ACGL214  MVC   ISACTOFF,GLISOFFC   Yes, so restore value                        
         MVC   ISOFFC,GLISOFFC                                                  
         CLI   GLSVERR#,ERRNONE    Were there errors before                     
         JE    ACGL218                                                          
         MVC   ERROR#,GLSVERR#                                                  
         J     EXITCC                                                           
                                                                                
ACGL218  MVI   GLSVERR#,ERRNONE                                                 
                                                                                
ACGL220  CLI   0(R4),EOR           End of record                                
         JE    ACGL400                                                          
         CLI   0(R4),TRSELQ        X'60' Transaction status element             
         JNE   ACGL224                                                          
         ST    R4,AELEM60          Save off address                             
         MVC   SVGLMOA,TRSPMOS                                                  
         TM    TRSSTAT,TRSSPEEL    Peeled transaction ?                         
         JO    EXITCC              Don't process peeled tranactions             
                                                                                
         USING GLDELD,R4                                                        
ACGL224  CLI   0(R4),GLDELQ        X'63' General Ledger element                 
         JNE   ACGL228                                                          
         OC    GLDDATE,GLDDATE                                                  
         JNZ   ACGL228             Already used                                 
         OC    AELEM63,AELEM63                                                  
         BNZ   ACGL228                                                          
         ST    R4,AELEM63          Un-used element                              
         TM    GLACTION,GLUPDTE                                                 
         BZ    ACGL228                                                          
         TM    GLDIND,GLDDRAFT     Is it draft                                  
         BZ    ACGL228                                                          
         OI    GLIND1,GLILIVE      Draft to live                                
                                                                                
ACGL228  LLC   RF,1(,R4)                                                        
         AR    R4,RF                                                            
         J     ACGL220                                                          
         DROP  R4                                                               
                                                                                
ACGL400  BRAS  RE,COMPANY          Process company                              
         JNE   EXIT                                                             
         TM    GLIND2,GLIONGL      On New GL system                             
         JZ    EXIT                Not on GL system                             
                                                                                
         BRAS  RE,PROFILES                                                      
         JNE   EXIT                                                             
         BRAS  RE,LEDGER           R5 = Ledger entry values                     
         JNE   EXIT                                                             
         TM    GLIND1,GLILIVE      Going from draft to live?                    
         JZ    ACGL420             No so process account                        
         MVI   GLACC_OK,NO         Will need to process                         
         J     ACGL430                                                          
                                                                                
ACGL420  BRAS  RE,PROCACC                                                       
         JNE   EXIT                                                             
                                                                                
ACGL430  BRAS  RE,ADDGLD                                                        
         JNE   EXITCC                                                           
         MVC   GLLSTULA,TRNULA     Save off account processed                   
         TM    GLACTION,GLUPDTE                                                 
         JZ    EXITCC                                                           
         BRAS  RE,GLADD            Add GL to tabel to update at end             
         JNE   EXITCC                                                           
         TM    PENDING,PENDPOST                                                 
         JZ    EXITCC                                                           
         TM    GLIND1,GLIOFF       Off-line                                     
         JZ    EXITCC                                                           
         MVC   SVACTION,GLACTION   Yes, force GLPOST                            
         MVI   GLACTION,GLPOST                                                  
         BRAS  RE,GLUPDATE                                                      
         MVC   GLACTION,SVACTION   Restore action                               
                                                                                
EXITCC   CLI   ERROR#,ERRNONE      See condition code                           
         JE    *+10                                                             
         MVC   GLSVERR#,ERROR#                                                  
                                                                                
EXIT     XIT1                                                                   
                                                                                
EXIT_R5  CLI   ERROR#,ERRNONE                                                   
         JE    *+10                                                             
         MVC   GLSVERR#,ERROR#                                                  
         XIT1  REGS=(R5)                                                        
         EJECT ,                                                                
***********************************************************************         
* Inialize storage and routine addresses                                        
***********************************************************************         
         USING WKSD,R3                                                          
GLINIT   NTR1                                                                   
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
         XC    @GLDEL,@GLDEL       Clear                                        
                                                                                
         LHI   RF,ACCRFST-ACCRECD  New file                                     
         TM    PRGIND,PRGNEWF      New file or old file record                  
         JO    *+8                                                              
         LHI   RF,ACCORFST         Old file                                     
         STH   RF,DATADISP                                                      
                                                                                
         L     R3,=A(WKSTAB)                                                    
         A     R3,GLRELO                                                        
GLINI010 ICM   RE,15,WKSAREA                                                    
         BZ    GLINI020                                                         
         AR    RE,RC                                                            
         MVC   0(L'WKSLABEL,RE),WKSLABEL                                        
         AHI   RE,L'WKSLABEL                                                    
         ICM   RF,15,WKSADDR                                                    
         BZ    GLINI012            Don't set address                            
         AR    RF,RC                                                            
         ST    RE,0(,RF)           Save off location                            
GLINI012 LA    R3,WKSLNQ(,R3)      Next entry                                   
         B     GLINI010                                                         
         DROP  R3                                                               
                                                                                
         USING COMFACSD,RF                                                      
GLINI020 L     RF,@COMFACS                                                      
         MVI   GLIND1,GLION        On-line                                      
                                                                                
         USING MASTD,RE                                                         
         ICM   RE,15,CMASTC        If zero then on-line                         
         BZ    GLINI030                                                         
         XI    GLIND1,GLIOFF+GLION Offline is on & online is off                
         CLI   MCRECOVR,C'W'                                                    
         BNE   GLINI030                                                         
         OI    GLIND1,GLISOON      Update soon                                  
         DROP  RE                                                               
                                                                                
GLINI030 MVC   DATAMGR,CDATAMGR                                                 
         MVC   DATCON,CDATCON                                                   
         MVC   GETPROF,CGETPROF                                                 
         MVC   HELLO,CHELLO                                                     
         MVC   CALLOV,CCALLOV                                                   
         MVC   SWITCH,CSWITCH                                                   
         MVC   PROTON,CPROTON                                                   
         MVC   PROTOFF,CPROTOFF                                                 
         OC    PROTON,PROTON       Is this actually set ?                       
         BNZ   GLINI032                                                         
         LA    RE,RETURN                                                        
         ST    RE,PROTON           Basicly a NOP                                
         ST    RE,PROTOFF                                                       
                                                                                
GLINI032 MVC   MASTC,CMASTC                                                     
         DROP  RF                                                               
                                                                                
***********************************************************************         
* On-line initialize                                                            
***********************************************************************         
         TM    GLIND1,GLION        On-line                                      
         BZ    GLINI100                                                         
         ICM   R0,15,=X'D9000A50'  QSORT                                        
         GOTOR CALLOV,PARM,0,(R0)                                               
         MVC   @QSORT,0(R1)                                                     
         ICM   R0,15,=X'D9000A63'  ADDTRNS                                      
         GOTOR CALLOV,PARM,0,(R0)                                               
         MVC   @ADTRANS,0(R1)                                                   
                                                                                
         OC    GL2POST,GL2POST     Was this set ?                               
         BNZ   GLINI200            Yes                                          
         XC    DUB,DUB                                                          
         MVC   DUB(4),=X'00FFFFFF'                                              
         GOTO1 SWITCH,DUB                                                       
         MVC   GLTCB,DUB                                                        
         MVI   GLTCB,0                                                          
         SAM31                                                                  
                                                                                
         USING TCBD,RF             Task control block                           
         L     RF,GLTCB                                                         
         L     R2,TCBMINI2         1M block of 31 bit addressing                
         DROP  RF                                                               
                                                                                
         LHI   RE,GL#KOLWS*K+16    Size of on-line block for GLPOST             
         AR    RE,R2                                                            
         ST    RE,@MEDIAS                                                       
         AHI   RE,K                One K block for media codes                  
         ST    RE,ACODES           A(Bin table buffer for names)                
         GOTOR PROTOFF                                                          
         MVC   0(16,R2),=2CL8'**GLWK**'  Eye catcher                            
         GOTOR PROTON                                                           
                                                                                
         L     RE,ACODES           Reload                                       
         LHI   RF,GL#KBUFS         Size of buffers                              
         STC   RF,GL1#OFK          250K of MINIO block 2                        
         STC   RF,GL2#OFK          250K of MINIO block 2                        
         SLL   RF,10               x1024                                        
         AHI   RF,16               For eye catcher                              
         AR    RE,RF                                                            
         ST    RE,GL2POST          A(Bin table of posting data)                 
         B     GLINI160                                                         
                                                                                
***********************************************************************         
* Off-line initialize                                                           
***********************************************************************         
GLINI100 TM    GLIND1,GLIOFF       Off-line                                     
         BO    *+6                                                              
         DC    H'00'               Had to be one or the other                   
                                                                                
         USING MASTD,R2                                                         
         L     R2,MASTC                                                         
         OC    @QSORT,@QSORT                                                    
         BNZ   GLINI102                                                         
         MVC   MCDUB,=CL8'T00A50'  QSORT                                        
         GOTOR MCVLOADM,PARM,0                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   @QSORT,4(R1)                                                     
*                                                                               
* Deactivate this code. Program will fail in case where we keep loading         
* the phase over and over - RUNNER should be okay since it keeps track.         
*                                                                               
                                                                                
GLINI102 OC    @ADTRANS,@ADTRANS   User passed address                          
         BNZ   GLINI104                                                         
*        OC    LADTRANS,LADTRANS   A(Locally saved static address)              
*        JZ    GLINI103                                                         
*        MVC   @ADTRANS,LADTRANS                                                
*        J     GLINI104                                                         
                                                                                
GLINI103 MVC   MCDUB,=CL8'T00A63'  ADDTRNS                                      
         GOTOR MCVLOADM,PARM,0                                                  
         JE    *+6                                                              
         DC    H'0'                                                             
         MVC   @ADTRANS,4(R1)                                                   
*        MVC   LADTRANS,4(R1)                                                   
         DROP  R2                                                               
                                                                                
GLINI104 OC    GL2POST,GL2POST     Was this set ?                               
         JNZ   GLINI200            Yes                                          
         GOTO1 =A(GETOBUF)         Get off-line buffers                         
                                                                                
GLINI160 SAM31                                                                  
         GOTOR PROTOFF                                                          
         L     R2,@MEDIAS                                                       
         MVC   0(16,R2),=2CL8'**MEDS**'    Eye catcher                          
         AHI   R2,16                                                            
         ST    R2,@MEDIAS                                                       
                                                                                
         L     R2,ACODES                                                        
         MVC   0(16,R2),=2CL8'**GLNM**'    Eye catcher                          
         AHI   R2,16                                                            
         ST    R2,ACODES                                                        
                                                                                
         L     R2,GL2POST                                                       
         MVC   0(16,R2),=2CL8'**GL2P**'    Eye catcher                          
         AHI   R2,16                                                            
         ST    R2,GL2POST          Re-adjust for eye-catcher                    
         GOTOR PROTON                                                           
         SAM24                                                                  
                                                                                
         SR    RE,RE                                                            
         LLC   RF,GL2#OFK          #k in XA storage to build postings           
         SLL   RF,10               Size of block in bytes x1024                 
         LA    R1,GLRLNQ           Size of single entry                         
         DR    RE,R1               RF = total number we can have                
         STH   RF,GL2MAX#                                                       
         XC    GL2#OF,GL2#OF       Initialize to zero, none so far              
                                                                                
         SAM31                                                                  
         L     RE,ACODES           A(Start of codes) Bintable                   
         LLC   RF,GL1#OFK                                                       
         SLL   RF,10               Number of byte of table x1024                
         AR    RE,RF               Point to end of table                        
         ST    RE,ANAMES           A(End of names)                              
         ST    RE,NAMEPTR                                                       
         SAM24                                                                  
                                                                                
         MVC   MAX#CDES,=H'1'      Start with 1, this will change               
         XC    #OFCODES,#OFCODES                                                
                                                                                
GLINI200 L     RE,=V(ACGLKEY)                                                   
         LTR   RE,RE                                                            
         BZ    *+12                                                             
         A     RE,GLRELO           Relocation factor                            
         ST    RE,@ACGLKEY                                                      
         OC    @ACGLKEY,@ACGLKEY                                                
         BNZ   *+6                                                              
         DC    H'00'                                                            
                                                                                
         L     RE,=V(ACADDBUK)                                                  
         LTR   RE,RE                                                            
         BZ    *+12                                                             
         A     RE,GLRELO           Relocation factor                            
         ST    RE,@ADDBUK                                                       
         OC    @ADDBUK,@ADDBUK                                                  
         BNZ   *+6                                                              
         DC    H'00'                                                            
                                                                                
         L     RE,=V(BINSRCH)                                                   
         LTR   RE,RE                                                            
         BZ    *+12                                                             
         A     RE,GLRELO           Relocation factor                            
         ST    RE,BIN31                                                         
         OC    BIN31,BIN31                                                      
         BNZ   *+6                                                              
         DC    H'00'                                                            
         OI    PRGIND,PRGINIT                                                   
         J     EXIT                                                             
                                                                                
RETURN   BR    RE                  Return from once I came                      
         EJECT ,                                                                
***********************************************************************         
* Initialize areas and indicators when processing a new company                 
***********************************************************************         
CPYINIT  NTR1                                                                   
         TIME  TU                                                               
         ST    R0,DAYTIME                                                       
         NI    PRGIND,TURNOFF-(PRGNEWOF+PRGPCPY)                                
         OI    PRGIND,PRGBYCPY                                                  
         XC    GLKEYWRK,GLKEYWRK                                                
         NI    GLIND2,TURNOFF-GLIONGL                                           
         XC    #OFCODES,#OFCODES   Reset number of account code/names           
         XC    GL2#OF,GL2#OF       Reset number of posting entries              
         MVC   MAX#CDES,=H'01'     Reset to one                                 
         MVC   NAMEPTR,ANAMES      Reset to end of XA block                     
         MVI   #OFMEDS,0           Set to zero                                  
         MVI   GLCPY_OK,NO         Process company                              
         MVI   GLACC_OK,NO         Process accounts                             
                                                                                
         USING DEFTABD,R2                                                       
         L     R2,=A(DEFTAB)       Table definitions based on #K given          
         A     R2,GLRELO                                                        
         CLI   GLBLK#K,GLMIN#K     Must be >= 2K                                
         BNL   *+6                                                              
         DC    H'00'               Programming error by caller                  
                                                                                
         LLC   RF,GLBLK#K          # of K                                       
         LR    R0,RF                                                            
         MHI   R0,K                Total # of bytes                             
         CLI   GLBLK#K,GLMAX#K                                                  
         BNH   *+8                                                              
         LA    RF,GLMAX#K          Upto 16K max used                            
         BCTR  RF,0                                                             
         MHI   RF,DEFTLNQ                                                       
         AR    R2,RF               R2 = A(DEFTAB entry)                         
                                                                                
*************************                                                       
* Setup @LEDGERS area   *                                                       
*************************                                                       
         SR    RE,RE                                                            
         LHI   RF,GLBLKLNQ         Use 1K area for GLBLOCK and ledgers          
         SR    R3,R3                                                            
         ICM   R3,7,GLBLOCK+1                                                   
         AHI   R3,GLENDQ           End of DSECT area                            
         SHI   RF,GLENDQ           Block area                                   
                                                                                
INIT080  MVI   #MAXLGRS,0                                                       
         MVC   0(8,R3),=CL8'*LEDGERS'                                           
         AHI   R3,8                Add eight for label                          
         SHI   RF,8*5              Less eight x 4 for 4 labels                  
         SHI   RF,4                Less 4 more for boundry alignment            
         ST    R3,@LEDGERS         R3 = A(@LEDGERS)                             
         D     RE,=A(LGRLNQ)                                                    
         STC   RF,#MAXLGRS         Total # of ledgers we can save               
         MHI   RF,LGRLNQ           RF = size of ledger table                    
         AR    R3,RF               Point to end of table                        
         SRL   R3,2                Align to fullword boundry                    
         SLL   R3,2                                                             
         AHI   R3,4                +4 just in cass we step on ledgers           
                                                                                
***********************************************************************         
* Initialize for @PROFBLK  and/or @OFRULES and/or @GLRULES                      
***********************************************************************         
         SR    RF,RF                                                            
         ICM   RF,1,DEFTPROF       Any storage  for profiles                    
         BZ    INIT100             No                                           
         MVC   0(8,R3),=CL8'*PROFILE'                                           
         AHI   R3,8                Add 8 for label                              
         ST    R3,@PROFBLK                                                      
         MVC   @PROF#K,DEFTPROF                                                 
         MHI   RF,K                                                             
         AR    R3,RF               End of block                                 
                                                                                
         SR    RF,RF                                                            
         ICM   RF,1,DEFTOFRL       Any storage for office rules                 
         BZ    INIT100             No                                           
         MVC   0(8,R3),=CL8'*OFRULES'                                           
         AHI   R3,8                Add 8 for label                              
         ST    R3,@OFRULES                                                      
         MVC   @OFRUL#K,DEFTOFRL                                                
         MHI   RF,K                                                             
         AR    R3,RF               End of block                                 
                                                                                
         SR    RF,RF                                                            
         ICM   RF,1,DEFTGLRL       Any storage for GL rules                     
         BZ    INIT100             No                                           
         MVC   0(8,R3),=CL8'*GLRULES'                                           
         AHI   R3,8                Add 8 for label                              
         ST    R3,@GLRULES                                                      
         MVC   @GLRUL#K,DEFTGLRL                                                
                                                                                
INIT100  MVI   GLSVERR#,ERRNONE                                                 
         MVC   GLLSTCPY,TRNCPY     Set to start processing this cpy             
         MVC   GLLSTULA,SPACES                                                  
         MVI   #LEDGERS,0          No ledgers stored                            
         TM    PRGIND,PRGMEDIA     Was media info passed to program             
         BO    *+8                                                              
         MVI   GLMEDIAC,C' '       Clear media code                             
*--------------------------*                                                    
*   Set up GL RULES Area   *                                                    
*--------------------------*                                                    
INIT110  SR    RE,RE                                                            
         SR    RF,RF                                                            
         XC    #GLRULES,#GLRULES                                                
         XC    @GLPPTR,@GLPPTR                                                  
         ICM   RF,1,@GLRUL#K                                                    
         BZ    INIT120                                                          
         MHI   RF,K                Size of area                                 
         D     RE,=A(PSTLNQ)                                                    
         STH   RF,#MAXGLRL         Max number of rules                          
         MVC   @GLPPTR,@GLRULES                                                 
*--------------------------*                                                    
*   Set up BLOCK area      *                                                    
*--------------------------*                                                    
INIT120  SR    RF,RF                                                            
         XC    @GLRPTR,@GLRPTR                                                  
         XC    #KBYTES,#KBYTES                                                  
         ICM   RF,1,@OFRUL#K                                                    
         BZ    INIT200                                                          
         MHI   RF,K                Size of area                                 
         STH   RF,#KBYTES                                                       
         L     RE,@OFRULES                                                      
         ST    RE,@GLRPTR                                                       
                                                                                
INIT200  DS    0H                                                               
         J     EXIT                                                             
         EJECT ,                                                                
***********************************************************************         
* Get company information                                                       
***********************************************************************         
         USING CPYRECD,R4                                                       
COMPANY  NTR1                                                                   
         TM    PRGIND,PRGPCPY      Did we already process                       
         BO    COMPXIT                                                          
                                                                                
COMP004  ICM   R4,15,@COMPANY      Was company record supplied ?                
         BZ    COMP010             No                                           
         CLC   CPYKCPY,TRNCPY      Right company                                
         BE    COMP020             Yes                                          
                                                                                
COMP010  LA    R4,IOKEY                                                         
         MVC   IOKEY,SPACES                                                     
         MVC   CPYKCPY,TRNCPY                                                   
         MVI   ERROR#,ERRNOCPY     No company record found                      
         LA    RE,*                                                             
         ST    RE,AGLERROR                                                      
         LA    R1,IOREAD+IORNFIL                                                
         TM    PRGIND,PRGNEWF      Records are new file?                        
         BO    *+8                 Yes                                          
         LA    R1,IOREAD                                                        
         GOTOR IO                                                               
         JNE   COMPXIT             Error                                        
         L     R4,AIO1                                                          
         MVI   ERROR#,ERRNONE                                                   
                                                                                
COMP020  AH    R4,DATADISP                                                      
         MVI   ERROR#,ERRNXCPY                                                  
         XC    GLCPYE6,GLCPYE6                                                  
                                                                                
COMP030  CLI   0(R4),EOR           End of record                                
         JE    COMPXIT             Error                                        
         CLI   0(R4),CPYELQ        X'10' Company element                        
         JE    COMP040                                                          
         LLC   RF,1(,R4)                                                        
         AR    R4,RF                                                            
         B     COMP030                                                          
                                                                                
         USING CPYELD,R4                                                        
COMP040  DS    0H                                                               
         MVI   ERROR#,ERRNONE                                                   
         OI    PRGIND,PRGPCPY                                                   
*&&US*&& TM    CPYSTAT8,CPYNEWGL                                                
*&&UK*&& TM    CPYSTATC,CPYNEWGL                                                
         BZ    *+8                                                              
         OI    GLIND2,GLIONGL      Company on New GL                            
                                                                                
         USING TRNBLKD,R2                                                       
         ICM   R2,15,@ATRNBLK                                                   
         BZ    COMP042                                                          
         XC    TRNGLMOA,TRNGLMOA                                                
         CLI   CPYLN,CPYLN4Q                                                    
         BL    *+10                                                             
         MVC   TRNGLMOA,CPYGLMOA                                                
         DROP  R2                                                               
                                                                                
COMP042  MVC   GLCPYOFC,CPYOFFC    Save of company default office               
         MVC   GLALPHA,CPYALPHA    Save alpha code                              
         TM    CPYSTAT4,CPYSOFF2   Two character office company ?               
         BZ    *+8                                                              
         OI    PRGIND,PRGNEWOF                                                  
         TM    CPYSTAT1,CPYSOROE                                                
         BZ    *+8                                                              
         OI    PRGIND,PRGEXPOF                                                  
                                                                                
         USING GLRELD,R4                                                        
COMP050  CLI   0(R4),EOR           End of record                                
         BE    COMPXIT             None present                                 
         CLI   0(R4),GLRELQ        X'E6' General ledger office rules            
         BE    COMP060                                                          
         LLC   RF,1(,R4)                                                        
         AR    R4,RF                                                            
         B     COMP050                                                          
                                                                                
COMP060  LA    R2,GLCPYE6          Area to store                                
         LLC   RF,GLRLN                                                         
         BCTR  RF,0                                                             
         EXMVC RF,0(R2),0(R4)      Save off values                              
                                                                                
COMPXIT  J     EXITCC                                                           
         DROP  R4                  CPYELD                                       
         EJECT ,                                                                
***********************************************************************         
* Get profile information - should be replaced by information stored            
*                           on CPY record and Ledger record                     
***********************************************************************         
         USING PRFKEYD,R6                                                       
PROFILES NTR1                                                                   
         LA    R6,PRFKEY                                                        
         XC    PRFKEY,PRFKEY                                                    
         MVI   PRFKSYS,ACCSYS      Accounting                                   
         MVC   PRFKPRG,=C'025'     A25 company level                            
         MVC   PRFKAGY,GLALPHA                                                  
         CLI   GLCPY_OK,YES                                                     
         BE    PROF04                                                           
         MVI   GLCPY_OK,YES        Set as processed                             
         XC    CPYPROF,CPYPROF                                                  
         GOTO1 GETPROF,PARM,PRFKEY,CPYPROF,(X'FF',DATAMGR),@PROFBLK             
                                                                                
PROF04   MVC   PRFKUNL,TRNUNT      A25 Unit/Ledger                              
         CLC   GLLSTULA(2),TRNUNT  Same ledger                                  
         BE    PROF50                                                           
         XC    LGRPROF,LGRPROF                                                  
         GOTO1 GETPROF,PARM,PRFKEY,LGRPROF,(X'FF',DATAMGR),@PROFBLK             
         OC    LGRPROF,LGRPROF     Was this set ?                               
         BNZ   *+10                Yes                                          
         MVC   LGRPROF,CPYPROF     No, so use CPYPROF                           
                                                                                
PROF50   CLI   CPYPROF+5,YES       Post by GL office ?                          
         BNE   *+8                                                              
         OI    GLRIND,GLROFF       Yes                                          
         CLI   CPYPROF+5,C'T'      Post by GL transaction office ?              
         BNE   *+8                                                              
         OI    GLRIND,GLROFFT      Yes                                          
                                                                                
         CLI   LGRPROF+6,YES       Use GL office rules                          
         BNE   PROF55                                                           
         OI    LDGIND,LDGOFFRL     Yes                                          
         CLI   LGRPROF+7,YES       Use GL office ruels for transaction          
         BNE   PROF55                                                           
         OI    LDGIND,LDGOFTRL     Yes                                          
                                                                                
PROF55   TM    PRGIND,PRGNEWOF     New office                                   
         BZ    *+8                                                              
         OI    GLRIND,GLROFF       Force to post by office                      
                                                                                
         TM    PRGIND,PRGNEWOF+PRGEXPOF                                         
         BNZ   *+8                                                              
         OI    GLRIND,GLROFFNO     No offices at all                            
                                                                                
PROFXIT  J     EXITCC                                                           
         DROP  R6                   PRFKEYD                                     
         EJECT                                                                  
**********************************************************************          
* Add entry to ledger table in passed otherwise use local entry      *          
**********************************************************************          
         USING LDGRECD,R4                                                       
LEDGER   NTR1                                                                   
         ICM   R5,15,@LEDGERS      Saved table of ledger info                   
         JNZ   *+8                                                              
         MVI   #MAXLGRS,0          No table so set to zero                      
                                                                                
         CLI   #MAXLGRS,0          Max # of ledgers we can store                
         JE    LEDGR090            Can't build so use local area                
         SR    R0,R0                                                            
         ICM   R0,1,#LEDGERS       Number of ledger have stored                 
         BZ    LEDGR022                                                         
                                                                                
LEDGR010 CLC   TRNLGR,LGRCODE      Match on ledger                              
         JNE   LEDGR020            Found, so pass back R5                       
         TM    LGRIND,LGRRREAD     Re-read cound not save all data              
         BO    LEDGR090            Force to local area                          
         B     LEDGRXIT                                                         
                                                                                
LEDGR020 AHI   R5,LGRLNQ                                                        
         BRCT  R0,LEDGR010         Try again                                    
*                                  Ledger not found so add                      
LEDGR022 MVI   BYTE,YES                                                         
         CLC   #LEDGERS,#MAXLGRS   Reached max for table ?                      
         JL    LEDGR100            Okay to build                                
                                                                                
LEDGR090 MVI   BYTE,NO             No ledger added indicator                    
         LA    R5,LLEDGER          No room so use local ledger area             
                                                                                
LEDGR100 ICM   R4,15,@LEDGER       Ledger record ?                              
         JZ    LEDGR110            Yes                                          
         CLC   LDGKLDG,TRNLGR      Protection code (right ledger ?)             
         JE    LEDGR120            Must be same ledger as transaction           
                                                                                
LEDGR110 LA    R4,IOKEY                                                         
         MVC   IOKEY,SPACES        No, so read                                  
         MVC   LDGKCPY,TRNCPY                                                   
         MVI   LDGKUNT,C'S'                                                     
         MVC   LDGKLDG,TRNLGR                                                   
         MVI   ERROR#,ERRNOLGR     No ledger                                    
         LA    R1,IOREAD+IORNFIL                                                
         TM    PRGIND,PRGNEWF      Records are new file?                        
         BO    *+8                 Yes                                          
         LA    R1,IOREAD           No                                           
         GOTOR IO                                                               
         JNE   LEDGRXIT                                                         
         MVI   ERROR#,ERRNONE                                                   
         L     R4,AIO1                                                          
                                                                                
LEDGR120 SR    R7,R7               R7 = # of GLPELD elements                    
         XC    AELEM15,AELEM15     Set to not found                             
         AH    R4,DATADISP                                                      
         MVC   LGRCODE,TRNLGR      Set ledger code                              
                                                                                
LEDGR122 CLI   0(R4),EOR           End of record                                
         JE    LEDGR600                                                         
         CLI   0(R4),LDGELQ        X'14' Ledger information                     
         JE    LEDGR200                                                         
         CLI   0(R4),GLPELQ        X'15' GL posting element                     
         JE    LEDGR300                                                         
         CLI   0(R4),ACLELQ        X'16' Account levels                         
         JE    LEDGR400                                                         
         CLI   0(R4),GLRELQ        X'E6' General Ledger office rules            
         JE    LEDGR500                                                         
                                                                                
LEDGR124 LLC   RF,1(,R4)                                                        
         AR    R4,RF                                                            
         J     LEDGR122                                                         
                                                                                
***********************************************************************         
* Ledger information about office                                               
***********************************************************************         
         USING LDGELD,R4                                                        
LEDGR200 MVC   LGROFPOS,LDGOPOS    Office position                              
         CLI   LGROFPOS,C'T'                                                    
         BE    *+8                                                              
         CLI   LGROFPOS,0          If null then set to transaction              
         BNE   LEDGR202                                                         
         OI    LGRIND,LGRTOFF                                                   
         MVI   LGROFPOS,C'T'                                                    
                                                                                
LEDGR202 CLI   LGROFPOS,C'1'       Filter office                                
         BL    LEDGR220                                                         
                                                                                
         LHI   R0,FILTSQ           Number of filters                            
         LA    RE,FILTS                                                         
LEDGR210 CLC   0(1,RE),LGROFPOS                                                 
         BE    LEDGR212                                                         
         AHI   RE,FILTLNQ                                                       
         BRCT  R0,LEDGR210                                                      
         MVI   ERROR#,ERRNOFLT                                                  
         B     LEDGRXIT                                                         
                                                                                
LEDGR212 MVC   LGRF#DSP,1(RE)      Save off displacement into RSTELD            
         OI    LGRIND,LGRFOFF      Filter holds office                          
                                                                                
LEDGR220 MVC   LGROFFC,LDGOFFC     Default office ledger code                   
         J     LEDGR124                                                         
                                                                                
***********************************************************************         
* General ledger rules - count how many elements before adding rules            
***********************************************************************         
LEDGR300 LH    RF,#OFGLPS          # of GLPEL's                                 
         AHI   RF,1                                                             
         STH   RF,#OFGLPS                                                       
         OC    AELEM15,AELEM15     Did we save the first one ?                  
         BNZ   *+8                                                              
         ST    R4,AELEM15                                                       
         J     LEDGR124                                                         
                                                                                
***********************************************************************         
* Ledger level definitions                                                      
***********************************************************************         
         USING ACLELD,R4                                                        
LEDGR400 LLC   RF,ACLLN                                                         
         SHI   RF,ACLLN1Q          RF = level portion only  / 16                
         SRL   RF,4                RF = max # of levels                         
         LA    R2,ACLVALS                                                       
         LA    RE,LGRACLVS                                                      
                                                                                
         USING ACLVALS,R2                                                       
LEDGR410 MVC   0(1,RE),ACLVLEN     level length                                 
LEDGR412 AHI   R2,L'ACLVALS                                                     
         AHI   RE,L'LGRACLVA                                                    
         BRCT  RF,LEDGR410                                                      
                                                                                
         CLI   LGRCODE,C'J'                                                     
         JNE   LEDGR124                                                         
         MVC   GLCLTLEN,LGRACLVA                                                
         MVC   GLCPRLEN,LGRACLVB                                                
         J     LEDGR124            Next element                                 
         DROP  R2                                                               
                                                                                
***********************************************************************         
* General ledger office over-ride rules                                         
***********************************************************************         
LIST     USING LGRD,R2                                                          
CPY      USING GLRELD,GLCPYE6                                                   
         USING GLRELD,R4                                                        
LEDGR500 LLC   RF,GLRLN                                                         
         BCTR  RF,0                                                             
         CLC   GLRLN,CPY.GLRLN     Are lengths the same ?                       
         BNE   LEDGR501                                                         
         EXCLC RF,GLRELD,CPY.GLRELD                                             
         BE    LEDGR124                                                         
         DROP  CPY                                                              
                                                                                
LEDGR501 ICM   R2,15,@LEDGERS      Saved table of ledger info                   
         BZ    LEDGR530                                                         
         SR    R0,R0                                                            
         ICM   R0,1,#LEDGERS       # of entries so far                          
         BZ    LEDGR510            None so far                                  
                                                                                
LEDGR502 ICM   RE,15,LIST.LGRELE6  A(GLRELQ) element for ledger                 
         BZ    LEDGR508            None to compare                              
         CLC   GLRLN,1(RE)         Are lengths the same                         
         BNE   LEDGR508            No, so not the same                          
         EXCLC RF,GLRELD,0(RE)     Same values ?                                
         BE    LEDGR550            No, so save copy                             
                                                                                
LEDGR508 AHI   RE,LGRLNQ                                                        
         BCT   R0,LEDGR502                                                      
         B     LEDGR510                                                         
         DROP  LIST                                                             
                                                                                
LEDGR510 ICM   RE,15,@GLRPTR       Free space passed                            
         BZ    LEDGR530            None, so save local copy                     
         LH    R0,#KBYTES                                                       
         CR    RF,R0               Is area big enough ?                         
         BH    LEDGR530            No                                           
         SR    R0,RF                                                            
         STH   R0,#KBYTES          Save what is left                            
         LR    R1,RF                                                            
         AR    R1,RE                                                            
         AHI   R1,1                                                             
         ST    R1,@GLRPTR          Save new location                            
         B     LEDGR532                                                         
                                                                                
LEDGR530 XC    LELEM_E6,LELEM_E6                                                
         LA    RE,LELEM_E6         Local office rules                           
         OI    LGRIND,LGRRREAD     Can't save data so always re-read            
                                                                                
LEDGR532 EXMVC RF,0(RE),GLRELD                                                  
                                                                                
LEDGR550 ST    RE,LGRELE6          Save address of this copy                    
         B     LEDGR124                                                         
                                                                                
***********************************************************************         
* Build general ledger account rules                                            
***********************************************************************         
         USING GLPELD,R4                                                        
         USING PSTD,R6                                                          
LEDGR600 DS    0H                                                               
         SR    R7,R7                                                            
         ICM   R7,3,#OFGLPS        Any rules ?                                  
         BZ    LEDGRXIT            No rules found                               
         CHI   R7,MAXRULES         Can't have more than allowed                 
         BNH   *+6                                                              
         DC    H'00'               Controled by =AFM                            
                                                                                
         LR    R0,R7                                                            
         BCTR  R0,0                                                             
         MHI   R0,PSTLNQ                                                        
         SR    R6,R6                                                            
         ICM   R6,15,@GLPPTR       Any area to build save rules ?               
         BZ    LEDGR602                                                         
         LH    RF,#GLRULES                                                      
         AR    RF,R7               RF = total # rules used                      
         CH    RF,#MAXGLRL         Do we have room for the rules ?              
         JH    LEDGR602            No                                           
         STH   RF,#GLRULES         Save new number of rules                     
         LR    RE,R7                                                            
         MHI   RE,PSTLNQ                                                        
         AR    RE,R6                                                            
         ST    RE,@GLPPTR          Save off new address                         
         B     LEDGR604                                                         
                                                                                
LEDGR602 OI    LGRIND,LGRRREAD     Can't save data so always re-read            
         L     R6,=A(LGLRULES-GLWORKD)        Local area                        
         AR    R6,RC                                                            
         AHI   R6,L'WKSLABEL                                                    
                                                                                
LEDGR604 ST    R6,LGRRULES         GL posting rules table                       
         STC   R7,LGR#GLRL         Number of rules                              
         AR    R6,R0               Point to last entry                          
                                                                                
         LR    R1,R7                                                            
         L     R4,AELEM15          Get 1st rule element                         
LEDGR610 CLI   0(R4),EOR           End of record ?                              
         BE    LEDGR650                                                         
         CLI   0(R4),GLPELQ        X'15'                                        
         BNE   LEDGR640                                                         
                                                                                
         XC    PSTD(PSTLNQ),PSTD   Clear                                        
         MVC   PSTLDG,TRNLGR                                                    
         MVC   PSTTO(10),GLPACC1   G/L ACCOUNT                                  
         CLI   GLPLN,26            NEW LENGTH                                   
         BL    *+10                                                             
         MVC   PSTTO(14),GLPACC1                                                
         OC    PSTTO,SPACES                                                     
                                                                                
         MVC   PSTFROM,GLPSUB                                                   
         TM    LDGIND,LDGPROD      Production                                   
         JZ    LEDGR620            No                                           
         CLI   PSTFROM,C'*'        Posting by office                            
         JNE   LEDGR620            No                                           
         CLI   PSTFROM+1,C' '      Post by Media ?                              
         JE    LEDGR620            Yes                                          
         OI    LGRIND,LGROFPRD                                                  
         MVC   PSTFROM,SPACES      Clear out value for office                   
         MVC   PSTFROM(1),GLPSUB+1 One character office                         
         TM    PRGIND,PRGNEWOF                                                  
         JZ    LEDGR620                                                         
         MVC   PSTFROM(2),GLPSUB+1 Two character office                         
                                                                                
LEDGR620 LA    RE,PSTFROM+L'PSTFROM-1                                           
         LA    RF,L'PSTFROM-1                                                   
LEDGR622 CLI   0(RE),C' '          Spaces                                       
         JH    LEDGR624                                                         
         BCTR  RE,0                                                             
         BRCT  RF,LEDGR622                                                      
                                                                                
LEDGR624 STC   RF,PSTFRLN          Length for compare                           
         SHI   R6,PSTLNQ           Bump backwards                               
         SHI   R1,1                R1 = # of element look for                   
         BNP   LEDGR650            Finished                                     
                                                                                
LEDGR640 LLC   RF,1(,R4)           Next element                                 
         AR    R4,RF                                                            
         J     LEDGR610                                                         
                                                                                
LEDGR650 TM    LDGIND,LDGPROD      Is it production ?                           
         JZ    LEDGR800                                                         
         LLC   R2,LGR#GLRL                                                      
         GOTOR @QSORT,PARM,(1,LGRRULES),(R2),PSTLNQ,L'PSTFROM,0                 
         DROP  R4,R6                                                            
                                                                                
LEDGR800 CLI   BYTE,YES            Added ledger to table ?                      
         JNE   LEDGRXIT            No                                           
         LLC   RF,#LEDGERS         Number of ledger can have stored             
         AHI   RF,1                                                             
         STC   RF,#LEDGERS         Incerment # of ledgers built                 
                                                                                
LEDGRXIT J     EXIT_R5                                                          
         EJECT ,                                                                
***********************************************************************         
* Process account levels                                                        
***********************************************************************         
         USING ACTRECD,R4                                                       
PROCACC  NTR1                                                                   
         MVC   FROMACC,SPACES                                                   
                                                                                
         TM    LDGIND,LDGPROD      Yes                                          
         BO    PROCA100                                                         
         MVC   FROMACC,TRNCPY      Initialize from account                      
                                                                                
         CLI   GLACC_OK,NO                                                      
         BE    PROCA002                                                         
         CLC   TRNULA,GLLSTULA     Same account as before ?                     
         BE    PROCA200            Yes, so used saved values                    
                                                                                
PROCA002 MVC   GLACTOFC,SPACES     Start fresh                                  
         SR    RF,RF                                                            
         ICM   RF,1,LGRACLVD       Length of level D                            
         BZ    PROCA010                                                         
         ICM   R4,15,@ACTLVD       Record at this level                         
         BNZ   PROCA005                                                         
         MVI   ERROR#,ERRNOLVD                                                  
         BRAS  RE,GETACC                                                        
         BE    PROCA005                                                         
         BRAS  RE,CHKPEND          Check if new pending account                 
         BNE   PROCAXIT            Error                                        
         B     PROCA010            Try next level                               
                                                                                
PROCA005 NI    GENIND,TURNOFF-GENLOWLV                                          
         CLI   LGRACLVD,LGRLVLOW   Lowest level                                 
         BNE   *+8                 No                                           
         OI    GENIND,GENLOWLV     Yes                                          
                                                                                
         MVI   ISRULE,GLDTOLVD     Level 4                                      
         BRAS  RE,GETRST                                                        
         BRAS  RE,ACCGLP           Get GLPELQ element                           
         BE    PROCA200                                                         
                                                                                
PROCA010 SR    RF,RF                                                            
         ICM   RF,1,LGRACLVC       Length of level C                            
         BZ    PROCA020                                                         
         ICM   R4,15,@ACTLVC       Record at this level                         
         BNZ   PROCA012                                                         
         MVI   ERROR#,ERRNOLVC                                                  
         BRAS  RE,GETACC                                                        
         BE    PROCA012                                                         
         BRAS  RE,CHKPEND          Check if new pending account                 
         BNE   PROCAXIT            Error                                        
         B     PROCA020            Try next level                               
                                                                                
PROCA012 NI    GENIND,TURNOFF-GENLOWLV                                          
         CLI   LGRACLVC,LGRLVLOW   Lowest level                                 
         BNE   *+8                                                              
         OI    GENIND,GENLOWLV                                                  
                                                                                
         MVI   ISRULE,GLDTOLVC     Level 3                                      
         BRAS  RE,GETRST                                                        
         BRAS  RE,ACCGLP           Get GLPELQ element                           
         BE    PROCA200            Found and set                                
                                                                                
PROCA020 SR    RF,RF                                                            
         ICM   RF,1,LGRACLVB       Length of level B                            
         BZ    PROCA030                                                         
         ICM   R4,15,@ACTLVB       Record at this level                         
         BNZ   PROCA022                                                         
         MVI   ERROR#,ERRNOLVB                                                  
         BRAS  RE,GETACC                                                        
         BE    PROCA022                                                         
         BRAS  RE,CHKPEND          Check if new pending account                 
         BNE   PROCAXIT            Error                                        
         B     PROCA030            Try next level                               
                                                                                
PROCA022 NI    GENIND,TURNOFF-GENLOWLV                                          
         CLI   LGRACLVB,LGRLVLOW   Lowest level                                 
         BNE   *+8                                                              
         OI    GENIND,GENLOWLV                                                  
                                                                                
         MVI   ISRULE,GLDTOLVB     Level 2                                      
         BRAS  RE,GETRST                                                        
         BRAS  RE,ACCGLP           Get GLPELQ element                           
         BE    PROCA200            Found and set                                
                                                                                
PROCA030 SR    RF,RF                                                            
         ICM   RF,1,LGRACLVA       Length of level A                            
         BZ    PROCA200                                                         
         ICM   R4,15,@ACTLVA       Record at this level                         
         BNZ   PROCA032                                                         
         MVI   ERROR#,ERRNOLVA                                                  
         BRAS  RE,GETACC                                                        
         BE    PROCA032                                                         
         BRAS  RE,CHKPEND          Check if new pending account                 
         BNE   PROCAXIT            Error                                        
         B     PROCA040            No, get from ledger                          
                                                                                
PROCA032 NI    GENIND,TURNOFF-GENLOWLV                                          
         CLI   LGRACLVA,LGRLVLOW   Lowest level                                 
         BNE   *+8                                                              
         OI    GENIND,GENLOWLV                                                  
                                                                                
         MVI   ISRULE,GLDTOLVA     Level 1                                      
         BRAS  RE,GETRST                                                        
         BRAS  RE,ACCGLP           Get GLPELQ element                           
         BE    PROCA200            Found and set                                
                                                                                
PROCA040 BRAS  RE,LGRGLP           Posting rule based on ledger                 
         BNE   PROCAXIT            Error                                        
         MVI   ISRULE,GLDTOLDG     From ledger C'L'                             
         B     PROCA200                                                         
                                                                                
***********************************************************************         
* Production ledger only                                                        
***********************************************************************         
PROCA100 LLC   RF,LGRACLVB                                                      
         LA    RE,TRNACT(RF)       Point to media code                          
         CLC   GLMEDIAC,0(RE)      Same as the last time                        
         BE    PROCA102                                                         
         MVC   GLMEDIAC,0(RE)                                                   
         MVC   GLMEDIAN,SPACES                                                  
         BRAS  RE,GETMEDIA                                                      
         BNE   PROCAXIT            Error                                        
                                                                                
PROCA102 MVC   FROMACC,GLMEDIAN    Media name for production                    
         CLI   GLACC_OK,NO                                                      
         BE    PROCA110                                                         
         LLC   RF,LGRACLVA                                                      
         BCTR  RF,0                                                             
         EXCLC RF,TRNACT,GLLSTACT  Same client processing ?                     
         BE    PROCA130            Yes                                          
                                                                                
PROCA110 MVC   GLCLIOFC,SPACES     Start fresh                                  
                                                                                
***********************************************************************         
* Client record                                                                 
***********************************************************************         
         ICM   R4,15,@ACTLVA       Client                                       
         BNZ   PROCA120                                                         
         LLC   RF,LGRACLVA                                                      
         MVI   ERROR#,ERRNOCLI     No client                                    
         BRAS  RE,GETACC           Get client record                            
         BNE   PROCAXIT            Error                                        
                                                                                
PROCA120 BRAS  RE,GETPPR                                                        
         MVC   GLCLIOFC,PPROFFC    Save @ client level                          
         MVI   ERROR#,ERRNOCLO     No client office                             
         CLC   GLCLIOFC,SPACES                                                  
         BNH   PROCAXIT            Error                                        
         MVI   ERROR#,ERRNONE      Okay                                         
         BRAS  RE,GETRST                                                        
         B     PROCA132            Force Product                                
                                                                                
***********************************************************************         
* Product record                                                                
***********************************************************************         
PROCA130 LLC   RF,LGRACLVB                                                      
         BCTR  RF,0                                                             
         EXCLC RF,TRNACT,GLLSTACT  Same cli/prd processing ?                    
         BE    PROCA140                                                         
                                                                                
PROCA132 MVC   GLPRDOFC,SPACES     Start fresh                                  
         ICM   R4,15,@ACTLVB       Product                                      
         BNZ   PROCA134                                                         
         LLC   RF,LGRACLVB         Client + Product length                      
         MVI   ERROR#,ERRNOPRD                                                  
         BRAS  RE,GETACC           Get client record                            
         BNE   PROCAXIT            Error                                        
                                                                                
PROCA134 BRAS  RE,GETPPR                                                        
         MVC   GLPRDOFC,PPROFFC    Save @ product level                         
*&&UK*&& B     PROCA142            Force job                                    
                                                                                
***********************************************************************         
* Job record                                                                    
***********************************************************************         
PROCA140 DS    0H                                                               
*&&UK                                                                           
         CLC   TRNACT,GLLSTACT     Same cli/prd/job ?                           
         BE    PROCA150                                                         
                                                                                
PROCA142 MVC   GLJOBOFC,SPACES     Start fresh                                  
         ICM   R4,15,@ACTLVC       Job                                          
         BNZ   PROCA144                                                         
         LLC   RF,LGRACLVC         Client + Product + Job length                
         MVI   ERROR#,ERRNOJOB                                                  
         BRAS  RE,GETACC           Get client record                            
         BNE   PROCAXIT            Error                                        
                                                                                
PROCA144 BRAS  RE,GETPPR                                                        
         MVC   GLJOBOFC,PPROFFC    Save @ job level                             
*&&                                                                             
PROCA150 BRAS  RE,LGRGLP           Posting rule based on ledger                 
         BNE   PROCAXIT            Error                                        
         DROP  R4                                                               
                                                                                
         USING TRNELD,R4                                                        
PROCA200 L     R4,@TRANACT                                                      
         AH    R4,DATADISP                                                      
         MVC   POSTOFF,SPACES                                                   
         MVC   TRAOFFC,SPACES                                                   
         MVC   ORGOFFC,SPACES                                                   
         MVI   ISOFFC,ISOFFNO         None                                      
         TM    GLRIND,GLROFFNO        No offices at all                         
         BO    PROCA310                                                         
         OC    GLSTAR,GLSTAR                                                    
         BNZ   *+8                 Post by transaction office                   
         TM    GLRIND,GLROFF+GLROFFT                                            
         BZ    PROCA202                                                         
         MVC   TRAOFFC,TRNOFFC        Set to transaction office                 
         MVI   ISOFFC,ISOFFTRN        Is transaction office                     
         DROP  R4                                                               
                                                                                
PROCA202 TM    LDGIND,LDGPROD      Production ?                                 
         BZ    PROCA206                                                         
*&&UK*&& MVI   ISOFFC,ISOFFJOB     Is job office from PPROFFC                   
*&&UK*&& MVC   TRAOFFC,GLJOBOFC    Job office code                              
*&&UK*&& CLC   GLJOBOFC,SPACES                                                  
*&&UK*&& BH    PROCA206                                                         
         MVI   ISOFFC,ISOFFPRD     Is product office from PPROFFC               
         MVC   TRAOFFC,GLPRDOFC    Product office code                          
         CLC   GLPRDOFC,SPACES                                                  
         BH    PROCA206                                                         
         MVI   ISOFFC,ISOFFCLI     Is client office from PPROFFC                
         MVC   TRAOFFC,GLCLIOFC    Client  office code                          
                                                                                
PROCA206 OC    TRAOFFC,SPACES      Pad with spaces                              
         MVC   ORGOFFC,TRAOFFC     Original office                              
         TM    LDGIND,LDGPROD      Production ?                                 
         BO    PROCA220            Yes, so OFFPOS=C always                      
         OC    GLSTAR,GLSTAR                                                    
         BNZ   PROCA220            Post by transaction office                   
         TM    GLRIND,GLROFF       Post by office                               
         BZ    PROCA208                                                         
         TM    LGRIND,LGRTOFF      Is offpos = transaction ?                    
         BO    PROCA220            Yes, so post by transaction office           
                                                                                
PROCA208 TM    GLRIND,GLROFFT      Post by tranaction office                    
         BO    PROCA220            Force the issue                              
         TM    LGRIND,LGRTOFF      Is offpos = transaction ?                    
         BO    PROCA220            Yes, so post by transaction office           
         MVC   TRAOFFC,SPACES      Start a new                                  
         TM    LGRIND,LGRFOFF      Is offpos = filter value ?                   
         BZ    PROCA210            No                                           
         MVC   BYTE,LGROFPOS                                                    
         NI    BYTE,TURNOFF-X'F0'  Change char # to binary                      
         LLC   RF,BYTE                                                          
         SLL   RF,4                Shift four bits                              
         MVC   BYTE,GLFLTLV#                                                    
         NI    BYTE,TURNOFF-X'F0'  Change char # to binary                      
         LLC   R1,BYTE                                                          
         OR    RF,R1                                                            
         STC   RF,ISOFFC           ISOFFC = F# + acct level #                   
         MVC   TRAOFFC(1),GLACTFLT Set office from filter                       
         MVC   ORGOFFC,TRAOFFC     Original office                              
         B     PROCA220                                                         
                                                                                
PROCA210 SR    R1,R1               0 = Length of EX if 1 character              
         TM    LGROFPOS,X'40'      Two character office ?                       
         BZ    *+8                 No                                           
         LHI   R1,1                Yes, 1 = Len of EX if 2 character            
         MVC   BYTE,LGROFPOS                                                    
         NI    BYTE,TURNOFF-X'40'                                               
         MVI   ERROR#,ERROFPOS     Error in OFFPOS                              
         CLI   BYTE,1              Check for valid office position              
         BL    PROCAXIT            Error                                        
         CLI   BYTE,12                                                          
         BH    PROCAXIT            Error                                        
         MVI   ERROR#,ERRNONE      Set back to okay                             
         LLC   RF,BYTE                                                          
         LA    RE,TRNLGR(RF)                                                    
         MVC   TRAOFFC(0),0(RE)    Move in office from account                  
         EX    R1,*-6                                                           
         MVC   ORGOFFC,TRAOFFC     Original office                              
         MVI   ISOFFC,ISOFFIN      Office is in the account                     
                                                                                
PROCA220 BRAS  RE,SETRULE          Set POSTNEW if replacing office              
                                                                                
PROCA224 ICM   RE,15,GLSTAR        Location of star to replace                  
         BZ    PROCA230            None                                         
         MVC   0(1,RE),POSTNEW                                                  
         CLC   POSTNEW,SPACES      Was it changed ?                             
         BH    *+10                Yes so use this                              
         MVC   0(1,RE),ORGOFFC     No so office we had before                   
                                                                                
PROCA230 CLC   POSTNEW,SPACES      Was this set ?                               
         BNH   PROCA300            No, so                                       
         TM    LDGIND,LDGOFTRL     Use GL rules for tranaction office           
         BZ    *+10                No                                           
         MVC   TRAOFFC,POSTNEW     Replace with new office value                
                                                                                
PROCA300 MVC   GLISOFFC,ISOFFC                                                  
         TM    GLRIND,GLROFF+GLROFFT  Post some office ?                        
         BZ    PROCA310               No                                        
         MVC   POSTOFF,GLACTOFC                                                 
         MVC   GLISOFFC,ISACTOFF   If it is GLACTOFC then it was                
         CLC   POSTOFF,SPACES      Use account office                           
         BH    PROCA310                                                         
         MVI   GLISOFFC,ISOFFLGR   Is from ledger record                        
         MVC   POSTOFF,LGROFFC                                                  
         CLC   POSTOFF,SPACES      Use ledger  office                           
         BH    PROCA310                                                         
         MVC   POSTOFF,TRAOFFC     All others are save in TRAOFFC               
         MVC   GLISOFFC,ISOFFC     Reset                                        
         CLC   POSTOFF,SPACES      Use other office                             
         BH    PROCA310                                                         
         MVI   GLISOFFC,ISOFFCPY   Is from company record                       
         MVC   POSTOFF,GLCPYOFC                                                 
                                                                                
PROCA310 TM    LDGIND,LDGPROD      Production                                   
         BO    PROCA500            FROMACC is media name                        
         CLI   GLTORULE,GLTOACCA                                                
         BL    PROCA320                                                         
         CLI   LGRPROF,YES         Post contra by unit/ledger only              
         BE    PROCA320                                                         
         CLI   LGRPROF,C'L'        Post contra by unit/ledger only              
         BE    PROCA320                                                         
         CLI   LGRPROF,C'P'        Post contra by level of account              
         BNE   PROCA500                                                         
         MVC   FROMACC,GLSVFROM                                                 
         B     PROCA500                                                         
                                                                                
PROCA320 MVC   FROMACC+3(L'FROMACC-3),SPACES                                    
                                                                                
PROCA500 MVI   GLACC_OK,YES        Have done once for account                   
                                                                                
PROCAXIT J     EXITCC                                                           
         EJECT ,                                                                
***********************************************************************         
* Add GLDEL, X'63'                                                              
* Update TRSELD, X'60' and GLDELD, X'63' element if GLACTION=GLUPDTE            
***********************************************************************         
         USING TRSELD,R4                                                        
ADDGLD   NTR1                                                                   
         OC    GLTODAYC,GLTODAYC                                                
         BNZ   ADDGL008                                                         
         GOTOR DATCON,PARM,(5,0),(2,GLTODAYC)                                   
         GOTOR DATCON,PARM,(2,GLTODAYC),(1,GLTODAYP)                            
                                                                                
ADDGL008 DS    0H                                                               
         TM    GLACTION,GLADDEL+GLUPDTE                                         
         BZ    ADDGLXIT                                                         
                                                                                
         USING GLRECD,R6                                                        
         LA    R6,GLBFREC                                                       
*                                                                               
ADDGL010 TM    GLACTION,GLUPDTE    Update TRSEL element?                        
         BZ    ADDGL012            No                                           
         MVI   ERROR#,ERRNOTRS     Missing TRSELQ element                       
         ICM   R4,15,AELEM60       TRSELQ element                               
         BZ    ADDGLXIT                                                         
         MVI   ERROR#,ERRNONE      Okay then                                    
         MVC   TRSUPDT,GLTODAYC                                                 
         OI    TRSSTAT,TRSSGLUP    Updated to GL                                
                                                                                
         USING TRNRECD,R3                                                       
         L     R3,@TRANACT                                                      
         GOTOR ADDNAME,TRNKCULA    Add name to buffer                           
         DROP  R3                                                               
*                                                                               
         USING GLDELD,R4                                                        
ADDGL012 ICM   R4,15,AELEM63       GLDELQ element                               
         BNZ   ADDGL020                                                         
         LA    R4,LELEM            Local element area                           
         XC    LELEM,LELEM                                                      
                                                                                
ADDGL020 MVI   GLDEL,GLDELQ        X'63'                                        
         MVI   GLDLN,GLDLNQ                                                     
         OI    GLDIND,GLDDRAFT     Set as draft                                 
         TM    GLACTION,GLUPDTE                                                 
         BZ    ADDGL022                                                         
         NI    GLDIND,TURNOFF-GLDDRAFT     Updating, not draft                  
         OI    GLDIND,GLDUBUK      Updated to Unit G and GLBRECD                
         MVC   GLDDATE,GLTODAYP    This should match TRSUPDT                    
         MVC   GLDUPTM,DAYTIME     Update time                                  
         MVC   GLDPMOS,SVGLMOA     Save MOA                                     
         TM    GLIND1,GLILIVE      Going from draft to live?                    
         BO    ADDGL030            Yes, so already set                          
                                                                                
ADDGL022 DS    0H                                                               
         MVC   GLDPMOS,SVGLMOA     Save MOA                                     
         MVC   GLDTODAY,GLTODAYP   When actually added to file                  
         MVC   GLDULA,GLTOACC                                                   
         MVC   GLDOFFC,POSTOFF                                                  
         MVC   GLDCNTRA,FROMACC                                                 
         MVC   GLDPRG#,GLPRG#      Set call idenifier (required)                
         CLI   GLDPRG#,0                                                        
         BNE   *+6                                                              
         DC    H'00'               Bad call by application                      
                                                                                
         MVC   GLDOFFO,ORGOFFC        Save original office                      
         TM    GLRIND,GLROFF+GLROFFT  Posting by office                         
         BZ    *+8                    No                                        
         OI    GLDIND,GLDBYOFF        Yes                                       
         MVC   GLDOFRUL,GLISOFFC      Office posting rule                       
         MVC   GLDTORUL,GLTORULE      TO    account  rule                       
                                                                                
         OC    AELEM63,AELEM63     Element on record already                    
         BNZ   ADDGL030            Done                                         
         LA    R2,ACCBIG                                                        
         TM    PRGIND,PRGNEWF      New file ?                                   
         BZ    *+8                                                              
         LA    R2,ACCMST                                                        
         GOTOR HELLO,PARM,(C'P',(R2)),@TRANACT,GLDELD,0                         
         ICM   R4,15,16(R1)                                                     
         CLI   12(R1),0            Any errors ?                                 
         BE    ADDGL030                                                         
         MVI   ERROR#,ERR2SMAL                                                  
         CLI   12(R1),5            Record too small                             
         BE    ADDGLXIT                                                         
         DC    H'00'               Bad call                                     
                                                                                
         USING TRNELD,R6                                                        
ADDGL030 ST    R4,@GLDEL           Return element address                       
         TM    GLACTION,GLUPDTE                                                 
         BZ    ADDGL031                                                         
         TM    LDGIND,LDGPROD      Is it production                             
         BO    ADDGL031            Yes, so don't add name                       
         GOTOR ADDNAME,GLDCCPY     Add Unit G contra name                       
                                                                                
ADDGL031 TM    PRGIND,PRGNEWOF     Two character office                         
         BZ    ADDGL032                                                         
         CLC   GLDOFFC,SPACES                                                   
         BH    ADDGL032                                                         
         MVI   ERROR#,ERRIVTRN     Invalid transaction                          
         B     ADDGLXIT                                                         
                                                                                
         USING ACTRECD,R2                                                       
         USING TRNRECD,R3                                                       
ADDGL032 LA    R2,IOKEY                                                         
         L     R3,@TRANACT                                                      
         MVC   ACTKEY,SPACES                                                    
         MVC   ACTKCPY,TRNKCPY                                                  
         MVI   ACTKUNT,C'G'                                                     
         MVC   ACTKULA,GLDULA                                                   
         MVI   ERROR#,ERRNOGLA     Invalid GL account                           
         GOTOR IO,IOREAD+IORNFIL                                                
         BNE   ADDGLXIT                                                         
         DROP  R3                                                               
*&&DO                              Remove,Canada locks all there GLs            
         L     R2,AIO1                                                          
         LA    R3,ACTRFST          A(First element)                             
ADDGL040 CLI   0(R3),EOR                                                        
         BE    ADDGL090                                                         
         CLI   0(R3),RSTELQ        X'30' Status element                         
         BE    ADDGL045                                                         
         LLC   RF,1(,R3)                                                        
         AR    R3,RF                                                            
         B     ADDGL040                                                         
                                                                                
         USING RSTELD,R3                                                        
         USING TRNBLKD,R7                                                       
ADDGL045 ICM   R7,15,@ATRNBLK                                                   
         BZ    ADDGL090                                                         
         MVI   ERROR#,ERRLKOCL              Locked or closed                    
         TM    RSTSTAT1,RSTSACIC+RSTSACIL                                       
         BZ    ADDGL090                                                         
         TM    TRNINDS,TRNIDUCL    TEST DON'T UNLOCK/UNCLOSE ACCOUNTS           
         BO    ADDGLXIT                                                         
         DROP  R3,R7                                                            
*&&                                                                             
ADDGL090 MVI   ERROR#,ERRNONE      All is okay                                  
         DROP  R2                                                               
                                                                                
ADDGLXIT CLI   ERROR#,ERRNONE                                                   
         J     EXIT                                                             
         DROP  R4,R6                                                            
         EJECT ,                                                                
***********************************************************************         
* Add GL details to table so we can create records at end             *         
***********************************************************************         
         USING TRNELD,R2                                                        
         USING GLDELD,R4                                                        
         USING TRNRECD,R6                                                       
GLADD    NTR1                                                                   
         MVI   ERROR#,ERRNONE                                                   
         L     R6,@TRANACT         Point to tranaction                          
         LR    R2,R6                                                            
         AH    R2,DATADISP                                                      
         CLI   TRNEL,TRNELQ        X'44'                                        
         BE    *+6                                                              
         DC    H'00'                                                            
                                                                                
         L     R4,@GLDEL                                                        
         XC    AELEM60,AELEM60                                                  
         LR    R3,R2                                                            
GLADD010 CLI   0(R3),EOR                                                        
         BNE   *+6                                                              
         DC    H'00'               Must have TRSELD element                     
         CLI   0(R3),TRSELQ        X'60'                                        
         BE    GLADD020                                                         
         LLC   RF,1(,R3)                                                        
         AR    R3,RF                                                            
         B     GLADD010                                                         
                                                                                
         USING TRSELD,R3                                                        
GLADD020 ST    R3,AELEM60                                                       
         GOTOR DATCON,PARM,(2,TRSUPDT),(1,WORK)                                 
                                                                                
         CLC   GLDDATE,WORK                                                     
         BE    *+6                                                              
         DC    H'00'               Must match                                   
                                                                                
         MVI   BYTE,0                                                           
         TM    PRGIND,PRGNEWF      New file structure ?                         
         BZ    *+8                 No                                           
         OI    BYTE,X'80'          Yes                                          
         GOTOR @ACGLKEY,PARM,(BYTE,@TRANACT),@GLDEL,GLTRNKEY,GLGLBKEY, +        
               (L'GLKEYWRK,GLKEYWRK),@COMFACS                                   
                                                                                
BF       USING GLRECD,GLBFREC                                                   
GL       USING GLBRECD,GLGLBKEY                                                 
GT       USING TRNRECD,GLTRNKEY                                                 
         XC    GLBFREC,GLBFREC                                                  
         MVC   BF.GLRKEY1,GL.GLBKGLA    Save off key                            
         CLC   GL.GLBKGSPC,SPACES       Production ?                            
         JE    GLADD040                 Yes, so okay as is                      
                                                                                
*&&UK*&& TM    PRGIND,PRGNEWOF                                                  
*&&UK*&& JZ    GLADD028                                                         
         CLC   BF.GLRKGOFF,SPACES     Can't be spaces                           
         JH    GLADD028                                                         
         MVI   ERROR#,ERRNOOFF     No office, US should have                    
         LA    RE,*                                                             
         ST    RE,AGLERROR         For debugging                                
         J     GLADDXIT                                                         
                                                                                
GLADD028 LA    RF,L'TRNKULA                                                     
         LA    RE,GT.TRNKULC+L'TRNKULC-1                                        
GLADD030 CLI   0(RE),C' '                                                       
         BH    GLADD036                                                         
         BCTR  RE,0                                                             
         BRCT  RF,GLADD030                                                      
         MVI   ERROR#,ERRNOGLA                                                  
         LA    RE,*                                                             
         ST    RE,AGLERROR         For debugging                                
         J     GLADDXIT            Did something wrong so check                 
                                                                                
GLADD036 STC   RF,BF.GLRKCLEN      Save off length                              
         BCTR  RF,0                                                             
         EXCLC RF,GL.GLBKSULA,GT.TRNKULC                                        
         BE    *+6                                                              
         DC    H'00'               Figure out what I did wrong                  
                                                                                
GLADD040 MVC   BF.GLRKMOA,TRSPMOS         Month posted to                       
         ZAP   BF.GLRAMNT,TRNAMNT                                               
         MVC   BF.GLRKSBR,GT.TRNKSBR      Say if it is CR/DR and/or rev         
         MVC   BF.GLRTYPE,TRNTYPE                                               
         DROP  R3                                                               
                                                                                
         SAM31                                                                  
         GOTOR PROTOFF                                                          
         SR    RF,RF                                                            
         ICM   RF,3,GL2#OF                                                      
         SR    R7,R7                                                            
         ICM   R7,3,GL2MAX#                                                     
         GOTOR BIN31,PARM,GLBFREC,GL2POST,(RF),                        +        
               (1,GLRLNQ),(0,GLRKLNQ),(R7)                                      
         MVC   GL2#OF,10(R1)       # of entries                                 
         ICM   RF,15,0(R1)                                                      
         BNZ   *+6                                                              
         DC    H'00'               Table full                                   
         CLC   GL2#OF,GL2MAX#      Are we about to max out                      
         BNE   *+8                                                              
         OI    PENDING,PENDPOST                                                 
                                                                                
         USING GLRECD,RF                                                        
         TM    0(R1),X'80'         Record just added?                           
         BO    GLADDXIT            Yes                                          
         AP    GLRAMNT,BF.GLRAMNT                                               
         DROP  RF                                                               
                                                                                
GLADDXIT GOTOR PROTON                                                           
         SAM24                                                                  
         CLI   ERROR#,ERRNONE                                                   
         J     EXIT                                                             
         DROP  R2,R4,R6                                                         
         DROP  GL,GT,BF                                                         
         EJECT ,                                                                
***********************************************************************         
* Update Unit GLBRECD and GL tranaction based records                           
*        Multi-pass processing                                                  
* Pass 1 Add/Update special record                                              
* Pass 2 Add transaction to unit G via ADDTRANS                                 
***********************************************************************         
         USING TRNBLKD,R2                                                       
NEW      USING TRNBLKD,LTRNBLK                                                  
GLUPDATE NTR1                                                                   
         SR    R0,R0                                                            
         ICM   R0,3,GL2#OF         Number of items in table                     
         BZ    GLUPDXIT            None                                         
         ICM   R2,15,@ATRNBLK                                                   
         BZ    GLUPDXIT            Can't update then                            
                                                                                
         GOTOR DATCON,PARM,(5,0),(X'20',SVGLREF)                                
         TM    TRNINDS,TRNIDRFT                                                 
         BO    GLUPDXIT            Don't process if draft transactions          
         XC    TRN#OF,TRN#OF                                                    
         XC    MAX#OF,MAX#OF                                                    
         XC    SVGLKEY,SVGLKEY                                                  
         MVI   PENDING,0           Nothing pending                              
                                                                                
*==============================*                                                
* First pass                   *                                                
*==============================*                                                
         USING GLRECD,R6                                                        
         SAM31                                                                  
         LA    R6,GLBFREC                                                       
         L     R3,GL2POST          31 bit address                               
         MVI   OLDEST,X'FF'        Initialize                                   
GLUPD100 GOTOR PROTOFF                                                          
         MVC   GLRECD(GLRLNQ),0(R3)                                             
         GOTOR PROTON                                                           
         SAM24                     24 bit address                               
         LH    R1,MAX#OF           Number of items in table                     
         AHI   R1,1                Add one for this one                         
         STH   R1,MAX#OF           # of free entries now in GL2POST             
         MVC   WORK(2),GLRKMOA                                                  
         MVI   WORK+2,1                                                         
         GOTOR DATCON,PARM,(X'31',WORK),(1,SVGLDATE),(1,0)                      
         GOTOR PS1RECS             Start posting GL special records             
         SAM31                                                                  
         AHI   R3,GLRLNQ           Next entry                                   
         BRCT  R0,GLUPD100         Any more accounts ?                          
         SAM24                                                                  
                                                                                
         XC    GL2#OF,GL2#OF       Reset to zero                                
         TM    PENDING,PENDGLBR    Do we need to write/add last one?            
         BZ    GLUPD110            No                                           
         BRAS  RE,DATETIME                                                      
*        BRAS  RE,HILOW            Set Low and High MOA                         
*        BRAS  RE,CXISET           Set changed/tramsmit info                    
         BRAS  RE,UPDATE           Updates record in AIO1                       
                                                                                
GLUPD110 OC    TRN#OF,TRN#OF       Number of transaction to post                
         BNZ   *+6                                                              
         DC    H'00'               No way                                       
                                                                                
*==================================================*                            
* Second pass post GL transactions via ADDTRANS    *                            
*==================================================*                            
         LA    RE,NEW.TRNBLK                                                    
         LA    RF,TRNBLKL                                                       
         SR    R0,R0                                                            
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
         MVI   NEW.TRNINDS,TRNICONV New file                                    
         MVC   NEW.TRNGLMOA,TRNGLMOA                                            
         MVC   NEW.TRNREC,AIO1                                                  
         CLI   GLWRITE,YES                                                      
         BE    *+8                                                              
         OI    NEW.TRNINDS,TRNIWRNO                                             
         OI    NEW.TRNINDS2,TRNIPSTG    Indicates coming from GL POST           
         MVC   NEW.TRNMODE,TRNMODE                                              
         MVC   NEW.TRNCTRY,TRNCTRY                                              
         MVC   NEW.TRNCOMF(TRNANCA-TRNCOMF),TRNCOMF                             
         MVC   NEW.TRNPUSER,TRNPUSER                                            
         MVC   NEW.TRNPDATS,TRNPDATS                                            
*&&UK*&& MVC   NEW.TRNTOBA,TRNTOBA                                              
         MVC   NEW.TRNCCURS,TRNCCURS                                            
         MVC   NEW.TRNCPYSA,TRNCPYSA                                            
         MVC   NEW.TRN#LDGS,TRN#LDGS                                            
         MVC   NEW.TRNUDATE,TRNUDATE                                            
         MVC   NEW.TRNEFDT,TRNEFDT                                              
                                                                                
         SAM31                                                                  
         SR    R0,R0                                                            
         ICM   R0,3,TRN#OF         Number of tranaction to post                 
         L     R3,GL2POST          31 bit address                               
         LA    R6,GLBFREC                                                       
GLUPD200 GOTOR PROTOFF                                                          
         MVC   GLRECD(GLRLNQ),0(R3)                                             
         GOTOR PROTON                                                           
         SAM24                                                                  
         BRAS  RE,BLDTRNR                                                       
                                                                                
         USING TRNRECD,R4                                                       
         L     R4,AIO1             Update/Add contra name                       
         XC    NEW.TRNCACNM,NEW.TRNCACNM                                        
         CLC   TRNKCULC(3),SPACES                                               
         JE    GLUPD206                                                         
         MVC   NEW.TRNCACNM,SPACES                                              
         GOTOR GETNAME,PARM,TRNKULC,NEW.TRNCACNM                                
         DROP  R4                                                               
                                                                                
GLUPD206 GOTOR @ADTRANS,NEW.TRNBLKD                                             
         JE    GLUPD220                                                         
         TM    GLIND1,GLION        On-line                                      
         JZ    GLUPD220            Off-line                                     
         LLC   RF,NEW.TRNERRS                                                   
         DC    H'00'               Die                                          
                                                                                
GLUPD220 SAM31                                                                  
         AHI   R3,GLRLNQ           Next entry                                   
         BRCT  R0,GLUPD200         Any more accounts ?                          
         SAM24                                                                  
                                                                                
         OI    NEW.TRNINDS,TRNILAST    Finish up                                
         GOTOR @ADTRANS,NEW.TRNBLKD                                             
                                                                                
GLUPDXIT SAM24                                                                  
         J     EXIT                                                             
         DROP  NEW                                                              
         DROP  R2,R6                                                            
         EJECT ,                                                                
***********************************************************************         
* Add/Write back GL records                                                     
***********************************************************************         
TKEY     USING TRNRECD,GLTRNKEY                                                 
GKEY     USING GLBRECD,GLGLBKEY                                                 
         USING GLRECD,R6                                                        
PS1RECS  NTR1                                                                   
*====================================*                                          
* Create special record key, GLBKEY  *                                          
*====================================*                                          
         LA    R6,GLBFREC                                                       
         MVI   GKEY.GLBKTYP,GLBKTYPQ    X'28'                                   
         MVC   GKEY.GLBKCPY,GLLSTCPY                                            
         MVC   GKEY.GLBKGLA(L'GLRKEY1),GLRKEY1                                  
         BRAS  RE,SETAMNT          Creat BUKEL element                          
                                                                                
         CLC   GKEY.GLBKEY,SVGLKEY                                              
         BE    PS1REC20                                                         
         MVC   SVGLKEY,GKEY.GLBKEY                                              
         TM    PENDING,PENDGLBR    Do we need to write/add one now              
         BZ    PS1REC10                                                         
         BRAS  RE,DATETIME                                                      
*        BRAS  RE,HILOW            Set Low and High MOA                         
*        BRAS  RE,CXISET           Set changed/tramsmit info                    
         BRAS  RE,UPDATE           Updates record in AIO1                       
         MVI   OLDEST,X'FF'        Reset                                        
         NI    PENDING,TURNOFF-PENDGLBR                                         
                                                                                
PS1REC10 BRAS  RE,GETGLBR          Get record                                   
PS1REC20 GOTOR @ADDBUK,PARM,(X'80',AIO1),LELEM_45,HELLO                         
                                                                                
*==================================================================*            
* Create transaction key for 2nd pass add to bintable, reuse table *            
*==================================================================*            
         MVC   TKEY.TRNKCPY,GLLSTCPY                                            
         MVI   TKEY.TRNKUNT,C'G'                                                
         MVC   TKEY.TRNKLDG(L'GLBKGLA),GLRKEY1 GB or GP acct.                   
         MVC   TKEY.TRNKOFF,GLRKGOFF                                            
         MVC   TKEY.TRNKCULC,SPACES                                             
         CLC   GKEY.GLBKGSPC,SPACES                                             
         BNE   PS1REC30            Must be SJ production                        
         MVC   TKEY.TRNKCULC,GLRKSULA                                           
         B     PS1REC32                                                         
                                                                                
PS1REC30 MVC   TKEY.TRNKCCPY,GLLSTCPY                                           
         LLC   RF,GLRKCLEN                                                      
         BCTR  RF,0                                                             
         MVC   TKEY.TRNKULC(0),GLRKSULA                                         
         EX    RF,*-6                                                           
                                                                                
PS1REC32 MVC   TKEY.TRNKREF,SVGLREF      Today                                  
         CLI   GLRTYPE,81                Unless type 81, then:                  
         BNE   *+10                                                             
         MVC   TKEY.TRNKREF,=CL6'RTEARN'                                        
         MVC   TKEY.TRNKDATE,SVGLDATE    End of month based on post MOS         
         MVC   TKEY.TRNKSBR,GLRKSBR                                             
         MVC   GLRKEY2,TKEY.TRNKEY                                              
                                                                                
         SAM31                                                                  
         GOTOR PROTOFF                                                          
         SR    RF,RF                                                            
         ICM   RF,3,TRN#OF                                                      
         SR    R7,R7                                                            
         ICM   R7,3,MAX#OF                                                      
         GOTOR BIN31,PARM,GLBFREC,GL2POST,(RF),(1,GLRLNQ),             +        
               (0,GLRKLNQ),(R7)                                                 
         MVC   TRN#OF,10(R1)       # of entries                                 
                                                                                
HI       USING GLRECD,R4                                                        
         ICM   R4,15,0(R1)                                                      
         BNZ   *+6                                                              
         DC    H'00'               Table full                                   
                                                                                
         TM    0(R1),X'80'         Record just added?                           
         BO    PS1RECX             Yes                                          
         AP    HI.GLRAMNT,GLRAMNT                                               
         DROP  HI                                                               
                                                                                
PS1RECX  GOTOR PROTON                                                           
         SAM24                                                                  
         J     EXIT                                                             
         DROP  GKEY,TKEY                                                        
         EJECT ,                                                                
***********************************************************************         
* Create BUKEL element from GLBFREC recrod                                      
***********************************************************************         
NEW      USING BUKELD,LELEM_45     Local element X'45'                          
SETAMNT  XC    LELEM_45,LELEM_45                                                
         MVI   NEW.BUKEL,BUKELQ        X'45'                                    
         MVI   NEW.BUKLN,BUKLNQ                                                 
         MVC   NEW.BUKMOS,GLRKMOA                                               
         ZAP   NEW.BUKDR,=P'0'                                                  
         ZAP   NEW.BUKCR,=P'0'                                                  
         LA    RF,NEW.BUKCR                                                     
         TM    GLRKSBR,X'01'       Even is a DR, odd is a CR                    
         BO    *+8                                                              
         LA    RF,NEW.BUKDR                                                     
         ZAP   0(L'BUKDR,RF),GLRAMNT                                            
         CLC   GLRKMOA,OLDEST      Current MOA vs OLDEST MOA Changed            
         BNL   *+10                                                             
         MVC   OLDEST,GLRKMOA      New oldest MOA                               
         BR    RE                                                               
         DROP  R6                                                               
         EJECT ,                                                                
***********************************************************************         
* Get GLBRECD to update or create one to add                                    
***********************************************************************         
         USING GLBRECD,R4                                                       
GETGLBR  ST    RE,SVRE                                                          
         L     R4,AIO1                                                          
         OI    PENDING,PENDGLBR    We need to write/add one now                 
         MVC   IOKEY,GLGLBKEY      Get special record                           
         GOTOR IO,IORUPD+IOREAD+IORNFIL+IORDEL                                  
         JE    GETGL10             Record found (not deleted)                   
                                                                                
         XC    0(GLBRFST-GLBRECD+2,R4),0(R4)                                    
         MVC   GLBKEY,GLGLBKEY                                                  
         MVC   GLBRLEN,=AL2(GLBRFST-GLBRECD+1)                                  
                                                                                
         USING CACELD,R3                                                        
GETGL10  LA    R3,LELEM                                                         
         XC    LELEM,LELEM                                                      
         MVI   CACEL,CACELQ        X'43'                                        
         MVC   CACCNT,SPACES       Set account                                  
         MVC   CACCNTC,GLBKCPY                                                  
         MVC   CACCNTU(2),=C'SJ'                                                
         MVC   CACCNTA(L'GLBKCP),GLBKCP                                         
         CLC   GLBKGSPC,SPACES     Production ?                                 
         BE    *+10                                                             
         MVC   CACCNTU(L'GLBKSULA),GLBKSULA                                     
         GOTOR GETNAME,PARM,CACCNTU,CACNAME                                     
         SR    RF,RF                                                            
         ICM   RF,1,PARM+3         Length passed back in P1+3                   
         BZ    GETGLXIT            Name not found                               
         AHI   RF,CACLN1Q                                                       
         STC   RF,CACLN                                                         
         GOTOR @ADDBUK,PARM,(X'80',AIO1),CACEL,HELLO                            
         DROP  R3                                                               
                                                                                
GETGLXIT L     RE,SVRE                                                          
         BR    RE                                                               
         DROP  R4                                                               
         EJECT ,                                                                
***********************************************************************         
* Build transaction record to pass to ADDTRANS                                  
***********************************************************************         
                                                                                
         USING TRNRECD,R4                                                       
         USING GLRECD,R6                                                        
BLDTRNR  ST    RE,SVRE                                                          
         L     R4,AIO1                                                          
         XC    0(100,R4),0(R4)                                                  
         LA    R5,LELEM                                                         
         LA    R6,GLBFREC                                                       
         MVC   TRNKEY,GLRKEY2      Set tranaction key                           
                                                                                
         USING TRNELD,R5                                                        
         XC    LELEM,LELEM                                                      
         MVI   TRNEL,TRNELQ        X'44'                                        
         MVI   TRNLN,TRNLN1Q+1                                                  
         MVC   TRNDATE,TRNKDATE                                                 
         MVC   TRNREF,TRNKREF                                                   
         MVC   TRNSUB,TRNKSBR                                                   
         MVI   TRNTYPE,25                                                       
         MVI   TRNSTAT,0                                                        
         MVI   TRNNARR,C' '        Not sure why but its everywhere              
         TM    TRNKSBR,X'01'       Is it odd or even                            
         BO    *+8                 It's odd so it is a credit                   
         MVI   TRNSTAT,TRNSDR                                                   
         ZAP   TRNAMNT,GLRAMNT                                                  
         MVC   TRNBTCH,SPACES                                                   
         MVC   WORK,GLRKMOA                                                     
         OI    WORK,X'F0'          Turn year into character                     
         OI    WORK+1,X'F0'        Tunr months 1 to 9 into character            
         CLI   GLRKMOA+1,X'10'     Less then 10 then done                       
         BL    BLDTRN20                                                         
         LLC   RF,GLRKMOA+1        Get month                                    
         AHI   RF,C'A'-X'10'                                                    
         STC   RF,WORK+1                                                        
                                                                                
BLDTRN20 MVC   TRNBTCH(2),WORK                                                  
         MVC   TRNOFFC,TRNKOFF                                                  
         TM    PRGIND,PRGNEWOF     Two character offices                        
         JO    *+10                Yes, so not applicable                       
         MVC   TRNKOFF,SPACES                                                   
         GOTOR HELLO,PARM,(C'P',ACCMST),(R4),LELEM,0                            
         CLI   12(R1),0            Any errors ?                                 
         BE    *+6                                                              
         DC    H'00'                                                            
                                                                                
         USING TRSELD,R5                                                        
         XC    LELEM,LELEM                                                      
         MVI   TRSEL,TRSELQ    X'60'                                            
         MVI   TRSLN,TRSLNQ                                                     
         MVC   TRSDATE,GLTODAYC                                                 
         MVC   TRSPMOS,GLRKMOA                                                  
         GOTOR HELLO,PARM,(C'P',ACCMST),(R4),LELEM,0                            
         CLI   12(R1),0            Any errors ?                                 
         BE    *+6                                                              
         DC    H'00'                                                            
         DROP  R5                                                               
                                                                                
         L     RE,SVRE                                                          
         BR    RE                                                               
         DROP  R4,R6                                                            
         DROP  NEW                                                              
         EJECT ,                                                                
***********************************************************************         
* Set date/time in key                                                          
***********************************************************************         
         USING GLBRECD,R4                                                       
DATETIME L     R4,AIO1                                                          
         XC    GLBRSTA,GLBRSTA                                                  
         MVC   GLBRUPDT,GLTODAYC                                                
         MVC   GLBRUPTM,DAYTIME                                                 
                                                                                
KEY      USING GLBRECD,IOKEY                                                    
         MVC   KEY.GLBKSTA,GLBRSTA                                              
         BR    RE                                                               
         DROP R4,KEY                                                            
         EJECT ,                                                                
*&&DO                                                                           
***********************************************************************         
* Set high and low mos in record and key                                        
***********************************************************************         
         USING BUKELD,R2                                                        
         USING GLBRECD,R4                                                       
HILOW    NTR1                                                                   
         L     R4,AIO1                                                          
         NI    GLBRSTAT,TURNOFF-GLBRXMIT                                        
         LA    R2,GLBRFST          First element                                
HILOW10  CLI   0(R2),EOR           End of record                                
         BE    HILOWXIT                                                         
         CLI   0(R2),BUKELQ        X'45' bucket element                         
         BE    HILOW20             Find first one                               
         LLC   R1,1(,R2)                                                        
         AR    R2,R1                                                            
         J     HILOW10                                                          
                                                                                
HILOW20  OC    GLBRLMOS,GLBRLMOS   There may be X'55' PBKELQ                    
         BZ    HILOW22             Never set                                    
         CLC   GLBRLMOS,BUKMOS     Is this lower then lowest                    
         BNH   *+10                No                                           
HILOW22  MVC   GLBRLMOS,BUKMOS     Yes so set                                   
                                                                                
HILOW30  CLI   0(R2),EOR           End of record                                
         JE    HILOWXIT            Done                                         
         CLI   0(R2),BUKELQ        X'45' bucket element                         
         JNE   HILOW40                                                          
         OC    BUKMOS,BUKMOS       Is this a dummy element                      
         JZ    HILOW40             Yes, so ignore                               
         CLC   GLBRHMOS,BUKMOS                                                  
         BNL   HILOW40                                                          
         MVC   GLBRHMOS,BUKMOS                                                  
                                                                                
HILOW40  LLC   R1,1(,R2)                                                        
         AR    R2,R1                                                            
         J     HILOW30                                                          
                                                                                
KEY      USING GLBRECD,IOKEY                                                    
HILOWXIT MVC   KEY.GLBKLMOS,GLBRLMOS   Set in key too                           
         MVC   KEY.GLBKHMOS,GLBRHMOS                                            
         NI    KEY.GLBKSTAT,TURNOFF-GLBKXMIT                                    
         J     EXIT                                                             
         DROP  R2,R4,KEY                                                        
         EJECT ,                                                                
***********************************************************************         
* Set high and low mos in record and key                                        
***********************************************************************         
         USING CXIELD,R2                                                        
         USING GLBRECD,R4                                                       
CXISET   NTR1                                                                   
         GOTOR DATCON,PARM,(5,0),(25,WORK)                                      
         L     R4,AIO1                                                          
         LA    R2,GLBRFST          First element                                
CXISET10 CLI   0(R2),EOR           End of record                                
         BE    CXISET20                                                         
         CLI   0(R2),CXIELQ        X'25' changed/transmited                     
         BE    CXISET30            Find first one                               
         LLC   R1,1(,R2)                                                        
         AR    R2,R1                                                            
         J     CXISET10                                                         
                                                                                
CXISET20 LA    R2,LELEM_25         Build new CXIEL element                      
         XC    LELEM_25,LELEM_25                                                
         MVI   CXIEL,CXIELQ        X'25'                                        
         MVI   CXILN,CXILNQ+L'CXIOMOA                                           
         GOTOR HELLO,PARM,(C'P',ACCMST),(R4),LELEM_25,0                         
         ICM   R2,15,16(R1)        A(Where added)                               
         CLI   12(R1),0            Any errors ?                                 
         BE    *+6                                                              
         DC    H'00'                                                            
                                                                                
CXISET30 MVC   CXICDATE(6),WORK    Date and time binary                         
         MVC   CXIOMOA,OLDEST                                                   
         J     EXIT                                                             
         DROP  R2,R4                                                            
*&&                                                                             
         EJECT ,                                                                
***********************************************************************         
* Get account record                                                            
*       RF = Length of current level                                            
*       R4 is return pointing to AIO1                                           
*       Condition code is set for error or not                                  
***********************************************************************         
         USING ACTRECD,R4                                                       
GETACC   ST    RE,SVRE                                                          
         LA    R4,IOKEY                                                         
         MVC   IOKEY,SPACES                                                     
         AHI   RF,2                Add only 2 for company/unit/ledger           
         EXMVC RF,ACTKCULA,TRNCULA                                              
         LA    R1,IOREAD+IORNFIL                                                
         TM    PRGIND,PRGNEWF      Records are new file?                        
         BO    *+8                 Yes                                          
         LA    R1,IOREAD           No                                           
         GOTOR IO                                                               
         JNE   *+8                                                              
         MVI   ERROR#,ERRNONE                                                   
         L     R4,AIO1                                                          
                                                                                
         CLI   ERROR#,ERRNONE      Set CC for error or not                      
         L     RE,SVRE                                                          
         BR    RE                                                               
         DROP  R4                                                               
         EJECT ,                                                                
***********************************************************************         
* Special case where account is being added via ADDTRNS                         
***********************************************************************         
         USING TRNBLKD,RE                                                       
CHKPEND  ST    RE,SVRE                                                          
         ICM   RE,15,@ATRNBLK                                                   
         JZ    CHKPENDX            Can't check                                  
         TM    TRNACCI,TRNIOAP     Account to be added?                         
         JZ    CHKPENDX            No, so error                                 
         MVI   ERROR#,ERRNONE      Yes, so skip level                           
         DROP  RE                                                               
                                                                                
CHKPENDX CLI   ERROR#,ERRNONE                                                   
         L     RE,SVRE                                                          
         BR    RE                                                               
         EJECT ,                                                                
***********************************************************************         
* Get media name                                                                
***********************************************************************         
         USING MEDD,R4                                                          
GETMEDIA ST    RE,SVRE                                                          
                                                                                
         SAM31                                                                  
         L     R4,@MEDIAS          Media code table                             
         SR    R1,R1                                                            
         ICM   R1,1,#OFMEDS        # of media codes in table                    
         JZ    GETMED20                                                         
GETMED10 CLC   GLMEDIAC,MEDCODE    Match on media code                          
         JNE   GETMED12            Found it                                     
         MVC   GLMEDIAN,MEDNAME    Move in media name                           
         MVI   ERROR#,ERRNONE                                                   
         J     GETMED90                                                         
                                                                                
GETMED12 LA    R4,MEDLNQ(,R4)      Next entry                                   
         BRCT  R1,GETMED10                                                      
         DROP  R4                                                               
                                                                                
         USING PMDRECD,R4                                                       
GETMED20 SAM24                                                                  
         LA    R4,IOKEY                                                         
         MVC   IOKEY,SPACES                                                     
         MVI   PMDKTYP,PMDKTYPQ    X'09' Production media record                
         MVC   PMDKCPY,TRNCPY                                                   
         MVC   PMDKMED,GLMEDIAC    Move in media code                           
         MVI   ERROR#,ERRNOMED     No media record                              
         LA    R1,IOREAD+IORNFIL                                                
         TM    PRGIND,PRGNEWF      Records are new file?                        
         BO    *+8                 Yes                                          
         LA    R1,IOREAD           No                                           
         GOTOR IO                                                               
         JNE   GETMED90                                                         
                                                                                
         L     R4,AIO1                                                          
         AH    R4,DATADISP                                                      
GETMED60 CLI   0(R4),EOR           End of record                                
         JE    GETMED90                                                         
         CLI   0(R4),PMDELQ        X'11' Media element                          
         JE    GETMED70                                                         
         LLC   RF,1(,R4)                                                        
         AR    R4,RF                                                            
         J     GETMED60                                                         
                                                                                
         USING PMDELD,R4                                                        
GETMED70 MVC   GLMEDIAN,PMDSPACE   PMDSPACE + PMDDESC                           
         DROP  R4                                                               
                                                                                
         USING MEDD,R4                                                          
         SAM31                     Switch to 31 bit mode                        
         GOTOR PROTOFF                                                          
         SR    R4,R4                                                            
         ICM   R4,1,#OFMEDS        Get current number                           
         AHI   R4,1                                                             
         CHI   R4,#MAXMEDS         Don't exceed max                             
         JH    GETMED90            Can't add                                    
         STC   R4,#OFMEDS                                                       
         BCTR  R4,0                                                             
         MHI   R4,MEDLNQ                                                        
         A     R4,@MEDIAS          Base of table                                
         MVC   MEDCODE,GLMEDIAC                                                 
         MVC   MEDNAME,GLMEDIAN                                                 
         GOTOR PROTON                                                           
         MVI   ERROR#,ERRNONE                                                   
         DROP  R4                                                               
                                                                                
GETMED90 SAM24                     Switch to 24 bit mode                        
         L     RE,SVRE                                                          
         CLI   ERROR#,ERRNONE                                                   
         BR    RE                                                               
         EJECT ,                                                                
***********************************************************************         
* Get status element                                                            
*     Set GLACTOFC - If prod set based on client record                         
*                    else set based on lowest level record                      
*     Set GLACTFLT - Set based on account level hierarchy (non-prod)            
*                    Only used if offpos is filter based                        
***********************************************************************         
         USING LGRD,R5                                                          
GETRST   ST    RE,SVRE                                                          
         LR    R2,R4                                                            
         AH    R2,DATADISP                                                      
GETRST10 CLI   0(R2),EOR           End of record ?                              
         JE    GETRST90                                                         
         CLI   0(R2),RSTELQ        X'30' Record status                          
         JE    GETRST20                                                         
         LLC   RF,1(,R2)                                                        
         AR    R2,RF                                                            
         J     GETRST10                                                         
                                                                                
         USING RSTELD,R2                                                        
GETRST20 TM    LDGIND,LDGPROD      Is it production                             
         BO    GETRST22                                                         
         TM    GENIND,GENLOWLV     Lowest level                                 
         BZ    GETRST24            No                                           
                                                                                
GETRST22 CLI   RSTLN,RSTLN2Q                                                    
         JL    GETRST24                                                         
         MVC   GLACTOFC,RSTOFFC                                                 
         MVI   ISACTOFF,ISACTCLI   Client office from RSTOFFC                   
         TM    LDGIND,LDGPROD      Is it production                             
         BO    GETRST24                                                         
         MVI   ISACTOFF,ISACTLOW   Lowest level office from RSTOFFC             
                                                                                
GETRST24 TM    LGRIND,LGRFOFF      Filter value is office                       
         BZ    GETRST90                                                         
         CLI   RSTLN,RSTLN2Q                                                    
         BNL   GETRST26                                                         
         CLI   LGRF#DSP,RSTFILT5-RSTELD                                         
         BNE   GETRST90            No filter 5 to look at                       
                                                                                
GETRST26 CLI   GLACTFLT,C' '       Was it set ?                                 
         BNH   GETRST90            Yes, so keep as is                           
         LLC   RE,LGRF#DSP                                                      
         AR    RE,R4               Point to filter value                        
         MVC   GLACTFLT,0(RE)      Save off value                               
         MVC   GLFLTLV#,ISRULE     Level filter was set at                      
                                                                                
GETRST90 L     RE,SVRE                                                          
         BR    RE                                                               
         DROP  R2,R5                                                            
         EJECT ,                                                                
***********************************************************************         
* Get production profile data to set client office                              
*     Set PPROFFC for client / product or job record                            
*     This we use to set  GLCLIOFC / GLPRDOFC or GLJOBOFC respectively          
***********************************************************************         
         USING ACTRECD,R2                                                       
GETPPR   ST    RE,SVRE                                                          
         MVC   PPROFFC,SPACES      Reset                                        
         LR    R2,R4                                                            
         AH    R2,DATADISP                                                      
GETPPR10 CLI   0(R2),EOR           End of record?                               
         BE    GETPPR90                                                         
         CLI   0(R2),PPRELQ        X'24' Production profile                     
         BE    GETPPR20                                                         
         LLC   RF,1(,R2)                                                        
         AR    R2,RF                                                            
         B     GETPPR10                                                         
                                                                                
         USING PPRELD,R2                                                        
GETPPR20 CLC   PPRGAOFF,SPACES       Any value ?                                
         BNH   *+10                  No                                         
*&&US*&& MVC   PPROFFC,PPRGAOFF      Save off client GL office                  
*&&UK*&& MVC   PPROFFC(1),PPRUFORA   Save off client GL office                  
                                                                                
GETPPR90 L     RE,SVRE                                                          
         BR    RE                                                               
         DROP  R2                                                               
         EJECT ,                                                                
***********************************************************************         
* Add names to table to be used later when making postings                      
***********************************************************************         
         USING ACNAMED,R4                                                       
ADDNAME  NTR1                                                                   
         LR    R6,R1                                                            
         TM    GLIND2,GLINMFUL     Table full                                   
         BO    ADDNMEX                                                          
         LA    R4,ACNBUFF                                                       
         XC    ACNBUFF,ACNBUFF                                                  
         MVC   ACNCODE,1(R6)                                                    
         TM    LDGIND,LDGPROD      Production ?                                 
         BZ    ADDNM10             The get the client/product only              
         SR    RF,RF                                                            
         ICM   RF,1,GLCPRLEN                                                    
         BNZ   *+6                                                              
         DC    H'00'                                                            
                                                                                
         AHI   RF,2                Add 2 for unit ledger                        
         MVC   ACNCODE,SPACES      Reset                                        
         BCTR  RF,0                                                             
         EXMVC RF,ACNCODE,1(R6)                                                 
                                                                                
ADDNM10  SAM31                                                                  
         GOTOR PROTOFF                                                          
         SR    RF,RF                                                            
         ICM   RF,3,#OFCODES                                                    
         SR    R7,R7                                                            
         ICM   R7,3,MAX#CDES                                                    
         GOTOR BIN31,PARM,ACNBUFF,ACODES,(RF),(1,ACNLNQ),              +        
               (0,L'ACNCODE),(R7)                                               
         GOTOR PROTON                                                           
         MVC   #OFCODES,10(R1)     # of entries                                 
         ICM   R4,15,0(R1)                                                      
         BNZ   ADDNM11                                                          
         OI    GLIND2,GLINMFUL                                                  
         TM    GLIND1,GLIOFF       Off-line                                     
         BO    ADDNMEX             Don't die if off-line                        
         DC    H'00'               Table full                                   
                                                                                
ADDNM11  TM    0(R1),X'80'         Record just added?                           
         BZ    ADDNMEX             No, aready in table                          
                                                                                
         SR    RF,RF                                                            
         ICM   RF,3,MAX#CDES                                                    
         AHI   RF,1                                                             
         STH   RF,MAX#CDES         Increase by one                              
         MHI   RF,ACNLNQ           See if we collide                            
                                                                                
         A     RF,ACODES           A(Begining of table)                         
         L     RE,NAMEPTR                                                       
         SHI   RE,36               Max entry length                             
                                                                                
         CR    RF,RE               Will we collide?                             
         BL    ADDNM11B            No                                           
         OI    GLIND2,GLINMFUL                                                  
         TM    GLIND1,GLIOFF       Off-line                                     
         BO    ADDNMEX             Don't die if off-line                        
         DC    H'00'               Table full                                   
                                                                                
ADDNM11B TM    LDGIND,LDGPROD      Production ?                                 
         BZ    ADDNM12             Then get the client/product only             
         ICM   R2,15,@ACTLVB       Is there a level B ?                         
         BZ    ADDNM16             Need to retrieve                             
         B     ADDNM20             Get from record                              
                                                                                
ADDNM12  ICM   R2,15,@ACTLVD       Is there a level D ?                         
         BNZ   ADDNM20                                                          
         ICM   R2,15,@ACTLVC       Is there a level C ?                         
         BNZ   ADDNM20                                                          
         ICM   R2,15,@ACTLVB       Is there a level B ?                         
         BNZ   ADDNM20                                                          
         ICM   R2,15,@ACTLVA       Is there a level A ?                         
         BNZ   ADDNM20                                                          
                                                                                
         USING TRNBLKD,RE                                                       
ADDNM16  CLI   ACNCODE,C'G'        Don't chekc if Unit G                        
         BE    ADDNM18             Continue                                     
         ICM   RE,15,@ATRNBLK                                                   
         BZ    ADDNM18             Can't check                                  
         TM    TRNACCI,TRNIOAP     Account to be added?                         
         BZ    ADDNM18             No, so go read                               
                                                                                
         USING ACTRECD,R2                                                       
         ICM   R2,15,TRNACC        Name was added from WK file                  
         LA    R3,ACTRFST          This will be new file style                  
         B     ADDNM22             Add name that came from posting file         
         DROP  RE                                                               
                                                                                
ADDNM18  MVC   IOKEY,SPACES                                                     
         MVC   IOKEY(1),0(R6)                                                   
         SAM31                                                                  
         GOTOR PROTOFF                                                          
         MVC   IOKEY+1(L'TRNKULA),ACNCODE                                       
         GOTOR PROTON                                                           
         SAM24                                                                  
         LA    R1,IOREAD+IORNFIL                                                
         TM    PRGIND,PRGNEWF      Records are new file?                        
         BO    *+8                 Yes                                          
         LA    R1,IOREAD                                                        
         GOTOR IO                                                               
         BE    *+6                                                              
         DC    H'00'               Just for now                                 
                                                                                
         L     R2,AIO1                                                          
ADDNM20  LTR   R3,R2                                                            
         BNZ   *+6                                                              
         DC    H'00'               Just for now                                 
                                                                                
         AH    R3,DATADISP                                                      
ADDNM22  CLI   0(R3),EOR           End of record                                
         BNE   *+6                                                              
         DC    H'00'               Just for now                                 
         CLI   0(R3),NAMELQ        X'20' name element                           
         BE    ADDNM24                                                          
         LLC   RF,1(,R3)                                                        
         AR    R3,RF                                                            
         B     ADDNM22                                                          
                                                                                
         USING NAMELD,R3                                                        
ADDNM24  SAM31                                                                  
         GOTOR PROTOFF                                                          
         LLC   RF,1(,R3)                                                        
         SHI   RF,NAMLN1Q                                                       
         STC   RF,ACNLEN           Save length of name                          
         L     R5,NAMEPTR          Name pointer                                 
         SR    R5,RF               Bump back in table                           
         ST    R5,NAMEPTR          Reset to new address                         
         BCTR  RF,0                One for EX instr.                            
                                                                                
ADDNME30 MVC   0(0,R5),NAMEREC                                                  
         EX    RF,*-6                                                           
         S     R5,ACODES           Displacement from begining of table          
         STCM  R5,7,ACNDISP        Displacement to name                         
         GOTOR PROTON                                                           
         SAM24                                                                  
                                                                                
ADDNMEX  J     EXIT                                                             
         DROP  R2,R3,R4                                                         
         EJECT ,                                                                
***********************************************************************         
* Get names                                                                     
***********************************************************************         
         USING ACNAMED,R4                                                       
GETNAME  NTR1                                                                   
         LR    R5,R1               Save R1                                      
         L     R2,0(,R1)           A(Code to read for)                          
         L     R3,4(,R1)           A(Location to return name)                   
         LA    R4,ACNBUFF                                                       
         MVC   ACNCODE,0(R2)                                                    
         MVC   0(L'NAMEREC,R3),SPACES                                           
                                                                                
         SR    R6,R6                                                            
         ICM   R6,3,#OFCODES                                                    
         BZ    GETNM20             Nothing in buffer                            
         SR    R7,R7                                                            
         ICM   R7,3,MAX#CDES                                                    
                                                                                
         SAM31                                                                  
         GOTOR PROTOFF                                                          
         GOTOR BIN31,PARM,ACNBUFF,ACODES,(R6),(0,ACNLNQ),              +        
               (0,L'ACNCODE),(R7)                                               
         TM    0(R1),X'80'         Record not found?                            
         BO    GETNM20             No, so see if we can read it                 
         ICM   R4,15,0(R1)         A(Found record)                              
         XC    0(4,R5),0(R5)                                                    
         SR    RF,RF                                                            
         ICM   RF,1,ACNLEN                                                      
         BZ    GETNM18             No length                                    
         ST    RF,0(,R5)           R5 = P1(1)                                   
         BCTR  RF,0                                                             
         SR    R2,R2                                                            
         ICM   R2,7,ACNDISP        Get to name                                  
         A     R2,ACODES                                                        
         EXMVC RF,0(R3),0(R2)      R3 = P2(location)                            
                                                                                
GETNM18  GOTOR PROTON                                                           
         B     GETNMEX                                                          
                                                                                
*---------------------------------------------------------------------*         
*  Get name by tring to read for the account                                    
*---------------------------------------------------------------------*         
GETNM20  SAM24                                                                  
         GOTOR PROTON                                                           
         TM    GLIND2,GLINMFUL     No room left in table                        
         BZ    GETNMEX             There was still room, so don't try           
         TM    LDGIND,LDGPROD      Production ?                                 
         BZ    GETNM22             Then get the client/product only             
         ICM   R2,15,@ACTLVB       Is there a level B ?                         
         BZ    GETNM26             Need to retrieve                             
         B     GETNM30             Get from record                              
                                                                                
GETNM22  ICM   R2,15,@ACTLVD       Is there a level D ?                         
         BNZ   GETNM30                                                          
         ICM   R2,15,@ACTLVC       Is there a level C ?                         
         BNZ   GETNM30                                                          
         ICM   R2,15,@ACTLVB       Is there a level B ?                         
         BNZ   GETNM30                                                          
         ICM   R2,15,@ACTLVA       Is there a level A ?                         
         BNZ   GETNM30                                                          
                                                                                
GETNM26  MVC   IOKEY,SPACES                                                     
         MVC   IOKEY(1),GLLSTCPY                                                
         MVC   IOKEY+1(L'TRNKULA),ACNCODE                                       
         LA    R1,IOREAD+IORNFIL                                                
         TM    PRGIND,PRGNEWF      Records are new file?                        
         BO    *+8                 Yes                                          
         LA    R1,IOREAD                                                        
         GOTOR IO                                                               
         BE    *+6                                                              
         DC    H'00'               Just for now                                 
                                                                                
         USING ACTRECD,R2                                                       
         L     R2,AIO1                                                          
GETNM30  LTR   RE,R2                                                            
         BNZ   *+6                                                              
         DC    H'00'               Just for now                                 
                                                                                
         AH    RE,DATADISP                                                      
GETNM32  CLI   0(RE),EOR           End of record                                
         BNE   *+6                                                              
         DC    H'00'               Just for now                                 
         CLI   0(RE),NAMELQ        X'20' name element                           
         BE    GETNM34                                                          
         LLC   RF,1(,RE)                                                        
         AR    RE,RF                                                            
         B     GETNM32                                                          
                                                                                
         USING NAMELD,R3                                                        
GETNM34  LLC   RF,1(,RE)                                                        
         SHI   RF,NAMLN1Q                                                       
         BCTR  RF,0                                                             
         EXMVC RF,0(R3),0(RE)                                                   
                                                                                
GETNMEX  SAM24                                                                  
         J     EXIT                                                             
                                                                                
***********************************************************************         
* Set GLTOACC based on ledger rules                                             
*    Set condition code to not equal if none found                              
***********************************************************************         
         USING LGRD,R5                                                          
         USING PSTD,R6                                                          
LGRGLP   ST    RE,SVRE                                                          
         MVI   ERROR#,ERRNORUL     Missing default rules                        
         ICM   R6,15,LGRRULES      A(Rules)                                     
         JZ    LGRGLXIT            None                                         
         SR    R0,R0                                                            
         ICM   R0,1,LGR#GLRL       Any entries ?                                
         JZ    LGRGLXIT            No                                           
                                                                                
LGRGLP10 CLC   PSTFROM,SPACES      Default setting                              
         JE    LGRGLP80                                                         
         LLC   RF,PSTFRLN          No                                           
         LA    RE,FROMACC+3                                                     
         TM    LDGIND,LDGPROD      Production ?                                 
         BZ    LGRGLP20            No                                           
         SR    RF,RF               Post by media                                
         LA    RE,GLMEDIAC                                                      
         TM    LGRIND,LGROFPRD     Posting prod by office?                      
         JZ    LGRGLP20            No                                           
         LA    RF,L'GLCLIOFC-1     Post by office                               
*&&UK*&& LA    RE,GLJOBOFC         Office at job     level                      
*&&UK*&& CLC   GLJOBOFC,SPACES                                                  
*&&UK*&& BH    LGRGLP20                                                         
         LA    RE,GLPRDOFC         Office at product level                      
         CLC   GLPRDOFC,SPACES                                                  
         BH    LGRGLP20                                                         
         LA    RE,GLCLIOFC         Office at client  level                      
                                                                                
LGRGLP20 EXCLC RF,PSTFROM,0(RE)                                                 
         BE    LGRGLP80                                                         
         AHI   R6,PSTLNQ           Next entry                                   
         BCT   R0,LGRGLP10                                                      
         B     LGRGLXIT                                                         
                                                                                
LGRGLP80 MVI   ERROR#,ERRNONE                                                   
         MVC   GLTOACC,PSTTO       Set TO account                               
         BRAS  RE,IS_STAR          Is there an "*" in the account               
         MVI   GLTORULE,GLTOLDGR                                                
         DROP  R6                                                               
                                                                                
LGRGLXIT L     RE,SVRE                                                          
         CLI   ERROR#,ERRNONE                                                   
         BR    RE                                                               
         DROP  R5                                                               
         EJECT ,                                                                
***********************************************************************         
* Get posting element if any                                                    
*    Set condition code to not equal if none found                              
***********************************************************************         
         USING LGRD,R5                                                          
ACCGLP   ST    RE,SVRE                                                          
         CLI   GLTORULE,GLTOACCA   Set already for a level                      
         BNLR  RE                  Yes                                          
                                                                                
         LR    R2,R4                                                            
         AH    R2,DATADISP                                                      
ACCGLP10 CLI   0(R2),EOR           End of record                                
         JE    ACCGLP80                                                         
         CLI   0(R2),GLPELQ        X'15' posting element                        
         JE    ACCGLP20                                                         
         LLC   RF,1(,R2)                                                        
         AR    R2,RF                                                            
         J     ACCGLP10                                                         
                                                                                
         USING GLPELD,R2                                                        
ACCGLP20 MVC   GLTOACC(10),GLPACC1 Set TO account                               
         CLI   GLPLN,26                                                         
         BL    *+10                                                             
         MVC   GLTOACC,GLPACC1     Set for full length                          
         BRAS  RE,IS_STAR          Is there an "*" in the account               
         MVC   GLTORULE,ISRULE     Set level of GLTORULE                        
         CLI   LGRPROF,C'P'        Account level is contra                      
         BNE   *+10                                                             
         MVC   GLSVFROM,0(R4)      Save off account level                       
         DROP  R2                                                               
                                                                                
ACCGLP80 TM    LGRIND,LGRFOFF      Need office filter value ?                   
         BZ    ACCGLP82            No, see if GLTORULE set                      
         CLI   GLACTFLT,C' '       Was it set ?                                 
         BNH   ACCGLPNO            No                                           
                                                                                
ACCGLP82 CLI   GLTORULE,GLTOACCA   Was this set ?                               
         BL    ACCGLPNO            No, so not okay yet                          
                                                                                
ACCGLPOK SR    RE,RE                                                            
ACCGLPNO LTR   RE,RE                                                            
         L     RE,SVRE                                                          
         BR    RE                                                               
         DROP  R5                                                               
         EJECT ,                                                                
***********************************************************************         
* Find c'*' in GLTOACC                                                          
***********************************************************************         
IS_STAR  XC    GLSTAR,GLSTAR       Clear                                        
         TM    PRGIND,PRGNEWOF     Two character offices                        
         BOR   RE                  Yes, so not applicable                       
                                                                                
         LA    R0,L'GLTOACC-1         # of characters to scan for               
         LA    RF,GLTOACC+L'GLTOACC-1 Point to end of account                   
IS_STAR2 CLI   0(RF),C'*'             Find star                                 
         JE    IS_STAR4                                                         
         BCTR  RF,0                                                             
         BRCT  R0,IS_STAR2                                                      
         BR    RE                                                               
                                                                                
IS_STAR4 ST    RF,GLSTAR           Save A(location of c'*' in GLTOACC)          
         BR    RE                                                               
         EJECT ,                                                                
***********************************************************************         
* Replace office in TRAOFFC with override                                       
***********************************************************************         
         USING GLRELD,R4                                                        
         USING LGRD,R5                                                          
SETRULE  ST    RE,SVRE                                                          
         MVC   POSTNEW,SPACES                                                   
         TM    LDGIND,LDGOFFRL     Apply office rules                           
         JZ    SETRULEX                                                         
         ICM   R4,15,LGRELE6       Ledger office rules?                         
         JNZ   *+8                 Yes                                          
         LA    R4,GLCPYE6          Load company GLRELD x'E6'                    
                                                                                
         SR    R0,R0                                                            
         ICM   R0,1,GLRPRS         # of pairs                                   
         JZ    SETRULEX            None                                         
         LA    RE,GLROFFP          Start of pairs                               
SETRUL10 CLC   TRAOFFC,0(RE)                                                    
         JE    SETRUL20                                                         
         AHI   RE,L'GLROFFP        Next pair                                    
         BRCT  R0,SETRUL10                                                      
         J     SETRULEX            No match                                     
                                                                                
SETRUL20 MVC   POSTNEW,2(RE)       Replace with this office                     
                                                                                
SETRULEX L     RE,SVRE                                                          
         BR    RE                                                               
         DROP  R4,R5               GLRELD, LGRD                                 
         EJECT ,                                                                
***********************************************************************         
*  DATAMGR INTERFACE                                                            
***********************************************************************         
* UPDATE only after a READ for update                                           
***********************************************************************         
         SPACE 1                                                                
UPDATE   TM    IOCTRL,IORUPD       Update in play ?                             
         JO    *+6                 Yes                                          
         DC    H'00'               Must be on, coding error                     
                                                                                
         TM    IOTYPE,IOPUT        Update record                                
         JZ    UPDATE10            No                                           
         MVC   COMMAND,PUTREC      No, just put record                          
         J     UPDATE20                                                         
                                                                                
UPDATE10 MVC   COMMAND,ADDREC      Add a new record                             
         TM    IOTYPE,IOADD        Must be set                                  
         JO    *+6                 Yes                                          
         DC    H'00'               Updating but not type set                    
                                                                                
UPDATE20 MVI   DMIND,DMUPD         Read for update (Not sure need this)         
         TM    IOCTRL,IORNFIL      Read new file                                
         JO    *+6                                                              
         DC    H'00'               ACCFILE not supported when writing           
         CLC   IOSVKEY(L'ACTKEY),IOKEY     Right key updating ?                 
         JE    *+6                 Yes                                          
         DC    H'00'               No, broke sequence                           
                                                                                
         TM    IOTYPE,IOPUT        Update record                                
         JZ    DMCALL              No need to check directory change            
         CLC   IOSVKEY+L'ACTKEY(L'ACTKSTA),IOKEY+L'ACTKEY                       
         JE    DMCALL                                                           
         OI    IOTYPE,IODIR        Directory change detected                    
         MVC   COMMAND,DMWRT       Yes, set to write for dir change             
         J     DMCALL              Yes                                          
                                                                                
***********************************************************************         
* READ, READHI and READSEQ                                                      
***********************************************************************         
IO       STC   R1,IOCTRL           Set IOCTRL                                   
         MVI   IOTYPE,0            Initialize                                   
         MVI   DMIND,0                                                          
         XC    COMMAND,COMMAND     This will die if not set                     
         MVC   FILE,ACCDIR                                                      
         TM    IOCTRL,IORNFIL      Read new file                                
         JO    *+10                Yes                                          
         MVC   FILE,ACCFILE        No emulated                                  
                                                                                
         TM    IOCTRL,IOREAD       Read                                         
         JZ    *+10                                                             
         MVC   COMMAND,DMREAD                                                   
         TM    IOCTRL,IORDSEQ      Read sequencial                              
         JZ    *+10                                                             
         MVC   COMMAND,DMRSEQ                                                   
         TM    IOCTRL,IORDHI       Read High                                    
         JZ    *+10                                                             
         MVC   COMMAND,DMRDHI                                                   
*                                                                               
         TM    IOCTRL,IORUPD       Updating ?                                   
         JZ    DMCALL              No                                           
         MVI   DMIND,DMUPD         Read for update                              
         TM    IOCTRL,IORDEL       Read for deleted                             
         JZ    *+8                                                              
         OI    DMIND,DMDEL         Read for update & deleted records            
         TM    IOCTRL,IORNFIL      New file                                     
         JO    DMCALL                                                           
         DC    H'00'               ACCFILE not supported when writing           
*                                                                               
DMCALL   NTR1                                                                   
         MVC   IOSVKEY,IOKEY                                                    
         CLC   COMMAND,ADDREC      Adding a new record                          
         JE    DMCALL40            No need to process directory                 
         CLC   COMMAND,PUTREC      Just putting a record                        
         JE    DMCALL40            Yes, no need to process directory            
         CLI   GLWRITE,YES         Allow updates to file                        
         BE    DMCALL06            Yes                                          
         MVI   IOERR,0                                                          
         CLC   COMMAND,DMWRT       No                                           
         BE    DMCALL08                                                         
                                                                                
DMCALL06 LA    R2,IOKEY                                                         
         CLC   FILE,ACCFILE        New file or emulated                         
         BNE   DMCALL07                                                         
         L     R2,AIO1                                                          
         TM    IOCTRL,IORUPD       In update mode ?                             
         BZ    *+6                                                              
         DC    H'00'               Programming error                            
                                                                                
DMCALL07 GOTOR DATAMGR,PARM,(DMIND,COMMAND),FILE,IOSVKEY,(R2),IOWRK             
         MVC   IOERR,PARM+8                                                     
                                                                                
DMCALL08 TM    IOERR,IOBAD         Disk error ?                                 
         JZ    *+6                 No                                           
         DC    H'00'               Irrecoverable disk error                     
                                                                                
         TM    IOCTRL,IORUPD       In update mode ?                             
         JO    DMCALL10            Yes                                          
         TM    IOERR,IORNF         No, read mode only. Record not found         
         JO    DMCALLX             True, so exit, set CC                        
         CLI   IOERR,0                                                          
         JE    DMCALL40            Get record                                   
         DC    H'00'               What happened                                
                                                                                
***********************************************************************         
* Check errors for directory write                                    *         
***********************************************************************         
DMCALL10 CLI   IOTYPE,0            Is this set                                  
         JE    DMCALL12            Yes, must be a write directory               
         CLI   IOERR,0                                                          
         JE    DMCALL40            Wrote directory okay                         
         DC    H'00'                                                            
                                                                                
***********************************************************************         
* Check errors for read for update                                    *         
***********************************************************************         
DMCALL12 CLI   IOERR,0             Must be a read for update, set CC            
         JNE   DMCALL14            Okay then, get record                        
         OI    IOTYPE,IOPUT        Record found so putrec needed                
         J     DMCALL40            Get record                                   
                                                                                
DMCALL14 TM    IOCTRL,IORDEL       Read delete records?                         
         JZ    DMCALL16                                                         
         TM    IOERR,IODEL         Was record deleted                           
         JZ    DMCALL16            No delete, check next                        
         OI    IOTYPE,IOPUT        Record found so putrec needed                
         J     DMCALL40            Get record                                   
                                                                                
DMCALL16 TM    IOERR,IORNF         Record not found                             
         JO    *+6                                                              
         DC    H'00'               What's the problem                           
         OI    IOTYPE,IOADD        Need to add record if not found              
         MVC   IOKEY,IOSVKEY       Restore key                                  
         J     DMCALLX                                                          
                                                                                
***********************************************************************         
* Process Getrec/Putrec/Addrec                                        *         
***********************************************************************         
DMCALL40 TM    IOCTRL,IORDIR       Read directory only ?                        
         BO    DMCALLX             Yes                                          
         CLC   FILE,ACCFILE        New file or emulated                         
         JE    DMCALLX             If emulated then done                        
         XC    IODA,IODA                                                        
         LA    RE,GETREC           Default                                      
         CLC   COMMAND,DMWRT                                                    
         JNE   *+8                                                              
         LA    RE,PUTREC           Switch it to PUTREC now                      
         CLC   COMMAND,PUTREC      Put record                                   
         BE    DMCALL42            Yes                                          
         CLC   COMMAND,ADDREC      Add record                                   
         JE    DMCALL44            Commad okay as is                            
         MVC   COMMAND,0(RE)       Set command as get or put                    
                                                                                
DMCALL42 MVC   IODA,IOKEY+(ACTKDA-ACTKEY)                                       
DMCALL44 CLI   GLWRITE,YES         Allow updates to file                        
         BE    DMCALL46            Yes                                          
         MVI   IOERR,0             Reset                                        
         CLC   COMMAND,GETREC                                                   
         BNE   DMCALLX                                                          
                                                                                
DMCALL46 GOTOR DATAMGR,PARM,(DMIND,COMMAND),ACCMST,IODA,AIO1,IOWRK              
         MVC   IOERR,PARM+8                                                     
         TM    IOERR,IOBAD                                                      
         JZ    *+6                                                              
         DC    H'00'               Irrecoverable disk error                     
                                                                                
         CLC   COMMAND,GETREC                                                   
         JNE   DMCALL50                                                         
         CLI   IOERR,0                                                          
         JE    DMCALLX                                                          
         TM    IOCTRL,IORDEL       Deleted record                               
         JO    *+6                                                              
         DC    H'00'               This should have been okay                   
         TM    IOERR,IODEL         Yes, deleted record                          
         JO    DMCALLX                                                          
         DC    H'00'                                                            
                                                                                
DMCALL50 CLI   IOERR,0                                                          
         JE    DMCALLX                                                          
         DC    H'00'               Might be duplicate key on add                
                                                                                
DMCALLX  CLI   IOERR,0             Set cc code                                  
         J     EXIT                                                             
         EJECT ,                                                                
***********************************************************************         
* Do this because not compliant with RSECT                                      
* We set LGL2POST locally off-line because GL2POST GETMAIN can be               
* cleared by off-line program which means we would do excessive amounts         
* of GETMAINS and run out of region                                             
***********************************************************************         
ACGLPOSC CSECT                                                                  
GETOBUF  NTR1  BASE=*,LABEL=*                                                   
         ICM   R1,15,LGL2POST      A(Locally saved static address)              
         JNZ   GETOBUF5            Re-adquire, RUNNER clears A(GL2POST)         
*                                  No, so get some storage                      
         L     R0,=A((((GL#KBUFS*2)+1)*K)+32)                                   
         GETMAIN RU,LV=(0),LOC=(ANY,ANY)                                        
         LTR   RF,RF                                                            
         JZ    *+6                                                              
         DC    H'00'                                                            
         ST    R1,LGL2POST                                                      
                                                                                
GETOBUF5 ST    R1,@MEDIAS          XA storage to build media code/names         
         AHI   R1,K                Add 1K                                       
         ST    R1,ACODES           XA storage to build code/names               
         LHI   RF,GL#KBUFS                                                      
         STC   RF,GL1#OFK          250K buffer for names                        
         STC   RF,GL2#OFK          250K buffer for postings                     
         SLL   RF,10               Multiply by 1024                             
         AR    R1,RF               #k to build postings                         
         ST    R1,GL2POST                                                       
         XIT1                                                                   
                                                                                
*ADTRANS DC    A(0)                Off-line static saved A(ACADDTRN)            
LGL2POST DC    A(0)                Off-line static saved A(getmain)             
                                                                                
ACGLPOST RSECT                                                                  
**********************************************************************          
* Table of displacements for filter value (See LGRF#DSP)                        
**********************************************************************          
FILTS    DS    0H                                                               
         DC    C'1',AL1(RSTFILT1-RSTELD)                                        
FILTLNQ  EQU   *-FILTS                                                          
         DC    C'2',AL1(RSTFILT2-RSTELD)                                        
         DC    C'3',AL1(RSTFILT3-RSTELD)                                        
         DC    C'4',AL1(RSTFILT4-RSTELD)                                        
         DC    C'5',AL1(RSTFILT5-RSTELD)                                        
FILTSQ   EQU   (*-FILTS)/FILTLNQ                                                
                                                                                
ACCFILE  DC    CL8'ACCFILE'                                                     
ACCBIG   DC    CL8'ACCBIG'                                                      
ACCDIR   DC    CL8'ACCDIR'                                                      
ACCMST   DC    CL8'ACCMST'                                                      
GETREC   DC    CL8'GETREC'                                                      
ADDREC   DC    CL8'ADDREC'                                                      
PUTREC   DC    CL8'PUTREC'                                                      
DMWRT    DC    CL8'DMWRT'                                                       
DMREAD   DC    CL8'DMREAD'                                                      
DMRDHI   DC    CL8'DMRDHI'                                                      
DMRSEQ   DC    CL8'DMRSEQ'                                                      
                                                                                
         LTORG                                                                  
         EJECT ,                                                                
***********************************************************************         
* Table to divide # of 1024 blocks passed                                       
*     1st 2K goes to GLDBLKD, at goes to LEDGER table                           
*     Rest is divided up as below                                               
*              AL1(#K,@PROF#K,@OFRUL#K,@GLRUL#K)                                
***********************************************************************         
DEFTAB   DC    AL1(2,0,0,0)                                                     
         DC    AL1(3,1,0,0)                                                     
         DC    AL1(4,1,1,0)                                                     
         DC    AL1(5,1,1,1)                                                     
         DC    AL1(6,1,1,2)                                                     
         DC    AL1(7,1,1,3)                                                     
         DC    AL1(8,1,2,3)                                                     
         DC    AL1(9,2,2,3)                                                     
         DC    AL1(10,2,2,4)                                                    
         DC    AL1(11,2,2,5)                                                    
         DC    AL1(12,3,2,5)                                                    
         DC    AL1(13,3,3,5)                                                    
         DC    AL1(14,3,3,6)                                                    
         DC    AL1(15,4,3,6)                                                    
         DC    AL1(16,4,4,6)                                                    
DEFTAB#  EQU   (*-DEFTAB)/DEFTLNQ                                               
         EJECT                                                                  
WKSTAB   DC    AL4(IO1-GLWORKD,AIO1-GLWORKD),CL8'**AIO1**'                      
         DC    AL4(LGLRULES-GLWORKD,0),CL8'*GLRULE*'                            
         DC    AL4(0)                                                           
         EJECT                                                                  
***********************************************************************         
* General working storage area DSECT                                            
***********************************************************************         
GLWORKD  DSECT                                                                  
DATAMGR  DS    V                                                                
GETPROF  DS    V                                                                
DATCON   DS    V                                                                
HELLO    DS    V                                                                
CALLOV   DS    V                                                                
BIN31    DS    V                   Binsearch 31 bit mode                        
SWITCH   DS    V                   Switched (Used here to get TCB)              
PROTON   DS    V                   Proctection on                               
PROTOFF  DS    V                   Proctection off                              
MASTC    DS    V                   MASTD                                        
                                                                                
GLMIN#K  EQU   2                                                                
GLMAX#K  EQU   16                                                               
GLBLK#K  DS    0XL1                                                             
GLBLOCK  DS    A                   A(GLBLKD)                                    
                                                                                
SVRE     DS    A                   Save register RE                             
GLRELO   DS    A                                                                
AIO1     DS    A                   A(IO1)                                       
                                                                                
AELEM15  DS    A                   GLPELD element found                         
AELEM60  DS    A                   TRSELD element found on transaction          
AELEM63  DS    A                   GLDELD element found on transaction          
                                                                                
IODA     DS    AL4                 Disk address for GETREC                      
IOWRK    DS    XL64                                                             
IOKEY    DS    CL(ACCKLEN)                                                      
IOSVKEY  DS    CL(ACCKLEN)                                                      
*                                                                               
IOCTRL   DS    XL1                                                              
IORNFIL  EQU   X'80'               .  New file                                  
IORUPD   EQU   X'40'               .  Read for update                           
IORDEL   EQU   X'20'               .  Read for deleted                          
IORDIR   EQU   X'08'               .  Read directory only                       
IOREAD   EQU   X'04'               .  Read                                      
IORDSEQ  EQU   X'02'               .  Read sequencial                           
IORDHI   EQU   X'01'               .  Read high                                 
*                                                                               
IOTYPE   DS    XL1                 Type of updating                             
IOADD    EQU   X'40'               .  Add record                                
IOPUT    EQU   X'20'               .  Put record                                
IODIR    EQU   X'10'               .  Change directory, Write DIR               
*                                                                               
IOERR    DS    X                                                                
IOEOF    EQU   X'80'               End of file                                  
IOBAD    EQU   X'40'               Disk error                                   
IODUP    EQU   X'20'               Duplicate key on add                         
IORNF    EQU   X'10'               Record not found                             
IODEL    EQU   X'02'               Record is deleted                            
*                                                                               
COMMAND  DS    CL8                                                              
FILE     DS    CL8                                                              
SAVEKEY  DS    CL(ACCKLEN)                                                      
SVGLKEY  DS    CL(ACCKLEN)                                                      
GLTRNKEY DS    CL(ACCKLEN)                                                      
GLGLBKEY DS    CL(ACCKLEN)                                                      
                                                                                
ACNBUFF  DS    XL(ACNLNQ)                                                       
GLBFREC  DS    XL(GLRLNQ)                                                       
PRFKEY   DS    XL(PRFKLNQ)                                                      
                                                                                
SVACTION DS    X                                                                
SVGLREF  DS    CL6                                                              
SVGLDATE DS    XL3                                                              
SVGLMOA  DS    CL2                                                              
                                                                                
GLRIND   DS    X                   General ledger rules indicator               
GLROFF   EQU   X'80'               .  Post by office                            
GLROFFT  EQU   X'40'               .  Post by transaction office                
GLROFFNO EQU   X'20'               .  No office at all                          
                                                                                
BYOFF    DS    C                   Posting by office (Yes/No)                   
                                                                                
TRNCULA  DS    0CL15                                                            
TRNCPY   DS    X                   Current company processing                   
TRNULA   DS    0CL14                                                            
TRNUNT   DS    C                   Unit S only                                  
TRNLGR   DS    C                   Current tranaction ledger                    
TRNACT   DS    CL12                Current tranaction account                   
                                                                                
LDGINDX# DS    X                   Index of Ledger list array                   
LDGLIST  DS    CL20                Unit S ledgers                               
LDGLIST# DS    XL20                # of entries per ledger                      
                                                                                
LDGIND   DS    X                   Ledger  indicator                            
LDGOFFRL EQU   X'80'               .  Rules by office                           
LDGOFTRL EQU   X'40'               .  Rules by office for tranaction            
LDGPROD  EQU   X'08'               .  Production ledger                         
                                                                                
GENIND   DS    X                                                                
GENLOWLV EQU   X'80'               .  Processing lowest level                   
                                                                                
PENDING  DS    X                   Pending writes                               
PENDGLBR EQU   X'80'               .  Pending GLBRECD                           
PENDPOST EQU   X'40'               .  Post record, table almost full            
                                                                                
OLDEST   DS    XL2                 Oldest MOA for GLBKEY                        
                                                                                
ISRULE   DS    C                                                                
ISOFFC   DS    C                                                                
ISOFFCPY EQU   GLDOFCPY            .  Office is from company rec. C'0'          
ISOFFLGR EQU   GLDOFLDG            .  Office is from ledger  rec. C'L'          
ISOFFTRN EQU   GLDOFTRN            .  Office is from trans.  rec. C'T'          
ISOFFCLI EQU   GLDOFCLI            .  Office is from client  rec. C'C'          
ISOFFPRD EQU   GLDOFPRD            .  Office is from product rec. C'P'          
ISOFFJOB EQU   GLDOFJOB            .  Office is from job     rec. C'J'          
ISOFFIN  EQU   GLDOFACT            .  Office is in the account    C'A'          
ISOFFNO  EQU   X'00'               .  Not posting by office                     
                                                                                
ISACTOFF DS    C                                                                
ISACTCLI EQU   GLDOFACL            .  Office is from client rec.  C'R'          
ISACTLOW EQU   GLDOFLOW            .  Office is from lowest lev.  C'S'          
                                                                                
CPYBALDR DS    PL8                 Company balance                              
CPYBALCR DS    PL8                 Company balance                              
                                                                                
DUB      DS    D                                                                
PARM     DS    6F                                                               
FULL     DS    F                                                                
DATADISP DS    H                                                                
HALF     DS    H                                                                
WORK     DS    XL64                                                             
WORK2    DS    XL36                                                             
BYTE     DS    X                                                                
DMIND    DS    X                                                                
DMUPD    EQU   X'80'               .  Read for update                           
DMDEL    EQU   X'08'               .  Read for deleted records                  
SPACES   DS    CL132                                                            
                                                                                
FROMACC  DS    CL15                                                             
TRAOFFC  DS    CL2                 Transaction   office                         
POSTOFF  DS    CL2                 Posting       office                         
POSTNEW  DS    CL2                 New over-ride office                         
ORGOFFC  DS    CL2                 Origin office code                           
PPROFFC  DS    CL2                 Office code                                  
                                                                                
TRN#OF   DS    H                   # of tranaction in table so far              
MAX#OF   DS    H                   Max # of entries allowed in Bin              
#OFGLPS  DS    H                                                                
LLEDGER  DS    XL(LGRLNQ)                                                       
LELEM    DS    XL256               Build element area                           
*ELEM_25 DS    XL(CXILNQ+L'CXIOMOA) Last Changed/Transmited element             
LELEM_45 DS    XL(BUKLNQ+1)        Bucket element                               
LELEM_E6 DS    XL256               Saved off element                            
LTRNBLK  DS    XL(TRNBLKL)         Local TRNBLK                                 
                                                                                
***********************************************************************         
* Local working areas                                                           
***********************************************************************         
         DS    0F                                                               
IO1      DS    XL(2*K+L'WKSLABEL)                                               
LGLRULES DS    XL(MAXRULES*PSTLNQ+L'WKSLABEL)                                   
                                                                                
GLWRKLNQ EQU   *-GLWORKD                                                        
         EJECT ,                                                                
***********************************************************************         
* General Equates                                                               
***********************************************************************         
K        EQU   1024                                                             
YES      EQU   C'Y'                                                             
NO       EQU   C'N'                                                             
EOT      EQU   0                                                                
EOR      EQU   0                                                                
TURNOFF  EQU   X'FF'                                                            
MAXRULES EQU   100                 Max GL posting rules per ledger              
ACCSYS   EQU   C'A'                Accounting                                   
                                                                                
***********************************************************************         
* Internal ledger DSECT                                                         
***********************************************************************         
LGRD     DSECT                                                                  
LGRCODE  DS    CL1                 Ledger - Unit S only                         
                                                                                
LGRACLVS DS    0XL4                                                             
LGRACLVA DS    XL1                 Account level A length                       
LGRACLVB DS    XL1                 Account level B length                       
LGRACLVC DS    XL1                 Account level C length                       
LGRACLVD DS    XL1                 Account level D length                       
LGRLVLOW EQU   12                  Lowest level                                 
                                                                                
LGRIND   DS    XL1                 Indicator                                    
LGRRREAD EQU   X'80'               .  Must read ledger, rule don't fit          
LGRTOFF  EQU   X'40'               .  Transaction office                        
LGRFOFF  EQU   X'20'               .  Filter office                             
LGROFPRD EQU   X'08'               .  Find posting rule by off for PROD         
LGRNOPST EQU   X'01'               .  No GL postings for ledger                 
                                                                                
LGROFFC  DS    CL2                 Office value                                 
LGROFPOS DS    CL1                 Offpos value                                 
LGRF#DSP DS    X                   Displacement to RSTELD filter value          
LGR#GLRL DS    X                   Number of GL rules                           
         DS    X                   Spare                                        
LGRRULES DS    A                   A(GL rules)                                  
LGRELE6  DS    A                   A(Office paris element)                      
LGRLNQ   EQU   *-LGRD                                                           
                                                                                
***********************************************************************         
* Posting rules DSECT                                                           
***********************************************************************         
PSTD     DSECT                                                                  
PSTLDG   DS    CL1                 Store by ledger                              
PSTFROM  DS    CL10                From account                                 
PSTFRLN  DS    XL1                 Length of from account -1                    
PSTTO    DS    CL14                To account                                   
PSTLNQ   EQU   *-PSTD                                                           
                                                                                
***********************************************************************         
* Profile key   DSECT                                                           
***********************************************************************         
PRFKEYD  DSECT                                                                  
PRFKSYS  DS    C                   SYSTEM                                       
PRFKPRG  DS    CL3                 PROGRAM                                      
         DS    X'00'               N/D                                          
PRFKUNL  DS    CL2                 UNIT/LEDGER                                  
PRFKACC  DS    CL3                 ACCOUNT                                      
PRFKIND  DS    C                   OFFICE INDICATOR                             
PRFK1CHR EQU   C'*'                C'*' INDICATES 1 CHARACTER OFFICE            
PRFK2CHR EQU   C'+'                C'+' INDICATES 2 CHARACTER OFFICE            
PRFKOFF1 DS    C                   OFFICE CODE                                  
PRFKAGY  DS    CL2                 AGENCY ALPHA-ID                              
PRFKOFF2 DS    CL2                 2 CHARACTER OFFICE CODE                      
PRFKLNQ  EQU   *-PRFKEYD                                                        
                                                                                
***********************************************************************         
* POSTING KEY                                                                   
***********************************************************************         
PSKD     DSECT                                                                  
PSKTYPE  DS    XL1                                                              
PSKTYGL  EQU   1                                                                
PSKTYSUB EQU   2                                                                
PSKTYKEY EQU   3                                                                
PSKTYBUK EQU   4                                                                
         ORG                                                                    
PSKGLCDE DS    0CL15                                                            
PSKSBCDE DS    0CL15                                                            
PSKGL#   DS    XL1                 Index to PSKTYGL                             
PSKOFFC  DS    CL2                 Office code                                  
PSKSUB#  DS    XL1                 Index to PSKTYSUB                            
PSKXTRA  DS    CL11                                                             
PSKLNQ   EQU   *-PSKD                                                           
                                                                                
PSKRKEY  DS    CL42                                                             
PSKMOS   DS    0XL2                                                             
PSKYEAR  DS    XL1                                                              
PSKMONTH DS    XL1                                                              
PSKKLNQ  EQU   *-PSKRKEY                                                        
PSKDR    DS    PL6                                                              
PSKCR    DS    PL6                                                              
***********************************************************************         
* Definition table of areas to divide up of storage                             
***********************************************************************         
DEFTABD  DSECT                                                                  
DEFTAB#K DS    AL1                 # of 1024 blocks of storage                  
DEFTPROF DS    AL1                 # of K for profiles       (@PROF#K)          
DEFTOFRL DS    AL1                 # of K for office rules   (@OFRUL#K)         
DEFTGLRL DS    AL1                 # of K for GL     rules   (@GLRUL#K)         
DEFTLNQ  EQU   *-DEFTABD                                                        
                                                                                
WKSD     DSECT                                                                  
WKSAREA  DS    AL4                 Disp of workering storage                    
WKSADDR  DS    AL4                 Disp to store A(working storge)              
WKSLABEL DS    CL8                 Eye catcher to set in storage                
WKSLNQ   EQU   *-WKSD                                                           
                                                                                
GLTABD   DSECT                                                                  
GLTULA   DS    CL14                GL Account                                   
GLTLINK# DS    H                                                                
GLTLNQ   EQU   *-GLTABD                                                         
                                                                                
ACNAMED  DSECT                     Account code to names DSECT                  
ACNCODE  DS    CL14                  Account code                               
ACNLEN   DS    X                     Length of name                             
ACNDISP  DS    AL3                   Displacement to name                       
ACNLNQ   EQU   *-ACNAMED                                                        
                                                                                
MEDD     DSECT                     Media code/name table DSECT                  
MEDCODE  DS    C                     Media code                                 
MEDNAME  DS    CL15                  Media name                                 
MEDLNQ   EQU   *-MEDD                                                           
                                                                                
GLRECD   DSECT                                                                  
GLRKEY1  DS    CL40                GLBKEY less type and company                 
         ORG   GLRKEY1                                                          
GLRKEY2  DS    CL42                Tranaction key                               
         ORG   GLRKEY1                                                          
GLRGKULA DS    CL13                                                             
GLRKGOFF DS    CL2                                                              
GLRKSULA DS    CL14                                                             
GLRKXTRA DS    CL11                Extra info of GLBRECD                        
GLRKCLEN DS    X                   Length of contra for reg GL posting          
GLRKSBR  DS    X                   Even DR, Odd CR                              
GLRKMOA  DS    XL2                 MOA                                          
GLRKLNQ  EQU   *-GLRECD                                                         
GLRAMNT  DS    PL8                                                              
GLRTYPE  DS    X                                                                
GLRLNQ   EQU   *-GLRECD                                                         
         EJECT ,                                                                
* ACGLPOSTD                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGLPOSTD                                                      
         PRINT ON                                                               
* ACADDTRND                                                                     
TRNBLKD  DSECT                                                                  
         PRINT OFF                                                              
       ++INCLUDE ACADDTRND                                                      
         PRINT ON                                                               
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
* DDMASTD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DDMASTD                                                        
         PRINT ON                                                               
* FATCB                                                                         
         PRINT OFF                                                              
       ++INCLUDE FATCB                                                          
         PRINT ON                                                               
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'007ACGLPOST  03/13/12'                                      
         END                                                                    
