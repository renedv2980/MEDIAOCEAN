*          DATA SET DDUSSQINQ  AT LEVEL 002 AS OF 10/28/15                      
*CATALP USSQINQ                                                                 
         TITLE 'QUERY USS QUEUE STATISTICS'                                     
* ALL ADDRESSES ARE 31 BIT. PROGRAM RUNS IN 31 BIT MODE.                        
*                                                                               
* INPUT PARAMETER  1  BYTE 0-3     A(4 BYTE USS QUEUE KEY)                      
*                  2  BYTE 0-3     USED FOR DATA RETURN                         
*                  3  BYTE 0-3     USED FOR DATA RETURN                         
*                  4  BYTE 0-3     USED FOR DATA RETURN                         
*                                                                               
* OUTPUT PARAMETER 1       0-3     UNCHANGED                                    
*                  2       0-3     RETURNED QUEUE DEPTH                         
*                  3       0-3     A(RETURNED QUEUE INFO) SEE MSQID_DS          
*                  4       0       AL1(RETURN CODE)                             
*                                  0=OK                                         
*                                  1=CAN'T LOAD USS LOAD MODULES                
*                                  2=UNEXPECTED RETURN CODE FROM USS            
*                                  3=QUEUE DOES NOT EXIST                       
*                                  4=ACCESS NOT ALLOWED TO QUEUE                
*                  4       1-3     NULLS                                        
*                                                                               
* 1. THE CONDITION CODE ON EXIT WILL BE ZERO/EQUAL IF THE RETURN CODE           
*    IN P4 BYTE 0 IS ZERO (OK)                                                  
* 2. NULLS WILL BE RETURNED IN P2 AND P3 IF A NON-ZERO RETURN CODE IS           
*    RETURNED IN P4 BYTE 0                                                      
* 3. THE ADDRESS RETURNED IN P3 IS A LOCATION IN USSQINQ'S NMOD WORKING         
*    STORAGE AND IS THEREFORE VOLATILE.                                         
* 4. MSQID_DS IS DEFINED BY THE BPXYMSG MACRO. THE QUEUE DEPTH RETURNED         
*    IN P2 IS EXTRACTED FROM THIS AREA TO AVOID THE NEED FOR THE CALLER         
*    TO REFER TO MSQID_DS. OTHER FIELDS IN MSQID_DS ARE UNLIKELY TO BE          
*    USEFUL                                                                     
         EJECT                                                                  
USSQINQ  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 INQWRKL,*USSQINQ                                                 
         USING INQWRK,RC                                                        
*                                                                               
         SAM31 ,                                                                
*                                                                               
         XC    INQWRK(INQWRKL),INQWRK                                           
*                                                                               
         ST    R1,APARMS                                                        
         MVC   AQID,0(R1)                                                       
*                                                                               
INQ100   OC    ABPX1QGT,ABPX1QGT                                                
         JNZ   INQ110                                                           
         LA    R0,BPX1QGT                                                       
         LOAD  EPLOC=(0)                                                        
         LTR   R3,RF                                                            
         JNZ   INQERR1             CAN'T LOAD                                   
         ST    R0,ABPX1QGT                                                      
*                                                                               
INQ110   OC    ABPX1QCT,ABPX1QCT                                                
         JNZ   INQ200                                                           
         LA    R0,BPX1QCT                                                       
         LOAD  EPLOC=(0)                                                        
         LTR   R3,RF                                                            
         JNZ   INQERR1             CAN'T LOAD                                   
         ST    R0,ABPX1QCT                                                      
*                                                                               
INQ200   MVI   S_TYPE,0            DO NOT CREATE (NO IPC_CREAT)                 
         MVI   S_MODE1,0           NOT USED                                     
         MVI   S_MODE2,0           NOT USED AS NOT CREATING                     
         MVI   S_MODE3,0           NOT USED AS NOT CREATING                     
*                                                                               
         L     R2,AQID                                                          
         L     RF,ABPX1QGT                                                      
         CALL  (15),((R2),S_MODE,RV,RCV,RSN),                          X        
               VL,MF=(E,MFPARMS),LINKINST=BASR                                  
         LT    RF,RV               QUEUE HANDLE (OR ERROR IF -VE)               
         JNM   INQ210                                                           
         CLC   RCV,=A(ENOENT)      QUEUE DOES NOT EXIST                         
         JE    INQERR3                                                          
         CLC   RCV,=A(EACCES)      ACCESS NOT ALLOWED                           
         JE    INQERR4                                                          
         J     INQERR2             UNEXPECTED RETURN CODE.                      
*                                                                               
INQ210   ST    RF,USSQHNDL         STORE THE QUEUE HANDLE                       
*                                                                               
INQ300   XC    RESULT,RESULT                                                    
         LA    R2,RESULT                                                        
         ST    R2,FULL                                                          
         LA    R2,FULL                                                          
         L     RF,ABPX1QCT                                                      
         CALL  (15),(USSQHNDL,=A(IPC_STAT),(R2),RV,RCV,RSN),           *        
               VL,MF=(E,MFPARMS),LINKINST=BASR                                  
*                                                                               
         LT    RF,RV               ERROR IF -VE                                 
         JNM   INQOK                                                            
         CLC   RCV,=A(EACCES)      ACCESS NOT ALLOWED                           
         JE    INQERR4                                                          
         J     INQERR2             UNEXPECTED RETURN CODE.                      
*                                                                               
INQERR1  MVI   RTNCODE,1                                                        
         J     INQRTN                                                           
*                                                                               
INQERR2  MVI   RTNCODE,2                                                        
         J     INQRTN                                                           
*                                                                               
INQERR3  MVI   RTNCODE,3                                                        
         J     INQRTN                                                           
*                                                                               
INQERR4  MVI   RTNCODE,4                                                        
         J     INQRTN                                                           
*                                                                               
INQOK    LA    R1,RESULT                                                        
         ST    R1,AQOUT            RETURN RESULT                                
CTL      USING MSQID_DS,RESULT                                                  
         MVC   QDEPTH,CTL.MSG_QNUM RETURN Q DEPTH (PART OF RESULT)              
         DROP  CTL                                                              
*                                                                               
INQRTN   L     R1,APARMS           RETURN TO CALLER                             
         MVC   0(L'PARMS,R1),PARMS                                              
*                                                                               
INQXIT   CLI   RTNCODE,0           SET CC EQ IF OK                              
XIT      XIT1  ,                                                                
         EJECT                                                                  
* LITERALS ETC.                                                                 
*                                                                               
         LTORG                                                                  
*                                                                               
         DS    0D                                                               
BPX1QGT  DC    CL8'BPX1QGT '                                                    
ABPX1QGT DC    A(0)                                                             
BPX1QCT  DC    CL8'BPX1QCT '                                                    
ABPX1QCT DC    A(0)                                                             
*                                                                               
         BPXYMODE DSECT=NO                                                      
*                                                                               
         BPXYMSG                                                                
         BPXYIPCP                                                               
*                                                                               
ENOENT   EQU   129                 IBM ERROR CODE (RESOURCE NOT AVAIL)          
EACCES   EQU   111                 IBM ERROR CODE (RESOURCE NOT AVAIL)          
*                                                                               
INQWRK   DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    C                                                                
BYTE1    DS    C                                                                
*                                                                               
APARMS   DS    A                                                                
*                                                                               
PARMS    DS    0XL16                                                            
AQID     DS    A                   A(QUEUE ID) - INPUT                          
QDEPTH   DS    F                   Q DEPTH - RETURNED                           
AQOUT    DS    A                   A(QUEUE INFO) - RETURNED                     
RTNCODE  DS    X                   RETURN CODE                                  
         DS    XL3                 UNUSED                                       
*                                                                               
RV       DS    F                   RETURN VALUE                                 
RCV      DS    F                   RETURN CODE                                  
RSN      DS    F                   REASON CODE                                  
*                                                                               
MFPARMS  DS    8F                                                               
*                                                                               
USSQHNDL DS    F                   QUEUE HANDLE                                 
*                                                                               
RESULT   DS    CL(MSQ#LENGTH)      SEE MSQID_DS                                 
*                                                                               
INQWRKL  EQU   *-INQWRK                                                         
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002DDUSSQINQ 10/28/15'                                      
         END                                                                    
