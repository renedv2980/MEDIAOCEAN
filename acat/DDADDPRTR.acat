*          DATA SET DDADDPRTR  AT LEVEL 005 AS OF 05/01/02                      
*CATALP ADDPRTR                                                                 
         PRINT NOGEN                                                            
         SPACE 1                                                                
PRINTER CSECT                                                                   
         NMOD1 42,PRINTER                                                       
         USING DCONT,RC                                                         
         L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
         LA    R7,PLINE                                                         
         AH    R7,OFFSET                                                        
         SPACE 1                                                                
         TM    MODE,X'0E'          PAGING FLAGS?                                
         BM    PAGING                                                           
         TM    MODE,X'C0'          HEAD LINE - FITS ON PAGE                     
         BM    PRNTUSER                                                         
         TM    MODE,X'10'          NEW COLUMN HEADS                             
         BO    PRTCLHDS              YES                                        
         SPACE 1                                                                
         PACK  DUB(2),SPACING+2(2)                                              
         AP    DUB(2),LINE                                                      
         CP    DUB(2),MAXLINE      WILL LINE FIT?                               
         BH    FOOT                  NO                                         
         SPACE 1                                                                
PRNTUSER MVC   PLINE,SPACES                                                     
         MVC   0(90,R7),P                                                       
         MVC   P,SPACES                                                         
         SPACE 1                                                                
         TM    MODE,X'C0'          UNDERSCORED HEAD?                            
         BM    DOHEAD                YES                                        
         SPACE 1                                                                
         MVC   PCONTROL,SPACING      NO                                         
         BAS   R9,PRINTEM                                                       
EXIT     XMOD1 1                                                                
         SPACE 1                                                                
PRINTEM  CLI   ULCFLAG,C'Y'        TN TRAIN                                     
         BE    *+10                  YES                                        
         OC    PLINE(95),SPACES      NO - CAP ALL SMALL LETTERS                 
         GOTO1 =V(PRINT),PARAS,PBYTE,PCONTROL                                   
         PACK  DUB(2),PCONTROL+2(2)                                             
         AP    LINE,DUB(2)                                                      
         BR    R9                                                               
         SPACE 1                                                                
DOHEAD   MVC   PCONTROL,=C'AL02'                                                
         MVC   HEAD,0(R7)          SAVE HEAD FOR USCORE                         
         TM    MODE,X'40'                                                       
         BO    *+8                                                              
         MVI   PCONTROL+3,C'0'                                                  
         NI    MODE,X'3F'                                                       
         BAS   R8,USCORE                                                        
         B     EXIT                                                             
         TITLE 'ADDRESSED DOCUMENT(S) PRINTER MODULE'                           
*                   SPECIAL PAGE HANDLING                                       
         SPACE 2                                                                
PAGING   TM    MODE,X'02'          FINISH PAGE AND RETURN?                      
         BO    FOOT                  YES                                        
         XI    MODE,X'08'            NO                                         
         B     ODDST                                                            
         SPACE 1                                                                
FOOT     CLC   TITLE(8),SPACES                                                  
         BE    FOOTOFF                                                          
         SPACE 1                                                                
         MVC   PCONTROL,=C'BL03'                                                
         ZAP   FOOTLINE,MAXLINE                                                 
         SP    FOOTLINE,LINE                                                    
         CP    FOOTLINE,=P'0'                                                   
         BNH   FOOTSET                                                          
         UNPK  PCONTROL+2(2),FOOTLINE                                           
         BAS   R9,MOVEIT                                                        
         SPACE 1                                                                
FOOTSET  MVC   PCONTROL,=C'AL03'   SKIP 2 LINES BEFORE FOOT (CAPTION)           
         BAS   R9,SETFOOT                                                       
         BAS   R8,USCORE           PRINT FOOT                                   
         SPACE 1                                                                
FOOTOFF  BAS   R8,ENDPAGE                                                       
         TM    MODE,X'02'          FINISH PAGE                                  
         BZ    ODDST                 NO                                         
         XI    MODE,X'02'            YES                                        
         B     EXIT                   GET OUT                                   
         SPACE 2                                                                
ENDPAGE  MVC   PCONTROL,=C'AC01'                                                
         AP    PAGE,=P'1'                                                       
         ZAP   LINE,=P'0'                                                       
         CLI   REPFLAG,C'Y'                                                     
         BNE   PAGEENDX                                                         
         CLI   POINTNUM,C'Y'                                                    
         BE    PAGEENDX                                                         
         SPACE 1                                                                
         CP    PAGE,LASTPG         REP RANGE FILLED                             
         BNH   PAGEENDX              NO                                         
         SPACE 1                                                                
         MVI   POINTNUM,C'Y'                                                    
         MVC   SVPNOLEN,LOFPGNO                                                 
         ZAP   PAGE,=P'1'                                                       
PAGEENDX BR    R8                                                               
         SPACE 2                                                                
MOVEIT   GOTO1 =V(PRINT),PARAS,SPACES-1,PCONTROL                                
         BR    R9                                                               
         EJECT                                                                  
SETFOOT  MVC   PLINE,SPACES                                                     
         LA    R4,58(R7)                                                        
         LH    R3,LOFTITLE                                                      
         SR    R4,R3                                                            
         EX    R3,MOVTITLE                                                      
         MVC   HEAD,0(R7)                                                       
         MVC   60(10,R7),REVDATE                                                
         BAS   R8,PAGENUM                                                       
         BR    R9                                                               
         SPACE 1                                                                
MOVTITLE MVC   0(0,R4),TITLE                                                    
         SPACE 1                                                                
ODDST    MVC   PCONTROL,=C'AC01'                                                
         BAS   R9,MOVEIT                                                        
         MVC   PCONTROL,=C'BL02'                                                
         MVC   PCONTROL+2(2),FIRSTLN                                            
         BAS   R9,MOVEIT                                                        
         MVC   PCONTROL,SPACING                                                 
         CLC   LOFTITLE,=H'0'      NEW TITLE                                    
         BNE   PRTCLHDS              NO                                         
         SPACE 1                                                                
         BAS   R8,TITLELEN           YES - GET NEW LENGTH                       
         MVC   PCONTROL,=C'BL00'                                                
         MVC   PLINE,SPACES                                                     
         LA    R5,79(R7)                                                        
         SH    R5,LOFTITLE                                                      
         MVC   0(58,R5),TITLE                                                   
         OC    0(58,R5),SPACES     ALL CAPS                                     
         MVC   HEAD,0(R7)                                                       
         BAS   R8,USCORE                                                        
         MVC   PLINE,SPACES                                                     
         MVI   PCONTROL+3,C'2'     SKIP TWO MORE LINES                          
         BAS   R9,PRINTEM                                                       
         EJECT                                                                  
PRTCLHDS CLC   COLHEADS,SPACES     ANYTHING TO PRINT                            
         BE    CLEARBIT              NO                                         
         SPACE 1                                                                
         TM    MODE,X'10'          NEW COLUMN HEADS                             
         BZ    CHPRINT               NO  - GO PRINT                             
         MVC   CHUSCORE,SPACES       YES                                        
         MVC   P,SPACES                                                         
         ZAP   CHCTDFLG,=P'0'                                                   
         LA    R3,COLHEADS                                                      
         LA    R4,CHUSCORE                                                      
         LA    R0,77(R3)                                                        
         SPACE 1                                                                
CHLP1    CLI   0(R3),C' '          LEADING SPACE                                
         BNE   CHLP2                 NO                                         
         LA    R3,1(R3)              YES                                        
         LA    R4,1(R4)                                                         
         CR    R3,R0               MORE IN COLHEADS                             
         BNH   CHLP1                 YES                                        
         B     CHPRINT               NO  - GO PRINT                             
         SPACE 1                                                                
CHLP2    CLI   ULCFLAG,C'Y'                                                     
         BE    *+12                                                             
         MVI   0(R4),C'-'                                                       
         B     *+8                                                              
         MVI   0(R4),X'6D'                                                      
         LA    R3,1(R3)                                                         
         LA    R4,1(R4)                                                         
         CR    R3,R0               CHECK LIMIT                                  
         BH    CHPRINT               REACHED                                    
         CLI   0(R3),C' '          TRAILING SPACE                               
         BNE   CHLP2                 NO - CONTINUE                              
         SPACE 1                                                                
         CLC   0(2,R3),SPACES      NON-FLOWING TEXT                             
         BNE   CHLP2                 NO - CONTINUE UNDERSCORE                   
         AP    CHCTDFLG,=P'1'        YES - COUNT ENTRY FOUND                    
         LA    R3,2(R3)                                                         
         LA    R4,2(R4)                                                         
         CR    R3,R0               CHECK LIMIT                                  
         BH    CHPRINT               REACHED                                    
         B     CHLP1                       LOOK FOR MORE                        
         SPACE 1                                                                
CLEARBIT NI    MODE,X'EF'          RESET NEW COLHEADS BIT                       
         B     PRNTUSER                                                         
         EJECT                                                                  
CHPRINT  MVC   PLINE,SPACES                                                     
         MVC   0(80,R7),COLHEADS                                                
         MVC   PCONTROL,=C'BL00'                                                
         CLI   ULCFLAG,C'Y'                                                     
         BE    *+8                                                              
         MVI   PCONTROL+3,C'1'                                                  
         BAS   R9,PRINTEM                                                       
         MVC   0(80,R7),CHUSCORE                                                
         MVI   PCONTROL+3,C'2'     ONE LINE AFTER COL HEADS                     
         BAS   R9,PRINTEM                                                       
         TM    MODE,X'10'          NEW COLUMN HEADS                             
         BZ    PRNTUSER              NO                                         
         SPACE 1                                                                
         XI    MODE,X'10'            YES - RESET BIT                            
         CP    CHCTDFLG,=P'1'      SINGLE ENTRY                                 
         BNE   EXIT                  NO                                         
         SPACE 1                                                                
         LA    R3,COLHEADS+79        YES                                        
         LA    R4,0                                                             
         CLI   0(R3),C' '          FIND PLACE FOR (CONT'D)                      
         BNE   *+12                                                             
         LA    R4,1(R4)                                                         
         BCT   R3,*-12                                                          
         SPACE 1                                                                
         CH    R4,=H'9'            ROOM FOR IT                                  
         BL    EXIT                  NO                                         
         MVC   2(8,R3),=X'4DC39695A37D845D'     (CONT'D)                        
         CLI   ULCFLAG,C'Y'                                                     
         BE    *+10                                                             
         OC    3(6,R3),SPACES                                                   
         B     EXIT                                                             
         EJECT                                                                  
*                   UNDERSCORE ANYTHING IN HEAD                                 
         SPACE 1                                                                
USCORE   BAS   R9,PRINTEM                                                       
         MVC   PLINE,SPACES                                                     
         LA    R2,HEAD                                                          
         LR    R5,R7                                                            
         LA    R4,79                                                            
         SPACE 1                                                                
PR6      CLI   0(R2),C' '          A(FIRST NON-SPACE) TO R2                     
         BNE   PR8                                                              
         LA    R2,1(R2)                                                         
         LA    R5,1(R5)                                                         
         BCT   R4,PR6                                                           
         B     PR16                                                             
         SPACE 1                                                                
PR8      LA    R3,HEAD+79          A(LAST NON-SPACE) TO R3                      
         SPACE 1                                                                
PR10     CLI   0(R3),C' '                                                       
         BNE   PR12                                                             
         BCTR  R4,0                                                             
         BCT   R3,PR10                                                          
         SPACE 1                                                                
PR12     CLI   ULCFLAG,C'Y'        U/LC TRAIN                                   
         BNE   *+12                                                             
         MVI   0(R5),X'6D'           YES - USE USCORE CHAR                      
         B     *+8                                                              
         MVI   0(R5),C'-'            NO  - USE DASH                             
         LTR   R4,R4               GET LENGTH OF UNDERSCORE                     
         BNP   PR14                                                             
         BCTR  R4,0                                                             
         EX    R4,UFINISH                                                       
         B     PR16                                                             
         SPACE 1  4                                                             
UFINISH  MVC   1(0,R5),0(R5)                                                    
         SPACE 1                                                                
PR14     CLI   ULCFLAG,C'Y'        SAME-LINE UNDERSCORE                         
         BE    PR16                  YES                                        
         MVC   PCONTROL,=C'AL01'     NO                                         
         BAS   R9,PRINTEM                                                       
         MVC   PLINE,SPACES                                                     
         SPACE 1                                                                
PR16     MVC   PCONTROL,=C'BL03'                                                
         BAS   R9,PRINTEM                                                       
         BR    R8                                                               
         EJECT                                                                  
*                   FORMAT PAGE NUMBER FOR PRINTING                             
         SPACE 1                                                                
PAGENUM  LA    R5,79(R7)                                                        
         LA    R3,PGNOWORK                                                      
         CLI   POINTNUM,C'Y'                                                    
         BNE   ARABIC1                                                          
         SPACE 1                                                                
         LH    R3,SVPNOLEN                                                      
         LA    R3,PGNOWORK(R3)                                                  
         MVI   0(R3),C'.'                                                       
         LA    R3,1(R3)                                                         
         B     CONTIN                                                           
         SPACE 1                                                                
ARABIC1  MVC   PGNOWORK,SPACES                                                  
         CLI   SECTNUM,C' '        SECTIONAL PAGE NUMBERING                     
         BE    CONTIN                NO                                         
         SPACE 1                                                                
         TM    SECTNUM,X'F0'       YES - NUMERIC                                
         BO    ONEDIGIT                    YES                                  
         SPACE 1                                                                
         MVN   P-1(1),SECTNUM                                                   
         NI    P-1,X'0F'                                                        
         SR    R4,R4                                                            
         IC    R4,P-1                                                           
         BCTR  R4,0                                                             
         SLA   R4,1                DOUBLE IT                                    
         LA    R4,DIGTAB(R4)                                                    
         MVC   0(2,R3),0(R4)                                                    
         MVI   2(R3),C'-'                                                       
         LA    R3,3(R3)                                                         
         B     CONTIN                                                           
         SPACE 1                                                                
ONEDIGIT MVC   0(1,R3),SECTNUM                                                  
         MVI   1(R3),C'-'                                                       
         LA    R3,2(R3)                                                         
CONTIN   EDIT  (P2,PAGE),(3,(R3)),ALIGN=LEFT                                    
         SPACE 1                                                                
         LA    R3,10                                                            
         LA    R4,PGNOWORK+9                                                    
         CLI   0(R4),C' '                                                       
         BNE   *+10                                                             
         BCTR  R4,0                                                             
         BCT   R3,*-10                                                          
         STH   R3,LOFPGNO                                                       
         SPACE 1                                                                
         SR    R5,R3                                                            
         MVC   0(10,R5),PGNOWORK                                                
         BR    R8                                                               
         EJECT                                                                  
*                   FIND OUT TITLE LENGTH                                       
         SPACE 1                                                                
TITLELEN LA    R3,TITLE+57                                                      
         LA    R4,58                                                            
         SPACE 1                                                                
         CLI   0(R3),C' '                                                       
         BNE   LENGTH                                                           
         BCTR  R3,0                                                             
         BCT   R4,*-10                                                          
         SPACE 1                                                                
LENGTH   STH   R4,LOFTITLE                                                      
         BR    R8                                                               
         SPACE 2                                                                
*                   WORK SPACE                                                  
         SPACE 1                                                                
HEAD     DS    CL80                AUTO U'SCORE LINE                            
CHUSCORE DS    CL80                COLUMN HEADS UNDERSCORE LINE                 
LOFPGNO  DS    H                                                                
FOOTLINE DC    PL2'0'                                                           
CHCTDFLG DS    PL1                 COLUMN HEADS CONTINUED FLAG                  
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
CPRINT   CSECT                                                                  
         DC    H'1'                                                             
         DS    CL2                                                              
         DC    CL132' '                                                         
         DC    CL132' '                                                         
         DC    CL80' '                                                          
         DC    CL60' '                                                          
         DC    CL10' '                                                          
         DC    CL16'1011121314151617'                                           
         DC    CL10' '                                                          
         DC    H'0'                                                             
         DC    PL2'0'                                                           
         DC    H'0'                                                             
         DC    CL2' '                                                           
         DC    C'BL01'                                                          
         DC    PL2'1'                                                           
         DC    PL2'54'                                                          
         DC    PL2'0'                                                           
         DC    PL1'8'                                                           
         DC    C' '                                                             
         DC    C'Y'                                                             
         DC    X'00'                                                            
         DC    C' '                                                             
         DC    C' '                                                             
         SPACE 1                                                                
*                  DSECT FOR PRINT CONTROLLER                                   
         SPACE 1                                                                
DCONT    DSECT                                                                  
PBYTE    DS    CL1                                                              
PLINE    DS    CL132                                                            
DUB      DS    D                                                                
WORK     DS    CL32                                                             
PCONTROL DS    CL4                                                              
PARAS    DS    CL8                                                              
         EJECT                                                                  
       ++INCLUDE DDDADDPRNT                                                     
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005DDADDPRTR 05/01/02'                                      
         END                                                                    
