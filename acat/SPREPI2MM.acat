*          DATA SET SPREPI2MM  AT LEVEL 027 AS OF 05/19/20                      
*CATALP SPI2MM                                                                  
         TITLE 'SPI2MM - SI2 MATCHMAKER MODULE'                                 
         PRINT NOGEN                                                            
***********************************************************************         
* USER    JIRA       DATE                  CHANGE LOG                 *         
* ---- ----------  -------- ----------------------------------------- *         
* AKAT SPEC-46400  05/19/20 INV DEMO MOVING FROM SNVIDDEM TO SNVIDDM4 *         
***********************************************************************         
SPI2MM   CSECT                                                                  
         NMOD1 WORKDL,**I2MM,CLEAR=YES                                          
         LR    R7,RC                                                            
         USING WORKD,R7                                                         
         USING SPWORKD,RA,R9                                                    
         LA    RC,SPACEND                                                       
         USING IMRWORK,RC                                                       
         USING IELEMD,R8                                                        
         LA    R5,MINBLOCK                                                      
         USING MINBLKD,R5                                                       
*                                                                               
         L     RF,ACOMFACS                                                      
         MVC   VMINIO,CMINIO-COMFACSD(RF)                                       
         MVC   VPERVERT,CPERVERT-COMFACSD(RF)                                   
         MVI   TRACEOPT,0          TRACE BITS                                   
         CLC   =C'MMTRACE',QUESTOR                                              
         BNE   *+8                                                              
         MVI   TRACEOPT,X'FF'      TRACE BITS                                   
*                                  X'80' = MIMOUT                               
*                                  X'40' = NEWPSV                               
*                                  X'20' = BLDS                                 
*                                  X'10' = FIXS                                 
*                                  X'08' = BLDPKT                               
*                                                                               
         CLI   MULTIMON,C'Y'       IF MULTIPLE MONTHS                           
         BE    EXIT                NO MM STUFF                                  
         CLI   QMGR,C' '           IF BY MARKET GROUP                           
         BNE   EXIT                NO MM STUFF                                  
*                                                                               
         CLI   NETPSW,C'Y'         IS IT NETPAK                                 
         BNE   SM00                                                             
         CLC   BQSTARTP,=X'CA21'   DON'T DO MM PRIOR TO JAN/01                  
         BL    EXIT                NO MM STUFF                                  
*                                                                               
SM00     CLC   QENDAUTO,=C'ALL'    IF PIGGY IS ALL                              
         BE    EXIT                NO MM STUFF                                  
         CLC   QENDAUTO,=C'YES'    IF PIGGY IS YES                              
         BNE   SM01                                                             
         CLC   QPRD,=C'POL'        AND PRD NOT POL                              
         BNE   EXIT                NO MM STUFF                                  
*                                                                               
SM01     CLI   0(R1),C'C'          COMMENTS MODE                                
         BNE   SM02                NO - INVOICE MODE                            
         BRAS  RE,MMCOM                                                         
*                                                                               
EXIT     XIT1                                                                   
*                                                                               
*  PROCESS INVOICES                                                             
*                                                                               
SM02     MVI   SVSOON,C'N'                                                      
         L     R1,VMASTC           A(DDMASTD)                                   
         USING MASTD,R1                                                         
         OC    MCREMPQK,MCREMPQK   SOON?                                        
         BZ    *+8                 ZERO = NOT SOON                              
         MVI   SVSOON,C'Y'                                                      
         DROP  R1                                                               
*                                                                               
         CLI   SVSOON,C'Y'         SOON REQUEST                                 
         BNE   SM06                                                             
         CLI   QBYID,C'Y'          IF REQUEST BY ID                             
         BNE   SM04                                                             
         CLI   RNOIDSW,C'Y'        PROCEESING RNOS BY ID                        
         BE    SMX                 SKIP                                         
SM04     CLI   SVMMPROC,C'Y'       SKIP IF ALREADY PROCESSED                    
         BNE   SM05                ALREADY PROCESSED MM !! $UDP DUMPS           
         CLC   QPRD,=C'ALL'        LET ALL PRDS THROUGH                         
         BE    SM05                                                             
         B     SMX                 ALREADY PROCESSED MM !! $UDP DUMPS           
*                                                                               
SM05     L     R1,AFBY             NEED TO PROCESS NO ORDERED                   
         C     R1,ALBY                                                          
         BNL   SM06                AT REQL OTHERWISE                            
         CLI   REQLSW,C'Y'         SKIP IF REALLY REQLAST                       
         BE    SMX                 ALREADY PROCESSED MM !! $UDP DUMPS           
*                                                                               
SM06     L     R1,ALINV                                                         
         S     R1,AFINV                                                         
         BNP   SMX                 IF NO INVOICE ITEMS, FORGET IT               
         LA    R1,1(R1)                                                         
         CLI   QBYID,C'Y'          WILL GET CALLED MULTIPLE                     
         BE    *+8                 TIMES IF BY ID                               
         MVI   SVMMPROC,C'Y'       SET PROCESSED MM                             
*                                                                               
         XC    SVBYRSEQ,SVBYRSEQ                                                
         CLI   NETPSW,C'Y'         IS IT NETPAK                                 
         BE    SM08                THEN SKIP                                    
         CLI   MKPROF+4,C'Y'       POPULATE BY BUYER                            
         BNE   SM08                                                             
         BRAS  RE,GETBYR           GET BUYER SEQ NUMBER                         
         BE    SM08                                                             
         MVC   P+2(45),=C'** NO BUYER FOUND- MM WILL NOT BE UPDATED **'         
         GOTO1 REPORT                                                           
         B     SMX                                                              
*                                                                               
SM08     BAS   RE,MNINIT           INITIALIZE MINIO                             
*                                                                               
         BRAS  RE,BLDSTAT          BUILD STATUS ELEM                            
*                                                                               
         CLC   SVINVCT,=H'1'       IF MORE THAN ONE INVOICE                     
         BL    SMX                 IF NO INVS, SKIP THE WHOLE THING             
         BNH   SM09                IF ONLY ONE INVOICE, SKIP SORT               
*                                  ELSE SORT ON INVOICE NUMBER                  
         SR    R0,R0                                                            
         LH    R4,IELMLEN          I2(OFF-LINE) LENGTH                          
         DR    R0,R4               R1= NO OF ITEMS                              
         ST    R1,DMCB+4                                                        
         LA    R3,IINVID-IELEM                                                  
         GOTO1 XSORT,DMCB,AFINV,,(R4),2,(R3)                                    
*                                                                               
SM09     DS    0H                  PROCESS INVOICE DETAILS                      
         L     R8,AFINV                                                         
         XC    LASTINV,LASTINV                                                  
SM10     C     R8,ALINV                                                         
         BNL   SM40                                                             
*                                                                               
         CLC   LASTINV,IINVID      SAME INVOICE?                                
         BE    SM30                                                             
         OC    LASTINV,LASTINV     NO, IS IT THE FIRST                          
         BNZ   SM40                NO, CLOSE LAST INVOICE                       
*                                                                               
SM20     DS    0H                  NEW INVOICE                                  
         MVC   LASTINV,IINVID                                                   
         TM    IINVID,X'80'        FROM OLD STYLE INVOICE- SKIP                 
         BNZ   SM39                                                             
         SR    RF,RF               SET MINIO RECORD KEY                         
         ICM   RF,3,IINVID                                                      
         BCTR  RF,0                                                             
         MH    RF,=Y(32)                                                        
         A     RF,AIKLIST                                                       
         MVC   MINMKEY(32),0(RF)                                                
*                                                                               
         GOTO1 VMINIO,DMCB,('MINOPN',MINBLKD)                                   
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLI   NETPSW,C'Y'         IS IT NETPAK                                 
         BNE   SM24                                                             
         CLI   SVSOON,C'Y'         AND A SOON REQUEST                           
         BNE   SM24                                                             
         BRAS  RE,CHKMMACT         CHECK INVOICE CAN HANDLE MM INFO             
         BE    SM24                                                             
         MVI   PSTOPT,C'M'         ABORT - INVOICE TOO BIG FOR SOON             
         B     SM41X               CLOSE WITH NO MM STUFF                       
*                                                                               
SM24     XC    MINEKEY,MINEKEY     GET HEADER ELEM                              
         MVI   MINEKEY,SNVHDELQ                                                 
         MVI   MINFILTL,1                                                       
         GOTO1 VMINIO,DMCB,('MINHI',MINBLKD)                                    
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                MISSING HEADER ELEM                          
*                                                                               
***>     BAS   RE,FIXSTAT          DEAL WITH EXISTING STATUS ELEMS              
*                                                                               
         GOTO1 DATCON,DMCB,(2,MELEM+SNVHDSDT-SNVHDEL),SAVSTRT                   
*                                                                               
SM30     DS    0H                  SAME INVOICE                                 
         XC    MINEKEY,MINEKEY                                                  
         LA    R3,MINEKEY                                                       
         USING SNVIDELD,R3                                                      
         MVI   SNVIDEL,SNVIDELQ                                                 
         MVC   SNVIDSEQ-1(1),IMNSEQ     SEQ #                                   
*                                                                               
         GOTO1 DATCON,(R1),(2,IDAT),WORK                                        
         GOTO1 VPERVERT,(R1),SAVSTRT,WORK                                       
         LLC   RF,DMCB+9           NUMBER OF DAYS (1 BYTE ONLY)                 
         BCTR  RF,0                PERVERT RETURNS INCLUSIVE COUNT              
         STC   RF,SNVIDDAY-1                                                    
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,3,ITIM                                                        
*                                                                               
         TM    ISTAT2,ISTTCNV      WAS THIS CONVERTED TO MILITARY TIME?         
         BZ    SM31                NO                                           
*                                                                               
         CHI   RF,600              BEFORE 6 A.M.?                               
         BNL   *+8                 NO                                           
         AHI   RF,2400             YES - 6 A.M. = 0 MINS, SO ADD 2400           
         SR    RE,RE                                                            
         D     RE,=F'100'                                                       
         MHI   RF,60                                                            
         AR    RF,RE                                                            
*                                                                               
SM31     SHI   RF,360              SUBTRACT 6 HOURS                             
         STCM  RF,3,SNVIDTIM-1     SO 6AM IS 0 MINUTES                          
*                                                                               
SM33     DS    0H                                                               
         GOTO1 VMINIO,DMCB,('MINRD',MINBLKD)                                    
         CLI   MINERR,0                                                         
         BE    SM33A                                                            
         CLI   MINERR,MINERNF      NOT FOUND?                                   
         BE    *+6                 YES                                          
         DC    H'0'                NO - SOME OTHER ERROR                        
***                                                                             
* SOMETIMES THE INVOICE RECORD WILL HAVE A DETAIL ELEMENT WITH A TIME           
* THAT IS GREATER THAN OR EQUAL TO 24 HOURS. IF THIS IS THE CASE, THEN          
* WE NEED TO ADD 24 HOURS TO SNVIDTIM AND LOOK AGAIN BEFORE ABENDING            
***                                                                             
         CLC   SNVIDTIM-1,=X'05A0' >= 24 HOURS (6AM)?                           
         BNH   *+6                 NO, ADD 24 HOURS                             
         DC    H'0'                YES - ALREADY TRIED                          
*                                                                               
         XR    RF,RF                                                            
         ICM   RF,3,SNVIDTIM-1     DETAIL TIME WE JUST LOOKED UP                
         AHI   RF,X'05A0'          ADD 24 HOURS                                 
         STCM  RF,3,SNVIDTIM-1     AND LOOK FOR THIS TIME                       
         B     SM33                                                             
*                                                                               
SM33A    LA    R3,MELEM            CLEAR MATCHING FIELDS                        
         CLI   NETPSW,C'Y'         IS IT NETPAK                                 
         BE    *+14                                                             
***      XC    SNVIDBES(10),SNVIDBES    <--- LEN = ALL SPOT BUY DATA            
         XC    SNVIDMM,SNVIDMM          <--- LEN = ALL SPOT BUY DATA            
         B     SM32                                                             
         XC    SNVIDUDT(6),SNVIDUDT     NETPAK UNIT DATA -                      
         XC    SNVIDUPG(8),SNVIDUPG    CAREFUL TO LEAVE INT AND PKG             
         MVI   SNVIDFLG,0                                                       
*                                                                               
SM32     SR    R6,R6               FIND BUY LINE FOR THIS ITEM                  
         ICM   R6,7,IBY                                                         
         BZ    SM36                NOT MATCHED - STILL NEED DEMO                
         BCTR  R6,0                                                             
         A     R6,AFBY                                                          
         USING BYELEMD,R6                                                       
*                                                                               
         CLI   NETPSW,C'Y'         IS IT NETPAK                                 
         BE    SM34                                                             
*                                  SET SPOT BUY DATA FOR MATCHMAKER             
         TM    UPDERROR,UPDERBIG   ERROR UPDATING BUYS                          
         BZ    SM34A                                                            
         CLC   BYLIN,ERRLINES      DIDN'T WANT TO USE A REGISTER                
         BE    SM36                                                             
         CLC   BYLIN,ERRLINES+2    IF LINE TO BIG FOR AFFIDS                    
         BE    SM36                DON'T PUT MATCH DATA ON INVOICE              
         CLC   BYLIN,ERRLINES+4                                                 
         BE    SM36                                                             
*                                  SET SPOT BUY DATA FOR MATCHMAKER             
SM34A    MVC   SNVIDBES,BYEST          ESTIMATE                                 
         CLI   BYLIN,0                 HAVE A REAL 2-BYTE BUYLINE?              
         BNE   *+10                    YES - DON'T POPULATE SNVIDBLN!           
         MVC   SNVIDBLN,BYLIN+1        LINE                                     
         MVC   SNVIDTBL,BYLIN          2-BYTE BUYLINE                           
         MVC   SNVIDBDT,BYSDT          DATE                                     
         MVC   SNVIDBNO,BYSPT          SPOT NUMBER WITHIN DATE                  
         MVC   SNVIDBOR,BYORBNO        ORBIT SPOT NUMBER -NOT USED              
         TM    BYSTAT,X'08'        TEST ORBIT SLAVE                             
         BZ    SM36                                                             
         BRAS  RE,ORBFIX           NEED TO GET REAL INFO FROM MASTER            
         MVC   SNVIDBDT,ORBSDT                                                  
         MVC   SNVIDBNO,ORBSPT                                                  
         B     SM36                                                             
*                                                                               
SM34     DS    0H                                                               
         TM    BYSTAT2,BYSTMIRQ    UNIT PART OF A MIRROR?                       
         BZ    SM35                                                             
         BRAS  RE,MIRMATCH         BOTH UNITS MUST MATCH TO SEED                
         BNE   SM36                                                             
*                                                                               
SM35     XC    WORK,WORK           GET UNIT KEY FOR DETAILS                     
         MVC   WORK(4),BYDISK      DISK ADDRESS                                 
         LA    R0,BUFFAGET                                                      
         GOTO1 =V(BUFFERIN),DMCB,((R0),ABUFFRIN),(0,WORK),ACOMFACS              
         CLI   4(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R1,WORK+4           SKIP DA                                      
         USING NUKEY,R1                                                         
*                                  SET NET UNIT DATA FOR MATCHMAKER             
         MVC   SNVIDUDT,NUKDATE        UNIT DATE                                
         MVC   SNVIDUTM,NUKTIME        UNIT TIME (1/4 HOUR)                     
         MVC   SNVIDUES,NUKEST         ESTIMATE                                 
         MVC   SNVIDUSB,NUKSUB         SUB LINE NUM                             
         MVC   SNVIDUDP,NUKDP          DAYPART                                  
         MVC   SNVIDUPG,NUKPROG        PROGRAM                                  
         TM    BYSTAT2,BYSTMGHQ    IS THIS A MIRROR GHOST UNIT                  
         BNO   *+8                                                              
         OI    SNVIDFLG,SNVIDMIR   DETAIL MATCHES TO GHOST UNIT                 
         DROP  R1,R6                                                            
*                                                                               
SM36     DS    0H                                                               
         TM    TRACEOPT,X'80'                                                   
         BZ    SM36A                                                            
         MVC   P(6),=C'IELEM '                                                  
         LH    R2,IELMLEN                                                       
         GOTO1 HEXOUT,DMCB,IELEM,P+6,(R2),=C'N'                                 
         GOTO1 REPORT                                                           
*                                                                               
SM36A    CLI   QBOOK1,C' '         TEST DEMO RUN                                
         BE    SM36X                NO                                          
         L     R6,DEMPARS+4                                                     
         LA    R4,MMW                                                           
         L     R0,DEMPARS+8                                                     
         LTR   R0,R0                                                            
         BNP   SM36X                                                            
         XC    MMW,MMW                                                          
         XC    MMFULL,MMFULL                                                    
*                                                                               
SM36B    MVC   0(3,R4),0(R6)                                                    
         CLC   0(3,R6),DCLIST      IF FIRST DEMO FOR REQ                        
         BNE   *+8                                                              
         ST    R4,MMFULL           SAVE POSITION IN MMW                         
         LA    R4,8(R4)                                                         
         A     R6,DEMPARS+12                                                    
         BCT   R0,SM36B                                                         
*                                                                               
         OC    MMW,MMW             TEST ANY DEMOS                               
         BZ    SM36X                                                            
         OC    MMFULL,MMFULL       ANY DEMO TO LOOK UP?                         
         BZ    SM36X               NO - DONE                                    
*                                                                               
         SR    R1,R1                                                            
         ICM   R1,7,IBY                                                         
         BZ    SM36D                                                            
         BCTR  R1,0                                                             
         A     R1,AFBY                                                          
         LR    R0,R1                                                            
         GOTO1 AACHDEM,DMCB,IELEM,MMW,(R0)    LOOK UP DEMO                      
         B     SM36E                                                            
SM36D    GOTO1 AACHDEM,DMCB,IELEM,MMW,0       LOOK UP DEMO                      
SM36E    L     R4,MMFULL                                                        
         CLI   NETPSW,C'Y'         IS IT NETPAK                                 
         BE    SM36F                                                            
***      MVC   SNVIDDEM,6(R4)      SPOTPAK DEMO HERE                            
***      TM    4(R4),X'40'         TEST 2-DEC FLAG                              
***      BZ    *+8                                                              
***      OI    SNVIDDEM,X'40'      SET IN 2-DEC FLAG IN 2-BYTE DEMO             
         MVC   SNVIDDM4,4(R4)      FULL 4-BYTE DEMO VALUE                       
         B     SM36X                                                            
*                                                                               
SM36F    MVC   SNVIDNDM,6(R4)      NETPAK DEMO HERE                             
*                                                                               
SM36X    DS    0H                  CHG ELEM LEN FOR NETPAK IF NEEDED            
         CLI   NETPSW,C'Y'         IS IT NETPAK                                 
         BNE   SM38                SKIP FOR SPOT                                
         CLI   SNVIDLEN,SNVIDL2Q   NEW LEN ALREADY                              
         BNL   SM38                                                             
         MVC   TMPELEM,MELEM       SAVE ELEM                                    
*                                                                               
         GOTO1 VMINIO,DMCB,('MINDEL',MINBLKD)    DELETE ELEM                    
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   MELEM,TMPELEM                                                    
         MVI   MELEM+1,SNVIDL2Q                  NEW LEN                        
         GOTO1 VMINIO,DMCB,('MINADD',MINBLKD)    ADD BACK BIG ELEM              
         CLI   MINERR,MINEDUP      DUPE ON ADD OK?                              
         BE    SM38X                                                            
         CLI   MINERR,0                                                         
         BE    SM38X                                                            
         DC    H'0'                                                             
*                                                                               
SM38     GOTO1 VMINIO,DMCB,('MINWRT',MINBLKD)                                   
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLI   SVSOON,C'Y'         SOON REQST?                                  
         BNE   SM38X                                                            
         TM    MINUPDT,MINUPFCQ+MINUPFQ  SOON, FILE WRITE THIS CALL?            
         BZ    *+12                                                             
         MVI   PSTOPT,C'M'         ABORT - INVOICE TOO BIG FOR SOON             
         B     SM41X               STRAIGHT TO CLOSE                            
*                                                                               
SM38X    TM    TRACEOPT,X'80'      PRINT OUT ELEM                               
         BZ    SM39                                                             
         MVC   P(6),=C'MMOUT '                                                  
         LLC   R2,MELEM+1                                                       
         GOTO1 HEXOUT,DMCB,MELEM,P+6,(R2),=C'N'                                 
         GOTO1 REPORT                                                           
*                                                                               
SM39     AH    R8,IELMLEN          NEXT INVOICE ITEM                            
         B     SM10                                                             
*                                                                               
SM40     DS    0H                  FINISHED WITH DETAILS                        
         CLI   MINOPEN,C'Y'                                                     
         BE    *+6                                                              
         DC    H'0'                MINIO SET SHOULD BE OPEN                     
*                                                                               
         BRAS  RE,DELDUMMY         DELETE DUMMY FE ELEM                         
*                                                                               
         TM    MINMKEY+SNVDSTAT+1-SNVKEY,X'02'    APPROVED STATUS SET?          
         BZ    *+12                               NO                            
         OI    SVSTATUS,X'02'                     YES - PRESERVE IT             
         OI    SVMMELM+SNVMMMST-SNVMMELD,X'02'    ON THE E8 AS WELL!            
*                                                                               
         BAS   RE,FIXSTAT          OR DELETE OLD STATUS - E8                    
*                                                                               
         SR    R1,R1               ADD NEW E8 ELEM                              
         ICM   R1,3,SVSTSEQ                                                     
         LA    R1,1(R1)                                                         
         STCM  R1,3,SVMMELM+SNVMMSEQ-SNVMMELD                                   
*                                                                               
         MVC   MELEM(L'SVMMELM),SVMMELM                                         
         GOTO1 VMINIO,DMCB,('MINADD',MINBLKD)                                   
         CLI   MINERR,MINEDUP      DUPE ON ADD OK?                              
         BE    SM41                                                             
         CLI   MINERR,0                                                         
         BE    SM41                                                             
         DC    H'0'                                                             
*                                                                               
SM41     DS    0H                                                               
         BAS   RE,ADDTRC           ADD MM TRACE ELEM - E9                       
         BRAS  RE,ADDRECON         ADD MATCH RECON ELEM - EA                    
*                                                                               
         LA    R3,SVMMELM          'NEW' STATUS ELEM                            
         BAS   RE,BLDPSSV          BUILD PASSIVE FOR IT - PASS R3               
*                                                                               
*                               READ FOR DELETES - THX GRANT                    
         GOTO1 DATAMGR,DMCB,(X'08',=C'DMRDHI'),=C'XSPDIR',XKEY2,XKEY3,0         
         CLC   XKEY2(L'SNVKEY),XKEY3                                            
         BE    SM41D               ALREADY ON FILE - CHECK STATUS               
*                                                                               
         TM    TRACEOPT,X'40'                                                   
         BZ    SM41C                                                            
         MVC   P(6),=C'ADDPSV'                                                  
         GOTO1 HEXOUT,DMCB,XKEY2,P+6,44,=C'N'                                   
         GOTO1 REPORT                                                           
*                                                                               
SM41C    CLI   RCWRITE,C'Y'        ADD TO FILE                                  
         BNE   SM41F                                                            
         GOTO1 DATAMGR,DMCB,=C'DMADD',=C'XSPDIR',XKEY2,XKEY2,0                  
         CLI   DMCB+8,0                                                         
         BE    SM41F                                                            
         DC    H'0'                                                             
*                                                                               
SM41D    DS    0H                      KEY ALREADY ON FILE -CHK STATUS          
         TM    XKEY3+32,X'80'          IS RECORD DELETED                        
         BNO   *+12                                                             
         NI    XKEY2+32,X'FF'-X'80'    THEN UNMARK                              
         B     SM41DA                  AND WRITE BACK                           
*                                                                               
*        CLC   XKEY2+33(1),XKEY3+33    SAME STATUS                              
         CLC   XKEY2+32(2),XKEY3+32    SAME STATUS                              
         BE    SM41F                   THEN NOTHING TO DO                       
*                                                                               
SM41DA   TM    TRACEOPT,X'40'                                                   
         BZ    SM41E                                                            
         MVC   P(6),=C'WRTPSV'                                                  
         GOTO1 HEXOUT,DMCB,XKEY2,P+6,44,=C'N'                                   
         GOTO1 REPORT                                                           
*                                                                               
SM41E    CLI   RCWRITE,C'Y'        WRITE OUT NEW STATUS                         
         BNE   SM41F                                                            
         GOTO1 DATAMGR,DMCB,=C'DMWRT',=C'XSPDIR',XKEY2,XKEY2,0                  
         CLI   DMCB+8,0                                                         
         BE    SM41F                                                            
         DC    H'0'                                                             
*                                                                               
SM41F    DS    0H                                                               
         MVC   BYTE,SVSTAT2                                                     
         NI    BYTE,X'FF'-X'C0'    MAKE SURE X'80' & X'40' ARE OFF!             
         MVC   MINMKEY+SNVDSTAT+0-SNVKEY(1),BYTE                                
         MVC   MINMKEY+SNVDSTAT+1-SNVKEY(1),SVSTATUS    SET STATUS BITS         
         MVC   MINMKEY+SNVDSTAT+2-SNVKEY(2),QAGY        SET AGENCY              
*                                                                               
SM41X    GOTO1 VMINIO,DMCB,('MINCLS',MINBLKD)                                   
         TM    TRACEOPT,X'40'                                                   
         BZ    SM42                                                             
         MVC   P(6),=C'MINCLS'                                                  
         GOTO1 HEXOUT,DMCB,MINMKEY,P+6,32,=C'N'                                 
         GOTO1 REPORT                                                           
*                                                                               
SM42     DS    0H                                                               
         CLI   PSTOPT,C'M'         MM GET OUT                                   
         BE    SMX                                                              
         C     R8,ALINV                                                         
         BNH   SM20                                                             
*                                                                               
SMX      DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*        FIXSTAT- DEAL WITH EXISTING STATUS ELEMS                               
***********************************************************************         
*                                                                               
FIXSTAT  NTR1                                                                   
         XC    SVSTSEQ,SVSTSEQ     CLEAR STATUS ELEM SEQUENCE                   
         XC    MELEM,MELEM                                                      
         LA    R3,MELEM                                                         
         USING SNVMMELD,R3                                                      
         XC    MINEKEY,MINEKEY                                                  
         MVI   MINEKEY,SNVMMELQ    X'E8'                                        
         MVI   MINFILTL,1                                                       
*                                                                               
         GOTO1 VMINIO,DMCB,('MINHI',MINBLKD)                                    
         B     FXS02D                                                           
FXS02    DS    0H                                                               
         GOTO1 VMINIO,DMCB,('MINSEQ',MINBLKD)                                   
FXS02D   DS    0H                                                               
         CLI   MINERR,0            TREAT ALL ERRORS AS END                      
         BNE   FIXSX                                                            
*                                                                               
*       IF NOT SAME REQUESTED PRD/EST THEN DELETE AND ADD NEW LATER             
*                                                                               
         CLC   SNVMMRP1(8),SVMMELM+SNVMMRP1-SNVMMELD                            
         BNE   FXS03                                                            
*                                                                               
         CLI   NETPSW,C'Y'         IS IT NETPAK                                 
         BE    FXS02F                                                           
         CLI   MKPROF+4,C'Y'       POPULATE BY BUYER                            
         BNE   FXS02E              THEN CHECK BUYER CHANGED                     
         CLC   SNVMMBYR,SVMMELM+SNVMMBYR-SNVMMELD                               
         BNE   FXS03                                                            
FXS02E   CLC   SNVMMMKT,SVMMELM+SNVMMMKT-SNVMMELD     MKT CHANGE?               
         BNE   FXS03                                                            
         B     FXS02H                                                           
*                                  ALSO DEAL WITH NETWORK CHANGE - NET          
FXS02F   CLC   SNVMMNWK,SVMMELM+SNVMMNWK-SNVMMELD                               
         BNE   FXS03                                                            
         CLC   SNVMMRPG,SVMMELM+SNVMMRPG-SNVMMELD   CHK PGROUP                  
         BNE   FXS03                                                            
*                                                                               
FXS02H   DS    0H                  IF THE SAME -                                
         CLC   SNVMMSEQ,SVSTSEQ    KEEP HIGHEST SEQ NUMBER - WHY?               
         BNH   *+10                                                             
         MVC   SVSTSEQ,SNVMMSEQ                                                 
         B     FXS06               BUT STILL GO DELETE OLD E8 ELEM              
*                                  WE'LL ADD THE NEW ONE LATER                  
FXS03    DS    0H                                                               
         TM    TRACEOPT,X'10'                                                   
         BZ    FXS04                                                            
         MVC   P(6),=C'DELOLD'                                                  
         LLC   R2,MELEM+1                                                       
         GOTO1 HEXOUT,DMCB,MELEM,P+6,(R2),=C'N'                                 
         GOTO1 REPORT                                                           
*                                                                               
FXS04    DS    0H                                                               
         BAS   RE,BLDPSSV          BUILD PASIVE POINTER IN XKEY2                
*                                                                               
         GOTO1 DATAMGR,DMCB,=C'DMREAD',=C'XSPDIR',XKEY2,XKEY2,0                 
         CLI   DMCB+8,0                      UNLESS ALREADY GONE                
         BNE   FXS06                                                            
         OI    XKEY2+SNVDSTAT-SNVKEY,X'80'   DELETE THE PASSIVE                 
         CLI   RCWRITE,C'Y'                                                     
         BNE   FXS06                                                            
         GOTO1 (RF),(R1),=C'DMWRT',=C'XSPDIR',XKEY2,XKEY2,0                     
*                                                                               
FXS06    DS    0H                                                               
         GOTO1 VMINIO,DMCB,('MINDEL',MINBLKD)                                   
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         B     FXS02               NEXT ELEM                                    
         DROP  R3                                                               
*                                                                               
FIXSX    DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*        BLDPSSV- BUILD A PASSIVE KEY BASED ON STATUS ELEM                      
*                                                                               
*               - ON INPUT R3 POINTS TO ELEM                                    
*               - KEY IS BUILT IN XKEY2                                         
*                                                                               
***********************************************************************         
*                                                                               
BLDPSSV  NTR1                                                                   
         USING SNVMMELD,R3                                                      
         XC    XKEY2,XKEY2         GET DISK ADDRESS OF INVOICE                  
         MVC   XKEY2(L'SNVKMAST),MINMKEY MASTER KEY                             
         MVC   XKEY2+L'SNVKMAST(L'SNVKMINK),=X'FFFFFFFFFFFF'                    
         GOTO1 DATAMGR,DMCB,=C'DMREAD',=C'XSPDIR',XKEY2,XKEY2,0                 
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   SVDISKAD,XKEY2+SNVDDA-SNVKEY                                     
*                                                                               
         XC    XKEY2,XKEY2                                                      
*                                                                               
         CLI   NETPSW,C'Y'         IS IT NETPAK                                 
         BE    BLDP50                                                           
*                                                                               
         LA    R2,XKEY2            SPOT PASSIVE KEY                             
         USING SNVPKEY,R2                                                       
         MVI   SNVPTYP,SNVPTYPQ    X'0E'                                        
         MVI   SNVPSUB,SNVPSUBQ    X'83'                                        
         MVC   SNVPAM,BAGYMD                                                    
         MVC   SNVPMKT,SNVMMMKT                 MARKET                          
         OC    SNVMMBYR,SNVMMBYR   IS THERE A BUYER                             
         BZ    *+16                                                             
         MVC   SNVPBYR,SNVMMBYR    THEN SET BUYER AS MARKET                     
         MVC   SNVPMKT2,SNVMMMKT   AND SET MARKET AT END                        
*                                                                               
         MVC   SNVPSTA,BMKTSTA+2                STATION                         
*                                                                               
         CLI   CANADSW,C'Y'        ONLY FOR US NOT CANADA                       
         BE    BLDP10                                                           
         CLI   SNVPSTA,X'E8'       CABLE?                                       
         BL    *+8                                                              
         NI    SNVPSTA+2,X'80'     TURN OFF NETWORK BITS                        
*                                                                               
BLDP10   MVC   SNVPMOS,MINMKEY+SNVKMOS-SNVKEY   MONTH OF SERVICE                
         MVC   SNVPCLT,BCLT                     CLIENT                          
*                                                                               
         MVC   SNVPPRD,SNVMMRP1                 PRODUCTS                        
         MVC   SNVPPRD2,SNVMMRP2                                                
*                                                                               
         MVC   SNVPEST,SNVMMRE1                 ESTIMATE                        
         MVC   SNVPEST2,SNVMMRE2                ESTIMATE                        
         MVC   SNVPINV,MINMKEY+SNVKINV-SNVKEY   INVOICE NUMBER                  
*                                                                               
         MVC   BYTE,SVSTAT2                                                     
         NI    BYTE,X'FF'-X'C0'    MAKE SURE X'80' & X'40' ARE OFF!             
         OC    SNVPSTAT+0(1),BYTE                                               
         MVC   SNVPSTAT+1(1),SVSTATUS         SET STATUS BITS                   
         B     BLDP100                                                          
         DROP  R2                                                               
*                                                                               
BLDP50   LA    R2,XKEY2            NETPAK PASSIVE KEY                           
         USING SNVNKEY,R2                                                       
         MVI   SNVNTYP,SNVNTYPQ    X'0E'                                        
         MVI   SNVNSUB,SNVNSUBQ    X'93'                                        
         MVC   SNVNAM,BAGYMD                                                    
         MVC   SNVNCLT,BCLT                     CLIENT                          
         MVC   SNVNNETW,SNVMMNWK                NETWORK                         
         MVC   SNVNMOS,MINMKEY+SNVKMOS-SNVKEY   MONTH OF SERVICE                
         MVC   SNVNPRD,SNVMMRP1                 PRODUCTS                        
         MVC   SNVNPRD2,SNVMMRP2                                                
         MVC   SNVNEST,SNVMMRE1                 ESTIMATE                        
         MVC   SNVNEST2,SNVMMRE2                ESTIMATE                        
         MVC   SNVNINV,MINMKEY+SNVKINV-SNVKEY   INVOICE NUMBER                  
         MVC   SNVNPGR,SNVMMRPG                 PRODUCT GROUP                   
*                                                                               
         MVC   BYTE,SVSTAT2                                                     
         NI    BYTE,X'FF'-X'C0'    MAKE SURE X'80' & X'40' ARE OFF!             
         OC    SNVNSTAT+0(1),BYTE                                               
         MVC   SNVNSTAT+1(1),SVSTATUS         SET STATUS BITS                   
         B     BLDP100                                                          
         DROP  R2                                                               
*                                                                               
BLDP100  DS    0H                  SET DISK ADDRESS                             
         MVC   XKEY2+SNVDDA-SNVKEY(4),SVDISKAD                                  
*                                                                               
         TM    TRACEOPT,X'01'                                                   
         BZ    BLDP140                                                          
         MVC   P(6),=C'BLDPK '                                                  
         GOTO1 HEXOUT,DMCB,XKEY2,P+6,44,=C'N'                                   
         GOTO1 REPORT                                                           
BLDP140  DS    0H                                                               
*                                                                               
BLDPX    DS    0H                                                               
         B     EXIT                                                             
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
*        ROUTINE TO ADD/UPDATE MM TRACE ELEMENT X'E9'                           
***********************************************************************         
*                                                                               
ADDTRC   NTR1                                                                   
*                                                                               
         XC    MELEM,MELEM                                                      
         LA    R3,MELEM                                                         
         USING SNVMTELD,R3                                                      
         XC    MINEKEY,MINEKEY                                                  
         MVI   MINEKEY,SNVMTELQ    X'E9'                                        
         MVI   MINFILTL,1                                                       
*                                                                               
         GOTO1 VMINIO,DMCB,('MINHI',MINBLKD)                                    
         CLI   MINERR,0            TREAT ALL ERRORS AS END                      
         BNE   AT50                                                             
*                                                                               
         GOTO1 VMINIO,DMCB,('MINDEL',MINBLKD)                                   
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
AT50     XC    MINEKEY,MINEKEY                                                  
         XC    MELEM,MELEM                                                      
         LA    R3,MELEM                                                         
         USING SNVMTELD,R3                                                      
         MVI   SNVMTEL,SNVMTELQ    X'E9'                                        
         MVI   SNVMTLEN,SNVMTLNQ                                                
         MVC   SNVMTCLT,QCLT                                                    
         MVC   SNVMTPRD,QPRD                                                    
         MVC   SNVMTEST,QEST                                                    
         MVC   SNVMTMKT,QMKT                                                    
         MVC   SNVMTSTA,QSTA                                                    
         MVC   SNVMTPR2,QENDAUTO   PIGGY PRD                                    
         MVC   SNVMTHUT,QHUT1                                                   
         MVC   SNVMTACM,TRAFMBF    ASSIGNED COMMERCIAL MATCH                    
         CLI   NETPSW,C'Y'         IS IT NETPAK                                 
         BNE   AT70                                                             
         MVC   SNVMTCAL,Q2USER+1   ANY CALENDAR OPTION                          
         MVC   SNVMTINT,SVINTOPT   INTEG OPTION SET IN BLDSTAT                  
*                                                                               
AT70     GOTO1 VMINIO,DMCB,('MINADD',MINBLKD)                                   
         CLI   MINERR,MINEDUP      DUPE ON ADD OK?                              
         BE    ATX                                                              
         CLI   MINERR,0                                                         
         BE    ATX                                                              
         DC    H'0'                                                             
*                                                                               
ATX      DS    0H                                                               
         TM    TRACEOPT,X'40'                                                   
         BZ    ATXX                                                             
         MVC   P(5),=C'TRACE'                                                   
         MVC   P+6(SNVMTLNQ),MELEM                                              
         GOTO1 REPORT                                                           
ATXX     J     EXIT                                                             
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* THIS ROUTINE INITIALIZES MINIO VARIABLES                                      
***********************************************************************         
*                                                                               
MNINIT   NTR1                                                                   
         LA    R0,MINBLOCK         CLEAR MINBLOCK                               
         LA    R1,MINBLKL                                                       
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         MVC   MINCOMF,ACOMFACS    A(COMFACS)                                   
         MVC   MINRECUP,RECUP      A(RECUP)                                     
         MVI   MINOPEN,C'N'        SET NOT OPEN                                 
         MVC   MINFIL,=CL8'XSPFIL'     FILE NAME                                
         MVC   MINDIR,=CL8'XSPDIR'     DIR NAME                                 
         MVI   MINFKLEN,L'SNVKEY   KEY LENGTH                                   
         MVI   MINEKLEN,L'SNVKMINK   ELEMENT KEY LENGTH                         
         MVI   MINEKDSP,L'SNVKMAST   DISPLACEMENT TO ELEMENT KEY                
         MVI   MINNCTL,L'SNVDSTAT  NUMBER OF CONTROL BYTES                      
         MVC   MINFRCLM,=AL2(3975) MAXIMUM RECORD LENGTH                        
         LA    RF,MBUFFR           A(MINIO BUFFER)                              
         ST    RF,MINBUFF                                                       
         MVI   MINNBUF,4           USE FOUR BUFFERS                             
*                                                                               
         LA    R1,MRECTAB          A(AREA FOR RECORD TABLE)                     
         ST    R1,MINRTAB                                                       
         MVC   MINRTABL,=Y(MRECTABL)  LENGTH OF RECORD TABLE                    
*                                                                               
         LA    RE,MELEM            A(AREA FOR ELEM OR CLUSTER)                  
         ST    RE,MINELEM                                                       
         MVC   MINMAXEL,=Y(L'MELEM)   MAX LENGTH OF ELEM OF CLUSTER             
         XC    0(L'MELEM,RE),0(RE)   CLEAR MINELEM AREA                         
         MVC   MINWRITE,RCWRITE                                                 
         MVI   MINDELSW,C'N'       BYPASS DELETES                               
*                                                                               
MINITX   B     EXIT                                                             
***********************************************************************         
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        CHECK IF GHOST UNIT MATCHES BEFORE SEEDING ACTUAL UNIT                 
***********************************************************************         
*                                                                               
MIRMATCH NTR1  BASE=*,LABEL=*                                                   
         USING IELEM,R8            R8 POINTS TO MIRROR INVOICE DETAIL           
         USING BYELEMD,R6          R6 POINTS TO BYELEM                          
*                                                                               
         LA    R0,2                FOR DUAL MIRRORS                             
         L     R2,AFBY                                                          
MMT10    C     R2,ALBY                                                          
         BH    MMTXNO              SAY NO IF CAN'T FIND GHOST                   
         CLC   BYMIRSEQ,BYMIRSEQ-BYELEM(R2)                                     
         BNE   MMT20                                                            
         CR    R2,R6               SKIP COMPARING TO ITSELF                     
         BNE   MMT30                                                            
MMT20    LA    R2,BYELEML(R2)                                                   
         B     MMT10                                                            
*                                                                               
MMT30    CLI   BYUNACH-BYELEM(R2),0     ANY UNMATCHED ON GHOST                  
         BE    MMT40                    NO UNMATCHED OK TO SEED                 
         OI    BYSTAT2,BYSTNOAQ         SET NO AFFID SEEDED                     
         B     MMTXNO                   SAY NO TO MATCH SEEDING                 
*                                                                               
MMT40    CLI   BYNSTAT,0                DUAL MIRROR?                            
         BE    MMTXYES                  NO                                      
         BCT   R0,MMT20                                                         
*                                                                               
MMTXYES  SR    RC,RC                                                            
MMTXNO   LTR   RC,RC                                                            
MMTX     J     EXIT                                                             
         DROP  R6,R8                                                            
***********************************************************************         
*        LTORG                                                                  
***********************************************************************         
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        PRINT I2 MATCHMAKER COMMENTS                                           
***********************************************************************         
*                                                                               
MMCOM    NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         USING XCOMKEY,R2                                                       
         XC    XKEY2,XKEY2                                                      
         LA    R2,XKEY2                                                         
         MVC   COMI2K,=X'0D0C'                                                  
         MVC   COMI2KAM,BAGYMD                                                  
         MVI   COMI2KTY,C'I'                                                    
         MVC   COMI2KCL,BCLT                                                    
         CLI   NETPSW,C'Y'         NETPAK?                                      
         BNE   MMC00               NO                                           
         CLC   PRD,=C'   '         HAVE 3 CHAR OVERFLOW PRODUCT?                
         BH    *+6                 YES                                          
         DC    H'0'                NO - WHAT HAPPENED TO THE PRODUCT?           
         MVC   COMI2KPR,PRD        PRODUCT                                      
         B     MMC06A                                                           
*                                                                               
MMC00    L     RF,ADCLT                         PRODUCT                         
         LA    RF,CLIST-CLTHDR(RF)                                              
         LA    R1,220              220 PRODS IN CLIST                           
MMC04    CLI   3(RF),0             EOL                                          
         BNE   *+6                                                              
         DC    H'0'                PRD NOT IN CLTHDR                            
         CLC   BPRD,3(RF)          MATCH ON HEX PRODUCT                         
         BE    MMC06                                                            
         LA    RF,4(RF)                                                         
         BCT   R1,MMC04                                                         
*                                                                               
         CLI   NETPSW,C'Y'         TEST NETPAK                                  
         BE    *+6                 ONLY CLIST2 FOR NET                          
         DC    H'0'                                                             
         L     RF,ADCLT                                                         
         LA    RF,CLIST2-CLTHDR(RF)                                             
         LA    R1,35               35 PRODUCTS IN CLIST2                        
MMC04A   CLI   3(RF),0             EOL                                          
         BNE   *+6                                                              
         DC    H'0'                PRD NOT IN CLTHDR                            
         CLC   BPRD,3(RF)          MATCH ON HEX PRODUCT                         
         BE    MMC06                                                            
         LA    RF,4(RF)                                                         
         BCT   R1,MMC04A                                                        
         DC    H'0'                                                             
*                                                                               
MMC06    MVC   COMI2KPR,0(RF)                   PRODUCT                         
*                                                                               
MMC06A   MVI   BPIGGY,0            GET CORRECT PIGGY                            
         MVC   BPIGGY,SVBPRD2      IF QPRD2=PRD                                 
         CLI   BPRD2,0                                                          
         BE    *+10                                                             
         MVC   BPIGGY,BPRD2        IF QPRD2=ALL                                 
*                                                                               
         CLI   BPIGGY,0            PIGGY                                        
         BNE   MMC06B                                                           
         CLI   NETPSW,C'Y'         NETPAK?                                      
         BNE   MMC12               NO                                           
         CLC   PRD2,=C'   '        HAVE 3 CHAR OVERFLOW PIGGY?                  
         BNH   MMC12               NO                                           
         MVC   COMI2KP2,PRD2       PIGGY                                        
         B     MMC12                                                            
*                                                                               
MMC06B   L     RF,ADCLT                         PRODUCT                         
         LA    RF,CLIST-CLTHDR(RF)                                              
         LA    R1,220              220 PRODS IN CLIST                           
MMC08    CLI   3(RF),0             EOL                                          
         BNE   *+6                                                              
         DC    H'0'                PRD NOT IN CLTHDR                            
         CLC   BPIGGY,3(RF)        MATCH ON HEX PRODUCT                         
         BE    MMC10                                                            
         LA    RF,4(RF)                                                         
         BCT   R1,MMC08                                                         
*                                                                               
         CLI   NETPSW,C'Y'         TEST NETPAK                                  
         BE    *+6                 ONLY CLIST2 FOR NET                          
         DC    H'0'                                                             
         L     RF,ADCLT                                                         
         LA    RF,CLIST2-CLTHDR(RF)                                             
         LA    R1,35               35 PRODUCTS IN CLIST2                        
MMC08A   CLI   3(RF),0             EOL                                          
         BNE   *+6                                                              
         DC    H'0'                PRD NOT IN CLTHDR                            
         CLC   BPIGGY,3(RF)        MATCH ON HEX PRODUCT                         
         BE    MMC10                                                            
         LA    RF,4(RF)                                                         
         BCT   R1,MMC08A                                                        
         DC    H'0'                                                             
*                                                                               
MMC10    MVC   COMI2KP2,0(RF)      PIGGY                                        
*                                                                               
MMC12    MVC   COMI2KES,BEST                                                    
         CLC   BEST,BESTEND                                                     
         BE    *+10                                                             
         MVC   COMI2KE2,BESTEND                                                 
*                                                                               
         MVC   COMI2KST,BMKTSTA+2                                               
         CLI   CANADSW,C'Y'        ONLY FOR US NOT CANADA                       
         BE    MMC13                                                            
         CLI   COMI2KST,X'E8'      CABLE                                        
         BL    *+8                                                              
         NI    COMI2KST+2,X'80'    TURN OFF NETWORK BITS                        
*                                                                               
MMC13    GOTO1 DATCON,DMCB,(2,BQENDP),(3,WORK)                                  
         MVC   COMI2KYM,WORK                                                    
*                                                                               
         GOTO1 DATAMGR,DMCB,=C'DMRDHI',=C'XSPDIR',XKEY2,XKEY3,0                 
         CLC   XKEY2(L'XCOMKEY),XKEY3                                           
         BNE   MMCX                NO COMMENTS                                  
*                                                                               
         GOTO1 DATAMGR,DMCB,=C'GETREC',=C'XSPFIL',XKEY3+36,            X        
               MRECTAB,DMWORK                                                   
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R2,MRECTAB          A(COMMENT REC)                               
         LA    R6,XCOMEL           FIRST ELEM                                   
MMC14    CLI   0(R6),0             EOR                                          
         BE    MMCX                                                             
         CLI   0(R6),X'05'                                                      
         BE    MMC30                                                            
MMC16    LLC   R1,1(R6)                                                         
         AR    R6,R1                                                            
         B     MMC14                                                            
*                                                                               
MMC30    SR    R2,R2                                                            
         IC    R2,1(R6)                                                         
         AHI   R2,-3               GIVES COMMENT LENGTH -1                      
         BM    MMC16                                                            
         EX    R2,*+8                                                           
         B     *+10                                                             
         MVC   P(0),2(R6)                                                       
         GOTO1 REPORT                                                           
         B     MMC16                                                            
*                                                                               
MMCX     J     EXIT                                                             
***********************************************************************         
*        LTORG                                                                  
***********************************************************************         
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        ROUTINE TO CHECK INVOICE TO MAKE SURE IT IS SET FOR MM                 
*        - IT MUST HAVE EITHER THE X'FE' ELEM OR THE X'E8' ELEM                 
***********************************************************************         
*                                                                               
CHKMMACT NTR1  BASE=*,LABEL=*                                                   
         XC    MELEM,MELEM                                                      
         LA    R3,MELEM                                                         
         XC    MINEKEY,MINEKEY                                                  
         MVI   MINEKEY,X'FE'                                                    
         MVI   MINFILTL,1                                                       
*                                                                               
         GOTO1 VMINIO,DMCB,('MINHI',MINBLKD)                                    
         CLI   MINERR,0            TREAT ALL ERRORS AS END                      
         BE    CMMXYES                                                          
*                                                                               
         XC    MELEM,MELEM                                                      
         LA    R3,MELEM                                                         
         XC    MINEKEY,MINEKEY                                                  
         MVI   MINEKEY,X'E8'                                                    
         MVI   MINFILTL,1                                                       
*                                                                               
         GOTO1 VMINIO,DMCB,('MINHI',MINBLKD)                                    
         CLI   MINERR,0            TREAT ALL ERRORS AS END                      
         BNE   CMMXNO                                                           
*                                                                               
CMMXYES  SR    RC,RC                                                            
CMMXNO   LTR   RC,RC                                                            
CMMX     J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*        ROUTINE TO DELETE X'FE' DUMMY ELEM - IF THERE                          
*        THIS ELEM IS A PLACE HOLDER SO UPDATIVE SOON I2S WILL NEVER            
*        HAVE TO MAKE A MINIO RECORD BIGGER - SO NO RECORD WILL SPLIT           
*        AS THIS WILL CAUSE SRUPD00 TO DIE                                      
***********************************************************************         
*                                                                               
DELDUMMY NTR1  BASE=*,LABEL=*                                                   
         MVI   RECONEL,C'N'        ONLY ADD RECON ELEM IF DELETE FE EL          
         XC    MELEM,MELEM                                                      
         LA    R3,MELEM                                                         
         XC    MINEKEY,MINEKEY                                                  
         MVI   MINEKEY,X'FE'                                                    
         MVI   MINFILTL,1                                                       
*                                                                               
         GOTO1 VMINIO,DMCB,('MINHI',MINBLKD)                                    
         CLI   MINERR,0            TREAT ALL ERRORS AS END                      
         BNE   DDX                                                              
*                                                                               
         GOTO1 VMINIO,DMCB,('MINDEL',MINBLKD)                                   
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         MVI   RECONEL,C'Y'        ONLY ADD RECON ELEM IF DELETE FE EL          
*                                                                               
DDX      J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*        BLDSTAT- BUILD STATUS ELEMENT                                          
*                                                                               
*          NOTE- STATUS DATA IS FOR WHOLE MATCH, NOT JUST ONE INVOICE           
*                                                                               
***********************************************************************         
*                                                                               
BLDSTAT  NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         XC    SVSTATUS,SVSTATUS                                                
         XC    SVINTOPT,SVINTOPT                                                
         XC    SVMMELM,SVMMELM                                                  
         LA    R3,SVMMELM                                                       
         USING SNVMMELD,R3                                                      
         MVI   SNVMMEL,SNVMMELQ                                                 
         MVI   SNVMMLEN,SNVMMLNQ                                                
*                                                                               
         CLC   BQENDP,=X'D501'     REQUEST END ON OR AFTER AUG01/06?            
         BL    *+8                 NO - USE THE OLD LENGTH                      
         MVI   SNVMMLEN,SNVMML2Q                                                
         CLI   NETPSW,C'Y'         NETPAK?                                      
         BNE   BLDS00              NO                                           
         CLC   PRD,=C'   '         HAVE 3 CHAR OVERFLOW PRODUCT?                
         BH    *+6                 YES                                          
         DC    H'0'                NO - WHAT HAPPENED TO THE PRODUCT?           
         MVC   SNVMMRP1,PRD        PRODUCT                                      
         CLI   BPRD,X'FF'          POL PRODUCT?                                 
         BNE   BLDS04A             NO                                           
         MVC   SNVMMRP1,=C'POL'    YES - MOVE POL INTO E8 ELEM                  
         B     BLDS04A                                                          
*                                                                               
BLDS00   L     RF,ADCLT                         PRODUCT                         
         LA    RF,CLIST-CLTHDR(RF)                                              
         LA    R1,220              220 PRDS IN CLIST                            
BLDS02   CLI   3(RF),0             EOL                                          
         BNE   *+6                                                              
         DC    H'0'                PRD NOT IN CLTHDR                            
         CLC   BPRD,3(RF)          MATCH ON HEX PRODUCT                         
         BE    BLDS04                                                           
         LA    RF,4(RF)                                                         
         BCT   R1,BLDS02                                                        
         DC    H'0'                                                             
*                                                                               
BLDS04   MVC   SNVMMRP1,0(RF)                   PRODUCT                         
*                                                                               
BLDS04A  MVI   BPIGGY,0            GET CORRECT PIGGY                            
         MVC   BPIGGY,SVBPRD2      IF QPRD2=PRD                                 
         CLI   BPRD2,0                                                          
         BE    *+10                                                             
         MVC   BPIGGY,BPRD2        IF QPRD2=ALL                                 
*                                                                               
         CLI   BPIGGY,0            PIGGY                                        
         BNE   BLDS04B                                                          
         CLI   NETPSW,C'Y'         NETPAK?                                      
         BNE   BLDS10              NO                                           
         CLC   PRD2,=C'   '        HAVE 3 CHAR OVERFLOW PIGGY?                  
         BNH   BLDS10              NO                                           
         MVC   SNVMMRP2,PRD2       PIGGY                                        
         B     BLDS10                                                           
*                                                                               
BLDS04B  L     RF,ADCLT                         PRODUCT                         
         LA    RF,CLIST-CLTHDR(RF)                                              
         LA    R1,220              220 PRDS IN CLIST                            
BLDS06   CLI   3(RF),0             EOL                                          
         BNE   *+6                                                              
         DC    H'0'                PRD NOT IN CLTHDR                            
         CLC   BPIGGY,3(RF)        MATCH ON HEX PRODUCT                         
         BE    BLDS08                                                           
         LA    RF,4(RF)                                                         
         BCT   R1,BLDS06                                                        
*                                                                               
         CLI   NETPSW,C'Y'         TEST NETPAK                                  
         BE    *+6                 ONLY CLIST2 FOR NET                          
         DC    H'0'                                                             
         L     RF,ADCLT                                                         
         LA    RF,CLIST2-CLTHDR(RF)                                             
         LA    R1,35               35 PRODUCTS IN CLIST2                        
BLDS06A  CLI   3(RF),0             EOL                                          
         BNE   *+6                                                              
         DC    H'0'                PRD NOT IN CLTHDR                            
         CLC   BPIGGY,3(RF)        MATCH ON HEX PRODUCT                         
         BE    BLDS08                                                           
         LA    RF,4(RF)                                                         
         BCT   R1,BLDS06A                                                       
         DC    H'0'                                                             
*                                                                               
BLDS08   MVC   SNVMMRP2,0(RF)                   PRODUCT                         
*                                                                               
BLDS10   MVC   SNVMMRE1,BEST                                                    
         MVC   SNVMMRE2,BESTEND                                                 
         MVC   SNVMMMKT,BMKT                                                    
         MVC   SNVMMDAT,TODAYP                                                  
         MVC   SNVMMBK,QBOOK1                                                   
*                                                                               
BLDS11   CLI   MATSTAT,0           SET STATUS BITS                              
         BNE   BLDS12                                                           
         TM    UPDERROR,UPDERBIG   ERROR UPDATING BUYS                          
         BO    BLDS12              THEN DON'T SET AS MATCHED                    
         OI    SNVMMMST,SNVMMSCQ   MATCHED                                      
*                                                                               
BLDS12   MVC   BYTE,Q2USER+1       CALENDAR MONTH                               
         CLI   BYTE,C' '                                                        
         BH    *+10                                                             
         MVC   BYTE,PROGPROF+9     THEN I2 PROFILE                              
         CLI   BYTE,C'C'           TEST CALENDAR MONTH                          
         BNE   *+8                                                              
         OI    SNVMMMST,SNVMMCMQ   YES                                          
*                                                                               
         CLI   MKUNWIRE,C'N'       DO ALL BUYERS DO UNWIRED?                    
         BNE   BLDS13              IF YES NOTHING TO SET                        
         L     RF,ADEST                                                         
         CLI   EPROF-ESTHDR(RF),C'U'    UNWIRED ESTIMATE                        
         BNE   *+8                                                              
         OI    SNVMMMST,SNVMMUWQ   YES                                          
*                                                                               
BLDS13   MVI   SNVMMGN,C'G'        GROSS                                        
         MVC   SNVMMTOT,IGRS                                                    
         MVC   SNVMMSPS,ISPTS                                                   
         CLI   NETINV,C'Y'                                                      
         BNE   *+14                                                             
         MVI   SNVMMGN,C'N'        OR NET                                       
         MVC   SNVMMTOT,INET                                                    
*                                                                               
         THMS  DDSTIME=YES                                                      
         ST    R1,WORK                                                          
         ST    R0,WORK+4                                                        
         AP    WORK(4),WORK+4(4)   ADD DDS TIME TO GET MILITARY                 
         L     R1,WORK                                                          
         SRL   R1,12               GET RID OF SECONDS AND SIGN                  
         STCM  R1,3,SNVMMTIM                                                    
*                                                                               
         XC    SNVMMNWK,SNVMMNWK   FOR SPOTPAK - MUST BE BLANK!!!               
*                                                                               
         CLI   SNVMMLEN,SNVMML2Q   HAVE NEW LENGTH FOR MM ERRORS?               
         BNE   BLDS14              NO, DO NOT SET THE ERRORS!                   
*                                                                               
         CLI   FILMDSCP,C'Y'       FILM ANALYSIS ERRORS?                        
         BNE   *+8                 NO                                           
         OI    SNVMMERR,SNVMMFLM                                                
*                                                                               
         CLI   HVROTERR,C'Y'       HORIZONTAL/VERTICAL ERRORS?                  
         BNE   *+8                 NO                                           
         OI    SNVMMERR,SNVMMHVE                                                
*                                                                               
         CLI   SECSEPER,C'Y'       SECONDARY SEPARATION ERRORS?                 
         BNE   *+8                 NO                                           
         OI    SNVMMERR,SNVMMSSE                                                
*                                                                               
BLDS14   CLI   NETPSW,C'Y'         IS IT NETPAK                                 
         BE    BLDS15              DO NETPAK STUFF                              
*                                                                               
         XC    SNVMMBYR,SNVMMBYR   CLEAR BUYER FIELD                            
         CLI   MKPROF+4,C'Y'       POPULATE BY BUYER - REDUNDANT CHK            
         BNE   BLDS20              NO THEN DONE                                 
         OC    SVBYRSEQ,SVBYRSEQ   MAKE SURE HAVE SOMETHING HERE!               
         BNZ   *+6                                                              
         DC    H'0'                                                             
         MVC   SNVMMBYR,SVBYRSEQ                                                
         B     BLDS20                                                           
*                                                                               
BLDS15   MVC   SNVMMNWK,BIGSTA     FOR NETPAK - NETWORK                         
         CLI   QPGR,C' '           PRODUCT GROUP REQUEST (NETPAK)               
         BE    BLDS16                                                           
         MVC   SNVMMRPG,QPGR       THEN USE IT                                  
         XC    SNVMMRP2,SNVMMRP2   CLEAR PIGGY                                  
         MVC   SNVMMRP1,PGR1+1  AND SET PRD1 TO GROUP#                          
         CLC   PGR1LEN,PGR2LEN     LENS = IS END                                
         BE    BLDS16                                                           
         MVC   SNVMMRP1,PGR2+1  GET MOST DETAILED #                             
         CLC   PGR2LEN,PGR3LEN     LENS = IS END                                
         BE    BLDS16                                                           
         MVC   SNVMMRP1,PGR3+1  GET MOST DETAILED #                             
*                                                                               
BLDS16   CLI   I2IPROF,C'Y'        MATCH ON INTEGRATION                         
         BNE   *+8                                                              
         MVI   SVINTOPT,C'I'       SET TO I                                     
         CLI   I2IPROF+1,C'Y'      MATCH ON INTEGRATION ONLY                    
         BNE   *+8                                                              
         MVI   SVINTOPT,C'O'       SET TO O                                     
         CLI   Q2USER+2,C' '       PROFILE OVERRIDDEN                           
         BNH   *+10                                                             
         MVC   SVINTOPT,Q2USER+2   ANY INTEGRATION OPTION                       
         CLI   SVINTOPT,C'I'       INT+TIME RUN                                 
         BNE   *+8                                                              
         OI    SNVMMMST,SNVMMITQ   YES                                          
         CLI   SVINTOPT,C'O'       INT ONLY RUN                                 
         BNE   *+8                                                              
         OI    SNVMMMST,SNVMMIOQ   YES                                          
*                                                                               
BLDS20   TM    TRACEOPT,X'20'                                                   
         BZ    BLDS50                                                           
         MVC   P(6),=C'NEW ST'                                                  
         LLC   R2,SVMMELM+1                                                     
         GOTO1 HEXOUT,DMCB,SVMMELM,P+6,(R2),=C'N'                               
         GOTO1 REPORT                                                           
*                                                                               
BLDS50   MVC   SVSTATUS,SNVMMMST   SAVE STATUS FOR KEYS                         
         MVC   SVSTAT2,SNVMMERR                                                 
*                                                                               
BLDSX    J     EXIT                                                             
         DROP  R3                                                               
***********************************************************************         
*        LTORG                                                                  
***********************************************************************         
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        ROUTINE TO ADD/UPDATE MATCH RECON ELEM X'EA'                           
***********************************************************************         
*                                                                               
ADDRECON NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         XC    MATPCT,MATPCT                                                    
         SR    R0,R0                                                            
         L     R1,MATSPTS          MATCHED SPOTS*2                              
         MHI   R1,2                                                             
         LR    R3,R1               DIVIDED BY MATCHED*2+RNO+ONR                 
         A     R3,ONRSPTS                                                       
         A     R3,RNOSPTS                                                       
         MHI   R1,100                                                           
         C     R3,=F'0'                                                         
         BNH   AR05                LET'S SKIP THE DIVIDE BY ZERO                
         DR    R0,R3                                                            
         ST    R1,MATPCT                                                        
*                                                                               
AR05     XC    MELEM,MELEM                                                      
         LA    R3,MELEM                                                         
         USING SNVMRELD,R3                                                      
         XC    MINEKEY,MINEKEY                                                  
         MVI   MINEKEY,SNVMRELQ    X'EA'                                        
         MVI   MINFILTL,1                                                       
*                                                                               
         GOTO1 VMINIO,DMCB,('MINHI',MINBLKD)                                    
         CLI   MINERR,0            TREAT ALL ERRORS AS END                      
         BNE   AR50                NO ERRS= DELETE AND RE-ADD                   
*                                                                               
         CLI   SNVMRLPC,100        IF LAST MATCH=100%                           
         BNE   AR10                                                             
         CLI   MATSTAT,0           AND STILL SUCCESSFULLY MATCHED               
         BE    ARXX                NOTHING TO DO                                
*                                                                               
AR10     MVC   SNVMRLDT,TODAYB     LAST MATCH DATE                              
         MVI   SNVMRLPC,100        LAST MATCH PERCENTAGE                        
         CLI   MATSTAT,0           =0 MEANS SUCCESSFUL MATCH                    
         BE    *+10                                                             
         MVC   SNVMRLPC,MATPCT+3                                                
*                                                                               
         SR    R1,R1               NUMBER OF I2S AND SPOTS                      
         ICM   R1,3,SNVMRI2S                                                    
         LA    R1,1(R1)                                                         
         STCM  R1,3,SNVMRI2S                                                    
         MVC   SNVMRMAT,MATSPTS+2                                               
         MVC   SNVMRONR,ONRSPTS+2                                               
         MVC   SNVMRRNO,RNOSPTS+2                                               
*                                                                               
         GOTO1 VMINIO,DMCB,('MINWRT',MINBLKD)                                   
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         B     ARX                                                              
*                                                                               
AR50     XC    MINEKEY,MINEKEY                                                  
         XC    MELEM,MELEM                                                      
         LA    R3,MELEM                                                         
         USING SNVMRELD,R3                                                      
         MVI   SNVMREL,SNVMRELQ    X'EA'                                        
         MVI   SNVMRLEN,SNVMRLNQ                                                
         MVC   SNVMRFDT,TODAYB     FIRST MATCH DATE                             
         MVI   SNVMRFPC,100        FIRST MATCH PERCENTAGE                       
         CLI   MATSTAT,0           =0 MEANS 100%                                
         BE    *+10                                                             
         MVC   SNVMRFPC,MATPCT+3                                                
*                                                                               
         SR    R1,R1               NUMBER OF I2S AND SPOTS                      
         ICM   R1,3,SNVMRI2S                                                    
         LA    R1,1(R1)                                                         
         STCM  R1,3,SNVMRI2S                                                    
         MVC   SNVMRMAT,MATSPTS+2                                               
         MVC   SNVMRONR,ONRSPTS+2                                               
         MVC   SNVMRRNO,RNOSPTS+2                                               
*                                                                               
AR100    GOTO1 VMINIO,DMCB,('MINADD',MINBLKD)                                   
         CLI   MINERR,MINEDUP      DUPE ON ADD OK?                              
         BE    ARX                                                              
         CLI   MINERR,0                                                         
         BE    ARX                                                              
         DC    H'0'                                                             
*                                                                               
ARX      DS    0H                                                               
         TM    TRACEOPT,X'40'                                                   
         BZ    ARXX                                                             
         MVC   P(5),=C'RECON'                                                   
         LLC   R2,MELEM+1                                                       
         GOTO1 HEXOUT,DMCB,MELEM,P+6,(R2),=C'N'                                 
         GOTO1 REPORT                                                           
ARXX     J     EXIT                                                             
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
*        LTORG                                                                  
***********************************************************************         
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        GETBYR - READ STATUS RECORD TO GET GETBUBL NAME                        
*                 THEN USE GETBUBL NAME TO GET BUYER RECORD SEQUENCE #          
***********************************************************************         
*                                                                               
GETBYR   NTR1  BASE=*,LABEL=*                                                   
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING STATKEY,R6                                                       
         MVC   STKTYPE,=X'0D71'                                                 
         MVC   STKAGMD,BAGYMD                                                   
         MVC   STKCLT,BCLT                                                      
         MVC   STKPRD,BPRD                                                      
         MVC   STKEST,BEST                                                      
         MVC   STKMKT,BMKT                                                      
         CLI   STPROF+0,C'P'       TEST DATA BY PRODUCT                         
         BE    *+8                                                              
         MVI   STKPRD,0                                                         
         CLI   STPROF+2,C'E'       TEST DATA BY ESTIMATE                        
         BE    *+8                                                              
         MVI   STKEST,0                                                         
         CLC   BEST,STKEST         TEST AT SAME ESTIMATE LEVELS                 
         BE    GETBYR6             IF YES, GET NAME                             
         CLI   BEST,0              TEST REQUEST ACROSS ESTIMATES                
         BE    GETBYRNO            YES - CAN'T GET A BUYER                      
*                                                                               
GETBYR6  DS    0H                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE     NOT FOUND ABORT                              
         BNE   GETBYRNO                                                         
*                                                                               
         L     R6,ADBUY                                                         
         ST    R6,AREC                                                          
         GOTO1 GET                 READ INTO BUYREC AREA                        
*                                                                               
         USING STATD,R6                                                         
         CLI   BPBUYER,C' '                                                     
         BNH   *+10                                                             
         MVC   SVBUBL,BPBUYER      OK GOT GETBUBL NAME                          
*                                                                               
         XC    KEY,KEY             NOW LOOK UP BUYER RECORD                     
         LA    R6,KEY                                                           
         USING BYRRECD,R6                                                       
         MVC   BYRPTYP3,=X'0DE4'                                                
         MVC   BYRPAGY3,BAGYMD                                                  
         NI    BYRPAGY3,X'F0'      NO MEDIA!                                    
         MVC   BYRPBUBL,SVBUBL                                                  
         GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE     NOT FOUND ABORT                              
         BNE   GETBYRNO                                                         
*                                                                               
         L     R6,ADBUY                                                         
         ST    R6,AREC                                                          
         GOTO1 GET                 READ INTO BUYREC AREA                        
         LA    R6,24(R6)                                                        
GETBYR10 CLI   0(R6),0                                                          
         BE    GETBYRNO                                                         
         CLI   0(R6),BYRBBLQ       X'30'                                        
         BE    GETBYR20                                                         
         LLC   R1,1(R6)                                                         
         AR    R6,R1                                                            
         B     GETBYR10                                                         
         USING BYRBBLD,R6                                                       
GETBYR20 MVC   SVBYRSEQ,BYRBBLCD   SAVE SEQ NUMBER                              
*                                                                               
GETBYRYS SR    RC,RC                                                            
GETBYRNO LTR   RC,RC                                                            
GETBYRX  J     EXIT                                                             
         DROP  R6                                                               
***********************************************************************         
*        LTORG                                                                  
***********************************************************************         
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        ORBFIX - GET CORRECT SPOT DATE FROM MASTER                             
*        ON ENTRY R6 POINTS TO SLAVE BUY ENTRY                                  
*        RETURN ORBSDT AND ORBSPT                                               
***********************************************************************         
*                                                                               
         USING BYELEMD,R6                                                       
ORBFIX   NTR1  BASE=*,LABEL=*                                                   
         XC    ORBSDT,ORBSDT                                                    
         XC    ORBSPT,ORBSPT                                                    
*                                                                               
         L     R5,AFBY             SEARCH FOR MASTER                            
         B     *+8                                                              
PS6B     DS    0H                                                               
ORB10    LA    R5,BYELEML(R5)                                                   
         C     R5,ALBY                                                          
         BL    *+6                                                              
         DC    H'0'                MASTER MUST BE THERE                         
         BAS   RE,ORBMCHK                                                       
         BNE   ORB10                                                            
         MVC   ORBSDT,BYSDT-BYELEM(R5)                                          
         MVC   ORBSPT,BYSPT-BYELEM(R5)                                          
         B     ORBX                                                             
*                                                                               
ORBMCHK  DS    0H                                                               
         TM    BYSTAT-BYELEM(R5),X'10'                                          
         BNZ   *+8                                                              
         LTR   RE,RE               NOT A MASTER                                 
         BR    RE                                                               
         CLC   BYEST(3),BYEST-BYELEM(R5)                                        
         BNER  RE                                                               
         CLC   BYORBNO,BYORBNO-BYELEM(R5)                                       
         BNER  RE                                                               
         CLC   BYSPT,BYSPT-BYELEM(R5)                                           
         BR    RE                                                               
*                                                                               
ORBX     J     EXIT                                                             
         DROP  R6                                                               
***********************************************************************         
*        LTORG                                                                  
***********************************************************************         
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        LOCAL WORKING STORAGE                                                  
***********************************************************************         
WORKD    DSECT                                                                  
MATPCT   DS    XL4                                                              
VMINIO   DS    V                                                                
VPERVERT DS    V                                                                
SAVSTRT  DS    CL6                                                              
TRACEOPT DS    CL1                                                              
SVSTSEQ  DS    XL2                                                              
BPIGGY   DS    XL1                                                              
SVSOON   DS    CL1                                                              
ORBSDT   DS    XL2                                                              
ORBSPT   DS    XL1                                                              
SVINTOPT DS    XL1                                                              
SVSTATUS DS    XL1                                                              
SVSTAT2  DS    XL1                                                              
RECONEL  DS    CL1                                                              
SVBYRSEQ DS    XL2                                                              
SVBUBL   DS    CL10                                                             
*                                                                               
TMPELEM  DS    XL60                                                             
*                                                                               
SVDISKAD DS    XL4                                                              
LASTINV  DS    XL2                                                              
SVMMELM  DS    XL(SNVMML2Q+1)        SAVED STATUS ELEM                          
XKEY2    DS    XL64                                                             
XKEY3    DS    XL64                                                             
*                                                                               
MMW      DS    CL200                                                            
MMFULL   DS    F                                                                
*                                                                               
         DS    0D                                                               
         DC    CL8'*MELEM*'                                                     
MELEM    DS    XL256               MINIO ELEMENT                                
         DS    0D                                                               
         DC    CL8'*MBLOCK*'                                                    
MINBLOCK DS    CL(MINBLKL)                                                      
*                                                                               
MRECTABL EQU   2400                                                             
         DS    0D                                                               
         DC    CL8'*MRTAB*'                                                     
MRECTAB  DS    XL(MRECTABL)                                                     
*                                                                               
         DS    0D                                                               
         DC    CL8'*MBUFF*'                                                     
MBUFFR   DS    XL(3975*4)          ROOM FOR 4 BUFFERS                           
*                                                                               
WORKDL   EQU   *-WORKD                                                          
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE SPREPWORKD                                                     
       ++INCLUDE SPREPI2WKN                                                     
       ++INCLUDE DDMINBLK                                                       
       ++INCLUDE SPGENSNV                                                       
       ++INCLUDE SPGENCLT                                                       
       ++INCLUDE SPGENEST                                                       
       ++INCLUDE SPGENSTAT                                                      
       ++INCLUDE SPGENBYR                                                       
       ++INCLUDE SPGENXCOM                                                      
       ++INCLUDE NEGENUNIT                                                      
       ++INCLUDE DDBUFFD                                                        
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE DDMASTD                                                        
         PRINT ON                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'027SPREPI2MM 05/19/20'                                      
         END                                                                    
