*          DATA SET ACPTACNV   AT LEVEL 023 AS OF 08/10/01                      
*CATALP ACPTACNV                                                                
PTACNV TITLE 'CONVERT PTA ELEMENTS TO OLD ELEMENTS'                             
*                                                                               
***********************************************************************         
* THIS MODULE WILL CONVERT PTA ELEMENTS TO BND ELEMENTS IN PLACE      *         
* PLIST FOR PTACNV                                                    *         
* P1 BYTE0    =X'80' FOR OLD FILE STRUCTURE                           *         
* P1 BYTES1-3 =A(TRANSACTION RECORD)                                  *         
* P2=A(COMFACS)                                                       *         
***********************************************************************         
ACPTACNV CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 CNVWRKX-CNVWRKD,**CNVP**,CLEAR=YES,RR=RE                         
         USING CNVWRKD,RC          RC=A(LOCAL W/S)                              
         ST    RE,CNVRELO          SAVE RELO FACTOR                             
         LR    R8,R1               R8=A(PARAMETER BLOCK)                        
         USING CNVIBLKD,R8                                                      
         SR    RF,RF                                                            
         ICM   RF,15,CNVIACMF      RF=A(COMFACS)                                
         BNZ   *+6                                                              
         DC    H'0'                A(COMFACS) MISSING IN PARM BLOCK             
         USING COMFACSD,RF                                                      
         MVC   CNVHELL,CHELLO                                                   
         MVC   CNVDATCN,CDATCON                                                 
         DROP  RF                                                               
*                                                                               
         MVC   CNVFILE,=C'ACCMST  '                                             
         TM    CNVISTAT,CNVIOLDQ   OLD FILE STRUCTURE?                          
         BNO   *+10                NO                                           
         MVC   CNVFILE,=C'ACCFIL  '                                             
*                                                                               
         EJECT                                                                  
***********************************************************************         
* PROCESS THE TRANSACTION                                             *         
***********************************************************************         
*                                                                               
         ICM   R2,15,CNVIATRN      R2=A(TRANSACTION RECORD)                     
         USING TRNRECD,R2                                                       
         LA    R3,TRNRFST          R3=A(FIRST ELEMENT ON NEW FILE)              
         TM    CNVISTAT,CNVIOLDQ   OLD FILE STRUCTURE?                          
         BNO   *+10                                                             
         LA    R3,ACCORFST                                                      
         AR    R3,R2               R3=A(FIRST ELEMENT ON OLD FILE)              
         CLI   0(R3),TRNELQ        TRANSACTION ELEMENT?                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         BAS   RE,CLEARBUF         CLEAR BUFFER OF PTA ELEMENTS                 
*                                                                               
         BAS   RE,PUTPTA           PUT PTA ELEMENTS TO BUFFER                   
*                                                                               
         BAS   RE,DELPTA           REMOVE PTA ELEMENTS FROM TRAN                
*                                                                               
         LA    R3,PTABUFF          CONVERT PTA ELS AND WRITE TO TRAN            
         XR    R5,R5                                                            
CNV50    CLI   0(R3),0             IS AN ELEMENT PRESENT                        
         BE    CNVX                NO, DONE                                     
         BAS   RE,CNVPTA           CONVERT PTA TO OLD STYLE ELEMENTS            
         GOTO1 CNVHELL,CNVPARM,(C'P',CNVFILE),CNVIATRN,CNVELEM,0                
         IC    R5,1(R3)            BUMP THROUGH TABLE                           
         LA    R3,0(R5,R3)                                                      
         B     CNV50                                                            
CNVX     XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
* CLEAR PTABUFF                                                       *         
***********************************************************************         
CLEARBUF NTR1                                                                   
         LA    RE,PTABUFF                                                       
         LH    RF,=Y(L'PTABUFF)                                                 
         XR    R1,R1                                                            
         MVCL  RE,R0                                                            
         B     CNVX                                                             
         EJECT                                                                  
***********************************************************************         
* WRITE THE PTA ELEMENTS OF THE TRANSACTION AT 0(R3) TO PTABUFF       *         
***********************************************************************         
         USING PTAELD,R3                                                        
PUTPTA   NTR1                                                                   
         LA    R4,PTABUFF                                                       
         XR    R1,R1                                                            
*                                                                               
PUTP30   CLI   0(R3),0             END OF RECORD                                
         BE    PUTPX               YES                                          
*                                                                               
         IC    R1,1(R3)                                                         
*                                                                               
         CLI   0(R3),PTAELQ        IS THIS A PTA ELEMENT                        
         BNE   PUTP50              NO, GET NEXT                                 
*                                                                               
         BAS   RE,CHKTYPE          IS IS THE TYPE I PROCESS HERE                
         BNE   PUTP50              N, GET NEXT                                  
*     - - - - - - - - - - - -                                                   
         BAS   RE,CHKNET           IF NET IS LARGE, HALF PTAEL, PUT #           
         LH    R2,CNVPTACT         # OF TIMES TO PUT PTAEL                      
*                                                                               
PUTP40   BCTR  R1,0                COPY PTAEL TO BUFFER                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(R3)                                                    
*                                                                               
         LA    R1,1(R1)                                                         
         LA    R4,0(R1,R4)         BUMP PTABUFF POINTER                         
*                                                                               
         BCT   R2,PUTP40                                                        
*     - - - - - - - - - - - -                                                   
*                                                                               
PUTP50   LA    R3,0(R1,R3)         GET NEXT ELEMENT                             
         B     PUTP30                                                           
*                                                                               
PUTPX    B     CNVX                                                             
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* REMOVE THE PTA ELEMENTS FROM THE TRANSACTION                        *         
* ON ENTR, R3 IS A(FIRST ELEMENT ON THE TRANSACTION)                            
***********************************************************************         
DELPTA   NTR1                                                                   
         XR    R1,R1                                                            
DELP10   CLI   0(R3),0             END OF RECORD?                               
         BE    DELP100                                                          
         CLI   0(R3),PTAELQ        IS THIS  A PTA ELEMENT                       
         BNE   DELP50                                                           
*                                                                               
         BAS   RE,CHKTYPE          CHECK PTA TYPE                               
         BNE   DELP50              NOT PROCESSED HERE                           
*                                                                               
         USING PTAELD,R3                                                        
DELP30   MVI   PTAEL,X'FF'         DELETE THIS ELEMENT                          
*                                                                               
DELP50   IC    R1,1(R3)            GET NEXT ELEMENT                             
         LA    R3,0(R1,R3)                                                      
         B     DELP10                                                           
*                                                                               
DELP100  GOTO1 CNVHELL,CNVPARM,(C'D',CNVFILE),(X'FF',CNVIATRN),0,0              
         B     CNVX                                                             
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* CONVERT THE PTA ELEMENT IN 0(R3) TO AN ELEMENT IN CNVELEM           *         
***********************************************************************         
*                                                                               
         USING PTAEL,R3                                                         
         USING BNDELD,R4                                                        
CNVPTA   NTR1                                                                   
         XC    CNVELEM,CNVELEM     SPACE TO BUILD THE ELEMENT                   
         LA    R4,CNVELEM                                                       
*                                                                               
         CLI   PTATYPE,PTATRAL     IS THIS BILLING                              
         BNE   CNVP100             NO                                           
*                                                                               
         MVI   BNDEL,BNDELQ                                                     
         MVI   BNDLN,BNDLN2Q                                                    
*                                                                               
         MVI   BNDBNO,C' '         SET ALLOCATED                                
         MVC   BNDBNO+1(L'BNDBNO-1),BNDBNO                                      
         TM    PTASTAT1,PTASPEND   PENDING BILLING IS ALLOCATED TO US           
         BO    *+10                                                             
         MVC   BNDBNO,PTARBLNO     SET BILLED BNDEL                             
*                                                                               
         ZAP   DUB,PTANET          AMOUNT                                       
         CVB   R1,DUB                                                           
         STCM  R1,15,BNDAMNT                                                    
*                                                                               
         ZAP   DUB,PTACDSC         CASH DISCOUNT                                
         CVB   R1,DUB                                                           
         STCM  R1,15,BNDCSD                                                     
*                                                                               
         LH    R1,PTAHOURS         HOURS                                        
         STCM  R1,15,BNDHRS                                                     
*                                                                               
         ZAP   BNDCMP,PTARCORT     COMMISSION RATE                              
         OI    BNDCMST,BNDCPACK                                                 
*                                                                               
         MVC   BNDRUN,PTARDATE     SAVE RUN DATE                                
         MVC   BNDDTE,PTARBLDT     SAVE BILL DATE                               
         MVC   BNDMOA,PTAMOA                                                    
         MVC   BNDFORM,PTARFORM    FORMAT/GROUP LEVEL                           
         MVC   BNDTYPE,PTARTYPE    WORKCODE TYPE                                
         MVC   BNDVATC,PTARGSTC    GST CODE                                     
         MVC   BNDPSTC,PTARPSTC    PST CODE                                     
         MVC   BNDPRVC,PTARPRVC    PROVINCE CODE                                
         B     CNVPX                                                            
*                                                                               
CNVP100  CLI   PTATYPE,PTATWOF     IS THIS A WRITE OFF                          
         BE    CNVP110                                                          
         CLI   PTATYPE,PTATWOFR    SET WRITE-OFF RECOVERY                       
         BNE   CNVP300             NO, NEXT TYPE                                
*                                                                               
CNVP110  MVI   BNDEL,BNDELQ                                                     
         MVI   BNDLN,BNDLN2Q                                                    
*                                                                               
         ZAP   DUB,PTANET          AMOUNT                                       
         CVB   R1,DUB                                                           
         STCM  R1,15,BNDAMNT                                                    
*                                                                               
         ZAP   DUB,PTACDSC         CASH DISCOUNT                                
         CVB   R1,DUB                                                           
         STCM  R1,15,BNDCSD                                                     
*                                                                               
         LH    R1,PTAHOURS         HOURS                                        
         STCM  R1,15,BNDHRS                                                     
*                                                                               
         MVI   BNDBNO,C'W'         SAVE WRITEOFF NUMBER                         
         MVI   BNDBNO+1,C'O'       ASSUME WRITE OFF                             
         CLI   PTATYPE,PTATWOFR    IS IT A RECOVERY                             
         BNE   *+8                 NO                                           
         MVI   BNDBNO+1,C'R'       START WITH WR                                
         MVC   DUB(2),PTASEQN      NOMBER IS STORED AS ITS COMPLEMENT           
         LH    RF,DUB                                                           
         LCR   RF,RF                                                            
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  BNDBNO+2(4),DUB                                                  
*                                                                               
         MVC   BNDWOT,PTAWWOT      WRITE-OFF TYPE                               
         MVC   BNDDTE,PTADATE      WRITE OFF DATE                               
         MVC   BNDMOA,PTAMOA       MOA       DATE                               
*                                                                               
*NVP200  CLI   PTATYPE,PTATTRFF    TRANSFER FROM                                
*        BNE   CNVP300                                                          
*        USING PXDELD,R4                                                        
*        MVI   PXDEL,PXDELQ                                                     
*        MVI   PXDLN,PXDLNQ                                                     
*        MVI   PXDTYPE,PXDTFROM                                                 
*        GOTO1 CNVDATCN,CNVPARM,(2,PTADATE),(1,PXDDATE)                         
*        L     R1,CNVIATRN                                                      
*        MVC   PXDFRTO(3),0(R1) GET COMPANY UNIT LEDGER FROM TRAN KEY           
*        MVC   PXDFRTOA,PTAFJOB                                                 
*        B     CNVPX                                                            
*                                                                               
CNVP300  CLI   PTATYPE,PTATTRFT    TRANSFER TO                                  
         BNE   CNVPX                                                            
*                                                                               
         USING PXDELD,R4                                                        
         MVI   PXDEL,PXDELQ                                                     
         MVI   PXDLN,PXDLNQ                                                     
         MVI   PXDTYPE,PXDTFROM                                                 
         GOTO1 CNVDATCN,CNVPARM,(2,PTADATE),(1,PXDDATE)                         
         L     R1,CNVIATRN                                                      
         MVC   PXDFRTO(3),0(R1) GET COMPANY UNIT LEDGER FROM TRAN KEY           
         MVC   PXDFRTOA,PTATJOB                                                 
CNVPX    B     CNVX                                                             
*                                                                               
         DROP  R3,R4                                                            
         EJECT                                                                  
***************************************** - - - - - - - - - - - *****           
* VERIFY THAT THE NET AMOUNT IN THE PTA ELEMENT CAN FIT INTO                    
* A REGISTER.  IF NOT CUT THE AMOUNTS IN HALF, AND PUT MULTIPLE PTAELS.         
*         ON ENTRY R3 - POINTS TO PTAEL                                         
*         ON EXIT CNVPTACT CONTAINS THE # OF TIMES TO PUT THE PTAEL             
*********************************************************************           
         USING PTAELD,R3                                                        
CHKNET   NTR1                                                                   
         LHI   R2,1                # OF TIMES TO PUT PTAEL                      
*                                                                               
CHKNET10 CP    PTANET,PREGMAX      X'7FFFFFFF' LARGEST # IN A REG               
         BH    CHKNET15                                                         
         CP    PTANET,NREGMAX      X'80000000' LARGEST -# IN A REG              
         BNL   CHKNETX                                                          
*                                                                               
CHKNET15 AR    R2,R2               DOUBLE THE AMOUNT OF TIMES TO ADD            
*                                                                               
         ZAP   DPAMT,PTANET                                                     
         DP    DPAMT,=P'2'         DIVIDE NET IN HALF                           
         ZAP   PTANET,DPRES                                                     
*                                                                               
         ZAP   DPAMT,PTACDSC       HALF CASH DISCOUNT                           
         DP    DPAMT,=P'2'                                                      
         ZAP   PTACDSC,DPRES                                                    
*                                                                               
         SR    R6,R6               HALF THE HOURS                               
         LH    R7,PTAHOURS                                                      
         D     R6,=F'2'                                                         
         STCM  R7,3,PTAHOURS                                                    
*                                                                               
CHKNET20 B     CHKNET10                                                         
*                                                                               
CHKNETX  STH   R2,CNVPTACT         STORE # OF TIMES PTAEL TO BE PUT             
         B     CNVX                                                             
*                                                                               
DPAMT    DS    0PL7                USED FOR DIVISION                            
DPRES    DS    PL6                                                              
DPREM    DS    PL1                                                              
*                                                                               
PREGMAX  DC    P'2147483647'       X'7FFFFFFF' LARGEST # IN A REG               
NREGMAX  DC    P'-2147483648'      X'80000000' LARGEST -# IN A REG              
         DROP  R3                                                               
         EJECT                                                                  
*********************************************************************           
* VERIFY THAT THE TYPE OF THE PTA ELEMENT IN 0(R3) IS ONE THAT CAN              
* BE CONVERTED BY THIS MODULE                                                   
*********************************************************************           
*                                                                               
         USING PTAELD,R3                                                        
CHKTYPE  NTR1                                                                   
         LA    R2,TYPETAB          TABLE OF TYPES PROCESSED HERE                
         LA    R0,NTYPETAB         NUMBER IN TABLE                              
CT20     CLC   PTATYPE,0(R2)       IS THIS PTA EL THE RIGHT TYPE                
         BE    CT50                YES, CHECK STAT1                             
         LA    R2,L'PTATYPE(R2)                                                 
         BCT   R0,CT20                                                          
         B     CTNG                                                             
*                                                                               
CT50     TM    PTASTAT1,PTASPEND   IS THIS ACTIVITY PENDING                     
         BNO   CTOK                NO, RETURN EQUAL CC                          
*                                                                               
         LA    R2,STATTAB          TABLE OF TYPES FOR WHICH PEND OK             
         LA    R0,NSTATTAB         NUMBER IN TABLE                              
CT70     CLC   PTATYPE,0(R2)       PENDING OK FOR THIS TYPE                     
         BE    CTOK                YES                                          
         LA    R2,L'PTATYPE(R2)                                                 
         BCT   R0,CT70                                                          
         B     CTNG                                                             
CTNG     CR    R0,RB               DON'T CONVERT THIS PTA ELEMENT               
         B     CNVX                                                             
CTOK     CR    RB,RB               CONVERT THIS PTA ELEMENT                     
         B     CNVX                                                             
*                                                                               
* TABLE OF PTA TYPES FOR ARE PROCESSED BY THIS MODULE                           
*                                                                               
TYPETAB  DC    AL(L'PTATYPE)(PTATRAL)                                           
         DC    AL(L'PTATYPE)(PTATWOF)                                           
         DC    AL(L'PTATYPE)(PTATWOFR)                                          
         DC    AL(L'PTATYPE)(PTATTRFT)                                          
         DC    AL(L'PTATYPE)(0)                                                 
NTYPETAB EQU   (*-TYPETAB)/L'PTATYPE                                            
*                                                                               
* TABLE OF PTA TYPES FOR WHICH PENDING VALUES ARE SIGNIFICANT                   
*                                                                               
STATTAB  DC    AL(L'PTATYPE)(PTATRAL)                                           
NSTATTAB EQU   (*-STATTAB)/L'PTATYPE                                            
*                                                                               
         DROP  R3                                                               
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* DSECT TO COVER ACPTACNV LOCAL WORKING STORAGE                       *         
***********************************************************************         
         SPACE 1                                                                
CNVWRKD  DSECT                                                                  
CNVPTACT DS    H                   # OF TIMES TO PUT PTAEL                      
DUB      DS    D                                                                
CNVPARM  DS    8F                                                               
CNVFILE  DS    CL8                                                              
CNVELEM  DS    XL255               AREA FOR PTAEL BUILDING                      
CNVDATCN DS    A                   A(DATCON)                                    
CNVHELL  DS    A                   A(HELLO)                                     
CNVRELO  DS    A                   RELOCATE OFFSET                              
PTABUFF  DS    CL2000                                                           
CNVWRKX  EQU   *                                                                
         SPACE 1                                                                
***********************************************************************         
* DSECT TO COVER INPUT PARAMETERS                                     *         
***********************************************************************         
         SPACE 1                                                                
CNVIBLKD DSECT                                                                  
CNVISTAT DS    0X     STATUS BYTE                                               
CNVIOLDQ EQU   X'80'  OLD FILE STRUCTURE                                        
CNVIATRN DS    A      A(TRANSACTION RECORD)                                     
CNVIACMF DS    A      A(COMFACS)                                                
CNVILNQ  EQU   *-CNVIBLKD                                                       
         EJECT                                                                  
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* FAFACTS                                                                       
         PRINT OFF                                                              
       ++INCLUDE FAFACTS                                                        
         PRINT ON                                                               
         SPACE 1                                                                
* DDCTRYEQUS                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDCTRYEQUS                                                     
         PRINT ON                                                               
         SPACE 1                                                                
* DDLANGEQUS                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDLANGEQUS                                                     
         PRINT ON                                                               
         SPACE 1                                                                
* DDMASTD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DDMASTD                                                        
         PRINT ON                                                               
         SPACE 1                                                                
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'023ACPTACNV  08/10/01'                                      
         END                                                                    
