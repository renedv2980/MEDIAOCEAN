*          DATA SET ACSRCHCALL AT LEVEL 035 AS OF 01/11/16                      
*CATALP ACSRCHC                                                                 
                                                                                
ACSRCHC  TITLE 'ACCOUNT SEARCH APPLICATION INTERFACE'                           
                                                                                
         PRINT NOGEN                                                            
***********************************************************************         
*                 FORMAT ACCOUNT CODE/NAME CALL                       *         
* P1: 0 = X'00'                                                       *         
*   1-3 = A(INPUT FIELD)                                              *         
*                                                                     *         
* P2:   = A(TWA)                                                      *         
*                                                                     *         
* P3: 0 = CHARACTER TO PREFIX ACCOUNT CODE IF NOT DEFAULT UNIT/LEDGER *         
*   1-3 = A(DEFAULT UNIT/LEDGER CODE) OR 0                            *         
*                                                                     *         
* ALTERNATIVELY TO DISPLAY A PORTION OF THE ACCOUNT CODE:             *         
*                                                                     *         
* P3: 0 = DISP. TO END OF PORTION + 1              = L'ACCOUNT LEVEL  *         
*   1-3 = A(DISP. TO START OF PORTION) OR 0        = L'PREVIOUS LEVEL *         
*                                                                     *         
* ALTERNATIVELY FOR NON ACCOUNT RECORD SEARCHES                       *         
*                                                                     *         
* P3:   = A(C'WC') FOR SEARCH ON WORK CODE                            *         
*       = A(C'MEDIA') FOR SEARCH ON MEDIA CODE                        *         
*       = A(C'ET') FOR SEARCH ON EXPENDITURE TYPE RECORD              *         
*       = A(C'DEPT') FOR SEARCH ON DEPARTMENT RECORD                  *         
*       = A(C'EST') FOR SEARCH ON ESTIMATE RECORD                     *         
*       = A(C'ORD') FOR SEARCH ON ORDER RECORD                        *         
*                                                                     *         
* P4: 0 = INDICATORS                                                  *         
*         X'80' RECORD IS ACCFIL, NOT ACCMST                          *         
*         X'40' USE NAME IN P6, NOT NAME ELEMENT ON RECORD            *         
*     3 = DISPLACEMENT OF NAME IN INPUT FIELD                         *         
*                                                                     *         
* P5:   = A(RECORD)                                                   *         
*                                                                     *         
* P6: 0 = L(NAME)                                                     *         
*   1-3 = A(NAME) IF P4/0 X'40' ON                                    *         
***********************************************************************         
*                     INPUT ANALYSIS CALL                             *         
* P1: 0 = TEMPSTR PAGE NO. FOR SEARCH (X'01'-X'04')                   *         
*   1-3 = A(INPUT FIELD)                                              *         
*                                                                     *         
* P2:   = A(TWA)                                                      *         
*                                                                     *         
* P3: 0 = CHARACTER TO PREFIX ACCOUNT CODE IF NOT DEFAULT UNIT/LEDGER *         
*   1-3 = A(DEFAULT UNIT/LEDGER CODE) OR 0                            *         
*                                                                     *         
* ALTERNATIVELY FOR NON ACCOUNT RECORD SEARCHES                       *         
*                                                                     *         
* P3:   = A(C'WC') FOR SEARCH ON WORK CODE                            *         
*       = A(C'MEDIA') FOR SEARCH ON MEDIA CODE                        *         
*       = A(C'ET') FOR SEARCH ON EXPENDITURE TYPE                     *         
*       = A(C'DEPT') FOR SEARCH ON DEPARTMENT RECORD                  *         
*       = A(C'EST') FOR SEARCH ON ESTIMATE RECORD                     *         
*       = A(C'ORD') FOR SEARCH ON ORDER RECORD                        *         
*                                                                     *         
* P4: 0 = L(KNOWN PREFIX OF ACCOUNT CODE)                             *         
*   1-3 = A(COMFACS)                                                  *         
*                                                                     *         
* P5: 0 = NIBBLE 1 = MINIMUM ACCOUNT LEVEL OR 0 FOR LOWEST LEVEL      *         
*         NIBBLE 2 = MAXIMUM ACCOUNT LEVEL OR 0 FOR LOWEST LEVEL      *         
*   1-3 = A(KNOWN PREFIX OF ACCOUNT CODE) OR 0                        *         
*                                                                     *         
* ALTERNATIVELY FOR WORK CODE SEARCH                                  *         
*                                                                     *         
* P5:   = A(UNIT/LEDGER) FOR WORK CODE                                *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
ACSRCHC  CSECT                                                                  
         NMOD1 WORKL,**ACSC**,RR=RE                                             
         USING WORKD,RC            RC=A(LOCAL W/S)                              
         MVC   PARMS,0(R1)                                                      
         A     RE,=V(SRCHCALL)                                                  
         ST    RE,VSRCHCAL                                                      
*                                                                               
         MVI   RECTYPE,RECTAC      ASSUME SEARCHING ON ACCOUNT CODES            
         XR    RF,RF                                                            
         ICM   RF,7,PARMS+9                                                     
         JZ    INIT02                                                           
         CLC   WC(2),0(RF)         TEST WORK CODE SEARCH                        
         JNE   INIT02                                                           
         MVI   RECTYPE,RECTWC                                                   
         J     INIT20                                                           
INIT02   CLC   MEDIA(2),0(RF)      TEST MEDIA CODE SEARCH                       
         JNE   INIT04                                                           
         MVI   RECTYPE,RECTMC                                                   
         J     INIT20                                                           
INIT04   CLC   EXPENDT(2),0(RF)    TEST EXPENDITURE TYPE SEARCH                 
         JNE   INIT06                                                           
         MVI   RECTYPE,RECTEX                                                   
         J     INIT20                                                           
INIT06   CLC   ESTIREC(2),0(RF)    TEST ESTIMATE SEARCH                         
         JNE   INIT08                                                           
         MVI   RECTYPE,RECTES                                                   
         J     INIT20                                                           
INIT08   CLC   DEPT(4),0(RF)       TEST DEPARTMENT SEARCH                       
         JNE   INIT10                                                           
         MVI   RECTYPE,RECTDP                                                   
         J     INIT20                                                           
INIT10   CLC   ORDERR(2),0(RF)     TEST ORDER SEARCH                            
         JNE   INIT20                                                           
         MVI   RECTYPE,RECTOR                                                   
*                                                                               
INIT20   CLI   ITEMPSTR,0                                                       
         BE    FORMAT                                                           
         CLI   ITEMPSTR,5                                                       
         BL    INPUT                                                            
         DC    H'0'                                                             
*                                                                               
EXIT     XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
* FORMAT ACCOUNT CODE/NAME                                            *         
***********************************************************************         
         SPACE 1                                                                
FORMAT   TM    FINDICS,FINDINAM    TEST GIVEN NAME                              
         BO    FORMAT10                                                         
*                                                                               
         XC    FNAME,FNAME         EXTRACT NAME FROM RECORD                     
         LA    RF,ACNAM                                                         
         CLI   RECTYPE,RECTAC                                                   
         JE    FORMAT02                                                         
         LA    RF,WCNAM                                                         
         CLI   RECTYPE,RECTWC                                                   
         JE    FORMAT02                                                         
         LA    RF,MCNAM                                                         
         CLI   RECTYPE,RECTMC                                                   
         JE    FORMAT02                                                         
         LA    RF,EXNAM                                                         
         CLI   RECTYPE,RECTEX                                                   
         JE    FORMAT02                                                         
         LA    RF,ESNAM                                                         
         CLI   RECTYPE,RECTES                                                   
         JE    FORMAT02                                                         
         LA    RF,ORNAM                                                         
         CLI   RECTYPE,RECTOR                                                   
         JE    FORMAT02                                                         
         LA    RF,DPNAM                                                         
         CLI   RECTYPE,RECTDP                                                   
         JE    FORMAT02                                                         
         DC    H'0'                                                             
*                                                                               
FORMAT02 BASR  RE,RF                                                            
*                                                                               
FORMAT10 MVC   CODE,SPACES         EXTRACT CODE                                 
         LA    RF,ACCOD                                                         
         CLI   RECTYPE,RECTAC                                                   
         JE    FORMAT12                                                         
         LA    RF,WCCOD                                                         
         CLI   RECTYPE,RECTWC                                                   
         JE    FORMAT12                                                         
         LA    RF,MCCOD                                                         
         CLI   RECTYPE,RECTMC                                                   
         JE    FORMAT12                                                         
         LA    RF,EXCOD                                                         
         CLI   RECTYPE,RECTEX                                                   
         JE    FORMAT12                                                         
         LA    RF,ESCOD                                                         
         CLI   RECTYPE,RECTES                                                   
         JE    FORMAT12                                                         
         LA    RF,DPCOD                                                         
         CLI   RECTYPE,RECTDP                                                   
         JE    FORMAT12                                                         
         LA    RF,ORCOD                                                         
         CLI   RECTYPE,RECTOR                                                   
         JE    FORMAT12                                                         
         DC    H'0'                                                             
*                                                                               
FORMAT12 BASR  RE,RF                                                            
*                                                                               
         L     R3,FAINP                                                         
         USING FHD,R3              R3=A(FIELD HEADER)                           
         XR    RF,RF                                                            
         IC    RF,FDNAME                                                        
         LA    RE,CODE-1(RF)                                                    
         CLI   0(RE),C' '                                                       
         BH    *+10                                                             
         BCTR  RE,0                                                             
         BCT   RF,*-10                                                          
         STC   RF,FHIL             SET INPUT LENGTH = CODE LENGTH               
         S     R3,FATWA                                                         
         GOTO1 VSRCHCAL,DMCB,(R3),FATWA,(FDNAME,CODE),FNAME                     
         B     EXIT                                                             
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* GET NAME FROM ACCOUNT RECORD                                        *         
***********************************************************************         
         SPACE 1                                                                
ACNAM    NTR1  ,                                                                
         BAS   RE,GETNAM           FIND NAMELD                                  
         B     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* GET NAME FROM WORK CODE RECORD                                      *         
***********************************************************************         
         SPACE 1                                                                
WCNAM    NTR1  ,                                                                
         BAS   RE,GETNAM           TEST NAMELD ON THE RECORD                    
         OC    FNAME,FNAME                                                      
         BNZ   EXIT                                                             
         L     R2,FAREC            NO - FIND DESCRIPTION                        
         LA    R3,WCORFST-WCORECD(R2)                                           
         TM    FINDICS,FINDIFIL                                                 
         BZ    *+8                                                              
         LA    R3,ACCORFST(R2)                                                  
         USING WCOELD,R3           R3=A(WORK CODE ELEMENT)                      
         XR    RF,RF                                                            
         CLI   WCOEL,WCOELQ                                                     
         BE    *+12                                                             
         IC    RF,WCOLN                                                         
         BXH   R3,RF,*-12                                                       
         MVI   FLNAME,L'WCODESC                                                 
         LA    RE,WCODESC                                                       
         STCM  RE,7,FANAME                                                      
         B     EXIT                                                             
         DROP  R3                                                               
         SPACE 1                                                                
***********************************************************************         
* GET NAME FROM MEDIA CODE RECORD                                     *         
***********************************************************************         
         SPACE 1                                                                
MCNAM    NTR1  ,                                                                
         XC    FNAME,FNAME                                                      
         L     R2,FAREC            FIND DESCRIPTION                             
         LA    R3,PMDRFST-PMDRECD(R2)                                           
         TM    FINDICS,FINDIFIL                                                 
         BZ    *+8                                                              
         LA    R3,ACCORFST(R2)                                                  
         USING PMDELD,R3           R3=A(MEDIA ELEMENT)                          
         XR    RF,RF                                                            
         CLI   PMDEL,PMDELQ                                                     
         BE    *+12                                                             
         IC    RF,PMDLN                                                         
         BXH   R3,RF,*-12                                                       
         MVI   FLNAME,L'PMDDESC                                                 
         LA    RE,PMDDESC                                                       
         STCM  RE,7,FANAME                                                      
         B     EXIT                                                             
         DROP  R3                                                               
         SPACE 1                                                                
***********************************************************************         
* GET NAME FROM EXPENDITURE TYPE RECORD                               *         
***********************************************************************         
         SPACE 1                                                                
EXNAM    NTR1  ,                                                                
         BAS   RE,GETNAM           FIND NAMELD                                  
         B     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* GET NAME FROM ORDER RECORD                                          *         
***********************************************************************         
         SPACE 1                                                                
ORNAM    NTR1  ,                                                                
         BAS   RE,GETENM           FIND ENMELD                                  
         B     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* GET NAME FROM ESTIMATE RECORD                                       *         
***********************************************************************         
         SPACE 1                                                                
ESNAM    NTR1  ,                                                                
         BAS   RE,GETENM           FIND ENMELD                                  
         B     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* GET NAME FROM DEPARTMENT RECORD                                     *         
***********************************************************************         
         SPACE 1                                                                
DPNAM    NTR1  ,                                                                
         BAS   RE,GETNAM           FIND NAMELD                                  
         B     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* GET NAME FROM NAMELD                                                *         
***********************************************************************         
         SPACE 1                                                                
GETNAM   NTR1  ,                                                                
         XC    FNAME,FNAME                                                      
         L     R2,FAREC                                                         
         LA    R3,ACTRFST-ACTRECD(R2)                                           
         TM    FINDICS,FINDIFIL                                                 
         BZ    *+8                                                              
         LA    R3,ACCORFST(R2)                                                  
         USING NAMELD,R3           R3=A(NAME ELEMENT)                           
         XR    RF,RF                                                            
GNAM02   CLI   NAMEL,0                                                          
         BE    GETNAMX                                                          
         IC    RF,NAMLN                                                         
         CLI   NAMEL,NAMELQ                                                     
         BE    *+8                                                              
         BXH   R3,RF,GNAM02                                                     
*                                                                               
         LA    RE,NAMLN1Q                                                       
         SR    RF,RE                                                            
         STC   RF,FLNAME           SAVE L'NAME                                  
         LA    RF,NAMEREC                                                       
         STCM  RF,7,FANAME         SAVE A(NAME)                                 
*                                                                               
GETNAMX  B     EXIT                                                             
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* GET NAME FROM ENMELD                                                *         
***********************************************************************         
         SPACE 1                                                                
GETENM   NTR1  ,                                                                
         XC    FNAME,FNAME                                                      
         L     R2,FAREC            (OR ORDRFST-ORDRECD)                         
         LA    R3,ESTRFST-ESTRECD(R2)                                           
         LHI   RE,L'ENMNAME        NOTE THAT SEARCH BUILDS UP TO 8/10           
         CLI   0(R2),ORDKTYPQ      SEARCH WORDS FROM EACH STRING, SO            
         JNE   GENM01              THE FURTHER TO THE RIGHT THE LESS            
         LHI   RE,2*(L'ENMNAME)    LIKELY A WORD CAUSES A POINTER|              
*                                                                               
GENM01   TM    FINDICS,FINDIFIL                                                 
         JZ    GENM02                                                           
         LA    R3,ACCORFST(R2)                                                  
*                                                                               
         USING ENMELD,R3           R3=A(ENM ELEMENT)                            
GENM02   LLC   RF,ENMLN                                                         
         AR    R3,RF                                                            
         CLI   ENMEL,0                                                          
         JE    GETENMX                                                          
         CLI   ENMEL,ENMELQ                                                     
         JNE   GENM02                                                           
         LLC   RF,ENMLN                                                         
         SHI   RF,ENMLNQ                                                        
         CR    RF,RE                                                            
         JNH   GENM03                                                           
         LR    RF,RE                                                            
GENM03   STC   RF,FLNAME           SAVE L'NAME                                  
         LA    RF,ENMNAME                                                       
         STCM  RF,7,FANAME         SAVE A(NAME)                                 
*                                                                               
GETENMX  B     EXIT                                                             
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* GET ACCOUNT CODE                                                    *         
***********************************************************************         
         SPACE 1                                                                
ACCOD    NTR1  ,                                                                
         L     R2,FAREC                                                         
         USING ACTRECD,R2                                                       
*                                                                               
         XR    RE,RE                                                            
         ICM   RE,1,FDISPX         TEST FOR PORTION OF CODE WANTED              
         BZ    ACCOD02                                                          
         CLI   FDISPX,L'ACTKACT                                                 
         BH    ACCOD04             GOT EXCEPTION CHARACTER                      
         XR    RF,RF                                                            
         XR    R1,R1                                                            
         ICM   RF,7,FADISP                                                      
         BZ    *+8                                                              
         IC    R1,0(RF)            RF=DISP. TO START OF CODE                    
         SR    RE,R1               RE=LENGTH OF CODE TO BE DISPLAYED            
         LA    R1,ACTKACT(R1)                                                   
         BCTR  RE,0                                                             
         MVC   CODE(0),0(R1)                                                    
         EX    RE,*-6                                                           
         B     ACCODX                                                           
*                                                                               
ACCOD02  OC    FADEFUL,FADEFUL     TEST UNIT/LEDGER TO BE DISPLAYED             
         BNZ   ACCOD06                                                          
         MVC   CODE(L'ACTKUNT+L'ACTKLDG+L'ACTKACT),ACTKUNT                      
         B     ACCODX                                                           
*                                                                               
ACCOD04  XR    RF,RF               MATCH ON DEFAULT UNIT LEDGER                 
         ICM   RF,7,FADEFUL                                                     
         CLC   ACTKUNT(L'ACTKUNT+L'ACTKLDG),0(RF)                               
         BE    ACCOD06                                                          
         MVC   CODE(1),FEXPTCHR                                                 
         MVC   CODE+1(L'ACTKUNT+L'ACTKLDG+L'ACTKACT),ACTKUNT                    
         B     ACCODX                                                           
*                                                                               
ACCOD06  MVC   CODE(L'ACTKACT),ACTKACT                                          
*                                                                               
ACCODX   B     EXIT                                                             
         DROP  R2                                                               
         SPACE 1                                                                
***********************************************************************         
* GET WORK CODE                                                       *         
***********************************************************************         
         SPACE 1                                                                
WCCOD    L     RF,FAREC                                                         
         MVC   CODE(L'WCOKWRK),WCOKWRK-WCORECD(RF)                              
         BR    RE                                                               
         SPACE 1                                                                
***********************************************************************         
* GET MEDIA CODE                                                      *         
***********************************************************************         
         SPACE 1                                                                
MCCOD    L     RF,FAREC                                                         
         MVC   CODE(L'PMDKMED),PMDKMED-PMDRECD(RF)                              
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* GET EXPENDITURE TYPE CODE                                           *         
***********************************************************************         
         SPACE 1                                                                
EXCOD    L     RF,FAREC                                                         
         MVC   CODE(L'ETYKCODE),ETYKCODE-ETYRECD(RF)                            
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* GET ORDER NUMBER                                                    *         
***********************************************************************         
         SPACE 1                                                                
ORCOD    L     RF,FAREC                                                         
         MVC   CODE(L'ORDKORD),ORDKORD-ORDRECD(RF)                              
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* GET ESTIMATE GLOBAL NUMBER                                          *         
***********************************************************************         
         SPACE 1                                                                
ESCOD    L     RF,FAREC                                                         
         AHI   RF,ESTRFST-ESTRECD                                               
         TM    FINDICS,FINDIFIL                                                 
         JZ    ESCOD2                                                           
         AHI   RF,ACCORFST-(ESTRFST-ESTRECD)                                    
         USING EMDELD,RF                                                        
ESCOD2   CLI   EMDEL,EMDELQ                                                     
         BNER  RE                                                               
         MVC   CODE(L'EMDGNO),EMDGNO                                            
         BR    RE                                                               
         DROP  RF                                                               
         EJECT                                                                  
***********************************************************************         
* GET DEPARTMENT CODE                                                 *         
***********************************************************************         
         SPACE 1                                                                
DPCOD    L     RF,FAREC                                                         
         MVC   CODE(L'DPTKCODE),DPTKCODE-DPTRECD(RF)                            
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* INPUT ANALYSIS                                                      *         
***********************************************************************         
         SPACE 1                                                                
INPUT    CLI   RECTYPE,RECTAC                                                   
         JE    INPAC                                                            
         CLI   RECTYPE,RECTWC                                                   
         JE    INPWC                                                            
         CLI   RECTYPE,RECTMC                                                   
         JE    INPMC                                                            
         CLI   RECTYPE,RECTEX                                                   
         JE    INPEX                                                            
         CLI   RECTYPE,RECTES                                                   
         JE    INPES                                                            
         CLI   RECTYPE,RECTDP                                                   
         JE    INPDP                                                            
         CLI   RECTYPE,RECTOR                                                   
         JE    INPOR                                                            
*                                                                               
INPAC    XC    OTHER,OTHER         ** ACCOUNT CODE INPUT ANALYSIS **            
         MVC   OTACTPRE,SPACES                                                  
         MVI   OTLENPRE,0                                                       
         XR    RF,RF                                                            
         ICM   RF,7,IAACTPRE                                                    
         BZ    INPAC01                                                          
         MVC   OTACTPRE,0(RF)                                                   
         MVC   OTLENPRE,ILACTPRE   TEST USER SET LENGTH OF PREFIX               
         CLI   OTLENPRE,0                                                       
         BNE   INPAC01                                                          
         LA    RF,OTACTPRE+L'OTACTPRE-1                                         
         LA    R0,L'OTACTPRE       CALCULATE LENGTH OF PREFIX                   
         CLI   0(RF),C' '                                                       
         BH    *+10                                                             
         BCTR  RF,0                                                             
         BCT   R0,*-10                                                          
         STC   R0,OTLENPRE                                                      
*                                                                               
INPAC01  MVC   OTLVLMAX,ILVLMNMX                                                
         NI    OTLVLMAX,X'0F'                                                   
         UNPK  OTLVLMIN,ILVLMNMX                                                
         NI    OTLVLMIN,X'0F'                                                   
*                                                                               
         ICM   R2,7,IAINP                                                       
         LA    R2,FHDAD(R2)        R2=A(INPUT FIELD)                            
         LR    R3,R2               R3=A(CODE IN INPUT FIELD)                    
*                                                                               
         CLI   IEXPTCHR,0          TEST FOR EXCEPTION CHARACTER                 
         BNE   INPAC04                                                          
         XR    RF,RF               TEST UNIT/LEDGER TO BE DISPLAYED             
         ICM   RF,7,IADEFUL                                                     
         BNZ   INPAC02                                                          
         MVC   SRCHTUL,0(R2)                                                    
         LA    R3,L'SRCHTUL(R3)                                                 
         B     INPAC10                                                          
INPAC02  MVC   SRCHTUL,0(RF)                                                    
         B     INPAC10                                                          
*                                                                               
INPAC04  CLC   IEXPTCHR,0(R2)      MATCH ON EXCEPTION CHARACTER                 
         BE    INPAC06                                                          
         XR    RF,RF                                                            
         ICM   RF,7,IADEFUL                                                     
         MVC   SRCHTUL,0(RF)                                                    
         B     INPAC10                                                          
INPAC06  MVC   SRCHTUL,1(R2)                                                    
         LA    R3,L'SRCHTUL+1(R3)                                               
*                                                                               
INPAC10  MVC   SRCHTSPC,SPACES                                                  
*                                                                               
         LR    RF,R3                                                            
         XR    RE,RE                                                            
         IC    RE,OTLENPRE                                                      
         LA    R0,L'ACTKACT                                                     
         SR    R0,RE                                                            
INPAC12  CLI   0(RF),C'='                                                       
         BE    INPAC14                                                          
         CLI   0(RF),C' '                                                       
         BE    INPAC20                                                          
         CLI   0(RF),C'<'                                                       
         BE    INPAC20                                                          
         LA    RF,1(RF)                                                         
         BCT   R0,INPAC12                                                       
         B     INPAC20                                                          
INPAC14  SR    RF,R3                                                            
         BZ    INPAC20                                                          
         LA    R0,0(RE,RF)                                                      
         STC   R0,OTLENPRE                                                      
         LA    RE,OTACTPRE(RE)                                                  
         BCTR  RF,0                                                             
         MVC   0(0,RE),0(R3)                                                    
         EX    RF,*-6                                                           
         LA    R3,1(RF,R3)                                                      
*                                                                               
INPAC20  ICM   RF,7,IAINP          RF=DISPLACEMENT TO INPUT FIELD               
         OI    FHOID(RF),FHOITR                                                 
         S     RF,IATWA                                                         
         SR    R3,R2                                                            
         SLL   R3,16               INSERT DISPLACEMENT TO CODE IN RF            
         OR    RF,R3                                                            
         XR    R0,R0                                                            
         ICM   R0,7,IACOMFAC                                                    
         GOTO1 VSRCHCAL,DMCB,(ITEMPSTR,(RF)),IATWA,(R0),               *        
               (L'OTHER,OTHER),(1,SRCHTYPE),0                                   
         B     EXIT                                                             
         SPACE 1                                                                
INPWC    DS    0H                  ** WORK CODE INPUT ANALYSIS **               
         XR    RF,RF                                                            
         ICM   RF,7,IAINP          RF=DISPLACEMENT TO INPUT FIELD               
         OI    FHOID(RF),FHOITR                                                 
         S     RF,IATWA                                                         
         XR    R0,R0                                                            
         ICM   R0,7,IACOMFAC                                                    
         GOTO1 VSRCHCAL,DMCB,(ITEMPSTR,(RF)),IATWA,(R0),               *        
               (L'WCOKWRK,IAWCUL),(1,WC),0                                      
         B     EXIT                                                             
         SPACE 1                                                                
INPMC    DS    0H                  ** MEDIA CODE INPUT ANALYSIS **              
         XR    RF,RF                                                            
         ICM   RF,7,IAINP          RF=DISPLACEMENT TO INPUT FIELD               
         OI    FHOID(RF),FHOITR                                                 
         S     RF,IATWA                                                         
         XR    R0,R0                                                            
         ICM   R0,7,IACOMFAC                                                    
         GOTO1 VSRCHCAL,DMCB,(ITEMPSTR,(RF)),IATWA,(R0),               *        
               0,(1,MEDIA),0                                                    
         B     EXIT                                                             
         SPACE 1                                                                
INPEX    DS    0H                  ** EXPENDITURE TYPE INPUT ANALYSIS *         
         XR    RF,RF                                                            
         ICM   RF,7,IAINP          RF=DISPLACEMENT TO INPUT FIELD               
         OI    FHOID(RF),FHOITR                                                 
         S     RF,IATWA                                                         
         XR    R0,R0                                                            
         ICM   R0,7,IACOMFAC                                                    
         GOTO1 VSRCHCAL,DMCB,(ITEMPSTR,(RF)),IATWA,(R0),               *        
               0,(1,EXPENDT),0                                                  
         B     EXIT                                                             
         SPACE 1                                                                
INPES    DS    0H                  ** ESTIMATE INPUT ANALYSIS *                 
         XR    RF,RF                                                            
         ICM   RF,7,IAINP          RF=DISPLACEMENT TO INPUT FIELD               
         OI    FHOID(RF),FHOITR                                                 
         S     RF,IATWA                                                         
         XR    R0,R0                                                            
         ICM   R0,7,IACOMFAC                                                    
         GOTO1 VSRCHCAL,DMCB,(ITEMPSTR,(RF)),IATWA,(R0),               *        
               0,(1,ESTIREC),0                                                  
         B     EXIT                                                             
         SPACE 1                                                                
INPOR    DS    0H                  ** ORDER INPUT ANALYSIS *                    
         XR    RF,RF                                                            
         ICM   RF,7,IAINP          RF=DISPLACEMENT TO INPUT FIELD               
         OI    FHOID(RF),FHOITR                                                 
         S     RF,IATWA                                                         
         XR    R0,R0                                                            
         ICM   R0,7,IACOMFAC                                                    
         GOTO1 VSRCHCAL,DMCB,(ITEMPSTR,(RF)),IATWA,(R0),               *        
               0,(1,ORDERR),0                                                   
         B     EXIT                                                             
         SPACE 1                                                                
INPDP    DS    0H                  ** DEPARTMENT INPUT ANALYSIS *               
         XR    RF,RF                                                            
         ICM   RF,7,IAINP          RF=DISPLACEMENT TO INPUT FIELD               
         OI    FHOID(RF),FHOITR                                                 
         S     RF,IATWA                                                         
         XR    R0,R0                                                            
         ICM   R0,7,IACOMFAC                                                    
         GOTO1 VSRCHCAL,DMCB,(ITEMPSTR,(RF)),IATWA,(R0),               *        
               0,(1,DEPT),0                                                     
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* CONSTANTS AND LITERALS                                              *         
***********************************************************************         
         SPACE 1                                                                
FF       EQU   X'FF'                                                            
         SPACE 1                                                                
         LTORG                                                                  
         SPACE 1                                                                
SPACES   DC    CL80' '                                                          
EFFS     DC    8X'FF'                                                           
WC       DC    CL8'WC'                                                          
MEDIA    DC    CL8'MEDIA'                                                       
EXPENDT  DC    CL8'ET'                                                          
ESTIREC  DC    CL8'EST'                                                         
ORDERR   DC    CL8'ORD'                                                         
DEPT     DC    CL8'DEPT'                                                        
         EJECT                                                                  
***********************************************************************         
* LOCAL W/S                                                           *         
***********************************************************************         
         SPACE 1                                                                
WORKD    DSECT                                                                  
PARMS    DS    0XL24               * INPUT PARAMETERS *                         
*                                                                               
FAINP    DS    A                   A(INPUT FIELD)                               
FATWA    DS    A                   A(TWA)                                       
FEXPTCHR DS    CL1                 EXCEPTION CHARACTER                          
FADEFUL  DS    AL3                 A(DEFAULT UNIT/LEDGER)                       
         ORG   FEXPTCHR                                                         
FDISPX   DS    XL1                 DISPLACEMENT TO END OF CODE + 1              
FADISP   DS    AL3                 A(DISP. TO START OF CODE)                    
FINDICS  DS    XL1                 INDICATORS                                   
FINDIFIL EQU   X'80'               RECORD IS ACCIL                              
FINDINAM EQU   X'40'               NAME IS GIVEN IN P5                          
         DS    XL2                                                              
FDNAME   DS    XL1                 DISPLACEMENT TO NAME IN INPUT FIELD          
FAREC    DS    A                   A(RECORD)                                    
FNAME    DS    0A                                                               
FLNAME   DS    XL1                 L(NAME)                                      
FANAME   DS    AL3                 A(NAME)                                      
*                                                                               
         ORG   PARMS                                                            
ITEMPSTR DS    XL1                 TEMPSTR PAGE NO.                             
IAINP    DS    AL3                 A(INPUT FIELD)                               
IATWA    DS    A                   A(TWA)                                       
IEXPTCHR DS    CL1                 EXCEPTION CHARACTER                          
IADEFUL  DS    AL3                 A(DEFAULT UNIT/LEDGER)                       
ILACTPRE DS    XL1                 L(KNOWN PREFIX OF ACCOUNT CODE)              
IACOMFAC DS    AL3                 A(COMFACS)                                   
IAWCUL   DS    0A                  A(UNIT/LEDGER) FOR WORK CODE SEARCH          
ILVLMNMX DS    XL1                 MINIMUM/MAXIMUM LEVEL OF ACCOUNT             
IAACTPRE DS    AL3                 A(KNOWN PREFIX OF ACCOUNT CODE)              
*                                                                               
         ORG   PARMS+L'PARMS                                                    
*                                                                               
VSRCHCAL DS    A                                                                
APARMS   DS    A                                                                
DMCB     DS    6A                                                               
*                                                                               
CODE     DS    XL64                                                             
*                                                                               
RECTYPE  DS    XL1                 RECORD TYPE                                  
RECTAC   EQU   X'80'               ACCOUNT RECORS                               
RECTWC   EQU   X'40'               WORK CODE RECORDS                            
RECTMC   EQU   X'20'               MEDIA CODE RECORDS                           
RECTEX   EQU   X'10'               EXPENDITURE TYPE RECORDS                     
RECTDP   EQU   X'08'               DEPARTMENT RECORDS                           
RECTES   EQU   X'04'               ESTIMATE RECORDS                             
RECTOR   EQU   X'02'               ORDER RECORDS                                
*                                                                               
SRCHTYPE DS    0CL8                SEARCH TYPE                                  
SRCHTUL  DS    CL2                 UNIT/LEDGER CODE                             
SRCHTSPC DS    CL6                 SPACES                                       
*                                                                               
OTHER    DS    0XL(L'SBOTHER)                                                   
OTACTPRE DS    CL(L'ACTKACT-1)     PREFIX TO ACCOUNT CODE                       
OTLVLMIN DS    XL1                 MINIUMUM ACCOUNT LEVEL                       
OTLVLMAX DS    XL1                 MAXIMUM ACCOUNT LEVEL                        
OTLENPRE DS    XL1                 LENGTH OF PREFIX TO ACCOUNT CODE             
         ORG   OTHER+L'OTHER                                                    
*                                                                               
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* GESRCHBLKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE GESRCHBLKD                                                     
         PRINT ON                                                               
         SPACE 1                                                                
* DDFH                                                                          
         PRINT OFF                                                              
       ++INCLUDE DDFH                                                           
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'035ACSRCHCALL01/11/16'                                      
         END                                                                    
