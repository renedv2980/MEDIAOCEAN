*          DATA SET FATORRCV   AT LEVEL 033 AS OF 06/04/99                      
*&&      SET   NOP=N                                                            
*CATALP FATORRCV                                                                
         TITLE 'RECOVERY FOR ABENDING AOR FACPAK'                               
         PRINT GEN                                                              
TORRCV   CSECT                                                                  
         NMOD1 TORWORKL,*TORRCV*                                                
         SAC   0                                                                
         LAM   R0,RF,ARZERO        CLEAR DOWN ACCESS REGS                       
*                                                                               
         USING TORWORKD,RC                                                      
         USING MSGD,MSGHDR                                                      
         L     RA,=V(SYSFAC)                                                    
         USING SYSFACD,RA                                                       
*                                                                               
         GOTO1 VTICTOC,PLST,C'SSET' SUSPEND TIMERS                              
         L     RE,VSSB                                                          
         USING SSBD,RE                                                          
         MVC   SAVMTIND,SSBMTIND   SUSPEND MULTI-TASKING WAITS                  
         MVI   SSBMTIND,0                                                       
         LAM   R3,R3,SSBALET                                                    
         ICM   R3,15,SSBATOR                                                    
         SAC   512                                                              
         DROP  RE                                                               
*                                                                               
         LA    R3,TORFACLQ(,R3)                                                 
         LH    R4,0(,R3)                                                        
         L     R5,2(,R3)                                                        
         LA    R3,6(,R3)                                                        
         USING SBEXCHD,R3                                                       
*                                                                               
RCV02    OC    SBSTOKEN,SBSTOKEN    AOR IN THIS SLOT?                           
         BZ    RCV04                NO                                          
         CLI   SBAVLBL,SBNO         AOR HAS GONE DOWN?                          
         BNE   RCV04                NO                                          
*                                                                               
         STAR  CLEAR=NO,ARS=SAME                                                
         BRAS  RE,RECOVER                                                       
         REAR  ARS=ON                                                           
*                                                                               
         CPYA  R6,R3                RESET AOR SLOT FOR REUSE                    
         LR    R6,R3                                                            
         LR    R7,R4                                                            
         XR    RE,RE                                                            
         XR    RF,RF                                                            
         MVCL  R6,RE                                                            
         LAM   R6,R6,ARZERO                                                     
*                                                                               
RCV04    BXLE  R3,R4,RCV02         FIND NEXT ABENDING AOR                       
*                                                                               
         SAC   0                   CLEAR ALL ACCESS REGISTERS AGAIN             
         LAM   R0,RF,ARZERO                                                     
*                                                                               
         L     RE,VSSB                                                          
         USING SSBD,RE                                                          
         MVC   SSBMTIND,SAVMTIND   RESTORE MULTI-TASKING STATE                  
         DROP  RE                                                               
*                                                                               
         GOTO1 VTICTOC,PLST,C'RSET'                                             
         XMOD1 ,                                                                
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* FACPAK TASK RECOVERY ROUTINE                                        *         
* NTRY: R3  =  A(AOR EXCHANGE BLOCK TO RECOVER)                       *         
***********************************************************************         
         SPACE 1                                                                
         USING SBEXCHD,R3                                                       
RECOVER  NTR1  ,                                                                
         ICM   R0,15,SBTSKMAX      NUMBER OF TASKS IN THIS FACPAK               
         CPYA  R2,R3                                                            
         LA    R2,SBTSKBLK                                                      
         USING EXCHNGD,R2                                                       
*                                                                               
RCVR02   CLI   EXCFLAG,EXCFREE  ** TASK IS FREE?                                
         BE    RCVR99              YES                                          
*                                                                               
         CLI   EXCFLAG,EXCNEW      TASK HAS UNPROCESSED WORK?                   
         BNE   RCVR10              NO                                           
*                                                                               
         ICM   RE,15,VUTL          RE=A(UTL BLOCK)                              
         LH    RF,EXCUTL           RF=TERMINAL NUMBER                           
         BCTR  RF,0                ZERO BASED                                   
         MH    RF,0(RE)            INDEX INTO BLOCK                             
         LA    R6,6(RE,RF)                                                      
         USING UTLD,R6             REQUEUE TRANSACTION TO FRONT OF SE           
*                                                                               
         SAC   0                                                                
         NI    TSTAT2,255-TSTATTIP                                              
*                                                                               
         LA    RF,1                FIND SE TO REQUEUE TO                        
         CLI   TSYS,0                                                           
         BE    *+8                                                              
         IC    RF,TSYS                                                          
*                                                                               
         L     R7,VSELIST                                                       
         LH    R8,0(R7)                                                         
         L     R9,2(R7)                                                         
         LA    R7,6(R7)                                                         
         USING SELISTD,R7                                                       
         CLM   RF,1,SESYS                                                       
         BE    RCVR04                                                           
         BXLE  R7,R8,*-8                                                        
         DC    H'0'                                                             
*                                                                               
RCVR04   OC    SEFIRST,SEFIRST     ANYTHING IN QUEUE?                           
         BNZ   RCVR06              YES                                          
         ST    R6,SEFIRST          PLACE AT FRONT OF QUEUE                      
         ST    R6,SELAST                                                        
         LA    RF,1                                                             
         STH   RF,SEQLEN                                                        
         B     RCVR08                                                           
*                                                                               
RCVR06   L     RE,SEFIRST          MOVE THIS TRANSACTION TO FRONT               
         STCM  RE,7,TNEXTIN                                                     
         ST    R6,SEFIRST                                                       
         LH    RF,SEQLEN                                                        
         LA    RF,1(RF)                                                         
         STH   RF,SEQLEN                                                        
*                                                                               
RCVR08   SAC   512                                                              
         B     RCVR99                                                           
*                                                                               
RCVR10   CLI   EXCFLAG,EXCDONE  ** TASK HAS COMPLETED WORK TO WRITE?            
         BNE   RCVR12                                                           
*                                                                               
         ICM   RE,15,VUTL          RE=A(UTL BLOCK)                              
         LH    RF,EXCUTL           RF=TERMINAL NUMBER                           
         BCTR  RF,0                ZERO BASED                                   
         MH    RF,0(RE)            INDEX INTO BLOCK                             
         LA    R6,6(RE,RF)                                                      
         USING UTLD,R6                                                          
*                                                                               
         STAR  CLEAR=ARZERO,ARS=OFF                                             
         GOTO1 VLCM,PLST,VTWRITE,(R6),0,0                                       
         REAR  ARS=ON                                                           
         B     RCVR99                                                           
*                                                                               
RCVR12   CLI   EXCFLAG,EXCBUSY  ** TASK HAS BEGUN PROCESSING?                   
         BNE   RCVR99              NO                                           
*                                                                               
         ICM   RE,15,VUTL          RE=A(UTL BLOCK)                              
         LH    RF,EXCUTL           RF=TERMINAL NUMBER                           
         BCTR  RF,0                ZERO BASED                                   
         MH    RF,0(RE)            INDEX INTO BLOCK                             
         LA    R6,6(RE,RF)                                                      
         USING UTLD,R6             REQUEUE TRANSACTION TO FRONT OF SE           
*                                                                               
         SAC   0                                                                
         NI    TSTAT2,255-TSTATTIP                                              
*                                                                               
         GOTO1 WRIMSG,PLST,UTLD    WRITE IN PROCESS MESSAGE                     
*                                                                               
         TM    TTORAOR,TTARCVR     RECOVERED FROM ABENDING AOR BEFORE?          
         BZ    *+14                NO                                           
         MVC   TSVCREQ,$SYSNOP     REQUEUE WITH NO SYSTEM MESSAGE               
         MVI   TSYS,0                                                           
*                                                                               
         OI    TTORAOR,TTARCVR     SET RECOVERED FROM ABENDING AOR              
         LA    RF,1                                                             
         CLI   TSYS,0                                                           
         BE    *+8                                                              
         IC    RF,TSYS                                                          
*                                                                               
         L     R7,VSELIST                                                       
         LH    R8,0(R7)                                                         
         L     R9,2(R7)                                                         
         LA    R7,6(R7)                                                         
         USING SELISTD,R7                                                       
         CLM   RF,1,SESYS                                                       
         BE    RCVR14                                                           
         BXLE  R7,R8,*-8                                                        
         DC    H'0'                                                             
*                                                                               
RCVR14   OC    SEFIRST,SEFIRST     ANYTHING IN QUEUE?                           
         BNZ   RCVR16              YES                                          
         ST    R6,SEFIRST          PLACE AT FRONT OF QUEUE                      
         ST    R6,SELAST                                                        
         LA    RF,1                                                             
         STH   RF,SEQLEN                                                        
         B     RCVR18                                                           
*                                                                               
RCVR16   L     RE,SEFIRST          MOVE THIS TRANSACTION TO FRONT               
         STCM  RE,7,TNEXTIN                                                     
         ST    R6,SEFIRST                                                       
         LH    RF,SEQLEN                                                        
         LA    RF,1(RF)                                                         
         STH   RF,SEQLEN                                                        
*                                                                               
RCVR18   SAC   512                                                              
         B     RCVR99                                                           
*                                                                               
RCVR99   LA    R2,EXCHNGLQ(,R2)                                                 
         BCT   R0,RCVR02                                                        
         XC    SBSTOKEN,SBSTOKEN   FREE RECOVERED FACPAK ROW                    
         XC    SBSTRTD,SBSTRTD                                                  
         MVI   SBSTRTD,SBNO                                                     
         MVC   SBWAKE,=CL4'RCVR'   TEMP FOR DEBUG                               
*                                                                               
RECOVERX XIT1  ,                                                                
         DROP  R2,R3,R6,R7                                                      
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO WRITE IN PROCESS TRANSACTION DETAILS FOR AOR ABEND       *         
***********************************************************************         
         SPACE 1                                                                
WRIMSG   NTR1  ,                                                                
         L     R2,0(R1)            P1=A(UTL)                                    
         USING UTLD,R2                                                          
*                                                                               
         LA    R0,MSGHDR           SPACE FILL MESSAGE BUFFER                    
         LA    R1,MSGLEN                                                        
         XR    RE,RE                                                            
         LA    RF,C' '                                                          
         SLL   RF,24                                                            
         MVCL  R0,RE                                                            
*                                                                               
         MVC   MSGHDRL,=AL2(L'MSGHDR)                                           
         MVC   MSG1L,=AL2(L'MSG1)                                               
         MVC   MSG2L,=AL2(L'MSG2)                                               
         MVC   MSG3L,=AL2(L'MSG3)                                               
         MVC   MSG4L,=AL2(L'MSG4)                                               
*                                                                               
         MVC   MSGHDR,HDRMSG       SET HEADER MESSAGE                           
         MVC   MSGTLUID,LUID       MOVE IN LUID=                                
         MVC   MSGTDDS,DDS         MOVE IN DDS=                                 
         MVC   MSGTSYS,SYSTEM      MOVE IN SYSTEM=                              
         MVC   MSGTPRG,PROGRAM     MOVE IN PROGRAM=                             
         MVC   MSGTSWC,SWITCH      MOVE IN SWITCHED TO                          
         MVC   MSGTUSER,USERID     MOVE IN USERID NO=                           
         MVC   MSGTPWD,PASSWORD    MOVE IN PASSWORD NO=                         
*                                                                               
         MVC   MSG1LUID,QUERY      DEFAULT ALL TO UNKNOWN                       
         MVC   MSG1DDS,QUERY                                                    
         MVC   MSG2SYS,QUERY                                                    
         MVC   MSG2PRG,QUERY                                                    
         MVC   MSG2SWC,QUERY                                                    
*                                                                               
         MVC   MSG1LUID,TLUID      MOVE IN LUID                                 
*                                                                               
         MVC   MSG1DDS,NO          SET TERMINAL STATUS                          
         TM    TSTAT1,TSTATDDS                                                  
         BZ    *+8                                                              
         MVC   MSG1DDS,YES                                                      
*                                                                               
         OC    TSVCREQ,TSVCREQ     RUNNING SERVICE REQUEST?                     
         BNZ   WRI04               YES                                          
*                                                                               
         XR    RF,RF               GET CONNECTED SYSTEM                         
         ICM   RF,7,TARSYS                                                      
         BZ    WRI02                                                            
         MVC   MSG2SYS,SENAME-SELISTD(RF)                                       
*                                                                               
         ICM   R0,15,SEPGMS-SELISTD(RF)                                         
         ICM   RF,7,TAPRG          GET CONNECTED PROGRAM                        
         BZ    WRI02                                                            
         AR    RF,R0                                                            
         MVC   MSG2PRG,PGMNAME-PGMLSTD(RF)                                      
*                                                                               
WRI02    XR    RF,RF               GET SWITCHED SYSTEM                          
         ICM   RF,7,TASYS                                                       
         BZ    WRI10                                                            
         MVC   MSG2SWC,SENAME-SELISTD(RF)                                       
         B     WRI10                                                            
*                                                                               
WRI04    L     R1,VSELIST          SERVICE REQUESTS HERE                        
         LH    RE,0(R1)                                                         
         L     RF,2(R1)                                                         
         LA    R1,6(R1)                                                         
         USING SELISTD,R1                                                       
         CLI   SESYS,1                                                          
         BE    WRI06                                                            
         BXLE  R1,RE,*-8                                                        
         B     WRI10                                                            
                                                                                
WRI06    MVC   MSG2SYS,SENAME                                                   
         ICM   R0,15,SEPGMS                                                     
         LH    RE,0(R1)                                                         
         L     RF,2(R1)                                                         
         LA    R1,6(R1)                                                         
         USING PGMLSTD,R1                                                       
         CLC   PGMNUM,TSVCREQ+1                                                 
         BE    WRI08                                                            
         BXLE  R1,RE,*-10                                                       
         XR    RF,RF                                                            
         IC    RF,TSVCREQ+1                                                     
         CVD   RF,DUB                                                           
         OI    DUB+L'DUB-1,X'0F'                                                
         UNPK  MSG2PRG,DUB                                                      
         B     WRI10                                                            
                                                                                
WRI08    MVC   MSG2PRG,PGMNAME                                                  
         DROP  R1                                                               
*                                                                               
WRI10    LH    R0,TUSER                                                         
         EDIT  (R0),(6,MSG1USER),ALIGN=LEFT,ZERO=NOBLANK                        
*                                                                               
         LH    R0,TPASSWD                                                       
         EDIT  (R0),(6,MSG1PWD),ALIGN=LEFT,ZERO=NOBLANK                         
                                                                                
         XR    R0,R0               WTO REQUIRES THIS CLEARED                    
         WTO   TEXT=((MSGHDRL,C),(MSG1L,D),(MSG2L,D),(MSG3L,D),(MSG4L,D*        
               E)),DESC=(12)                                                    
*                                                                               
         XIT1  ,                                                                
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTANTS                                              *         
***********************************************************************         
         SPACE 1                                                                
ARZERO   DC    16F'0'                                                           
$SYSNOP  DC    XL2'0103'                                                        
*                                                                               
HDRMSG   DC    CL34'Task in process during AOR abend'                           
QUERY    DC    16C'?'                                                           
YES      DC    C'Y'                                                             
NO       DC    C'N'                                                             
LUID     DC    C'Luid='                                                         
DDS      DC    C'DDS='                                                          
SYSTEM   DC    C'System='                                                       
PROGRAM  DC    C'Program='                                                      
SWITCH   DC    C'Switched to '                                                  
USERID   DC    C'UserID No='                                                    
PASSWORD DC    C'Password No='                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* DSECTS                                                              *         
***********************************************************************         
         SPACE 1                                                                
TORWORKD DSECT                     MAIN W/S DSECT                               
DUB      DS    D                                                                
FULL     DS    F                                                                
PLST     DS    6F                                                               
WORK     DS    CL20                                                             
SAVMTIND DS    XL(L'SSBMTIND)                                                   
*                                                                               
MSGHDRL  DS    AL2                                                              
MSGHDR   DS    CL34                                                             
MSG1L    DS    AL2                                                              
MSG1     DS    CL68                                                             
MSG2L    DS    AL2                                                              
MSG2     DS    CL68                                                             
MSG3L    DS    AL2                                                              
MSG3     DS    CL68                                                             
MSG4L    DS    AL2                                                              
MSG4     DS    CL68                                                             
MSGLEN   EQU   *-MSGHDR                                                         
*                                                                               
TORWORKL EQU   *-TORWORKD                                                       
*                                                                               
MSGD     DSECT                     IN PROCESS TRANSACTION MESSAGE DSECT         
         DS    CL2                                                              
MSGDHDR  DS    CL34                                                             
         DS    CL2                                                              
MSGDL1   DS    0CL68                                                            
MSGTLUID DS    CL(L'LUID)      5   LUID                                         
MSG1LUID DS    CL(L'TLUID)     7                                                
         DS    CL1             1                                                
MSGTUSER DS    CL(L'USERID)   10   USERID                                       
MSG1USER DS    CL6             6                                                
         DS    CL1             1                                                
MSGTPWD  DS    CL(L'PASSWORD) 12   PASSWORD ID                                  
MSG1PWD  DS    CL6             6                                                
         DS    CL1             1                                                
MSGTDDS  DS    CL(L'DDS)       4   DDS TERMINAL                                 
MSG1DDS  DS    CL(L'YES)       3                                                
         ORG   MSGDL1+L'MSGDL1                                                  
*                                                                               
         DS    CL2                                                              
MSGDL2   DS    0CL68                                                            
MSGTSYS  DS    CL(L'SYSTEM)    7    SYSTEM                                      
MSG2SYS  DS    CL(L'SENAME)    7                                                
         DS    CL1             1                                                
MSGTPRG  DS    CL(L'PROGRAM)   8    PROGRAM                                     
MSG2PRG  DS    CL(L'PGMNAME)   7                                                
         DS    CL1             1                                                
MSGTSWC  DS    CL(L'SWITCH)   12    SWITCHED TO                                 
MSG2SWC  DS    CL(L'SENAME)    7                                                
         ORG   MSGDL2+L'MSGDL2                                                  
         DS    CL2                                                              
MSGDL3   DS    0CL68                                                            
         ORG   MSGDL3+L'MSGDL3                                                  
         DS    CL2                                                              
MSGDL4   DS    0CL68                                                            
         ORG   MSGDL4+L'MSGDL4                                                  
         EJECT                                                                  
***********************************************************************         
* INCLUDED BOOKS                                                      *         
***********************************************************************         
         SPACE 1                                                                
*FASELIST                                                                       
         PRINT OFF                                                              
       ++INCLUDE FASELIST                                                       
         PRINT ON                                                               
         SPACE 1                                                                
*FAPGMLST                                                                       
         PRINT OFF                                                              
       ++INCLUDE FAPGMLST                                                       
         PRINT ON                                                               
         SPACE 1                                                                
*FASSB                                                                          
         PRINT OFF                                                              
       ++INCLUDE FASSB                                                          
         PRINT ON                                                               
         SPACE 1                                                                
*FAUTL                                                                          
         PRINT OFF                                                              
       ++INCLUDE FAUTL                                                          
         PRINT ON                                                               
         SPACE 1                                                                
*FASYSFAC                                                                       
         PRINT OFF                                                              
       ++INCLUDE FASYSFAC                                                       
         PRINT ON                                                               
         SPACE 1                                                                
*DMDSHDR                                                                        
         PRINT OFF                                                              
       ++INCLUDE DMDSHDR                                                        
         PRINT ON                                                               
         SPACE 1                                                                
*FAPIGFACD                                                                      
         PRINT OFF                                                              
       ++INCLUDE FAPIGFACD                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'033FATORRCV  06/04/99'                                      
         END                                                                    
