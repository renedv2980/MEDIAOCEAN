*          DATA SET MPDPTDEM   AT LEVEL 004 AS OF 05/01/02                      
*CATALP DPTDEM                                                                  
         TITLE 'MPDPTDEM- GET DEMOS BY DAYPART'                                 
DPTDEM   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WKL,**DPTDEM,RR=R5                                               
         USING WKD,RC                                                           
         ST    R1,APARM            SAVE PLIST ADDRESS                           
         MVC   PARMS,0(R1)         SAVE PARAMS                                  
         ST    R5,RELO             SAVE PROGRAM RELOCATION FACTOR               
         SPACE 3                                                                
*                                                                               
         L     R6,ASTATN                                                        
         USING STNTABD,R6                                                       
         L     RA,ADBLK                                                         
         USING DBLOCK,RA                                                        
         L     R9,DBCOMFCS                                                      
         USING COMFACSD,R9                                                      
*                                                                               
         L     RE,ADTATAB          CLEAR  OUTPUT AREA                           
         ZIC   RF,NDEMS                                                         
         MH    RF,=Y(NDPTS)                                                     
         XCEF                                                                   
*                                                                               
         MVC   DBSELSTA,STNID      SET STATION                                  
         LA    RF,STNADI           AND MARKET- ADI                              
         CLI   DBSELSRC,C'A'                                                    
         BE    *+8                                                              
         LA    RF,STNDMA           OR DMA                                       
         CLI   0(RF),C' '          TEST PRESENT                                 
         BE    SD60                                                             
         PACK  DUB,0(3,RF)                                                      
         CVB   R0,DUB                                                           
         STCM  R0,3,DBSELMK                                                     
*                                                                               
         LA    R8,DPTDEFS                                                       
         USING DFDS,R8                                                          
*                                                                               
SD40     DS    0H                                                               
         CLI   0(R8),X'FF'         END OF DTP DEF LIST                          
         BE    SD60                                                             
*                                                                               
         CLI   DFTZ,C'E'           TEST TIME ZONES                              
         BNE   SD42                                                             
         CLI   STNTZ,1             EASTERN                                      
         BE    SD42D                                                            
         CLI   STNTZ,4             PACIFIC                                      
         BE    SD42D                                                            
         B     SD58                SKIP DEFINITION                              
*                                                                               
SD42     DS    0H                                                               
         CLI   DFTZ,C'C'                                                        
         BNE   SD42D                                                            
         CLI   STNTZ,2             CENTRAL                                      
         BE    SD42D                                                            
         CLI   STNTZ,3             MOUNTAIN                                     
         BE    SD42D                                                            
         B     SD58                SKIP DEFINITION                              
*                                                                               
SD42D    DS    0H                                                               
         CLI   DFAFF,C'N'          TEST AFFILIATION                             
         BNE   SD42G                                                            
         CLI   STNAFF,C'A'         ABC                                          
         BE    SD42J                                                            
         CLI   STNAFF,C'C'         CBS                                          
         BE    SD42J                                                            
         CLI   STNAFF,C'N'         NBC                                          
         BE    SD42J                                                            
         B     SD58                SKIP                                         
*                                                                               
SD42G    DS    0H                                                               
         CLI   DFAFF,C'I'                                                       
         BNE   SD42J                                                            
         CLI   STNAFF,C'I'         IND                                          
         BE    SD42J                                                            
         CLI   STNAFF,C' '         UNKNOWN                                      
         BNH   SD42J                                                            
         B     SD58                SKIP                                         
*                                                                               
SD42J    DS    0H                                                               
         MVC   DBSELDAY,DFDAYS     SET DAYS FOR THIS DPT                        
         MVC   DBSELTIM,DFSTIM     START-END TIMES                              
         XC    DOUTS2,DOUTS2                                                    
*                                                                               
         GOTO1 CDEMAND,DMCB,ADBLK,SVSDEMS                                       
         CLI   DBERROR,0                                                        
         BE    SD50                                                             
         CLI   DBERROR,X'80'                                                    
         BE    SD50                                                             
         B     SD50                NO-OP                                        
         DC    H'0'                                                             
*                                                                               
SD50     DS   0H                                                                
         ZIC   R5,NDEMS                                                         
         LA    R2,DOUTS2                                                        
         L     R3,ADTATAB          OUTPUT AREA                                  
         ZIC   RF,DFDPT            DAYPART NUMBER                               
         BCTR  RF,R0                                                            
         SLL   RF,2                X 4                                          
         MR    RE,R5               X NDEMS                                      
         AR    R3,RF               POSITION TO THIS DPT                         
*                                                                               
SD54     DS    0H                  ***THIS IS NOT RIGHT**                       
         L     R1,0(R2)                                                         
         M     R0,=F'1'                                                         
         LH    RF,DBDIVSOR                                                      
         BAS   RE,DIV                                                           
         A     R1,0(R3)                                                         
         ST    R1,0(R3)                                                         
         LA    R2,4(R2)                                                         
         LA    R3,4(R3)                                                         
         BCT   R5,SD54                                                          
*                                                                               
SD58     DS    0H                                                               
         LA    R8,DFLEN(R8)      NEXT DPT                                       
         B     SD40                                                             
*                                                                               
SD60     DS    0H                                                               
EXIT     DS    0H                                                               
         XIT1                                                                   
*                                                                               
         SPACE 2                                                                
*        GET DEMOS                                                              
         SPACE 2                                                                
SVSDEMS  NTR1                                                                   
         CLI   DFINEX,C' '         TEST PGM TYP INCL/EXCL                       
         BE    SVD48               NO-OK                                        
         LA    R3,DFTYPS                                                        
         LA    R4,L'DFTYPS                                                      
*                                                                               
SVD43    DS    0H                                                               
         CLI   0(R3),C' '          DONE                                         
         BE    SVD48               OK TO USE                                    
         LA    R5,NEWSWDS                                                       
         CLI   0(R3),C'N'                                                       
         BE    SVD44                                                            
         LA    R5,SPTSWDS                                                       
         CLI   0(R3),C'S'                                                       
         BE    SVD44                                                            
         DC    H'0'                BAD PGM TYPE                                 
*                                                                               
SVD44    DS    0H                                                               
         BAS   RE,TSTPGM                                                        
         CLI   TYPRET,C'I'         OK TO INCLUDE                                
         BE    SVD48                                                            
         CLI   TYPRET,C'X'         EXCLUDE                                      
         BE    SVDX                                                             
*                                                                               
SVD47    DS    0H                  ELSE CONTINUE                                
         LA    R3,1(R3)            NEXT TYPE                                    
         BCT   R4,SVD43                                                         
*                                                                               
SVD47D   DS    0H                                                               
         CLI   TYPRET,C'I'         IF INCLUDING                                 
         BE    EXIT                SKIP IF NOT FOUND                            
*                                                                               
SVD48    DS    0H                                                               
         GOTO1 CDEMOUT,DMCB,(C'L',DEMLIST),ADBLK,DOUTS                          
*                                                                               
         ZIC   R5,NDEMS                                                         
         LA    R2,DOUTS                                                         
         LA    R3,DOUTS2           HOLD SUM IN DOUTS2                           
*                                                                               
SVD50    DS    0H                                                               
         L     R1,0(R2)                                                         
         MH    R1,DBFACTOR                                                      
         A     R1,0(R3)                                                         
         ST    R1,0(R3)                                                         
         LA    R2,4(R2)                                                         
         LA    R3,4(R3)                                                         
         BCT   R5,SVD50                                                         
*                                                                               
         DROP  R6                                                               
SVDX     DS    0H                                                               
         B     EXIT                                                             
         SPACE 2                                                                
*        TSTPGM - TEST PROGRAM TYPE                                             
         SPACE 2                                                                
TSTPGM   NTR1                                                                   
         L     R4,DBAQUART         QTR HOUR ELEM                                
         USING QHELEM,R4                                                        
         CLI   1(R4),7             TEST NAME PRESENT                            
         BNH   TPTX                                                             
         MVI   WORK,C' '                                                        
         MVC   WORK+1(15),WORK                                                  
         ZIC   RF,1(R4)                                                         
         SH    RF,=H'7'                                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   WORK(0),QHPNAME                                                  
*                                                                               
         MVI   TYPRET,C' '                                                      
         MVC   BYTE,1(R5)          SAVE PGM TYPE FROM FIRST                     
*                                                                               
TPT4     DS    0H                                                               
         CLC   BYTE,1(R5)          TEST DONE WITH TYPE                          
         BNE   TPTX                                                             
*                                                                               
         ZIC   R2,0(R5)                                                         
         GOTO1 =V(TSCAN),DMCB,WORK,15,((R2),2(R5)),((R2),2(R5))                 
         CLI   DMCB+16,0           TEST FOUND                                   
         BE    TPT8                NO- CONTINUE                                 
*                                                                               
         MVC   TYPRET,DFINEX       IF FOUND- SET RETURN CODE                    
         B     TPTX                                                             
*                                                                               
TPT8     DS    0H                                                               
         LA    R5,2(R2,R5)         NEXT ENTRY                                   
         B     TPT4                                                             
*                                                                               
TPTX     DS    0H                                                               
         B     EXIT                                                             
*                                                                               
         SPACE 2                                                                
DIV      DIV   (R0),(RF)           RETURNS VIA RE                               
*                                                                               
*                                                                               
*                                  PROGRAM WORD TABLE                           
*                                                                               
NEWSWDS  DS    0X                                                               
         DC    AL1(4),C'N',C' NW '      NEWS                                    
         DC    AL1(3),C'N',C' N '                                               
         DC    AL1(3),C'N',C'NWS'                                               
         DC    AL1(4),C'N',C'NEWS'                                              
         DC    AL1(4),C'N',C'NWCN'                                              
         DC    AL1(3),C'N',C'EDT'                                               
         DC    AL1(4),C'N',C'EYEW'                                              
         DC    AL1(3),C'N',C'EYW'                                               
         DC    AL1(3),C'N',C'NWC'                                               
         DC    AL1(5),C'N',C'NWRPT'                                             
         DC    AL1(3),C'N',C'NWT'                                               
         DC    AL1(6),C'N',C'REPORT'   OK IF IN NEWS TP                         
         DC    AL1(5),C'N',C' RPT '                                             
         DC    AL1(3),C'N',C'NW2'                                               
         DC    AL1(3),C'N',C'NW4'                                               
         DC    AL1(3),C'N',C'NW5'                                               
         DC    AL1(3),C'N',C'NW7'                                               
         DC    AL1(3),C'N',C'NW/'                                               
         DC    AL1(7),C'N',C'LIVE AT'                                           
         DC    AL1(7),C'N',C'LIVE ON'                                           
         DC    AL1(4),C'N',C'SPTS'      =NEWS IF IN NEWS TP                     
         DC    AL1(3),C'N',C'WEA'                                               
*                                                                               
SPTSWDS  DS    0X                                                               
         DC    AL1(4),C'S',C'SPTS'      SPORTS                                  
         DC    AL1(5),C'S',C'WREST'                                             
         DC    AL1(4),C'S',C'WRST'                                              
         DC    AL1(5),C'S',C'SPORT'                                             
         DC    AL1(6),C'S',C'BSKTBL'                                            
         DC    AL1(4),C'S',C'BSKB'                                              
         DC    AL1(3),C'S',C'BKB'                                               
         DC    AL1(5),C'S',C'BOWLN'                                             
         DC    AL1(5),C'S',C'BOWLI'                                             
         DC    AL1(4),C'S',C'BLWN'                                              
         DC    AL1(5),C'S',C'BOWLE'                                             
         DC    AL1(4),C'S',C'GOLF'                                              
         DC    AL1(4),C'S',C'FTBL'                                              
         DC    AL1(8),C'S',C'FOOTBALL'                                          
         DC    AL1(5),C'S',C'BASEB'                                             
         DC    AL1(2),C'S',C'BB'                                                
         DC    AL1(3),C'S',C'BSB'                                               
         DC    AL1(4),C'S',C'RACE'                                              
         DC    AL1(5),C'S',C'RACNG'                                             
         DC    AL1(5),C'S',C' RAC '                                             
*                                                                               
MOVWDS   DS    0X                                                               
         DC    AL1(3),C'M',C'MOV'       MOVIES                                  
         DC    AL1(4),C'M',C'CINE'                                              
         DC    AL1(4),C'M',C'CNMA'                                              
         DC    AL1(5),C'M',C' CIN '                                             
         DC    AL1(4),C'M',C' MV '                                              
         DC    AL1(4),C'M',C'MATI'                                              
         DC    AL1(5),C'M',C'MATNE'                                             
         DC    AL1(5),C'M',C' MAT '                                             
         DC    AL1(4),C'M',C'THTR'                                              
         DC    AL1(5),C'M',C'THEAT'                                             
         DC    AL1(5),C'M',C'WSTRN'                                             
*                                                                               
         DC    X'FFFFFFFF'                                                      
         EJECT                                                                  
*        DAYPART TIME DEFINITIONS                                               
         SPACE 2                                                                
NDPTS    EQU   10                  NUMBER OF DAYPARTS                           
         SPACE 2                                                                
DFDS     DSECT                     DSECT FOR DAYPART DEFINITION TABLE           
DFDPT    DS    XL1                 DPT NUM                                      
DFDAYS   DS    XL1                 DAYS                                         
DFSTIM   DS    XL2                 START TIME                                   
DFETIM   DS    XL2                 END TIME                                     
DFINEX   DS    CL1                 I=INCLUDE,E=EXCLUDE PGM TYPES                
DFTYPS   DS    CL4                 PGM TYPES FOR INCL/EXCL                      
DFTZ     DS    CL1                 TIME ZONE (BLANK=ALL)                        
*                                  E=EAST+WEST                                  
*                                  C=CENTRAL+MOUNTAIN                           
DFAFF    DS    CL1                 N=NETWORK AFF'S, I=INDS, BLANK=ALL           
         DS    CL3                 SPARE                                        
DFLEN    EQU   *-DFDS                                                           
*                                                                               
         SPACE 2                                                                
DPTDEM   CSECT                                                                  
DPTDEFS  DS    0X                                                               
         DC    AL1(01)             EM                                           
         DC    X'7D'               M-F/SU                                       
         DC    AL2(0600)           6A                                           
         DC    AL2(1000)           10A                                          
         DC    CL10'          '                                                 
*                                                                               
         DC    AL1(02)             DAY                                          
         DC    X'7D'               M-F/SU                                       
         DC    AL2(1000)           10A                                          
         DC    AL2(1600)           4P                                           
         DC    CL10'          '                                                 
*                                                                               
         DC    AL1(03)             EFR                                          
         DC    X'7F'               M-SU                                         
         DC    AL2(1600)           4P                                           
         DC    AL2(1800)           6P                                           
         DC    CL10'EN        '    EXCLUDE NEWS                                 
*                                                                               
         DC    AL1(04)             EN                                           
         DC    X'7F'               M-SU                                         
         DC    AL2(1700)           5P                                           
         DC    AL2(1930)           730P                                         
         DC    CL10'IN        '    NEWS ONLY                                    
*                                                                               
         DC    AL1(05)             PA                                           
         DC    X'7F'               M-SU                                         
         DC    AL2(1900)           7P                                           
         DC    AL2(2000)           8P                                           
         DC    CL10'EN   E    '    EXCLUDE NEWS, TZ=E+W                         
*                                                                               
         DC    AL1(05)             PA                                           
         DC    X'7F'               M-SU                                         
         DC    AL2(1800)           6P                                           
         DC    AL2(1900)           7P                                           
         DC    CL10'EN   C    '    EXCLUDE NEWS, TZ=C+P                         
*                                                                               
         DC    AL1(06)             PRIME                                        
         DC    X'7F'               M-SU                                         
         DC    AL2(2000)           8P                                           
         DC    AL2(2300)           11P                                          
         DC    CL10'EN   E    '    EXCLUDE NEWS,TZ=E+W                          
*                                                                               
         DC    AL1(06)             PRIME                                        
         DC    X'7F'               M-SU                                         
         DC    AL2(1900)           7P                                           
         DC    AL2(2200)           10P                                          
         DC    CL10'EN   C    '    EXCLUDE NEWS,TZ=C+P                          
*                                                                               
         DC    AL1(07)             LN                                           
         DC    X'7F'               M-SU                                         
         DC    AL2(2200)           10P                                          
         DC    AL2(2330)           1130P                                        
         DC    CL10'IN        '    NEWS ONLY                                    
*                                                                               
         DC    AL1(08)             LFR                                          
         DC    X'7F'               M-SU                                         
         DC    AL2(2330)           1130P                                        
         DC    AL2(0200)           2A                                           
         DC    CL10'EN   E    '    EXCLUDE NEWS,TZ=E+W                          
*                                                                               
         DC    AL1(08)             LFR                                          
         DC    X'7F'               M-SU                                         
         DC    AL2(1000)           1000P                                        
         DC    AL2(0200)           2A                                           
         DC    CL10'EN   C    '    EXCLUDE NEWS,TZ=C+P                          
*                                                                               
         DC    AL1(09)             SPORTS                                       
         DC    X'03'               SA-SU                                        
         DC    AL2(1000)           10A                                          
         DC    AL2(1600)           6P                                           
         DC    CL10'IS        '    SPORTS ONLY                                  
*                                                                               
         DC    AL1(09)             SPORTS                                       
         DC    X'7F'               M-SU                                         
         DC    AL2(1930)           730P                                         
         DC    AL2(2200)           10P                                          
         DC    CL10'IS        '    SPORTS ONLY                                  
*                                                                               
         DC    AL1(10)             KID                                          
         DC    X'02'               SA                                           
         DC    AL2(0700)           7A                                           
         DC    AL2(1200)           12N                                          
         DC    CL10'          '                                                 
*                                                                               
         DC    X'FF'                                                            
         EJECT                                                                  
*        WORK DSECT                                                             
         SPACE 2                                                                
WKD      DSECT                                                                  
RELO     DS    F                                                                
APARM    DS    A                                                                
*                                                                               
PARMS    DS    0XL24                                                            
ADBLK    DS    A                   A(DBLOCK)                                    
ASTATN   DS    A                   A(STATION DESC)                              
ADTATAB  DS    A                   A(DATA TABLE)  NDEMS*NDPTS*4                 
TSCAN    DS    A                   A(TSCAN)                                     
DEMLIST  DS    F                   DEMO LIST                                    
         ORG   DEMLIST                                                          
NDEMS    DS    X                   NUMBER OF DEMOS                              
         DS    F                                                                
*                                                                               
DMCB     DS    6F                                                               
DUB      DS    D                                                                
         DS    0F                                                               
DOUTS    DS    XL(25*4)            25 DEMOS                                     
DOUTS2   DS    XL(25*4)                                                         
TYPRET   DS    C                                                                
BYTE     DS    X                                                                
         DS    0F                                                               
WORK     DS    XL64                                                             
*                                                                               
WKL      EQU   *-WKD                                                            
         SPACE 3                                                                
*        STATION TABLE DSECT                                                    
STNTABD  DSECT                                                                  
STNID    DS    CL5                                                              
STNKLEN  EQU   *-STNID                                                          
STNADI   DS    CL3                                                              
STNDMA   DS    CL3                                                              
STNAFF   DS    CL3                                                              
STNTZ    DS    CL1                                                              
STNMCLS  DS    CL1                                                              
STNLTST  DS    CL4                                                              
STNSTAT  DS    CL1                                                              
STNLEN   EQU   *-STNTABD                                                        
         SPACE 2                                                                
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE DEDBLOCK                                                       
       ++INCLUDE DEDEMFILE                                                      
         PRINT ON                                                               
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004MPDPTDEM  05/01/02'                                      
         END                                                                    
