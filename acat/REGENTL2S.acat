*          DATA SET REGENTL2S  AT LEVEL 035 AS OF 06/26/00                      
*CATALP REGENTL2                                                                
         TITLE 'REGENTL2 - REPPAK CONTRACT TOTAL ROUTINE'                       
*              P1 = A(BUCKETS)                                                  
*              P2 = A(4 X 80 = 320 OUTPUT AREA)                                 
*                   BYTE 0 = NUMBER OF LINES ON RETURN                          
*                                                                               
*******************************************************************             
*                                                                 *             
*        REGENTL2 --- REPPAK CONTRACT TOTAL ROUTINE               *             
*                                                                 *             
* --------------------------------------------------------------- *             
* UPDATE HISTORY:                                                 *             
*                                                                 *             
* 20DEC89  (EFJ) --- HISTORY LOST                                 *             
*                                                                 *             
* 20DEC89  (EFJ) --- MOVED TOTALSPOTS TO NEXT LINE ON REPORT      *             
*                                                                 *             
* 29AUG90  (EFJ) --- FIX 'EXTRA LINE' PROBLEM CAUSED BY 20DEC89   *             
*                                                                 *             
* 28SEP90  (EFJ) --- FIX '0 LINES' PROBLEM CAUSED BY 29AUG90 FIX  *             
*                                                                 *             
* 16NOV92  (SKU) --- DON'T PRINT AN EMPTY () IN GRAND TOTALS      *             
*                                                                 *             
*******************************************************************             
*        THIS MODULE DISPLAYS 4 LINES OF CONTRACT TOTALS                        
REGENTL2 CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 15,REGENTL2,CLEAR=YES                                            
         USING WORKD,RC                                                         
         L     R8,0(R1)            BUCKETS                                      
         L     R9,4(R1)            OUTPUT                                       
         XC    0(250,R9),0(R9)                                                  
         XC    250(70,R9),250(R9)                                               
* BUILD LIST OF MONTHS                                                          
         ST    R1,SAVER1                                                        
         LA    R6,1                LINE CTR                                     
         MVI   4(R1),2                                                          
         CLC   0(2,R8),=H'2'                                                    
         BE    TOTAL                                                            
         LA    R3,2(R8)            FIRST BUCKET                                 
* GET LAST MONTH                                                                
         MVC   DUB(2),0(R8)        LENGTH                                       
         LH    R5,DUB                                                           
         AR    R5,R3                                                            
         SH    R5,=H'3'                                                         
         SR    R4,R4                                                            
         LA    R7,MONTHS                                                        
*                                                                               
         MVC   DUB(2),2(R3)        YR-MON                                       
         IC    R4,1(R3)                                                         
         BXLE  R3,R4,*-10                                                       
         LA    R2,3                                                             
* LAST MONTH NOW IN DUB(2)                                                      
         SR    R5,R5                                                            
         IC    R4,4(R8)            FIRST YR                                     
         IC    R5,5(R8)            FIRST MON                                    
TOT50    STC   R4,0(R7)                                                         
         STC   R5,1(R7)                                                         
*                                                                               
         CLC   0(2,R7),DUB         LAST MONTH?                                  
         BNL   TOT100                                                           
         LA    R5,1(R5)            NEXT MONTH                                   
         CH    R5,=H'13'                                                        
         BL    *+12                                                             
         LA    R4,1(R4)            NEXT YR                                      
         LA    R5,1                                                             
         LA    R7,2(R7)                                                         
         BCT   R2,TOT50                                                         
         LA    R2,3                                                             
         LA    RE,MONTHS+30                                                     
         CR    RE,R7                                                            
         BE    TOT50                                                            
         BL    TOT100                                                           
         MVC   0(2,R7),=2X'FF'     NULL MONTHS                                  
         LA    R7,2(R7)                                                         
         LA    R6,1(R6)            LINE CTR                                     
         B     TOT50                                                            
         SPACE 2                                                                
* MONTH LIST BUILT - GET DATA                                                   
TOT100   L     R1,SAVER1                                                        
         STC   R6,4(R1)            NUMBER OF LINES                              
         CLI   4(R1),1             ALWAYS AT LEAST 2 LINES                      
         BNE   *+8                                                              
         MVI   4(R1),2                                                          
         LA    R3,2(R8)            FIRST BUCKET                                 
         SR    R4,R4                                                            
         LA    R7,MONTHS                                                        
*                                                                               
* CONVERT MONTHS TO OUTPUT                                                      
TOT150   SR    RE,RE                                                            
         IC    RE,1(R7)            MONTH                                        
         MH    RE,=H'3'                                                         
         LA    RE,MONTABLE-3(RE)                                                
         MVC   0(3,R9),0(RE)                                                    
         SR    RE,RE                                                            
         IC    RE,0(R7)            YEAR                                         
         CVD   RE,DUB                                                           
         UNPK  3(2,R9),DUB                                                      
         OI    4(R9),X'F0'                                                      
*                                                                               
* TEST FOR ANY MONTH DATA                                                       
         CLC   0(2,R7),2(R3)       SAME YR-MON?                                 
         BNE   TOT200                                                           
         TM    6(R3),X'80'         IS IT A NEGATIVE VALUE?                      
         BO    TOT155                                                           
         CLC   =F'9999999',6(R3)   NEED TO DROP PENNIES FOR SPACE?              
         BNH   TOT155              YES                                          
         EDIT  (4,6(R3)),(12,7(R9)),2,MINUS=YES  NO - PRINT W/PENNIES           
         B     TOT156                                                           
TOT155   DS    0H                                                               
         SR    R0,R0                                                            
*                                                                               
         TM    6(R3),X'80'         IS IT A NEGATIVE VALUE?                      
         BZ    *+10                                                             
         LA    R0,1                                                             
         LNR   R0,R0                                                            
*                                                                               
         L     R1,6(R3)                                                         
         D     R0,=F'100'          DROP PENNIES                                 
         EDIT  (R1),(12,7(R9)),MINUS=YES                                        
TOT156   DS    0H                                                               
*                                                                               
* TEST IF SPOTS WILL FIT                                                        
         MVC   DUB(4),10(R3)                                                    
         L     R6,DUB                                                           
         LTR   R6,R6                                                            
         BNP   TOT175                                                           
         CH    R6,=H'9'                                                         
         BH    TOT160                                                           
         MVC   5(3,R9),=C'( )'                                                  
         EDIT  (R6),(1,6(R9))                                                   
         B     TOT175                                                           
*                                                                               
TOT160   CH    R6,=H'99'                                                        
         BH    TOT162                                                           
         CLI   8(R9),C' '                                                       
         BNE   TOT175                                                           
         MVC   5(4,R9),=C'(  )'                                                 
         EDIT  (R6),(2,6(R9))                                                   
         B     TOT175                                                           
*                                                                               
TOT162   CH    R6,=H'999'                                                       
         BH    TOT175                                                           
         CLI   9(R9),C'$'                                                       
         BE    *+12                                                             
         CLI   9(R9),C' '                                                       
         BNE   TOT175                                                           
         MVC   5(5,R9),=C'(   )'                                                
         EDIT  (R6),(3,6(R9))                                                   
*                                                                               
* ADD TO TOTALS                                                                 
TOT175   MVC   DUB(8),6(R3)                                                     
         LM    RE,RF,DUB                                                        
         A     RE,TOTDOL                                                        
         A     RF,TOTSPOTS                                                      
         STM   RE,RF,TOTDOL                                                     
*                                                                               
* GET NEXT BUCKET                                                               
         IC    R4,1(R3)            BUCKET LENGTH                                
         AR    R3,R4                                                            
* NEXT MONTH                                                                    
TOT200   LA    R7,2(R7)                                                         
         LA    R9,20(R9)           NEXT OUTPUT                                  
****     CLI   0(R7),0             LAST MONTH?                                  
****     BE    TOTAL                                                            
         OC    0(2,R7),0(R7)       ANY YR-MONTH IN FIELD?                       
         BZ    TOTAL               NO                                           
         CLI   0(R7),X'FF'         NULL MONTH? (EVERY 4TH MONTH)                
         BNE   TOT150                                                           
         LA    R7,2(R7)                                                         
         LA    R9,20(R9)                                                        
         B     TOT150                                                           
* DISPLAY TOTALS                                                                
TOTAL    L     R1,SAVER1                                                        
         L     R9,4(R1)            OUTPUT                                       
         EDIT  (4,TOTDOL),(12,64(R9)),2,MINUS=YES                               
         CLI   64(R9),C','                                                      
         BNE   *+10                                                             
         MVC   64(1,R9),WORK+4                                                  
*         CLI   75(R9),C'-'                                                     
*         BNE   TOTAL50                                                         
*         EDIT  (4,TOTSPOTS),(3,76(R9))                                         
*         B     TOTAL100                                                        
*                                                                               
*TOTAL50  EDIT  (4,TOTSPOTS),(4,75(R9))                                         
TOTAL100 CLI   65(R9),C' '                                                      
         BNE   TOTAL150                                                         
         MVC   60(5,R9),=C'TOTAL'                                               
         MVI   140(R9),C'('                                                     
         EDIT  (4,TOTSPOTS),(5,141(R9)),ALIGN=LEFT                              
         B     TOTAL200                                                         
TOTAL150 MVC   59(5,R9),=C'TOTAL'                                               
         MVI   139(R9),C'('                                                     
         EDIT  (4,TOTSPOTS),(5,140(R9)),ALIGN=LEFT                              
TOTAL200 DS    0H                                                               
         LA    R1,140(R9)                                                       
TOTAL210 CLI   0(R1),C' '                                                       
         BE    TOTAL220                                                         
         LA    R1,1(R1)                                                         
         B     TOTAL210                                                         
TOTAL220 MVI   0(R1),C')'                                                       
*                                                                               
         CLC   =C'()',139(R9)      IF NOTHING, THEN DON'T SHOW IT               
         BE    TOTAL230                                                         
         CLC   =C'()',140(R9)      IF NOTHING, THEN DON'T SHOW IT               
         BNE   XMOD                                                             
*                                                                               
TOTAL230 DS    0H                                                               
         MVC   139(3,R9),=C'   '                                                
         B     XMOD                                                             
XMOD     XMOD1 1                                                                
MONTABLE DC    C'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'                          
*                                                                               
WORKD    DSECT                                                                  
MONTHS   DS    17H                                                              
TOTDOL   DS    F                                                                
TOTSPOTS DS    F                                                                
DUB      DS    D                                                                
SAVER1   DS    F                                                                
WORK     DS    CL17                                                             
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'035REGENTL2S 06/26/00'                                      
         END                                                                    
