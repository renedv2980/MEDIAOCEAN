*          DATA SET FATOSTR    AT LEVEL 004 AS OF 08/13/14                      
*CATALP FATOSTR                                                                 
         TITLE 'TOSTR  - STEREO 3270 TERMINAL OUTPUT TRANSLATOR'                
***********************************************************************         
* THIS MODULE CONVERTS DATA FLDS IN THE TWA TO A STEREO 3270          *         
* OUTPUT STRING IN A BUFFER. PARAMS VIA R1 ARE                        *         
* CL1  INPUT  0=TRANSMIT CHANGED FLDS  1=TRANSMIT ALL FLDS            *         
*      OUTPUT 0=WRITE                  1=ERASE/WRITE                  *         
* AL3  A(TWA)                                                         *         
* AL4  A(OUTPUT BUFFER)                                               *         
* AL4  A(UTL ENTRY)                                                   *         
* AL4  A(TRANSLATOR I/O BLOCK)                                        *         
***********************************************************************         
         PRINT NOGEN                                                            
TOSTR    CSECT                                                                  
         NMOD1 WORKX-TIOBD,**TOSTR*,RA                                          
         USING TIOBD,RC                                                         
         SAM24                     FORCE 24 BIT MODE                            
*                                                                               
         ST    R1,SAVEAPL                                                       
         L     RE,12(R1)                                                        
         MVC   TIOBD(TIOBL),0(RE)  MOVE TIOB TO MY W/S                          
         SR    RE,RE                                                            
         ICM   RE,7,1(R1)                                                       
         ST    RE,ATWA             SAVE A(TWA)                                  
         LA    RE,64(RE)                                                        
         ST    RE,AFSTFH           SAVE A(FIRST FIELD HEADER)                   
*                                                                               
         GOTO1 =V(PROTOFF)                                                      
*                                                                               
         XC    DATAROW,DATAROW                                                  
         XC    NEXTROW,NEXTROW                                                  
         MVI   COPIED,C'N'                                                      
         L     RF,=V(SSB)                                                       
         MVC   TWALEN,SSBTWAL-SSBD(RF)                                          
         MVI   XLAT,0              CLEAR CHARACTER SET FALGS                    
         TM    SSBSTAT6-SSBD(RF),SSB6ALLC                                       
         BZ    *+8                                                              
         OI    XLAT,X'01'          SET FACPAK USES ALL CHR TRANSLATE            
*                                                                               
         L     RE,8(R1)            POINT TO UTL ENTRY                           
         USING UTLD,RE                                                          
         CLC   LASTSIN,TSIN        TEST FOR MINI DUMP LOOP                      
         BE    MDLOOP                                                           
         MVC   LASTSIN,TSIN        SAVE LAST SIN                                
         MVC   STAT6,TSTAT6        SAVE UTL STATUS BYTE 6                       
         MVC   STAT8,TSTAT8        SAVE UTL STATUS BYTE 8                       
         TM    STAT6,TST6IAMS      =IAM SOURCE TERMINALS XMIT ALL               
         BZ    *+8                                                              
         MVI   0(R1),1                                                          
*                                                                               
INIT0    MVI   NONO,0              SET NO SPECIAL MESSAGE                       
         TM    TSTAT5,TST5NONO     TEST UPDATES INHIBITED                       
         BZ    INIT0J                                                           
         OI    NONO,X'01'          THIS TRANS HAD UPDATES INHIBITED             
         NI    TSTAT5,255-TST5NONO                                              
*                                                                               
INIT0A   TM    TTEST,TTESTUEN      NO MESSAGE IF CONNECTED WITH U=N             
         BO    INIT0J                                                           
         CLC   TSYS(2),=X'0A0C'    NO MESSAGE FOR CON/MAD                       
         BE    INIT0J                                                           
         L     RF,ATWA             RF=A(TWA)                                    
         CLC   72(8,RF),=C'<FALINK>'                                            
         BE    INIT0J              NO MESSAGE FOR FALINK SCREEN                 
*                                                                               
INIT0B   L     RF,=V(SSB)          RF=A(SSB)                                    
         USING SSBD,RF                                                          
         TM    SSBSYSFL,X'80'      IS THIS IS TEST FACPAK                       
         BZ    INIT0D              NO                                           
*                                                                               
INIT0C   EQU   *                   RULES FOR TEST FACPAKS                       
*&&US                                                                           
         CLI   SSBSYSID,1          TST GETS MESSAGE                             
         BE    INIT0E                                                           
         CLI   SSBSYSID,6          MEL GETS MESSAGE                             
         BE    INIT0E                                                           
*&&                                                                             
*&&UK                                                                           
         CLI   SSBSYSID,3          TTS GETS MESSAGE                             
         BE    INIT0E                                                           
         CLI   SSBSYSID,5          NEW GETS MESSAGE                             
         BE    INIT0E                                                           
*&&                                                                             
         B     INIT0J              OTHER TEST SYSTEMS GET NO MESSAGE            
*                                                                               
INIT0D   EQU   *                   RULES FOR PRODUCTION FACPAKS                 
*&&US                                                                           
         CLI   SSBDAYNO,7          ON A SUNDAY                                  
         BNE   *+12                                                             
         TM    SSBSYSFL,X'20'      REP SYSTEM DOESN'T WANT THIS MESSAGE         
         BO    INIT0J                                                           
*&&                                                                             
INIT0E   GOTO1 =V(GETTXT),DUB,X'FF00000A' GET DW/0010 MESSAGE                   
         DROP  RF                                                               
         OI    NONO,X'02'          SET HAVE OUTPUT DW/0010 MSG                  
*                                                                               
INIT0F   L     R1,SAVEAPL          POSITION CURSOR TO MESSAGE FIELD             
         OI    TIOBINDS,TIOBSETC   FORCE CURSOR TO MESSAGE FIELD                
         LA    RF,64                                                            
         STCM  RF,3,TIOBCURD                                                    
         XC    TIOBCURI,TIOBCURI                                                
         XC    TIOBCURS,TIOBCURS                                                
         L     RE,8(R1)            POINT TO UTL ENTRY                           
*                                                                               
INIT0J   CLC   TSVCREQ,=X'0000'    TEST APPLICATION PROGRAM                     
         BNE   INIT1                                                            
         TM    TSTAT2,TSTATTCT     TEST HAVE JUST CONNECTED                     
         BZ    INIT1                                                            
         NI    TSTAT2,255-TSTATTCT                                              
         B     INIT1               *NOP* REMOVE THIS TO ACTIVATE CODE           
         L     R3,AFSTFH                                                        
         MVC   SVFSTFH(68),0(R3)   SAVE FIRST FIELD HDR/DATA                    
         BRAS  RE,WHODAT                                                        
         OI    NONO,X'04'          SET HAVE PUT WHOAMI DATA IN FIELD            
*                                                                               
INIT1    L     R1,SAVEAPL          POINT TO UTL ENTRY                           
         L     RE,8(R1)                                                         
         L     RF,=V(SSB)          SET SYS FROM TCBSWSOV                        
         L     RF,SSBTKADR-SSBD(RF)                                             
         MVC   SYS,TCBSWSOV-TCBD(RF)                                            
         CLI   SYS,0                                                            
         BNE   *+10                                                             
         MVC   SYS,TOVSYS          OR FROM TOVSYS IF NOT SET                    
*                                                                               
         MVC   CTRY,TCTRY          SAVE TERMINALS CTRY/LANG/SYSTEM              
         MVC   LANG,TLANG                                                       
         MVI   XTND,0              SET NO EXTENDED ATTRIBUTES                   
         XC    XCOLMASK,XCOLMASK                                                
         CLC   TSVCREQ,=X'0000'                                                 
         BE    *+8                                                              
         MVI   SYS,1                                                            
         TM    TCLRMSK,X'80'       TEST COLOUR MASK ON                          
         BZ    INIT4                                                            
         TM    TTYPE,X'04'         TEST EXTENDED ATTRIBUTE TERMINAL             
         BZ    INIT4                                                            
         MVI   XTND,1                                                           
         TM    TCLRMSK,X'10'       TEST IGNORE OVERIDE FLAG                     
         BZ    *+12                                                             
         NI    TCLRMSK,X'EF'                                                    
         B     INIT4                                                            
         TM    TCLRMSK,X'40'       TEST OVERIDE COLOURS IN MASK                 
         BZ    INIT4                                                            
         ICM   R2,15,TCLRMSK       EXPAND COLOUR MASK INTO BYTE VALUES          
         LA    R4,XCOLMASK+7                                                    
         LA    RF,8                                                             
INIT2    SRDL  R2,4                EXPAND MASK INTO BYTE VALUES                 
         SRL   R3,28                                                            
         STC   R3,0(R4)                                                         
         AHI   R4,-1                                                            
         BCT   RF,INIT2                                                         
*                                                                               
INIT4    TM    XLAT,X'01'          TEST IF USING ALL CHRS VALID TABLE           
         BZ    INIT4A                                                           
*NOP*    CLI   CTRY,2              ONLY FOR UK/US                               
*NOP*    BH    INIT4A                                                           
         MVC   OUTXLAT,=A(OUTALLC)                                              
         B     INIT5                                                            
INIT4A   LLC   RF,CTRY             POINT TO TRANSLATE TABLE FOR CTRY            
         CHI   RF,15                                                            
         BNH   *+6                                                              
         SR    RF,RF                                                            
         SLL   RF,2                                                             
         LA    RF,CTRYXLOT(RF)                                                  
         MVC   OUTXLAT,0(RF)       SAVE A(OUTPUT TRANSLATE TABLE)               
*                                                                               
INIT5    ST    R1,SAVER1           OUTPUT STEREO INFO IN S/R FIELD              
         STM   RD,RE,SAVERD                                                     
         TM    STAT6,TST6STRO+TST6STFU                                          
         BZ    INIT5X                                                           
         L     R4,AFSTFH           LOCATE FIRST FIELD                           
         LLC   RF,0(R4)                                                         
         AR    R4,RF                                                            
         CLI   8(R4),C'*'          CHECK FOR * IN 1ST CHR                       
         BNE   INIT5S              NO MUST BE A SERVICE REQ                     
         TM    STAT6,TST6STRO                                                   
         BZ    INIT5X              EXIT IF SHOW S/R ONLY (S=NY)                 
*                                                                               
INIT5A   MVC   DUB+0(1),TCOSYS     SET SYSTEM                                   
         TM    TIOBINDS,TIOBSYS    TEST TO USE TOVSYS                           
         BZ    *+10                                                             
         MVC   DUB+0(1),TOVSYS                                                  
         MVC   DUB+1(1),TPRG       SET PROGRAM                                  
         MVI   DUB+2,X'FF'         SET SCREEN (DEFAULT)                         
INIT5B   SR    R5,R5               LOCATE LAST SCREEN OVERLAY NUMBER            
         ICM   R5,1,TSCRNE                                                      
         BP    *+12                                                             
         LA    R5,DUB+2                                                         
         B     INIT5C                                                           
         AHI   R5,-1                                                            
         MHI   R5,3                                                             
         LA    R5,TSCRNUM(R5)                                                   
INIT5C   TM    TIOBINDS,TIOBSCRN   TEST SCREEN NUM IN TIOBCNT(1)                
         BZ    *+10                                                             
         MVC   0(1,R5),TIOBCNT                                                  
         MVC   DUB+2(1),0(R5)      SET SCREEN                                   
         SR    R1,R1                                                            
INIT5D   TM    TIOBINDS,TIOBSUBS   TEST SUBSCREEN NUM IN TIOBAID                
         BZ    *+8                                                              
         IC    R1,TIOBAID                                                       
         SLL   R1,4                                                             
         STC   R1,DUB+3            SET SUBSCREEN                                
         MVC   DUB+4(1),TAGCTRY                                                 
         NI    DUB+4,X'0F'                                                      
         OC    DUB+3(1),DUB+4      SET COUNTRY                                  
         MVC   SVSCRNID,DUB                                                     
*                                                                               
         GOTO1 =V(HEXOUT),DMCB,DUB,(0,8(R4)),4,(24,0)                           
*                                                                               
         MVC   16(2,R4),SPACES                                                  
         TM    TIOBINDS,TIOBASP    TEST ALTERNATE SCREEN PROGRAM SET            
         BZ    INIT5E                                                           
         MVI   16(R4),C'1'         FIRST SET OF ALTERNATE SCREENS               
         MVI   17(R4),C'0'         SPARE - KEEP THINGS EVEN FOR NOW             
*                                                                               
INIT5E   MVI   08(R4),C'*'                                                      
         OI    06(R4),X'80'        TRANSMIT STEREO DATA *SPPSSXC                
         B     INIT5X                                                           
*                                                                               
INIT5S   CLI   TSVCREQ+1,0         TEST IF WELL DEFINED S/R                     
         BE    INIT5T                                                           
         GOTO1 =V(HEXOUT),DMCB,TSVCREQ+1,(0,23(R4)),1,(24,0)                    
         B     INIT5V                                                           
INIT5T   OC    08(17,R4),08(R4)    TEST IF EMPTY S/R FIELD                      
         BZ    INIT5X                                                           
         CLI   TSYS,0                                                           
         BNE   INIT5U                                                           
         MVC   23(2,R4),=C'10'     SET CONNECT VALUE                            
         B     INIT5V                                                           
INIT5U   MVC   23(2,R4),=C'00'     SET DONT KNOW VALUE                          
         CLC   08(7,R4),=C'=SEARCH'                                             
         BE    INIT5U1                                                          
         CLC   08(3,R4),=C'=SRC'                                                
         BNE   INIT5V                                                           
INIT5U1  MVC   23(2,R4),=C'2A'     =SEARCH,N *SPP*2A                            
         MVC   DUB+0(1),TCOSYS                                                  
         MVC   DUB+1(1),TPRG                                                    
         GOTO1 =V(HEXOUT),DMCB,DUB,(0,18(R4)),2,(24,0)                          
         MVI   18(R4),C'*'                                                      
INIT5V   OC    08(17,R4),SPACES                                                 
         MVI   8(R4),C'='                                                       
         MVI   22(R4),C'*'                                                      
INIT5W   OI    6(R4),X'80'         TRANSMIT *SR IN LAST THREE BYTES             
*                                                                               
INIT5X   L     R1,SAVER1                                                        
         L     RE,SAVERE                                                        
*                                                                               
HLP      TM    TFLAG,TFLAGHLP      TEST IF HELP PANEL ON SCREEN                 
         BZ    HLPX                                                             
         CLC   TSVCREQ+1(1),$PFK+1 IGNORE IF $PFK S/R                           
         BE    HLPX                                                             
         CLC   TSVCREQ+1(1),$HLP+1 IGNORE IF $HELP S/R                          
         BE    HLPX                                                             
         MVI   0(R1),1             SET TO XMIT ALL FIELDS                       
         NI    TFLAG,255-TFLAGHLP  AND TURN OFF HELP PANEL FLAG                 
HLPX     EQU   *                                                                
         DROP  RE                                                               
*                                                                               
XMT      CLI   0(R1),1             XMT ALL IF BEFORE=AFTER=YES                  
         BE    XMTX                                                             
         L     R3,AFSTFH                                                        
         SR    R0,R0                                                            
XMT1     ICM   R0,1,0(R3)          SEARCH FOR END OF TWA                        
         BZ    XMT3                                                             
*                                                                               
         CLI   1(R3),NOPFLD                                                     
         BE    XMT2                                                             
*                                                                               
         TM    1(R3),FATBXHDR      TEST EXTENDED FLDHDR                         
         BZ    XMT1A                                                            
         CHI   R0,17                                                            
         BNL   XMT2                                                             
         DC    H'0'                                                             
*                                                                               
XMT1A    CHI   R0,9                                                             
         BNL   XMT2                                                             
         DC    H'0'                DIE IF INVALID FIELD IN TWA                  
*                                                                               
XMT2     AR    R3,R0                                                            
         B     XMT1                                                             
*                                                                               
XMT3     CLI   1(R3),0             TEST BEFORE                                  
         BNE   *+12                                                             
         CLI   2(R3),0             TEST AFTER                                   
         BE    XMTX                                                             
         MVI   0(R1),1             SET TO XMT ALL FIELDS                        
XMTX     EQU   *                                                                
*                                                                               
         BRAS  RE,SCRSPLT                                                       
                                                                                
***********************************************************************         
* READ THROUGH TWA PROCESSING EACH FIELD BUILDING                     *         
* RESULT IN STEREO DATA BUFFER WORK AREA, STDBUF                      *         
***********************************************************************         
FSTFLD   OC    4(4,R1),4(R1)       TEST A(OUTPUT AREA) PARAMETER OK             
         BZ    EXIT                                                             
         MVC   XMTFLAG,0(R1)       SAVE XMIT ALL FLAG                           
         LA    R2,STDBUF           R2=A(STEREO DATA BUFFER WORK AREA)           
         ST    R1,SAVER1                                                        
*                                                                               
         LR    RE,R2               CLEAR STD BUFF                               
         ICM   RF,15,=AL4(L'STDBUF)                                             
         LA    R0,*                                                             
         L     R1,=F'0'                                                         
         MVCL  RE,R0                                                            
         L     R1,SAVER1                                                        
*                                                                               
*                                  BUILD STEREO MESSAGE HEADER                  
FFLD003  L     RE,=V(SSB)          GET FACPAK ID FROM SSB                       
         LLC   RF,SSBSYSID-SSBD(RE)                                             
         CLI   SSBSYSID-SSBD(RE),10                                             
         BL    FFLD005                                                          
         AHI   RF,-10              CONVERT IT TO A LETTER                       
         LA    RF,C'A'(RF)                                                      
         B     *+8                                                              
*                                                                               
FFLD005  LA    RF,C'0'(RF)         CONVERT IT TO A NUMBER                       
         STC   RF,1(R2)                                                         
         L     RE,8(R1)            GET THE SESSION NUMBER FROM THE UTL          
         LLC   RF,TSESSION-UTLD(RE)                                             
         LA    RF,C'A'(RF)         CONVERT IT TO A LETTER                       
         STC   RF,2(R2)                                                         
         MVI   ROWNUM,X'FF'                                                     
*                                                                               
FFLD007  L     R3,AFSTFH           R3=A(NEXT TWA FIELD)                         
         USING FLDHDRD,R3                                                       
         L     RE,8(R1)            TEST STEREO OPEN PROGRAM                     
         USING UTLD,RE                                                          
         CLI   TSVCREQ+1,X'21'     IF =PC TREAT AS A NORMAL SCREEN              
         BE    FFLD020                                                          
         CLI   TSVCREQ+1,X'EE'     IF =BYE NEED TO TEST FOR MAD                 
         BE    *+12                                                             
         CLI   TSVCREQ+1,0         ANY OTHER S/R DOESNT USE MAD                 
         BNE   FFLD011                                                          
*                                                                               
         SR    RF,RF               TEST IF CON/MAD (MAY HAVE SWITCHED)          
         ICM   RF,7,TARSYS                                                      
         BZ    FFLD011                                                          
         CLI   SESYS-SELISTD(RF),X'0A'                                          
         BNE   FFLD011                                                          
         CLI   TPRG-UTLD(RE),X'0C'                                              
         BE    MADPROG                                                          
         DROP  RE                                                               
                                                                                
***********************************************************************         
* TEST FOR STEREO OPEN FIELD SCREEN                                   *         
* SEARCH FOR 2 CONSECUTIVE 79 BYTE UNP FIELDS BEFORE ROW 6            *         
***********************************************************************         
FFLD011  LA    R0,79               DFLT TO 79 CHAR FLDS                         
*&&US                                                                           
         CLI   TPRG-UTLD(RE),X'1B'   TEST DEM                                   
         BNE   FFLD011A                                                         
         CLI   TOVSYS-UTLD(RE),X'02' TEST SPOT                                  
         BE    *+12                                                             
         CLI   TOVSYS-UTLD(RE),X'08' TEST REP                                   
         BNE   FFLD011A                                                         
         L     RE,AFSTFH                                                        
         CLC   8(8,RE),=C'<FALINK>'                                             
         BE    *+8                                                              
         LA    R0,78               DEM16 HAS 78 CHAR FIELDS                     
*&&                                                                             
FFLD011A STH   R0,CHKLEN                                                        
         AHI   R0,8                                                             
         STH   R0,CHKLEN8                                                       
*                                                                               
         IC    R0,FLDLEN                                                        
         AR    R3,R0                                                            
         IC    R0,FLDLEN                                                        
         AR    R3,R0               SKIP FIRST TWO FIELDS                        
*                                                                               
FFLD012  CLC   FLDADR,=H'401'      TEST PAST ROW 6/COL 2                        
         BH    FFLD020                                                          
         IC    R0,FLDLEN                                                        
         TM    FLDATB,FATBXHDR     TEST EXTENDED FLDHDR                         
         BZ    *+8                                                              
         AHI   R0,-8                                                            
         CH    R0,CHKLEN8          RIGHT FIELD LENGTH                           
         BNE   FFLD014                                                          
         TM    FLDATB,FATBPROT     TEST PROT                                    
         BZ    FFLD016             NO                                           
*                                                                               
FFLD014  IC    R0,FLDLEN           POINT TO NEXT FIELD                          
         AR    R3,R0                                                            
         CLI   FLDLEN,0            TEST E-O-S                                   
         BE    FFLD020                                                          
         B     FFLD012                                                          
*                                                                               
FFLD016  IC    R0,FLDLEN           NOW TEST FOR ANOTHER                         
         AR    R3,R0               POINT TO NEXT FIELD                          
*                                                                               
         IC    R0,FLDLEN                                                        
         TM    FLDATB,FATBXHDR     TEST EXTENDED FLDHDR                         
         BZ    *+8                                                              
         AHI   R0,-8                                                            
         CH    R0,CHKLEN8          RIGHT FIELD LENGTH ?                         
         BNE   FFLD014                                                          
         TM    FLDATB,FATBPROT     TEST PROT                                    
         BZ    STROPEN             NO - THIS IS SECOND 79 UNP FIELD             
         B     FFLD014             YES - SKIP                                   
*                                  IF ANY LINE CHANGES, XMIT ALL                
FFLD020  L     R3,AFSTFH                                                        
         LR    R0,R3                                                            
         SR    RF,RF                                                            
*                                                                               
FFLD022  TM    FLDOLEN,X'80'       THIS A NEW FIELD?                            
         BZ    FFLD030             NO                                           
         MVI   0(R1),1             ELSE SET XMIT ALL FLDS                       
         MVI   XMTFLAG,1                                                        
         B     FFLD040                                                          
*                                                                               
FFLD030  LLC   RF,FLDLEN                                                        
         AR    R3,RF                                                            
         CLI   0(R3),0                                                          
         BNE   FFLD022                                                          
*                                                                               
FFLD040  LR    R3,R0                                                            
         MVI   0(R2),SDBMHSTM      SET TO BE STEREO MESSAGE TYPE                
         CLI   XMTFLAG,1           CHECK TRASNMIT ALL FLAG SET                  
         BE    *+8                                                              
         MVI   0(R2),SDBMHSTC      ELSE FLAG XMIT CHANGED FIELD ONLY            
*                                                                               
*                                  TEST IF SESSION UNSWAPPABLE                  
         TM    STAT8,TST8DRTY      ONLY IF NEW (AKA DIRTY) STEREO MODE          
         BZ    FFLD050                                                          
         L     R1,SAVER1                                                        
         L     RE,8(R1)            GET SESSION NUMBER FROM UTL                  
         LLC   R1,TSESSION-UTLD(RE)                                             
         LLC   RF,SSACBITS(R1)                                                  
         EX    RF,*+8                                                           
         B     *+8                                                              
         TM    TSSXBITS-UTLD(RE),0                                              
         BZ    FFLD050                                                          
         MVI   0(R2),SDBMSSTM      SET TO BE STEREO MESSAGE TYPE                
         CLI   XMTFLAG,1           CHECK TRASNMIT ALL FLAG SET                  
         BE    *+8                                                              
         MVI   0(R2),SDBMSSTC      ELSE FLAG XMIT CHANGED FIELD ONLY            
*                                                                               
FFLD050  L     R1,SAVER1                                                        
         TR    0(1,R2),DECTRAN     TRANSLATE STEREO DECIMAL TO EBCDIC           
         LA    R2,3(R2)            BUMP POINTER PAST MESSAGE HEADER             
*                                                                               
         XC    FUNP,FUNP           (SBA)(1,1)(SF)(PROT)                         
*                                                                               
         MVI   CURSES,0                                                         
         MVI   NEWFLDS,0                                                        
*                                                                               
         MVI   LTATB,X'40'+FATBPROT  LAST TRANS ATB = (PROT)                    
         XC    LTATBX,LTATBX                                                    
         MVC   LTEND,=X'40C1'      LAST TRANS END = (1,2)                       
         MVI   LTFLAG,FOUTTRN      LAST TRANS FLAG= TRANS                       
                                                                                
***********************************************************************         
* PROCESS NEXT FIELD IN TWA - LOOP BACK HERE AFTER TRANS              *         
***********************************************************************         
NXTFLD   LLC   R6,FLDLEN           R6=LEN OF TWA FLD                            
         CHI   R6,9                TEST FOR VALID TWA FIELD                     
         BNL   NF10                                                             
         LTR   R6,R6               ZERO FOR END OF TWA                          
         BZ    ENDFLDS                                                          
NF10     LA    RF,0(R3,R6)         SET R9=A(NEXT ACTIVE FIELD)                  
         SR    R9,R9                                                            
         SR    R0,R0                                                            
NF20     CLI   0(RF),0                                                          
         BE    NF30                                                             
         TM    1(RF),NOPFLD                                                     
         BNO   *+14                                                             
         IC    R0,0(RF)                                                         
         AR    RF,R0                                                            
         B     NF20                                                             
         LR    R9,RF                                                            
*                                                                               
NF30     TM    FLDATB,NOPFLD       BYPASS NOP FIELDS                            
         BO    TRANSX                                                           
         CLI   FLDOLEN,X'FF'                                                    
         BNE   *+8                                                              
         OI    NEWFLDS,X'80'       SET ALL FIELDS ARE NOW NEW                   
         MVI   CURTHIS,0                                                        
         TM    STAT6,TST6STRO                                                   
         BZ    NF32                                                             
         TM    FLDATB,FATBPROT+FATBNUM                                          
         BO    TRANSX              BYPASS PROT/NUM FIELDS FOR STEREO            
*                                                                               
NF32     MVC   FATB,FLDOIND        CALC FLD ATB BYTE AT FATB                    
         OC    FATB,FLDATB                                                      
         NI    FATB,FATBPROT+FATBLOW+FATBMOD IGNORE NUMERIC ATTRIBUTE           
         TR    FATB,TRNTBL                                                      
         MVI   FATBX,0                                                          
*                                                                               
         OC    DATAROW,DATAROW     TEST STEREO TRANSMIT                         
         BZ    NF40                NO                                           
*                                                                               
         OC    NEXTROW,NEXTROW     SENT ANY DATA YET                            
         BZ    NF34                NO                                           
         CLC   FLDADR,NEXTROW      INTO NEXT ROW YET ?                          
         BL    NF36                NO                                           
                                                                                
***********************************************************************         
* STEREO WILL TRANSMIT ONLY UNP FIELDS UNTIL NEXTROW > DATAROW        *         
***********************************************************************         
NF34     LH    R0,FLDADR                                                        
         SRDL  R0,32                                                            
         D     R0,=F'80'                                                        
         LA    R1,1(R1)                                                         
         M     R0,=F'80'           GIVES ROW NUM-1 * 80                         
         STH   R1,NEXTROW          GIVES START OF NEXT ROW                      
*                                                                               
         CLC   NEXTROW,DATAROW     TEST REACHED FIRST DATAROW YET               
         BH    STR10               YES - RETURN TO STEREO CODE                  
         ST    R2,SAVER2           SAVE BUFFER POINTER                          
         XC    0(79,R2),0(R2)      CLEAR 79 BYTES                               
*                                                                               
NF36     TM    FLDATB,FATBPROT     TEST PROT                                    
         BO    NFX                 YES - IGNORE                                 
*                                                                               
NF40     SR    R4,R4               CALC FLD ATB ADR AT FSTART                   
         ICM   R4,3,2(R3)                                                       
         STH   R4,FSTRABS          SAVE ABSOLUTE ADR OF FIRST FLD CHR           
         AHI   R4,-1               ATTB ONE POSN BACK FROM START                
         STC   R4,FSTART+1                                                      
         NI    FSTART+1,X'3F'                                                   
         SRL   R4,6                                                             
         STC   R4,FSTART                                                        
         TR    FSTART,TRNTBL                                                    
*                                                                               
NF50     TM    FATB,FATBPROT       SAVE ADDR & ATB OF 1ST UNPROT FIELD          
         BO    NF60                                                             
         OC    FUNP,FUNP                                                        
         BNZ   NF60                                                             
         MVC   FUNP,FLDADR                                                      
*                                                                               
NF60     TM    LTFLAG,FOUTTRN      SAVE FATB IF PREV FLD TRANS                  
         BZ    NF70                                                             
         MVC   LTNATB,FATB                                                      
         MVC   LTNATBX(1),FATBX                                                 
*                                                                               
NF70     EQU   *                                                                
         TM    TIOBINDS,TIOBSETC   TEST SPECIAL CURSOR SETTING                  
         BZ    NF80                                                             
         TM    CURSES,X'02'        TEST CURSOR FIELD FOUND                      
         BO    NF80                                                             
         OC    TIOBCURD,TIOBCURD   TEST IF A TWA DISPLACEMENT                   
         BZ    NF72                                                             
         LR    RE,R3                                                            
         S     RE,ATWA                                                          
         CLM   RE,3,TIOBCURD       TEST THIS IS THE CORRECT FIELD               
         BNE   NF80                                                             
         A     RE,ATWA                                                          
         SR    RF,RF                                                            
         ICM   RF,3,2(RE)                                                       
         LLC   R0,TIOBCURI                                                      
         AR    RF,R0                                                            
         STCM  RF,3,CURADDR                                                     
         B     NF76                                                             
NF72     OC    TIOBCURS,TIOBCURS   TEST IF AN ABSOULTE ADDRESS                  
         BZ    NF80                                                             
         SR    RF,RF                                                            
         ICM   RF,3,FLDADR                                                      
         CLM   RF,3,TIOBCURS                                                    
         BE    NF74                CURSOR AT START OF THIS FIELD                
         BH    NF80                CURSOR BEFORE THIS FIELD                     
         LLC   RE,FLDLEN                                                        
         TM    FLDATB,FATBXHDR                                                  
         BZ    *+8                                                              
         AHI   RE,-8                                                            
         AHI   RE,-8                                                            
         AR    RF,RE                                                            
         CLM   RF,3,TIOBCURS                                                    
         BNH   NF80                                                             
NF74     MVC   CURADDR,TIOBCURS                                                 
NF76     OI    CURSES,X'02'        YES - SET CURSOR FIELD FOUND                 
         OI    CURTHIS,X'01'       FLAG CURSOR IN THIS FIELD                    
         B     NF80                                                             
*                                                                               
NF80     CLI   XMTFLAG,1           TEST TO TRANSMIT ALL FIELDS                  
         B     TRANS0              *NOP* BE - ALWAYS TRANSMIT ALL               
         TM    NEWFLDS,X'80'                                                    
         BZ    *+8                                                              
         OI    FLDOLEN,X'80'                                                    
         TM    FLDOIND,FOUTTRN                                                  
         BO    TRANS               TRANS THIS FIELD                             
         TM    FLDOLEN,X'80'                                                    
         BO    TRANS               TRANS THIS NEW FIELD                         
         TM    LTFLAG,X'02'                                                     
         BO    TRANS               TRANS THIS BECAUSE PREV WAS NEW              
         LTR   R9,R9                                                            
         BZ    *+12                                                             
         TM    7(R9),X'80'                                                      
         BO    TRANS               TRANS THIS BECAUSE NEXT IS NEW               
         TM    FLDOIND,FOUTCUR                                                  
         BO    TRANS               TRANS IF CURSOR ON FIELD                     
         TM    CURTHIS,X'01'                                                    
         BO    TRANS               TRANS IF CURSOR ON THIS FIELD                
         TM    LTFLAG,FOUTCUR                                                   
         BO    TRANSX                                                           
         MVI   LTFLAG,0            SET FIELD NOT TRANSMITTED                    
         B     TRANSX                                                           
*                                                                               
NFX      B     TRANSX              THIS BUMPS TO NXTFLD                         
*                                                                               
TRANS    OI    FLDOIND,FOUTTRN                                                  
*                                                                               
TRANS0   BAS   RE,FTRANS                                                        
*                                                                               
TRANSX   C     R3,AFSTFH           IS THIS THE FIRST FIELD                      
         BNE   TRANSXX                                                          
         TM    NONO,X'04'          IS THERE WHOAMI DATA IN FIRST FIELD          
         BZ    TRANSXX                                                          
         MVC   0(68,R3),SVFSTFH    RESTORE ORIGINAL FIRST FIELD DATA            
         NI    NONO,255-X'04'                                                   
         B     NXTFLD              BACK TO RESHIP THE FIRST FIELD               
*                                                                               
TRANSXX  LLC   R0,FLDLEN           BUMP TO NEXT FIELD IN TWA                    
         AR    R3,R0                                                            
         B     NXTFLD                                                           
*                                                                               
* HERE IF MINI DUMP LOOP DETECTED                                               
*                                                                               
MDLOOP   EQU   *                                                                
         LA    R2,STDBUF                                                        
         ST    R1,SAVER1                                                        
*                                  CLEAR STDBUF                                 
         LR    RE,R2                                                            
         ICM   RF,15,=AL4(L'STDBUF)                                             
         LA    R0,*                                                             
         L     R1,=F'0'                                                         
         MVCL  RE,R0                                                            
         L     R1,SAVER1                                                        
*                                  BUILD STEREO MESSAGE HEADER                  
         L     RE,=V(SSB)          GET FACPAK ID FROM SSB                       
         LLC   RF,SSBSYSID-SSBD(RE)                                             
         CLI   SSBSYSID-SSBD(RE),10                                             
         BL    MDL010                                                           
         AHI   RF,-10              MAKE IT A LETTER                             
         LA    RF,C'A'(RF)                                                      
         B     *+8                                                              
*                                                                               
MDL010   LA    RF,C'0'(RF)         MAKE IT A NUMBER                             
         STC   RF,1(R2)                                                         
         L     RE,8(R1)            GET THE SESSION NUMBER FROM UTL              
         LLC   RF,TSESSION-UTLD(RE)                                             
         LA    RF,C'A'(RF)         MAKE IT A LETTER                             
         STC   RF,2(R2)                                                         
         MVI   0(R2),SDBMHSTM                                                   
         TR    0(1,R2),DECTRAN                                                  
         MVC   3(L'MDMSG,R2),MDMSG MOVE OUT ERROR MESSAGE                       
         LA    R2,3+L'MDMSG(R2)                                                 
         ST    R1,SAVER1                                                        
         MVI   0(R2),X'03'         MARK END OF STDBUF WITH ETX CODE             
         BAS   RE,OUTSCRN          OUTPUT STANDARD STEREO SCREEN                
         L     R1,SAVER1                                                        
         B     EXIT                                                             
                                                                                
***********************************************************************         
* HERE AT END OF TWA FIELD PROCESSING                                 *         
* TEST FOR SESSION STOLEN                                             *         
* AFTER HAVING GONE THROUGH FIELD PROCESSING FOR SAKE OF DICTATE      *         
***********************************************************************         
ENDFLDS  L     R1,SAVEAPL                                                       
         L     RE,8(R1)            RE=A(UTL)                                    
         TM    STAT8,TST8DRTY      TEST IF NEW (DIRTY) STEREO MODE              
         BZ    EFLD010                                                          
         LLC   R1,TSESSION-UTLD(RE)                                             
         LLC   RF,SSACBITS(R1)                                                  
         EX    RF,*+8                                                           
         B     *+8                                                              
         TM    TNAHNAH-UTLD(RE),0                                               
         BZ    EFLD010                                                          
*                                  CLEAR STOLEN SESSION FLAG                    
         LLC   RF,SSNABITS(R1)                                                  
         EX    RF,*+8                                                           
         B     *+8                                                              
         NI    TNAHNAH-UTLD(RE),0                                               
         B     EFLD010             *NOP* DONT SEND THIS ANYMORE                 
         LA    R2,STDBUF           R2=A(STEREO DATA BUFFER WORK AREA)           
         MVI   0(R2),SDBMHSTS      SET TO STOLEN SESSION FLAGGED                
         TR    0(1,R2),DECTRAN     TRANSLATE STEREO DECIMAL TO EBCDIC           
         LA    R2,3(R2)            BUMP POINTER PAST MESSAGE HEADER             
*                                                                               
EFLD010  EQU   *                                                                
         MVI   0(R2),X'03'         MARK END OF STDBUF WITH ETX CODE             
         BAS   RE,OUTSCRN          OUTPUT STANDARD STEREO SCREEN                
         B     EXIT                                                             
*                                                                               
EXIT     CLI   COPIED,C'Y'         COPY BACK ORIGINAL TWA IF REQ.               
         BNE   EXITX                                                            
         L     R0,ATWA                                                          
         LH    R1,TWALEN                                                        
         LHI   RE,TWACOPY-TIOBD                                                 
         AR    RE,RC                                                            
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
EXITX    GOTO1 =V(PROTON)                                                       
         XMOD1 1                                                                
         EJECT                                                                  
***********************************************************************         
* MAD STEREO INTERFACE - TA0CFF AND FATOSTR                           *         
* LINE 1       60P/17U   3/60/16                                      *         
* LINE 2       60U/18U   1/60/18 (1 FROM ROW 1)                       *         
* LINES 3-24   79U       79                                           *         
***********************************************************************         
MADPROG  EQU   *                                                                
         MVI   0(R2),C'M'          SET STEREO MAD MESSAGE TYPE                  
         LA    R2,3(R2)            BUMP POINTER PAST MESSAGE HEADER             
*                                                                               
         MVC   0(60,R2),FLDDATA    PROCESS FIELD ROW 1/COLUMN 2                 
         LA    R2,60(R2)                                                        
         LA    R0,60                                                            
         BAS   RE,SPACFILL                                                      
*                                                                               
         LLC   R6,FLDLEN           PROCESS FIELD ROW 1/COLUMN 63                
         AR    R3,R6                                                            
         MVC   0(17,R2),FLDDATA                                                 
         LA    R2,17(R2)                                                        
         LA    R0,17                                                            
         BAS   RE,SPACFILL                                                      
*                                                                               
         LLC   R6,FLDLEN           PROCESS FIELD ROW 2/COLUMN 2                 
         AR    R3,R6                                                            
         MVC   0(60,R2),FLDDATA                                                 
         LA    R2,60(R2)                                                        
         LA    R0,60                                                            
         BAS   RE,SPACFILL                                                      
*                                                                               
         LLC   R6,FLDLEN           PROCESS FIELD ROW 2/COLUMN 63                
         AR    R3,R6                                                            
         MVC   0(18,R2),FLDDATA                                                 
         LA    R2,18(R2)                                                        
         LA    R0,18                                                            
         BAS   RE,SPACFILL                                                      
         LLC   R6,FLDLEN                                                        
*                                                                               
MADPNXT  EQU   *                   PROCESS REMAINING LINES ON SCREEN            
         AR    R3,R6                                                            
         ICM   R6,1,FLDLEN                                                      
         BZ    MADPEND                                                          
         MVC   0(79,R2),FLDDATA                                                 
         LA    R2,79(R2)                                                        
         LA    R0,79                                                            
         BAS   RE,SPACFILL                                                      
         B     MADPNXT                                                          
*                                                                               
MADPEND  EQU   *                                                                
         B     ENDFLDS                                                          
         EJECT                                                                  
***********************************************************************         
* PROCESS STEREO OPEN INTERFACE                                       *         
* ON ARRIVAL, R3 POINTS TO THE SECOND 79 BYTE UNP FIELD               *         
* MESSAGE TYPE = B FOR ROW 2, C FOR ROW 3, D FOR ROW 4, ...           *         
***********************************************************************         
STROPEN  EQU   *                                                                
         SR    R0,R0                                                            
         ICM   R0,3,FLDADR         GET FIELD ADDRESS                            
         SRDL  R0,32                                                            
         D     R0,=F'80'           GIVES CURRENT ROW-1                          
         STC   R1,0(R2)            SET START ROW ID IN BUFFER                   
         TR    0(1,R2),DECTRAN     TRANSLATE STEREO DECIMAL TO EBCDIC           
         LA    R2,3(R2)            BUMP POINTER PAST MESSAGE HEADER             
*                                                                               
         STC   R1,DATAROWN         SAVE DATA ROW NUMBER                         
         AHI   R1,-1                                                            
         MHI   R1,80                                                            
         LA    R1,1(R1)                                                         
         STH   R1,DATAROW          SAVE FLD ADDR OF FIRST DATA ROW              
*                                                                               
         LLC   R0,DATAROWN                                                      
         LA    RE,STDBUF+3         DON'T OVERWRITE CONTROL CHARS                
         XC    0(79,RE),0(RE)      CLEAR BUFFER UP TO DATAROW                   
         LA    RE,79(RE)                                                        
         BCT   R0,*-10                                                          
*                                                                               
         L     R3,AFSTFH                                                        
*                                  PROCESS FIELD ROW 1/COLUMN 2                 
         MVC   0(60,R2),FLDDATA                                                 
         LA    R2,60(R2)                                                        
         LA    R0,60                                                            
         BAS   RE,SPACFILL                                                      
*                                                                               
         LLC   R6,FLDLEN                                                        
         AR    R3,R6                                                            
*                                  PROCESS FIELD ROW 1/COLUMN 63                
         MVC   0(16,R2),FLDDATA+1  NOTE MOVES FROM FLDDATA+1(16)                
         LA    R2,16(R2)                                                        
         LA    R0,16                                                            
         BAS   RE,SPACFILL                                                      
*                                  NOTE 79 CHRS MOVED SO FAR                    
         LLC   R6,FLDLEN                                                        
         AR    R3,R6                                                            
         CLI   DATAROWN,2          TEST DATA STARTS ROW 2                       
         BNE   NXTFLD              GO TRANSMIT UP TO DATAROW                    
                                                                                
***********************************************************************         
* REACHED THE FIRST DATA ROW.                                         *         
* POSITION DATAROW-1*79.                                              *         
* DATA ROWS JUST MOVE OUT 78 OR 79 CHARS AND SPACE FILL               *         
***********************************************************************         
STR10    LLC   R0,DATAROWN                                                      
         AHI   R0,-1                                                            
         MHI   R0,79               DATALENGTH SO FAR                            
*                                                                               
         LA    R2,STDBUF                                                        
         AR    R2,R0               POINT TO DATAROW POSN IN STDBUF              
*                                                                               
STR12    MVC   0(79,R2),FLDDATA                                                 
         CLI   CHKLEN+1,79                                                      
         BE    *+8                                                              
         MVI   78(R2),0            USE NULL IF DATA IS ONLY 78                  
*                                                                               
         LHI   R0,79               R0 IS INPUT TO SPACFILL                      
         AR    R2,R0                                                            
         BAS   RE,SPACFILL                                                      
*                                                                               
         LLC   R6,FLDLEN           ADVANCE TO NEXT TWA FIELD                    
         AR    R3,R6                                                            
         CLI   0(R3),0                                                          
         BNE   STR12                                                            
*                                                                               
STROEND  EQU   *                                                                
         B     ENDFLDS                                                          
                                                                                
***********************************************************************         
*        SPACE FILL NULLS IN BUFFER AT (R2-1) FOR LENGTH (R0)         *         
***********************************************************************         
SPACFILL LR    RF,R2                                                            
         AHI   RF,-1                                                            
         CLI   0(RF),0                                                          
         BNE   *+12                                                             
         MVI   0(RF),C' '                                                       
         BCT   R0,*-16                                                          
         BR    RE                                                               
                                                                                
***********************************************************************         
* TRANSLATE FIELD FOR OUTPUT TO STEREO MESSAGE BUFFER                 *         
***********************************************************************         
FTRANS   NTR1                                                                   
         LLC   R6,FLDLEN                                                        
         LR    R7,R6               CALC FLD END AT FEND                         
         AHI   R7,-8                                                            
         TM    FLDATB,FATBXHDR     TEST EXTENDED FIELD HEADER                   
         BZ    *+8                                                              
         AHI   R7,-8                                                            
         MVC   FEND,FLDADR                                                      
         AH    R7,FEND                                                          
         CHI   R7,1919             LAST CHR ON SCR USED                         
         BNH   *+6                 NO                                           
         SR    R7,R7               YES SET FLD END TO 1ST CHR                   
         STH   R7,FEND                                                          
         NI    FEND+1,X'3F'                                                     
         SRL   R7,6                                                             
         STC   R7,FEND                                                          
         TR    FEND,TRNTBL                                                      
*                                  SET UP FLD DATA AT FDATA                     
FT10     LR    R7,R6                                                            
         AHI   R7,-9               R7=MAX FLD LEN - 1                           
         TM    FLDATB,FATBXHDR     TEST EXTENDED FIELD HEADER                   
         BZ    *+8                                                              
         AHI   R7,-8                                                            
         LTR   R7,R7               TEST CLOBBERED EXTENDED HEADER               
         BNM   *+6                                                              
         DC    H'0'                                                             
         EX    R7,MOVEIT                                                        
         STM   R1,R2,SAVER1                                                     
         EX    R7,TESTDDIT         TEST IF DATA DICTIONARY REQUIRED             
         BNZ   FT20                YES                                          
FT12     LA    R8,FDATA(R7)        POINT TO LAST CHR OF DATA                    
         LA    R7,1(R7)                                                         
         B     FT30                                                             
*                                                                               
FT20     MVI   P1+0,C'T'           SET TRANSLATE WHOLE FIELD                    
         MVI   P1+1,C'U'           SET CASE TO UPPER/LOWER                      
         TM    FLDATB,FATBLC                                                    
         BZ    *+8                                                              
         MVI   P1+1,C'L'                                                        
         MVC   P1+2(1),SYS         SET SYSTEM                                   
         MVC   P1+3(1),LANG        SET LANGUAGE                                 
         LA    RF,FDATA            SET ADR AND LEN OF DATA                      
         ST    RF,P2                                                            
         LA    RF,1(R7)                                                         
         STC   RF,P2                                                            
         ICM   RF,15,=V(DICTATE)   GO TO V(DICTATE) FOR TRANSLATION             
         BZ    FT22                                                             
         LA    R1,P1                                                            
         BASR  RE,RF                                                            
FT22     LM    R1,R2,SAVER1        RESTORE R1,R2 DESTROYED BY TRT               
         EX    R7,RESTORIT         MOVE TRANSLATED FIELD BACK TO TWA            
         B     FT12                                                             
*                                                                               
FT30     TM    FLDATB,FATBDEL      DELETE TRAILING BLANKS                       
         BO    *+16                NO                                           
         CLI   0(R8),C' '                                                       
         BNE   *+8                                                              
         MVI   0(R8),0                                                          
         CLI   0(R8),0                                                          
         BNE   FT32                                                             
         AHI   R8,-1                                                            
         BCT   R7,FT30                                                          
         B     *+12                                                             
FT32     LR    R7,R8                                                            
         LA    R8,FDATA-1                                                       
         SR    R7,R8                                                            
         NI    LTFLAG,X'FF'-FOUTMOD  SET FIELD NOT NEW                          
         TM    FLDOLEN,X'80'                                                    
         BZ    *+8                                                              
         OI    LTFLAG,FOUTMOD      SET NEW FIELD FLAG                           
         STC   R7,FLDOLEN          SET SIGNIFICANT FIELD LEN                    
         LR    R7,R6                                                            
         AHI   R7,-9                                                            
         TM    FLDATB,FATBXHDR     TEST EXTENDED FIELD HEADER                   
         BZ    *+8                                                              
         AHI   R7,-8                                                            
         L     RF,OUTXLAT          POINT TO TRANLATE TABLE FOR CTRY             
         EX    R7,CLEANIT          CLEAN OUTPUT CHRS FOR TRANSMISSION           
         EX    R7,RESTORIT         MOVE BACK TO TWA                             
*                                                                               
         MVC   COMPFLEN,FLDOLEN    COMPRESS THE DATA                            
         SR    RF,RF                                                            
         ICM   RF,1,FLDOLEN                                                     
         BZ    FT40                                                             
         ST    R1,SAVER1                                                        
         GOTO1 COMPRESS,DMCB,FDATA,(RF),COMBUF                                  
         ICM   RF,15,4(R1)                                                      
         L     R1,SAVER1                                                        
         STC   RF,COMPFLEN         SAVE COMPRESSED FIELD DATA LENGTH            
         AHI   RF,-1                                                            
         EX    RF,RESCOM           RESTORE FDATA FROM COMPRESS BUFFER           
*                                                                               
FT40     SR    RE,RE               SET CURRENT STEREO ROW                       
         SR    RF,RF                                                            
         ICM   RF,3,FLDADR                                                      
         D     RE,=F'80'                                                        
         CLM   RF,1,ROWNUM                                                      
         BE    FT50                SAME ROW                                     
*                                                                               
         STCM  RF,1,ROWNUM         OUTPUT SET ROW STEREO FIELD                  
         LA    RF,SDSRMIN(RF)      SET RF FOR TRANSLATION                       
         STC   RF,0(R2)            SET THE NEW ROW                              
         TR    0(1,R2),DECTRAN     TRANSLATE TO STERE0                          
         LA    R2,1(R2)            NEXT BYTE                                    
*                                                                               
FT50     SR    RE,RE               SET CURRENT STEREO COLUMN                    
         SR    RF,RF                                                            
         ICM   RF,3,FLDADR                                                      
         D     RE,=F'80'                                                        
         LA    RE,1(RE)                                                         
         STCM  RE,1,COLNUM                                                      
         ST    R1,SAVER1                                                        
*                                                                               
         TM    FLDATB,FATBXHDR     TEST FOR EXTENDED FIELD HEADER               
         BZ    FT60                                                             
         LLC   RE,FLDLEN                                                        
         AR    RE,R3                                                            
         AHI   RE,-8               POINT TO START OF EXT FLDHDR                 
         CLI   FLDXSFN-FLDXNUM(RE),0                                            
         BE    FT60                NO STEREO FLDNUM                             
         MVI   0(R2),SDSFNUM       MOVE STEREO FLDNUM                           
         TR    0(1,R2),DECTRAN     TRANSLATE TO STEREO                          
         LA    R2,1(R2)                                                         
         LLC   RF,FLDXSFN-FLDXNUM(RE)                                           
         SRL   RF,6                                                             
         STC   RF,0(R2)                                                         
         TR    0(1,R2),DECTRAN     TRANSLATE TO STEREO MOD 64                   
         LA    R2,1(R2)                                                         
         LLC   RF,FLDXSFN-FLDXNUM(RE)                                           
         SLL   RF,26                                                            
         SRL   RF,26                                                            
         STC   RF,0(R2)                                                         
         TR    0(1,R2),DECTRAN     TRANSLATE TO STEREO REMAINDER                
         LA    R2,1(R2)                                                         
*                                                                               
FT60     TM    FATB,FATBPROT       MOVE FIELD DATA TO STDBUF                    
         BZ    *+12                                                             
         BAS   RE,TRANPFLD         TRANSLATE PROTECTED FIELD TO STEREO          
         B     *+8                                                              
         BAS   RE,TRANUFLD         TRANSLATE UNPROT. FIELD TO STEREO            
         L     R1,SAVER1                                                        
*                                                                               
         LA    RE,STDBUF           TEST FOR STDBUF OVERFLOW                     
         ICM   RF,15,=AL4(L'STDBUF)                                             
         AR    RF,RE                                                            
         CR    RF,R2                                                            
         BNH   BFOVER              EXIT WITH ERROR IF STDBUF OVERFLOW           
*                                                                               
         MVC   LTATB,FATB          SET LAST TRANS ATB BYTE                      
         MVC   LTATBX,FATBX                                                     
         MVC   LTEND,FEND          SET LAST TRANS END ADDR                      
*                                                                               
         OI    LTFLAG,FOUTTRN      SET TRANS FLAG                               
         NI    LTFLAG,FOUTTRN+FOUTMOD  TURN OFF CURSOR/PREVNEW FLAGS            
         TM    LTFLAG,FOUTMOD                                                   
         BZ    *+8                                                              
         OI    LTFLAG,X'02'        TURN ON PREVNEW FLAG                         
*                                                                               
FTX      XIT1  REGS=(R2)                                                        
*                                                                               
* EXECUTE STATEMENTS                                                            
*                                                                               
NULLIT   XC    FDATA(0),FDATA                                                   
MOVEIT   MVC   FDATA(0),FLDDATA                                                 
TESTDDIT TRT   FDATA(0),TRTDD                                                   
CLEANIT  TR    FDATA(0),0(RF)                                                   
TRANSIT  MVC   0(0,R2),FDATA                                                    
RESTORIT MVC   FLDDATA(0),FDATA                                                 
RESCOM   MVC   FDATA(0),COMBUF                                                  
                                                                                
***********************************************************************         
* HERE IF STDBUF OVERFLOW DETECTED                                    *         
***********************************************************************         
BFOVER   EQU   *                                                                
         LA    R2,STDBUF           R2=A(STEREO DATA BUFFER WORK AREA)           
         ST    R1,SAVER1                                                        
*                                  CLEAR STDBUF (SAVING FIRST 3 BYTES)          
         LA    RE,3(R2)                                                         
         ICM   RF,15,=AL4(L'STDBUF-3)                                           
         LA    R0,*                                                             
         L     R1,=F'0'                                                         
         MVCL  RE,R0                                                            
         L     R1,SAVER1                                                        
         MVC   3(L'BOMSG,R2),BOMSG MOVE OUT ERROR MESSAGE                       
         L     RE,AFSTFH                                                        
         LR    RF,R3                                                            
         SR    RF,RE                                                            
         STCM  RF,15,FULL                                                       
         LA    R6,3+L'BOMSG-8(R2)                                               
         GOTO1 =V(HEXOUT),DMCB,FULL,(R6),4,(24,0)                               
         LA    R2,3+L'BOMSG(R2)                                                 
         ST    R1,SAVER1                                                        
         MVI   0(R2),X'03'         MARK END OF STDBUF WITH ETX CODE             
         BAS   RE,OUTSCRN          OUTPUT STANDARD STEREO SCREEN                
         L     R1,SAVER1                                                        
         L     RD,SAVERD                                                        
         B     EXIT                                                             
                                                                                
***********************************************************************         
* TRANUFLD - TRANSLATE PROTECTED FIELD                                *         
* INPUT: R2->CURRENT LOCATION IN THE TWA WORKAREA                     *         
*        R3->HEADER OF FIELD TO BE TRANSLATED                         *         
* OUTPT: R2->NEW LOCATION IN THE TWA WORKAREA                         *         
***********************************************************************         
TRANPFLD NTR1                                                                   
         LA    RE,FDATA            TEST EMBEDDED MULTIPLE FIELD                 
         SR    RF,RF                                                            
         ICM   RF,1,COMPFLEN                                                    
         BZ    TRPF015                                                          
TRPF010  CLI   0(RE),X'5F'         MULTIPLE FIELD CODE IS X'5F'                 
         BE    TRANMFLD                                                         
         LA    RE,1(RE)                                                         
         BCT   RF,TRPF010                                                       
TRPF015  EQU   *                                                                
*                                                                               
TRPF020  LA    R4,SDBPFNI          SET NORMAL INTENSITY                         
         TM    FATB,X'0C'          CHECK INTENSITY                              
         BZ    TRPF022                                                          
         BO    TRPF021                                                          
         LA    R4,SDBPFHI          SET HIGH INTENSITY                           
         B     TRPF022                                                          
TRPF021  LA    R4,SDBPFZI          SET ZERO INTENSITY                           
*                                                                               
TRPF022  TM    FATB,X'01'          TEST IF MODIFIED                             
         BZ    *+8                 NO                                           
         LA    R4,3(R4)            MODIFIED VALUES ARE PLUS 3                   
         STC   R4,0(R2)                                                         
         TR    0(1,R2),DECTRAN     TRANSLATE TO STEREO                          
         LA    R2,1(R2)                                                         
*                                                                               
         MVC   0(1,R2),COLNUM      STORE THE COLUMN NUMBER                      
         TR    0(1,R2),DECTRAN     TRANSLATE TO STEREO                          
         LA    R2,1(R2)                                                         
*                                                                               
         MVC   0(1,R2),COMPFLEN    STORE THE DATA LENGTH                        
         TR    0(1,R2),DECTRAN     TRANSLATE TO STEREO                          
         LA    R2,1(R2)                                                         
*                                                                               
         SR    RF,RF               TEST ANY DATA                                
         ICM   RF,1,COMPFLEN                                                    
         BZ    TRPFX                                                            
         AHI   RF,-1                                                            
         EX    RF,TRANSIT          MOVE TO STDBUF FROM FDATA                    
         LA    R2,1(R2,RF)                                                      
         B     TRPFX                                                            
*                                                                               
TRANMFLD LA    R4,SDBPFNI          SET NORMAL INTENSITY                         
         TM    FATB,X'0C'          CHECK INTENSITY                              
         BZ    TRMF002                                                          
         BO    TRMF001                                                          
         LA    R4,SDBPFHI          SET HIGH INTENSITY                           
         B     TRMF002                                                          
TRMF001  LA    R4,SDBPFZI          SET ZERO INTENSITY                           
*                                                                               
TRMF002  TM    FATB,X'01'          TEST IF MODIFIED                             
         BZ    *+8                 NO                                           
         LA    R4,3(R4)            MODIFIED VALUES ARE PLUS 3                   
         STC   R4,0(R2)                                                         
         TR    0(1,R2),DECTRAN     TRANSLATE TO STEREO                          
*                                                                               
         LA    R3,FDATA            R3=A(FIELD DATA)                             
         LLC   R4,COMPFLEN         R4=A(TOTAL LENGTH MULTIPLE FIELD)            
*                                                                               
TRMF010  EQU   *                   LOOP TROUGH EACH MULTIPLE FIELD              
         LR    R5,R2               SAVE A(START OF STEREO FIELD)                
         MVC   1(1,R5),COLNUM      MOVE STEREO FIELD COLUMN                     
         TR    1(1,R5),DECTRAN                                                  
         LA    R2,3(R2)            POINT TO START STEREO FIELD DATA             
         SR    RF,RF                                                            
         LLC   R6,COLNUM                                                        
         MVI   REPEATFG,C'N'                                                    
*                                                                               
TRMF020  CLI   0(R3),X'5F'         MOVE DATA TO STEREO FIELD                    
         BE    TRMF030             UPTO NEXT MULTIPLE FIELD CODE                
         MVC   0(1,R2),0(R3)                                                    
         CLI   REPEATFG,C'N'                                                    
         BE    TRMF024                                                          
         MVI   REPEATFG,C'N'                                                    
         CLI   0(R3),C'?'                                                       
         BE    TRMF022                                                          
         MVC   REPEATLN,0(R3)                                                   
         TR    REPEATLN,EBDTRAN                                                 
         LLC   R1,REPEATLN                                                      
         AHI   R1,-1                                                            
         AR    R6,R1                                                            
         B     TRMF028                                                          
TRMF022  LA    R6,1(R6)                                                         
         B     TRMF028                                                          
TRMF024  CLI   0(R3),C'?'                                                       
         BE    TRMF026                                                          
         LA    R6,1(R6)                                                         
         B     TRMF028                                                          
TRMF026  MVI   REPEATFG,C'Y'                                                    
         B     TRMF028                                                          
TRMF028  LA    R2,1(R2)                                                         
         LA    R3,1(R3)                                                         
         LA    RF,1(RF)                                                         
         BCT   R4,TRMF020                                                       
         B     TRMF040             EXIT LOOP IF NORMAL END OF FIELD             
TRMF030  LA    R3,1(R3)            BUMP PAST MULTIPLE FIELD CODE                
         LA    R6,1(R6)                                                         
         AHI   R4,-1                                                            
TRMF040  LTR   RF,RF                                                            
*NOP*    BZ    TRPFX               FIX FOR X'5F' BUG IN ACLFMD2 - JOMU          
         STC   RF,2(R5)            MOVE STEREO FIELD LENGTH                     
         TR    2(1,R5),DECTRAN                                                  
         LTR   R4,R4                                                            
         BZ    TRPFX                                                            
         STC   R6,COLNUM           UPDATE NEXT FIELD COLUMN NUMBER              
         MVC   0(1,R2),0(R5)       MOVE OUT NEXT FIELD INTENESITY CODE          
         B     TRMF010             LOOP TO PROCESS NEXT FIELD                   
*                                                                               
TRPFX    XIT1  REGS=(R2)                                                        
         EJECT                                                                  
***********************************************************************         
* TRANUFLD - TRANSLATE UNPROTECTED FIELD                              *         
* INPUT: R2->CURRENT LOCATION IN THE TWA WORKAREA                     *         
*        R3->HEADER OF FIELD TO BE TRANSLATED                         *         
* OUTPT: R2->NEW LOCATION IN THE TWA WORKAREA                         *         
***********************************************************************         
TRANUFLD NTR1                                                                   
         LA    R4,SDBUFNI          SET NORMAL INTENSITY                         
         TM    FATB,X'0C'          CHECK INTENSITY                              
         BZ    TRUF002                                                          
         BO    TRUF001                                                          
         LA    R4,SDBUFHI          SET HIGH INTENSITY                           
         B     TRUF002                                                          
TRUF001  LA    R4,SDBUFZI          SET ZERO INTENSITY                           
*                                                                               
TRUF002  TM    FATB,X'01'          TEST IF MODIFIED                             
         BZ    *+8                 NO                                           
         LA    R4,3(R4)            MODIFIED VALUES ARE PLUS 3                   
*                                                                               
TRUF006  LLC   R1,FLDLEN           FIELD LENGTH                                 
         TM    FLDATB,X'02'        EXTENDED FIELD HEADER?                       
         BZ    *+8                 NO                                           
         AHI   R1,-8                                                            
         AHI   R1,-8               DATA LENGTH IN R1                            
         LR    R0,R1               FIELD LENGTH IN R0                           
*                                                                               
         LHI   R6,-1               R6 NEGATIVE MEANS NO CURSOR                  
         TM    TIOBINDS,TIOBSETC   TEST SPECIAL CURSOR SETTING                  
         BZ    TRUF010                                                          
         TM    CURTHIS,X'01'                                                    
         BZ    TRUF030             TEST IF CURSOR ON THIS FIELD                 
         SR    R6,R6                                                            
         LH    RF,FLDADR                                                        
         CLM   RF,3,CURADDR        IS THE CURSOR AT A(FIELD)                    
         BE    TRUF020             YES                                          
         BH    TRUF030             ALREADY FOUND THE CURSOR                     
*                                                                               
         AR    RF,R0               SCREEN ADRESS + FIELD LENGTH                 
         CLM   RF,3,CURADDR        IN THE MIDDLE OF THE FIELD?                  
         BNH   TRUF030             NO                                           
         B     TRUF020                                                          
*                                                                               
TRUF010  TM    FLDOIND,FOUTCUR     TEST FIELD CURSOR FLAG                       
         BZ    TRUF030                                                          
         SR    R6,R6               SET CURSOR AT START OF THIS FIELD            
         B     TRUF030                                                          
*                                                                               
TRUF020  ICM   R6,3,CURADDR        SET R6 TO DISPLACEMENT                       
         SH    R6,FLDADR                                                        
*                                                                               
TRUF030  STC   R4,0(R2)            SET UNPROT FIELD NUMBER                      
         TR    0(1,R2),DECTRAN     TRANSLATE TO STEREO                          
         LA    R2,1(R2)                                                         
*                                                                               
         MVC   0(1,R2),COLNUM      STORE THE COLUMN NUMBER                      
         TR    0(1,R2),DECTRAN     TRANSLATE TO STEREO                          
         LA    R2,1(R2)                                                         
*                                                                               
         MVC   0(1,R2),COMPFLEN    STORE THE DATA LENGTH                        
         TR    0(1,R2),DECTRAN     TRANSLATE TO STEREO                          
         LA    R2,1(0,R2)                                                       
*                                                                               
         STC   R0,0(R2)            STORE THE FIELD LENGTH                       
         TR    0(1,R2),DECTRAN     TRANSLATE TO STEREO                          
         LA    R2,1(0,R2)                                                       
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,1,COMPFLEN                                                    
         BZ    TRUF100                                                          
*                                  COPY THE COMPRESSED DATA                     
         AHI   RF,-1                                                            
         EX    RF,TRANSIT          MOVE TO STDBUF FROM FDATA                    
         LA    R2,1(R2,RF)                                                      
*                                                                               
TRUF100  LTR   R6,R6               TEST IF CURSOR ON THIS FIELD                 
         BM    TRUFX                                                            
         MVI   0(R2),SDBCEXP       CURSOR EXCEPTION                             
         STC   R6,1(R2)            OFFSET OF CURSOR INTO FIELD                  
         TR    0(2,R2),DECTRAN     TRANSLATE TO STEREO                          
         LA    R2,2(R2)                                                         
*                                                                               
TRUFX    XIT1  REGS=(R2)                                                        
         EJECT                                                                  
***********************************************************************         
* THIS ROUTINE COMPRESSES OUTGOING DATA OF THE ITEM ABOUT TO BE PUT   *         
* COPIED FROM CTMAD00/148 06/16/95                                    *         
*                                                                     *         
* 'DINK' WILL BE REPLACED BY A '?0A'  REMOVED THIS                    *         
* '+' WILL BE REPLACED BY A '?0B'      AND THIS                       *         
* '?' WILL BE REPLACED BY A '??'                                      *         
*                                                                     *         
* PARAM1 A(DATA)  ADDRESS OF DATA TO BE COMPRESSED                    *         
* PARAM2 L'DATA   INPUT SOURCE LENGTH/RETURN RESULT                   *         
* PARAM3 A(DEST)  ADRESS OF DESTINATION                               *         
*                                                                     *         
* COMPRESSIONS WILL HAVE THE FOLLOWING FORMAT: ?NX.                   *         
* ? IS THE ESCAPE CHARACTER                                           *         
* N IS THE # OF CHRACTER REPEATS TRANSLATED TO STEREO                 *         
* X IS THE CHARACTER TO BE REPEATED                                   *         
***********************************************************************         
COMPRESS NTR1                                                                   
         OC    4(4,R1),4(R1)       IF NO LENGTH                                 
         BZ    CMX                 THEN NOTHING TO COMPRESS                     
         LR    R7,R1                                                            
*                                                                               
         L     R5,0(R7)            R5 = A(FIRST CHAR IN SOURCE DATA)            
         L     R4,4(R7)            R4 = LENGTH(SOURCE DATA)                     
         L     R3,8(R7)            R3 = A(FIRST CHAR IN DEST DATA)              
         SR    R2,R2               R2 = REPEATING CHARACTER                     
         SR    R6,R6               R6 = NUMBER OF REPETITIONS                   
*                                                                               
CM10     CLM   R2,1,0(R5)          TEST IF NEW CHARACTER                        
         BE    CM100                                                            
         BRAS  RE,COMPREV          THEN COMPRESS PREV REPEATED CHARS            
*                                                                               
CM30     CLI   0(R5),C'?'          TEST IF NEW CHARACTER IS A '?'               
         BNE   CM40                                                             
         CHI   R4,1                AND NOT LAST IN FIELD                        
         BE    CM30A                                                            
         CLI   1(R5),C'?'          AND NOT ITSELF START OF REPETITION           
         BE    CM40                                                             
*                                                                               
CM30A    MVC   0(2,R3),=C'??'      STORE ESCAPE SEQUENCE ??                     
         LA    R3,2(R3)                                                         
         B     CM300                                                            
*                                                                               
CM40     IC    R2,0(R5)            R2 = NEW CHAR                                
         LA    R6,1                R6 = NUM OF REPS = 1                         
         B     CM300                                                            
*                                                                               
CM100    LA    R6,1(R6)            ELSE (NOT NEW CHAR) BUMP REP COUNT           
         CHI   R6,61               IF REP COUNT >= 61                           
         BL    CM300                                                            
         BRAS  RE,COMPREV          THEN COMPRESS PREV REPEATED CHARS            
         B     CM300                                                            
*                                                                               
CM300    LA    R5,1(R5)            BUMP R5 TO NEXT BYTE OF SOURCE DATA          
         BCT   R4,CM10             REPEAT UNTIL NO MORE SOURCE DATA             
*                                                                               
         LTR   R6,R6               IF CHARS LEFT OVER TO BE PUT                 
         BZ    *+8                                                              
         BRAS  RE,COMPREV          THEN COMPRESS LEFT OVER DATA                 
         L     RF,8(R7)            SET LEN OF COMPRESSED DATA                   
         SR    R3,RF                                                            
         ST    R3,4(R7)                                                         
*                                                                               
CMX      XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* THIS ROUTINE STORES THE APPROPRIATE COMPRESSION SEQUENCE FOR THE    *         
* PREVIOUSLY REPEATED CHARACTERS IN THE DESTINATION DATA.             *         
***********************************************************************         
COMPREV  LTR   R6,R6               IF NO PREVIOUS CHAR                          
         BZ    CPX                 THEN RETURN                                  
*                                                                               
         CLM   R2,1,=C'?'          ALWAYS COMPRESS IF ??>                       
         BE    CP100                                                            
         CHI   R6,4                OR IF NUMBER OF REPS >= 4                    
         BNL   CP100                                                            
*                                                                               
CP10     STC   R2,0(R3)            THEN LOOP AND SAVE REPEATED CHAR             
         LA    R3,1(R3)            TO DEST DATA                                 
         BCT   R6,CP10                                                          
         B     CPX                 AND RETURN                                   
*                                                                               
CP100    MVI   0(R3),C'?'          STORE COMP SEQUENCE IN DEST DATA             
         STC   R6,1(R3)                                                         
         TR    1(1,R3),DECTRAN     CONVERT TO STEREO                            
         STC   R2,2(R3)                                                         
         LA    R3,3(R3)            BUMP R3 TO NEXT POS IN DEST DATA             
         SR    R6,R6               CLEAR REPEAT CHAR AND COUNT                  
         SR    R2,R2                                                            
*                                                                               
CPX      BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* OUTSCRN - OUTPUT STANDARD STEREO SCREEN                             *         
*                                                                     *         
* MAXIMUM NUMBER OF LINES TO OUTPUT IN MAXOLIN                        *         
* MAXIMUM SCREEN LINE CODE NUMBER IN MAXSLIN                          *         
* WHEN NUMBER OF OUTPUT LINES EXCEEDS MAXSLIN THEN CONTINUE TO        *         
* OUTPUT THE SAME MAXSLIN SCREEN LINE NUMBER CODE UNTIL MAXOLIN       *         
* REACHED OR END OF DATA BUFFER.                                      *         
***********************************************************************         
OUTSCRN  NTR1                                                                   
         L     R1,SAVEAPL          R1=A(INPUT PARAMETER LIST)                   
         ICM   R2,15,4(R1)         R2=A(OUTPUT BUFFER)                          
         XC    OUTADDR,OUTADDR                                                  
         MVI   OUTFLAG,0                                                        
         LA    R3,STDBUF           R3=A(STEREO DATA BUFFER WORK AREA)           
         LA    R4,MAXOLIN          R4=LOOP COUNT - MAX OUTPUT LINES             
         LA    R6,MAXSLIN          R6=LINE COUNT - MAX SCREEN LINE CODE         
*                                                                               
OSCR010  MVI   0(R2),X'11'         (SBA)(FSTRABS)(IC)                           
         SR    RE,RE                                                            
         ICM   RE,3,OUTADDR                                                     
         STC   RE,2(R2)                                                         
         NI    2(R2),X'3F'                                                      
         SRL   RE,6                                                             
         STC   RE,1(R2)                                                         
         TR    1(2,R2),TRNTBL                                                   
         LA    R2,3(R2)                                                         
         MVI   0(R2),X'1D'         (SF)                                         
         MVI   1(R2),X'00'         ATTRIBUTE BYTE                               
         LA    R2,2(R2)                                                         
         LA    R5,79               R5=LOOP COUNT - 79 CHARS. PER LINE           
*                                                                               
OSCR020  CLI   OUTFLAG,X'FF'                                                    
         BE    OSCR030                                                          
         CLI   0(R3),X'03'         TEST FOR END OF DATA CODE IN STDBUF          
         BNE   OSCR022                                                          
         MVI   OUTFLAG,X'FF'                                                    
         B     OSCR030                                                          
*                                                                               
OSCR022  MVC   0(1,R2),0(R3)                                                    
         LA    R2,1(R2)                                                         
         LA    R3,1(R3)                                                         
         BCT   R5,OSCR020                                                       
*                                                                               
OSCR030  EQU   *                                                                
         LTR   R6,R6               TEST SCREEN LINE CODE MAX REACHED            
         BZ    *+8                                                              
         BCT   R6,OSCR032          DECREMENT SCREEN LINE CODE COUNT             
         CLI   OUTFLAG,X'FF'       TEST END OF DATA BUFFER REACHED              
         BE    OSCR100             IF SO BRANCH TO PROCESS ETX                  
         CLI   0(R3),X'03'                                                      
         BE    OSCR100                                                          
         B     OSCR034             ELSE GET NEXT DATA BUFFER LINE               
*                                                                               
OSCR032  SR    RE,RE               BUMP SCREEN LINE ADDRESS                     
         ICM   RE,3,OUTADDR                                                     
         AHI   RE,80                                                            
         STCM  RE,3,OUTADDR                                                     
*                                                                               
OSCR034  EQU   *                   BUMP TO NEXT LINE OF DATA IN BUFFER          
         BCT   R4,OSCR010          UNTIL MAXIMUM OUTPUT LINES REACHED           
*                                                                               
OSCR100  MVI   0(R2),X'03'         END PROCESSING OUTPUT (ETX)                  
         LA    R2,1(R2)                                                         
         MVI   0(R2),X'00'         DOUBLE SURE APPEND WITH ZERO BYTE            
         L     R1,SAVEAPL          R1=A(INPUT PARAMETER LIST)                   
         ICM   R6,15,4(R1)         R6=A(START OF OUTPUT BUFFER)                 
         SR    R2,R6                                                            
         AHI   R6,-8                                                            
         STH   R2,DUB                                                           
         MVC   6(2,R6),DUB                                                      
         MVI   0(R1),1             RETURN ERASE/WRITE ACTION                    
*                                                                               
OSCRX    XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* SPLIT UP ANY FIELDS THAT ARE GREATER THAN 80 BYTES LONG             *         
***********************************************************************         
SCRSPLT  NTR1                                                                   
         L     R3,AFSTFH           R3=A(FIRST FIELD HEADER)                     
         USING FHD,R3                                                           
         XR    RF,RF                                                            
*                                                                               
SPLT02   ICM   RF,1,FHLN           SEE IF FIELDS ARE TOO BIG                    
         BZ    SPLTX                                                            
         CLI   FHAT,FHATNP         IGNORE NOP FIELDS                            
         BE    SPLT04                                                           
*                                                                               
         LR    RE,RF                                                            
         AHI   RE,-(FHDAD)                                                      
         TM    FHAT,FHATXH         EXTENDED FLDHDR?                             
         BZ    *+8                                                              
         AHI   RE,-(FHDAD)                                                      
         CHI   RE,80               WE WILL ONLY TOLERATE 80 OR LESS             
         BH    SPLT06                                                           
*                                                                               
SPLT04   BXH   R3,RF,SPLT02                                                     
         DC    H'0'                NEVER REACH HERE                             
*                                                                               
SPLT06   ICM   RF,1,FHLN                                                        
         BZ    SPLT08                                                           
         BXH   R3,RF,SPLT06                                                     
         DC    H'0'                                                             
*                                                                               
SPLT08   L     R0,ATWA                                                          
         LH    R1,TWALEN                                                        
         LHI   RE,TWACOPY-TIOBD                                                 
         AR    RE,RC                                                            
         LR    RF,R1                                                            
         MVCL  RE,R0               COPY OUT BAD TWA                             
         MVI   COPIED,C'Y'                                                      
*                                                                               
         LHI   R2,TWACOPY-TIOBD                                                 
         AR    R2,RC                                                            
         LA    R2,64(R2)           R2 = COPIED OUT FIELD                        
COPY     USING FHD,R2                                                           
         L     R4,AFSTFH           R4 = NEW REBUILT TWA                         
BLD      USING FHD,R4                                                           
*                                                                               
SPLT10   XR    RF,RF                                                            
         ICM   RF,1,COPY.FHLN      END OF COPIED OUT SCREEN                     
         BZ    SPLT20                                                           
         LHI   RE,-(FHDAD+1)                                                    
         TM    COPY.FHAT,FHATXH                                                 
         BZ    *+8                                                              
         LHI   RE,-(FHDAD+FHDAD+1)                                              
         SR    RF,RE                                                            
         STH   RF,FLDLN            SAVE LENGTH OF THIS FIELD                    
*                                                                               
         CHI   RF,80               WE WILL ONLY TOLERATE 80 OR LESS             
         BH    SPLT12              THIS FIELD NEEDS SPLITTING                   
         EX    RF,*+8              FIELD IS OK - MOVE IT BACK                   
         B     *+10                                                             
         MVC   BLD.FHD(0),COPY.FHD                                              
*                                                                               
         AR    RF,RE                                                            
         AR    R2,RF               MOVE COPIED OUT FIELD POINTER                
         AR    R4,RF               MOVE REBUILT FIELD POINTER                   
         B     SPLT10              NEXT FIELD                                   
*                                                                               
* WE HAVE A BAD FIELD AT COPY.FHLN                                              
* WE NEED TO SPLIT IT INTO 2 SO THAT THE SECOND FIELD STARTS IN ROW1            
* OF THE NEXT LINE                                                              
*                                                                               
SPLT12   XC    AXTND,AXTND         RESET A(EXTENDED FIELD HEADER)               
         TM    COPY.FHAT,FHATXH                                                 
         BZ    SPLT13              THIS FIELD HAS NO EXTRA HEADER               
         LLC   RF,COPY.FHLN                                                     
         LA    RF,COPY.FHLN(RF)                                                 
         AHI   RF,-FHDAD                                                        
         ST    RF,AXTND            SET A(EXTENDED FIELD HEADER)                 
*                                                                               
SPLT13   XR    RE,RE                                                            
         ICM   RE,3,COPY.FHAD                                                   
         SRDL  RE,32                                                            
         D     RE,=F'80'                                                        
         STH   RF,BADROW           SAVE FIRST BAD ROW                           
*                                                                               
         LHI   RF,80                                                            
         SR    RF,RE               RF = NUMBER OF BYTES ON THIS LINE            
         STH   RF,MVSOFAR          SET HOW MUCH MOVED                           
         AHI   RF,FHDAD                                                         
         TM    COPY.FHAT,FHATXH                                                 
         BZ    *+8                                                              
         AHI   RF,FHDAD            RF HOLDS LENGTH OF FIRST FIELD               
*                                                                               
         MVC   BLD.FHLN(FHDAD),COPY.FHLN                                        
         STC   RF,BLD.FHLN         MOVE IN HEADER AND SET NEW LENGTH            
*                                                                               
         LH    RF,MVSOFAR                                                       
         AHI   RF,-1                                                            
         EX    RF,*+8              MOVE IN RIGHT AMOUNT OF DATA                 
         B     *+10                                                             
         MVC   BLD.FHDA(0),COPY.FHDA                                            
         LA    RF,1(RF)                                                         
         STC   RF,BLD.FHOL         SET NEW OUTPUT LENGTH                        
         OI    BLD.FHOI,FHOITR                                                  
*                                                                               
         TM    COPY.FHAT,FHATXH    IS THIS AN EXTENDED FIELD?                   
         BZ    SPLT14              NO                                           
         LA    RF,BLD.FHDA(RF)                                                  
         L     RE,AXTND                                                         
         MVC   0(FHDAD,RF),0(RE)   COPY IN EXTENDED HEADER TO FIELD             
*                                                                               
SPLT14   LLC   RF,BLD.FHLN                                                      
         AR    R4,RF               MOVE BUILT SCREEN POINTER                    
         LH    RF,MVSOFAR          AMOUNT OF DATA MOVED SO FAR                  
         LA    RF,1(RF)            FIRST COLUMN IN LINE IS IGNORED              
         STH   RF,MVSOFAR          SO BUMP THIS POINTER                         
*                                                                               
         LH    RF,FLDLN            REDUCE AMOUNT OF DATA REMAINING              
         SH    RF,MVSOFAR          BY AMOUNT MOVED SO FAR                       
         BNP   SPLT18              MOVED WHOLE FIELD                            
         STH   RF,FLDLN            SET AMOUNT REMAINING                         
*                                                                               
         MVC   BLD.FHLN(FHDAD),COPY.FHLN                                        
         LH    RF,BADROW           FORCE FIELD TO START COL2 NEXT ROW           
         LA    RF,1(RF)                                                         
         STH   RF,BADROW                                                        
         MHI   RF,80                                                            
         LA    RF,1(RF)            COLUMN 1 HAS ATT BYTE FOR VTAM               
         STCM  RF,3,BLD.FHAD                                                    
         LA    RE,COPY.FHDA                                                     
         AH    RE,MVSOFAR          RE=DATA MOVE FROM POINT                      
*                                                                               
         LH    RF,FLDLN            WILL REST FIT ON THIS LINE?                  
         CHI   RF,79                                                            
         BNH   *+8                                                              
         LHI   RF,79                                                            
         AHI   RF,-1                                                            
         EX    RF,*+8              MOVE DATA INTO THIS LINE                     
         B     *+10                                                             
         MVC   BLD.FHDA(0),0(RE)                                                
         LA    RF,1(RF)                                                         
         STC   RF,BLD.FHOL                                                      
         OI    BLD.FHOI,FHOITR                                                  
*                                                                               
         LR    R0,RF                                                            
         AHI   R0,FHDAD                                                         
         TM    COPY.FHAT,FHATXH    IS THIS AN EXTENDED FIELD?                   
         BZ    *+8                 NO                                           
         AHI   R0,FHDAD                                                         
         STC   R0,BLD.FHLN         SET NEW LENGTH OF FIELD                      
*                                                                               
         TM    COPY.FHAT,FHATXH    IS THIS AN EXTENDED FIELD?                   
         BZ    SPLT16              NO                                           
         LA    R1,BLD.FHDA                                                      
         AR    R1,RF               R1=NEW XTNDD HEADER POINT                    
         L     RE,AXTND                                                         
         MVC   0(FHDAD,R1),0(RE)   COPY IN EXTENDED HEADER TO FIELD             
*                                                                               
SPLT16   AH    RF,MVSOFAR                                                       
         STH   RF,MVSOFAR                                                       
         B     SPLT14                                                           
*                                                                               
SPLT18   LLC   RF,COPY.FHLN        GO ON TO NEXT FIELD IN COPY SCREEN           
         AR    R2,RF                                                            
         B     SPLT10                                                           
*                                                                               
SPLT20   MVC   BLD.FHLN(8),COPY.FHLN                                            
*                                                                               
SPLTX    XIT1                                                                   
         DROP  R3,COPY,BLD                                                      
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTANTS                                              *         
***********************************************************************         
NOPFLD   EQU   X'FF'                                                            
LASTSIN  DC    F'0'                                                             
$PFK     DC    X'010B'                                                          
$HLP     DC    X'0109'                                                          
SPACES   DC    CL80' '                                                          
MDMSG    DC    CL26'4LBVFATOSTR ABEND DETECTED'                                 
BOMSG    DC    CL52'4LBPFATOSTR BUFFER OVERFLOW AT DISPLACEMENT '               
SSACBITS DC    X'0102040810204080'                                              
SSNABITS DC    X'FEFDFBF7EFDFBF7F'                                              
*                                                                               
         LTORG                                                                  
*                                                                               
TRNTBL   DC    XL16'40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F'  00-0F                    
         DC    XL16'50D1D2D3D4D5D6D7D8D95A5B5C5D5E5F'  10-1F                    
         DC    XL16'6061E2E3E4E5E6E7E8E96A6B6C6D6E6F'  20-2F                    
         DC    XL16'F0F1F2F3F4F5F6F7F8F97A7B7C7D7E7F'  30-3F                    
*                                                                               
TRTDD    DC    XL16'00000000000000000000000000000000'  00-0F                    
         DC    XL16'00000000000000000000000000000000'  10-1F                    
         DC    XL16'03030404040401040303040404040000'  20-2F                    
         DC    XL16'00000000000000000000000000000000'  30-3F                    
         DC    XL16'00000000000000000000000000000000'  40-4F                    
         DC    XL16'00000000000000000000000000000000'  50-5F                    
         DC    XL16'00000000000000000000000000000000'  60-6F                    
         DC    XL16'00000000000000000000000000000000'  70-7F                    
         DC    XL16'00000000000000000000000000000000'  80-8F                    
         DC    XL16'00000000000000000000000000000000'  90-9F                    
         DC    XL16'00000000000000000000000000000000'  A0-AF                    
         DC    XL16'00000000000000000000000000000000'  B0-BF                    
         DC    XL16'00000000000000000000000000000000'  C0-CF                    
         DC    XL16'00000000000000000000000000000000'  D0-DF                    
         DC    XL16'00000000000000000000000000000000'  E0-EF                    
         DC    XL16'00000000000000000000000000000000'  F0-FF                    
                                                                                
* DECIMAL TO EBCDIC TRANSLATION TABLE FOR STEREO DATA BUFFER                    
*                                                                               
DECTRAN  DS    0CL256                                                           
         DC    X'A1'                                   00                       
         DC    X'C1C2C3C4C5C6C7C8C9D1'                 01-10                    
         DC    X'D2D3D4D5D6D7D8D9E2E3'                 11-20                    
         DC    X'E4E5E6E7E8E9F0F1F2F3'                 21-30                    
         DC    X'F4F5F6F7F8F981828384'                 31-40                    
         DC    X'85868788899192939495'                 41-50                    
         DC    X'96979899A2A3A4A5A6A7'                 51-60                    
         DC    X'A8A97D4B4C4D4E7E505A'                 61-70                    
         DC    X'5B5C5D5E7F6061C06B6C'                 71-80                    
         DC    X'6D6E6F797A7B7C7D7E7F'                 81-90                    
DEFDECQ  EQU   *-DECTRAN                                                        
         DC    XL(L'DECTRAN-DEFDECQ)'00'                                        
                                                                                
* EBCDIC ID TO DECIMAL TRANSLATION TABLE FOR STEREO DATA BUFFER                 
*                                                                               
EBDTRAN  DC    XL16'00000000000000000000000000000000'  00-0F                    
         DC    XL16'00000000000000000000000000000000'  10-1F                    
         DC    XL16'00000000000000000000000000000000'  20-2F                    
         DC    XL16'00000000000000000000000000000000'  30-3F                    
         DC    XL16'00000000000000000000004041424300'  40-4F                    
         DC    XL16'45000000000000000000464748494A00'  50-5F                    
         DC    XL16'4C4D0000000000000000004F50515253'  60-6F                    
         DC    XL16'000000000000000000545556573F444B'  70-7F                    
         DC    XL16'0025262728292A2B2C2D000000000000'  80-8F                    
         DC    XL16'002E2F30313233343536000000000000'  90-9F                    
         DC    XL16'00003738393A3B3C3D3E000000000000'  A0-AF                    
         DC    XL16'00000000000000000000000000000000'  B0-BF                    
         DC    XL16'4E010203040506070809000000000000'  C0-CF                    
         DC    XL16'000A0B0C0D0E0F101112000000000000'  D0-DF                    
         DC    XL16'0000131415161718191A000000000000'  E0-EF                    
         DC    XL16'1B1C1D1E1F2021222324000000000000'  F0-FF                    
                                                                                
*DDDDEQUS                                                                       
       ++INCLUDE DDDDEQUS                                                       
                                                                                
* LIST OF OUTPUT TRANSLATE TABLES INDEXED BY COUNTRY CODE                       
*                                                                               
CTRYXLOT DC    A(OUTUKUS),A(OUTUKUS),A(OUTUKUS),A(OUTUKUS)                      
         DC    A(OUTUKUS),A(OUTUKUS),A(OUTUKUS),A(OUTUKUS)                      
         DC    A(OUTUKUS),A(OUTUKUS),A(OUTUKUS),A(OUTUKUS)                      
         DC    A(OUTUKUS),A(OUTUKUS),A(OUTUKUS),A(OUTUKUS)                      
                                                                                
**********************************************************************          
* VALID OUTPUT CHARACTERS FOR UK AND USA                             *          
**********************************************************************          
OUTUKUS  DC    XL16'00404040404040404040404040404040'  00-0F                    
         DC    XL16'40404040404040404040404040404040'  10-1F                    
         DC    XL16'40404040404040404040404040404040'  20-2F                    
         DC    XL16'40404040404040404040404040404040'  30-3F                    
         DC    XL16'404040404040404040404A4B4C4D4E4F'  40-4F                    
         DC    XL16'504040404040404040405A5B5C5D5E5F'  50-5F                    
         DC    XL16'606140404040404040406A6B6C6D6E6F'  60-6F                    
         DC    XL16'404040404040404040797A7B7C7D7E7F'  70-7F                    
         DC    XL16'40818283848586878889404040404040'  80-8F                    
         DC    XL16'4091929394959697989940404040409F'  90-9F                    
         DC    XL16'40A1A2A3A4A5A6A7A8A9404040404040'  A0-AF                    
         DC    XL16'40404040404040404040404040404040'  B0-BF                    
         DC    XL16'C0C1C2C3C4C5C6C7C8C9404040404040'  C0-CF                    
         DC    XL16'D0D1D2D3D4D5D6D7D8D9404040404040'  D0-DF                    
         DC    XL16'E040E2E3E4E5E6E7E8E9404040404040'  E0-EF                    
         DC    XL16'F0F1F2F3F4F5F6F7F8F940404040FE40'  F0-FF                    
                                                                                
**********************************************************************          
* ALL CHARACTERS ABOVE X'40' ARE DEFINED AS VALID                    *          
* BOX CHARACTERS ARE 8F,AB,AC,AD,BB,BC,BD,BF,CB,CC,EB,EC,FA AND 42   *          
**********************************************************************          
OUTALLC  DC    XL16'00404040404040404040404040404040'  00-0F                    
         DC    XL16'40404040404040404040404040404040'  10-1F                    
         DC    XL16'40404040404040404040404040404040'  20-2F                    
         DC    XL16'40404040404040404040404040404040'  30-3F                    
         DC    XL16'404142434445464748494A4B4C4D4E4F'  40-4F                    
         DC    XL16'505152535455565758595A5B5C5D5E5F'  50-5F                    
         DC    XL16'606162636465666768696A6B6C6D6E6F'  60-6F                    
         DC    XL16'707172737475767778797A7B7C7D7E7F'  70-7F                    
         DC    XL16'808182838485868788898A8B8C8D8E8F'  80-8F                    
         DC    XL16'909192939495969798999A9B9C9D9E9F'  90-9F                    
         DC    XL16'A0A1A2A3A4A5A6A7A8A9AAABACADAEAF'  A0-AF                    
         DC    XL16'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'  B0-BF                    
         DC    XL16'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'  C0-CF                    
         DC    XL16'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'  D0-DF                    
         DC    XL16'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'  E0-EF                    
         DC    XL16'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'  F0-FF                    
         EJECT                                                                  
***********************************************************************         
* SUBROUTINE TO BUILD MINI WHOAMI DATA IN FIRST PROTECTED HEADER FIELD*         
***********************************************************************         
WHODAT   NTR1  BASE=*,LABEL=*                                                   
         MVC   0(8,R3),=X'442000010000803C' STANDARD FIELD HDR                  
         LA    R3,8(R3)                                                         
         USING WHODATD,R3                                                       
         L     R1,SAVEAPL                                                       
         L     R4,8(R1)                                                         
         USING UTLD,R4                                                          
         XC    WDDATA,WDDATA                                                    
*                                                                               
WD1      MVC   WDMID,=CL8'XD/0001 '                                             
*                                                                               
WD2      MVC   WDPID(2),TAGYPER    AGENCY ALPHA ID FOR PERSON ID                
         XC    HALF,HALF                                                        
         OC    TPERSON,TPERSON     USE PERSON NUM OR PASSWORD NUM               
         BZ    *+14                                                             
         MVC   HALF,TPERSON                                                     
         B     WD2A                                                             
         OC    TPASSWD,TPASSWD                                                  
         BZ    WD2A                                                             
         MVC   HALF,TPASSWD                                                     
WD2A     GOTO1 =V(HEXOUT),DMCB,HALF,WDPID+2,2,(24,0)                            
*                                                                               
WD3      MVC   WDPEXP,=CL60' '     PASSWORD EXPIRATION                          
         CLI   TPASSEXP,0          TEST IF PASSWORD EXPIRATION DEFINED          
         BE    WD3X                                                             
         LLC   R0,TPASSEXP                                                      
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  WDPEXP(2),DUB                                                    
         TM    TSTAT7,TST7PSWN     TEST IF WITHIN WARNING PERIOD                
         BZ    *+8                                                              
         MVI   WDPEXP+2,C'W'                                                    
WD3X     EQU   *                                                                
*                                                                               
WD4      L     RF,=V(SSB)          EXTRACT MAX SESSION INFO FROM SSB            
         MVC   FULL+0(2),SSBSSMAX-SSBD(RF)                                      
         MVC   FULL+2(2),SSBSSMXP-SSBD(RF)                                      
         TM    TSTATC,TSTCXSES     TEST IF TRM SUPPORTS EXTRA SESSIONS          
         BO    WD4A                                                             
         LHI   R0,4                OLD CMV SUPPORTS 4 SESSIONS                  
         LHI   R1,4                                                             
         CLC   FULL+2(2),FULL+0                                                 
         BNH   *+8                                                              
         LHI   R1,5                PLUS EXTRA HIDDEN SESSION                    
         STH   R0,FULL                                                          
         STH   R1,FULL+2                                                        
WD4A     LH    R0,FULL                                                          
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  WDPSES(2),DUB                                                    
         MVI   WDPSES+2,C' '                                                    
         CLC   FULL+2(2),FULL+0                                                 
         BNH   *+8                                                              
         MVI   WDPSES+2,C'+'       EXTRA PHYSICAL SESSIONS                      
*                                                                               
WDX      XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
WHODATD  DSECT                                                                  
WDDATA   DS    0CL60               WHOAMI DATA IN TWA MESSAGE FIELD             
WDMID    DS    CL8                 MESSAGE ID                                   
WDPID    DS    CL6                 PID ID                                       
WDPEXP   DS    CL3                 PASSWORD EXPIRY DAYS AND WARNING             
WDPSES   DS    CL3                 SESSIONS                                     
WDPSOX   DS    CL19                SOX FIELDS                                   
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE - FIRST FIELDS DEFINED BY FATIOB                    *         
***********************************************************************         
* FATIOB                                                                        
       ++INCLUDE FATIOB                                                         
*                                                                               
DUB      DS    D                                                                
SVSCRNID DS    D                                                                
FULL     DS    F                                                                
DMCB     DS    6F                                                               
SAVEAPL  DS    F                                                                
SAVER1   DS    F                                                                
SAVER2   DS    F                                                                
SAVERD   DS    F                                                                
SAVERE   DS    F                                                                
OUTXLAT  DS    A                                                                
ATWA     DS    A                                                                
AFSTFH   DS    A                                                                
*                                                                               
P1       DS    F                                                                
P2       DS    F                                                                
P3       DS    F                                                                
P4       DS    F                                                                
*                                                                               
HALF     DS    H                                                                
CHKLEN   DS    H                                                                
CHKLEN8  DS    H                                                                
DATAROW  DS    H                                                                
NEXTROW  DS    H                                                                
TWALEN   DS    H                                                                
WORK     DS    CL80                                                             
LTEND    DS    H                                                                
FSTART   DS    H                   FIELD START 3270 ADR (ATB CHR)               
FSTRABS  DS    H                   FIELD START ABSOLUTE ADR (1ST CHR)           
FEND     DS    H                                                                
FUNP     DS    H                                                                
NEWFLDS  DS    C                   X'80' IF ALL SUBSEQUENT FIELDS NEW           
CURSES   DS    C                                                                
CURTHIS  DS    C                   CURSOR ON THIS FIELD BY TIOB                 
LTATB    DS    C                                                                
LTATBX   DS    X                                                                
LTNATB   DS    C                                                                
LTNATBX  DS    X                                                                
FATB     DS    C                                                                
FATBX    DS    X                                                                
FATBXX   DS    XL3                                                              
FATBXL   DS    X                                                                
FATBXV   DS    CL7                                                              
LTFLAG   DS    C                                                                
CMBYTE   DS    X                                                                
CTRY     DS    X                                                                
LANG     DS    X                                                                
SYS      DS    X                                                                
XTND     DS    X                                                                
NONO     DS    X                                                                
STAT6    DS    X                                                                
STAT8    DS    X                                                                
COPIED   DS    C                                                                
DATAROWN DS    X                                                                
REPEATFG DS    XL1                 COMPRESSION REPEAT FLAG                      
REPEATLN DS    XL1                 COMPRESSION REPEAT LENGTH                    
ROWNUM   DS    XL1                 CURRENT STEREO ROW NUMBER                    
COLNUM   DS    XL1                 CURRENT STEREO COLUMN NUMBER                 
COMPFLEN DS    XL1                 COMPRESSED FIELD DATA LENGTH                 
CURADDR  DS    XL2                 CURSOR ADDRESS AS SET IN TIOBD               
OUTADDR  DS    XL2                 OUTPUT SCREEN ADDRESS                        
OUTFLAG  DS    X                   OUTPUT SCREEN STATUS FLAG                    
XMTFLAG  DS    X                   XMIT ALL FIELDS FLAG                         
XLAT     DS    X                                                                
         DS    X                                                                
XCOLMASK DS    XL8                                                              
*                                                                               
MVSOFAR  DS    H                                                                
FLDLN    DS    H                                                                
BADROW   DS    H                                                                
AXTND    DS    F                   A(COPY EXTENDED FIELD HEADER)                
*                                                                               
SVFSTFH  DS    XL8                                                              
SVFSTF   DS    CL60                                                             
FDATA    DS    CL255                                                            
COMBUF   DS    CL255               FIELD DATA COMPRESSION BUFFER                
MAXSLIN  EQU   24                  MAXIMUM SCREEN LINE CODE NUMBER              
MAXOLIN  EQU   48                  MAXIMUM NUMBER OUTPUT LINES                  
STDBUF   DS    CL(4096)            STEREO DATA BUFFER WORK AREA                 
STDOVER  DS    CL255               OVERFLOW AREA FOR STDBUF                     
TWACOPY  DS    20000C                                                           
TWACOPYL EQU   *-TWACOPY                                                        
WORKX    EQU   *                                                                
                                                                                
*DDFH                                                                           
       ++INCLUDE DDFH                                                           
*DDFLDHDR                                                                       
       ++INCLUDE DDFLDHDR                                                       
*FAPGMLST                                                                       
       ++INCLUDE FAPGMLST                                                       
*FASELIST                                                                       
       ++INCLUDE FASELIST                                                       
*FASSB                                                                          
       ++INCLUDE FASSB                                                          
*FATCB                                                                          
       ++INCLUDE FATCB                                                          
*FAUTL                                                                          
       ++INCLUDE FAUTL                                                          
*FASTEREOQ                                                                      
       ++INCLUDE FASTEREOQ                                                      
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004FATOSTR   08/13/14'                                      
         END                                                                    
