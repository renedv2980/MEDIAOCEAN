*          DATA SET DDMENU     AT LEVEL 017 AS OF 05/01/02                      
*CATALP MENU                                                                    
*                                                                               
***********************************************************************         
*                                                                     *         
*  TITLE:        MENU -- GENERIC MENU HANDLER                         *         
*                                                                     *         
*  COMMENTS:     BUILDS A MENU ON-SCREEN AND ALLOWS SELECTION         *         
*                                                                     *         
*  PARAMS:       P1:   A(MNBLKD)  (SEE DDMENUBLK)                     *         
*                                                                     *         
*  OUTPUTS:      TWA WITH MENU                                        *         
*                                                                     *         
*  REGISTERS:    R0 -- WORK                                           *         
*                R1 -- WORK                                           *         
*                R2 -- WORK                                           *         
*                R3 -- WORK                                           *         
*                R4 -- WORK                                           *         
*                R5 -- S P A R E                                      *         
*                R6 -- S P A R E                                      *         
*                R7 -- S P A R E                                      *         
*                R8 -- S P A R E                                      *         
*                R9 -- VALUE TABLE                                    *         
*                RA -- MNBLKD                                         *         
*                RB -- FIRST BASE                                     *         
*                RC -- LOCAL WORKING STORAGE                          *         
*                RD -- REGISTER SAVE AREA                             *         
*                RE -- WORK                                           *         
*                RF -- WORK                                           *         
*                                                                     *         
***********************************************************************         
         TITLE 'MENU -- GENERIC MENU HANDLER'                                   
MENU     CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKDX-WORKD,**MENU**,CLEAR=YES                                  
         USING WORKD,RC                                                         
*                                                                               
         MVC   USERRD,4(RD)        SAVE APPLICATION RD FOR HOOKS                
*                                                                               
         L     RA,0(R1)            A(MENU PARAMETER BLOCK)                      
         USING MNBLKD,RA                                                        
         L     R9,MNVALTAB         A(VALUE TABLE)                               
         USING MNVALTBD,R9                                                      
*                                                                               
         L     RF,MNATIOB          A(TIOB) -- GET AID BYTE                      
         SR    RE,RE                                                            
         IC    RE,TIOBAID-TIOBD(RF)                                             
         CH    RE,=H'12'           WAS PF13..PF24 HIT?                          
         BNH   *+8                 NO                                           
         SH    RE,=H'12'           YES -- ADJUST TO PF1..PF12                   
         STC   RE,PFKEY                                                         
*                                                                               
         OC    MNVFRSTE,MNVFRSTE   INITIALIZE?                                  
         BNZ   *+12                NO                                           
         BAS   RE,INITIAL                                                       
         B     XIT                                                              
*                                                                               
         BAS   RE,CONTINUE                                                      
*                                                                               
XIT      XIT1                                                                   
         EJECT                                                                  
INITIAL  NTR1                                                                   
*                                                                               
* FIRST CALL TO MENU -- THE MENU IS NOT ON THE SCREEN                           
*                                                                               
         BAS   RE,SAVETWA          SAVE APPLICATION SCREEN                      
         BAS   RE,PFKEYS           BUILD PFKEY INSTRUCTIONS                     
         BAS   RE,FILLBACK         FILL IN BACKWARDS VALTABLE LENGTHS           
         BAS   RE,BLDTWA           BUILD THE MENU TWA                           
         LA    RE,L'MNVHEAD(R9)    A(FIRST MENU ITEM)                           
         ST    RE,FULL                                                          
         BAS   RE,FILLTWA          FILL IN THE MENU TWA                         
*                                                                               
         B     XIT                                                              
         SPACE 5                                                                
CONTINUE NTR1                                                                   
*                                                                               
* NOT FIRST CALL TO MENU -- THE MENU IS ALREADY ON THE SCREEN                   
*                                                                               
         L     RF,MNATWA                                                        
         AH    RF,MNVTABF          A(TAB FIELD ON SCREEN)                       
         OI    1(RF),X'01'         MODIFY TAB FIELD                             
         OI    6(RF),X'80'         XMIT TAB FIELD                               
*                                                                               
         BAS   RE,SELECTS          LOOK FOR SELECTS                             
         CLI   MNERR,MNERROKQ      WERE ALL SELECTS VALID?                      
         BNE   CONTX               NO                                           
*                                                                               
         CLI   PFKEY,0             DID USER HIT ENTER?                          
         BE    CONT20              YES -- RETURN SELECTS TO APPLICATION         
         CLC   PFKEY,MNPFCAN       CANCEL PFKEY                                 
         BE    CONT20              YES, DESELECTS DONE SO RETURN                
*                                                                               
CONT10   DS    0H                                                               
         BAS   RE,SCROLL           A PFKEY WAS HIT -- SCROLL                    
         BAS   RE,FILLTWA          FILL IN THE MENU TWA                         
         B     CONTX                                                            
*                                                                               
CONT20   DS    0H                                                               
         BAS   RE,RESTTWA          RESTORE APPLICATION SCREEN                   
         MVI   MNMODE,MNPROCQ      "PROCESS SELECTS" MODE                       
         BAS   RE,GOHOOK           HOOK TO APPLICATION                          
*                                                                               
CONTX    B     XIT                                                              
         EJECT                                                                  
SAVETWA  NTR1                                                                   
*                                                                               
* FIND LAST TWA FIELD WHICH IS PRIOR TO STARTING LINE NUMBER,                   
* AND SAVE TWA FROM THAT POINT ON FOR 6144 BYTES                                
*                                                                               
         L     R3,MNATWA           A(TWA)                                       
         LA    R2,64(R3)           SKIP 64-BYTE HEADER                          
         SR    R1,R1                                                            
*                                                                               
SVTWA10  SR    R0,R0                                                            
         ICM   R1,3,2(R2)          START OF DATA SCREEN ADR                     
         D     R0,=F'80'                                                        
         LA    R1,1(R1)            R1 = ABSOLUTE ROW NUMBER                     
         CLM   R1,1,MNSTRTLN       IS ROW >= STARTING ROW NUMBER?               
         BNL   SVTWA20             YES                                          
*                                                                               
         ZIC   R1,0(R2)            FIELD LENGTH                                 
         AR    R2,R1               A(NEXT FIELD HEADER)                         
         CLI   0(R2),0             END OF TWA?                                  
         BNE   SVTWA10             NO                                           
*                                                                               
SVTWA20  CLI   MNSAVEPG,0          SHOULD WE SAVE THE SCREEN?                   
         BE    SVTWA30             NO                                           
*                                                                               
         XC    DMCB(24),DMCB                                                    
         MVC   DMCB+8(1),MNSAVEPG  PAGE NUMBER                                  
         MVC   DMCB+10(2),2(R3)    TERMINAL NUMBER                              
         L     RE,MNACOM           A(COMFACS)                                   
         L     RF,CDATAMGR-COMFACSD(RE)                                         
         GOTO1 (RF),DMCB,(0,=C'DMWRT'),=C'TEMPSTR',,(R2)                        
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
SVTWA30  SR    R2,R3                                                            
         STH   R2,MNVFRSTF         DISP TO FIRST MENU FIELD                     
*                                                                               
         B     XIT                                                              
         EJECT                                                                  
RESTTWA  NTR1                                                                   
*                                                                               
* RESTORE AND TRANSMIT THE SAVED PORTION OF THE APPLICATION SCREEN              
*                                                                               
         CLI   MNSAVEPG,0          SHOULD WE RESTORE THE SCREEN?                
         BE    RSTWAX              NO                                           
*                                                                               
         L     RF,MNATWA           A(TWA)                                       
         LR    R2,RF                                                            
         AH    R2,MNVTITLF         A(TO BEGIN RESTORE)                          
         LH    R1,=H'6144'                                                      
         SH    R1,MNVTITLF         LENGTH TO RESTORE                            
*                                                                               
         XC    DMCB(24),DMCB                                                    
         MVC   DMCB+8(1),MNSAVEPG  PAGE NUMBER                                  
         MVC   DMCB+10(2),2(RF)    TERMINAL NUMBER                              
         MVC   DMCB+20(2),=C'L='                                                
         STH   R1,DMCB+22          PARTIAL RESTORE                              
         L     RE,MNACOM           A(COMFACS)                                   
         L     RF,CDATAMGR-COMFACSD(RE)                                         
         GOTO1 (RF),DMCB,(0,=C'DMREAD'),=C'TEMPSTR',,(R2)                       
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     R2,MNATWA                                                        
         LA    R2,64(R2)                                                        
         SR    R0,R0                                                            
RSTWA10  CLI   0(R2),0             END OF TWA?                                  
         BE    RSTWA20             YES                                          
         OI    6(R2),X'80'         XMIT                                         
         IC    R0,0(R2)                                                         
         AR    R2,R0               BUMP TO NEXT FIELD                           
         B     RSTWA10                                                          
*                                                                               
RSTWA20  MVC   1(2,R2),=X'0100'    CLEAR BEFORE/AFTER                           
*                                                                               
RSTWAX   B     XIT                                                              
         EJECT                                                                  
PFKEYS   NTR1                                                                   
*                                                                               
* BUILD THE PFKEY INSTRUCTIONS                                                  
*                                                                               
         LA    R1,DMCB                                                          
         USING GETTXTD,R1                                                       
         XC    GTBLOCK,GTBLOCK     CLEAR PARAMETER BLOCK                        
         LA    R4,PFDSPLAY         BUILD PFKEY INSTRUCTIONS HERE                
         MVI   GTMTYP,GTMSCR       SCREEN MESSAGE                               
         LA    RF,HALF                                                          
         STCM  RF,7,GTATXT         A(SUBSTITUTION TEXT FOR &T)                  
         MVI   GTMSYS,X'FF'        GENERAL SYSTEM MESSAGES                      
         L     RE,MNACOM                                                        
         L     RF,CGETTXT-COMFACSD(RE)  RF=A(GETTXT)                            
*                                                                               
         MVC   BYTE,MNPFUP         APPLICATION 'UP' PF KEY NUMBER               
         CLI   BYTE,0              WAS ONE SUPPLIED?                            
         BNE   *+8                 YES                                          
         MVI   BYTE,DEFPFUPQ                                                    
         MVI   GTMSGNO1,110        MESSAGE 110 = PF&T=UP                        
         BAS   RE,ONEPFKEY                                                      
*                                                                               
         MVC   BYTE,MNPFDN         APPLICATION 'DOWN' PF KEY NUMBER             
         CLI   BYTE,0              WAS ONE SUPPLIED?                            
         BNE   *+8                 YES                                          
         MVI   BYTE,DEFPFDNQ                                                    
         MVI   GTMSGNO1,111        MESSAGE 111 = PF&T=DOWN                      
         BAS   RE,ONEPFKEY                                                      
*                                                                               
         MVC   BYTE,MNPFTOP        APPLICATION 'TOP' PF KEY NUMBER              
         CLI   BYTE,0              WAS ONE SUPPLIED?                            
         BNE   *+8                 YES                                          
         MVI   BYTE,DEFPFTPQ                                                    
         MVI   GTMSGNO1,112        MESSAGE 112 = PF&T=TOP                       
         BAS   RE,ONEPFKEY                                                      
*                                                                               
         MVC   BYTE,MNPFBOT        APPLICATION 'BOTTOM' PF KEY NUMBER           
         CLI   BYTE,0              WAS ONE SUPPLIED?                            
         BNE   *+8                 YES                                          
         MVI   BYTE,DEFPFBTQ                                                    
         MVI   GTMSGNO1,113        MESSAGE 113 = PF&T=BOTTOM                    
         BAS   RE,ONEPFKEY                                                      
*                                                                               
         MVC   BYTE,MNPFCAN        APPLICATION 'CANCEL' PF KEY NUMBER           
         CLI   BYTE,0              WAS ONE SUPPLIED?                            
         BE    PF20                NO DEFAULT FOR CANCEL                        
         MVI   GTMSGNO1,115        MESSAGE 115 = PF&T=CANCEL                    
         BAS   RE,ONEPFKEY                                                      
*                                                                               
PF20     DS    0H                                                               
PFX      B     XIT                                                              
         EJECT                                                                  
ONEPFKEY ST    RE,FULL             MUST SAVE RE SINCE WE GO TO GETTXT           
         EDIT  BYTE,(2,HALF),ALIGN=LEFT                                         
         STC   R0,GTLTXT           L'(SUBSTITUTION TEXT)                        
         MVI   GTMAXL,24           24-BYTE MAXIMUM                              
         STCM  R4,7,GTAOUT         A(OUTPUT AREA)                               
         MVI   GT1INDS,GT1OWRK     GTAOUT IS A WORK AREA                        
         BASR  RE,RF                                                            
         ZIC   R2,DMCB+4           MESSAGE LENGTH                               
         LA    R4,1(R2,R4)         BUMP UP POINTER                              
         L     RE,FULL                                                          
         BR    RE                                                               
*                                                                               
         DROP  R1                                                               
         EJECT                                                                  
BLDTWA   NTR1                                                                   
*                                                                               
* BUILD THE TWA.  WE BUILD EACH LINE IN COLUMNS 2 THROUGH 80 OF THE             
* SCREEN.  IF WE ALLOW ONE BLANK BETWEEN ALL SELECT AND VALUE FIELDS,           
* THEN THE FORMULA FOR COMPUTING THE LENGTH OF EACH VALUE FIELD IS:             
*                                                                               
* LENGTH = (79 - (MNNCOLS * (MNSELWID + 1)) - (MNNCOLS - 1)) / MNNCOLS          
*                                                                               
         ZIC   RE,MNSELWID         WIDTH OF SELECTS                             
         LA    RE,1(RE)            PLUS ONE BLANK BEFORE VALUE FIELD            
         STH   RE,HALF                                                          
         IC    RE,MNNCOLS                                                       
         MH    RE,HALF             TIMES NUMBER OF COLUMNS                      
         LA    RF,79               79 COLUMNS PER LINE                          
         CLI   MNSELWID,0          IF THERE ARE NO SELECT FIELDS. . .           
         BE    *+6                 . . . THEN FORMULA GETS ADJUSTED             
         SR    RF,RE                                                            
         ZIC   RE,MNNCOLS                                                       
         BCTR  RE,0                                                             
         SR    RF,RE                                                            
         SR    RE,RE                                                            
         ZIC   R0,MNNCOLS          MUST DIVIDE BY NUMBER OF COLUMNS             
         DR    RE,R0               RF = LENGTH OF EACH VALUE FIELD              
         STC   RF,VALUELEN                                                      
*                                                                               
         LA    R1,DMCB                                                          
         USING TWAPARMD,R1                                                      
         XC    DMCB(24),DMCB                                                    
         L     RF,MNATWA                                                        
         AH    RF,MNVFRSTF                                                      
         ST    RF,TWAPAOUT         A(FIRST TWA FIELD TO BUILD)                  
         MVC   TWAPATWA,MNATWA     A(TWA)                                       
*                                                                               
         LA    R3,TWAELEM                                                       
         ST    R3,TWAPAFST         A(BUILD ELEMENT)                             
         USING TWAELEMD,R3                                                      
         MVI   TWAELCD,1           CAN BE ANYTHING NON-ZERO                     
         MVI   TWAELLN,TWAELLNQ                                                 
         MVC   TWAERLN,MNSTRTLN    ABSOLUTE STARTING ROW NUMBER                 
         OI    TWAERLN,X'80'                                                    
*                                                                               
         MVC   MNVTITLF,MNVFRSTF   DISPLACMENT TO FIRST TITLE FIELD             
         CLI   MNATITLN,0          ANY TITLES GIVEN?                            
         BE    BUILD20             NO                                           
*                                                                               
         ZIC   R0,MNATITLN         R0 = NUMBER OF TITLE LINES                   
         MVI   TWAEFLN,79          YES -- ALWAYS 79 BYTES LONG                  
         MVI   TWAEATB,X'60'       PROTECTED AND LOWER CASE                     
BUILD10  GOTO1 MNATWBLD            BUILD TITLE FIELD                            
         CLI   TWAPERRS,TWAPEOK    ANY ERROR?                                   
         BE    *+6                 NO                                           
         DC    H'0'                                                             
         MVC   TWAPAOUT,TWAPANXT   BUMP UP TWA POINTER                          
         MVI   TWAERLN,1           BUMP TO NEXT ROW                             
         BCT   R0,BUILD10                                                       
*                                                                               
BUILD20  ZIC   R4,MNSTRTLN         STARTING ROW NUMBER                          
         ZIC   R0,MNATITLN         NUMBER OF TITLES                             
         AR    R4,R0                                                            
         SH    R4,=H'24'           24 LINES PER SCREEN                          
         LPR   R4,R4               R4 = NUMBER OF VALUE LINES TO BUILD          
         MVI   BYTE,0              NO FIELDS FOUND YET                          
*                                                                               
* BUILD SELECT AND VALUE FIELDS (ONE PAIR AT A TIME)                            
*                                                                               
BUILD30  ZIC   R0,MNNCOLS          OUTER LOOP -- NUMBER OF COLUMNS              
*                                                                               
BUILD40  CLI   MNSELWID,0          INNER LOOP -- ANY SELECT FIELDS?             
         BE    BUILD50             NO                                           
         CLI   BYTE,0              IS THIS THE FIRST FIELD?                     
         BNE   *+16                NO                                           
         L     RF,TWAPAOUT                                                      
         S     RF,MNATWA                                                        
         STH   RF,MNVFRSTF         DISPLACEMENT TO FIRST SELECT FIELD           
         MVC   TWAEFLN,MNSELWID    LENGTH OF SELECT FIELD                       
         MVI   TWAEATB,X'22'       PROTECTED (TO START) AND EXTENDED            
         MVC   TWAEFLD,MNSELID     ID NUMBER (DEFAULT)                          
         GOTO1 MNATWBLD            BUILD SELECT FIELD                           
         CLI   TWAPERRS,TWAPEOK    ANY ERROR?                                   
         BE    *+6                 NO                                           
         DC    H'0'                                                             
         MVC   TWAPAOUT,TWAPANXT   BUMP UP TWA POINTER                          
         MVI   BYTE,X'FF'          SET FLAG -- WE HAVE A FIELD                  
*                                                                               
BUILD50  MVC   TWAEFLN,VALUELEN    LENGTH OF VALUE FIELD                        
         CLI   BYTE,0              IS THIS THE FIRST FIELD?                     
         BE    *+12                YES                                          
         MVI   TWAERLN,0           NO -- STAY ON THE ROW                        
         B     *+16                                                             
         L     RF,TWAPAOUT                                                      
         S     RF,MNATWA                                                        
         STH   RF,MNVFRSTF         DISPLACEMENT TO FIRST VALUE FIELD            
         MVI   TWAEATB,X'60'       PROTECTED AND LOWER CASE                     
         MVI   TWAEFLD,0           NO ID NUMBER FOR PROTECTED FIELDS            
         GOTO1 MNATWBLD            BUILD VALUE FIELD                            
         CLI   TWAPERRS,TWAPEOK    ANY ERROR?                                   
         BE    *+6                 NO                                           
         DC    H'0'                                                             
         MVC   TWAPAOUT,TWAPANXT   BUMP UP TWA POINTER                          
         MVI   BYTE,X'FF'          SET FLAG -- WE HAVE A FIELD                  
         BCT   R0,BUILD40          DO NEXT PAIR OF FIELDS                       
*                                                                               
         MVI   TWAERLN,1           BUMP TO NEXT ROW                             
         BCT   R4,BUILD30                                                       
*                                                                               
         MVI   TWAEFLN,L'PFDSPLAY  BUILD PFKEY INSTRUCTION FIELD                
         MVI   TWAERLN,24+X'80'    ABSOLUTE LINE 24                             
         GOTO1 MNATWBLD            BUILD PFKEY FIELD                            
         CLI   TWAPERRS,TWAPEOK    ANY ERROR?                                   
         BE    *+6                 NO                                           
         DC    H'0'                                                             
         L     RF,TWAPAOUT                                                      
         MVC   8(L'PFDSPLAY,RF),PFDSPLAY   MOVE INSTRUCTIONS TO SCREEN          
         MVC   TWAPAOUT,TWAPANXT   BUMP UP TWA POINTER                          
*                                                                               
         MVI   TWAEFLN,1           BUILD ONE BYTE TAB FIELD. . .                
         MVI   TWAECOL,78          . . . ON LINE 24, COLUMN 78. . .             
         MVI   TWAEATB,X'01'       . . . UNPROTECTED, MODIFIED                  
         GOTO1 MNATWBLD            BUILD TAB FIELD                              
         CLI   TWAPERRS,TWAPEOK    ANY ERROR?                                   
         BE    *+6                 NO                                           
         DC    H'0'                                                             
         L     RF,TWAPAOUT                                                      
         S     RF,MNATWA                                                        
         STH   RF,MNVTABF          DISPLACEMENT TO TAB FIELD                    
*                                                                               
         DROP  R1,R3                                                            
*                                                                               
         B     XIT                                                              
         EJECT                                                                  
FILLTWA  NTR1                                                                   
*                                                                               
* CLEAR SCREEN, THEN HOOK TO APPLICATION WITH EACH VALUE TABLE ENTRY            
* TO FILL IN TWA FIELDS WITH SELECTIONS.                                        
*                                                                               
* THE FIELD 'FULL' CONTAINS THE ADDRESS OF THE FIRST VALUE TABLE                
* ENTRY FROM WHICH TO DISPLAY (GETS COMPUTED BY SCROLLING ROUTINE).             
*                                                                               
         OC    FULL,FULL           DO WE KNOW WHERE TO START?                   
         BZ    FILLX               NO                                           
*                                                                               
         L     R2,MNATWA                                                        
         AH    R2,MNVTITLF         A(FIRST TWA FIELD IN MENU)                   
         LR    R3,R2               SAVE IT                                      
*                                                                               
FILL10   MVI   4(R2),0             RESET INPUT INDICATORS                       
         MVI   5(R2),0             RESET INPUT LENGTH                           
         TM    1(R2),X'20'         IS FIELD PROTECTED?                          
         BO    *+12                YES                                          
         OI    1(R2),X'20'         PROTECT THE FIELD                            
         OI    6(R2),X'80'         XMIT                                         
*                                                                               
         ZIC   R1,0(R2)                                                         
         SH    R1,=H'9'            HEADER LENGTH MINUS 1 FOR EX                 
         TM    1(R2),X'02'         EXTENDED HEADER?                             
         BZ    *+8                 NO                                           
         SH    R1,=H'8'                                                         
*                                                                               
         EX    R1,*+8              PAD WITH BLANKS                              
         B     *+10                                                             
         OC    8(0,R2),=80C' '                                                  
         EX    R1,*+8              IS FIELD EMPTY?                              
         B     *+10                                                             
         CLC   8(0,R2),=80C' '                                                  
         BE    *+22                YES                                          
         EX    R1,*+8              NO -- CLEAR FIELD                            
         B     *+10                                                             
         XC    8(0,R2),8(R2)                                                    
         OI    6(R2),X'80'         XMIT                                         
*                                                                               
         ZIC   R0,0(R2)            BUMP TO NEXT FIELD                           
         AR    R2,R0                                                            
*                                                                               
         SR    R0,R0                                                            
         ICM   R1,3,2(R2)          START OF DATA SCREEN ADR                     
         D     R0,=F'80'                                                        
         CH    R1,=H'23'           HAVE WE HIT THE PFKEY LINE?                  
         BNE   FILL10              NO                                           
*                                                                               
         CLI   MNATITLN,0          ANY TITLES?                                  
         BE    FILL20              NO                                           
         ICM   RF,7,MNATITLE       A(FIRST TITLE)                               
         ZIC   R0,MNATITLN         NUMBER OF TITLES                             
         MVC   8(79,R3),0(RF)      MOVE TITLE INTO TWA FIELD                    
         LA    RF,79(RF)           NEXT TITLE IN BLOCK                          
         LA    R3,87(R3)           NEXT TWA FIELD (79 + 8-BYTE HEADER)          
         BCT   R0,*-14             DO EACH TITLE                                
*                                                                               
FILL20   MVI   MNMODE,MNDISPQ      DISPLAY MODE                                 
         MVI   BYTE,0              NUMBER OF COLUMNS COMPLETED                  
         L     R2,MNATWA                                                        
         AH    R2,MNVFRSTF         FIRST TWA FIELD IN MENU                      
         L     R3,FULL             A(FIRST MENU ITEM TO DISPLAY)                
         USING MNVALENT,R3                                                      
*                                                                               
         LR    R0,R3                                                            
         SR    R0,R9                                                            
         STCM  R0,7,MNVFRSTE       DISPLACEMENT TO THIS ENTRY                   
*                                                                               
FILL30   ST    R3,MNAVENT          A(THIS VALUE TABLE ENTRY)                    
         BAS   RE,GOHOOK           HOOK TO APPLICATION                          
*                                                                               
         TM    MNVFLAG1,MNVNOOPQ   IS FIELD A NO-OP?                            
         BO    FILL70              YES -- SKIP THE ENTRY                        
*                                                                               
         TM    MNVFLAG1,MNVHEADQ   IS FIELD A HEADING?                          
         BZ    *+12                NO                                           
         LR    R0,R3                                                            
         SR    R0,R9                                                            
         STCM  R0,7,MNVLASTH       DISPLACEMENT TO THIS ENTRY                   
*                                                                               
         CLI   MNSELWID,0          ARE THERE SELECT FIELDS?                     
         BE    FILL50              NO                                           
         TM    MNVFLAG1,MNVNOSLQ   IS FIELD SELECTABLE?                         
         BO    *+12                NO -- IT'S ALREADY PROTECTED                 
         NI    1(R2),X'FF'-X'20'   YES -- UNPROTECT IT                          
         OI    6(R2),X'80'         XMIT                                         
*                                                                               
         TM    MNVFLAG1,MNVSELEQ   WAS ITEM SELECTED?                           
         BZ    FILL40              NO                                           
         ZIC   R1,MNSELWID                                                      
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   8(0,R2),MNVSELVL    DISPLAY SELECT VALUE                         
         OI    6(R2),X'80'         XMIT THE FIELD                               
*                                                                               
FILL40   IC    R1,0(R2)                                                         
         AR    R2,R1               BUMP TO VALUE FIELD                          
         CLI   MNVSELID,0          ANY OVERRIDE ID NUMBER GIVEN?                
         BE    FILL50              NO                                           
         SH    R2,=H'8'            BACK UP TO EXTENSION OF SELECT FIELD         
         MVC   0(1,R2),MNVSELID    MOVE IN OVERRIDE SELECT                      
         MVC   4(1,R2),MNSCRID     SET HELP SCREEN ID                           
         LA    R2,8(R2)            RESTORE R2                                   
*                                                                               
FILL50   ZIC   R1,0(R2)            LENGTH OF TWA VALUE FIELD                    
         SH    R1,=H'9'            8 FOR HEADER, 1 FOR EX                       
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   8(0,R2),MNVALTXT    PUT VALUE TEXT IN TWA FIELD                  
         OI    6(R2),X'80'         XMIT THE FIELD                               
*                                                                               
         TM    MNVFLAG1,MNVHINTQ   TEST HIGH INTENSITY                          
         BZ    *+8                                                              
         OI    6(R2),X'08'                                                      
         TM    MNVFLAG1,MNVLINTQ   TEST LOW INTENSITY                           
         BZ    *+8                                                              
         OI    6(R2),X'0C'                                                      
*                                                                               
         SR    R0,R0                                                            
         ICM   R1,3,2(R2)          START OF DATA SCREEN ADR                     
         D     R0,=F'80'                                                        
         CH    R1,=H'22'           LAST LINE OF MENU ITEMS? (LINE 23)           
         BNE   FILL60              NO                                           
*                                                                               
         ZIC   R1,BYTE             INCREMENT COLUMNS COMPLETED                  
         LA    R1,1(R1)                                                         
         STC   R1,BYTE                                                          
         CLC   BYTE,MNNCOLS        ARE THERE MORE COLUMNS TO FILL IN?           
         BE    FILLX               NO                                           
*                                                                               
         L     R2,MNATWA                                                        
         AH    R2,MNVFRSTF         FIRST TWA FIELD IN MENU                      
         IC    R1,BYTE             NUMBER OF COLUMNS TO SKIP                    
         CLI   MNSELWID,0          ANY SELECTS?                                 
         BE    *+8                 NO                                           
         SLL   R1,1                YES -- SKIP OVER THEM AS WELL                
         SR    R0,R0                                                            
         IC    R0,0(R2)                                                         
         AR    R2,R0                                                            
         BCT   R1,*-6              BUMP TO TOP OF NEXT COLUMN                   
         B     FILL70                                                           
*                                                                               
FILL60   ZIC   R1,MNNCOLS          NUMBER OF COLUMNS                            
         CLI   MNSELWID,0          ANY SELECTS?                                 
         BE    *+14                NO                                           
         BCTR  R1,0                                                             
         SLL   R1,1                                                             
         LA    R1,1(R1)            R1 = NUMBER OF FIELDS TO SKIP                
         ZIC   R0,0(R2)                                                         
         AR    R2,R0               BUMP TO NEXT SELECT FIELD ON. . .            
         BCT   R1,*-6              . . . FOLLOWING ROW                          
*                                                                               
FILL70   ZIC   R0,MNVALLEN                                                      
         AR    R3,R0               BUMP TO NEXT VALUE TABLE ENTRY               
         CLI   MNVALLEN,0          END OF VALUE TABLE?                          
         BNE   FILL30              NO                                           
*                                                                               
FILLX    B     XIT                                                              
         EJECT                                                                  
SELECTS  NTR1                                                                   
*                                                                               
* PROCESS SELECTS (HANDLE MODE MNVSELQ)                                         
*                                                                               
         MVI   MNERR,MNERROKQ      ASSUME ALL SELECTS WILL BE VALID             
         CLI   PFKEY,0                                                          
         BE    SEL10                                                            
         CLC   PFKEY,MNPFCAN       WAS CANCEL PFKEY HIT?                        
         BNE   SEL10               NO                                           
*                                  CANCEL = DESELECT ALL AND RETURN             
         LA    R3,L'MNVHEAD(R9)    A(FIRST MENU ITEM)                           
*                                                                               
SEL06    DS    0H                                                               
         CLI   MNVALLEN,0          EOT                                          
         BE    SELX                                                             
         NI    MNVFLAG1,255-MNVSELEQ   DESELECT                                 
         XC    MNVSELVL,MNVSELVL                                                
         ZIC   R0,MNVALLEN                                                      
         AR    R3,R0                                                            
         B     SEL06                                                            
*                                                                               
SEL10    CLI   MNSELWID,0          ANY SELECT FIELDS ON SCREEN?                 
         BE    SELX                NO -- NOTHING TO DO                          
*                                                                               
         MVI   MNMODE,MNVSELQ      "VALIDATE SELECT" MODE                       
         MVI   BYTE,0              NUMBER OF COLUMNS COMPLETED                  
         MVI   HALF,0              NO SELECTS FOUND YET                         
         L     R2,MNATWA                                                        
         AH    R2,MNVFRSTF         FIRST TWA FIELD IN MENU                      
         LR    R3,R9               A(VALUE TABLE)                               
         SR    R0,R0                                                            
         ICM   R0,7,MNVFRSTE       DISPLACEMENT TO FIRST ENTRY                  
         AR    R3,R0               A(FIRST VALUE TABLE ENTRY DISPLAYED)         
*                                                                               
SEL20    XC    MNVSELVL,MNVSELVL   ASSUME ITEM WAS NOT SELECTED                 
         NI    MNVFLAG1,X'FF'-MNVSELEQ                                          
         TM    MNVFLAG1,MNVNOOPQ   IS THIS ENTRY A NO-OP?                       
         BO    SEL50               YES -- SKIP IT                               
         TM    MNVFLAG1,MNVNOSLQ   IS THIS ENTRY NON-SELECTABLE?                
         BO    SEL30               YES                                          
         CLI   5(R2),0             WAS THIS ITEM SELECTED?                      
         BE    SEL30               NO                                           
*                                                                               
         ZIC   R1,MNSELWID                                                      
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   MNVSELVL(0),8(R2)   PUT SELECT VALUE INTO VALUE TABLE            
         ST    R3,MNAVENT          A(THIS VALUE TABLE ENTRY)                    
         BAS   RE,GOHOOK           HOOK TO APPLICATION                          
*                                                                               
         CLI   MNERR,MNERROKQ      WAS SELECT VALUE OK?                         
         BE    *+12                YES                                          
         STCM  R2,7,MNACURSR       USER SHOULD PUT CURSOR AT THIS FIELD         
         B     SELX                GOOD-BYE                                     
*                                                                               
         CLI   HALF,0              IS THIS THE FIRST SELECT?                    
         BNE   *+16                                                             
         MVI   HALF,X'FF'          YES                                          
         OI    MNVFLAG1,MNVSELEQ   FLAG ITEM AS SELECTED                        
         B     SEL30                                                            
         TM    MNCNTL,MNMULTQ      ARE MULTIPLE SELECTS ALLOWED?                
         BZ    *+12                NO                                           
         OI    MNVFLAG1,MNVSELEQ   FLAG ITEM AS SELECTED                        
         B     SEL30                                                            
         MVI   MNERR,MNERRMSQ                                                   
         STCM  R2,7,MNACURSR       USER SHOULD PUT CURSOR AT THIS FIELD         
         B     SELX                GOOD-BYE                                     
*                                                                               
SEL30    ZIC   R1,0(R2)                                                         
         AR    R2,R1               BUMP TO VALUE FIELD                          
         SR    R0,R0                                                            
         ICM   R1,3,2(R2)          START OF DATA SCREEN ADR                     
         D     R0,=F'80'                                                        
         CH    R1,=H'22'           LAST LINE OF MENU ITEMS? (LINE 23)           
         BNE   SEL40               NO                                           
*                                                                               
         ZIC   R1,BYTE             INCREMENT COLUMNS COMPLETED                  
         LA    R1,1(R1)                                                         
         STC   R1,BYTE                                                          
         CLC   BYTE,MNNCOLS        ARE THERE MORE COLUMNS TO FILL IN?           
         BE    SELX                NO                                           
*                                                                               
         L     R2,MNATWA                                                        
         AH    R2,MNVFRSTF         FIRST TWA FIELD IN MENU                      
         IC    R1,BYTE             NUMBER OF COLUMNS TO SKIP                    
         SLL   R1,1                YES -- SKIP OVER THEM AS WELL                
         SR    R0,R0                                                            
         IC    R0,0(R2)                                                         
         AR    R2,R0                                                            
         BCT   R1,*-6              BUMP TO TOP OF NEXT COLUMN                   
         B     SEL50                                                            
*                                                                               
SEL40    ZIC   R1,MNNCOLS          NUMBER OF COLUMNS                            
         BCTR  R1,0                                                             
         SLL   R1,1                                                             
         LA    R1,1(R1)            R1 = NUMBER OF FIELDS TO SKIP                
         ZIC   R0,0(R2)                                                         
         AR    R2,R0               BUMP TO NEXT SELECT FIELD ON. . .            
         BCT   R1,*-6              . . . FOLLOWING ROW                          
*                                                                               
SEL50    ZIC   R0,MNVALLEN                                                      
         AR    R3,R0               BUMP TO NEXT VALUE TABLE ENTRY               
         CLI   MNVALLEN,0          END OF VALUE TABLE?                          
         BNE   SEL20               NO                                           
*                                                                               
SELX     B     XIT                                                              
         EJECT                                                                  
SCROLL   NTR1                                                                   
*                                                                               
* GIVEN THE CURRENT MENU DISPLAY, FIGURE OUT WHERE TO START                     
* DISPLAYING (DEPENDING UPON THE PFKEY HIT).  R4 WILL CONTAIN THE               
* NUMBER OF VALUES TO SCROLL (UP OR DOWN ONLY).                                 
*                                                                               
         XC    FULL,FULL           THIS HOLDS A(STARTING VALUE ENTRY)           
*                                                                               
         MVC   BYTE,MNPFTOP        APPLICATION 'TOP' PF KEY NUMBER              
         CLI   BYTE,0              WAS ONE SUPPLIED?                            
         BNE   *+8                 YES                                          
         MVI   BYTE,DEFPFTPQ                                                    
         CLC   PFKEY,BYTE          SCROLL TO TOP?                               
         BNE   *+16                NO                                           
         LA    RE,L'MNVHEAD(R9)    YES -- RE = A(1ST VALUE TABLE ITEM)          
         ST    RE,FULL                                                          
         B     SCRLX                                                            
*                                                                               
         ZIC   R2,MNSTRTLN         STARTING ROW NUMBER                          
         ZIC   RE,MNATITLN         NUMBER OF TITLE LINES                        
         AR    R2,RE                                                            
         SH    R2,=H'24'           24 LINES PER SCREEN                          
         LPR   R2,R2               R2 = NUMBER OF VALUE LINES                   
         MVI   HALF,0                                                           
         MVC   HALF+1(1),MNNCOLS   NUMBER OF COLUMNS                            
         MH    R2,HALF             R2 = NUMBER OF VALUE FIELDS                  
*                                                                               
         LH    R4,MNSCROLL         SCROLL VALUE                                 
         LTR   R4,R4               WAS ONE GIVEN?                               
         BP    *+14                YES -- IT'S AN ABSOLUTE SCROLL VALUE         
         LR    R4,R2                                                            
         BZ    *+8                 NO -- DEFAULT TO FULL-PAGE SCROLL            
         SRL   R4,1                YES -- HALVE NUMBER OF FIELDS                
*                                                                               
         MVC   BYTE,MNPFBOT        APPLICATION 'BOTTOM' PF KEY NUMBER           
         CLI   BYTE,0              WAS ONE SUPPLIED?                            
         BNE   *+8                 YES                                          
         MVI   BYTE,DEFPFBTQ                                                    
         CLC   PFKEY,BYTE          SCROLL TO BOTTOM?                            
         BNE   SCRL30              NO                                           
*                                                                               
         SR    R3,R3                                                            
         ICM   R3,7,MNVLASTE       DISPLACEMENT TO END OF VALUE TABLE           
         AR    R3,R9               A(END OF VALUE TABLE)                        
         SR    R1,R1               COUNT NUMBER OF ENTRIES                      
         ZIC   R0,MNVLASTL         LENGTH OF LAST ENTRY                         
*                                                                               
SCRL10   SR    R3,R0               BACK UP TO PREVIOUS ENTRY                    
         CLI   MNVBACKL,0          BEGINNING OF VALUE TABLE?                    
         BNE   *+12                NO                                           
         ST    R3,FULL             SAVE A(START OF DISPLAY)                     
         B     SCRLX                                                            
*                                                                               
         TM    MNVFLAG1,MNVNOOPQ   IS FIELD A NO-OP?                            
         BO    SCRL20              YES -- SKIP THE ENTRY                        
         LA    R1,1(R1)            INCREMENT NUMBER OF ENTRIES                  
         CR    R1,R2               HAVE WE BUMPED BACK ENOUGH?                  
         BL    SCRL20              NO                                           
         ST    R3,FULL             SAVE A(START OF DISPLAY)                     
         B     SCRLX                                                            
*                                                                               
SCRL20   IC    R0,MNVBACKL         LENGTH OF PREVIOUS ENTRY                     
         B     SCRL10                                                           
*                                                                               
SCRL30   MVC   BYTE,MNPFUP         APPLICATION 'UP' PF KEY NUMBER               
         CLI   BYTE,0              WAS ONE SUPPLIED?                            
         BNE   *+8                 YES                                          
         MVI   BYTE,DEFPFUPQ                                                    
         CLC   PFKEY,BYTE          SCROLL UP?                                   
         BNE   SCRL40              NO                                           
*                                                                               
         SR    R3,R3                                                            
         ICM   R3,7,MNVFRSTE       DISPLACEMENT TO 1ST ENTRY DISPLAYED          
         AR    R3,R9               A(1ST ENTRY DISPLAYED)                       
         SR    R1,R1               COUNT NUMBER OF ENTRIES                      
         ZIC   R0,MNVBACKL         LENGTH OF PREVIOUS ENTRY                     
         LR    R2,R4               NUMBER OF VALUES TO SCROLL                   
         B     SCRL10              LOGIC IS SAME AS 'BOTTOM'                    
*                                                                               
SCRL40   MVC   BYTE,MNPFDN         APPLICATION 'DOWN' PF KEY NUMBER             
         CLI   BYTE,0              WAS ONE SUPPLIED?                            
         BNE   *+8                 YES                                          
         MVI   BYTE,DEFPFDNQ                                                    
         CLC   PFKEY,BYTE          SCROLL DOWN?                                 
         BNE   SCRLX               IGNORE ALL OTHER PFKEYS                      
*                                                                               
         LR    R3,R9               A(VALUE TABLE)                               
         SR    R0,R0                                                            
         ICM   R0,7,MNVFRSTE       DISPLACEMENT TO FIRST ENTRY                  
         AR    R3,R0               A(FIRST VALUE TABLE ENTRY DISPLAYED)         
         SR    R1,R1               COUNT NUMBER OF ENTRIES                      
*                                                                               
SCRL50   TM    MNVFLAG1,MNVNOOPQ   IS FIELD A NO-OP?                            
         BO    SCRL60              YES -- SKIP THE ENTRY                        
         LA    R1,1(R1)            INCREMENT ENTRY COUNTER                      
         CR    R1,R4               HAVE WE BUMPED UP ENOUGH?                    
         BNH   SCRL60              NO                                           
         ST    R3,FULL             SAVE A(START OF DISPLAY)                     
         B     SCRLX                                                            
*                                                                               
SCRL60   ZIC   R0,MNVALLEN         BUMP TO NEXT ENTRY                           
         AR    R3,R0                                                            
         CLI   MNVALLEN,0          END OF VALUE TABLE?                          
         BNE   SCRL50              NO                                           
*                                                                               
SCRLX    B     XIT                                                              
         DROP  R3                                                               
         EJECT                                                                  
GOHOOK   NTR1                                                                   
*                                                                               
         L     RF,MNHOOK           A(USER HOOK)                                 
         LTR   RF,RF                                                            
         BZ    GOHOOKX             NO HOOK GIVEN                                
*                                                                               
         L     RE,USERRD           USER'S RD                                    
         LM    R0,RC,20(RE)        RESTORE USER'S R0-RC                         
         BASR  RE,RF               GO TO HOOK                                   
*                                                                               
GOHOOKX  XIT1                                                                   
         SPACE 5                                                                
FILLBACK NTR1                                                                   
*                                                                               
* FILL IN THE BACKWARDS LENGTHS IN THE VALUE TABLE                              
*                                                                               
         LA    R3,L'MNVHEAD(R9)    A(FIRST MENU ITEM)                           
         USING MNVALENT,R3                                                      
         CLI   MNVALLEN,0          IS THERE A FIRST ENTRY?                      
         BNE   *+6                 YES                                          
         DC    H'0'                NO VALUE TABLE ENTRIES AT ALL                
         MVI   MNVBACKL,0          FIRST ENTRY HAS NO BACKWARD VALUE            
         SR    R0,R0                                                            
*                                                                               
FB10     MVC   BYTE,MNVALLEN       HANG ON TO THIS LENGTH                       
         IC    R0,MNVALLEN                                                      
         AR    R3,R0               BUMP TO NEXT VALUE                           
         CLI   MNVALLEN,0          END OF TABLE?                                
         BE    *+14                YES                                          
         MVC   MNVBACKL,BYTE       PUT BACKWARD LENGTH IN TABLE ENTRY           
         B     FB10                                                             
*                                                                               
         MVC   MNVLASTL,BYTE       SAVE LENGTH OF LAST VALTABLE ENTRY           
         SR    R3,R9                                                            
         STCM  R3,7,MNVLASTE       SAVE DISPLACMENT TO END OF VALTABLE          
*                                                                               
         B     XIT                                                              
         DROP  R3                                                               
         EJECT                                                                  
DEFPFUPQ EQU   7                   DEFAULT 'UP' PFKEY NUMBER                    
DEFPFDNQ EQU   8                   DEFAULT 'DOWN' PFKEY NUMBER                  
DEFPFTPQ EQU   9                   DEFAULT 'TOP' PFKEY NUMBER                   
DEFPFBTQ EQU   10                  DEFAULT 'BOTTOM' PFKEY NUMBER                
DEFPFSLQ EQU   12                  DEFAULT 'SELECT' PFKEY NUMBER                
         SPACE 3                                                                
         LTORG                                                                  
         SPACE 3                                                                
*              WORKING STORAGE                                                  
         SPACE                                                                  
WORKD    DSECT                                                                  
*                                                                               
DUB      DS    D                                                                
DMCB     DS    6F                                                               
USERRD   DS    F                   USER'S RD (FOR HOOKING)                      
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
WORK     DS    CL17                                                             
PFKEY    DS    X                   PFKEY HIT (ADJUSTED 1..12), 0=ENTER          
TWAELEM  DS    XL(TWAELLNQ)        BUILD TWABLD ELEMENT HERE                    
TWAELEMX DS    X                   ALWAYS NULL (END OF ELEMENT LIST)            
PFDSPLAY DS    CL76                PFKEY INSTRUCTIONS                           
VALUELEN DS    X                   LENGTH OF EACH VALUE FIELD                   
*                                                                               
WORKDX   EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE DDMENUBLK                                                      
         EJECT                                                                  
       ++INCLUDE DDTWABLDD                                                      
         PRINT OFF                                                              
         EJECT                                                                  
       ++INCLUDE FAGETTXTD                                                      
         EJECT                                                                  
       ++INCLUDE DDCOMFACS                                                      
         EJECT                                                                  
       ++INCLUDE FATIOB                                                         
         PRINT ON                                                               
         SPACE 3                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'017DDMENU    05/01/02'                                      
         END                                                                    
