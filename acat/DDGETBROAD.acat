*          DATA SET DDGETBROAD AT LEVEL 010 AS OF 05/01/02                      
*CATALP GETBROAD                                                                
         SPACE 1                                                                
*===========================================================*                   
* PARAMETER 1 A(6-BYTE EBCDIC DATE)                         *                   
* PARAMETER 2 A(12-BYTE START-END OUT)                      *                   
*                                                           *                   
* WHEN PARAMETER 1 BYTE 0=X'01' ...                         *                   
* PARAMETER 3 A(GETDAY)                                     *                   
* PARAMETER 4 A(ADDAY)                                      *                   
*                                                           *                   
* WHEN PARAMETER 1 BYTE 0=X'02' ...                         *                   
* PARAMETER 5 A(SYSPROF)                                    *                   
*                                                           *                   
* AFTER GETBROAD                                            *                   
* BYTE 0 PARA 1  X'FF' = INVALID                            *                   
*                X'04' = 4-WEEK MONTH                       *                   
*                X'05' = 5-WEEK MONTH                       *                   
*===========================================================*                   
         SPACE 1                                                                
*===========================================================*                   
* AS OF 3/1/89 THIS MODULE IS ALSO CORE-RESIDENT AS T00A1D  *                   
* 21JUL98 EJOR P1=X'02' MEANS P5=A(SYSPROF) SO SPOT WRITER  *                   
*              CAN GET AT IT                                *                   
*===========================================================*                   
         SPACE 1                                                                
         TITLE 'MODULE TO FIND BROAD-MONTH START AND END'                       
GETBROAD CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**GTBR**,CLEAR=YES                                   
         USING WORKD,RC                                                         
         LM    R2,R3,0(R1)                                                      
         XC    0(12,R3),0(R3)      CLEAR OUTPUT                                 
         LR    R4,R1                                                            
         MVC   GETDAY(8),8(R1)                                                  
*                                                                               
         L     R0,=V(SYSPROFS)                                                  
         TM    0(R1),X'02'         TEST SPOTPROF PASSED                         
         BZ    *+8                 NO                                           
         L     R0,16(R1)           P5 IS A(SPOTPROF)                            
         ST    R0,SPOTPROF                                                      
*                                                                               
         LA    RA,RELO                                                          
         S     RA,RELO                                                          
         EJECT                                                                  
*  NOW GET DOWN TO SOME LOGIC                                                   
         MVC   WORK(6),0(R2)                                                    
         CLI   0(R1),0             NEED TO WORK OUT ADDRESSES OF GETDAY         
         BNE   GB2                 AND ADDAY IF USER DIDNT SUPPLY               
         L     R5,=V(GETDAY)                                                    
         AR    R5,RA                                                            
         ST    R5,GETDAY                                                        
         L     R5,=V(ADDAY)                                                     
         AR    R5,RA                                                            
         ST    R5,ADDAY                                                         
         SPACE 2                                                                
GB2      MVC   WORK+4(2),=C'01'    SET TO START OF MONTH                        
         MVC   SAVEMON,WORK                                                     
         GOTO1 GETDAY,PARA,WORK,DAY                                             
         MVI   0(R4),X'FF'                                                      
         CLC   DAY,=C'   '         IS DATE VALID ?                              
         BE    GBEXT                                                            
         LA    R5,1                ASSUME MONDAY                                
         SPACE 2                                                                
GB3      ZIC   R6,PARA             COMPUTE N'DAYS TO BACK UP                    
         SR    R5,R6                                                            
         BZ    GB4                                                              
         BM    *+8                                                              
         SH    R5,=H'7'                                                         
         MVC   WORK+6(6),WORK                                                   
         GOTO1 ADDAY,PARA,WORK+6,WORK,(R5)                                      
         SPACE 2                                                                
GB4      MVI   0(R4),5                                                          
         GOTO1 ADDAY,PARA,WORK,WORK+6,34                                        
         CLC   SAVEMON(4),WORK+6   IS IT A FIVE WEEK MONTH                      
         BE    GB6                                                              
         MVI   0(R4),4             THEN IT BETTER BE FOUR                       
         MVI   11(R1),27                                                        
         BASR  RE,RF               (GO TO ADDAY AGAIN)                          
         SPACE 2                                                                
GB6      CLC   0(6,R2),WORK+6      IT POSSIBLE THE SELECTED DATE                
         BNH   GB8                 STILL DOESNT FALL IN MONTH                   
         GOTO1 (RF),(R1),WORK+6,WORK,15                                         
         B     GB2                                                              
         SPACE 2                                                                
GB8      MVC   0(12,R3),WORK                                                    
*                                                                               
         ICM   RF,15,SPOTPROF      TEST SYSPROFS AVAILABLE                      
         BZ    GBEXT               NO                                           
         CLI   8(RF),2                                                          
         BL    GBEXT                                                            
         ZIC   R5,8(RF)                                                         
         SH    R5,=H'8'            BACK UP FOR SPECIAL DAYS                     
         GOTO1 ADDAY,PARA,WORK,0(R3),(R5)                                       
         GOTO1 ADDAY,PARA,WORK+6,6(R3),(R5)                                     
         SPACE 2                                                                
GBEXT    XIT1                                                                   
* CONSTANT AND WORK SPACE                                                       
*                                                                               
RELO     DC    A(*)                                                             
         LTORG                                                                  
         SPACE 2                                                                
WORKD    DSECT                                                                  
PARA     DS    6F                                                               
GETDAY   DS    V                                                                
ADDAY    DS    V                                                                
SPOTPROF DS    V                                                                
WORK     DS    CL32                                                             
DAY      DS    CL3                                                              
SAVEMON  DS    CL6                                                              
WORKX    EQU   *                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'010DDGETBROAD05/01/02'                                      
         END                                                                    
