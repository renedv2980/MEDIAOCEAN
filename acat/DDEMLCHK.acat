*          DATA SET DDEMLCHK   AT LEVEL 001 AS OF 10/15/13                      
*CATALP EMLCHK                                                                  
*INCLUDE SCANNER                                                                
*                                                                               
*  PARSES A STRING, EXTRACTING ANY VALID EMAIL ADDRESSES INTO A NEW             
*  STRING. IF MULTIPLE ADDRESSES FOUND THEY WILL BE COMMA-SEPARATED             
*  IN THE REPLY.                                                                
*                                                                               
*  AT THE TIME OF WRITING, THIS IS JUST AN ATTEMPT TO MINIMISE                  
*  GARBAGE CAUSING TROUBLE ELSEWHERE, RATHER THAN VALIDATION TO                 
*  EXACT SMTP STANDARDS. FEEL FREE TO EXPAND.                                   
*                                                                               
*  27AUG13 - NOW INCLUDED IN SEACS08 (CTL/SEC, PERSON REC),                     
*            SO RELINK THAT WHEN CHANGED                                        
*                                                                               
* ENTRY:                                                                        
*    P1 B0     LENGTH OF STRING                                                 
*    P1 B1-3   A(STRING TO PARSE)                                               
*                                                                               
* RETURNS:                                                                      
*    P1 B0     LENGTH OF NEW STRING                                             
*    P1 B1-3   A(NEW STRING) (IN LOCAL STORAGE, COPY IMMEDIATELY)               
*                                                                               
*NRAK 001 28FEB13 <OT75296L> CREATE                                             
*                                                                               
*                                                                               
                                                                                
         TITLE 'EXTRACT EMAIL ADDRESSES FROM A STRING'                          
EMLCHK   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKDX-WORKD,**EMAIL*                                            
         USING WORKD,RC                                                         
         LR    RA,R1               RA=A(PARAM LIST)                             
         USING PARMD,RA                                                         
*                                                                               
* check for unescaped quotes/brackets. build substitution table                 
* THIS SIMPLIFIES THE STRUCTURE CHECKING.                                       
* LINE1->LINE2                                                                  
QUOTING  DS    0H                                                               
         LLC   R5,PARLEN                                                        
         LTR   R5,R5                                                            
         JZ    EMLCHKXX            NOTHING TO DO, SEND BACK PARMS               
*                                                                               
         XC    LINE1,LINE1                                                      
         XC    LINE2,LINE2                                                      
         LR    RE,R5                                                            
         SHI   RE,1                                                             
         SR    R2,R2                                                            
         ICM   R2,7,PARDATA                                                     
         MVC   LINE1(0),0(R2)                                                   
         EX    RE,*-6                                                           
         LA    R2,LINE1                                                         
         ST    R2,BEGIN                                                         
* DROP TRAILING SPACES, NULLS                                                   
         LA    RE,LINE1-1                                                       
         AR    RE,R5                                                            
QUOT0005 DS    0H                                                               
         CLI   0(RE),C' '                                                       
         JH    QUOT0007                                                         
         SHI   RE,1                                                             
         JCT   R5,QUOT0005                                                      
         J     EMPTYXX                                                          
*                                                                               
QUOT0007 DS    0H                                                               
         STC   R5,TEMPLEN                                                       
*                                                                               
*        AHI   R5,1                                                             
         LA    R3,LINE2                                                         
*                                                                               
         LA    R4,SUBTAB                                                        
         USING SUBTABD,R4                                                       
         XC    SUBID(SUBTBFIX),SUBID                                            
         MVI   SUBTYPE,SUBTYDUM    'SEED' ENTRY                                 
         MVI   SUBLEN,3                                                         
         MVI   SCANINDS,0                                                       
         MVI   SUBDATA,FF          TERMINATOR                                   
         J     QUOT0020                                                         
*                                                                               
QUOT0010 AHI   R2,1                                                             
         BCT   R5,QUOT0020                                                      
         TM    SCANINDS,SIBROPEN+SIQUOPEN                                       
         JNZ   QUOT0015            DONE - NOTE OPEN "/[ WILL BE DROPPED         
         BAS   RE,COPY23           ELSE COPY LAST CHUNK                         
QUOT0015 DS    0H                                                               
         LA    R2,LINE2                                                         
         SR    R3,R2                                                            
         STC   R3,TEMPLEN                                                       
         J     SEPARATE                                                         
*                                                                               
* NOTE WE DON'T HANDLE ESCAPED " YET                                            
QUOT0020 DS    0H                                                               
         CLI   0(R2),C'"'                                                       
         JNE   QUOT0030                                                         
         TM    SCANINDS,SIBROPEN   IGNORE IF ALREADY PROCESSING []              
         JNZ   QUOT0010                                                         
         TM    SCANINDS,SIQUOPEN                                                
         JNZ   QUOT0023                                                         
         BAS   RE,COPY23           COPY ANYTHING BEFORE                         
         OI    SCANINDS,SIQUOPEN                                                
         ST    R2,BEGIN                                                         
         J     QUOT0010                                                         
* END OF A QUOTED LABEL                                                         
QUOT0023 DS    0H                  BUILD SUBSTITUTION ENTRY                     
         LLC   RE,SUBID            INCREMENT FROM LAST                          
         LLC   RF,SUBLEN                                                        
         AR    R4,RF                                                            
         MVI   SUBTYPE,SUBTYLOC                                                 
         AHI   RE,1                                                             
         STC   RE,SUBID                                                         
         LR    RE,R2                                                            
         L     RF,BEGIN                                                         
         SR    RE,RF                                                            
         MVC   SUBDATA(0),0(RF)                                                 
         EX    RE,*-6                                                           
         AHI   RE,SUBTBFIX+1                                                    
         STC   RE,SUBLEN                                                        
         LR    RF,R4                                                            
         AR    RF,RE                                                            
         MVI   0(RF),FF            TERMINATOR                                   
*                                                                               
         MVC   0(L'SUBTYPE,R3),SUBTYPE       SUBSTITUTE WITH MARKER             
         MVC   1(L'SUBID,R3),SUBID                                              
         LA    R3,2(R3)                                                         
         NI    SCANINDS,FF-SIQUOPEN                                             
         LA    RF,1(R2)                                                         
         ST    RF,BEGIN                                                         
         J     QUOT0010                                                         
*                                                                               
* START [...]                                                                   
QUOT0030 DS    0H                                                               
         CLI   0(R2),C'['                                                       
         JNE   QUOT0035                                                         
         TM    SCANINDS,SIQUOPEN   IGNORE IF ALREADY PROCESSING ""              
         JNZ   QUOT0010                                                         
         TM    SCANINDS,SIBROPEN   IGNORE IF ALREADY [...                       
         JNZ   QUOT0010                 - NOTE WE DON'T HANDLE [[]]!            
         BAS   RE,COPY23                                                        
         OI    SCANINDS,SIBROPEN                                                
         ST    R2,BEGIN                                                         
         J     QUOT0010                                                         
* END OF A BRACKETED SECTION                                                    
QUOT0035 DS    0H                                                               
         CLI   0(R2),C']'                                                       
         JNE   QUOT0010                                                         
         TM    SCANINDS,SIQUOPEN   IGNORE IF ALREADY PROCESSING ""              
         JNZ   QUOT0010                                                         
         TM    SCANINDS,SIBROPEN   DROP IF NO [...                              
         JNZ   QUOT0040                 - NOTE WE DON'T HANDLE [[]]!            
         LA    RE,1(R2)                                                         
         ST    RE,BEGIN                                                         
         J     QUOT0010                                                         
*                                                                               
QUOT0040 DS    0H                  BUILD SUBSTITUTION ENTRY                     
         LLC   RE,SUBID            INCREMENT FROM LAST ENTRY                    
         LLC   RF,SUBLEN                                                        
         AR    R4,RF                                                            
         MVI   SUBTYPE,SUBTYDOM                                                 
         AHI   RE,1                                                             
         STC   RE,SUBID                                                         
         LR    RE,R2                                                            
         L     RF,BEGIN                                                         
         SR    RE,RF                                                            
         MVC   SUBDATA(0),0(RF)                                                 
         EX    RE,*-6                                                           
         AHI   RE,SUBTBFIX+1                                                    
         STC   RE,SUBLEN                                                        
         LR    RF,R4                                                            
         AR    RF,RE                                                            
         MVI   0(RF),FF            TERMINATOR                                   
*                                                                               
         MVC   0(L'SUBTYPE,R3),SUBTYPE      PUT MARKER INSTEAD OF TEXT          
         MVC   1(L'SUBID,R3),SUBID                                              
         LA    R3,L'SUBTYPE+L'SUBID(R3)                                         
         NI    SCANINDS,FF-SIBROPEN                                             
QUOT0050 DS    0H                                                               
         LA    R2,1(R2)            INGORE ANYTHING BEF NEXT SEPARATOR           
         JCT   R5,*+8                                                           
         J     QUOT0015            NOTHING LEFT                                 
         CLI   0(R2),C','                                                       
         JE    QUOT0055                                                         
         CLI   0(R2),C' '                                                       
         JH    QUOT0050                                                         
QUOT0055 DS    0H                                                               
         ST    R2,BEGIN                                                         
         J     QUOT0010                                                         
*                                                                               
* FIND SEPARATORS, NORMALISE                                                    
* LINE2->LINE1                                                                  
SEPARATE DS    0H                                                               
         XC    LINE1,LINE1                                                      
         LA    R2,LINE2                                                         
         ST    R2,BEGIN                                                         
         LA    R3,LINE1                                                         
         LLC   R5,TEMPLEN                                                       
         LTR   R5,R5                                                            
         JZ    EMPTYXX             NOTHING LEFT!                                
*                                                                               
SEPAR010 DS    0H                                                               
         AHI   R2,1                                                             
         BCT   R5,SEPAR020                                                      
         TM    SCANINDS,SIATSIGN                                                
         JZ    SEPAR015                                                         
         BAS   RE,COPY23           COPY LAST SECTION                            
SEPAR015 LA    RE,LINE1                                                         
         SR    R3,RE                                                            
         STC   R3,TEMPLEN                                                       
         J     STRUCT                                                           
*                                                                               
SEPAR020 DS    0H                                                               
         CLI   0(R2),C' '                                                       
         JE    SEPAR030                                                         
         CLI   0(R2),C','                                                       
         JE    SEPAR030                                                         
         CLI   0(R2),C'@'                                                       
         JNE   SEPAR010                                                         
         OI    SCANINDS,SIATSIGN                                                
         J     SEPAR010                                                         
*                                                                               
SEPAR030 DS    0H                                                               
         MVI   0(R2),C','          REPLACE WITH STANDARD                        
         TM    SCANINDS,SIATSIGN   DROP IF NO @, ELSE COPY                      
         JZ    SEPAR035                                                         
         NI    SCANINDS,FF-SIATSIGN                                             
         BAS   RE,COPY23           COPY UP TO SEPARATOR                         
SEPAR035 DS    0H                                                               
         ST    R2,BEGIN            NEXT EMAIL (INCL LEAD SEPARATOR)             
         J     SEPAR010                                                         
*                                                                               
* CHECK STRUCTURE MATCHES <LOCAL PART>@<DOMAIN>                                 
* LOCAL PART IS <LABEL>.<LABEL>...                                              
* DOMAIN IS [DOMAIN] OR <DOMAIN>.<DOMAIN>...                                    
* LINE1->LINE2                                                                  
STRUCT   DS    0H                                                               
         MVI   SCANINDS,0                                                       
         LA    R2,LINE1                                                         
         ST    R2,BEGIN                                                         
         LA    R3,LINE2                                                         
         SR    R5,R5                                                            
         XC    LINE2,LINE2                                                      
         IC    R5,TEMPLEN                                                       
         LTR   R5,R5                                                            
         JZ    EMPTYXX                                                          
         SR    R4,R4                                                            
         J     STRUC015                                                         
*                                                                               
STRUC010 DS    0H                                                               
         AHI   R2,1                                                             
         BCT   R5,STRUC015                                                      
         OI    SCANINDS,SILSTBIT                                                
         J     STRUC020                                                         
*                                                                               
STRUC015 DS    0H                                                               
         CLI   0(R2),C','                                                       
         JE    STRUC020                                                         
         CLI   0(R2),C'@'                                                       
         JNE   STRUC010                                                         
         LR    R4,R2                                                            
         J     STRUC010                                                         
*                                                                               
STRUC020 DS    0H                                                               
         L     RE,BEGIN                                                         
         CR    RE,R2                                                            
         JE    STRUC080            NO LOCAL PART                                
         CR    R4,R2                                                            
         JE    STRUC080            NO DOMAIN                                    
         LTR   R4,R4                                                            
         JNZ   *+6                                                              
         DC    H'0'                SEPARATE SHOULD HAVE REMOVED THIS            
*                                                                               
         GOTOR LABVAL,DMCB,BEGIN,(R4)                                           
         JNE   STRUC080                                                         
         AHI   R4,1                                                             
         GOTOR DOMVAL,DMCB,(R4),(R2)                                            
         JNE   STRUC080                                                         
* KEEP THIS ENTRY.                                                              
         TM    SCANINDS,SILSTBIT                                                
         JNZ   *+8                                                              
         AHI   R2,1                SEPARATOR IF NOT LAST EMAIL                  
         BAS   RE,COPY23                                                        
         TM    SCANINDS,SILSTBIT                                                
         JNZ   STRUC090                                                         
         SHI   R2,1                                                             
*                                                                               
STRUC080 TM    SCANINDS,SILSTBIT                                                
         JNZ   STRUC090                                                         
         LA    RE,1(R2)                                                         
         ST    RE,BEGIN                                                         
         SR    R4,R4                                                            
         J     STRUC010                                                         
*                                                                               
STRUC090 DS    0H                                                               
         LA    RE,LINE2                                                         
         SR    R3,RE                                                            
         STC   R3,TEMPLEN                                                       
*                                                                               
* DESUBSTITUTE, SET RETURN PARAMS                                               
* LINE2->LINE1                                                                  
DESUB    DS    0H                                                               
*                                                                               
         XC    LINE1,LINE1                                                      
         LA    R2,LINE2                                                         
         LA    R3,LINE1                                                         
         ST    R2,BEGIN                                                         
         SR    R5,R5                                                            
         IC    R5,TEMPLEN                                                       
         LTR   R5,R5                                                            
         JZ    EMPTYXX                                                          
*        SHI   R5,1                                                             
*                                                                               
DESUB010 DS    0H                                                               
         CLI   0(R2),0                                                          
         JNE   *+6                                                              
         DC    H'0'                                                             
         CLI   0(R2),SUBTYMAX                                                   
         JNH   DESUB020                                                         
         AHI   R2,1                                                             
         JCT   R5,DESUB010                                                      
         J     DESUB080                                                         
*                                                                               
DESUB020 DS    0H                                                               
         BAS   RE,COPY23           COPY PREVIOUS BIT                            
         LA    R4,SUBTAB                                                        
         USING SUBTABD,R4                                                       
DESUB021 DS    0H                                                               
         LLC   RF,SUBLEN                                                        
         AR    R4,RF                                                            
         CLI   SUBTYPE,FF                                                       
         JNE   *+6                                                              
         DC    H'0'                LOST SUBSTITUTION!                           
         CLC   SUBTYPE,0(R2)                                                    
         JNE   DESUB021                                                         
         CLC   SUBID,1(R2)                                                      
         JNE   DESUB021                                                         
*                                                                               
         LLC   RF,SUBLEN                                                        
         SHI   RF,SUBTBFIX+1                                                    
         MVC   0(0,R3),SUBDATA                                                  
         EX    RF,*-6                                                           
         AHI   RF,1                                                             
         AR    R3,RF                                                            
         SHI   R5,L'SUBID+L'SUBTYPE                                             
         LA    R2,L'SUBID+L'SUBTYPE(R2)                                         
         ST    R2,BEGIN                                                         
         LTR   R5,R5                                                            
         JNZ   DESUB010                                                         
         J     DESUB085                                                         
*                                                                               
DESUB080 DS    0H                                                               
         BAS   RE,COPY23           COPY LAST BIT                                
         LR    RF,R3                                                            
         SHI   RF,1                                                             
         CLI   0(RF),C','          DANGLING SEPARATOR?                          
         JNE   DESUB085                                                         
         MVI   0(RF),C' '          REMOVE                                       
         LR    R3,RF                                                            
*                                                                               
DESUB085 DS    0H                                                               
         LA    RF,LINE1                                                         
         SR    R3,RF                                                            
         STC   R3,TEMPLEN                                                       
*                                                                               
         LA    RE,LINE1                                                         
         STCM  RE,7,PARDATA                                                     
         MVC   PARLEN,TEMPLEN                                                   
         J     EMLCHKXX                                                         
*                                                                               
FF       EQU   X'FF'                                                            
*                                                                               
EMPTYXX  XC    WORK(4),WORK        NO VALID EMAIL ADDRESSES FOUND               
         LA    R1,WORK                                                          
         STCM  R1,7,PARDATA                                                     
         MVI   PARLEN,0                                                         
         CLI   *,FF                                                             
         J     EMLCHKNO                                                         
*                                                                               
EXIT3    XIT1  REGS=(R3)                                                        
EXITY    CR    RB,RB                                                            
         J     EXIT                                                             
EXITL    CLI   *,FF                                                             
EXIT     XIT1                                                                   
*                                                                               
EMLCHKNO CLI   *,FF                                                             
         J     *+6                                                              
EMLCHKXX CR    RB,RB                                                            
         XMOD1 1                                                                
*                                                                               
         EJECT                                                                  
*                                                                               
***********************************************************************         
* VALIDATE A LOCAL PART                                                         
* PARAMS:                                                                       
* P1= START OF LABEL                                                            
* P2= LOCATION OF @                                                             
***********************************************************************         
         SPACE 1                                                                
LABVAL   NTR1                                                                   
         LM    R2,R3,0(R1)                                                      
         CLI   0(R2),C'.'                                                       
         JE    EXITL               CAN'T START WITH FULLSTOP                    
         CLI   0(R2),C' '                                                       
         JE    EXITL               CAN'T START WITH A SPACE                     
         LR    RE,R3                                                            
         SHI   RE,1                                                             
         CLI   0(RE),C'.'                                                       
         JE    EXITL               OR END WITH ONE                              
         SR    RE,R2                                                            
         CHI   RE,63                                                            
         JH    EXITL               NO LONGER THAN 64CHAR                        
*                                                                               
LABVAL10 DS    0H                                                               
         CLI   0(R2),SUBTYMAX      QUOTED LABELS                                
         JH    LABVAL20                                                         
* LIMITED SUPPORT, ATM.                                                         
* SHOULD ALSO CHECK FOR UNESCAPED [\Âª                                           
* ESCAPING " ALSO POSSIBLE, BUT NOT SUPPORTED IN SUBSTITUTION                   
         CLI   0(R2),SUBTYLOC                                                   
         JNE   EXITL               NOT A QUOTED SUBSTITUTION                    
         AHI   R2,L'SUBID          NEED TO SKIP SUBID                           
         CLI   1(R2),C'.'          QUOTED LABEL MUST BE FOLLOWED BY             
         JE    LABVAL20                            FULLSTOP OR @                
         CLI   1(R2),C'@'                                                       
         JNE   EXITL                                                            
         J     LABVAL70                                                         
*                                                                               
LABVAL20 DS    0H                                                               
         CLI   1(R2),C'.'                                                       
         JNE   LABVAL25                                                         
         CLI   0(R2),C'.'                                                       
         JE    EXITL               CAN'T HAVE ..                                
*                                                                               
LABVAL25 DS    0H                                                               
*                                                                               
LABVAL70 DS    0H                                                               
         AHI   R2,1                                                             
         CR    R2,R3                                                            
         JE    EXITY                                                            
         J     LABVAL10                                                         
         SPACE 1                                                                
         EJECT                                                                  
*                                                                               
***********************************************************************         
* VALIDATE A DOMAIN                                                             
* PARAMS:                                                                       
* P1= START OF DOMAIN                                                           
* P2= LOCATION OF NEXT SEPARATOR                                                
***********************************************************************         
         SPACE 1                                                                
DOMVAL   NTR1                                                                   
         LM    R2,R3,0(R1)                                                      
         CLI   0(R2),SUBTYMAX                                                   
         JH    DOMV015                                                          
         CLI   0(R2),SUBTYDOM                                                   
         JNE   EXITL               NOT A BRACKETED DOMAIN                       
         LA    R4,SUBTAB                                                        
         SR    RE,RE                                                            
DOMV010  CLI   0(R4),FF                                                         
         JNE   *+6                                                              
         DC    H'0'                SUB NOT FOUND                                
         CLC   0(1,R2),SUBTYPE                                                  
         JNE   DOMV012                                                          
         CLC   1(1,R2),SUBID                                                    
         JE    DOMV013                                                          
DOMV012  DS    0H                                                               
         IC    RE,SUBLEN                                                        
         AR    R4,RE                                                            
         J     DOMV010                                                          
* POINT TO DOMAIN STRING IN SUBTAB                                              
DOMV013  DS    0H                                                               
         LA    R2,SUBDATA                                                       
         LR    R3,R2                                                            
         IC    RE,SUBLEN                                                        
         SHI   RE,SUBTBFIX+1                                                    
         AR    R3,RE               STOP BEFORE ]                                
         AHI   R2,1                SKIP [                                       
*                                                                               
DOMV015  DS    0H                                                               
         LR    R4,R2                                                            
*                                                                               
         CLI   0(R2),C'-'                                                       
         JE    EXITL               CAN'T START WITH HYPHEN                      
         CLI   0(R2),C'.'                                                       
         JE    EXITL               OR DOT                                       
         CLI   0(R2),C' '                                                       
         JE    EXITL               OR SPACE                                     
         LR    RE,R3                                                            
         SHI   RE,1                                                             
         CLI   0(RE),C'-'                                                       
         JE    EXITL               CAN'T END WITH HYPHEN                        
         CLI   0(RE),C'.'                                                       
         JE    EXITL               OR DOT                                       
* LOOP THROUGH DOMAIN FROM HERE                                                 
DOMV020  DS    0H                                                               
         CLI   1(R2),C'.'                                                       
         JNE   DOMV025                                                          
         LA    R4,2(R2)            SAVE START OF LAST LABEL                     
         CLI   0(R2),C'.'                                                       
         JE    EXITL               CAN'T HAVE ..                                
         CLI   0(R2),C'-'          OR -.                                        
         JE    EXITL                                                            
*                                                                               
DOMV025  DS    0H                                                               
         CLI   1(R2),C'-'                                                       
         JNE   DOMV060                                                          
         CLI   0(R2),C'.'          CAN'T HAVE .-                                
         JE    EXITL                                                            
         CLI   0(R2),C'-'                                                       
         JE    EXITL               OR --                                        
*                                                                               
DOMV060  DS    0H                                                               
         AHI   R2,1                                                             
         LR    RE,R2                                                            
         AHI   RE,1                                                             
         CR    RE,R3                                                            
         JL    DOMV020                                                          
* CHECK LAST LABEL IS ALPHABETIC                                                
         LR    RE,R2                                                            
         SR    RE,R4                                                            
         MVC   WORK(0),0(R4)                                                    
         EX    RE,*-6                                                           
         OC    WORK(0),SPACES                                                   
         EX    RE,*-6                                                           
         LA    R4,WORK                                                          
         AHI   RE,1                                                             
*                                                                               
DOMV070  DS    0H                                                               
         CLI   0(R4),C'A'                                                       
         JL    EXITL                                                            
         CLI   0(R4),C'Z'                                                       
         JH    EXITL                                                            
         AHI   R4,1                                                             
         BCT   RE,DOMV070                                                       
*                                                                               
         J     EXITY                                                            
         SPACE 1                                                                
         EJECT                                                                  
***********************************************************************         
*                                                                               
***********************************************************************         
* COPY STRING FROM BEGIN TO 0(R2) (EXCLUSIVE), TO 0(R3)               *         
* on return r3 points to 1st byte after copied string.                          
***********************************************************************         
COPY23   NTR1                                                                   
         L     RE,BEGIN                                                         
         CR    RE,R2                                                            
         JE    EXIT3               NOTHING TO DO                                
         LR    RF,R2                                                            
         SR    RF,RE                                                            
         SHI   RF,1                                                             
         L     RE,BEGIN                                                         
         MVC   0(0,R3),0(RE)                                                    
         EX    RF,*-6                                                           
         AR    R3,RF                                                            
         AHI   R3,1                                                             
         J     EXIT3                                                            
         SPACE 1                                                                
         EJECT                                                                  
***********************************************************************         
* CONVERT TABLE                                                       *         
***********************************************************************         
         SPACE 1                                                                
         LTORG                                                                  
VSCANNER DC    V(SCANNER)                                                       
SPACES   DC    255C' '                                                          
*                                                                               
WORKD    DSECT                                                                  
BEGIN    DS    A                                                                
DMCB     DS    4F                                                               
*                                                                               
SCANINDS DS    XL1                                                              
SIQUOPEN EQU   X'80'               ACTIVE OPEN QUOTE                            
SIBROPEN EQU   X'40'               ACTIVE OPEN SQ BRACKET                       
SIATSIGN EQU   X'20'               '@' FOUND SINCE LAST SEPARATOR               
SILSTBIT EQU   X'10'               PROCESS LAST CHUNK                           
TEMPLEN  DS    XL1                 CURRENT LENGTH OF PARSED STRING.             
*                                                                               
LINE1    DS    CL255                                                            
LINE2    DS    CL255                                                            
WORK     DS    CL255                                                            
*                                                                               
SUBTAB   DS    650X                BIG ENOUGH FOR (MAXLEN/2)+1 ENTRIES          
*                                                                               
WORKDX   DS    0X                                                               
         SPACE 1                                                                
SUBTABD  DSECT                                                                  
SUBID    DS    XL1                 SEQUENCE NUMBER                              
SUBTYPE  DS    XL1                                                              
SUBTYDUM EQU   X'00'               DUMMY (FIRST ENTRY)                          
SUBTYLOC EQU   X'01'               LOCAL PART SUBSTITUTION ("")                 
SUBTYDOM EQU   X'02'               DOMAIN SUBSTITUTION ([C.C])                  
SUBTYMAX EQU   X'10'               MAX VALUE FOR SUBSTITUTION TYPE              
SUBLEN   DS    XL1                                                              
*                                                                               
SUBTBFIX EQU   *-SUBTABD           LENGTH OF FIXED PART                         
SUBDATA  DS    0X                                                               
         SPACE 1                                                                
PARMD    DSECT                                                                  
PARMS    DS    0X                                                               
PARLEN   DS    XL1                                                              
PARDATA  DS    AL3                                                              
         DS    XL2                                                              
         DS    XL2                                                              
PARMSLEN EQU   *-PARMS                                                          
* DDCTRYEQUS                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDCTRYEQUS                                                     
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'001DDEMLCHK  10/15/13'                                      
         END                                                                    
