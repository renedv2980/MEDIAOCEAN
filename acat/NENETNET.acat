*          DATA SET NENETNET   AT LEVEL 024 AS OF 08/08/07                      
*CATALP NETNET                                                                  
NETNET   TITLE 'NETNET - MODULE TO COMPUTE GROSS AND NET'                       
NETNET   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKL,**NNET**                                                   
         USING WORKD,RC            RC=A(LOCAL WORKING STORAGE)                  
         LR    R3,R1               R3=A(PARAMETER LIST)                         
                                                                                
         L     R2,0(R3)            R2=A(INPUT VALUE)                            
         CLI   4(R3),C'P'          TEST PACKED VALUES                           
         BNE   *+14                                                             
         ZAP   RATE,0(8,R2)        SET PACKED RATE                              
         B     *+12                                                             
         ICM   R0,15,0(R2)         GET BINARY RATE                              
         CVD   R0,RATE             SET PACKED RATE                              
                                                                                
         ZAP   GROSS,RATE          SET DEFAULT GROSS RATE                       
         ZAP   NET,RATE            SET DEFAULT NET RATE                         
         ZAP   WORK,RATE           SET RATE IN PACKED WORK AREA                 
                                                                                
         CLI   0(R3),C'F'          TEST FOR SPECIAL RATES                       
         BE    FRATE                                                            
         CLI   0(R3),C'Q'                                                       
         BE    QRATE                                                            
         CLI   0(R3),C'N'                                                       
         BE    NRATE                                                            
         CLI   0(R3),C'V'                                                       
         BE    VRATE                                                            
         CLI   0(R3),C'S'                                                       
         BE    SRATE                                                            
         CLI   0(R3),C'X'                                                       
         BE    XRATE                                                            
         CLI   0(R3),C'W'                                                       
         BE    WRATE                                                            
         CLI   0(R3),C'Y'                                                       
         BE    YRATE                                                            
         CLI   0(R3),C'P'                                                       
         BE    PRATE                                                            
         CLI   0(R3),C'C'                                                       
         BE    CRATE                                                            
         CLI   0(R3),C'H'                                                       
         BE    HRATE                                                            
         CLI   0(R3),C'T'                                                       
         BE    TRATE                                                            
         CLI   0(R3),C'R'                                                       
         BE    RRATE                                                            
         CLI   0(R3),C'J'                                                       
         BE    JRATE                                                            
                                                                                
         MP    WORK,=P'8500'       DEFAULT NET = 85% GROSS                      
         GOTOR SETNET                                                           
         B     NETNETX                                                          
                                                                                
FRATE    B     NETNETX             FRATE GROSS=NET                              
                                                                                
QRATE    MP    WORK,=P'8925'       QRATE GROSS = 105% OF NET                    
         GOTOR SETGROSS                          OR 85 + 4.25%                  
         ZAP   WORK,RATE                                                        
         MP    WORK,=P'8500'       NET = 85%                                    
         GOTOR SETNET                                                           
         B     NETNETX                                                          
                                                                                
NRATE    MP    WORK,=P'10000'      NRATE GROSS = 85%                            
         AP    WORK,=P'500'                                                     
         DP    WORK,=P'8500'                                                    
         ZAP   GROSS,WORK(13)                                                   
         B     NETNETX                                                          
                                                                                
WRATE    MP    WORK,=P'10500'      WRATE GROSS = 105%                           
         GOTOR SETGROSS                                                         
         ZAP   WORK,RATE                                                        
         MP    WORK,=P'8500'       NET = 85%                                    
         GOTOR SETNET                                                           
         B     NETNETX                                                          
                                                                                
YRATE    MP    WORK,=P'10588'      YRATE GROSS = 105.88%                        
         GOTOR SETGROSS                                                         
         ZAP   WORK,RATE                                                        
         MP    WORK,=P'8500'       NET = 85%                                    
         GOTOR SETNET                                                           
         B     NETNETX                                                          
                                                                                
VRATE    MP    WORK,=P'11500'      VRATE GROSS = 115%                           
         GOTOR SETGROSS                                                         
         B     NETNETX                                                          
                                                                                
SRATE    MP    WORK,=P'8500'       RATE GROSS=NET=85% OF RATE                   
         GOTOR SETNET                                                           
         ZAP   GROSS,NET                                                        
         B     NETNETX                                                          
                                                                                
XRATE    MP    WORK,=P'8500'       XRATE NET = 85/90                            
         AP    WORK,=P'500'                                                     
         DP    WORK,=P'9000'                                                    
         ZAP   NET,WORK(13)                                                     
         B     NETNETX                                                          
                                                                                
PRATE    ZAP   GROSS,=P'0'         GROSS=ZERO                                   
         ZAP   NET,=P'0'           NET=ZERO                                     
         B     NETNETX                                                          
                                                                                
CRATE    ZAP   NET,=P'0'           CRATE NET = ZERO                             
         B     NETNETX                                                          
                                                                                
HRATE    MP    WORK,=P'5000'       HRATE GROSS = 50%                            
         GOTOR SETGROSS                                                         
         ZAP   WORK,RATE                                                        
         MP    WORK,=P'4250'       NET = 42.50%                                 
         GOTOR SETNET                                                           
         B     NETNETX                                                          
                                                                                
TRATE    MP    WORK,=P'7500'       IRATE GROSS = 75%                            
         GOTOR SETGROSS                                                         
         ZAP   WORK,RATE                                                        
         MP    WORK,=P'6375'       NET = 63.75%                                 
         GOTOR SETNET                                                           
         B     NETNETX                                                          
                                                                                
RRATE    MP    WORK,=P'6500'       RRATE GROSS = 65%                            
         GOTOR SETGROSS                                                         
         ZAP   WORK,RATE                                                        
         MP    WORK,=P'5525'       NET = 55.25%                                 
         GOTOR SETNET                                                           
         B     NETNETX                                                          
                                                                                
JRATE    MP    WORK,=P'7000'       JRATE GROSS = 70%                            
         GOTOR SETGROSS                                                         
         ZAP   WORK,RATE                                                        
         MP    WORK,=P'5950'       NET = 59.50%                                 
         GOTOR SETNET                                                           
                                                                                
NETNETX  L     R2,4(R3)            POINT TO OUTPUT AREA                         
         CLI   4(R3),C'P'          TEST PACKED RATES                            
         JNE   NETNETX2                                                         
         ZAP   0(8,R2),GROSS       RETURN PACKED AMOUNTS                        
         ZAP   8(8,R2),NET                                                      
         B     EXIT                                                             
                                                                                
NETNETX2 CVB   R0,GROSS            RETURN BINARY AMOUNTS                        
         STCM  R0,15,0(R2)                                                      
         CVB   R0,NET                                                           
         STCM  R0,15,4(R2)                                                      
                                                                                
EXIT     XIT1  ,                                                                
         EJECT                                                                  
SETNET   SRP   WORK,64-4,5         SET ROUNDED NET VALUE                        
         ZAP   NET,WORK                                                         
         BR    RE                                                               
                                                                                
SETGROSS SRP   WORK,64-4,5         SET ROUNDED GROSS VALUE                      
         ZAP   GROSS,WORK                                                       
         BR    RE                                                               
                                                                                
         LTORG                                                                  
                                                                                
WORKD    DSECT                     ** LOCAL WORKING STORAGE **                  
RATE     DS    PL8                 INPUT RATE                                   
WORK     DS    PL16                PACKED WORK AREA                             
GROSS    DS    PL8                 GROSS AMOUNT                                 
NET      DS    PL8                 NET AMOUNT                                   
WORKL    EQU   *-WORKD                                                          
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'024NENETNET  08/08/07'                                      
         END                                                                    
