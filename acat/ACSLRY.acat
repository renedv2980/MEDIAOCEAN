*          DATA SET ACSLRY     AT LEVEL 035 AS OF 09/18/97                      
*CATALP ACSLRY                                                                  
         TITLE 'ACSLRY - ROUTINE TO BUILD SALARY INFO '                         
***********************************************************************         
*                                                                     *         
*           PARM 1    BYTE 0     X'80' COMFACS IS PASSED              *         
*                     BYTE 1-3         A(RECORD)                      *         
*                                                                     *         
*           PARM 2    BYTE 1-3         A(DATE FIELD)                  *         
*                                      START END - 2BYTES EACH YYMM(P)*         
*           PARM 3    BYTE 1-3         A(OUTPUT AREA - SLRD)          *         
*                                                                     *         
*           PARM 4    BYTE 1-3         A(COMFACS)                     *         
*                                                                     *         
***********************************************************************         
*                                                                               
ACSLRY   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 ACSLRYX-ACSLRYD,*ACSLRY*,CLEAR=YES                               
         USING ACSLRYD,RC                                                       
         MVC   PARMS,0(R1)                                                      
         L     R7,AOUT                                                          
         USING SLRD,R7                                                          
         XC    SLRTOT(SLRLEN),SLRTOT                                            
         ZAP   SLRTOT,=P'0'                                                     
         ZAP   SLRSAL,=P'0'                                                     
         ZAP   SLROVT,=P'0'                                                     
         ZAP   SLRTMP,=P'0'                                                     
         ZAP   SLRBON,=P'0'                                                     
         ZAP   SLRPEN,=P'0'                                                     
         ZAP   SLRBEN,=P'0'                                                     
         ZAP   SLRADM,=P'0'                                                     
         ZAP   SLRBUD,=P'0'                                                     
         ZAP   SLRRTE,=P'0'                                                     
*                                                                               
         L     R2,ADATES                                                        
         MVC   START(4),0(R2)      START/END DATES                              
         CLC   START,END                                                        
         BNH   *+6                                                              
         DC    H'0'                START HIGHER THAN END                        
         MVC   THISMNTH,START                                                   
         XC    STATLST,STATLST                                                  
         BAS   RE,CONVYTD          FIX YTD ELEMENTS                             
*                                                                               
ACSL20   LA    R4,RECORD                                                        
         MVI   ELCODE,X'52'                                                     
         BAS   RE,GETEL                                                         
         BNE   XIT                 NO 52 GET OUT                                
*                                                                               
         USING MSAELD,R4                                                        
ACSL22   CLC   MSABEG,THISMNTH                                                  
         BH    ACSLNXT             STARTS AFTER REQUEST MONTH                   
         OC    MSAEND,MSAEND                                                    
         BZ    ACSL25              NO END DATE                                  
         CLC   MSAEND,THISMNTH                                                  
         BL    ACSLNXT             ENDS BEFORE REQUEST MONTH                    
*                                                                               
ACSL25   LA    R3,TYPELST                                                       
ACSL26   CLI   0(R3),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'0'                UNKNOWN TYPE                                 
         CLC   MSATYPE,0(R3)                                                    
         BE    ACSL30                                                           
         LA    R3,3(R3)                                                         
         B     ACSL26                                                           
*                                                                               
ACSL30   ST    R3,ADTYPE           A(TYPELIST ENTRY)                            
         LA    R3,BASELST                                                       
ACSL32   CLI   0(R3),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'0'                UNKNOWN BASES                                
         CLC   MSABASIS,0(R3)                                                   
         BE    ACSL33                                                           
         LA    R3,3(R3)                                                         
         B     ACSL32                                                           
*                                                                               
ACSL33   ST    R3,ADBASE           A(BASELST ENTRY)                             
         L     R1,ADTYPE                                                        
         SR    R2,R2                                                            
         IC    R2,1(R1)            DISP. TO BUCKET IN SLRD                      
         LA    R5,SLRD(R2)         R5 TO BUCKET IN SLRD                         
         TM    6(R5),PERCT         IF I HAVE ALREADY ADDED A PERCENT            
         BO    ACSLNXT             TO THIS BUCKET DON'T ADD ANYMORE             
         TM    6(R5),PERCT5        IF I HAVE ALREADY ADDED A PERCENT            
         BO    ACSLNXT             TO THIS BUCKET DON'T ADD ANYMORE             
         SR    R2,R2                                                            
         IC    R2,2(R1)                                                         
         LA    R2,STATLST(R2)      STATUS BYTE FOR THIS TYPE                    
         CLI   0(R2),0                                                          
         BE    ACSL37              NOTHING IN THIS BUCKET                       
         CLI   MSABASIS,C'M'                                                    
         BE    ACSL37                                                           
         CLI   MSABASIS,C'Y'                                                    
         BE    ACSL37                                                           
         CLC   0(1,R2),1(R3)                                                    
         BNE   ACSLNXT             BASE NOT EQUAL TO THE LAST BASES             
*                                                                               
ACSL37   CP    MSALARY,=P'0'                                                    
         BE    ACSLNXT             SKIP ZERO DATA                               
         MVC   0(1,R2),1(R3)       BASES TO STATLST                             
         CLI   MSABASIS,C'H'       HOURLY                                       
         BE    ACSL40                                                           
         TM    MSASTAT,PERCT                                                    
         BO    ACSL38              2DP                                          
         TM    MSASTAT,PERCT5                                                   
         BO    ACSL39              5DP                                          
         B     ACSL42              NOT A PERCENT                                
*                                                                               
ACSL38   ZAP   0(6,R5),MSALARY    PERCENT TO SLRD                               
         OI    6(R5),PERCT         MARK IT AS A PERCENT                         
         B     ACSLNXT                                                          
*                                                                               
ACSL39   ZAP   0(6,R5),MSALARY    PERCENT TO SLRD                               
         OI    6(R5),PERCT5        MARK IT AS A PERCENT                         
         B     ACSLNXT                                                          
*                                                                               
ACSL40   ZAP   0(6,R5),MSALARY    RATE TO SLRD                                  
         B     ACSLNXT                                                          
*                                                                               
ACSL42   ZAP   AMOUNT,MSALARY                                                   
         XC    FULL,FULL                                                        
         MVC   FULL+3(1),2(R3)     DIVISOR 3 MONTHS ,12 MONTHS                  
         OC    FULL,FULL                                                        
         BNZ   ACSL45                                                           
         LA    R2,MSABEG                                                        
         BAS   RE,CNTMNTHS         FOR YTD COUNT MONTHS IN PERIOD               
         ST    RF,FULL                                                          
*                                                                               
ACSL45   CLI   FULL+3,1                                                         
         BE    *+8                 DO NOT DIVIDE BY ONE                         
         BAS   RE,DIV              MONTHLY RATE                                 
         AP    0(6,R5),AMOUNT      ADD TO SLRD                                  
         L     R7,AOUT             RE-SET R7 TO SLRD                            
         CLI   MSATYPE,X'08'                                                    
         BE    ACSLNXT                                                          
         CLI   MSATYPE,X'07'                                                    
         BE    ACSLNXT                                                          
         AP    SLRTOT,AMOUNT       ADD TO TOTAL                                 
*                                                                               
ACSLNXT  BAS   RE,NEXTEL           NEXT 52                                      
         BE    ACSL22                                                           
         MVC   OLD,THISMNTH                                                     
         BAS   RE,ADDMNTH          ADD 1 TO THISMNTH                            
         MVC   THISMNTH,NEW                                                     
         CLC   THISMNTH,END                                                     
         BNH   ACSL20              START AGAIN WITH NEW MONTH                   
*                                                                               
XIT      XMOD1                                                                  
         DROP  R4,R7                                                            
         EJECT                                                                  
***********************************************************************         
*              FIX YTD ELEMENTS                                       *         
***********************************************************************         
*                                                                               
CONVYTD  ST    RE,SVRE                                                          
         LA    R2,RECORD                                                        
         LA    R3,L'RECORD                                                      
         L     R4,ARECORD          A(INPUT RECORD)                              
         SR    R5,R5                                                            
         ICM   R5,3,ACCORLEN(R4)                                                
         MVCL  R2,R4               SAVE INPUT RECORD                            
*                                                                               
         MVI   ELCODE,X'52'                                                     
         LA    R4,RECORD                                                        
         BAS   RE,GETEL                                                         
         BNE   CONVXIT                                                          
*                                                                               
         USING MSAELD,R4                                                        
CNV03    CLI   MSABASIS,C'Y'       IS IT YTD                                    
         BNE   CONV11                                                           
         SR    R1,R1                                                            
         IC    R1,1(R4)                                                         
         LA    R6,0(R1,R4)         GET TO NEXT ELEMENT                          
*                                                                               
CNV07    CLI   0(R6),X'52'                                                      
         BNE   CONV11                                                           
         CLI   MSABASIS-MSAEL(R6),C'Y'                                          
         BE    CNV09                                                            
CNV08    SR    R1,R1                                                            
         IC    R1,1(R6)                                                         
         AR    R6,R1                                                            
         B     CNV07                                                            
*                                                                               
CNV09    CLC   MSATYPE,MSATYPE-MSAEL(R6)                                        
         BNE   CNV08                                                            
         CLC   MSABEG,MSABEG-MSAEL(R6)                                          
         BNE   CONV11                                                           
         SP    MSALARY,MSALARY-MSAEL(L'MSALARY,R6)                              
         MVC   OLD,MSAEND-MSAEL(R6)     END OF SECOND ELEMENT                   
         BAS   RE,ADDMNTH                   PLUS ONE MONTH                      
         MVC   MSABEG,NEW             GIVES CORRECT START FOR THIS              
*                                                                               
CONV11   BAS   RE,NEXTEL                                                        
         BE    CNV03                                                            
*                                                                               
CONVXIT  L     RE,SVRE                                                          
         BR    RE                                                               
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
*              COUNT MONTHS IN PERIOD                                 *         
***********************************************************************         
*                                                                               
CNTMNTHS LR    R0,RE                                                            
         TM    PARMS,PFACS        TEST COMFACS PASSED                           
         BO    CNT03                                                            
         LA    RF,1                                                             
         ZAP   WORK(2),=P'0'                                                    
         ZAP   WORK+2(2),=P'0'                                                  
         MVC   WORK(1),0(R2)       YY0P                                         
         MVC   WORK+2(1),1(R2)     MM0P                                         
CNT01    MVC   WORK+4(1),WORK                                                   
         MVC   WORK+5(1),WORK+2    YYMM                                         
         CLC   WORK+4(2),2(R2)                                                  
         BE    CNTX                                                             
         AH    RF,=H'1'                                                         
         AP    WORK+2(2),=P'10'                                                 
         CP    WORK+2(2),=P'130'                                                
         BL    CNT01                                                            
         ZAP   WORK+2(2),=P'10'                                                 
         AP    WORK(2),=P'10'                                                   
         B     CNT01                                                            
*                                                                               
*              NEED COMFACS FOR Y2000                                           
CNT03    L     RA,ACOMFACS                                                      
         USING COMFACSD,RA                                                      
         MVI   WORK,C' '                                                        
         MVC   WORK+1(L'WORK-1),WORK                                            
         MVC   DUB(2),0(R2)                                                     
         MVI   DUB+2,1                                                          
         GOTO1 CDATCON,DMCB,(1,DUB),(9,WORK)    MMM/YY                          
         MVI   WORK+6,C'-'                                                      
         MVC   DUB(2),2(R2)                                                     
         GOTO1 CDATCON,DMCB,(1,DUB),(9,WORK+7)  -MMM/YY                         
         LA    R8,PERVWK                                                        
         USING PERVALD,R8                                                       
         LA    RF,13+X'80'                                                      
         GOTO1 CPERVAL,DMCB,((RF),WORK),(X'00',PVALOUTB)                        
         CLI   4(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         SR    RF,RF                                                            
         ICM   RF,3,PVALNMNS       NUMBER OF MONTHS                             
*                                                                               
CNTX     LR    RE,R0                                                            
         BR    RE                                                               
         DROP  R8,RA                                                            
         EJECT                                                                  
***********************************************************************         
*              ADD A MONTH TO THISMNTH                                *         
***********************************************************************         
*                                                                               
ADDMNTH  LR    R0,RE                                                            
         TM    PARMS,PFACS         TEST COMFACS IS PASSED                       
         BO    ADDM03                                                           
         ZAP   WORK(2),=P'0'                                                    
         ZAP   WORK+2(2),=P'0'                                                  
         MVC   WORK(1),OLD                                                      
         MVC   WORK+2(1),OLD+1                                                  
         AP    WORK+2(2),=P'10'                                                 
         CP    WORK+2(2),=P'130'                                                
         BL    *+16                                                             
         ZAP   WORK+2(2),=P'10'                                                 
         AP    WORK(2),=P'10'                                                   
         MVC   NEW(1),WORK                                                      
         MVC   NEW+1(1),WORK+2                                                  
         B     ADDMX                                                            
*                                                                               
*              NEED COMFACS FOR Y2000                                           
ADDM03   L     RA,ACOMFACS                                                      
         USING COMFACSD,RA                                                      
         MVC   WORK(2),OLD                                                      
         MVI   WORK+2,1                                                         
         GOTO1 CDATCON,DMCB,(1,WORK),(0,WORK+6)                                 
         GOTO1 CADDAY,DMCB,(C'M',WORK+6),WORK+12,F'1'                           
         GOTO1 CDATCON,DMCB,(0,WORK+12),(1,WORK+18)                             
         MVC   NEW,WORK+18                                                      
ADDMX    LR    RE,R0                                                            
         BR    RE                                                               
         DROP  RA                                                               
         EJECT                                                                  
***********************************************************************         
*              GET A MONTHLY RATE                                     *         
***********************************************************************         
*                                                                               
DIV      ZAP   WORK(16),AMOUNT         SALARY                                   
         L     R0,FULL                 NO OF MONTHS                             
         CVD   R0,AMOUNT                                                        
         MP    WORK(16),=P'10'                                                  
         DP    WORK(16),AMOUNT         SALARY / NO. OF MNTHS                    
         SRP   WORK(8),64-1,5                                                   
         ZAP   AMOUNT,WORK(8)                                                   
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
*              TABLES                                                 *         
***********************************************************************         
*                                                                               
         GETEL R4,49,ELCODE                                                     
*                                                                               
TYPELST  DC    X'50',AL1(SLRSAL-SLRD),AL1(0)                                    
         DC    X'40',AL1(SLROVT-SLRD),AL1(1)                                    
         DC    X'30',AL1(SLRTMP-SLRD),AL1(2)                                    
         DC    X'20',AL1(SLRBON-SLRD),AL1(3)                                    
         DC    X'15',AL1(SLRPEN-SLRD),AL1(3)                                    
         DC    X'10',AL1(SLRBEN-SLRD),AL1(4)                                    
         DC    X'09',AL1(SLRADM-SLRD),AL1(5)                                    
         DC    X'08',AL1(SLRBUD-SLRD),AL1(6)                                    
         DC    X'07',AL1(SLRRTE-SLRD),AL1(7)                                    
         DC    X'FF'                                                            
*                                                                               
BASELST  DC    C'M',AL1(MONTH),AL1(1)                                           
         DC    C'Q',AL1(QUART),AL1(3)                                           
         DC    C'Y',AL1(YTDTE),AL1(0)                                           
         DC    C'A',AL1(ANNUL),AL1(12)                                          
         DC    C'H',AL1(HOURL),AL1(0)                                           
         DC    X'FF'                                                            
*                                                                               
MONTH    EQU   X'80'                                                            
QUART    EQU   X'40'                                                            
YTDTE    EQU   X'20'                                                            
ANNUL    EQU   X'10'                                                            
HOURL    EQU   X'08'                                                            
PERCT    EQU   X'20'            2DP                                             
PERCT5   EQU   X'10'            5DP                                             
         EJECT                                                                  
***********************************************************************         
*              LITERALS                                               *         
***********************************************************************         
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*              ACSLRY DSECT                                           *         
***********************************************************************         
*                                                                               
ACSLRYD  DSECT                                                                  
         DS    0F                                                               
PARMS    DS    0XL16                                                            
PFACS    EQU   X'80'               COMFACS IS PASSED                            
ARECORD  DS    A                   A(RECORD)                                    
ADATES   DS    A                   A(DATE FIELD6)                               
AOUT     DS    A                   A(OUTPUT AREA)                               
ACOMFACS DS    A                   A(COMFACS)                                   
*                                                                               
DMCB     DS    6F                                                               
WORK     DS    CL64                                                             
ELCODE   DS    CL1                                                              
START    DS    CL2                                                              
END      DS    CL2                                                              
THISMNTH DS    CL2                                                              
OLD      DS    CL2                                                              
NEW      DS    CL2                                                              
STATLST  DS    CL10                                                             
ADTYPE   DS    A                                                                
ADBASE   DS    A                                                                
AMOUNT   DS    D                                                                
DUB      DS    D                                                                
FULL     DS    F                                                                
BYTE     DS    CL1                                                              
SVRE     DS    F                                                                
PERVWK   DS    XL(L'PVALOUTB+L'PVALOUTC)                                        
RECORD   DS    CL2008                                                           
ACSLRYX  EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
*              ++INCLUDES                                             *         
***********************************************************************         
*                                                                               
       ++INCLUDE DDSLRD                                                         
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
* DDCOMFACSD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACSD                                                     
         PRINT ON                                                               
* DDPERVALD                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDPERVALD                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'035ACSLRY    09/18/97'                                      
         END                                                                    
