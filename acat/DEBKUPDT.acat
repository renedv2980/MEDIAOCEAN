*          DATA SET DEBKUPDT   AT LEVEL 002 AS OF 12/04/08                      
*CATALP DEBKUPDT                                                                
         TITLE 'ROUTINE TO CREATE DEMO UPDATE CONTROL RECORDS'                  
***********************************************************************         
* ROUTINE TO CREATE DEMO UPDATE CONTROL RECORDS                       *         
*                                                                     *         
* INPUT  BYTE 0-3  A(PARAMETER LIST). SEE DBUPARMSD FOR DSECT.        *         
*        BYTE 4-7  MODE: INIT,ADD,RELEASE                             *         
*                                                                     *         
* THIS ROUTINE IS CALLED FROM CONVERSIONS FOR ALL THE ACTIVE DEMO     *         
* RECORDS BEING CREATED. DEBKUPDT KEEPS A LIST OF BOOKS AND           *         
* IN THE FINAL STEP RELEASES ONE CONTROL RECORD FOR EACH BOOK.        *         
*                                                                     *         
* MODE=INIT        INITIALIZE ROUTINE                                 *         
* MODE=ADD         ADD NEW BOOK TO THE LIST IF NOT ALREADY THERE      *         
* MODE=RELEASE     RELEASE CONTROL RECORDS                            *         
*                                                                     *         
***********************************************************************         
         EJECT                                                                  
DEBKUPDT CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKL,DEBKUPDT                                                   
         USING WORKD,RC                                                         
         L     R9,0(R1)            PARAMETER LIST                               
         USING DBUPARMD,R9                                                      
         MVC   MODE,7(R1)                                                       
         ICM   R8,15,DBUACOMS      A(COMFACS)                                   
         USING COMFACSD,R8                                                      
*                                                                               
         CLI   MODE,INITQ          INITIALIZE                                   
         BNE   *+12                                                             
         BAS   RE,INIT                                                          
         B     EXIT                                                             
*                                                                               
         CLI   MODE,ADDQ           ADD NEW ENTRY                                
         BNE   *+12                                                             
         BAS   RE,ADD                                                           
         B     EXIT                                                             
*                                                                               
         CLI   MODE,RELQ           RELEASE CONTROL RECORDS                      
         BNE   *+12                                                             
         BAS   RE,RELEASE                                                       
         B     EXIT                                                             
*                                                                               
         DC    H'0'                UNKNOWN MODE                                 
*                                                                               
EXIT     XMOD1                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------*         
* MODE=INIT   :INITIALIZE                                                       
*---------------------------------------------------------------------*         
INIT     NTR1                                                                   
         XCEF  SVBKTAB,'SVBKTABL'  CLEAR BOOK SAVE AREA                         
         B     XIT                                                              
*                                                                               
*---------------------------------------------------------------------*         
* MODE=ADD    :ADD A BOOK TO TABLE, IF NOT ALREADY THERE                        
*---------------------------------------------------------------------*         
ADD      NTR1                                                                   
         CLI   DBURTYP,DBURNETQ    IS THIS RECORD FOR NET-SQL EXTRACT?          
         BE    ADD05               YES                                          
         CLI   DBURTYP,DBUEMAIQ    OR FOR EMAIL GENERATION?                     
         BE    ADD05               YES                                          
         DC    H'0'                NO. DON'T SUPPORT ANYTHING ELSE YET          
*                                                                               
ADD05    LA    R2,BKENTRY          BUILD BOOK ENTRY                             
         USING SVBKTABD,R2                                                      
         MVC   SVBKFLG,DBUBKFLG                                                 
         MVC   SVBKBOOK,DBUBOOK                                                 
         MVC   SVBKPTYP,DBUPSTYP                                                
         DROP  R2                                                               
*                                                                               
         LA    R3,SVBKTAB          SAVE BK ENTRY TO TABLE IF NOT THERE          
ADD10    OC    0(SVBKTABL,R3),0(R3)                                             
         BZ    ADD20               ENTRY NOT IN TABLE. GO ADD IT                
         CLC   BKENTRY,0(R3)                                                    
         BE    ADDX                BOOK ENTRY ALREADY IN TABLE. EXIT            
         LA    R3,SVBKTABL(R3)                                                  
         B     ADD10                                                            
ADD20    MVC   0(SVBKTABL,R3),BKENTRY    ADD ENTRY TO TABLE                     
*                                                                               
ADDX     B     XIT                                                              
*                                                                               
*---------------------------------------------------------------------*         
* MODE=RELEASE :RELEASE BOOK ENTRIES AS CONTROL RECORDS                         
*---------------------------------------------------------------------*         
RELEASE  NTR1                                                                   
         CLI   DBURTYP,DBURNETQ    RECORD FOR NET SQL EXTRACT                   
         BNE   REL05                                                            
         LA    R4,CTAPE                                                         
         B     REL08                                                            
*                                                                               
REL05    CLI   DBURTYP,DBUEMAIQ    EMAIL LINE                                   
         BE    *+6                                                              
         DC    H'0'                INVALID RECORD TYPE                          
         LA    R4,EMTAPE                                                        
*                                                                               
REL08    OPEN  ((R4),OUTPUT)                                                    
*                                                                               
         LA    R3,SVBKTAB          FOR EVERY BOOK ENTRY IN THE TABLE,           
REL10    OC    0(SVBKTABL,R3),0(R3)                                             
         BZ    RELEASEX                                                         
         USING SVBKTABD,R3                                                      
*                                                                               
         CLI   DBURTYP,DBURNETQ    RECORD FOR NET SQL EXTRACT                   
         BNE   *+8                                                              
         BAS   RE,PUTCTREC                                                      
*                                                                               
         CLI   DBURTYP,DBUEMAIQ    EMAIL LINE                                   
         BNE   *+8                                                              
         BAS   RE,PUTEMAIL                                                      
*                                                                               
         LA    R3,SVBKTABL(R3)                                                  
         B     REL10                                                            
*                                                                               
RELEASEX CLOSE (R4)                                                             
         B     XIT                                                              
*                                                                               
*---------------------------------------------------------------------*         
* CREATE CONTROL RECORDS FOR NET EXTRACT AND PUT TO OUTPUT FILE                 
* R3 POINTS TO BOOK ENTRY IN SVBKTAB                                            
* R4 POINTS TO CTAPE                                                            
*---------------------------------------------------------------------*         
PUTCTREC NTR1                                                                   
         USING SVBKTABD,R3                                                      
*                                                                               
         XCEF  CTREC,L'CTREC       CREATE A CONTROL RECORD                      
         LA    R2,CTREC+4                                                       
         USING CTDURECD,R2                                                      
         MVI   CTDURTYP,CTDURTYQ                                                
         MVI   CTDUSRTP,CTDUSRTQ                                                
         GOTO1 CDATCON,DMCB,(5,0),(2,CTDULDAT)  TODAY'S DATE COMPRESSED         
         MVC   CTDUBFLG,SVBKFLG                                                 
         MVC   CTDUPTYP,SVBKPTYP                                                
*                                                                               
         CLI   SVBKFLG,DBUBKWQ     CREATE START/END DATES FOR WEEKLY BK         
         BNE   PTCR20                                                           
         GOTO1 UNWEEK,DMCB,SVBKBOOK,DUB   GET START DATE IN YYMMDD FMT          
         GOTO1 CDATCON,DMCB,(0,DUB),(2,CTDUSDAT) START DATE COMPRESSED          
         GOTO1 CADDAY,DMCB,DUB,WORK,6       GET END DATE IN YYMMDD FMT          
         GOTO1 CDATCON,DMCB,(0,WORK),(2,CTDUEDAT)  END DATE COMPRESSED          
         B     PTCR30                                                           
*                                                                               
PTCR20   GOTO1 CDEMTABS,DMCB,MTHDATES  CREATE ST/END DATES FOR MTHLY BK         
         ICM   R5,15,DMCB          A(TABLE OF MONTH/DATE DEFINITIONS)           
         BNZ   *+6                                                              
         DC    H'0'                BAD TABLEID PASSED                           
         ICM   R0,15,DMCB+4        LENGTH OF TABLE ENTRY                        
         USING MTHDATD,R5                                                       
PTCR25   CLC   SVBKBOOK,MDNLBOOK   COMPARE THE YYMM BOOK                        
         BNH   PTCR27                                                           
         AR    R5,R0               NEXT ENTRY IN MONTH/DATE TABLE               
         B     PTCR25                                                           
PTCR27   GOTO1 CDATCON,DMCB,(3,MDSDATE),(2,CTDUSDAT)  STRT DATE COMPRSD         
         GOTO1 CDATCON,DMCB,(3,MDEDATE),(2,CTDUEDAT)  END DATE COMPRSD          
         DROP  R5                                                               
*                                                                               
PTCR30   MVC   CTDULEN,=AL2(CTDUDATA-CTDURECD)   RECORD LENGTH                  
*                                                                               
         LA    RE,WORK             CREATE THE '02' ELEMENT                      
         USING CTDU30D,RE                                                       
         MVI   CTDU30CD,CTDU30Q    ELEMENT CODE                                 
         MVI   CTDU30LN,CTDU30LQ   ELEMENT LENGTH                               
         MVC   CTDU30BK,SVBKBOOK   DEMO BOOK                                    
         DROP  RE                                                               
         GOTO1 CHELLO,DMCB,(C'P',=C'CTFILE'),CTREC+4,WORK,0                     
         CLI   DMCB+12,0                                                        
         BE    *+6                                                              
         DC    H'0'                ERROR ADDING ELEMENT                         
         SR    RE,RE                                                            
         ICM   RE,7,DMCB+13        NEW RECORD LENGTH                            
         LA    RE,4(RE)            PLUS HEADER                                  
         STCM  RE,3,CTREC          OUTPUT RECORD LENGTH                         
*                                                                               
         PUT   (R4),CTREC                                                       
*                                                                               
PUTCTRCX B     XIT                                                              
         DROP  R2,R3                                                            
*                                                                               
*---------------------------------------------------------------------*         
* CREATE EMAIL LINE AND PUT TO OUTPUT FILE                                      
* R3 POINTS TO BOOK ENTRY IN SVBKTAB                                            
* R4 POINTS TO EMTAPE                                                           
*---------------------------------------------------------------------*         
PUTEMAIL NTR1                                                                   
         USING SVBKTABD,R3                                                      
*                                                                               
         MVC   EMREC,=CL80' '      CREATE AN EMAIL LINE                         
         LA    R2,EMREC                                                         
*                                                                               
         CLI   SVBKFLG,DBUBKWQ     WRITE START DATE FOR WEEKLY BOOK             
         BNE   PTEM20                                                           
         GOTO1 UNWEEK,DMCB,SVBKBOOK,DUB   GET START DATE IN YYMMDD FMT          
         GOTO1 CDATCON,DMCB,(0,DUB),(11,(R2))                                   
         LA    R2,9(R2)                                                         
         B     PTEM30                                                           
*                                                                               
PTEM20   DS    0X                  WRITE MONTHLY BOOK                           
         GOTO1 CDATCON,DMCB,(3,SVBKBOOK),(9,(R2))                               
         LA    R2,7(R2)                                                         
*                                                                               
PTEM30   CLI   SVBKPTYP,DBUPNETQ   WRITE MEDIA                                  
         BNE   *+10                                                             
         MVC   0(9,R2),=C'BROADCAST'                                            
*                                                                               
         CLI   SVBKPTYP,DBUPSYNQ                                                
         BNE   *+10                                                             
         MVC   0(11,R2),=C'SYNDICATION'                                         
*                                                                               
         CLI   SVBKPTYP,DBUPCBLQ                                                
         BNE   *+10                                                             
         MVC   0(5,R2),=C'CABLE'                                                
*                                                                               
         CLI   SVBKPTYP,DBUPHSPQ                                                
         BNE   *+10                                                             
         MVC   0(8,R2),=C'HISPANIC'                                             
*                                                                               
         PUT   (R4),EMREC                                                       
*                                                                               
         B     XIT                                                              
         DROP  R3                                                               
*                                                                               
*---------------------------------------------------------------------*         
* CONVERT YYWW BOOK INTO 6 BYTE START DATE                                      
* P1=A(BINARY BOOK IN YYWW FORMAT)                                              
* P2=A(START DATE OF WEEK IN YYMMDD FORMAT)                                     
*---------------------------------------------------------------------*         
UNWEEK   NTR1                                                                   
         LM    R2,R3,0(R1)                                                      
*                                                                               
         ZIC   R1,0(R2)            GENERATE JAN01 OF SPECIFIED YEAR             
         EDIT  (R1),(2,0(R3))                                                   
         OI    0(R3),X'F0'                                                      
         MVC   2(4,R3),=C'0101'                                                 
         GOTO1 CGETDAY,DMCB,0(R3),DMCB+12                                       
         ZIC   R0,0(R1)            BACK UP TO PREVIOUS MONDAY                   
         LA    R4,1                                                             
         SR    R4,R0                                                            
         GOTO1 CADDAY,DMCB,0(R3),DMCB+12,(R4)                                   
*                                                                               
         ZIC   R5,1(R2)            WEEK NUMBER                                  
         BCTR  R5,0                                                             
         MH    R5,=H'7'            X 7 DAYS A WEEK                              
         ST    R5,DMCB+8                                                        
         GOTO1 CADDAY,DMCB,DMCB+12,0(R3)                                        
         B     XIT                                                              
*                                                                               
XIT      XIT1                                                                   
         EJECT                                                                  
*                                                                               
         LTORG                                                                  
CTAPE    DCB   DDNAME=CTAPE,                                           *        
               DSORG=PS,                                               *        
               RECFM=VB,                                               *        
               LRECL=1000,                                             *        
               MACRF=PM                                                         
*                                                                               
EMTAPE   DCB   DDNAME=EMTAPE,                                          *        
               DSORG=PS,                                               *        
               RECFM=FB,                                               *        
               LRECL=80,                                               *        
               MACRF=PM                                                         
*                                                                               
SVBKTAB  DS    100XL(SVBKTABL)     SAVE TABLE FOR BOOKS UPDATED                 
*                                                                               
WORKD    DSECT                                                                  
*                                                                               
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
DMCB     DS    6F                                                               
WORK     DS    XL64                                                             
*                                                                               
MODE     DS    X                                                                
*                                                                               
BKENTRY  DS    XL(SVBKTABL)        BUILD BOOK TABLE ENTRY HERE                  
CTREC    DS    XL1000              AREA FOR CONTROL RECORD                      
EMREC    DS    CL80                                                             
*                                                                               
WORKL    EQU   *-WORKD                                                          
*                                                                               
SVBKTABD DSECT                     DSECT TO COVER SVBKTAB                       
SVBKFLG  DS    X                   BOOK FLAG (1=MONTHLY/2=WEEKLY)               
SVBKBOOK DS    XL2                 BOOK                                         
SVBKPTYP DS    C                   POSTING TYPE                                 
SVBKTABL EQU   *-SVBKTABD                                                       
*                                                                               
       ++INCLUDE DBUPARMSD                                                      
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE DEDEMTABD                                                      
       ++INCLUDE CTGENFILE                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002DEBKUPDT  12/04/08'                                      
         END                                                                    
