*          DATA SET CTTERMADD2 AT LEVEL 021 AS OF 09/19/96                      
*CATALP CTTRMA2                                                                 
***********************************************************************         
*  TTL:      CTTERMADD2: CONTROL FILE TERMINAL ADD                    *         
*  PURPOSE:  ADD TERMINALS LISTED                                     *         
*                                                                     *         
***********************************************************************         
CTTRMA2  CSECT                                                                  
         PRINT NOGEN                                                            
         NBASE 0,*CTTRMA2,=V(REGSAVE)                                           
*                                                                               
         ENTRY UTL                                                              
         ENTRY SSB                                                              
*                                                                               
         GOTO1 =V(STXITER),DMCB,A(DUMPLIST)                                     
*                                                                               
         L     R8,=V(CPRINT)                                                    
         USING DPRINT,R8                                                        
         MVC   TITLE(28),=C'CTTRMA2 - ADD SELT TERMINALS'                       
*                                                                               
         EJECT                                                                  
MAIN     DS    0H                                                               
         GOTO1 =V(DATAMGR),DMCB,=C'DMOPEN',=C'CONTROL',FLIST,AIO                
         MVI   DATADISP+1,28                                                    
         OPEN  (TOUT,OUTPUT)       QSAM MACRO                                   
         GOTO1 =V(SORTER),DMCB,SORTCARD,RECCARD                                 
*                                                                               
*  GET HIGH TERMINAL NUMBER                                                     
*                                                                               
         LA    R7,KEY                                                           
         USING CTTREC,R7                                                        
         MVC   KEY,HITERM          HIGH TERMINAL NUMBER KEY                     
         GOTO1 =V(DATAMGR),DMCB,=C'DMREAD',=C'CTFILE',KEY,AIO                   
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                DIE, BAD READ                                
         L     R6,AIO              POINT AT REC READ                            
         MVI   ELCODE,X'03'        GET 03 ELEM (TERM NUM)                       
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING CTPASD,R6                                                        
         MVC   HIGH,CTPASDTA       GET HIGH TERM NUMBER                         
         DROP  R6                                                               
*                                                                               
*   READ KATZ0300 TERM RECORD AS A BASIS FOR NEW RECS                           
*                                                                               
         XC    KEY,KEY                                                          
         MVI   CTTKEY,C'T'         TERM REC                                     
         MVC   CTTKTID,=C'KATZ0300' MY TERMINAL                                 
         GOTO1 =V(DATAMGR),DMCB,=C'DMREAD',=C'CTFILE',KEY,AIO                   
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                DIE, BAD READ                                
         DROP  R7                                                               
         GOTO1 =V(HELLO),DMCB,(C'D',=C'CTFILE'),(X'03',AIO),0                   
*                                                                               
*  CREATE TERMINAL RECS KATZ0.01 --> KATZ0.99 AS EXACT DUPS OF KATZ0300         
*                                                                               
         L     R7,AIO                                                           
         USING CTTREC,R7                                                        
         LA    R4,CU2                                                           
M10      DS    0H                                                               
         MVC   CTTKTID,LUID                                                     
         MVI   CTTSTAT,0                                                        
*                                                                               
         LH    R1,HIGH                                                          
         CH    R1,=X'FFFF'                                                      
         BNE   *+6                                                              
         DC    H'0'                                                             
         LA    R1,1(R1)                                                         
         STH   R1,HIGH                                                          
         GOTO1 =V(HELLO),DMCB,(C'P',=C'CTFILE'),(X'03',AIO),HELL                
         CLI   DMCB+12,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   RE,SPUT                                                          
* NOW BUILD PASSIVE KEY & REC                                                   
         GOTO1 =V(HELLO),DMCB,(C'D',=C'CTFILE'),(X'03',AIO),0                   
         GOTO1 =V(HELLO),DMCB,(C'P',=C'CTFILE'),(X'03',AIO),VELL                
         XC    CTTKEY,CTTKEY                                                    
         MVI   CTTKEY,C'T'                                                      
         MVC   CTTKEY+23(2),HIGH                                                
         MVI   CTTSTAT,1                                                        
         BAS   RE,SPUT                                                          
* NOW BUMP TERM NUMBER                                                          
         PACK  DUB(2),LCUDV+2(2)                                                
         AP    DUB(2),=P'1'                                                     
         CP    DUB(2),=P'99'                                                    
         BNH   M14                                                              
         LA    R4,1(R4)                                                         
         CLI   0(R4),X'FF'                                                      
         BE    M15                                                              
         MVC   LCUDV+1(1),0(R4)                                                 
         ZAP   DUB(2),=P'1'                                                     
*                                                                               
M14      UNPK  LCUDV+2(2),DUB(2)                                                
         OI    LCUDV+3,X'F0'       IGNORE SIGN                                  
         XC    CTTKEY,CTTKEY                                                    
         MVI   CTTKEY,C'T'                                                      
         GOTO1 =V(HELLO),DMCB,(C'D',=C'CTFILE'),(X'03',AIO),0                   
         B     M10                                                              
*                                                                               
*  PUT NEW HIGH TERMINAL NUMBER TO FILE                                         
*                                                                               
M15      MVC   KEY,HITERM                                                       
         GOTO1 =V(DATAMGR),DMCB,=C'DMREAD',=C'CTFILE',KEY,AIO                   
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                DIE, READ ERROR                              
         GOTO1 =V(HELLO),DMCB,(C'D',=C'CTFILE'),(X'03',AIO),0                   
         GOTO1 =V(HELLO),DMCB,(C'P',=C'CTFILE'),(X'03',AIO),HELL                
         CLI   DMCB+12,0                                                        
         BE    *+6                                                              
         DC    H'0'                DIE, ELEMENT ERROR                           
         BAS   RE,SPUT                                                          
*                                                                               
* PUT ALL OTHER RECS TO SORTER                                                  
*                                                                               
         L     R7,AIO                                                           
         USING CTTREC,R7                                                        
         XC    KEY,KEY                                                          
         GOTO1 =V(DATAMGR),DMCB,(X'08',=C'DMRDHI'),=C'CTFILE',KEY,AIO           
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                DIE, BAD READ                                
M20      BAS   RE,SPUT                                                          
M25      GOTO1 =V(DATAMGR),DMCB,(X'08',=C'DMRSEQ'),=C'CTFILE',KEY,AIO           
         CLI   DMCB+8,X'80'                                                     
         BE    M30                                                              
         L     R6,AIO                                                           
         CLI   0(R6),C'T'                                                       
         BNE   M20                                                              
         CLC   HITERM,0(R6)                                                     
         BE    M25                 DON'T WRITE OLD HI-TERM                      
         B     M20                 OTHERWISE PUT REC                            
*                                                                               
M30      GOTO1 =V(SORTER),DMCB,=C'GET'                                          
         ICM   R6,15,4(R1)                                                      
         BZ    MX                                                               
         CLC   KEYSAVE,4(R6)                                                    
         BNE   M40                                                              
         BAS   RE,DUPKEY                                                        
         B     M30                                                              
M40      PUT   TOUT,(R6)                                                        
         MVC   KEYSAVE,4(R6)                                                    
         B     M30                                                              
MX       GOTO1 =V(SORTER),DMCB,=C'END'                                          
         XBASE                                                                  
*                                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* SPUT: SORT PUT, SET LEN, 2 BYTES OF NULLS, AND VERIFY VALID LENGTH  *         
***********************************************************************         
         SPACE                                                                  
SPUT     NTR1                                                                   
         L     R7,AIO                                                           
         USING CTTREC,R7                                                        
         L     R2,ARECLEN                                                       
         MVC   0(2,R2),CTTLEN      GET RECORD LEN FOR PUT                       
         LA    R1,4                ADD 4 TO COVER 2 BYTES LEN + ...             
         AH    R1,0(R2)            2 BYTES OF NULLS                             
         STH   R1,0(R2)                                                         
         CH    R1,=H'32'           25B KEY + 4B ABOVE + 3B(LEN+STAT)            
         BNL   *+6                                                              
         DC    H'0'                DIE, BAD LENGTH                              
         GOTO1 =V(SORTER),DMCB,=C'PUT',ARECLEN                                  
SPX      B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* DUPKEY: PRINT DUPLICATE KEY ERROR                                   *         
***********************************************************************         
         SPACE                                                                  
DUPKEY   NTR1                                                                   
         MVC   P(22),=C'ERROR, DUPLICATE KEY: '                                 
         LA    R2,P                                                             
         MVC   22(08,R2),11(R6)                                                 
         LA    R6,4(R6)                                                         
         GOTO1 =V(PRINTER)                                                      
         GOTO1 =V(HEXOUT),DMCB,(R6),P,25,=C'TOG'                                
         GOTO1 =V(PRINTER)                                                      
         B     EXIT                                                             
         EJECT                                                                  
*                                                                               
DUMPLIST DS    0F                                                               
         DC    A(CTTRMA2,65000)                                                 
         ORG   *-4                                                              
         DC    X'80'                                                            
         ORG   *                                                                
*                                                                               
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*KATZ0300                                                                       
*KATZ0301 - KATZ0999                                                            
*                                                                               
*KATZ0A01 - KATZ0A99                                                            
*KATZ0B01 - KATZ0B99                                                            
*KATZ0C01 - KATZ0C99                                                            
*KATZ0D01 - KATZ0D99                                                            
*KATZ0E01 - KATZ0E99                                                            
*KATZ0F01 - KATZ0F99                                                            
*KATZ0G01 - KATZ0G99                                                            
*KATZ0H01 - KATZ0H99                                                            
*KATZ0I01 - KATZ0I99                                                            
*KATZ0J01 - KATZ0J99                                                            
*KATZ0K01 - KATZ0K99                                                            
*KATZ0L01 - KATZ0L99                                                            
*KATZ0M01 - KATZ0M99                                                            
*KATZ0N01 - KATZ0N99                                                            
*KATZ0O01 - KATZ0O99                                                            
*KATZ0P01 - KATZ0P99                                                            
*                                                                               
TOUT     DCB   DDNAME=TOUT,DSORG=PS,MACRF=(PM),                        X        
               RECFM=VB,BLKSIZE=8200,LRECL=2048,BUFNO=2                         
*                                                                               
SSB      DC    F'0'                                                             
UTL      DC    F'0',X'0A'                                                       
*                                                                               
SORTCARD DC    CL80'SORT FIELDS=(5,25,A),FORMAT=BI,WORK=1'                      
RECCARD  DC    CL80'RECORD TYPE=V,LENGTH=2048'                                  
*                                                                               
         GETEL R6,DATADISP,ELCODE                                               
*                                                                               
FLIST    DS    0H                                                               
         DC    CL8' CTFILE '                                                    
         DC    CL8'X       '                                                    
*                                                                               
ARECLEN  DC    A(RECLEN)                                                        
AIO      DC    A(IO)                                                            
*                                                                               
DUB      DS    D                                                                
WORK     DS    CL17                                                             
DATADISP DS    H                                                                
ELCODE   DS    CL1                                                              
KEY      DS    CL25                                                             
KEYSAVE  DS    CL25                                                             
HITERM   DS    0CL25               HIGH TERMINAL KEY                            
         DC    C'T'                                                             
         DC    XL24'00'                                                         
*                                                                               
         DS    0H                                                               
HELL     DC    X'0304'                                                          
HIGH     DS    XL2                 HIGH TERMINAL NUMBER                         
*                                                                               
VELL     DC    X'030A'                                                          
LUID     DS    0CL8                                                             
LNAME    DC    CL4'KATZ'                                                        
LCUDV    DC    CL4'0301'                                                        
*                                                                               
CU2      DC    C'3456789ABCDEFGHIJKLMNOP',X'FF'                                 
*                                                                               
DMCB     DS    6F                                                               
         DS    0D                                                               
         DC    C'***IO***'                                                      
RECLEN   DS    H                   REC LEN FOR QSAM PUT                         
         DC    H'0'                                                             
IO       DS    XL2000              IO AREA                                      
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
       ++INCLUDE DDDPRINT                                                       
         PRINT ON                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'021CTTERMADD209/19/96'                                      
         END                                                                    
