*          DATA SET SPDSTCALC  AT LEVEL 002 AS OF 06/16/94                      
*CATALP SPDSTCAL                                                                
         SPACE 2                                                                
*====================================================================*          
* THIS ROUTINE ADJUSTS A DAY/TIME TO EASTERN STANDARD TIME BASED     *          
* TIME ZONE, DATE, AND WHETHER STATION OBSERVES DAYLIGHT SAVINGS TIME*          
*====================================================================*          
         SPACE 2                                                                
*====================================================================*          
*                                                                    *          
* PARAMETER LIST - PASS ADDRESS OF STRUCTURE WHICH CONTAINS:         *          
*                                                                    *          
* DSTIDATE   6 BYTE INPUT DATE IN YYMMDD FORMAT                      *          
* DSTISTTM   2 BYTE INPUT START TIME                                 *          
* DSTINDTM   2 BYTE INPUT END TIME (MAY BE 0)                        *          
* DSTODATE   6 BYTE OUTPUT DATE AREA (YYMMDD FORMAT)                 *          
* DSTOSTTM   2 BYTE OUTPUT START TIME                                *          
* DSTONDTM   2 BYTE OUTPUT END TIME                                  *          
* DSTADDAY   V(ADDAY)                                                *          
* DSTGTDAY   V(GETDAY) ***                                           *          
* DSTTIMEV   1 BYTE TIME VALUE DISPLACEMENT FROM ESTTIME.            *          
*              GET FROM MKTZONE                                      *          
* DSTFLG     1 BYTE DAYLIGHT SAVINGS TIME FLAG.                      *          
*              C'Y' = STATION DOES NOT DO DAYLIGHT TIME              *          
*              GET FROM SFLAG1 IN STATION MASTER RECORD              *          
* DSTERR     24 BYTE OUTPUT ERROR MSG AREA.                          *          
* ===================================================================*          
         TITLE 'DSTCALC - ADJUST TIME ZONES TO EASTERN STANDARD TIME'           
         PRINT NOGEN                                                            
DSTCALC  CSECT                                                                  
         NMOD1 WORKX-WORKD,**DSTC**                                             
         USING WORKD,RC                                                         
*                                                                               
         L     RA,0(R1)                                                         
         USING DSTBLKD,RA                                                       
*                                                                               
         MVC   DSTODATE,DSTIDATE   SET DEFAULT OUTPUT DATE                      
         XC    DSTONDTM,DSTONDTM   SET DEFAULT OUTPUT TIME                      
         SR    R3,R3                                                            
         ICM   R3,3,DSTISTTM       GET START TIME                               
         CH    R3,=H'0600'         TEST PRIOR TO 6A                             
         BNL   DST2                                                             
         GOTO1 DSTADDAY,DMCB,DSTIDATE,DSTIDATE,F'1'                             
*                                                                               
DST2     SR    R4,R4                                                            
         ICM   R4,3,DSTINDTM       GET END TIME                                 
         EJECT                                                                  
*==================================================================*            
* TEST BUY IN DAYLIGHT TIME EFFECTIVE DATES                        *            
*==================================================================*            
***********************  INSERT  *******************************                
         MVC   WORK(2),DSTIDATE    EBCDIC YEAR                                  
         MVC   WORK+2(4),=C'0331'  START LOOKING AT APRIL 30                    
*                                                                               
DAYLITE5 GOTO1 DSTADDAY,DMCB,WORK,WORK,F'1'                                     
         GOTO1 DSTGTDAY,DMCB,WORK,WORK+6                                        
         CLI   DMCB,7              IS IT SUNDAY?                                
         BNE   DAYLITE5            NO                                           
*                                                                               
         CLC   DSTIDATE,WORK                                                    
         BL    DST30               TODAY IS PRIOR TO START OF DST               
*                                                                               
         MVC   WORK(2),DSTIDATE    EBCDIC YEAR                                  
         MVC   WORK+2(4),=C'1101'  START LOOKING AT NOVEMBER 1                  
*                                                                               
DAYLITE7 GOTO1 DSTADDAY,DMCB,WORK,WORK,F'-1'                                    
         GOTO1 DSTGTDAY,DMCB,WORK,WORK+6                                        
         CLI   DMCB,7              IS IT SUNDAY?                                
         BNE   DAYLITE7            NO                                           
         GOTO1 DSTADDAY,DMCB,WORK,WORK,F'-1' SATURDAY IS END OF DST             
*                                                                               
         CLC   DSTIDATE,WORK                                                    
         BH    DST30               TODAY IS AFTER THE END OF DST                
*                                                                               
* ELSE DST20                                                                    
****************************************************************                
*                                                                               
************************** REMOVE ******************************                
*        LA    R1,DSTCLNDR                                                      
*                                                                               
*ST10    CLC   DSTIDATE,0(R1)      TEST PRIOR TO DST START                      
*        BL    DST30               YES - DONE                                   
*        CLC   DSTIDATE,6(R1)      TEST PRIOR TO DST END                        
*        BL    DST20               YES - ADJUST                                 
*        LA    R1,12(R1)           NEXT YEAR                                    
*        CLI   0(R1),X'FF'                                                      
*        BNE   DST10                                                            
*        MVI   DSTERR,DSTERR1Q                                                  
*        B     DSTX                                                             
****************************************************************                
*                                                                               
DST20    CLI   DSTFLG,C'N'         TEST NO DAYLIGHT TIME                        
         BNE   *+8                                                              
         AH    R3,=H'100'          IF NONE, ADD 1 HOUR                          
*                                                                               
         LTR   R4,R4               TEST END TIME PRESENT                        
         BZ    DST30                                                            
*                                                                               
         CLI   DSTFLG,C'N'                                                      
         BNE   *+8                                                              
         AH    R4,=H'100'                                                       
*                                                                               
DST30    NI    DSTTIMEV,X'0F'      MAKE DSPL BINARY                             
         ZIC   R0,DSTTIMEV                                                      
         MH    R0,=H'100'                                                       
         AR    R3,R0               ADD #OF HOURS TO EST                         
         STH   R3,DSTOSTTM                                                      
*                                                                               
         LTR   R4,R4                                                            
         BZ    DST35                                                            
         AR    R4,R0               ADJUST END TIME                              
         STH   R4,DSTONDTM                                                      
*                                                                               
DST35    CH    R3,=H'2400'         TEST TIME > 2400                             
         BL    DSTX                NO                                           
         BE    *+8                 IF 12M DO NOT ADJUST BUT CHANGE DATE         
         SH    R3,=H'2400'         SUBTRACT 24 HRS FROM TIME                    
         STH   R3,DSTOSTTM                                                      
*                                                                               
         CH    R4,=H'2400'         TEST END TIME >2400                          
         BNH   *+8                                                              
         SH    R4,=H'2400'         SUBTRACT 24 HRS FROM TIME                    
         STH   R4,DSTONDTM                                                      
*                                                                               
DST40    DS    0H                                                               
         GOTO1 DSTADDAY,DMCB,DSTIDATE,DSTODATE,F'1'                             
*                                                                               
DSTX     XMOD1 1                                                                
         SPACE 2                                                                
SPACES   DC    CL6'      '                                                      
*STCLNDR DC    C'000000931031'                                                  
*        DC    C'940403941030'                                                  
*        DC    C'950402951029'                                                  
*        DC    X'FF'                                                            
         LTORG                                                                  
         EJECT                                                                  
WORKD    DSECT                                                                  
DMCB     DS    6F                                                               
DUB      DS    D                                                                
WORK     DS    20C                                                              
WORKX    EQU   *                                                                
         SPACE 2                                                                
       ++INCLUDE SPDSTBLK                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002SPDSTCALC 06/16/94'                                      
         END                                                                    
