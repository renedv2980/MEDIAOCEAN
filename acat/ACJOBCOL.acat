*          DATA SET ACJOBCOL   AT LEVEL 018 AS OF 07/23/13                      
*CATALP ACJOBCOL                                                                
         SPACE 2                                                                
************************************************************                    
*                                                          *                    
* ACJOBCOL - EDIT A FIELD CONTAINING A COLUMN LIST         *                    
* AND BUILD A LIST OF ENTRIES FOR ACJOBBER COVERED         *                    
* BY JBCLD.                                                *                    
*                                                          *                    
* ON ENTRY, P1  BYTE  0   = MAXIMUM N'COLUMNS OR ZERO      *                    
*               BYTES 1-3 = A(FIELD HEADER)                *                    
*           P2  BYTES 1-3 = A(COLUMN LIST OUTPUT)          *                    
*           P3  BYTES 1-3 = A(COMFACS)                     *                    
*                                                          *                    
* ON EXIT,  P2  BYTE  0   = N'COLUMN LIST ENTRIES OR ZERO  *                    
*                           IF ERROR                       *                    
*               BYTE  1   = DISPLACEMENT INTO FIELD FOR    *                    
*                           CURSOR POSITION ON ERROR       *                    
*                                                          *                    
************************************************************                    
         TITLE 'ACJOBCOL - EDIT COLUMN LIST FIELD'                              
ACJOBCOL CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**JBCL**,RA,RR=RE,CLEAR=YES                          
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         MVC   PARMS(PARML),0(R1)                                               
         MVI   4(R1),0             INITIALIZE RETURN BYTE                       
         ST    RE,RELO                                                          
*                                                                               
COL1     L     R9,ACOMFACS                                                      
         USING COMFACSD,R9                                                      
         LA    R4,BUFF             R4=A(PARSNIP BLOCK)                          
         USING PSND,R4                                                          
         L     R5,ACOLS            R5=COLUMN LIST AREA                          
         USING JBCLD,R5                                                         
*                                                                               
COL2     GOTO1 CPARSNIP,DMCB,(X'00',AFLD),PSND,('PSNNONLQ',SEPTAB)              
         CLI   8(R1),0             TEST FOR PARSNIP ERROR                       
         BE    COL4                NO                                           
*                                                                               
         SR    R3,R3                                                            
         ICM   R3,7,9(R1)          GET A(PARSNIP ERROR)                         
         L     RE,AFLD             GET A(FIELD HEADER)                          
         LA    RE,8(RE)            CLEAR HOB-ADD L'HEADER                       
         SR    R3,RE               COMPUTE DISP TO ERROR                        
         STC   R3,DISP                                                          
         B     COLERR                                                           
*                                                                               
COL4     CLI   PSNTAG,PSNFLDQ      TEST WE HAVE A FIELD FIRST                   
         BNE   COLERR              NO-MUST BE AN ERROR                          
*                                                                               
         ZIC   R1,NCOLS                                                         
         LA    R1,1(R1)            INCREMENT COLUMN ENTRY COUNT                 
         STC   R1,NCOLS                                                         
         CLI   MAXCOLS,0           TEST IF MAXIMUM COLUMNS SPECIFIED            
         BE    *+14                NO                                           
         CLC   NCOLS,MAXCOLS       TEST IF PAST COLUMN LIMIT                    
         BH    COLERR                                                           
*                                                                               
         XC    JBCLD(JBCLENQ),JBCLD CLEAR COLUMN LIST ENTRY                     
         MVI   JBCLD+JBCLENQ,0                                                  
         MVI   JBCLTYP,JBCLCOL                                                  
         BAS   RE,VALCOL           CHECK COLUMN CODES                           
         BNE   COLERR                                                           
         MVC   JBCLDATA,CLDATA     SET IN COLUMN DATA                           
         MVC   JBCLIND,CLIND       COPY IN INDICATORS                           
*                                                                               
COL6     CLI   PSNVSEP,0           TEST FOR VALUE SEPARATOR                     
         BE    COL8                NO-NEXT ENTRY                                
*                                                                               
         MVI   JBCLTYP,JBCLFRM     FORMULA                                      
         MVC   JBCLOPER,PSNVSEP    VALUE SEPARATOR=OPERATION                    
         CLI   JBCLOPER,C'%'       TEST FOR PERCENTAGE OPERATION                
         BE    *+8                                                              
         NI    JBCLIND,X'FF'-JBCLIPER  TURN OFF PERCENTAGE FLAG                 
         BAS   RE,UPDISP                                                        
         ICM   R4,15,PSNVAL        GET VALUE POINTER                            
         BZ    COLERR              DID NOT HAVE SECOND COLUMN                   
         BAS   RE,VALCOL                                                        
         BNE   COLERR                                                           
         MVC   JBCLCN2(L'JBCLDATA),CLDATA SET IN SECOND COLUMN                  
         OC    PSNVAL,PSNVAL       TEST FOR ANY MORE VALUES                     
         BZ    COL8                NONE-CONTINUE                                
         BAS   RE,UPDISP           BUMP DISPLACEMENT                            
         B     COLERR                                                           
*                                                                               
COL8     LA    R5,JBCLENQ(R5)      NEXT COLUMN LIST ENTRY                       
         BAS   RE,UPDISP                                                        
         LA    R4,PSNL(R4)                                                      
         CLI   PSNTAG,0            TEST FOR ANOTHER FIELD                       
         BNE   COL4                YES-GO PROCESS IT                            
*                                                                               
COL10    L     R1,APARM            END OF STRING                                
         MVC   4(1,R1),NCOLS       RETURN N'COLUMN LIST ENTRIES                 
*                                                                               
COLX     XMOD1 1                                                                
*                                                                               
EXIT     XIT1                                                                   
*                                                                               
COLERR   L     R1,APARM                                                         
         MVC   5(1,R1),DISP        RETURN DISPLACEMENT                          
         B     COLX                                                             
         SPACE 2                                                                
* SUB-ROUTINE TO UPDATE THE DISPLACEMENT INTO FIELD                             
*                                                                               
UPDISP   ZIC   RF,DISP                                                          
         ZIC   R1,PSNLEN                                                        
         LA    RF,1(R1,RF)                                                      
         STC   RF,DISP                                                          
         BR    RE                                                               
         EJECT                                                                  
* SUB-ROUTINE TO VALIDATE A POTENTIAL COLUMN EXPRESSION                         
*                                                                               
* AT ENTRY, R4=A(PARSNIP ENTRY)                                                 
* ON EXIT, CC=EQ AND CLDATA SET IF OK, CC=NEQ IF ERROR                          
*          CLIND SET FOR NON-ESTIMATE COLUMNS                                   
*                                                                               
VALCOL   NTR1  ,                                                                
         L     R3,PSNCOMP          R3=A(COMPONENT STRING)                       
         MVI   CLIND,0             CLEAR COLUMN INDICATORS                      
         XC    CLDATA,CLDATA       CLEAR COLUMN DATA                            
         ZIC   R1,PSNLEN                                                        
         BCTR  R1,0                                                             
         LA    R6,COLTAB                                                        
         USING COLTABD,R6                                                       
         LA    R0,NCOLENT                                                       
*                                                                               
VALCOL2  CLC   PSNLEN,COLMINL                                                   
         BL    VALCOL3                                                          
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R3),COLNAME     MATCH ON COLUMN CODE                         
         BE    VALCOL4                                                          
*                                                                               
VALCOL3  LA    R6,COLTABL(R6)                                                   
         BCT   R0,VALCOL2                                                       
         B     VALCOL6             NOT IN TABLE-LOOK FOR ESTIMATES              
*                                                                               
VALCOL4  MVC   CLNUM,COLNUM        SET DATA NUMBER                              
         MVC   CLIND,COLIND        COPY INDICATORS                              
         B     VALCOLY                                                          
*                                                                               
VALCOL6  MVC   CLNUM,ESTN          LOOK FOR ESTIMATE                            
         BAS   RE,VALEST                                                        
         BE    VALCOLY             ITS AN ESTIMATE                              
         CLI   PSNLEN,5            TEST AT LEAST 5 BYTES THERE                  
         BL    VALCOLN                                                          
         CLC   =C'MAX',0(R3)       TEST STARTS WITH 'MAX'                       
         BNE   VALCOLN             NO                                           
         MVC   CLNUM,MAXESTN                                                    
         LA    R3,3(R3)            BUMP STRING POINTER                          
         ZIC   RF,PSNLEN                                                        
         SH    RF,=H'3'                                                         
         STC   RF,PSNLEN                                                        
         BAS   RE,VALEST                                                        
         BE    VALCOLY                                                          
         B     VALCOLN                                                          
*                                                                               
VALCOLY  B     OK                                                               
*                                                                               
VALCOLN  B     NOTOK                                                            
         EJECT                                                                  
* SUB-ROUTINE TO VALIDATE A POTENTIAL ESTIMATE EXPRESSION                       
*                                                                               
* AT ENTRY, R3=A(ESTIMATE STRING), R4=A(PARSNIP BLOCK)                          
* ON EXIT, CC=EQ IF OK AND CLDATA COMPLETED, CC=NEQ IF ERROR                    
*                                                                               
VALEST   NTR1  ,                                                                
         XC    SVCOLDAT,SVCOLDAT   CLEAR COLUMN DATA NUMBER                     
         CLI   0(R3),C'P'          TEST FOR CORRECT PREFIX                      
         BE    VALEST1                                                          
         CLI   0(R3),C'R'                                                       
         BE    VALEST1                                                          
         CLI   0(R3),C'S'          MUST BE A SUPPLEMENT                         
         BNE   VALESTN             NO                                           
         MVI   JBCLTYP,JBCLSUPP    CHANGE COLUMN TYPE                           
*                                                                               
VALEST1  MVC   CLESTTYP,0(R3)                                                   
*                                                                               
         ZIC   RE,PSNLEN           GET COMPONENT LENGTH                         
         LA    RE,0(R3,RE)                                                      
         BCTR  RE,0                RE=A(LAST BYTE OF STRING)                    
         LA    R1,JBDESTN-JBDEST   ADJUSTMENT FOR NET FORCED                    
         CLI   0(RE),C'N'          TEST FOR 'NET' SUFFIX                        
         BNE   VALEST2             NO ,CONTINUE                                 
         AHI   RE,-1               YES, BACKUP A BYTE                           
         CLI   0(RE),C'H'          IS IT HOURS?                                 
         LA    RE,1(RE)            ADD BYTE BACK FIRST (AHI SETS CC)            
         BNE   VALEST4             NO, TREAT AS NET                             
*                                                                               
VALEST2  LA    R1,100                                                           
         CLI   0(RE),C'G'          TEST FOR 'GROSS' SUFFIX                      
         BE    VALEST4                                                          
         LA    R1,200                                                           
         CLI   0(RE),C'C'          TEST FOR 'COMMISSION' SUFFIX                 
         BE    VALEST4                                                          
         LA    R1,500                                                           
         CLI   0(RE),C'H'          TEST FOR 'HOURS' SUFFIX                      
         BE    VALEST4                                                          
         LA    R1,600                                                           
         CLI   0(RE),C'B'          TEST FOR 'B HOURS' SUFFIX                    
         BE    VALEST3                                                          
         LA    R1,700                                                           
         CLI   0(RE),C'N'          TEST FOR 'N HOURS' SUFFIX                    
         BE    VALEST3                                                          
         LA    R1,800                                                           
         CLI   0(RE),C'R'          TEST FOR 'R HOURS' SUFFIX                    
         BNE   VALEST5                                                          
*                                                                               
VALEST3  BCTR  RE,0                                                             
         CLI   0(RE),C'H'          MUST BE HR, HB OR HN                         
         BNE   VALESTN                                                          
         LA    RE,1(RE)            BUMP BACK UP                                 
*                                                                               
VALEST4  STH   R1,SVCOLDAT         SAVE THE COLUM DATA NUMBER                   
         AH    R1,CLNUM                                                         
         STH   R1,CLNUM            REPLACE COLUMN DATA NUMBER                   
         ZIC   RF,PSNLEN                                                        
         BCTR  RF,0                                                             
         STC   RF,PSNLEN                                                        
*                                                                               
VALEST5  LA    R3,1(R3)            BUMP R3 PAST PREFIX                          
         ZIC   R6,PSNLEN                                                        
         BCTR  R6,0                                                             
         CLC   SVCOLDAT,=H'600'    YES, IS THIS SPECIAL HOURS?                  
         BL    *+6                 NO                                           
         BCTR  R6,0                YES, ONLY ALLOW FOR 2-DIGITS                 
         CH    R6,=H'2'            TEST MORE THAN TWO DIGITS                    
         BH    VALESTN             YES, ERROR                                   
*                                                                               
VALEST6  LR    RE,R3               RE=A(NUMBER)                                 
         LR    RF,R6               RF=LOOP COUNTER                              
*                                                                               
VALEST7  CLI   0(RE),C'0'          TEST FOR VALID VERSION NUMBER                
         BL    VALESTN                                                          
         CLI   0(RE),C'9'                                                       
         BH    VALESTN                                                          
         LA    RE,1(RE)                                                         
         BCT   RF,VALEST7                                                       
*                                                                               
VALEST8  BCTR  R6,0                                                             
         EX    R6,*+8                                                           
         B     *+10                                                             
         PACK  DUB,0(0,R3)                                                      
         CVB   R0,DUB                                                           
         LTR   R0,R0                                                            
         BZ    VALESTN             CANNOT BE VERSION ZERO                       
         STC   R0,CLESTVER                                                      
*                                                                               
VALESTY  B     OK                                                               
*                                                                               
VALESTN  B     NOTOK                                                            
         EJECT                                                                  
OK       CR    RB,RB                                                            
         B     EXIT                                                             
*                                                                               
NOTOK    LTR   RB,RB                                                            
         B     EXIT                                                             
         SPACE 2                                                                
* CONSTANTS                                                                     
*                                                                               
ESTN     DC    AL2(JBDEST)                                                      
MAXESTN  DC    AL2(JBDMEST)                                                     
         SPACE 2                                                                
* SEPARATOR OVERRIDE TABLE FOR PARSNIP                                          
*                                                                               
SEPTAB   DC    AL1(4),C'+-*%'      FIELD DIVIDER                                
         DC    AL1(1),C','                                                      
         DC    AL1(1),C','                                                      
         DC    AL1(1),C','                                                      
         SPACE 2                                                                
* TABLE OF VALID COLUMN DATA TYPES                                              
*                                                                               
COLTAB   DS    0CL(COLTABL)                                                     
*                                                                               
         DC    CL8'OE      ',X'00',X'02',AL2(JBDORG)                            
         DC    CL8'OEN     ',X'00',X'03',AL2(JBDORGN)                           
         DC    CL8'OEG     ',X'00',X'03',AL2(JBDORGG)                           
         DC    CL8'OEC     ',X'00',X'03',AL2(JBDORGC)                           
         DC    CL8'OEH     ',X'00',X'03',AL2(JBDORGH)                           
         DC    CL8'OEHB    ',X'00',X'03',AL2(JBDORGHB)                          
         DC    CL8'OEHN    ',X'00',X'03',AL2(JBDORGHN)                          
         DC    CL8'OEHR    ',X'00',X'03',AL2(JBDORGHR)                          
*                                                                               
         DC    CL8'CE      ',X'00',X'02',AL2(JBDCUR)                            
         DC    CL8'CEN     ',X'00',X'03',AL2(JBDCURN)                           
         DC    CL8'CEG     ',X'00',X'03',AL2(JBDCURG)                           
         DC    CL8'CEC     ',X'00',X'03',AL2(JBDCURC)                           
         DC    CL8'CEH     ',X'00',X'03',AL2(JBDCURH)                           
         DC    CL8'CEHB    ',X'00',X'03',AL2(JBDCURHB)                          
         DC    CL8'CEHN    ',X'00',X'03',AL2(JBDCURHN)                          
         DC    CL8'CEHR    ',X'00',X'03',AL2(JBDCURHR)                          
*                                                                               
         DC    CL8'PRV     ',X'00',X'03',AL2(JBDPRV)                            
         DC    CL8'PRVN    ',X'00',X'04',AL2(JBDPRVN)                           
         DC    CL8'PRVG    ',X'00',X'04',AL2(JBDPRVG)                           
         DC    CL8'PRVC    ',X'00',X'04',AL2(JBDPRVC)                           
*                                                                               
         DC    CL8'HR      ',X'00',X'02',AL2(JBDHR)                             
         DC    CL8'HRG     ',X'00',X'03',AL2(JBDHRG)                            
         DC    CL8'HRC     ',X'00',X'03',AL2(JBDHRC)                            
         DC    CL8'HRH     ',X'00',X'03',AL2(JBDHRH)                            
         DC    CL8'HRHB    ',X'00',X'03',AL2(JBDHRHB)                           
         DC    CL8'HRHN    ',X'00',X'03',AL2(JBDHRHN)                           
         DC    CL8'HRHR    ',X'00',X'03',AL2(JBDHRHR)                           
*                                                                               
         DC    CL8'ACT     ',X'00',X'03',AL2(JBDACT)                            
         DC    CL8'ACTG    ',X'00',X'04',AL2(JBDACTG)                           
         DC    CL8'ACTC    ',X'00',X'04',AL2(JBDACTC)                           
         DC    CL8'ACTH    ',X'00',X'04',AL2(JBDACTH)                           
*                                                                               
         DC    CL8'RATE    ',X'00',X'04',AL2(JBDRATE)                           
*                                                                               
         DC    CL8'PO      ',X'00',X'02',AL2(JBDPO)                             
         DC    CL8'POG     ',X'00',X'03',AL2(JBDPOG)                            
         DC    CL8'POC     ',X'00',X'03',AL2(JBDPOC)                            
*                                                                               
         DC    CL8'BAL     ',X'00',X'03',AL2(JBDBAL)                            
         DC    CL8'BALG    ',X'00',X'04',AL2(JBDBALG)                           
         DC    CL8'BALC    ',X'00',X'04',AL2(JBDBALC)                           
*                                                                               
         DC    CL8'UNC     ',X'00',X'03',AL2(JBDUNC)                            
         DC    CL8'UNCG    ',X'00',X'04',AL2(JBDUNCG)                           
         DC    CL8'UNCC    ',X'00',X'04',AL2(JBDUNCC)                           
*                                                                               
         DC    CL8'PCOM    ',AL1(COLIPER),X'04',AL2(JBDPCOM)                    
         DC    CL8'PCOMG   ',AL1(COLIPER),X'05',AL2(JBDPCOMG)                   
         DC    CL8'PCOMC   ',AL1(COLIPER),X'05',AL2(JBDPCOMC)                   
*                                                                               
         DC    CL8'PUNC    ',AL1(COLIPER),X'04',AL2(JBDPUNC)                    
         DC    CL8'PUNCG   ',AL1(COLIPER),X'05',AL2(JBDPUNCG)                   
         DC    CL8'PUNCC   ',AL1(COLIPER),X'05',AL2(JBDPUNCC)                   
*                                                                               
         DC    CL8'MAXOE   ',X'00',X'05',AL2(JBDMORG)                           
         DC    CL8'MAXOEG  ',X'00',X'06',AL2(JBDMORGG)                          
         DC    CL8'MAXOEC  ',X'00',X'06',AL2(JBDMORGC)                          
*                                                                               
         DC    CL8'MAXCE   ',X'00',X'05',AL2(JBDMCUR)                           
         DC    CL8'MAXCEG  ',X'00',X'06',AL2(JBDMCURG)                          
         DC    CL8'MAXCEC  ',X'00',X'06',AL2(JBDMCURC)                          
*                                                                               
         DC    CL8'PEBCE   ',X'00',X'05',AL2(JBDPCUR)                           
         DC    CL8'PEBCEG  ',X'00',X'06',AL2(JBDPCURG)                          
         DC    CL8'PEBCEC  ',X'00',X'06',AL2(JBDPCURC)                          
*                                                                               
NCOLENT  EQU   (*-COLTAB)/L'COLTAB                                              
         SPACE 2                                                                
* LITERAL POOL                                                                  
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
* DSECT TO COVER ACJOBCOL WORKING STORAGE                                       
*                                                                               
WORKD    DSECT                                                                  
RELO     DS    A                                                                
APARM    DS    A                                                                
PARMS    DS    0A                                                               
MAXCOLS  DS    0X                                                               
AFLD     DS    A                                                                
ACOLS    DS    A                                                                
ACOMFACS DS    A                                                                
PARML    EQU   *-PARMS                                                          
SAVERE   DS    A                                                                
SVCOLDAT DS    H                                                                
*                                                                               
DUB      DS    D                                                                
DMCB     DS    6F                                                               
*                                                                               
NCOLS    DS    X                                                                
DISP     DS    X                                                                
CLIND    DS    X                                                                
         DS    0H                                                               
CLDATA   DS    0XL(L'JBCLDATA)                                                  
CLNUM    DS    H                                                                
CLESTTYP DS    C                                                                
CLESTVER DS    X                                                                
         DS    XL(L'JBCLDATA-(*-CLDATA))                                        
*                                                                               
BUFF     DS    CL1000              BUFFER FOR PARSNIP CALL                      
*                                                                               
WORKX    EQU   *                                                                
         SPACE 2                                                                
* DSECT TO COVER COLUMN TABLE                                                   
*                                                                               
COLTABD  DSECT                                                                  
COLNAME  DS    CL8                 COLUMN NAME                                  
COLIND   DS    X                   COLUMN INDICATORS                            
COLIPER  EQU   X'80'               COLUMN IS A PERCENTAGE                       
COLMINL  DS    X                   COLUMN MINIMUM LENGTH                        
COLNUM   DS    XL2                 COLUMN DATA NUMBER                           
COLTABL  EQU   *-COLTABD           ENTRY LENGTH                                 
         SPACE 2                                                                
       ++INCLUDE ACJOBBERD                                                      
         SPACE 2                                                                
       ++INCLUDE DDPARSNIPD                                                     
         SPACE 2                                                                
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'018ACJOBCOL  07/23/13'                                      
         END                                                                    
