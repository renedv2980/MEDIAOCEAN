*          DATA SET DDEUREKA   AT LEVEL 002 AS OF 04/18/01                      
*CATALP EUREKA                                                                  
*                                                                               
* LSKI 003 28DEC00 NEW TEST CALL TO CHECK FOR MEMBER CURRENCIES                 
* LSKI 002 28DEC00 ADD GREEK DRACHMA TO MEMBER CURRENCIES                       
*                                                                               
         TITLE 'GET/APPLY CURRENCY EXCHANGE RATES'                              
EUREKA   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**EURK**,CLEAR=YES                                   
         USING WORKD,RC            RC=A(W/S)                                    
         LR    RA,R1               RA=A(PARAMETER LIST)                         
         USING EURKPRMD,RA                                                      
         L     R9,EURKPCBK         R9=A(CONTROL BLOCK)                          
         USING EURKBLKD,R9                                                      
*                                                                               
         MVI   RETCODE,PARMERRQ                                                 
         TM    EURKPTYP,GETQ+INVERTQ+APPLYQ+DERIVEQ+TESTQ                       
         BZ    EXIT                PARAMETER ERROR IF NO CALL TYPE SET          
*                                                                               
         MVI   RETCODE,OKQ                                                      
         TM    EURKPTYP,GETQ                                                    
         BNO   EURK100                                                          
         BAS   RE,GET                                                           
         CLI   RETCODE,OKQ                                                      
         BNE   EXIT                                                             
*                                                                               
EURK100  TM    EURKPTYP,INVERTQ                                                 
         BNO   EURK200                                                          
         BAS   RE,INVERT                                                        
         CLI   RETCODE,OKQ                                                      
         BNE   EXIT                                                             
*                                                                               
EURK200  TM    EURKPTYP,APPLYQ                                                  
         BNO   EURK300                                                          
         BAS   RE,APPLY                                                         
         CLI   RETCODE,OKQ                                                      
         BNE   EXIT                                                             
*                                                                               
EURK300  TM    EURKPTYP,DERIVEQ                                                 
         BNO   EURK400                                                          
         BAS   RE,DERIVE                                                        
*                                                                               
EURK400  TM    EURKPTYP,TESTQ                                                   
         BNO   EXIT                                                             
         BAS   RE,TEST                                                          
*                                                                               
EXIT     MVC   EURKPRET,RETCODE                                                 
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* GET AN EXCHANGE RATE RULE AND PUT ITS DETAILS IN CONTROL BLOCK      *         
***********************************************************************         
         SPACE                                                                  
GET      ST    RE,SAVERE                                                        
         MVI   EURKIND,0           CLEAR INDICATOR, RULE AND PERIOD             
         XC    EURKRULE,EURKRULE   FIELDS                                       
         XC    EURKPERD,EURKPERD                                                
         MVC   SAVCURF,EURKCUFR                                                 
         MVC   SAVCURT,EURKCUTO                                                 
         CLC   EURKCUFR,EURKCUTO                                                
         BNE   GET050                                                           
         MVC   EURKRLRT,ONETOONE   IF FROM-CURRENCY IS THE SAME AS TO-          
         MVC   EURKEND,EFFS        CURRENCY, RATE IS 1:1                        
         B     GETX                                                             
*                                                                               
GET050   ICM   R1,15,EURKAFAC                                                   
         USING COMFACSD,R1                                                      
         MVC   ADATAMGR,CDATAMGR                                                
         MVC   ASWITCH,CSWITCH                                                  
         DROP  R1                                                               
         MVI   ONOROFF,ONQ         CHECK ON OR OFFLINE                          
         ICM   RE,15,=V(UTL)                                                    
         BZ    GET100                                                           
         MVI   ONOROFF,OFFQ                                                     
*                                                                               
         MVC   SAVESE,4(RE)                                                     
         MVI   4(RE),SYSCNTLQ      SWITCH TO CONTROL SYSTEM                     
         MVI   SAVESYS,SYSMEDQ                                                  
         TM    EURKTYPE,ACCQ                                                    
         BNO   *+8                                                              
         MVI   SAVESYS,SYSACCQ                                                  
         B     GET200                                                           
*                                                                               
GET100   MVC   DMCB,EFFS                                                        
         GOTO1 ASWITCH,DMCB,,0     GET (TCB) (UTL)                              
         L     RE,0(R1)            RE=A(UTL)                                    
         MVC   SAVESYS,7(RE)                                                    
         MVC   SAVESE,4(RE)                                                     
*                                                                               
         MVC   DMCB,EFFS                                                        
         MVI   DMCB,SYSCNTLQ                                                    
         GOTO1 ASWITCH,DMCB,,0     SWITCH TO CONTROL SYSTEM                     
         CLI   4(R1),0                                                          
         BE    GET200                                                           
         CLI   4(R1),2             SWITCH TO NO-OP SYS                          
         BE    *+6                                                              
         DC    H'0'                INVALID SYSTEM                               
         MVI   RETCODE,RATENFQ                                                  
         B     GET800              RATE NOT FOUND IF SYSTEM NOT UP              
*                                                                               
GET200   LA    R3,INKEY                                                         
         USING GEXCD,R3            R3=A(KEY OF REQUIRED EXC RECORD)             
         XC    GEKEY,GEKEY                                                      
         MVI   GEKREC,GEKRECQ                                                   
         MVC   GEKSYS,SAVESYS                                                   
         MVC   GEKAGY,EURKALPH                                                  
         MVC   GEKCURF,EURKCUFR                                                 
         MVC   GEKCURT,EURKCUTO                                                 
         MVC   GEKPEND,EURKDATE                                                 
         MVI   FLAG,0                                                           
*                                                                               
         LA    RF,EUROTAB                                                       
         USING EUROTABD,RF                                                      
*                                                                               
GET300   CLI   EUROCURR,EOT        LOOK FOR MEMBER CURRENCIES IF DATE           
         BE    GET350              IS ON OR AFTER 01/01/1999                    
         CLC   EURKCUFR,EUROCURR                                                
         BNE   GET320                                                           
         CLC   EURKDATE,EURODATE                                                
         BL    GET320                                                           
         MVC   GEKCURF,EURO        FROM-CURRENCY IS EURO MEMBER, SO SET         
         MVC   SAVCURF,GEKCURF     FLAG AND CHANGE TO EUROS                     
         OI    FLAG,FROMEURQ                                                    
*                                                                               
GET320   CLC   EURKCUTO,EUROCURR                                                
         BNE   GET340                                                           
         CLC   EURKDATE,EURODATE                                                
         BL    GET340                                                           
         MVC   GEKCURT,EURO        TO-CURRENCY IS EURO MEMBER, SO SET           
         MVC   SAVCURT,GEKCURT     FLAG AND CHANGE TO EUROS                     
         OI    FLAG,TOEUROQ                                                     
*                                                                               
GET340   LA    RF,EUROTABL(RF)                                                  
         B     GET300                                                           
         DROP  RF                                                               
*                                                                               
GET350   CLC   GEKCURF,GEKCURT                                                  
         BNE   GET400                                                           
         MVC   EURKRLRT,ONETOONE   IF (ADJUSTED) FROM-CURRENCY IS THE           
         XC    EURKSTA,EURKSTA     SAME AS (ADJUSTED) TO-CURRENCY THEN          
         MVC   EURKEND,EFFS        RATE IS 1:1                                  
         B     GET700                                                           
*                                                                               
GET400   TM    EURKTYPE,FTONLYQ    NO FURTHER SET-UP REQUIRED FOR FT            
         BNO   GET410                                          RATES            
         LA    R1,ACCTREC                                                       
         BAS   RE,GETRATE                                                       
         BNE   GET800                                                           
         B     GET600                                                           
*                                                                               
GET410   CLI   GEKSYS,SYSMEDQ                                                   
         BE    GET500                                                           
         CLI   GEKSYS,SYSACCQ                                                   
         BE    GET450                                                           
         DC    H'0'                MUST BE MED OR ACC                           
*                                                                               
GET450   MVC   GEKACT,EFFS         SET UP FOR ACC                               
         OC    EURKACT,EURKACT                                                  
         BZ    *+10                                                             
         MVC   GEKACT,EURKACT                                                   
         LA    R1,ACCTREC                                                       
         BAS   RE,GETRATE                                                       
         BNE   GET800                                                           
         B     GET600                                                           
*                                                                               
GET500   MVC   GEKMED,EFFS         SET UP FOR MED                               
         MVC   GEKCLI,EFFS                                                      
         OC    EURKCLI,EURKCLI                                                  
         BZ    *+10                                                             
         MVC   GEKCLI,EURKCLI                                                   
         MVC   GEKPRO,EFFS                                                      
         MVC   GEKCAM,EFFS                                                      
*                                                                               
         TM    EURKTYPE,MEDBOOKQ                                                
         BO    GET510                                                           
         TM    EURKTYPE,MEDACCQ                                                 
         BO    GET520                                                           
         DC    H'0'                                                             
*                                                                               
GET510   MVI   GEKCTYP,C'B'                                                     
         LA    R1,BOOKREC                                                       
         BAS   RE,GETRATE                                                       
         BNE   GET800                                                           
         B     GET600                                                           
*                                                                               
GET520   MVI   GEKCTYP,C'A'                                                     
         LA    R1,ACCTREC                                                       
         BAS   RE,GETRATE                                                       
         BNE   GET800                                                           
         DROP  R3                                                               
*                                                                               
         USING GEXCD,R1                                                         
GET600   MVC   EURKRLST,GESTAT     PASS BACK DETAILS OF EXCHANGE RATE           
         MVC   EURKRLRT,GEXRATE    IN CONTROL BLOCK                             
         MVC   EURKRLSH,GEXSHFT                                                 
         MVC   EURKSTA,GEKPSTA                                                  
         MVC   EURKEND,GEKPEND                                                  
         CLI   GEKCTYP,GEKBOOQ                                                  
         BNE   *+8                                                              
         OI    EURKIND,RETMBKQ                                                  
         CLI   GEKCTYP,GEKACCQ                                                  
         BNE   *+8                                                              
         OI    EURKIND,RETMACCQ                                                 
         TM    GEXFLAG,GEXFTPL+GEXFTMI                                          
         BZ    *+8                                                              
         OI    EURKIND,RETADJQ                                                  
*                                                                               
         CLI   GEKSYS,SYSMEDQ                                                   
         BE    GET620                                                           
         CLI   GEKSYS,SYSACCQ                                                   
         BE    GET640                                                           
         B     GET700                                                           
*                                                                               
GET620   CLC   GEKCLI,EFFS                                                      
         BNE   *+8                                                              
         OI    EURKIND,RETALLQ                                                  
         B     GET700                                                           
*                                                                               
GET640   CLC   GEKACT,EFFS                                                      
         BNE   *+8                                                              
         OI    EURKIND,RETALLQ                                                  
         DROP  R1                                                               
*                                                                               
GET700   NI    FLAG,FROMEURQ+TOEUROQ                                            
         BZ    GET750                                                           
         OC    EURKRLST,FLAG       SET SPECIAL STATUS BITS IF REQUIRED          
*                                                                               
         LA    RF,EUROTAB                                                       
         USING EUROTABD,RF                                                      
*                                                                               
GET720   CLI   EUROCURR,EOT                                                     
         BE    GET800                                                           
         CLC   EURKCUFR,EUROCURR                                                
         BNE   GET730                                                           
         CLC   EURKSTA,EURODATE    FROM-CURRENCY IS A EURO MEMBER SO            
         BNL   *+10                AMEND START DATE IF NECESSARY                
         MVC   EURKSTA,EURODATE                                                 
*                                                                               
GET730   CLC   EURKCUTO,EUROCURR                                                
         BNE   GET740                                                           
         CLC   EURKSTA,EURODATE    TO-CURRENCY IS A EURO MEMBER SO              
         BNL   *+10                AMEND START DATE IF NECESSARY                
         MVC   EURKSTA,EURODATE                                                 
*                                                                               
GET740   LA    RF,EUROTABL(RF)                                                  
         B     GET720                                                           
         DROP  RF                                                               
*                                                                               
GET750   LA    RF,EUROTAB                                                       
         USING EUROTABD,RF                                                      
*                                                                               
GET770   CLI   EUROCURR,EOT                                                     
         BE    GET800                                                           
         CLC   EURKCUFR,EUROCURR                                                
         BNE   GET780                                                           
         CLC   EURKEND,EUROEND     FROM-CURRENCY IS A EURO MEMBER SO            
         BNH   *+10                AMEND END DATE IF NECESSARY                  
         MVC   EURKEND,EUROEND                                                  
*                                                                               
GET780   CLC   EURKCUTO,EUROCURR                                                
         BNE   GET790                                                           
         CLC   EURKEND,EUROEND     TO-CURRENCY IS A EURO MEMBER SO              
         BNH   *+10                AMEND END DATE IF NECESSARY                  
         MVC   EURKEND,EUROEND                                                  
*                                                                               
GET790   LA    RF,EUROTABL(RF)                                                  
         B     GET770                                                           
         DROP  RF                                                               
*                                                                               
GET800   CLI   RETCODE,OKQ                                                      
         BE    GET820                                                           
         MVC   EURKCUFR,SAVCURF    ON ERROR, PASS BACK CURRENCY CODES           
         MVC   EURKCUTO,SAVCURT    FOR RULE WE WERE LOOKING FOR                 
*                                                                               
GET820   CLI   ONOROFF,ONQ                                                      
         BE    GET850                                                           
*                                                                               
         L     RE,=V(UTL)          SWITCH BACK OFFLINE                          
         MVC   4(1,RE),SAVESE                                                   
         B     GETX                                                             
*                                                                               
GET850   MVC   DMCB(4),EFFS                                                     
         MVC   DMCB(1),SAVESE                                                   
         GOTO1 ASWITCH,DMCB,,0     SWITCH BACK TO SAVESE                        
         CLI   4(R1),0                                                          
         BE    GETX                                                             
         CLI   4(R1),2             SYSTEM GONE NO-OP                            
         BE    GETX                                                             
         DC    H'0'                CAN'T SWITCH BACK                            
*                                                                               
GETX     L     RE,SAVERE                                                        
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* INVERT THE EXCHANGE RATE RULE IN CONTROL BLOCK INCLUDING SWAPPING   *         
* FROM- AND TO-CURRENCY CODES (IF SPECIFIED)                          *         
***********************************************************************         
         SPACE                                                                  
INVERT   XC    EURKCUFR,EURKCUTO   SWAP FROM- AND TO-CURRENCY CODES             
         XC    EURKCUTO,EURKCUFR                                                
         XC    EURKCUFR,EURKCUTO                                                
*                                                                               
         TM    EURKPTYP,APPLYQ                                                  
         BNO   INVT200                                                          
         OC    EURKCUT2,EURKCUT2                                                
         BZ    INVT200                                                          
         MVI   RETCODE,PARMERRQ    INVALID TO HAVE SECOND CURRENCY IF           
         B     INVTX               INVERTING RATE BEFORE APPLYING               
*                                                                               
INVT200  OC    EURKRULE,EURKRULE   NOTHING FURTHER TO DO IF NO RULE IS          
         BZ    INVTX               PASSED                                       
*                                                                               
         XI    EURKRLST,DIVIDEQ    FLIP DIVIDE BIT                              
*                                                                               
         XR    RF,RF               INVERT SHIFT VALUE                           
         ICM   RF,8,EURKRLSH                                                    
         SRA   RF,24                                                            
         LCR   RF,RF                                                            
         STC   RF,EURKRLSH                                                      
*                                                                               
         TM    EURKRLST,FROMEURQ+TOEUROQ                                        
         BNM   INVTX               NOTHING TO DO IF BOTH BITS ARE OFF           
         TM    EURKRLST,FROMEURQ   OR IF BOTH BITS ARE ON                       
         BO    INVT500                                                          
*                                                                               
         NI    EURKRLST,X'FF'-TOEUROQ   SWITCH OFF 'TO EURO' BIT                
         OI    EURKRLST,FROMEURQ        SET 'FROM EURO' BIT INSTEAD             
         B     INVTX                                                            
*                                                                               
INVT500  NI    EURKRLST,X'FF'-FROMEURQ  SWITCH OFF 'FROM EURO' BIT              
         OI    EURKRLST,TOEUROQ         SET 'TO EURO' BIT INSTEAD               
*                                                                               
INVTX    BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* APPLY AN EXCHANGE RATE RULE IN CONTROL BLOCK TO INPUT AMOUNT        *         
***********************************************************************         
         SPACE                                                                  
APPLY    ST    RE,SAVERE                                                        
         MVI   ANYRULE,C'Y'                                                     
         ZAP   INPUT,ZERO                                                       
         L     RF,EURKPINA                                                      
         TM    EURKPINT,BINARYQ                                                 
         BNO   APLY050                                                          
*                                                                               
         ICM   R1,15,0(RF)         INPUT IS BINARY                              
         CVD   R1,DUB              CONVERT TO PACKED                            
         B     *+10                                                             
*                                                                               
APLY050  ZAP   DUB,0(8,RF)                                                      
         BZ    APLY600             ZERO VALUE PASSED - JUST PASS BACK           
         ZAP   INPUT,DUB                                                        
*                                                                               
         CLC   EURKCUFR,EURKCUTO   NO CONVERSION TO DO IF TO- AND               
         BE    APLY600             FROM-CURRENCIES ARE THE SAME                 
*                                                                               
         OC    EURKRULE,EURKRULE                                                
         BNZ   APLY200                                                          
         MVI   ANYRULE,C'N'        SET FLAG TO SAY NO RULE HAS BEEN             
         MVC   EURKRLRT,ONETOONE   PASSED AND SET UP DUMMY RULE                 
         CLC   EURKCUFR,EURO                                                    
         BE    *+8                                                              
         OI    EURKRLST,FROMEURQ   ASSUME CURRENCIES ARE EURO MEMBERS           
         CLC   EURKCUTO,EURO       IF NOT THE EURO                              
         BE    *+8                                                              
         OI    EURKRLST,TOEUROQ                                                 
*                                                                               
APLY200  MVC   RULE,EURKRULE                                                    
         TM    EURKRLST,FROMEURQ+TOEUROQ                                        
         BZ    APLY500             SIMPLE CONVERT IF NO BITS SET                
         TM    EURKRLST,FROMEURQ                                                
         BO    APLY300                                                          
*                                                                               
         CLI   ANYRULE,C'N'        ONLY ONE CONVERSION TO DO IF NO RULE         
         BE    APLY400                                        IS PASSED         
*                                                                               
         XR    R1,R1               USE PASSED RULE FOR FIRST CONVERSION         
         ICM   R1,8,RULESHFT       BUT ADD TWO TO SHIFT VALUE TO GET            
         SRA   R1,24               INTERMEDIATE VALUE TO FOUR DECIMAL           
         AH    R1,TWO              PLACES                                       
         STC   R1,RULESHFT                                                      
         BAS   RE,CONVERT                                                       
         B     APLY400                                                          
*                                                                               
APLY300  LA    RF,EUROTAB                                                       
         USING EUROTABD,RF                                                      
*                                                                               
APLY320  CLI   EUROCURR,EOT        FROM-CURRENCY IS EURO MEMBER, SO             
         BNE   APLY340             LOOK FOR CURRENCY CODE IN TABLE              
         MVI   RETCODE,PARMERRQ    PARAMETER ERROR IF NOT FOUND                 
         B     APLYX                                                            
*                                                                               
APLY340  CLC   EUROCURR,EURKCUFR                                                
         BNE   APLY380                                                          
         MVC   RULE,EURORULE       GET FROM-EURO RULE                           
         XI    RULESTAT,DIVIDEQ    REVERSE IT                                   
         XR    R1,R1                                                            
         ICM   R1,8,RULESHFT                                                    
         SRA   R1,24                                                            
         LCR   R1,R1                                                            
         TM    EURKRLST,TOEUROQ                                                 
         BO    APLY360                                                          
         CLI   ANYRULE,C'N'                                                     
         BNE   APLY360                                                          
         STC   R1,RULESHFT         IF FROM-EURO CONVERSION WITH NO RULE         
         B     APLY500             PASSED JUST DO SIMPLE CONVERSION             
*                                                                               
APLY360  AH    R1,TWO              ADD TWO TO SHIFT VALUE TO GET                
         STC   R1,RULESHFT         INTERMEDIATE VALUE TO FOUR DECIMAL           
         BAS   RE,CONVERT          PLACES                                       
         B     APLY400                                                          
*                                                                               
APLY380  LA    RF,EUROTABL(RF)                                                  
         B     APLY320                                                          
         DROP  RF                                                               
*                                                                               
APLY400  TM    EURKRLST,TOEUROQ                                                 
         BO    APLY410                                                          
*                                                                               
         MVC   RULE,EURKRULE                                                    
         XR    R1,R1               USE PASSED RULE FOR SECOND                   
         ICM   R1,8,RULESHFT       CONVERSION BUT CORRECT SHIFT VALUE           
         SRA   R1,24               TO GET OUTPUT TO TWO DECIMAL PLACES          
         SH    R1,TWO                                                           
         STC   R1,RULESHFT                                                      
         B     APLY500                                                          
*                                                                               
APLY410  LA    RF,EUROTAB                                                       
         USING EUROTABD,RF                                                      
*                                                                               
APLY420  CLI   EUROCURR,EOT        TO-CURRENCY IS EURO MEMBER, SO LOOK          
         BNE   APLY440             FOR CURRENCY CODE IN TABLE                   
         MVI   RETCODE,PARMERRQ    PARAMETER ERROR IF NOT FOUND                 
         B     APLYX                                                            
*                                                                               
APLY440  CLC   EUROCURR,EURKCUTO                                                
         BNE   APLY480                                                          
         MVC   RULE,EURORULE       GET FROM-EURO RULE                           
         XR    R1,R1                                                            
         ICM   R1,8,RULESHFT                                                    
         SRA   R1,24               CORRECT SHIFT VALUE TO GET OUTPUT            
         TM    EURKRLST,FROMEURQ   TO TWO DECIMAL PLACES...                     
         BO    *+12                                                             
         CLI   ANYRULE,C'N'        ...UNLESS WE DID NOT DO EARLIER              
         BE    *+8                                      CONVERSION              
         SH    R1,TWO                                                           
         STC   R1,RULESHFT                                                      
         B     APLY500                                                          
*                                                                               
APLY480  LA    RF,EUROTABL(RF)                                                  
         B     APLY420                                                          
         DROP  RF                                                               
*                                                                               
APLY500  BAS   RE,CONVERT                                                       
*                                                                               
APLY600  L     RF,EURKPOUA                                                      
         XR    RE,RE                                                            
         ICM   RE,8,EURKADJ        ANY BINARY ADJUSTMENT?                       
         BZ    APLY620                                                          
         SRA   RE,24                                                            
         CVB   R1,DUB                                                           
         AR    R1,RE               ADD ADJUSTMENT TO OUTPUT VALUE               
         CVD   R1,DUB                                                           
*                                                                               
APLY620  TM    EURKPOUT,BINARYQ                                                 
         BNO   APLY650                                                          
*                                                                               
         CVB   R1,DUB              BINARY OUTPUT REQUIRED - CONVERT             
         STCM  R1,15,0(RF)                                                      
         B     *+10                                                             
*                                                                               
APLY650  ZAP   0(8,RF),DUB                                                      
*                                                                               
         OC    EURKCUT2,EURKCUT2                                                
         BZ    APLYX                                                            
         CP    DUB,ZERO                                                         
         BE    APLY905                                                          
         CLC   EURKCUT2,EURO                                                    
         BE    APLY800                                                          
         CLC   EURKCUTO,EURO       GET SECOND TO-CURRENCY FROM TO-              
         BE    APLY680             CURRENCY OR FROM FROM-CURRENCY IF            
         CLC   EURKCUFR,EURO       EITHER ARE THE EURO (ASSUME SECOND           
         BE    APLY670             TO-CURRENCY IS A MEMBER CURRENCY)            
         MVI   RETCODE,PARMERRQ                                                 
         B     APLYX                                                            
*                                                                               
APLY670  ZAP   DUB,INPUT                                                        
*                                                                               
APLY680  LA    RF,EUROTAB                                                       
         USING EUROTABD,RF                                                      
*                                                                               
APLY700  CLI   EUROCURR,EOT        SECOND TO-CURRENCY IS EURO MEMBER,           
         BNE   APLY740             SO LOOK FOR CURRENCY CODE IN TABLE           
         MVI   RETCODE,PARMERRQ    PARAMETER ERROR IF NOT FOUND                 
         B     APLYX                                                            
*                                                                               
APLY740  CLC   EUROCURR,EURKCUT2                                                
         BNE   APLY780                                                          
         MVC   RULE,EURORULE       GET FROM-EURO RULE                           
         B     APLY900                                                          
*                                                                               
APLY780  LA    RF,EUROTABL(RF)                                                  
         B     APLY700                                                          
         DROP  RF                                                               
*                                                                               
APLY800  CLC   EURKCUFR,EURO       SECOND TO-CURRENCY IS THE EURO, SO           
         BNE   APLY805             SEE IF BOTH TO- AND FROM-CURRENCY            
         CLC   EURKCUTO,EURO       ARE THE EURO                                 
         BE    APLY905                                                          
*                                                                               
APLY805  LA    RE,EURKCUTO         SEE IF EITHER TO- OR FROM-CURRENCY           
         LA    R0,2                IS A MEMBER                                  
*                                                                               
APLY810  LA    RF,EUROTAB                                                       
         USING EUROTABD,RF                                                      
*                                                                               
APLY820  CLI   EUROCURR,EOT        LOOK FOR CURRENCY IN TABLE OF                
         BNE   APLY840             MEMBERS                                      
         LA    RE,EURKCUFR         LOOK FOR FROM-CURRENCY IF TO-                
         ZAP   DUB,INPUT           CURRENCY IS NOT FOUND                        
         BCT   R0,APLY810                                                       
         MVI   RETCODE,PARMERRQ    PARAMETER ERROR IF NEITHER IS FOUND          
         B     APLYX                                                            
*                                                                               
APLY840  CLC   EUROCURR,0(RE)                                                   
         BNE   APLY880                                                          
         MVC   RULE,EURORULE       GET FROM-EURO RULE                           
         XI    RULESTAT,DIVIDEQ    REVERSE IT                                   
         XR    R1,R1                                                            
         ICM   R1,8,RULESHFT                                                    
         SRA   R1,24                                                            
         LCR   R1,R1                                                            
         STC   R1,RULESHFT                                                      
         B     APLY900                                                          
*                                                                               
APLY880  LA    RF,EUROTABL(RF)                                                  
         B     APLY820                                                          
         DROP  RF                                                               
*                                                                               
APLY900  BAS   RE,CONVERT                                                       
*                                                                               
APLY905  L     RF,EURKPO2A                                                      
         TM    EURKPO2T,ADJUSTQ                                                 
         BNO   APLY950                                                          
         CLC   EURKCUFR,EURKCUT2   SECOND TO-CURRENCY MUST BE THE SAME          
         BE    APLY910             AS FROM-CURRENCY FOR ADJUSTMENT              
         MVI   RETCODE,PARMERRQ    PARAMETER ERROR IF NOT                       
         B     APLYX                                                            
*                                                                               
APLY910  ZAP   PL8,DUB                                                          
         ZAP   DUB,INPUT                                                        
         SP    DUB,PL8                                                          
         CVB   R1,DUB                                                           
         STC   R1,0(RF)                                                         
         B     APLYX                                                            
*                                                                               
APLY950  TM    EURKPO2T,BINARYQ                                                 
         BNO   APLY970                                                          
*                                                                               
         CVB   R1,DUB              BINARY OUTPUT REQUIRED - CONVERT             
         STCM  R1,15,0(RF)                                                      
         B     *+10                                                             
*                                                                               
APLY970  ZAP   0(8,RF),DUB                                                      
*                                                                               
APLYX    CLI   ANYRULE,C'N'                                                     
         BNE   *+10                                                             
         XC    EURKRULE,EURKRULE   REMOVE ANY DUMMY RULE                        
         L     RE,SAVERE                                                        
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* DERIVE AN EXCHANGE RATE RULE FROM THE GIVEN INPUT AND OUTPUT        *         
* AMOUNTS AND PASS IT BACK IN THE CONTROL BLOCK                       *         
***********************************************************************         
         SPACE                                                                  
DERIVE   ST    RE,SAVERE                                                        
         L     RF,EURKPINA                                                      
         TM    EURKPINT,BINARYQ                                                 
         BNO   DRIV200                                                          
*                                                                               
         ICM   R1,15,0(RF)         INPUT IS BINARY                              
         CVD   R1,DUB              CONVERT TO PACKED                            
         B     *+10                                                             
*                                                                               
DRIV200  ZAP   DUB,0(8,RF)                                                      
         BZ    DRIV900             INPUT IS ZERO - PARAMETER ERROR              
*                                                                               
         ZAP   PL8,DUB                                                          
         L     RF,EURKPOUA                                                      
         TM    EURKPOUT,BINARYQ                                                 
         BNO   DRIV300                                                          
*                                                                               
         ICM   R1,15,0(RF)         OUTPUT IS BINARY                             
         CVD   R1,DUB              CONVERT TO PACKED                            
         B     *+10                                                             
*                                                                               
DRIV300  ZAP   DUB,0(8,RF)                                                      
         BZ    DRIV900             OUTPUT IS ZERO - PARAMETER ERROR             
*                                                                               
         XC    RULE,RULE                                                        
         MVC   RULERATE,ONETOONE                                                
*                                                                               
         MVC   RULESHFT,EURKPOUT   DERIVE SHIFT VALUE IF ANY                    
         NI    RULESHFT,X'03'                                                   
         XR    RF,RF                                                            
         IC    RF,RULESHFT         GET DECIMAL PLACES OF OUTPUT                 
         MVC   RULESHFT,EURKPINT                                                
         NI    RULESHFT,X'03'                                                   
         XR    RE,RE                                                            
         IC    RE,RULESHFT         GET DECIMAL PLACES OF INPUT                  
         MVI   RULESHFT,0                                                       
         SR    RF,RE                                                            
         BZ    DRIV400             NO SHIFT VALUE REQUIRED                      
         STC   RF,RULESHFT                                                      
         BM    DRIV350                                                          
         SRP   PL8,0(RF),0         POSITIVE SHIFT - ADJUST INPUT                
         B     DRIV400                                                          
*                                                                               
DRIV350  LCR   RF,RF                                                            
         SRP   DUB,0(RF),0         NEGATIVE SHIFT - ADJUST OUTPUT               
*                                                                               
DRIV400  LA    RE,X'20'            SET NOP TO BH                                
         CP    DUB,ZERO                                                         
         BH    *+8                                                              
         LA    RE,X'40'            SET NOP TO BL                                
         ZAP   PL16,ONE                                                         
         CP    PL8,DUB                                                          
         EX    RE,*+4                                                           
         NOP   DRIV500                                                          
         BE    DRIV800             1:1 RULE AS INPUT = OUTPUT                   
*                                                                               
         MP    PL16,DUB                                                         
         ZAP   DUB,PL8             INPUT < OUTPUT SO OK                         
         B     DRIV600                                                          
*                                                                               
DRIV500  MVI   RULESTAT,DIVIDEQ    INPUT > OUTPUT SO INVERT THE RULE TO         
         MP    PL16,PL8            INCREASE PRECISION                           
*                                                                               
DRIV600  SRP   PL16,1,0                                                         
         DP    PL16,DUB                                                         
         ZAP   DUB,PL16(8)                                                      
         SRP   DUB,63,5                                                         
         SRP   DUB,1,0             PWOS NOW IN DUB+2 TO DUB+6                   
         CP    DUB,ZERO                                                         
         BL    DRIV900             NEGATIVE RATE - PARAMETER ERROR              
         OC    DUB(2),DUB                                                       
         BNZ   DRIV900             RATE TOO BIG - PARAMETER ERROR               
         MVC   RULERATE,DUB+2                                                   
*                                                                               
DRIV800  MVC   EURKRULE,RULE                                                    
         B     *+8                                                              
*                                                                               
DRIV900  MVI   RETCODE,PARMERRQ                                                 
*                                                                               
DRIVX    L     RE,SAVERE                                                        
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* TEST TO SEE IF PASSED CURRENCY IS A EURO MEMBER                     *         
***********************************************************************         
         SPACE                                                                  
TEST     MVI   RETCODE,NONMEMBQ                                                 
         LA    RF,EUROTAB          LOOK FOR CURRENCY IN TABLE                   
         USING EUROTABD,RF                                                      
*                                                                               
TEST200  CLI   EUROCURR,EOT                                                     
         BE    TESTX               CURRENCY NOT FOUND, SO NOT A MEMBER          
         CLC   EURKCUFR,EUROCURR                                                
         BNE   TEST500                                                          
         OC    EURKDATE,EURKDATE                                                
         BZ    TEST400                                                          
         CLC   EURKDATE,EURODATE   CHECK DATE RANGE IF REQUIRED                 
         BL    TESTX                                                            
*                                                                               
TEST400  MVI   RETCODE,OKQ                                                      
         B     TESTX                                                            
*                                                                               
TEST500  LA    RF,EUROTABL(RF)                                                  
         B     TEST200                                                          
         DROP  RF                                                               
*                                                                               
TESTX    BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* GET A GENFILE EXCHANGE RATE RECORD INTO I/O AREA SUPPLIED           *         
* ON ENTRY: R1 = A(I/O AREA)                                          *         
*           R3 = A(INPUT KEY)                                         *         
* ON EXIT : CC = NEQ IF NOT FOUND OR ERROR                            *         
***********************************************************************         
         SPACE                                                                  
         USING GEXCD,R3                                                         
GETRATE  NTR1                                                                   
         MVC   KEY(GEKEYL),0(R3)   SET INPUT KEY                                
         MVC   KEYSAVE,KEY         SAVE INPUT KEY                               
         MVI   LEVEL,1             DET DEF LEVEL 1                              
         LR    R3,R1               POINT TO CALLERS AREA FOR I/O                
         TM    EURKTYPE,FTONLYQ                                                 
         BO    GTRT500                                                          
*                                                                               
GTRT100  GOTO1 ADATAMGR,DMCB,(0,READHI),GENDIR,KEY,GEKEY                        
         MVC   DMERROR,8(R1)                                                    
         TM    DMERROR,X'7F'       TEST FOR DATAMGR ERRORS                      
         BNZ   GTRTX                                                            
         TM    DMERROR,X'80'       TEST E-O-F                                   
         BNZ   GTRT200                                                          
*                                  TEST RECORD FOUND                            
         CLC   KEY(GEKPEND-GEKEY),GEKEY                                         
         BNE   GTRT200                                                          
*                                                                               
         CLC   KEY+(GEKPEND-GEKEY)(L'GEKPEND),GEKPSTA                           
         BNL   GTRT600             DATE IS WITHIN PERIOD                        
*                                                                               
GTRT200  CLI   SAVESYS,SYSMEDQ     WHICH SYSTEM                                 
         BE    GTRT300                                                          
         CLI   SAVESYS,SYSACCQ                                                  
         BE    GTRT400                                                          
         DC    H'0'                                                             
*                                                                               
GTRT300  CLI   LEVEL,1             LEVEL 1 NOT FOUND                            
         BH    GTRT320             SET LEVEL 2 - ALL FROM-CURRENCIES            
         MVC   KEY,KEYSAVE                                                      
         MVC   KEY+(GEKCURF-GEKEY)(3),EFFS                                      
         MVI   LEVEL,2                                                          
         B     GTRT100                                                          
*                                                                               
GTRT320  CLI   LEVEL,2             LEVEL 2 NOT FOUND                            
         BH    GTRT340             SET LEVEL 3 - ALL CLIENTS                    
         MVC   KEY,KEYSAVE                                                      
         MVC   KEY+(GEKCLI-GEKEY)(L'GEKCLI),EFFS                                
         MVI   LEVEL,3                                                          
         B     GTRT100                                                          
*                                                                               
GTRT340  CLI   LEVEL,3             LEVEL 3 NOT FOUND                            
         BH    GTRT360             SET LEVEL 4 - ALL FROM-CURRENCIES            
         MVC   KEY,KEYSAVE         AND ALL CLIENTS                              
         MVC   KEY+(GEKCURF-GEKEY)(3),EFFS                                      
         MVC   KEY+(GEKCLI-GEKEY)(L'GEKCLI),EFFS                                
         MVI   LEVEL,4                                                          
         B     GTRT100                                                          
*                                                                               
GTRT360  CLI   KEYSAVE+(GEKCTYP-GEKEY),GEKACCQ                                  
         BNE   GTRT380                                                          
         MVI   KEYSAVE+(GEKCTYP-GEKEY),GEKBOOQ                                  
         MVC   KEY,KEYSAVE         ACCOUNTING TYPE NOT FOUND                    
         MVI   LEVEL,1             SET TYPE TO BOOKING AND REPEAT               
         B     GTRT100             SEARCH                                       
*                                                                               
GTRT380  TM    EURKTYPE,ALLOWFTQ   FT RATES ALLOWED?                            
         BO    GTRT500             YES - GO LOOK FOR ONE                        
         MVI   DMERROR,X'10'       NO - SET RECORD NOT FOUND                    
         B     GTRTX                                                            
*                                                                               
GTRT400  CLI   LEVEL,1             LEVEL 1 NOT FOUND                            
         BH    GTRT420             SET LEVEL 2 - ALL FROM-CURRENCIES            
         MVC   KEY,KEYSAVE                                                      
         MVC   KEY+(GEKCURF-GEKEY)(3),EFFS                                      
         MVI   LEVEL,2                                                          
         B     GTRT100                                                          
*                                                                               
GTRT420  CLI   LEVEL,2             LEVEL 2 NOT FOUND                            
         BH    GTRT440             SET LEVEL 3 - ALL CLIENTS                    
         MVC   KEY,KEYSAVE                                                      
         MVC   KEY+(GEKACT-GEKEY)(L'GEKACT),EFFS                                
         MVI   LEVEL,3                                                          
         B     GTRT100                                                          
*                                                                               
GTRT440  CLI   LEVEL,3             LEVEL 3 NOT FOUND                            
         BH    GTRT460             SET LEVEL 4 - ALL FROM-CURRENCIES            
         MVC   KEY,KEYSAVE         AND ALL CLIENTS                              
         MVC   KEY+(GEKCURF-GEKEY)(3),EFFS                                      
         MVC   KEY+(GEKACT-GEKEY)(L'GEKACT),EFFS                                
         MVI   LEVEL,4                                                          
         B     GTRT100                                                          
*                                                                               
GTRT460  TM    EURKTYPE,ALLOWFTQ   FT RATES ALLOWED?                            
         BO    GTRT500             YES - GO LOOK FOR ONE                        
         MVI   DMERROR,X'10'       NO - SET RECORD NOT FOUND                    
         B     GTRTX                                                            
*                                                                               
GTRT500  OI    FLAG,FTRATEQ        SET FT RATES USED AND LOOK FOR FT            
         MVC   KEY,KEYSAVE         RATES                                        
         XC    KEY+GEKAGY-GEKEY(L'GEKAGY),KEY+GEKAGY-GEKEY                      
         MVI   KEY+GEKSYS-GEKEY,X'FF'                                           
         MVI   KEY+GEKCTYP-GEKEY,C'C'                                           
         XC    KEY+GEKKEY-GEKEY(L'GEKKEY),KEY+GEKKEY-GEKEY                      
*                                                                               
         TM    EURKTYPE,SWAPQ      REVERSE RATE REQUIRED                        
         BNO   GTRT550                                                          
         XC    KEY+GEKCURF-GEKEY(L'GEKCURF),KEY+GEKCURT-GEKEY                   
         XC    KEY+GEKCURT-GEKEY(L'GEKCURT),KEY+GEKCURF-GEKEY                   
         XC    KEY+GEKCURF-GEKEY(L'GEKCURF),KEY+GEKCURT-GEKEY                   
*                                                                               
GTRT550  GOTO1 ADATAMGR,DMCB,(0,READHI),GENDIR,KEY,GEKEY                        
*                                                                               
         TM    EURKTYPE,SWAPQ      REVERSE RATE REQUIRED                        
         BNO   GTRT570                                                          
         XI    GEDSTAT,GEINVRT     FLIP DIVIDE BIT                              
*                                                                               
GTRT570  MVC   DMERROR,8(R1)                                                    
         TM    DMERROR,X'7F'       TEST FOR DATAMGR ERRORS                      
         BNZ   GTRTX                                                            
         TM    DMERROR,X'80'       TEST E-O-F                                   
         BZ    *+12                                                             
         MVI   DMERROR,X'10'       SET RECORD NOT FOUND                         
         B     GTRTX                                                            
         CLC   KEY(GEKPEND-GEKEY),GEKEY                                         
         BE    *+12                                                             
         MVI   DMERROR,X'10'                                                    
         B     GTRTX                                                            
         CLC   KEY+(GEKPEND-GEKEY)(L'GEKPEND),GEKPSTA                           
         BNL   GTRT600             DATE IS WITHIN PERIOD                        
         MVI   DMERROR,X'10'                                                    
         B     GTRTX                                                            
*                                                                               
GTRT600  MVC   DDA,GEDDA                                                        
         GOTO1 ADATAMGR,DMCB,(0,GETREC),GENFIL,DDA,GEKEY,DMWORK                 
         TM    FLAG,FTRATEQ        TEST FOR FT RATES USED                       
         BNO   GTRT620                                                          
         OI    GESTAT,GEFIXFT      SET FIXED RATE                               
         TM    EURKTYPE,SWAPQ      REVERSE RATE REQUIRED                        
         BNO   GTRT620                                                          
*                                                                               
         XI    GESTAT,GEINVRT      FLIP DIVIDE BIT                              
         XR    RF,RF               INVERT SHIFT VALUE                           
         ICM   RF,8,GEXSHFT                                                     
         SRA   RF,24                                                            
         LCR   RF,RF                                                            
         STC   RF,GEXSHFT                                                       
*                                                                               
GTRT620  CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         TM    GEXFLAG,GEXFTPL+GEXFTMI                                          
         BZ    GTRTX               EXIT IF NO ADJUSTMENT NEEDED                 
*                                                                               
         TM    EURKTYPE,ALLOWFTQ   FT RATES ALLOWED?                            
         BNZ   *+12                YES - GET FT RATE FOR ADJUSTMENT             
         MVI   DMERROR,X'10'       NO - SET RECORD NOT FOUND                    
         B     GTRTX                                                            
*                                                                               
         MVC   RATWK1,GEXRATE      HOLD ADJUSTMENT IN RATWK1                    
*                                                                               
         ST    R3,AREC             SAVE A(RECORD)                               
         LA    R3,WORKREC                                                       
         OI    FLAG,FTRATEQ        SET FT RATES USED                            
         MVC   KEY,KEYSAVE                                                      
         XC    KEY+GEKAGY-GEKEY(L'GEKAGY),KEY+GEKAGY-GEKEY                      
         MVI   KEY+GEKSYS-GEKEY,X'FF'                                           
         MVI   KEY+GEKCTYP-GEKEY,C'C'                                           
         XC    KEY+GEKKEY-GEKEY(L'GEKKEY),KEY+GEKKEY-GEKEY                      
*                                                                               
         TM    EURKTYPE,SWAPQ      REVERSE RATE REQUIRED                        
         BNO   GTRT650                                                          
         XC    KEY+GEKCURF-GEKEY(L'GEKCURF),KEY+GEKCURT-GEKEY                   
         XC    KEY+GEKCURT-GEKEY(L'GEKCURT),KEY+GEKCURF-GEKEY                   
         XC    KEY+GEKCURF-GEKEY(L'GEKCURF),KEY+GEKCURT-GEKEY                   
*                                                                               
GTRT650  GOTO1 ADATAMGR,DMCB,(0,READHI),GENDIR,KEY,GEKEY                        
*                                                                               
         TM    EURKTYPE,SWAPQ      REVERSE RATE REQUIRED                        
         BNO   GTRT670                                                          
         XI    GEDSTAT,GEINVRT     FLIP DIVIDE BIT                              
*                                                                               
GTRT670  MVC   DMERROR,8(R1)                                                    
         TM    DMERROR,X'7F'       TEST FOR DATAMGR ERRORS                      
         BNZ   GTRTX                                                            
         TM    DMERROR,X'90'       TEST E-O-F                                   
         BZ    *+12                                                             
         MVI   DMERROR,X'10'       SET RECORD NOT FOUND                         
         B     GTRTX                                                            
         CLC   KEY(GEKPEND-GEKEY),GEKEY                                         
         BE    *+12                                                             
         MVI   DMERROR,X'10'                                                    
         B     GTRTX                                                            
         CLC   KEY+(GEKPEND-GEKEY)(L'GEKPEND),GEKPSTA                           
         BNL   GTRT700             DATE IS WITHIN PERIOD                        
         MVI   DMERROR,X'10'                                                    
         B     GTRTX                                                            
*                                                                               
GTRT700  MVC   DDA,GEDDA                                                        
         GOTO1 ADATAMGR,DMCB,(0,GETREC),GENFIL,DDA,GEKEY,DMWORK                 
         OI    GESTAT,GEFIXFT                                                   
*                                                                               
         TM    EURKTYPE,SWAPQ      REVERSE RATE REQUIRED                        
         BNO   GTRT720                                                          
         XI    GESTAT,GEINVRT      FLIP DIVIDE BIT                              
         XR    RF,RF               INVERT SHIFT VALUE                           
         ICM   RF,8,GEXSHFT                                                     
         SRA   RF,24                                                            
         LCR   RF,RF                                                            
         STC   RF,GEXSHFT                                                       
*                                                                               
GTRT720  CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   RATWK2,GEXRATE      HOLD FT RATE IN RATWK2                       
*                                                                               
         L     R3,AREC             RESTORE A(RECORD)                            
         MVC   GESTAT,WORKREC+GESTAT-GEXCD   RESTORE STATUS                     
         MVC   GEXSHFT,WORKREC+GEXSHFT-GEXCD RESTORE SHIFT                      
*                                                                               
         MVI   RATWK1+5,X'0C'                                                   
         MVI   RATWK2+5,X'0C'                                                   
         TM    GEXFLAG,GEXFTPC                                                  
         BNO   GTRT750                                                          
*                                                                               
         XC    RATWK3,RATWK3       ADJUSTMENT IS PERCENTAGE OF FT RATE          
         MVC   RATWK3+7(5),RATWK1  SO RATWK3 IS RATWK1 CONVERTED TO A           
         NI    RATWK3+11,X'F0'     PERCENTAGE                                   
         OI    RATWK3+11,X'0C'                                                  
*                                                                               
         MP    RATWK3,RATWK2       MULTIPLY RATWK2 BY THE PERCENTAGE IN         
         NI    RATWK3+8,X'F0'      RATWK3 AND MOVE BACK TO RATWK1               
         OI    RATWK3+8,X'0C'                                                   
         MVC   RATWK1,RATWK3+3                                                  
*                                                                               
GTRT750  TM    GEXFLAG,GEXFTPL                                                  
         BNO   GTRT760                                                          
         AP    RATWK2,RATWK1       ADJUSTMENT IS FT+                            
         B     GTRT770                                                          
GTRT760  SP    RATWK2,RATWK1       ADJUSTMENT IS FT-                            
GTRT770  MVC   GEXRATE,RATWK2                                                   
*                                                                               
GTRTX    CLI   DMERROR,0                                                        
         BE    *+8                                                              
         MVI   RETCODE,RATENFQ                                                  
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* CONVERT AMOUNT USING EXCHANGE RATE RULE SUPPLIED                    *         
* ON ENTRY: DUB  = PACKED AMOUNT TO BE CONVERTED                      *         
*           RULE = EXCHANGE RATE RULE                                 *         
* ON EXIT : DUB  = PACKED CONVERTED AMOUNT                            *         
***********************************************************************         
         SPACE                                                                  
CONVERT  ZAP   PL16,DUB                                                         
         ZAP   PL8,ZERO                                                         
         MVO   PL8,RULERATE                                                     
         LA    RF,DUB                                                           
*                                                                               
         TM    RULESTAT,DIVIDEQ    TRANSPOSE MULTIPLIER AND DIVISOR IF          
         BO    CNVT200             RULE SAYS DON'T DIVIDE BY RATE               
*                                                                               
         ZAP   DUB,PL8                                                          
         LA    RF,PL8                                                           
*                                                                               
CNVT200  ZAP   0(8,RF),ONE         HANDLE PRECISION (1/10/100/1000-N)           
         TM    RULESTAT,TENQ+HUNDREDQ+THOUSNDQ                                  
         BZ    CNVT500                                                          
*                                                                               
         SRP   0(8,RF),1,0         MULTIPLY BY 10                               
         TM    RULESTAT,TENQ       IF PRECISION IN 10'S WE ARE DONE             
         BO    CNVT500                                                          
*                                                                               
         SRP   0(8,RF),1,0         MULTIPLY BY 10 AGAIN                         
         TM    RULESTAT,HUNDREDQ   IF PRECISION IN 100'S WE ARE DONE            
         BO    CNVT500                                                          
*                                                                               
         SRP   0(8,RF),1,0         PRECISION MUST BE IN 1000'S                  
*                                                                               
CNVT500  XR    RF,RF                                                            
         IC    RF,RULESHFT                                                      
         TM    RULESHFT,X'80'                                                   
         BO    *+10                IF SHIFT VALUE POSITIVE (MULTIPLY)           
         SRP   PL16,0(RF),5        DO SHIFT NOW                                 
*                                                                               
         MP    PL16,DUB                                                         
         SRP   PL16,1,0                                                         
         DP    PL16,PL8                                                         
         SRP   PL16(8),63,5                                                     
*                                                                               
         TM    RULESHFT,X'80'                                                   
         BNO   *+10                IF SHIFT VALUE NEGATIVE (DIVIDE) DO          
         SRP   PL16(8),0(RF),5     SHIFT NOW                                    
*                                                                               
         ZAP   DUB,PL16(8)                                                      
*                                                                               
CNVTX    BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTANTS                                              *         
***********************************************************************         
         SPACE                                                                  
         LTORG                                                                  
         SPACE                                                                  
TWO      DC    Y(2)                                                             
EURO     DC    C'EUR'                                                           
ONETOONE DC    X'0000100000'                                                    
EFFS     DC    X'FFFFFFFFFFFFFFFF'                                              
READHI   DC    C'DMRDHI  '                                                      
GETREC   DC    C'GETREC  '                                                      
GENDIR   DC    C'GENDIR  '                                                      
GENFIL   DC    C'GENFIL  '                                                      
ZERO     DC    P'0'                                                             
ONE      DC    P'100000'           ONE - TO FIVE DECIMAL PLACES                 
*                                                                               
ONQ      EQU   X'00'               ONLINE                                       
OFFQ     EQU   X'FF'               OFFLINE                                      
*                                                                               
SYSMEDQ  EQU   X'04'               MEDIA SYSTEM                                 
SYSACCQ  EQU   X'06'               ACCOUNTING SYSTEM                            
SYSCNTLQ EQU   X'0A'               CONTROL SYSTEM                               
*                                                                               
EUROTAB  DC    C'IEP',AL1(0),X'0000787564',AL1(-1)                              
         DC    AL.7(99),AL.4(1),AL.5(1),AL.7(98),AL.4(12),AL.5(31)              
         DC    C'PTE',AL1(0),X'0020048200',AL1(-2)                              
         DC    AL.7(99),AL.4(1),AL.5(1),AL.7(98),AL.4(12),AL.5(31)              
         DC    C'FIM',AL1(0),X'0000594573',AL1(0)                               
         DC    AL.7(99),AL.4(1),AL.5(1),AL.7(98),AL.4(12),AL.5(31)              
         DC    C'ESP',AL1(0),X'0016638600',AL1(-2)                              
         DC    AL.7(99),AL.4(1),AL.5(1),AL.7(98),AL.4(12),AL.5(31)              
         DC    C'ITL',AL1(0),X'0193627000',AL1(-2)                              
         DC    AL.7(99),AL.4(1),AL.5(1),AL.7(98),AL.4(12),AL.5(31)              
         DC    C'NLG',AL1(0),X'0000220371',AL1(0)                               
         DC    AL.7(99),AL.4(1),AL.5(1),AL.7(98),AL.4(12),AL.5(31)              
         DC    C'DEM',AL1(0),X'0000195583',AL1(0)                               
         DC    AL.7(99),AL.4(1),AL.5(1),AL.7(98),AL.4(12),AL.5(31)              
         DC    C'BEF',AL1(0),X'0004033990',AL1(-2)                              
         DC    AL.7(99),AL.4(1),AL.5(1),AL.7(98),AL.4(12),AL.5(31)              
         DC    C'LUF',AL1(0),X'0004033990',AL1(-2)                              
         DC    AL.7(99),AL.4(1),AL.5(1),AL.7(98),AL.4(12),AL.5(31)              
         DC    C'ATS',AL1(0),X'0001376030',AL1(0)                               
         DC    AL.7(99),AL.4(1),AL.5(1),AL.7(98),AL.4(12),AL.5(31)              
         DC    C'FRF',AL1(0),X'0000655957',AL1(0)                               
         DC    AL.7(99),AL.4(1),AL.5(1),AL.7(98),AL.4(12),AL.5(31)              
         DC    C'GRD',AL1(0),X'0034075000',AL1(-2)                              
         DC    AL.7(101),AL.4(1),AL.5(1),AL.7(100),AL.4(12),AL.5(31)            
EUROTABX DC    AL1(EOT)                                                         
         EJECT                                                                  
***********************************************************************         
* DSECTS                                                              *         
***********************************************************************         
         SPACE                                                                  
EUROTABD DSECT                                                                  
EUROCURR DS    CL3                 CURRENCY CODE                                
EURORULE DS    0CL7                FIXED RULE FROM EURO TO CURRENCY             
EUROSTAT DS    XL1                 STATUS                                       
EURORATE DS    XL5                 CONVERSION RATE                              
EUROSHFT DS    XL1                 SHIFT VALUE FOR DEC PLACE DIFFERENCE         
EURODATE DS    XL2                 DATE OF EURO MEMBERSHIP                      
EUROEND  DS    XL2                 LAST DATE BEFORE EURO MEMBERSHIP             
EUROTABL EQU   *-EUROTABD                                                       
EOT      EQU   X'FF'                                                            
         SPACE                                                                  
WORKD    DSECT                                                                  
DUB      DS    D                                                                
DMWORK   DS    12D                                                              
SAVERE   DS    F                                                                
ACOMFACS DS    A                                                                
ASWITCH  DS    A                                                                
ADATAMGR DS    A                                                                
AREC     DS    A                                                                
DDA      DS    F                                                                
DMCB     DS    6F                                                               
DMERROR  DS    X                                                                
RETCODE  DS    X                                                                
ONOROFF  DS    X                                                                
SAVESYS  DS    X                                                                
SAVESE   DS    X                                                                
FLAG     DS    X                                                                
LEVEL    DS    X                                                                
ANYRULE  DS    C                                                                
INKEY    DS    CL32                                                             
BOOKREC  DS    CL250                                                            
ACCTREC  DS    CL250                                                            
WORKREC  DS    CL250                                                            
KEY      DS    CL32                                                             
KEYSAVE  DS    CL32                                                             
RATWK1   DS    XL6                                                              
RATWK2   DS    XL6                                                              
RATWK3   DS    XL12                                                             
RULE     DS    0CL7                                                             
RULESTAT DS    CL1                                                              
FTRATEQ  EQU   X'80'               FT RATE (FIXED)                              
TENQ     EQU   X'40'               RATE HELD AS 10 TO N                         
HUNDREDQ EQU   X'20'               RATE HELD AS 100 TO N                        
THOUSNDQ EQU   X'10'               RATE HELD AS 1000 TO N                       
FROMEURQ EQU   X'08'               FROM-CURRENCY IS EURO MEMBER                 
TOEUROQ  EQU   X'04'               TO-CURRENCY IS EURO MEMBER                   
DIVIDEQ  EQU   X'01'               DIVIDE BY RATE                               
RULERATE DS    CL5                                                              
RULESHFT DS    CL1                                                              
PL16     DS    PL16                                                             
PL8      DS    PL8                                                              
INPUT    DS    PL8                                                              
SAVCURF  DS    CL3                                                              
SAVCURT  DS    CL3                                                              
WORKX    EQU   *                                                                
         SPACE                                                                  
* GEGENEXC                                                                      
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE GEGENEXC                                                       
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
         EJECT                                                                  
       ++INCLUDE DDEUREKAD                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002DDEUREKA  04/18/01'                                      
         END                                                                    
