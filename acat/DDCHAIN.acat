*          DATA SET DDCHAIN    AT LEVEL 018 AS OF 05/17/11                      
*CATALP CHAIN                                                                   
         TITLE 'CHAIN LENGTH DETECTION'                                         
CHAIN    CSECT                                                                  
         ENTRY CHAINBLK                                                         
         NMOD1 0,*CHAIN**                                                       
         STAR  CLEAR=ARZERO,ARS=OFF                                             
         USING CHAIND,CHAINBLK                                                  
         SAM31 ,                                                                
*                                                                               
         USING SYSFACD,R7                                                       
         L     R7,=V(SYSFAC)                                                    
         L     R2,VSSB                                                          
         ICM   R2,15,SSBTKADR-SSBD(R2)                                          
         BZ    CHAINX                                                           
         USING TCBD,R2                                                          
         CLI   CHTRAP,C'Y'         TRAP REQUIRED?                               
         BNE   CHAINX              NO                                           
         CLI   CHINIT,C'Y'         AREA INITIALISED?                            
         BE    *+8                 NO                                           
         BRAS  RE,INIT             DO IT                                        
         CLI   CHINIT,C'Y'         INITIALISED OK?                              
         BNE   CHAINX              NO                                           
         CLI   CHHIT,C'Y'                                                       
         BE    CHAINX                                                           
*                                                                               
         C     RD,TCBTIA           DON'T CARE UNLESS INTO TIA                   
         BL    CRAPON                                                           
                                                                                
         LR    R0,RD                                                            
         S     R0,TCBWRKA                                                       
         C     R0,CHDSPMX          THIS IS BIGGER THAN PREVIOUS HIGH?           
         BL    CRAPON              NO                                           
*                                                                               
         ST    R0,CHDSPMX          SAVE NEW HIGH                                
         B     CHAINON             TOO BIG                                      
***********************************************************************         
* CHECK ALL CHAINS FOR CRAPPER                                        *         
***********************************************************************         
         USING SSBD,R4                                                          
CRAPON   L     R4,VSSB                                                          
         LT    R2,SSBTKADR         CURRENT RUNNING TASK                         
         BZ    CHAINX                                                           
                                                                                
CRAPON10 DS    0H                                                               
         LTR   R1,RD                                                            
         BZ    CHAINX                                                           
                                                                                
         C     R1,=V(FACEND)       RD < V(FACEND)                               
         BL    CHAINX              THIS IS BELOW FACPAK                         
                                                                                
         LT    RE,TCBUTL                                                        
         BZ    CHAINX                                                           
                                                                                
         CLI   TCBOVSYS,X'03'      NET                                          
         BNE   CHAINX                                                           
         CLI   TCBPRG,X'11'        NBUY                                         
         BNE   CHAINX                                                           
         LR    R3,R2                                                            
                                                                                
CRAPON12 C     R1,TCBWRKA          WITHIN MY TASK WORKING AREA                  
         BL    CRAPONIT            TCBWRK <= R1 <= TCBTIA                       
         C     R1,TCBTIA                                                        
         BH    CRAPONIT            ABEND IF NOT IN RANGE                        
                                                                                
         LT    R3,8(,R3)           FORWARD                                      
         JZ    CRAPONIT                                                         
         CR    R3,R1               SCAN CHAIN                                   
         BL    CRAPON12            KEEP MOVING FORWARD                          
         JH    CRAPONIT            I'M LOST                                     
         J     CHAINX                                                           
                                                                                
CRAPONIT DS    0H                                                               
         MVI   CHHIT,C'Y'          SET HAVE A HIT                               
*        ICM   R2,15,SSBTKADR      CURRENT TASK                                 
*        BZ    CHAINON                                                          
*        OI    TCBFLAG3,TCBKILL                                                 
*        B     CHAINON                                                          
         XC    TCBSVECB,TCBSVECB                                                
         XC    TCBSVRD,TCBSVRD                                                  
         XC    TCBTIMEW,TCBTIMEW                                                
         DC    H'00'                                                            
         DROP  R2,R4                                                            
                                                                                
***********************************************************************         
* HAVE A CHAIN THAT EXCEEDS TIA                                       *         
***********************************************************************         
CHAINON  SAM31 ,                                                                
         MVI   CHHIT,C'Y'          SET HAVE A HIT                               
         BRAS  RE,REPORT                                                        
*                                                                               
CHAINX   REAR  ARS=OFF                                                          
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* EXIT POINTS AND HANDY ROUTINES                                      *         
***********************************************************************         
EXIT     XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
* INITIALISE TEST BUFFER                                              *         
***********************************************************************         
INIT     NTR1  ,                                                                
         L     R0,CHAWRK           CLEAR W/S BUFFER                             
         L     R1,CHLWRK                                                        
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
         L     R0,CHATIA           CLEAR TIA BUFFER                             
         L     R1,CHLTIA                                                        
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
         L     R0,CHATWA           CLEAR TWA BUFFER                             
         L     R1,CHLTWA                                                        
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
         L     R0,CHAMAP           CLEAR MAP BUFFER                             
         L     R1,CHLMAP                                                        
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
         L     R0,CHAPGM           CLEAR PGMS BUFFER                            
         L     R1,CHLPGM                                                        
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
         L     R0,CHAUTL           CLEAR UTL BUFFER                             
         L     R1,CHLUTL                                                        
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
         XC    THISTCB,THISTCB                                                  
*                                                                               
INITX    MVI   CHINIT,C'Y'         INITIALISED CORE BUFFER OK                   
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* REPORT ON NEW MAXIMUM CHAIN LENGTH                                  *         
***********************************************************************         
REPORT   NTR1  ,                                                                
         L     R2,VSSB             PICK UP SSB                                  
         USING SSBD,R2                                                          
         MVC   MSGH+4(3),SSBSYSNA  SET FACPAK NAME INTO MESSAGE HEADER          
         MVC   MSG1TSK,CRQUERY                                                  
         MVC   MSG1SIN,CRQUERY                                                  
         MVC   MSG1LUID,CRQUERY                                                 
*                                                                               
         ICM   R3,15,SSBTKADR      PICK UP TCB OF CHAIN EXCEEDED                
         BZ    REP02                                                            
         USING TCBD,R3                                                          
*                                                                               
         SAM31 ,                                                                
         L     R0,CHAWRK           SAVE W/S                                     
         L     R1,CHLWRK                                                        
         L     RE,TCBWRKA                                                       
         ST    RE,CHCWRK                                                        
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     R0,CHATIA           SAVE TIA                                     
         L     R1,CHLTIA                                                        
         L     RE,TCBTIA                                                        
         ST    RE,CHCTIA                                                        
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     R0,CHATWA           SAVE TWA                                     
         L     R1,CHLTWA                                                        
         L     RE,TCBTWA                                                        
         ST    RE,CHCTWA                                                        
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     R0,CHAMAP           SAVE MAP                                     
         L     R1,CHLMAP                                                        
         L     RE,TCBMAP                                                        
         ST    RE,CHCMAP                                                        
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     R0,CHAPGM           SAVE PGMS                                    
         L     R1,CHLPGM                                                        
         L     RE,TCBPGMA                                                       
         ST    RE,CHCPGM                                                        
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     R0,CHAUTL           SAVE UTL                                     
         L     R1,CHLUTL                                                        
         L     RE,TCBUTL                                                        
         ST    RE,CHCUTL                                                        
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
         SAM24 ,                                                                
         MVC   MSG1TSK,TCBID                                                    
         GOTO1 VHEXOUT,CRPLIST,TCBSIN,MSG1SIN,L'TCBSIN,0,0                      
*                                                                               
         ICM   RF,15,TCBUTL                                                     
         BZ    *+10                                                             
         MVC   MSG1LUID,TLUID-UTLD(RF)                                          
*                                                                               
REP02    XR    R0,R0               MUST BE CLEAR FOR MULTILINE WTO              
         WTO   TEXT=((MSGHL,C),(MSG1L,D),(MSG2L,D),(0,E))                       
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTANTS                                              *         
***********************************************************************         
         LTORG                                                                  
*                                                                               
ARZERO   DC    16F'0'                                                           
VHEXOUT  DC    V(HEXOUT)                                                        
CRPLIST  DS    6F                                                               
CRQUERY  DC    16C'?'                                                           
THISTCB  DC    A(0)                                                             
*                                                                               
         DS    0Q                                                               
         DC    CL16'CHAINBLKCHAINBLK'                                           
CHAINBLK DC    (CHAINL)X'00'                                                    
*                                                                               
*                                                                               
MSGALL   DS    0D                                                               
MSGHL    DC    H'25'                                                            
MSGH     DC    CL25'*FACPAK* CHAIN EXCEEDED'                                    
MSG1L    DC    H'50'                                                            
MSG1     DC    CL50' '                                                          
         ORG   MSG1                                                             
         DC    CL05'TASK='                                                      
MSG1TSK  DC    CL08'?'                                                          
         DC    CL02', '                                                         
         DC    CL04'SIN='                                                       
MSG1SIN  DC    CL08'?'                                                          
         DC    CL02', '                                                         
         DC    CL05'LUID='                                                      
MSG1LUID DC    CL08'?'                                                          
         ORG   MSG1+L'MSG1                                                      
MSG2L    DC    H'50'                                                            
MSG2     DC    CL50'PLEASE CALL SYSTEMS IF YOU SEE THIS MESSAGE'                
         EJECT                                                                  
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
* DDCHAIND                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDCHAIND                                                       
         PRINT ON                                                               
* FASSB                                                                         
         PRINT OFF                                                              
       ++INCLUDE FASSB                                                          
         PRINT ON                                                               
* FASYSFAC                                                                      
         PRINT OFF                                                              
       ++INCLUDE FASYSFAC                                                       
         PRINT ON                                                               
* FATCB                                                                         
         PRINT OFF                                                              
       ++INCLUDE FATCB                                                          
         PRINT ON                                                               
* FAUTL                                                                         
         PRINT OFF                                                              
       ++INCLUDE FAUTL                                                          
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'018DDCHAIN   05/17/11'                                      
         END                                                                    
