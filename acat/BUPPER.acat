*          DATA SET BUPPER     AT LEVEL 004 AS OF 05/01/02                      
*CATALP BUPPER                                                                  
         TITLE 'BUPPER - BUDGET SYSTEM DATA EXTRACT/UPDATE'                     
BUPPER   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 BUPWRKX-BUPBLKD,**BUPPER,RA,RR=RE,CLEAR=YES                      
         USING BUPBLKD,RC          RC=A(GLOBAL W/S)                             
         ST    RE,RELO                                                          
         ST    R1,APARM                                                         
         MVC   ACT,0(R1)           EXTRACT ACTION & BLOCK VALUES                
         L     R1,0(R1)                                                         
         MVC   BUPBLKD(BUPBLKL),0(R1)                                           
*                                                                               
         LA    R2,ACTTAB           SEARCH ACTION TABLE FOR ACTION               
         USING ACTTABD,R2                                                       
BU2      CLI   ACTTHIS,EOT         TEST E-O-T                                   
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLC   ACTTHIS,ACT         MATCH INPUT ACTION WITH TABLE                
         BE    *+12                                                             
         LA    R2,ACTTABL(R2)      BUMP TO NEXT TABLE ENTRY                     
         B     BU2                                                              
         OC    ACTLAST(2),ACTLAST  TEST STAND ALONE ACTION                      
         BZ    BU5                                                              
         ICM   RF,1,ACTLAST        TEST ACTION SEQUENCE CHECKING                
         BZ    BU4                                                              
         EX    RF,*+8                                                           
         B     *+8                                                              
         TM    LNACT,0                                                          
         BNZ   BU4                                                              
         MVI   RETURN,X'FF'        SET INVALID ACTION SEQUENCE                  
         B     BU6                                                              
*                                                                               
BU4      MVC   LNACT,ACTNEXT       SET NEXT TIME VALID ACTIONS                  
BU5      ICM   RF,7,ACTROUT        PICK UP ROUTINE ADDRESS                      
         LA    RF,0(RF)                                                         
         A     RF,RELO                                                          
         MVI   RETURN,0                                                         
         BASR  RE,RF                                                            
         BE    BU6                                                              
         OC    ACTLAST(2),ACTLAST  TEST STAND ALONE ACTION                      
         BZ    BU6                                                              
         MVI   LNACT,0             RESET NEXT TIME ACTION                       
*                                                                               
BU6      L     R1,APARM                                                         
         MVC   0(1,R1),RETURN      RETURN ERROR & RESTORE BLOCK                 
         L     R1,0(R1)                                                         
         MVC   0(BUPBLKL,R1),BUPBLKD                                            
*                                                                               
BUX      CLI   RETURN,0            SET CC=EQUAL IF ACTION OK                    
         XIT1  ,                   RETURN TO CALLER                             
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* BUINIT - INITIALIZE WORKER FILE                                     *         
*                                                                     *         
* NTRY - USER SETS BUIKEY VALUES FROM WHICH A WORKER KEY IS BUILT     *         
*                                                                     *         
* EXIT - RETURN SET TO WORKER ERROR (X'80'=EOF ETC.)                  *         
***********************************************************************         
         SPACE 1                                                                
BUINIT   NTR1  ,                                                                
         L     RE,BUPABUFF                                                      
         LH    RF,=Y(WKBUFFLN)                                                  
         XCEF  ,                                                                
         XC    BUWKEY(BUWKEYL),BUWKEY                                           
         XC    BUPKEY(BUPKEYL),BUPKEY                                           
*                                                                               
         OC    BUWKUSER,BUIKUSER   BUILD BUWKEY                                 
         BNZ   *+6                                                              
         DC    H'0'                                                             
         ZIC   R0,BUIKDSYS                                                      
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  BUWKDSYS,DUB                                                     
         MVC   BUWKOPGM,BUIKOPGM                                                
         ZIC   R0,BUIKBDAY                                                      
         CVD   R0,DUB                                                           
         ICM   R0,3,DUB+6                                                       
         SRL   R0,4                                                             
         STC   R0,BUWKPDAY                                                      
         MVI   BUWKCLS,BUWKCLSQ                                                 
*                                                                               
         LA    R9,WORK                                                          
         USING UKRECD,R9           R9=A(WORKER KEY)                             
         XC    UKINDEX,UKINDEX                                                  
         L     R8,BUPAREC                                                       
         USING WKRECD,R8           R8=A(WORKER RECORD)                          
         XC    WKRECD(WKDATA1-WKRECD),WKRECD                                    
         MVC   UKKEY,BUWKEY                                                     
         OI    UKFLAG,X'11'        ALLOW DUPLICATE WKFILE KEYS                  
         MVC   WKCOMNT,COMMENT                                                  
         GOTO1 BUPADMGR,DMCB,WKOPEN,WKFILE,UKRECD,WKRECD,BUPABUFF               
         MVC   RETURN,8(R1)                                                     
         TM    RETURN,X'FF'        TEST ERRORS                                  
         BNZ   BUINITX                                                          
         MVC   BUWKEY(BUWKEYL),UKRECD                                           
         MVC   BUPKCIAD,UKCIADDR                                                
*                                                                               
         MVI   BUPKTYP,BUPKTYPQ    BUILD BUPKEY                                 
         MVC   BUPKDSYS,BUWKDSYS                                                
         MVC   BUPKPDAY,BUWKPDAY                                                
         MVC   BUPKPCLS,BUWKCLS                                                 
*                                                                               
BUINITX  B     BUX                                                              
         DROP  R8,R9                                                            
         EJECT                                                                  
***********************************************************************         
* BUADD - ADD A RECORD TO A WORKER FILE                               *         
***********************************************************************         
         SPACE 1                                                                
BUADD    NTR1  ,                                                                
         GOTO1 BUPADMGR,DMCB,WKADD,WKFILE,BUWKEY,BUPAREC,BUPABUFF               
         MVC   RETURN,8(R1)                                                     
BUADDX   B     BUX                                                              
         SPACE 2                                                                
***********************************************************************         
* BUCLOSE - CLOSE A WORKER FILE                                       *         
***********************************************************************         
         SPACE 1                                                                
BUCLOSE  NTR1  ,                                                                
         GOTO1 BUPADMGR,DMCB,WKCLOSE,WKFILE,BUWKEY,BUPAREC,BUPABUFF             
         MVC   RETURN,8(R1)                                                     
BUCLOSEX B     BUX                                                              
         EJECT                                                                  
***********************************************************************         
* BUOPEN - OPEN A WORKER FILE FOR READING                             *         
*                                                                     *         
* NTRY - USER SETS BUPKEY VALUES FROM WHICH A WORKER FILE BLOCK IS    *         
*        READ                                                         *         
*                                                                     *         
* EXIT - RETURN IS SET TO WORKER ERROR (X'10'=NOT FOUND ETC.) ON      *         
*        ERROR, ELSE BUIKEY & BUWKEY FIELDS ARE SET FOR SUSEQUENT     *         
*        FILE READING (BUPREAD) AND/OR STATUS CHANGE (BUPUSED)        *         
***********************************************************************         
         SPACE 1                                                                
BUOPEN   NTR1  ,                                                                
         GOTO1 BUPADMGR,DMCB,WKBUFF,WKFILE,BUWKEY,BUPAREC,BUPABUFF              
         L     RE,BUPABUFF                                                      
         ICM   RE,15,8(RE)                                                      
         A     RE,BUPABUFF                                                      
         USING SKBUFFD,RE                                                       
         MVC   SKFSTCI+0(2),BUPKCIAD                                            
         MVC   SKFSTCI+2(2),=X'0100'                                            
         L     RE,BUPAREC                                                       
         XC    0(4,RE),0(RE)                                                    
         GOTO1 (RF),(R1),WKRANDOM                                               
         MVC   RETURN,8(R1)                                                     
         TM    RETURN,X'FF'        TEST FOR ERRORS                              
         BNZ   BUOPENX                                                          
*                                                                               
         XC    BUWKEY(BUWKEYL),BUWKEY                                           
         L     R1,BUPABUFF         BUILD BUWKEY                                 
         MVC   BUWKEY(L'WKKEY),WKKEY-WKRECD(R1)                                 
         MVC   BUWKCIAD,BUPKCIAD                                                
         OC    BUIKUSER,BUIKUSER   TEST USER SET                                
         BZ    *+14                                                             
         CLC   BUIKUSER,BUWKUSER                                                
         BNE   BUOPEN2                                                          
         CLC   BUWKDSYS,BUPKDSYS   OTHER KEY FIELD CHECKING                     
         BNE   BUOPEN2                                                          
         CLC   BUWKPDAY(2),BUPKPDAY                                             
         BNE   BUOPEN2                                                          
*                                                                               
         MVC   BUIKUSER,BUWKUSER   SET BUIKEY VALUES                            
         PACK  DUB,BUWKDSYS                                                     
         CVB   R0,DUB                                                           
         STC   R0,BUIKDSYS                                                      
         MVC   BUIKOPGM,BUWKOPGM                                                
         UNPK  WORK(3),BUWKPDAY(2)                                              
         PACK  DUB,WORK(2)                                                      
         CVB   R0,DUB                                                           
         STC   R0,BUIKBDAY                                                      
         B     BUOPENX                                                          
*                                                                               
BUOPEN2  OI    RETURN,X'10'        SET WKFILE NOT FOUND ERROR                   
*                                                                               
BUOPENX  B     BUX                                                              
         EJECT                                                                  
***********************************************************************         
* BUREAD - READS A WORKER FILE SEQUENTIALLY UNTIL EOF POSTED          *         
*                                                                     *         
* NTRY - USER MUST HAVE ISSUED A BUPOPEN COMMAND PREVIOUSLY           *         
*                                                                     *         
* EXIT - RETURN IS SET TO WORKER ERROR (X'80'=EOF ETC.) OR ZERO IF    *         
*        RECORD HAS BEEN READ OK                                      *         
***********************************************************************         
         SPACE 1                                                                
BUREAD   GOTO1 BUPADMGR,DMCB,WKREAD,WKFILE,BUWKEY,BUPAREC,BUPABUFF              
         MVC   RETURN,8(R1)                                                     
         TM    RETURN,X'FF'-X'90'  TEST FOR ERRORS (NOT EOF)                    
         BNZ   BUREAD2                                                          
         TM    RETURN,X'80'        TEST FOR EOF                                 
         BZ    BUREADX                                                          
         MVI   RETURN,X'80'        SET E-O-F FOR CALLER                         
         MVI   LNACT,BUPUSED       SET NEXT ACTION                              
         B     BUREADX                                                          
*                                                                               
BUREAD2  MVI   LNACT,0                                                          
*                                                                               
BUREADX  B     BU6                                                              
         SPACE 2                                                                
***********************************************************************         
* BUUSED - ROUTINE TO SET STATUS OF WORKER FILE TO PURGED             *         
*                                                                     *         
* NTRY - BUPOPEN COMMAND MUST HAVE BEEN ISSUED PREVIOUSLY             *         
*                                                                     *         
* EXIT - RETURN IS SET TO WORKER ERROR OR ZERO IF OK                  *         
***********************************************************************         
         SPACE 1                                                                
BUUSED   NTR1  ,                                                                
         GOTO1 BUPADMGR,DMCB,WKPURGE,WKFILE,BUWKEY,BUPAREC,BUPABUFF             
         MVC   RETURN,8(R1)                                                     
BUUSEDX  B     BUX                                                              
         EJECT                                                                  
***********************************************************************         
* BUPUT - ADD/UPDATE BUDDIR/BUDFIL RECORDS FROM PASSED RECORD         *         
*                                                                     *         
* NTRY - BUPAREC CONTAINS ADDRESS OF RECORD TO BE ADDED/UPDATED       *         
*                                                                     *         
* EXIT - RETURN IS SET TO DATAMGR ERROR OR ZERO IF OK                 *         
***********************************************************************         
         SPACE 1                                                                
BUPUT    NTR1  WORK=(R7,PUTWRKX-PUTWRKD)                                        
         USING PUTWRKD,R7          R7=A(LOCAL W/S)                              
         MVI   PUTFLAG,0                                                        
         L     R8,BUPAREC                                                       
         LA    R8,4(R8)                                                         
         USING BURECD,R8           R8=A(UPDATE RECORD)                          
*                                                                               
         LA    R1,BUFRSTEL                                                      
         USING BUDAD,R1            TEST FOR NON-ZERO DATA ELEMENTS              
         SR    R0,R0                                                            
BUPUT1   CLI   BUDAEL,0            TEST E-O-R                                   
         BE    BUPUT3                                                           
         CLI   BUDAEL,BUDAELQ      TEST DATA VALUE ELEMENT                      
         BNE   BUPUT2                                                           
         CP    BUDATA,=P'0'        TEST DATA VALUE IS ZERO                      
         BE    BUPUT2                                                           
         OI    PUTFLAG,X'80'       NO - SET NON-ZERO DATA FOUND                 
         B     BUPUT3                                                           
BUPUT2   IC    R0,BUDALEN                                                       
         AR    R1,R0                                                            
         B     BUPUT1                                                           
         DROP  R1                                                               
*                                                                               
BUPUT3   GOTO1 BUPADMGR,DMCB,(X'88',DMREAD),BUDDIR,BUKEY,PUTKEY                 
         TM    8(R1),X'FF'-X'92'   TEST FOR DISK ERRORS                         
         BZ    *+14                                                             
         MVC   RETURN,8(R1)                                                     
         B     BUPUTX                                                           
         TM    8(R1),X'90'         TEST EOF/NOT FOUND                           
         BZ    BUPUT4                                                           
         TM    PUTFLAG,X'80'       TEST ANY NON-ZERO DATA FOUND                 
         BNZ   *+12                                                             
         MVI   RETURN,0            NO - SET OK & EXIT                           
         B     BUPUTX                                                           
         OI    PUTFLAG,X'40'       SET RECORD NOT FOUND                         
         B     BUPUT6              COPY RECORD & BUILD ACTIVITY ELEMENT         
*                                                                               
BUPUT4   LA    R8,PUTKEY           POINT TO RETURNED KEY                        
         TM    8(R1),X'02'         TEST RECORD DELETED                          
         BZ    BUPUT5                                                           
         OI    PUTFLAG,X'20'       SET THIS RECORD IS DELETED                   
         NI    BUKCTL,X'FF'-X'80'  YES - UNDELETE & WRITE BACK                  
         GOTO1 (RF),(R1),DMWRT                                                  
         MVC   RETURN,8(R1)                                                     
         TM    RETURN,X'FF'        TEST FOR ERRORS                              
         BNZ   BUPUTX                                                           
*                                                                               
BUPUT5   GOTO1 (RF),(R1),(X'80',GETREC),BUDFIL,BUKDA,PUTIO,PUTWORK              
         MVC   RETURN,8(R1)                                                     
         TM    RETURN,X'FF'        TEST FOR ERRORS                              
         BNZ   BUPUTX                                                           
         TM    PUTFLAG,X'20'       TEST RECORD IS DELETED                       
         BZ    BUPUT7                                                           
*                                                                               
BUPUT6   LA    R0,PUTIO            COPY USER RECORD INTO I/O                    
         L     RE,BUPAREC                                                       
         LA    RE,4(RE)            RE=A(SOURCE)                                 
         SR    RF,RF               RF=L'SOURCE                                  
         ICM   RF,3,BURLEN-BURECD(RE)                                           
         LA    R1,2(RF)            CLEAR TWO BYTES AT EOR                       
         MVCL  R0,RE                                                            
         B     BUPUT12                                                          
*                                                                               
BUPUT7   L     R8,BUPAREC          COPY DATA ELEMENTS TO OUTPUT RECORD          
         LA    R8,4(R8)                                                         
         LA    R2,BUFRSTEL                                                      
         USING BUDAD,R2                                                         
BUPUT8   CLI   BUDAEL,0            TEST E-O-R                                   
         BE    BUPUT12                                                          
         CLI   BUDAEL,BUDAELQ      TEST DATA ELEMENT                            
         BNE   BUPUT10                                                          
         LA    R0,BUDAELQ          YES - ADD TO OUTPUT RECORD                   
         LA    RF,L'BUDADATE                                                    
         GOTO1 BUPAHELO,DMCB,(C'D',BUDFIL),((R0),PUTIO),((RF),BUDADATE)         
         CLI   BUDAPREC,X'FF'      TEST CALLER WANTS ELEMENT DELETED            
         BE    BUPUT10                                                          
         GOTO1 (RF),(R1),(C'P',BUDFIL),,BUDAD                                   
         CLI   12(R1),0            TEST ELEMENT PUT OK                          
         BE    *+6                                                              
         DC    H'0'                                                             
BUPUT10  ZIC   R0,BUDALEN          BUMP TO NEXT ELEMENT                         
         AR    R2,R0                                                            
         B     BUPUT8                                                           
*                                                                               
BUPUT12  LA    R0,BUACTELQ                                                      
         GOTO1 BUPAHELO,DMCB,(C'G',BUDFIL),((R0),PUTIO),0                       
         CLI   12(R1),0                                                         
         BNE   BUPUT14                                                          
         L     R2,12(R1)           UPDATE ACTIVITY ELEMENT                      
         USING BUACTD,R2                                                        
         BAS   RE,BUACT                                                         
         B     BUPUT16                                                          
*                                                                               
BUPUT14  LA    R2,PUTTEMP          BUILD & ADD ACTIVITY ELEMENT                 
         USING BUACTD,R2                                                        
         XC    BUACTD(BUACTLNQ),BUACTD                                          
         MVI   BUACTEL,BUACTELQ                                                 
         MVI   BUACTLEN,BUACTLNQ                                                
         BAS   RE,BUACT                                                         
         GOTO1 BUPAHELO,DMCB,(C'P',BUDFIL),PUTIO,BUACTD,0                       
         CLI   12(R1),0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
BUPUT16  LA    R0,ADDREC           PUT OR ADD RECORD                            
         TM    PUTFLAG,X'40'                                                    
         BNZ   *+8                                                              
         LA    R0,PUTREC                                                        
         LA    R8,PUTKEY                                                        
         GOTO1 BUPADMGR,DMCB,(R0),BUDFIL,BUKDA,PUTIO,PUTWORK                    
         MVC   RETURN,8(R1)                                                     
*                                                                               
BUPUTX   B     BUX                                                              
         DROP  R2,R7,R8                                                         
         EJECT                                                                  
***********************************************************************         
* BUACT - UPDATE ACTIVITY ELEMENT                                     *         
*                                                                     *         
* NTRY - R2=A(ACTIVITY ELEMENT)                                       *         
*        BUPORIG=CALLER ORIGIN TYPE                                   *         
*        BUPBDATE=ACTIVITY DATE                                       *         
***********************************************************************         
         SPACE 1                                                                
         USING BUACTD,R2                                                        
BUACT    LA    R1,BUACTAB          R1=A(ACTIVITY TYPE TABLE)                    
BUACT2   CLI   0(R1),EOT                                                        
         BE    BUACTX                                                           
         CLC   BUPORIG,0(R1)       MATCH TABLE TYPE TO ORIGIN                   
         BE    *+12                                                             
         LA    R1,L'BUACTAB(R1)                                                 
         B     BUACT2                                                           
         LA    RF,BUACTLUP         DATA UPDATE                                  
         CLI   1(R1),1                                                          
         BE    *+8                                                              
         LA    RF,BUACTLMT         MAINTENANCE UPDATE                           
         MVC   0(3,RF),BUPBDATE                                                 
         MVC   3(1,RF),BUPORIG                                                  
BUACTX   BR    RE                                                               
         DROP  R2                                                               
         SPACE 1                                                                
BUACTAB  DS    0XL2                                                             
         DC    AL1(BUACTCPY),AL1(1)                                             
         DC    AL1(BUACTEXT),AL1(1)                                             
         DC    AL1(BUACTINP),AL1(1)                                             
         DC    AL1(BUACTTRA),AL1(1)                                             
         DC    AL1(BUACTSND),AL1(2)                                             
         DC    AL1(BUACTSNO),AL1(2)                                             
         DC    AL1(BUACTSNP),AL1(2)                                             
         DC    AL1(EOT)                                                         
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
COMMENT  DC    CL16'EXTRACT UPDATE'                                             
         SPACE 1                                                                
BUDDIR   DC    C'BUDDIR  '                                                      
BUDFIL   DC    C'BUDFIL  '                                                      
WKFILE   DC    C'WKFILE  '                                                      
DMREAD   DC    C'DMREAD  '                                                      
DMRSEQ   DC    C'DMRSEQ  '                                                      
DMWRT    DC    C'DMWRT   '                                                      
GETREC   DC    C'GETREC  '                                                      
PUTREC   DC    C'PUTREC  '                                                      
ADDREC   DC    C'ADDREC  '                                                      
WKOPEN   DC    C'OPE'                                                           
WKADD    DC    C'ADD'                                                           
WKBUFF   DC    C'BUF'                                                           
WKREAD   DC    C'REA'                                                           
WKCLOSE  DC    C'CLO'                                                           
WKPURGE  DC    C'PUR'                                                           
WKRANDOM DC    C'RAN'                                                           
         SPACE 1                                                                
EOT      EQU   0                                                                
WKBUFFLN EQU   4096                                                             
         EJECT                                                                  
* ACTION TABLE (SEE ACTTABD)                                                    
*                                                                               
ACTTAB   DS    0XL6                                                             
         DC    AL1(BUPINIT,0,BUPADD),AL3(BUINIT)                                
         DC    AL1(BUPADD,BUPINIT+BUPADD,BUPADD+BUPCLOSE),AL3(BUADD)            
         DC    AL1(BUPCLOSE,BUPADD,0),AL3(BUCLOSE)                              
         DC    AL1(BUPOPEN,0,BUPREAD+BUPUSED),AL3(BUOPEN)                       
         DC    AL1(BUPREAD,BUPOPEN+BUPREAD,BUPREAD),AL3(BUREAD)                 
         DC    AL1(BUPUSED,255,0),AL3(BUUSED)                                   
         DC    AL1(BUPPUT,0,0),AL3(BUPUT)                                       
         DC    AL1(EOT)                                                         
         EJECT                                                                  
* BUGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE BUGENFILE                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DMWRKRK                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMWRKRK                                                        
         PRINT ON                                                               
         SPACE 1                                                                
* DMWRKRD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMWRKRD                                                        
         PRINT ON                                                               
         SPACE 1                                                                
* DMWRKRS                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMWRKRS                                                        
         PRINT ON                                                               
         SPACE 1                                                                
ACTTABD  DSECT                     ** ACTION TABLE **                           
ACTTHIS  DS    X                   THIS TIME ACTION - FOLLOWS                   
ACTLAST  DS    X                   LAST TIME ACTION - WHICH SETS                
ACTNEXT  DS    X                   NEXT TIME ACTION - TO VALIDATE THIS          
ACTROUT  DS    AL3                 A(ACTION ROUTINE)                            
ACTTABL  EQU   *-ACTTABD                                                        
         SPACE 1                                                                
PUTWRKD  DSECT                     ** BUPUT LOCAL W/S **                        
PUTFLAG  DS    X                                                                
PUTKEY   DS    XL64                                                             
PUTTEMP  DS    XL64                                                             
PUTWORK  DS    8D                                                               
PUTIO    DS    2000C                                                            
PUTWRKX  EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE BUPPERD                                                        
         EJECT                                                                  
         ORG   BUPSAVE                                                          
LNACT    DS    X                   LAST TIME/NEXT TIME ACTIONS                  
         DS    XL15                N/D                                          
         ORG                                                                    
DUB      DS    D                                                                
WORK     DS    XL64                                                             
DMCB     DS    6F                                                               
RELO     DS    A                   BUPPER RELOCATION FACTOR                     
APARM    DS    A                   A(BUPPER PARAMETER LIST)                     
ASAVE    DS    A                   A(WORKER BUFFER SAVE AREA)                   
ACT      DS    X                   BUPPER ACTION CODE                           
RETURN   DS    X                   BUPPER RETURN BYTE                           
*                                                                               
BUPWRKX  EQU   *                                                                
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004BUPPER    05/01/02'                                      
         END                                                                    
