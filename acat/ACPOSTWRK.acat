*          DATA SET ACPOSTWRK  AT LEVEL 016 AS OF 04/06/20                      
*CATALP POSTWRK                                                                 
         TITLE 'ACPOSTWRK - POST A  WORKER FILE'                                
**********************************************************************          
* PARM 1                                                                        
*  BYTE 1-3     A(ACWORKD)                                                      
* PARM 2                                                                        
*  BYTE 0       C'R' = SET REMOTE KEY                                           
*  BYTE 1-3     A(WORKER FILE ID)                                               
**********************************************************************          
*                                                                               
*---------------------------------------------------------------------*         
*  ID  LVL   DATE    TICKET            COMMENTS                       *         
* ---- --- ------- ------------ --------------------------------------*         
* RKEJ 016 20DEC19 <SPEC-40066> Fix BUG in Acc update - SMF records   *         
*                               not being written                     *         
*---------------------------------------------------------------------*         
*                                                                               
***********************************************************************         
* LOAD AND RUN SORT/MERGE AND UPDATE                                  *         
***********************************************************************         
                                                                                
         PRINT NOGEN                                                            
         USING ACWORKD,RA                                                       
POSTWRK  CSECT                                                                  
UPDT     NMOD1 0,*UPDATE*,R9                                                    
         MVC   PARM,0(R1)                                                       
         ICM   RA,15,AWORK                                                      
         SR    R2,R2                                                            
         ICM   R2,7,AWRKID                                                      
         MVC   ID,0(R2)                                                         
         OC    ID,ID                                                            
         BZ    UPDX                                                             
*                                                                               
         USING MASTD,R7                                                         
         USING RUNXTRAD,R6                                                      
         L     R6,VEXTRAS                                                       
         MVC   WKFILEID,ID         SET WORKER FILE                              
         L     R7,ADMASTC                                                       
         OI    MCPRTIND,MCPRTINL   SUSPEND LOGOS                                
         L     RF,MCVLOGO          SAVE LOGO                                    
         MVC   SVLOGO,0(RF)                                                     
         MVC   0(2,RF),=X'07FE'                                                 
         MVC   SVWKID,ID           SAVE WORKER FILE ID                          
         MVC   SVPROG,RCPROG       PROGRAM ID                                   
         MVC   SVRCWRT,RCWRITE     RCWRITE VALUE                                
         MVC   SVREMOTE,REMOTEC                                                 
         LA    RF,TEMPREMK         SET FALSE REMOTE KEY                         
         STCM  RF,15,REMOTEC                                                    
         CLI   REMSW,C'R'          TEST REMOTE SWITCH                           
         BNE   *+8                                                              
         BAS   RE,SETREM           SET REMOTE PQ ID FOR ZIP                     
*                                                                               
         LA    R3,LOADLST          LOAD SM & AU PHASES                          
         SR    R0,R0                                                            
UPDT02   MVC   MCDUB,8(R3)                                                      
         OC    0(4,R3),0(R3)       Did we do a load yet?                        
         BNZ   UPDT02B             Yes, skip it                                 
         SR    R2,R2                                                            
         GOTO1 MCVLOADM,PARA,(R2)                                               
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   0(4,R3),4(R1)       ADDRESS OF PHASE                             
         MVC   4(4,R3),0(R1)       LENGTH                                       
UPDT02B  LA    R3,L'LOADLST(R3)                                                 
         CLI   0(R3),X'FF'                                                      
         BNE   UPDT02              LOAD NEXT                                    
                                                                                
         LM    RE,RF,ADSM02        SETUP DUMP LIST                              
         AR    RF,RE                                                            
         STM   RE,RF,MCUSRDMP                                                   
         LM    RE,RF,ADAU02                                                     
         AR    RF,RE                                                            
         STM   RE,RF,MCUSRDMP+8                                                 
*                                                                               
         L     R0,ASAVSPAC         SAVE SPACEND                                 
         LH    R1,=Y(SPACENDL)                                                  
         LA    RE,SPACEND                                                       
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LA    R0,SPACEND          CLEAR SPACEND                                
         LH    R1,=Y(SPACENDL)                                                  
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                  ** SORT MERGE **                             
         USING SMFBLKD,SMFAREA                                                  
         LA    RE,SMFAREA                                                       
         ST    RE,ASMFBLK          IN XTRA BLOCK                                
         LHI   RF,SMFBMAXQ                                                      
         STH   RF,SMF#MAX          Set max count                                
         TM    SMFIND,SMFIXA       Was this initialized?                        
         BO    UPDT04                                                           
         L     R3,=A((SMFBRECX-SMFBRECD)*SMFBMAXQ)                              
         STORAGE OBTAIN,LENGTH=(3),ADDR=SMFXAMEM,LOC=31,COND=YES                
         LTR   RF,RF                                                            
         BNZ   *+8                                                              
         OI    SMFIND,SMFIXA       XA initialized                               
                                                                                
UPDT04   MVC   RCPROG,=C'SM'       CALL SORT/MERGE                              
         MVI   RCWRITE,C'Y'                                                     
         MVC   ACSPECS,ADSM01      SPECS                                        
         MVI   MODE,RUNFRST                                                     
         GOTO1 ADSM02,PARA,ACWORKD                                              
         MVI   MODE,RUNLAST                                                     
         GOTO1 ADSM02,PARA,ACWORKD                                              
*                                                                               
         MVI   LIVE,C'N'                                                        
         BAS   RE,ACAU             TEST THE UPDATE                              
*                                                                               
         CP    UPDATERR,=P'0'      ANY ERRORS                                   
         BNE   UPDT10                                                           
*                                                                               
         MVI   LIVE,C'Y'           NOW DO IT LIVE                               
         L     RF,ACREPORT                                                      
         MVC   SVACRP,0(RF)        SAVE FIRST 2 BYTES OF ACREPORT               
         MVC   0(2,RF),=X'07FE'    NOP PRINTING                                 
         BRAS  RE,GOSMF            WRITE OUT SMF RECORDS                        
         BAS   RE,ACAU             TEST THE SORT/MERGE/UPDATE                   
*NOTYET  BRAS  RE,GOSMF            WRITE OUT SMF RECORDS (APE)                  
         L     RF,ACREPORT         RESTORE ACREPORT (PRINTING)                  
         MVC   0(2,RF),SVACRP                                                   
*                                                                               
*                                                                               
         CLI   SVRCWRT,C'Y'        WRITE=YES                                    
         BNE   UPDT06                                                           
         MVC   RCPROG,=C'KE'       ** KEEP FILE **                              
*                                                                               
UPDT05   LA    R0,SPACEND          CLEAR SPACEND                                
         LH    R1,=Y(8*K)                                                       
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
         MVC   ACSPECS,ADSM01      SPECS                                        
         MVI   MODE,RUNFRST                                                     
         MVI   RCWRITE,C'Y'                                                     
         GOTO1 ADSM02,PARA,ACWORKD                                              
         MVI   MODE,RUNLAST                                                     
         GOTO1 ADSM02,PARA,ACWORKD                                              
*                                                                               
UPDT06   CLI   REMSW,C'R'          TEST REMOTE SWITCH                           
         BNE   UPDT08                                                           
         GOTO1 PRINT,PARA,=C'CLOSE'                                             
         BAS   RE,RESETREM         RESET REMOTE FIELDS ARCHIVE ETC              
*                                                                               
UPDT08   MVC   RCPROG,SVPROG       RESTORE PROGRAM CODE                         
         L     RF,MCVLOGO                                                       
         MVC   0(2,RF),SVLOGO      RESTORE LOGOS                                
         MVC   RCWRITE,SVRCWRT     RESTORE WRITE=                               
         MVC   REMOTEC,SVREMOTE    RESTORE REMOTEC                              
*                                                                               
         LA    R0,SPACEND          RESTORE SPACEND                              
         LH    R1,=Y(SPACENDL)                                                  
         L     RE,ASAVSPAC                                                      
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         B     UPDX                                                             
*                                                                               
UPDT10   TM    RCFLAG3,X'80'       ARE WE COMING FROM THE SN?                   
         BNO   UPDT06              NO                                           
         NI    RCFLAG3,X'FF'-X'80' YES, CLEAR FLAG                              
         MVC   RCPROG,=C'HO'       AND MARK THE FILE 'HOLD'                     
         ZAP   UPDATERR,=P'0'      RESET ANY PREVIOUS ERRORS                    
         OI    ERRIND,UPDERR       PASS ERROR BACK TO SN                        
         B     UPDT05                                                           
*                                                                               
UPDX     DS    0H                                                               
EXIT     XIT1                                                                   
*                                                                               
DELPHASE DC    X'FFFFFFFF'                                                      
         EJECT                                                                  
***********************************************************************         
* SMFOUT for balancing                                                          
***********************************************************************         
GOSMF    NTR1  ,                                                                
         SR    R8,R8                                                            
         ICM   R8,3,SMF#RECS                                                    
         BZ    GOSMFXIT                                                         
         ICM   R3,15,SMFXAMEM      A(stored SMF record(s) from ACSM)            
                                                                                
         USING SMFBRECD,SMFREC                                                  
GOSMF10  SAM31                                                                  
         MVC   SMFREC,0(R3)        Move to low core                             
         SAM24                                                                  
                                                                                
         CLI   MCTSTRUN,X'FF'      RUN=TEST                                     
         BE    GOSMFXIT                                                         
                                                                                
         USING COMFACSD,R4                                                      
GOSMF20  ICM   R4,15,ADCOMFAC      A(COMFACS)                                   
         JZ    GOSMFXIT                                                         
         ICM   RF,15,CSMFOUT       SMFOUT ROUTINE                               
         BZ    GOSMFXIT                                                         
         LHI   R4,12               TYPE 12 FILE BALANCE                         
         CLI   SVRCWRT,C'Y'        WRITE=YES                                    
         BE    *+8                                                              
         OILH  GR4,X'8000'                                                      
         GOTO1 (RF),SMFPARM,(R4),SMFREC                                         
                                                                                
GOSMF30  LLH   RF,SMFBLEN                                                       
         AR    R3,RF                                                            
         BRCT  R8,GOSMF10                                                       
                                                                                
GOSMFXIT XC    SMF#RECS,SMF#RECS   All processed set to zero                    
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*        ** UPDATE **                                                           
***********************************************************************         
ACAU     NTR1  ,                                                                
         LA    R0,SPACEND          CLEAR SPACEND                                
         LHI   R1,8*K                                                           
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
         MVC   RCPROG,=C'AU'       CALL UPDATE                                  
         MVC   ACSPECS,ADAU01      SPECS                                        
         MVI   RCWRITE,C'N'        FIRST TIME, MAKE IT DRAFT                    
         CLI   LIVE,C'Y'                                                        
         BNE   *+10                                                             
         MVC   RCWRITE,SVRCWRT     SECOND TIME DO IT                            
         MVI   MODE,RUNFRST                                                     
         GOTO1 ADAU02,PARA,ACWORKD                                              
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*        ** SET REMOTE KEY **                                                   
***********************************************************************         
         USING RUNXTRAD,R6                                                      
         USING MASTD,R7                                                         
         USING REMOTED,RF                                                       
SETREM   NTR1  ,                                                                
         L     R6,VEXTRAS                                                       
         L     R7,ADMASTC                                                       
         L     RF,MCVREMOT                                                      
         MVC   SVARC,MCARC                                                      
         XC    MCARC,MCARC                                                      
         XC    REMOTKEY,REMOTKEY     TOTAL PAGE GOES TO DATA CONT               
         MVI   REMOTJID,C'A'                                                    
         MVC   REMOTJID+1(2),QPROG                                              
         MVI   REMOTCLS,C'T'                                                    
         MVI   REMOTCPY,X'01'                                                   
         MVC   REMOTDST,=AL2(1106)   ID=ZIP OR X'0452'                          
         MVC   REMOTKEY(8),MCJOB                                                
         MVC   REMOTFNO,REMOTKEY                                                
         MVC   SVRMTARC,REMOTARC   CLEAR ARCHIVE CLASS                          
         MVC   SVRMTTYP,REMOTTYP                                                
         MVC   SVRMTTY1,REMOTTY1                                                
         NI    REMOTTYP,X'FF'-X'60'                                             
         NI    REMOTTY1,X'FF'-(REMOTAEQ+REMOTARQ+REMOTADQ)                      
         XC    REMOTARC,REMOTARC                                                
         MVI   REMOTPRT,0                                                       
         MVI   FORCEHED,C'Y'                                                    
         MVC   PAGE,=H'1'                                                       
         J     EXIT                                                             
**********************************************************************          
*     ** RESET REMOTE FIELDS **                                                 
**********************************************************************          
RESETREM NTR1  ,                                                                
         L     R6,VEXTRAS                                                       
         L     R7,ADMASTC                                                       
         L     RF,MCVREMOT                                                      
         MVC   MCARC,SVARC         ARC=   CARD                                  
         MVC   REMOTARC,SVRMTARC   RESTORE REMOTE ARCHIVE INFO                  
         MVC   REMOTTYP,SVRMTTYP                                                
         MVC   REMOTTY1,SVRMTTY1                                                
         J     EXIT                                                             
         DROP  R6,R7                                                            
         EJECT                                                                  
***********************************************************************         
* Local storage - constants                                           *         
***********************************************************************         
K        EQU   1024                                                             
YES      EQU   C'Y'                                                             
NO       EQU   C'N'                                                             
         DS    0D                                                               
LOADLST  DS    0XL16                                                            
ADSM02   DC    AL4(0),AL4(0),CL8'ACSM02  '                                      
ADAU02   DC    AL4(0),AL4(0),CL8'ACAU02  '                                      
ADSM01   DC    AL4(0),AL4(0),CL8'ACSM01  '                                      
ADAU01   DC    AL4(0),AL4(0),CL8'ACAU01  '                                      
         DC    X'FF'                                                            
*                                                                               
ASAVSPAC DC    A(SAVSPAC)                                                       
VPRNTBL  DC    V(PRNTBL)                                                        
*                                                                               
         DS    0F                                                               
PARM     DS    0XL8                INPUT PARAMETERS                             
AWORK    DS    A                   A(ACWORKD)                                   
REMSW    DS    CL1                 R=SET REMOTE KEY FOR ZIP ID                  
AWRKID   DS    XL3                 A(WORKER ID)                                 
*                                                                               
PARA     DS    6F                                                               
*                                                                               
SVWKID   DS    XL16                WORKER FILE ID                               
SVPROG   DS    CL2                 PROGRAM CODE                                 
SVLOGO   DS    CL2                 FIRST 2 BYTES OF LOGO CODE                   
SVACRP   DS    CL2                 FIRST 2 BYTES OF ACREPORT                    
SVRCWRT  DS    CL1                 RCWRITE VALUE                                
SVREMOTE DS    CL4                 REMOTEC                                      
SVRMTTYP DS    XL1                 SAVED REMOTTYP                               
SVRMTTY1 DS    XL1                 SAVED REMOTTY1                               
SVRMTARC DS    XL1                 SAVED REMOTARC                               
SVARC    DS    CL(L'MCARC)         ARC=  CARD                                   
LIVE     DS    CL1                 LIVE OR TEST                                 
TEMPREMK DS    CL20                TEMP REMOTE KEY                              
ID       DS    CL16                                                             
         DS    0A                                                               
SMFAREA  DS    XL(SMFBLNQ)         SMF BALANCE RECORDS                          
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*              SAVE AREA FOR SPACEND                                            
***********************************************************************         
SPACENDL EQU   (8*K)                                                            
SAVSPAC  DS    XL(SPACENDL)                                                     
         EJECT                                                                  
* ACDDEQUS                                                                      
         PRINT OFF                                                              
       ++INCLUDE ACDDEQUS                                                       
         PRINT ON                                                               
* ACGENBOTH                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENBOTH                                                      
         PRINT ON                                                               
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
* ACGENMODES                                                                    
         PRINT OFF                                                              
       ++INCLUDE ACGENMODES                                                     
         PRINT ON                                                               
* ACGENPOST                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENPOST                                                      
         PRINT ON                                                               
* ACLANGEQU                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACLANGEQU                                                      
         PRINT ON                                                               
* ACMASTD                                                                       
         PRINT OFF                                                              
       ++INCLUDE ACMASTD                                                        
         PRINT ON                                                               
SMFBLKD  DSECT                                                                  
SMFBMAXQ EQU   100                                                              
SMFIND   DS    X                   Indicators                                   
SMFIXA   EQU   X'20'               .  Use XA storage and return SMF             
SMFIDCB  EQU   X'10'               .  Use DCB DSN(SMFOUT)                       
         DS    XL3                                                              
SMFXAMEM DS    A                                                                
SMFADCB  DS    A                   A(DCB)                                       
SMF#RECS DS    H                                                                
SMF#MAX  DS    H                                                                
SMFPARM  DS    6F                                                               
SMFREC   DS    XL(SMFBRECX-SMFBRECD)                                            
SMFBLNQ  EQU   *-SMFBLKD                                                        
                                                                                
* ACOFFALD                                                                      
         PRINT OFF                                                              
       ++INCLUDE ACOFFALD                                                       
         PRINT ON                                                               
* ACREPWORKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE ACREPWORKD                                                     
         PRINT ON                                                               
* DDBUFFALOD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDBUFFALOD                                                     
         PRINT ON                                                               
* DDLOGOD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DDLOGOD                                                        
         PRINT ON                                                               
* DDCOMFACSD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACSD                                                     
         PRINT ON                                                               
* DDMASTD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DDMASTD                                                        
         PRINT ON                                                               
* DDREMOTED                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDREMOTED                                                      
         PRINT ON                                                               
* DDREPXTRAD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDREPXTRAD                                                     
         PRINT ON                                                               
* DDSMFFBAL                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDSMFFBAL                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'016ACPOSTWRK 04/06/20'                                      
         END                                                                    
