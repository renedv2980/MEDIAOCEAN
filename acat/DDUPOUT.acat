*          DATA SET DDUPOUT    AT LEVEL 029 AS OF 06/27/08                      
*CATALP UPOUT                                                                   
         TITLE 'MODULE TO EDIT UPGRADE EXPRESSION'                              
UPOUT    CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 UPDL,*UPOUT**                                                    
         USING UPD,RC                                                           
         USING RAVLNEL,RA                                                       
         L     RA,0(R1)            A(UPGRADE ELEMENT)  PARA1 1                  
         LR    R9,R1               A(OUTPUT START)           2                  
*                                                                               
         XC    ACOMFACS,ACOMFACS                                                
         CLI   8(R1),C'C'          TEST A(COMFACS) PASSED                       
         BNE   *+10                                                             
         MVC   ACOMFACS,8(R1)      A(COMFACS)                                   
*                                                                               
         SPACE 2                                                                
         MVI   AREA,C' '           CLEAR A SCRATCH PAD                          
         MVC   AREA+1(79),AREA                                                  
         LA    R3,AREA                                                          
         CLI   RAVLNTYP,X'42'      SELECT UPGRADE TYPE                          
         BE    UP2                                                              
         CLI   RAVLNTYP,2          X'02',X'42'=RATING                           
         BE    UP2                                                              
         BL    UPEND                                                            
         CLI   RAVLNTYP,6          6=HPT                                        
         BE    UP14                                                             
         CLI   RAVLNTYP,9          9=MMU  / LPM(NEW UPGRADE)                    
         BE    UP16                                                             
         CLI   RAVLNTYP,X'0B'      11=PUT BOOK TO BOOK INDEX                    
         BE    UP20                                                             
         CLI   RAVLNTYP,X'0C'      12=SHARE BOOK TO BOOK INDEX                  
         BE    UP20                                                             
         CLI   RAVLNTYP,X'0D'      13=PUT BOOK AVERAGE                          
         BE    UP20                                                             
         CLI   RAVLNTYP,X'0E'      14=SHARE BOOK AVERAGE                        
         BE    UP20                                                             
         CLI   RAVLNTYP,X'43'                                                   
         BE    *+16                                                             
         CLI   RAVLNTYP,4          4=INDEX                                      
         BE    UP10                                                             
         BH    UPEND                                                            
         CLI   RAVLNCAT,C'P'                                                    
         BE    UP4                                                              
         OC    RAVLNOP1,RAVLNOP1                                                
         BZ    UP12                                                             
         CLI   RAVLNOP1,X'10'                                                   
         BH    UP6                                                              
         B     UP8                                                              
         EJECT                                                                  
*              HANDLE THE EXPRESSIONS                                           
         SPACE 3                                                                
UP2      MVC   0(30,R3),=C'DEMOS. ADJUSTED TO A RATING OF'                      
         LA    R3,31(R3)                                                        
         SPACE 1                                                                
UP3      LH    R1,RAVLNOP1                                                      
         BAS   RE,LEFT                                                          
         LH    R1,RAVLNOP2                                                      
         LTR   R1,R1                                                            
         BZ    UPEND                                                            
         CLI   RAVLNOP2,0                                                       
         BE    UP3B                                                             
         MVC   0(03,R3),=C'AND'                                                 
         LA    R3,4(R3)                                                         
         LA    R1,RAVLNOP2                                                      
         BAS   RE,BOOK                                                          
         MVC   0(5,R3),=C'SHARE'                                                
         LA    R3,6(R3)                                                         
         B     UPEND                                                            
         SPACE 1                                                                
UP3B     MVC   0(14,R3),=C'AND A SHARE OF'                                      
         LA    R3,15(R3)                                                        
         BAS   RE,LEFT                                                          
         B     UPEND                                                            
         SPACE 1                                                                
UP4      LA    R1,RAVLNOP2                                                      
         BAS   RE,BOOK                                                          
         MVC   0(38,R3),=C'HOUSEHOLD AND DEMO SHARES PROJECTED TO'              
         LA    R3,39(R3)                                                        
         LA    R1,RAVLNOP1                                                      
         BAS   RE,BOOK                                                          
         MVC   0(10,R3),=C'HPT LEVELS'                                          
         LA    R3,10(R3)                                                        
         B     UPEND                                                            
         SPACE 1                                                                
UP6      LA    R1,RAVLNOP2                                                      
         BAS   RE,BOOK                                                          
         MVC   0(28,R3),=C'HOUSEHOLD SHARE PROJECTED TO'                        
         LA    R3,29(R3)                                                        
         LA    R1,RAVLNOP1                                                      
         BAS   RE,BOOK                                                          
         MVC   0(29,R3),=C'HUT LEVEL WITH DEMOS ADJUSTED'                       
         LA    R3,30(R3)                                                        
         B     UPEND                                                            
         SPACE 1                                                                
UP8      MVC   0(33,R3),=C'DEMOS. ADJUSTED TO A HUT LEVEL OF'                   
         LA    R3,34(R3)                                                        
         B     UP3                                                              
         SPACE 1                                                                
UP10     MVC   0(30,R3),=C'DEMOS. ADJUSTED BY AN INDEX OF'                      
         LA    R3,31(R3)                                                        
         LH    R1,RAVLNOP1                                                      
         BAS   RE,LEFT                                                          
         B     UPEND                                                            
         SPACE 1                                                                
UP12     MVC   0(29,R3),=C'DEMOS. ADJUSTED TO A SHARE OF'                       
         LA    R3,30(R3)                                                        
         LH    R1,RAVLNOP2                                                      
         BAS   RE,LEFT                                                          
         B     UPEND                                                            
         SPACE 1                                                                
UP14     DS    0C                                                               
         CLI   RAVLNCAT,C'V'                                                    
         BE    UP15                                                             
         LA    R1,RAVLNOP2                                                      
         BAS   RE,BOOK                                                          
         MVC   0(27,R3),=C'HOUSEHOLD AND DEMO SHARES X'                         
         LA    R3,28(R3)                                                        
         LA    R1,RAVLNOP1                                                      
         BAS   RE,BOOK                                                          
         MVC   0(22,R3),=C'HPT LEVELS FACTORED BY'                              
         LA    R3,23(R3)                                                        
         LH    R1,RAVLNOP3                                                      
         BAS   RE,LEFT                                                          
         B     UPEND                                                            
         SPACE 1                                                                
UP15     LA    R1,RAVLNOP1                                                      
         BAS   RE,BOOK                                                          
         MVC   0(29,R3),=C'HPT LEVELS ADJUSTED TO HUT OF'                       
         LA    R3,30(R3)                                                        
         LA    R1,RAVLNOP3                                                      
         BAS   RE,LEFT                                                          
         MVC   0(31,R3),=C'SHR LEVELS ADJUSTED TO SHARE OF'                     
         LA    R3,32(R3)                                                        
         LH    R1,RAVLNOP4                                                      
         BAS   RE,LEFT                                                          
         B     UPEND                                                            
         SPACE 1                                                                
UP16     CLI   RAVLNOP2,X'C1'   ALPHA?                                          
         BL    *+12             IF SECOND PARAMTER IS ALPHA THEN                
         CLI   RAVLNOP2,X'E9'   WE MUST HAVE A LPM UPGRADE NOT MMU              
         BNH   UP18                                                             
         CLC   =X'9999',RAVLNOP3   LPS UPGRADE?                                 
         BE    UP18D                                                            
         LA    R1,RAVLNOP1                                                      
         BAS   RE,BOOK                                                          
         MVC   0(22,R3),=C'HPT LEVELS ADJUSTED TO'                              
         LA    R3,23(R3)                                                        
         LH    R1,RAVLNOP2                                                      
         MVC   0(31,R3),=C'INDEXED BY PREV YEAR HPT LEVELS'                     
         LA    R3,32(R3)                                                        
         B     UPEND                                                            
                                                                                
UP18     LH    R1,RAVLNOP4    MUST BE LPM UPGRADE IF RAVLNOP2 IS ALPHA          
         MVC   0(5,R3),=C'APPLY'                                                
         AHI   R3,6                                                             
         OC    RAVLNOP4,RAVLNOP4     ANY PUT INDEX MKT SUPPLIED ?               
         BNZ   UP19                                                             
* NO PUT INDEX MARKET SPECIFIED                                                 
         MVC   0(4,R3),RAVLNOP2     MOVE STATION                                
         AHI   R3,5                                                             
         MVC   0(8,R3),=C'HOME MKT'                                             
         AHI   R3,9                                                             
         B     UP19A                                                            
                                                                                
UP18D    MVC   0(22,R3),=C'HPT LEVELS ADJUSTED TO'                              
         LA    R3,23(R3)                                                        
         LH    R1,RAVLNOP2                                                      
         MVC   0(22,R3),=C'INDEXED BY INDEX BOOKS'                              
         LA    R3,23(R3)                                                        
         LA    R1,RAVLNOP1                                                      
         BAS   RE,BOOK                                                          
         LA    R1,RAVLNOP2                                                      
         BAS   RE,BOOK                                                          
         B     UPEND                                                            
* PUT INDEX MARKET NOT SPECIFIED                                                
UP19     MVC   0(4,R3),=C'MKT#'                                                 
         LA    R3,5(R3)                                                         
         BAS   RE,LEFT                                                          
UP19A    MVC   0(11,R3),=C'HPT INDX TO'                                         
         AHI   R3,12                                                            
         LA    R1,RAVLNOP1                                                      
         BAS   RE,BOOK                                                          
         MVC   0(5,R3),=C'HPTS,'                                                
         AHI   R3,6                                                             
         LA    R1,RAVLNOP2                                                      
         MVC   0(4,R3),RAVLNOP2                                                 
         AHI   R3,5                                                             
         MVC   0(8,R3),=C'SHR INDX'                                             
         AHI   R3,9                                                             
         MVC   0(15,R3),=C'TO SHARE LEVELS'                                     
         AHI   R3,16                                                            
         B     UPEND                                                            
*                                                                               
         SPACE 1                                                                
UP20     CLI   RAVLNCAT,C'N'                                                    
         BNE   UP24                                                             
         MVC   0(17,R3),=C'DEMOS ADJUSTED BY'                                   
         LA    R3,18(R3)                                                        
         MVC   0(09,R3),=C'PUT TREND'                                           
         SPACE 1                                                                
         CLI   RAVLNTYP,X'0D'      AVERAGE TYPES                                
         BE    *+8                                                              
         CLI   RAVLNTYP,X'0E'                                                   
         BNE   *+10                                                             
         MVC   4(5,R3),=C'AVE. '                                                
         SPACE 1                                                                
         CLI   RAVLNTYP,X'0C'      SHARE TYPES                                  
         BE    *+8                                                              
         CLI   RAVLNTYP,X'0E'                                                   
         BNE   *+10                                                             
         MVC   0(3,R3),=C'SHR'                                                  
         SPACE 1                                                                
         LA    R3,10(R3)                                                        
         LA    R1,RAVLNOP1                                                      
         BAS   RE,BOOK                                                          
         LA    R1,RAVLNOP2                                                      
         BAS   RE,BOOK                                                          
         CLI   RAVLNOP3,0                                                       
         BE    UPEND                                                            
         LA    R1,RAVLNOP3                                                      
         BAS   RE,BOOK                                                          
         CLI   RAVLNOP4,0                                                       
         BE    UPEND                                                            
         LA    R1,RAVLNOP4                                                      
         BAS   RE,BOOK                                                          
         B     UPEND                                                            
         SPACE 1                                                                
UP24     DS    0C                                                               
         MVC   0(17,R3),=C'DEMOS ADJUSTED BY'                                   
         LA    R3,18(R3)                                                        
         CLI   RAVLNCAT,C'M'                                                    
         BE    UP26                                                             
         LH    R1,RAVLNOP2                                                      
         BAS   RE,LEFT                                                          
         CLI   RAVLNCAT,C'Y'                                                    
         BNE   *+14                                                             
         MVC   0(4,R3),=C'YEAR'                                                 
         LA    R3,5(R3)                                                         
         CLI   RAVLNCAT,C'Q'                                                    
         BNE   *+14                                                             
         MVC   0(4,R3),=C'QUARTER'                                              
         LA    R3,8(R3)                                                         
         CLI   RAVLNCAT,C'S'                                                    
         BNE   *+14                                                             
         MVC   0(4,R3),=C'SWEEP'                                                
         LA    R3,6(R3)                                                         
         MVC   0(09,R3),=C'PUT TREND'                                           
         CLI   RAVLNTYP,X'0C'                                                   
         BNE   *+10                                                             
         MVC   0(3,R3),=C'SHR'                                                  
         LA    R3,10(R3)                                                        
         MVC   0(09,R3),=C'BEGINNING'                                           
         LA    R3,10(R3)                                                        
         LA    R1,RAVLNOP1                                                      
         BAS   RE,BOOK                                                          
         B     UPEND                                                            
         SPACE 1                                                                
UP26     DS    0H                                                               
         LA    R1,RAVLNOP1                                                      
         BAS   RE,BOOK                                                          
         LA    R1,RAVLNOP2                                                      
         BAS   RE,BOOK                                                          
         CLI   RAVLNOP3,0                                                       
         BE    UP27                                                             
         LA    R1,RAVLNOP3                                                      
         BAS   RE,BOOK                                                          
         CLI   RAVLNOP4,0                                                       
         BE    UP27                                                             
         LA    R1,RAVLNOP4                                                      
         BAS   RE,BOOK                                                          
UP27     MVC   0(22,R3),=C'PUT INDICES MULTIPLIED'                              
         LA    R3,22(R3)                                                        
         B     UPEND                                                            
         SPACE 1                                                                
UPEND    LA    R4,AREA                                                          
         SR    R3,R4                                                            
         STC   R3,4(R9)                                                         
         LTR   R3,R3                                                            
         BZ    UPEND2                                                           
         L     R4,4(R9)                                                         
         BCTR  R3,0                                                             
         EX    R3,*+8                                                           
         B     UPEND2                                                           
         MVC   0(0,R4),AREA                                                     
         SPACE 1                                                                
UPEND2   XIT1                                                                   
         EJECT                                                                  
*              SUBSIDIARY AIDS, DSECT                                           
         SPACE 3                                                                
LEFT     TM    RAVLNTYP,X'C0'      DECIMAL PRECISION                            
         BM    LEFT2                                                            
         PRINT GEN                                                              
         EDIT  (R1),(3,(R3)),ALIGN=LEFT                                         
         B     LEFT3                                                            
LEFT2    EDIT  (R1),(5,(R3)),ALIGN=LEFT,1                                       
         PRINT NOGEN                                                            
LEFT3    AR    R3,R0                                                            
         LA    R3,1(R3)                                                         
         BR    RE                                                               
         SPACE 1                                                                
BOOK     ST    RE,SAVERE                                                        
         ZIC   RF,1(R1)            MONTH                                        
         CLI   0(R1),0                                                          
         BER   RE                                                               
         CLI   1(R1),0                                                          
         BER   RE                                                               
         CLI   1(R1),12                                                         
         BHR   RE                                                               
         BCTR  RF,0                                                             
         MH    RF,=H'3'                                                         
         LA    RF,MONTHS(RF)                                                    
         MVC   0(3,R3),0(RF)                                                    
         ZIC   RF,0(R1)                                                         
         EDIT  (RF),(2,3(R3))      YEAR                                         
         LA    R3,6(R3)                                                         
         CLI   RAVLNBT,0           SKIP IF NO BOOK TYPE                         
         BE    BOOK1                                                            
         BCTR  R3,0                                                             
         MVI   0(R3),C'('          ADD ON BOOKTYPE                              
         MVC   1(1,R3),RAVLNBT                                                  
                                                                                
* TRANSLATE BOOKTYPE TO 2 BYTE EXTERNAL                                         
         ICM   RF,15,ACOMFACS                                                   
         BZ    BOOK1                                                            
         ICM   RF,15,CDEMTABS-COMFACSD(RF)                                      
         GOTO1 (RF),DMCB,SPBOOKTB  GET A(BOOK TABLE)                            
         ICM   RF,15,0(R1)         A(TABLE)RETURNED IN P1                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         L     RE,4(R1)            L'TABLE ENTRY RETURNED IN P2                 
         USING SPBKTYPD,RF                                                      
BOOK10   CLI   0(RF),X'FF'                                                      
         BE    BOOK1               INVALID BOOK TYPE                            
         CLC   SPBKTYPN,RAVLNBT                                                 
         BE    *+10                                                             
         AR    RF,RE                                                            
         B     BOOK10                                                           
         MVC   1(2,R3),SPBKTYPA                                                 
         DROP  RF                                                               
                                                                                
         CLI   2(R3),C' '         CHECK IF 2 CHARACTER BOOKTYPE                 
         BH    BOOK20                                                           
         MVI   2(R3),C')'         1 CHAR BOOKTYPE                               
         LA    R3,4(R3)                                                         
         B     BOOK1                                                            
BOOK20   MVI   3(R3),C')'         2 CHAR BOOKTYPE                               
         LA    R3,5(R3)                                                         
                                                                                
BOOK1    DS    0H                                                               
         L     RE,SAVERE                                                        
         BR    RE                                                               
         SPACE 1                                                                
MONTHS   DC    C'JANFEBMARAPRMAYJUN'                                            
         DC    C'JULAUGSEPOCTNOVDEC'                                            
         SPACE 1                                                                
UPD      DSECT                                                                  
AREA     DS    CL80                                                             
DUB      DS    D                                                                
WORK     DS    CL64                                                             
ACOMFACS DS    A                                                                
SAVERE   DS    A                                                                
DMCB     DS    6F                                                               
UPDL     EQU   *-UPD                                                            
         EJECT                                                                  
       ++INCLUDE REGENAVL                                                       
       ++INCLUDE DEDEMTABD                                                      
       ++INCLUDE DDCOMFACS                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'029DDUPOUT   06/27/08'                                      
         END                                                                    
