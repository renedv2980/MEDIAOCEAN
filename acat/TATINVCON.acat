*          DATA SET TATINVCON  AT LEVEL 003 AS OF 07/15/98                      
*CATALP TINVCON                                                                 
         TITLE 'TINVCON - TALENT INVOICE CONVERTER'                             
TINVCON  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 MYEND-MYSTART,TINVCON                                            
         USING MYD,RC                                                           
         SPACE 1                                                                
*              PARAMETER 1         A(INPUT INVOICE NUMBER)                      
*                                  FIRST BYTE X'80'=SHOW ORIGINAL               
*                                     INVOICE, NOT CANCEL INV #                 
*                                  WILL BE SET TO X'FF'=INVALID                 
*              PARAMETER 2         A(OUTPUT INVOICE NUMBER)                     
*              PARAMETER 3         A(DATCON)                                    
*                                                                               
*                                                                               
*              OLD INVOICES        MM1234    (US)                               
*                                  MN1234    (CANADA) MONTH A,B...              
*                                  9WW123    T&R 1989                           
*                                  5WW123    T&R 1990                           
*                                                                               
*                  (EXAMPLES)      120123                                       
*                                  LN0124                                       
*                                  951012                                       
*                                                                               
*              NEW INVOICE EBCDIC  MYNNNN    Y=LAST DIGIT OF YEAR               
*                                              0-9=REGULAR                      
*                                              A-I=CANCELLED 1-9                
*                                              J  =CANCELLED 0                  
*                                            M IS A-L                           
*                  (EXAMPLES)      A00123                                       
*                                  L10124                                       
*                                  CJ0125                                       
*                                                                               
*              NEW INVOICE PWOS    YYYYMMNNNNOF  F 0=NEW TP                     
*                                  (F=FORMAT)      1=OLD US                     
*                                                  2=OLD CANADIAN               
*                                  YYYY0WWNNN03    3=OLD T&R                    
*                                                  4=CONVERTED USAGE            
*                                                  5=PAYROLL ADJUST             
*                                                  X'80'=CANCELLED NEW          
*                                                                               
*                  (EXAMPLES)      198912012301                                 
*                                  198912012402                                 
*                                                                               
*                                                                               
*                                                                               
         SPACE 1                                                                
         LR    R9,R1                                                            
         LM    R2,R4,0(R9)                                                      
         ST    R4,DATCON                                                        
         CLI   0(R2),X'19'         ANALYZE FIRST BYTE OF INPUT                  
         BE    TINVUNPK                                                         
         CLI   0(R2),X'20'         X'19' AND X'20' NEW PWOS                     
         BE    TINVUNPK                                                         
         CLI   1(R2),C'N'          CANADIAN HAVE SECOND BYTE N                  
         BE    TINVOLD                                                          
         CLI   0(R2),C'5'          OLD T&R CAN START WITH 5                     
         BE    TINVOLD                                                          
         CLI   0(R2),X'F0'         NEW INVOICES HAVE FIRST BYTE ALPHA           
         BL    TINVPACK                                                         
         B     TINVOLD                                                          
         EJECT                                                                  
*              PACKING OLD INVOICE NUMBERS                                      
         SPACE 3                                                                
*              INPUT               R2=A(MM1234) EBCDIC                          
*                                      (MN1234) EBCDIC CANADIAN                 
*              OUTPUT              R3=A(YYYYMMNNNN00) PWOS                      
         SPACE 1                                                                
         USING OLDD,R2                                                          
         USING PWOD,R3                                                          
TINVOLD  MVI   PWOCENT,X'19'       CENTURY 19..                                 
         MVI   PWOYEAR,X'90'       YEAR    90..                                 
         CLI   OLDYEAR,C'5'                IF OLD T&R START WITH 5              
         BE    TINVTNR                                                          
         MVC   PWOYEAR,OLDYEAR     CONVERT OLD TNR YEARS                        
         NI    PWOYEAR,X'8F'               F3-F9 BECOME 83-89                   
         CLI   OLDYEAR,C'2'                                                     
         BH    TINVTNR                                                          
         LA    R1,MONTABLE                                                      
         USING MOND,R1                                                          
         LA    R0,12                                                            
         SPACE 1                                                                
TO2      MVI   PWOIND,2            MATCH ON CANADIAN?                           
         CLC   OLDCAN,MONCAN                                                    
         BE    TO4                                                              
         MVI   PWOIND,1                  OR US?                                 
         CLC   OLDUS,MONUS                                                      
         BE    TO4                                                              
         LA    R1,L'MONENT(R1)                                                  
         BCT   R0,TO2                                                           
         B     BADINV                                                           
         SPACE 1                                                                
TO4      MVC   PWOMONTH,MONPWO                                                  
         PACK  WORK(3),OLDNUM(5)                                                
         MVC   PWONUM,WORK                                                      
         MVI   PWOYEAR,X'90'       ESTABLISH YEAR BASED ON MONTH                
         CLI   MONPWO,X'09'                                                     
         BL    XIT                                                              
         MVI   PWOYEAR,X'89'                                                    
         B     XIT                                                              
         SPACE 1                                                                
         USING TNRD,R2                                                          
TINVTNR  PACK  WORK(2),TNRWEEK(3)  T&R YWWNNN                                   
         MVC   PWOWEEK,WORK                                                     
         MVC   DUB+1(3),TNRNUM                                                  
         MVI   DUB,X'F0'                                                        
         PACK  WORK(3),DUB(5)                                                   
         MVC   PWONUM,WORK                                                      
         MVI   PWOIND,3                                                         
         ZIC   R0,PWOWEEK          TRANSFORM 5201 TO 0521                       
         ZIC   R1,PWONUM                                                        
         SLL   R1,28               R0=00000052 R1=10000000                      
         SRDL  R0,28               R0=00000000 R1=00000521                      
         STH   R1,PWOWEEK                                                       
         B     XIT                                                              
         EJECT                                                                  
*              PACKING NEW INVOICE NUMBERS                                      
*                                                                               
*              INPUT               R2=A(MYNNNN) EBCDIC                          
*              OUTPUT              R3=A(YYYYMMNNNN00) PWOS                      
*                                                                               
         USING NEWD,R2                                                          
         USING PWOD,R3                                                          
TINVPACK MVC   YEAR(1),NEWYEAR     LAST DIGIT OF YEAR                           
         CLI   NEWYEAR,C'J'        (J IS CANCELLED 0)                           
         BNE   *+8                                                              
         MVI   YEAR,C'0'                                                        
         OI    YEAR,X'F0'          (CONVERT A-I TO 1-9 (CANCELLED))             
         L     RF,DATCON                                                        
         MVC   NOW,0(RF)                                                        
         CLI   DATCON,X'FF'        (TEST FF=A(NOW PROVIDED))                    
         BE    TINVP2                                                           
         GOTO1 DATCON,DMCB,(5,0),(20,NOW)                                       
*                                                                               
TINVP2   MVC   DECADE,NOW+2        MOVE IN PRESENT DECADE                       
         CLC   YEAR(1),NOW+3       IF DIGIT IS GREATER THAN PRESENT             
         BNH   TP1                    IT IS REALLY IN LAST DECADE               
         PACK  WORK(4),NOW(4)                                                   
         SP    WORK(4),=P'10'      BACK UP A DECADE                             
         UNPK  NOW(4),WORK(4)                                                   
         OI    NOW+3,X'F0'         FIX THE SIGN                                 
         MVC   DECADE,NOW+2        MOVE IN ADJUSTED DECADE                      
*                                                                               
TP1      PACK  PWOCENT(2),NOW(3)                                                
         PACK  PWOYEAR(2),DECADE(3)                                             
         MVI   PWOYEAR+1,0                                                      
*                                                                               
         LA    R1,MONTABLE                                                      
         USING MOND,R1                                                          
         LA    R0,12                                                            
*                                                                               
TP2      CLC   NEWMONTH,MONNEW     MATCH ON MONTH?                              
         BE    TP4                                                              
         LA    R1,L'MONENT(R1)                                                  
         BCT   R0,TP2                                                           
         B     BADINV                                                           
*                                                                               
TP4      MVC   PWOMONTH,MONPWO                                                  
         PACK  PWONUM(3),NEWNUM(5)                                              
         MVI   PWOIND,0                                                         
         CLI   NEWYEAR,C'J'        IF YEAR IS A-J                               
         BH    XIT                                                              
         OI    PWOIND,X'80'        X'80'=CANCELLED                              
         B     XIT                                                              
         DROP  R1,R2,R3                                                         
         EJECT                                                                  
*              UNPACKING NEW INVOICE NUMBERS                                    
         SPACE 3                                                                
*              INPUT               R2=A(YYYYMMNNNN) PWOS                        
*              OUTPUT              R3=A(YMNNNN) EBCDIC                          
         SPACE 1                                                                
         USING PWOD,R2                                                          
         USING NEWD,R3                                                          
TINVUNPK MVC   WORKIND,PWOIND                                                   
         NI    WORKIND,X'7F'                                                    
         CLI   WORKIND,3           T&R BELOW                                    
         BE    TUTNR                                                            
         CLI   WORKIND,4           CONVERTED USAGE RECORDS                      
         BE    TUUSAGE                                                          
         CLI   WORKIND,5           PAYROLL ADJUSTMENT                           
         BE    TUUSAGE                                                          
         MVC   NEWYEAR,PWOYEAR     YEAR                                         
         OI    NEWYEAR,X'F0'       (LAST DIGIT EBCDIC)                          
         TM    PWOIND,X'80'        SLIGHTLY DIFFERENT FOR NEW CANCEL            
         BNO   TU1                                                              
         TM    0(R9),X'80'         UNLESS CALLER DIDN'T WANT THIS               
         BO    TU1                                                              
         MVC   NEWYEAR,PWOYEAR                                                  
         NI    NEWYEAR,X'0F'       (CRUNCH OFF YEAR)                            
         OI    NEWYEAR,X'C0'       (CONVERT TO A-I)                             
         CLI   NEWYEAR,X'C0'                                                    
         BNE   TU1                                                              
         MVI   NEWYEAR,C'J'        (C0 BECOMES C'J')                            
         SPACE 1                                                                
TU1      LA    R1,MONTABLE                                                      
         USING MOND,R1                                                          
         LA    R0,12                                                            
         SPACE 1                                                                
TU2      CLC   PWOMONTH,MONPWO     MATCH ON MONTH?                              
         BE    TU4                                                              
         LA    R1,L'MONENT(R1)                                                  
         BCT   R0,TU2                                                           
         B     BADINV                                                           
         SPACE 1                                                                
TU4      MVC   NEWMONTH,MONNEW                                                  
         UNPK  WORK(5),PWONUM(3)                                                
         MVC   NEWNUM,WORK                                                      
         CLI   WORKIND,1                                                        
         BL    XIT                 0=NEW - DONE                                 
         MVC   NEWMONTH(2),MONUS                                                
         BE    XIT                 1=OLD US - MM NOT MY                         
         MVC   NEWMONTH,MONCAN                                                  
         MVI   NEWYEAR,C'N'        2=CANADA - MN NOT MY                         
         B     XIT                                                              
         SPACE 1                                                                
         USING TNRD,R3                                                          
TUTNR    MVI   TNRYEAR,C'5'                                                     
         CLI   PWOYEAR,X'90'                                                    
         BE    TUTNR2                                                           
         MVC   TNRYEAR,PWOYEAR                                                  
         OI    TNRYEAR,X'F0'                                                    
         SPACE 1                                                                
TUTNR2   LH    R1,PWOWEEK          TRANSFORM 0WWNNN TO WW0NNN                   
         LR    RF,R1               (SAVE IT FIRST)                              
         SR    R0,R0                                                            
         SLDL  R0,28                                                            
         SRL   R1,28                                                            
         STC   R0,PWOWEEK                                                       
         STC   R1,PWONUM                                                        
         UNPK  WORK(7),PWOWEEK(4)  CONVERTING WW0NNN                            
         MVC   TNRWEEK,WORK                                                     
         MVC   TNRNUM,WORK+3                                                    
         STH   RF,PWOWEEK          RESTORE I/P TO ORIGINAL FORMAT               
         B     XIT                                                              
         SPACE 1                                                                
TUUSAGE  MVC   0(6,R3),=CL6' '                                                  
         B     XIT                                                              
         EJECT                                                                  
*              ODDMENTS!                                                        
         SPACE 3                                                                
BADINV   MVI   0(R9),X'FF'                                                      
         SPACE 1                                                                
XIT      XIT1                                                                   
         SPACE 1                                                                
MONTABLE DS    0D                                                               
         DC    C'01',C'A',C'A',X'01'                                            
         DC    C'02',C'B',C'B',X'02'                                            
         DC    C'03',C'C',C'C',X'03'                                            
         DC    C'04',C'D',C'D',X'04'                                            
         DC    C'05',C'E',C'E',X'05'                                            
         DC    C'06',C'F',C'F',X'06'                                            
         DC    C'07',C'G',C'G',X'07'                                            
         DC    C'08',C'H',C'H',X'08'                                            
         DC    C'09',C'I',C'I',X'09'                                            
         DC    C'10',C'J',C'J',X'10'                                            
         DC    C'11',C'K',C'K',X'11'                                            
         DC    C'12',C'L',C'L',X'12'                                            
         DC    C'00',C'0',C'0',X'00'                                            
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
*              DSECTS                                                           
         SPACE 3                                                                
OLDD     DSECT                                                                  
OLDYEAR  DS    0CL1                                                             
OLDCAN   DS    0CL1                                                             
OLDUS    DS    CL2                                                              
OLDNUM   DS    CL4                                                              
         SPACE 1                                                                
TNRD     DSECT                                                                  
TNRYEAR  DS    CL1                                                              
TNRWEEK  DS    CL2                                                              
TNRNUM   DS    CL3                                                              
         SPACE 1                                                                
NEWD     DSECT                                                                  
NEWMONTH DS    CL1                                                              
NEWYEAR  DS    CL1                                                              
NEWNUM   DS    CL4                                                              
         SPACE 1                                                                
PWOD     DSECT                                                                  
PWOCENT  DS    PL1                                                              
PWOYEAR  DS    PL1                                                              
PWOMONTH DS    0PL1                                                             
PWOWEEK  DS    PL1                                                              
PWONUM   DS    PL2                                                              
PWOIND   DS    PL1                                                              
         SPACE 1                                                                
MOND     DSECT                                                                  
MONENT   DS    0CL5                                                             
MONUS    DS    CL2                                                              
MONCAN   DS    CL1                                                              
MONNEW   DS    CL1                                                              
MONPWO   DS    PL1                                                              
         SPACE 1                                                                
MYD      DSECT                                                                  
MYSTART  DS    0C                                                               
DATCON   DS    A                                                                
DMCB     DS    6F                                                               
WORK     DS    CL32                                                             
DUB      DS    D                                                                
NOW      DS    CL8                                                              
DECADE   DS    C                                                                
YEAR     DS    CL3                                                              
WORKIND  DS    CL1                                                              
MYEND    DS    0C                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003TATINVCON 07/15/98'                                      
         END                                                                    
