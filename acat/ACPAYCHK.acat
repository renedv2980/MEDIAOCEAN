*          DATA SET ACPAYCHK   AT LEVEL 020 AS OF 11/08/13                      
*CATALP ACPAYCHK                                                                
         TITLE 'ACPAYCHK - FIND CHECK INFO FOR MEDIA PAYABLES'                  
ACPAYCHK CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 PCBX-PCBD,*PAYCHK*,RA,CLEAR=YES                                  
         USING PCBD,RC                                                          
         ST    R1,APCB             SAVE ADDRESS CONTROL BLOCK                   
         MVC   PCB(PCBLNQ),0(R1)   SAVE CONTROL BLOCK                           
*                                                                               
         L     R8,PCBCOMF                                                       
         USING COMFACSD,R8                                                      
         MVC   DATAMGR,CDATAMGR                                                 
         MVC   DATCON,CDATCON                                                   
         DROP  R8                                                               
*                                                                               
         LA    RF,IO1                                                           
         STCM  RF,15,AIO1                                                       
         AH    RF,=Y(L'IO1)                                                     
         STCM  RF,15,AIO2                                                       
*                                                                               
         MVC   AUTL,PCBAUTL        MASTER SE NUMBER                             
         L     R2,PCBIDT                                                        
         TM    PCBSTA,PCBSINI      HAS TABLE BEEN INITIALIZED                   
         BO    PAY1                                                             
         MVI   0(R2),EOT                                                        
         OI    PCBSTA,PCBSINI                                                   
         XC    ACOPNSE,ACOPNSE     CLEAR LIST OF OPENED ACC FILES               
         L     RF,AUTL                                                          
         MVC   ACOPNSE,4(RF)       CURRENT FILE IS OPENED                       
*                                                                               
PAY1     CLI   PCBACTN,PCBAPRCV    PROCESS ACC RECOVERY FILE                    
         BNE   PAY3                                                             
         BAS   RE,PROCRCV                                                       
         B     PAYXIT                                                           
*                                                                               
PAY3     CLI   PCBACTN,PCBAACCF    PROCESS ACC FILE                             
         BNE   PAY5                                                             
         BAS   RE,PROCACC                                                       
         B     PAYXIT                                                           
*                                                                               
PAY5     CLI   PCBACTN,PCBASPOT    PROCESS SPOT CLEARANCE ELEMENT               
         BNE   PAY7                                                             
****     BAS   RE,PROCSPT                                                       
         B     PAYXIT                                                           
*                                                                               
PAY7     CLI   PCBACTN,PCBAPRNT    PROCESS PRINT CLEARANCE ELEMENT              
         BNE   PAYXIT                                                           
****     BAS   RE,PROCPRT                                                       
                                                                                
PAYXIT   L     R1,APCB                                                          
         MVC   0(PCBLNQ,R1),PCB    RETURN CONTROL BLOCK                         
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* PROCESS A RECOVERY FILE RECORD                                      *         
***********************************************************************         
                                                                                
PROCRCV  NTR1  ,                                                                
         L     R2,PCBINPUT                                                      
         USING RCVRECD,R2          R2=A(RECOVERY HEADER)                        
         CLI   RCVRECTY,RCVRADDQ   TAKE ONLY COPIES & CHANGES                   
         BE    XNO                                                              
         CLI   RCVFILTY,RCVFAMST   TEST ACCMST RECORD                           
         BNE   XNO                                                              
*                                                                               
         LA    R3,RCVRECRD                                                      
         USING TRNRECD,R3          R3=A(TRANSACTION KEY)                        
         CLI   TRNKUNT,C'S'        MUST BE UNIT 'S'                             
         BNE   XNO                                                              
*                                                                               
         LA    R6,TRNRFST          MUST BE A TRANSACTION                        
         USING TRNELD,R6           R6=A(TRANSACTION ELEMENT)                    
         CLI   TRNEL,TRNELQ                                                     
         BNE   XNO                                                              
         CLC   TRNAMNT,SPACES      ?????                                        
         BE    XNO                                                              
*                                                                               
         XC    PCBRTN(PCBRTNL),PCBRTN   CLEAR RETURN BLOCK                      
*                                                                               
         MVC   PCBACSE,RCVSYSSE    ACC SE NUMBER                                
         ZAP   PCBAMNT,TRNAMNT     SET TRANSACTION AMOUNT                       
         CLC   TRNKUNT(2),=C'SC'   TEST CASH RECONCILIATION                     
         BNE   PROC05                                                           
         LA    R1,TRNKCUNT         CONTRA IS PAYABLE LEDGER                     
         BAS   RE,LDGFLT           FILTER BY CONTRA LEDGER                      
         BNE   XNO                 SKIP IT                                      
         BAS   RE,CKREC            CHECK RECONCILIATION ITEM                    
         BNE   XNO                 SKIP IT                                      
         L     R3,AIO1             R3=A(PAYABLE TRANSACTION)                    
         LA    R6,TRNRFST          R6=TRANSACTION ELEMENT                       
         B     PROC07                                                           
*                                                                               
PROC05   LA    R1,TRNKUNT                                                       
         BAS   RE,LDGFLT           FILTER PAYABLE LEDGERS                       
         BNE   XNO                                                              
         TM    TRNSTAT,TRNSDR      SKIP DEBITS                                  
         BO    XNO                                                              
         BAS   RE,PAYBL            PROCESS PAYABLE ITEM                         
         BNE   XNO                                                              
*                                                                               
PROC07   BAS   RE,GETCLR           GET CLEARANCE DATE                           
         BNE   XNO                                                              
         BAS   RE,MEDCLI           GET MEDIA & CLIENT CODES                     
         BAS   RE,AGYSE            GET AGENCY CODE, SE NUMBER                   
         BNE   XNO                                                              
         B     XYES                                                             
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
***********************************************************************         
* PROCESS AN ACCOUNT FILE TRANSACTION                                 *         
***********************************************************************         
                                                                                
PROCACC  NTR1  ,                                                                
         L     R3,PCBINPUT                                                      
         USING TRNRECD,R3          R3=A(TRANSACTION KEY)                        
         CLI   TRNKUNT,C'S'        MUST BE UNIT 'S'                             
         BNE   XNO                                                              
*                                                                               
         LA    R6,TRNRFST          MUST BE A TRANSACTION                        
         USING TRNELD,R6           R6=A(TRANSACTION ELEMENT)                    
         CLI   TRNEL,TRNELQ                                                     
         BNE   XNO                                                              
*                                                                               
         XC    PCBRTN(PCBRTNL),PCBRTN   CLEAR RETURN BLOCK                      
*                                                                               
         L     R5,AUTL                                                          
         MVC   PCBACSE,4(R5)       ACC SE NUMBER                                
         ZAP   PCBAMNT,TRNAMNT     SET TRANSACTION AMOUNT                       
         CLC   TRNKUNT(2),=C'SC'   TEST CASH RECONCILIATION                     
         BNE   PROCA05                                                          
         LA    R1,TRNKCUNT         CONTRA IS PAYABLE LEDGER                     
         BAS   RE,LDGFLT           FILTER BY CONTRA LEDGER                      
         BNE   XNO                 SKIP IT                                      
         BAS   RE,ACKREC           CHECK RECONCILIATION ITEM                    
         BNE   XNO                 SKIP IT                                      
         L     R3,AIO1             R3=A(PAYABLE TRANSACTION)                    
         LA    R6,TRNRFST          R6=TRANSACTION ELEMENT                       
         B     PROCA07                                                          
*                                                                               
PROCA05  LA    R1,TRNKUNT                                                       
         BAS   RE,LDGFLT           FILTER PAYABLE LEDGERS                       
         BNE   XNO                                                              
         TM    TRNSTAT,TRNSDR      SKIP DEBITS                                  
         BO    XNO                                                              
         BAS   RE,APAYBL            PROCESS PAYABLE ITEM                        
         BNE   XNO                                                              
*                                                                               
PROCA07  BAS   RE,GETCLR           GET CLEARANCE DATE                           
         BNE   XNO                                                              
         BAS   RE,MEDCLI           GET MEDIA & CLIENT CODES                     
         BAS   RE,AGYSE            GET AGENCY CODE, SE NUMBER                   
         BNE   XNO                                                              
         B     XYES                                                             
         DROP  R3,R6                                                            
         EJECT                                                                  
***********************************************************************         
* FILTER PAYABLE LEDGERS                                              *         
***********************************************************************         
                                                                                
LDGFLT   LA    RF,LDGTAB                                                        
LDGFLT3  CLC   0(2,R1),0(RF)       TEST UNIT/LEDGER MATCH                       
         BE    LDGFLT5                                                          
         LA    RF,L'LDGTAB(RF)                                                  
         CLI   0(RF),EOT                                                        
         BNE   LDGFLT3                                                          
         LTR   RB,RB                                                            
         BR    RE                                                               
*                                                                               
LDGFLT5  OC    PCBTYPE,2(RF)       SET LEDGER TYPE                              
         MVC   PCBLDGR,1(RF)       LEDGER CODE                                  
         CR    RB,RB                                                            
         BR    RE                                                               
*                                                                               
LDGTAB   DS    0XL3                                                             
         DC    C'SP',AL1(PCBTPRT)                                               
         DC    C'SQ',AL1(PCBTPRT)                                               
         DC    C'SU',AL1(PCBTSPT)                                               
         DC    C'SS',AL1(PCBTSPT)                                               
         DC    C'ST',AL1(PCBTSPT)                                               
         DC    AL1(EOT)                                                         
         EJECT                                                                  
***********************************************************************         
* CHECK RECONCILIATION ITEM - RECOVERY FILE                           *         
***********************************************************************         
         SPACE 1                                                                
         USING RCVRECD,R2          R2=A(RECOVERY HEADER)                        
         USING TRNRECD,R3          R3=A(TRANSACTION RECORD - SC)                
         USING TRNELD,R6           R6=A(TRANSACTION ELEMENT)                    
CKREC    NTR1  ,                                                                
         OC    RCVPRGNO,RCVPRGNO   OFFLINE EOD JOB                              
         BZ    CKREC10             DOESNT HAVE PRG NUMBER                       
         CLI   RCVPRGNO,RCVPMRKQ   TEST MARKER PROGRAM                          
         BNE   XNO                                                              
CKREC10  CLI   TRNTYPE,37          SKIP MARKER VOIDS                            
         BE    XNO                                                              
*                                                                               
         CLI   RCVRECTY,RCVRCPYQ   TEST COPY                                    
         BNE   CKREC40                                                          
         MVC   OLDSTAT,TRNSTAT     SAVE OLD STATUS                              
         XC    OLDPDDT,OLDPDDT     CLEAR OLD PAID DATE FIELD                    
         LR    R4,R6               SKIP TO GENERAL DATE ELEMENT                 
CKREC20  CLI   0(R4),0             EOR?                                         
         BE    XNO                                                              
         CLI   0(R4),GDAELQ        GET GENERAL DATE ELEMENT                     
         BE    CKREC30                                                          
CKREC25  SR    R1,R1                                                            
         IC    R1,1(R4)                                                         
         AR    R4,R1                                                            
         B     CKREC20                                                          
*                                                                               
         USING GDAELD,R4                                                        
CKREC30  CLI   GDATYPE,GDATRECN    DO WE HAVE THE RIGHT ONE?                    
         BNE   CKREC25                                                          
         MVC   OLDPDDT,GDADATE2                                                 
         B     XNO                                                              
         DROP  R4                                                               
*                                                                               
CKREC40  CLI   RCVRECTY,RCVRCHAQ   TEST CHANGE                                  
         BNE   XNO                                                              
*                                                                               
         XC    NEWPDDT,NEWPDDT     CLEAR NEW PAID CHECK DATE                    
         LR    R4,R6               SKIP TO GENERAL DATE ELEMENT                 
CKREC50  CLI   0(R4),0             EOR?                                         
         BE    CKREC70                                                          
         CLI   0(R4),GDAELQ        GET GENERAL DATE ELEMENT                     
         BE    CKREC60                                                          
CKREC55  SR    R1,R1                                                            
         IC    R1,1(R4)                                                         
         AR    R4,R1                                                            
         B     CKREC50                                                          
*                                                                               
         USING GDAELD,R4                                                        
CKREC60  CLI   GDATYPE,GDATRECN    DO WE HAVE THE RIGHT ONE?                    
         BNE   CKREC55                                                          
         MVC   NEWPDDT,GDADATE2    SAVE OFF NEW PAID (CLEAR) DATE               
         DROP  R4                                                               
*                                                                               
CKREC70  OC    NEWPDDT,NEWPDDT     ANY NEW PAID DATE?                           
         BNZ   *+14                                                             
         OC    OLDPDDT,OLDPDDT     ANY OLD PAID DATE?                           
         BZ    XNO                 IF NEITHER EXIT                              
*                                                                               
         MVC   NEWSTAT,TRNSTAT                                                  
         NI    OLDSTAT,TRNSBREC    ONLY INTERESTED IN RECONCILED                
         NI    NEWSTAT,TRNSBREC                                                 
         CLC   OLDSTAT,NEWSTAT     TEST CHANGE OF STATUS                        
         BNE   *+14                IF EQUAL CHECK PAID DATES FOR =REQ           
         CLC   OLDPDDT,NEWPDDT     IF DATES ARE THE SAME-SKIP                   
         BE    XNO                                                              
*                                                                               
         TM    NEWSTAT,TRNSBREC    RECON BIT IS ON NOW                          
         BNO   *+8                                                              
         OI    PCBCKSTA,TRNSBREC   TURN ON RECON BIT IN RECORD                  
*                                                                               
         MVC   PCBCKNUM,TRNKREF                                                 
         GOTO1 DATCON,DMCB,(1,TRNKDATE),(2,PCBCKDTE)                            
*                                                                               
         OC    NEWPDDT,NEWPDDT     ANY PAID DATE?                               
         BZ    CKREC80                                                          
         GOTO1 DATCON,DMCB,(1,NEWPDDT),(2,PCBPDDTE)                             
         OI    PCBCKSTA,PCBBKCLR   TURN ON CLEARED BIT IN RECORD                
CKREC80  OI    PCBTYPE,PCBTREC     SET TYPE FOR CASH RECONCILIATION             
*                                                                               
         L     R5,AUTL                                                          
         MVC   SAVSE,4(R5)         SAVE CURRENT SE NUMBER                       
         MVC   4(1,R5),RCVSYSSE                                                 
*                                                                               
         BAS   RE,OPNACC           MAKE SURE ACC FILE IS OPENED                 
*                                                                               
         BAS   RE,GACC             GET INFO FROM PAYABLE RECORD                 
         MVC   4(1,R5),SAVSE       RESTORE SE                                   
         BNE   XNO                                                              
         B     XYES                                                             
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
***********************************************************************         
* CHECK RECONCILIATION ITEM - ACCOUNT FILE                            *         
***********************************************************************         
                                                                                
         USING TRNRECD,R3          R3=A(TRANSACTION RECORD - SC)                
         USING TRNELD,R6           R6=A(TRANSACTION ELEMENT)                    
ACKREC   NTR1  ,                                                                
         CLI   TRNTYPE,37          SKIP MARKER VOIDS                            
         BE    XNO                                                              
*                                                                               
         TM    TRNSTAT,TRNSBREC    TEST RECONCILED                              
         BNO   *+8                                                              
         OI    PCBCKSTA,TRNSBREC   TURN ON RECON BIT IN RECORD                  
*                                                                               
         MVC   PCBCKNUM,TRNKREF                                                 
         GOTO1 DATCON,DMCB,(1,TRNKDATE),(2,PCBCKDTE)                            
*                                                                               
         LR    R4,R6               SKIP TO GENERAL DATE ELEMENT                 
ACKREC10 CLI   0(R4),0             EOR?                                         
         BE    ACKREC30                                                         
         CLI   0(R4),GDAELQ        GET GENERAL DATE ELEMENT                     
         BE    ACKREC20                                                         
ACKREC15 SR    R1,R1                                                            
         IC    R1,1(R4)                                                         
         AR    R4,R1                                                            
         B     ACKREC10                                                         
*                                                                               
         USING GDAELD,R4                                                        
ACKREC20 CLI   GDATYPE,GDATRECN    DO WE HAVE THE RIGHT ONE?                    
         BNE   ACKREC15                                                         
         OC    GDADATE2,GDADATE2   ANY DATE?                                    
         BZ    ACKREC30                                                         
         GOTO1 DATCON,DMCB,(1,GDADATE2),(2,PCBPDDTE)                            
         OI    PCBCKSTA,PCBBKCLR   TURN ON CLEARED BIT IN RECORD                
*                                                                               
ACKREC30 OI    PCBTYPE,PCBTREC     SET TYPE FOR CASH RECONCILIATION             
*                                                                               
         BAS   RE,GACC             GET INFO FROM PAYABLE RECORD                 
         B     XYES                                                             
         DROP  R3,R4,R6                                                         
         EJECT                                                                  
***********************************************************************         
* READ ACC FILE & GET INFO FROM PAYABLE RECORDS                       *         
***********************************************************************         
                                                                                
         USING TRNRECD,R3                                                       
GACC     NTR1  ,                                                                
         MVI   LOCLRDTE,X'FF'      FORCE UPDATE OF FIRST CLEARANCE              
         MVC   DKEY,SPACES                                                      
         MVC   DKEY(L'ACTKCULA),TRNKCULC  CONTRA OF SC TRANSACTION              
         MVC   SVULA,TRNKULA       SAVE OFF U/L/A FOR LATER                     
         GOTO1 ADMGR,ACCRD         READ ACCOUNT RECORD                          
         BNE   XNO                                                              
*                                                                               
GACC10   LA    R3,DIR                                                           
         CLC   DKEY(L'ACTKCULA),TRNKCULA  TEST SAME ACCOUNT                     
         BNE   GACC60                                                           
         CLC   TRNKDATE,SPACES     TRANSACTIONS MUST HAVE DATE                  
         BNH   GACC50                                                           
         MVC   AIO,AIO1            SET IO AREA                                  
         L     R3,AIO                                                           
*                                                                               
         GOTO1 ADMGR,ACCGET        GET RECORD                                   
         LA    R6,TRNRFST                                                       
         CLI   0(R6),TRNELQ        TEST TRANSACTION                             
         BNE   GACC50                                                           
         USING TRNELD,R6                                                        
         TM    TRNSTAT,TRNSDR      SKIP DEBITS                                  
         BO    GACC20                                                           
*                                                                               
         ZAP   PKAMNT,TRNAMNT      SAVE OFF TRNSACTION AMOUNT                   
*                                                                               
         MVI   ELCODE,MPYELQ       GET MEDIA PAYMENT ELEMENT                    
         BAS   RE,NEXTEL                                                        
         BNE   GACC50                                                           
         USING MPYELD,R6                                                        
         CLC   MPYNO,PCBCKNUM      TEST CHECK NUMBER                            
         BNE   GACC50                                                           
         CLC   MPYBNK,SVULA        TEST SAME SC ACCOUNT                         
         BNE   GACC50                                                           
*                                                                               
         BAS   RE,GETCLR           GET CLEARANCE DATE                           
         BNE   GACC20              NO DATE                                      
         CLC   PCBCLRDT,LOCLRDTE   TEST LOWER DATE                              
         BNL   GACC20                                                           
         MVC   LOCLRDTE,PCBCLRDT   SET NEW LOW DATE                             
         MVC   LOKEY,TRNKEY        AND KEY                                      
*                                                                               
GACC20   TM    PCBTYPE,PCBTREC     SET TYPE FOR CASH RECONCILIATION             
         BNO   GACC50                                                           
         LA    R6,TRNRFST                                                       
         BAS   RE,MEDCLI           GET MEDIA & CLIENT CODES                     
*                                                                               
         LA    R6,TRNRFST          RESET FOR GETEL                              
         MVI   ELCODE,XPYELQ       GET EXTRA PAYMENT ELEMENT                    
         BAS   RE,NEXTEL                                                        
         BNE   GACC50              MUST HAVE ELEMENT                            
*                                                                               
         USING XPYELD,R6                                                        
         OC    XPYAGY,XPYAGY       TEST AGENCY CODE                             
         BZ    GACC50                                                           
         TM    XPYSTA,XPYSMOV      SPOT 'MOVED' BUY ?                           
         BO    GACC50              YES, SKIP IT                                 
*                                                                               
         MVC   PCBSEQN,XPYSEQ      SEQUENCE NUMBER                              
         MVC   PCBINV#,XPYINV      INVOICE NUMBER                               
         MVC   PCBPRO,TRNKREF      PRODUCT 1                                    
         MVC   PCBEST,XPYEST       ESTIMATE #                                   
         MVC   PCBBMKT,SPACES      CLEAR BINARY MARKET FIELD                    
         MVC   PCBPRO2,SPACES      CLEAR PRODUCT 2 FIELD                        
         CLI   XPYLN,XPYLN2Q       DOES XPYELD CONTAIN MKT#                     
         BL    GACC30                                                           
         MVC   PCBBMKT,XPYBMKT     BINARY MARKET NUMBER                         
         CLI   XPYLN,XPYLN3Q       DOES XPYELD CONTAIN PROD2                    
         BL    GACC30                                                           
         MVC   PCBPRO2,XPYPRO2     PRODUCT 2                                    
*                                                                               
         USING SEQTBD,RE                                                        
GACC30   L     RE,PCBSQTB          FIND NEXT OPEN SLOT                          
         LA    R0,SEQTMAX                                                       
GACC40   CLI   0(RE),X'FF'         X'FF' - SIGNIFIES EOT                        
         BE    *+14                                                             
         LA    RE,SEQTLNQ(RE)      BUMP TO NEXT ENTRY                           
         BCT   R0,GACC40                                                        
         DC    H'0'                MUST FIT                                     
*                                                                               
         MVC   SEQNUM,PCBSEQN      SEQUENCE NUMBER                              
         ZAP   SEQAMNT,PKAMNT      TRANSACTION AMOUNT                           
         MVC   SEQCLRDT,PCBCLRDT   BANK CLEARED DATE                            
         MVC   SEQINV#,PCBINV#     INVOICE NUMBER                               
         MVC   SEQPRO,PCBPRO       PRODUCT 1                                    
         MVC   SEQPRO2,PCBPRO2     PRODUCT 2                                    
         MVC   SEQEST,PCBEST       ESTIMATE #                                   
         MVC   SEQBMKT,PCBBMKT     BINARY MARKET NUMBER                         
         TM    PCBTYPE,PCBTPRT     ARE WE DOING PRINT?                          
         BNO   *+20                                                             
         MVC   SEQPUB,PCBKPUB      PRINT PUB                                    
         MVC   SEQPCLI,PCBKPCLT    PRINT CLIENT                                 
         B     *+16                                                             
         MVC   SEQSTA,PCBKSTN      SPOT/NET STATION                             
         MVC   SEQSCLI,PCBKSCLT    SPOT CLIENT                                  
*                                                                               
         LA    RE,SEQTLNQ(RE)      BUMP TO NEXT ENTRY                           
         MVI   0(RE),X'FF'         SET NEW END OF TABLE                         
*                                                                               
GACC50   GOTO1 ADMGR,ACCSEQ        GET NEXT RECORD                              
         B     GACC10                                                           
*                                                                               
GACC60   CLI   LOCLRDTE,X'FF'      TEST ANYTHING FOUND                          
         BE    XNO                                                              
         MVC   DKEY,LOKEY          GET FIRST CLEARANCE FOR THIS CHECK           
         GOTO1 ADMGR,ACCRD                                                      
         BNE   XNO                                                              
         GOTO1 ADMGR,ACCGET        GET RECORD                                   
         B     XYES                                                             
         DROP  R3,R6                                                            
         EJECT                                                                  
***********************************************************************         
* OPEN ACCOUNT FILE                                                   *         
***********************************************************************         
                                                                                
OPNACC   LR    R0,RE                                                            
         LA    R1,ACOPNSE          LIST OF OPENED ACC FILES                     
         L     RF,AUTL                                                          
OPNACC3  CLC   4(1,RF),0(R1)       MATCH SE TO LIST                             
         BE    OPNACCX             IT'S OK                                      
         LA    R1,1(R1)                                                         
         CLI   0(R1),0                                                          
         BNE   OPNACC3                                                          
         MVC   0(1,R1),4(RF)       UPDATE THE LIST                              
         GOTO1 ADMGR,ACCOPN        OPEN THE ACC FILE                            
OPNACCX  LR    RE,R0                                                            
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* PROCESS PAYABLE LEDGERS - RECOVERY FILE                             *         
***********************************************************************         
                                                                                
         USING RCVRECD,R2          R2=A(RECOVERY HEADER)                        
         USING TRNRECD,R3          R3=A(TRANSACTION RECORD)                     
         USING TRNELD,R6           R6=A(TRANSACTION ELEMENT)                    
PAYBL    NTR1  ,                                                                
         CLI   RCVRECTY,RCVRCPYQ   TEST COPY                                    
         BNE   PAYBL3                                                           
         MVC   OLDCHKN,SPACES                                                   
         XC    OLDCHKD,OLDCHKD                                                  
         MVI   ELCODE,MPYELQ       GET MEDIA PAYMENT ELEMENT                    
         BAS   RE,NEXTEL                                                        
         BNE   XNO                                                              
         USING MPYELD,R6                                                        
         MVC   OLDCHKN,MPYNO       SAVE OLD CHECK NUMBER                        
         MVC   OLDCHKD,MPYDTE      AND DATE                                     
         B     XNO                                                              
*                                                                               
PAYBL3   CLI   RCVRECTY,RCVRCHAQ   TEST CHANGE                                  
         BNE   XNO                                                              
         MVI   ELCODE,MPYELQ       GET MEDIA PAYMENT ELEMENT                    
         BAS   RE,NEXTEL                                                        
         BNE   XNO                                                              
         USING MPYELD,R6                                                        
         CLC   OLDCHKN,MPYNO       TEST CHECK NUMBER CHANGED                    
         BE    XNO                 NO, SKIP IT                                  
         CLC   OLDCHKN,SPACES      WAS THERE AN OLD CHECK NUMBER                
         BE    PAYBL5              NO OLD CHECK                                 
         MVC   PCBCKNUM,SPACES     CLEAR CHECK NUMBER                           
         XC    PCBCKDTE,PCBCKDTE   AND DATE                                     
         OI    PCBTYPE,PCBTVOD     SET "VOID"                                   
         B     XYES                KEEP RECORD                                  
*                                                                               
PAYBL5   MVC   PCBCKNUM,MPYNO      CHECK NUMBER                                 
         MVC   PCBCKDTE,MPYDTE     AND DATE                                     
         CLI   RCVPRGNO,RCVPMRKQ   TEST MARKER PROGRAM                          
         BNE   *+12                                                             
         OI    PCBTYPE,PCBTUNV     "UNVOID" MANUAL CHECK?                       
         B     *+8                                                              
         OI    PCBTYPE,PCBTCHK     SET "CHECK"                                  
         B     XYES                                                             
         DROP  R2,R3,R6                                                         
         EJECT                                                                  
***********************************************************************         
* PROCESS PAYABLE LEDGERS  - ACCOUNT FILE                             *         
***********************************************************************         
                                                                                
         USING TRNRECD,R3          R3=A(TRANSACTION RECORD)                     
         USING TRNELD,R6           R6=A(TRANSACTION ELEMENT)                    
APAYBL   NTR1  ,                                                                
         MVI   ELCODE,MPYELQ       GET MEDIA PAYMENT ELEMENT                    
         BAS   RE,NEXTEL                                                        
         BNE   XNO                                                              
         USING MPYELD,R6                                                        
         MVC   PCBCKNUM,MPYNO      CHECK NUMBER                                 
         MVC   PCBCKDTE,MPYDTE     AND DATE                                     
         OI    PCBTYPE,PCBTCHK     SET "CHECK"                                  
         B     XYES                                                             
         DROP  R3,R6                                                            
         EJECT                                                                  
***********************************************************************         
* GET CLEARANCE DATE                                                  *         
***********************************************************************         
                                                                                
         USING TRNRECD,R3                                                       
GETCLR   NTR1  ,                                                                
         XC    PCBCLRDT,PCBCLRDT                                                
         LA    R6,TRNRFST          ELSE USE TRANSACTION STATUS ELEM             
         MVI   ELCODE,TRSELQ                                                    
         USING TRSELD,R6           R6=A(TRANSACTION STATUS ELEMENT)             
         BAS   RE,NEXTEL                                                        
         BNE   GETCLR3                                                          
         MVC   PCBCLRDT,TRSDATE    CLEARANCE DATE (DATE ADDED TO FILE)          
         GOTO1 DATCON,DMCB,(2,TRSDATE),(1,WORK)                                 
*                                                                               
         LA    R6,TRNRFST                                                       
         MVI   ELCODE,GDAELQ                                                    
GETCLR3  BAS   RE,NEXTEL           USE GENERAL DATE ELEMENT                     
         BNE   GETCLR7                                                          
         USING GDAELD,R6           R6=A(GENERAL DATE ELEMENT)                   
         CLI   GDATYPE,GDATCLR     IS THIS A CLEARANCE DATE                     
         BNE   GETCLR3                                                          
         CLC   TRNKUNT(2),=C'SP'   FOR SOME PRINT CLEARANCES                    
         BE    *+14                                                             
         CLC   TRNKUNT(2),=C'SQ'                                                
         BNE   GETCLR5                                                          
         CLC   WORK(2),=X'9906'     FOR JUN & JUL '99                           
         BL    GETCLR5                                                          
         CLC   WORK(2),=X'9907'                                                 
         BH    GETCLR5                                                          
         B     GETCLR7              IGNORE THE GDAEL (IT'S WRONG)               
*                                                                               
GETCLR5  GOTO1 DATCON,DMCB,(1,GDADATE),(2,PCBCLRDT)                             
*                                                                               
GETCLR7  OC    PCBCLRDT,PCBCLRDT                                                
         BZ    XNO                                                              
         B     XYES                                                             
*                                                                               
         DROP  R3,R6                                                            
         EJECT                                                                  
***********************************************************************         
* GET MEDIA - CLIENT CODES                                            *         
***********************************************************************         
                                                                                
         USING TRNRECD,R3          R3=A(TRANSACTION RECORD)                     
         USING TRNELD,R6           R6=A(TRANSACTION ELEMENT)                    
MEDCLI   NTR1  ,                                                                
         CLI   TRNTYPE,X'22'       TEST SPOT REP                                
         BNE   *+8                                                              
         OI    PCBTYPE,PCBTREP                                                  
         CLI   TRNTYPE,X'32'       TEST PRINT REP                               
         BNE   *+8                                                              
         OI    PCBTYPE,PCBTREP                                                  
*                                                                               
         TM    PCBTYPE,PCBTPRT     TEST PRINT                                   
         BNO   MEDCLI3                                                          
         MVC   PCBKPCLT,TRNKCACT+9 PRINT CLIENT                                 
*                                  MEDIA/PUB                                    
         LA    RF,TRNKACT          RF=A(MEDIA/PUB)                              
         TM    PCBTYPE,PCBTREP     TEST REP                                     
         BNO   *+8                                                              
         LA    RF,TRNKCCPY         FOR REPS MEDIA/PUB IS IN CONTRA              
         MVC   PCBKMED,0(RF)                                                    
         XC    WORK(10),WORK                                                    
         MVC   WORK(8),1(RF)       NO ZONE EDITION IN THIS KEY                  
         GOTO1 PCBPUBV,DMCB,WORK,PCBKPUB                                        
         B     XYES                                                             
*                                                                               
MEDCLI3  GOTO1 PCBCLPK,DMCB,TRNKCACT+9,PCBKSCLT SPOT CLIENT                     
         LA    RF,TRNKACT          SET MEDIA CODE/STATION                       
         TM    PCBTYPE,PCBTREP                                                  
         BNO   *+8                                                              
         LA    RF,TRNKCACT                                                      
         MVC   PCBKMED,0(RF)       SPOT MEDIA                                   
         MVC   PCBKSTN,1(RF)          STATION                                   
         CLI   PCBKMED,C'T'                                                     
         BNE   *+16                                                             
         CLI   PCBKSTN+4,C' '                                                   
         BH    *+8                                                              
         MVI   PCBKSTN+4,C'T'                                                   
         B     XYES                                                             
         DROP  R3,R6                                                            
         EJECT                                                                  
***********************************************************************         
* GET AGENCY ID AND SE NUMBER                                         *         
***********************************************************************         
                                                                                
         USING TRNRECD,R3                                                       
AGYSE    NTR1  ,                                                                
         LA    R6,TRNRFST                                                       
         MVI   ELCODE,XPYELQ       GET EXTRA PAYMENT ELEMENT                    
         BAS   RE,NEXTEL                                                        
         BNE   XNO                 MUST HAVE ELEMENT                            
*                                                                               
         USING XPYELD,R6                                                        
         OC    XPYAGY,XPYAGY       TEST AGENCY CODE                             
         BZ    XNO                                                              
         TM    XPYSTA,XPYSMOV      SPOT 'MOVED' BUY ?                           
         BO    XNO                 YES, SKIP IT                                 
*                                                                               
         MVC   PCBKALPH,XPYAGY     ALPHA AGENCY                                 
         MVC   PCBSEQN,XPYSEQ      SEQUENCE NUMBER                              
         MVC   PCBINV#,XPYINV      INVOICE NUMBER                               
         MVC   PCBPRO,TRNKREF      PRODUCT 1                                    
         MVC   PCBEST,XPYEST       ESTIMATE #                                   
         MVC   PCBBMKT,SPACES      CLEAR BINARY MARKET FIELD                    
         MVC   PCBPRO2,SPACES      CLEAR PRODUCT 2 FIELD                        
         CLI   XPYLN,XPYLN2Q       DOES XPYELD CONTAIN MKT#                     
         BL    AGYSE10                                                          
         MVC   PCBBMKT,XPYBMKT     BINARY MARKET NUMBER                         
         CLI   XPYLN,XPYLN3Q       DOES XPYELD CONTAIN PROD2                    
         BL    AGYSE10                                                          
         MVC   PCBPRO2,XPYPRO2     PRODUCT 2                                    
*                                                                               
AGYSE10  L     R5,PCBIDT           R5=A(START OF TABLE)                         
         SR    R6,R6                                                            
         ICM   R6,3,PCBIDTL                                                     
         AR    R6,R5               R6=A(END OF TABLE)                           
*                                                                               
         USING IDD,R5                                                           
AGYSE20  CLI   0(R5),EOT           TEST END OF LIST                             
         BE    AGYSE30                                                          
         CLC   IDALPH,PCBKALPH                                                  
         BE    AGYSE40                                                          
         LA    R5,IDLNQ(R5)                                                     
         B     AGYSE20             ID TABLE FULL                                
*                                                                               
AGYSE30  BAS   RE,ACCESS           READ ACCESS RECORD FOR SE NUMBERS            
*                                                                               
AGYSE40  LA    RF,IDPRSE           SET FOR PRINT                                
         MVI   PCBKSYS,PCBKSYPR                                                 
         TM    PCBTYPE,PCBTPRT                                                  
         BO    AGYSE50                                                          
         MVI   PCBKSYS,PCBKSYSP                                                 
         LA    RF,IDSPSE           SET FOR SPOT                                 
         CLI   PCBKMED,C'N'        IS THE MEDIA NETWORK                         
         BNE   AGYSE50                                                          
         CLI   IDCTRY,C'C'         IS THE COUNTRY CANADA                        
         BE    AGYSE50             NO NETWORK IN CANADA                         
         CLI   IDNESE,0            IS THERE A NETWORK SE                        
         BE    AGYSE50                                                          
         LA    RF,IDNESE           MUST BE NETWORK                              
         MVI   PCBKSYS,PCBKSYNE                                                 
AGYSE50  MVC   PCBKSEN,0(RF)       MEDIA SE NUMBER                              
         B     XYES                                                             
         DROP  R3,R5,R6                                                         
         EJECT                                                                  
***********************************************************************         
* READ CONTROL FILE FOR SE NUMBERS                                    *         
***********************************************************************         
                                                                                
         USING IDD,R5                                                           
ACCESS   NTR1  ,                                                                
         XC    IDD(IDLNQ),IDD      CLEAR LAST ENTRY                             
         MVI   IDLNQ(R5),EOT       MARK NEW END OF TABLE                        
         MVC   IDALPH,PCBKALPH     ADD ALPHA TO TABLE                           
         MVI   IDCTRY,C'U'         DEFAULT COUNTRY                              
*                                                                               
         MVC   AIO,AIO2                                                         
         L     R2,AIO                                                           
         USING CT5REC,R2                                                        
         XC    CT5KEY,CT5KEY                                                    
         MVI   CT5KTYP,CT5KTYPQ                                                 
         MVC   CT5KALPH,PCBKALPH                                                
         MVC   DKEY,0(R2)                                                       
         GOTO1 ADMGR,CONRD                                                      
         BE    *+6                                                              
         DC    H'0'                CAN'T READ ACCESS RECORD                     
*                                                                               
         SR    R0,R0                                                            
         LA    R6,CT5DATA                                                       
*                                                                               
ACCESS3  CLI   0(R6),0             TEST EOR                                     
         BE    XYES                                                             
         CLI   0(R6),CTSYSELQ      SYSTEM ELEMENT                               
         BE    ACCESS7                                                          
         CLI   0(R6),CTAGDELQ                                                   
         BE    ACCESS11                                                         
ACCESS5  IC    R0,1(R6)                                                         
         AR    R6,R0                                                            
         B     ACCESS3                                                          
*                                                                               
         USING CTSYSD,R6                                                        
ACCESS7  LA    RF,IDSPSE                                                        
         CLI   CTSYSNUM,SENSPOT    TEST SPOT                                    
         BE    ACCESS9                                                          
         LA    RF,IDNESE                                                        
         CLI   CTSYSNUM,SENNET     TEST NET                                     
         BE    ACCESS9                                                          
         LA    RF,IDPRSE                                                        
         CLI   CTSYSNUM,SENPRNT    TEST PRINT                                   
         BE    ACCESS9                                                          
         LA    RF,IDACSE                                                        
         CLI   CTSYSNUM,SENACCT    TEST ACCOUNT                                 
         BNE   ACCESS5                                                          
ACCESS9  MVC   0(1,RF),CTSYSSE     SYSTEM SE NUMBER                             
         B     ACCESS5                                                          
*                                  FIND COUNTRY CODE                            
         USING CTAGDD,R6                                                        
ACCESS11 CLI   CTAGDLEN,CTAGDCTY-CTAGDD                                         
         BL    ACCESS5                                                          
         TM    CTAGDCTY,X'08'      IS IT CANADA                                 
         BNO   *+8                                                              
         MVI   IDCTRY,C'C'         SET COUNTRY CODE                             
         B     ACCESS5                                                          
*                                                                               
         DROP  R2,R5,R6                                                         
         EJECT                                                                  
***********************************************************************         
* OTHER ROUTINES -  EXITS                                             *         
***********************************************************************         
         SPACE 1                                                                
         GETEL R6,DATADISP,ELCODE                                               
         SPACE 2                                                                
*                                                                               
XNO      LTR   RB,RB                                                            
         B     XIT                                                              
XYES     CR    RB,RB                                                            
XIT      XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* CONSTANTS AND LITERAL POOL                                          *         
***********************************************************************         
                                                                                
ADMGR    DC    A(DMGR)                                                          
DATADISP DC    Y(ACTRFST-ACTRECD)                                               
SPACES   DC    CL132' '                                                         
*                                                                               
OLDSTAT  DC    X'00'               STATUS BYTE FROM COPY                        
NEWSTAT  DC    X'00'                                                            
OLDCHKN  DC    CL6' '              CHECK NUMBER FROM COPY                       
OLDCHKD  DC    XL2'00'             CHECK DATE FROM COPY                         
OLDPDDT  DC    PL3'00'             CHECK PAID DATE FROM COPY (CLEAR)            
NEWPDDT  DC    PL3'00'             CHECK PAID DATE FROM CHG (CLEAR)             
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* DATA MANAGER AND WORKER FILE ROUTINES                               *         
***********************************************************************         
                                                                                
DMGR     NMOD1 0,*DMGR*                                                         
         L     RF,4(RD)                                                         
         L     RC,68(RF)           RESTORE RC                                   
*                                                                               
         STC   R1,DMFLG            SAVE COMMAND CODE                            
         SLL   R1,2                                                             
         B     *(R1)                                                            
         B     DCONRD              READ CONTROL FILE                            
*                                                                               
         B     DACCOPN             OPEN ACCOUNT FILE                            
         B     DACCRD              READ ACCDIR                                  
         B     DACCHI              READ HIGH ACCDIR                             
         B     DACCSEQ             READ SEQ ACCDIR                              
         B     DACCGET             GETREC ACCMST                                
*                                                                               
         B     DSPTOPN             OPEN SPOT FILES                              
         B     DSPTRD              READ SPOTDIR                                 
         B     DSPTHI              READ HIGH SPOTDIR                            
         B     DSPTSEQ             READ SEQ SPOTDIR                             
         B     DSPTGET             GETREC SPOT FILE                             
         B     DSTARD              READ STATION FILE                            
         B     DSTAHI              READ HIGH STATION FILE                       
*                                                                               
         B     DPRTOPN             OPEN PRINT FILES                             
         B     DPRTRD              READ PRINT DIR                               
         B     DPRTHI              READ HIGH PRINT DIR                          
         B     DPRTSEQ             READ SEQ PRINT DIR                           
         B     DPRTGET             GETREC PRINT FILE                            
         B     DPUBRD              READ PUBFILE                                 
         B     DPUBHI              READ HIGH PUBFILE                            
         B     DPUBSEQ             READ SEQUENTIAL PUBFILE                      
         B     DPUBGET             GETREC PUBFILE                               
*                                                                               
DCONRD   GOTO1 DATAMGR,DMCB,DMREAD,CTFILE,DKEY,AIO   READ CONTROL               
         B     DMERR                                                            
         EJECT                                                                  
***********************************************************************         
* DATA MANAGER ROUTINES - ACCOUNT FILES                               *         
***********************************************************************         
                                                                                
DACCOPN  GOTO1 DATAMGR,DMCB,OPEN,ACCOUNT,ACFILEL     OPEN ACCOUNT               
         B     DMXIT                                                            
*                                                                               
DACCRD   GOTO1 DATAMGR,DMCB,DMREAD,ACCDIR,DKEY,DIR   READ ACC DIR               
         MVC   DA,DIR+(ACCKDA-ACCRECD)                                          
         B     DMERR                                                            
*                                                                               
DACCHI   GOTO1 DATAMGR,DMCB,DMRDHI,ACCDIR,DKEY,DIR   READ HI ACC DIR            
         MVC   DA,DIR+(ACCKDA-ACCRECD)                                          
         B     DMERR                                                            
*                                                                               
DACCSEQ  GOTO1 DATAMGR,DMCB,DMRSEQ,ACCDIR,DKEY,DIR   READ SEQ ACC DIR           
         MVC   DA,DIR+(ACCKDA-ACCRECD)                                          
         B     DMERR                                                            
*                                                                               
DACCGET  GOTO1 DATAMGR,DMCB,GETREC,ACCMST,DA,AIO,DMWORK GETREC ACCMST           
         B     DMERR                                                            
         EJECT                                                                  
***********************************************************************         
* DATA MANAGER ROUTINES - SPOT FILES                                  *         
***********************************************************************         
                                                                                
DSPTOPN  GOTO1 DATAMGR,DMCB,OPEN,SPOT,SPFILEL        OPEN SPOT FILES            
         B     DMXIT                                                            
*                                                                               
DSPTRD   GOTO1 DATAMGR,DMCB,DMREAD,SPTDIR,DKEY,DIR   READ SPOT DIR              
         MVC   DA,DIR+14                                                        
         B     DMERR                                                            
*                                                                               
DSPTHI   GOTO1 DATAMGR,DMCB,DMRDHI,SPTDIR,DKEY,DIR   READ HI SPOT DIR           
         MVC   DA,DIR+14                                                        
         B     DMERR                                                            
*                                                                               
DSPTSEQ  GOTO1 DATAMGR,DMCB,DMRSEQ,SPTDIR,DKEY,DIR   READ SEQ SPOT DIR          
         MVC   DA,DIR+14                                                        
         B     DMERR                                                            
*                                                                               
DSPTGET  GOTO1 DATAMGR,DMCB,GETREC,SPTFIL,DA,AIO,DMWORK SPOT FILE               
         B     DMERR                                                            
*                                                                               
DSTARD   GOTO1 DATAMGR,DMCB,DMREAD,STATION,DKEY,AIO    READ STATION             
         B     DMERR                                                            
*                                                                               
DSTAHI   GOTO1 DATAMGR,DMCB,DMRDHI,STATION,DKEY,AIO     READ HI STATION         
         B     DMERR                                                            
         EJECT                                                                  
***********************************************************************         
* DATA MANAGER ROUTINES - PRINT FILES                                 *         
***********************************************************************         
                                                                                
DPRTOPN  GOTO1 DATAMGR,DMCB,OPEN,PRINTF,PRFILEL      OPEN PRINT FILES           
         B     DMXIT                                                            
*                                                                               
DPRTRD   GOTO1 DATAMGR,DMCB,DMREAD,PRTDIR,DKEY,DIR   READ PRINT DIR             
         MVC   DA,DIR+(PAGYCNTL-PAGYKEY)                                        
         B     DMERR                                                            
*                                                                               
DPRTHI   GOTO1 DATAMGR,DMCB,DMRDHI,PRTDIR,DKEY,DIR   READ HI PRINT DIR          
         MVC   DA,DIR+(PAGYCNTL-PAGYKEY)                                        
         B     DMERR                                                            
*                                                                               
DPRTSEQ  GOTO1 DATAMGR,DMCB,DMRSEQ,PRTDIR,DKEY,DIR   READ SEQ PRINT DIR         
         MVC   DA,DIR+(PAGYCNTL-PAGYKEY)                                        
         B     DMERR                                                            
*                                                                               
DPRTGET  GOTO1 DATAMGR,DMCB,GETREC,PRTFIL,DA,AIO,DMWORK PRINT FILE              
         B     DMERR                                                            
*                                                                               
DPUBRD   GOTO1 DATAMGR,DMCB,DMREAD,PUBDIR,DKEY,DIR   READ PUBDIR                
         MVC   DA,DIR+(PAGYCNTL-PAGYKEY)                                        
         B     DMERR                                                            
*                                                                               
DPUBHI   GOTO1 DATAMGR,DMCB,DMRDHI,PUBDIR,DKEY,DIR   READ HI PUBDIR             
         MVC   DA,DIR+(PAGYCNTL-PAGYKEY)                                        
         B     DMERR                                                            
*                                                                               
DPUBSEQ  GOTO1 DATAMGR,DMCB,DMRSEQ,PUBDIR,DKEY,DIR   READ SEQ PUBDIR            
         MVC   DA,DIR+(PAGYCNTL-PAGYKEY)                                        
         B     DMERR                                                            
*                                                                               
DPUBGET  GOTO1 DATAMGR,DMCB,GETREC,PUBFILE,DA,AIO,DMWORK PUBFILE                
         B     DMERR                                                            
*                                                                               
DMERR    MVC   DMBYTE,8(R1)                                                     
         CLI   DMBYTE,X'10'        TEST RECORD NOT FOUND                        
         BE    DMXITN              RETURN NOT EQUAL                             
         CLI   DMBYTE,0                                                         
         BE    DMXITY                                                           
         DC    H'0'                                                             
DMXITN   LTR   RB,RB                                                            
         B     *+6                                                              
DMXITY   CR    RB,RB                                                            
DMXIT    XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* CONSTANTS AND LITERAL POOL                                          *         
***********************************************************************         
                                                                                
ALL      EQU   X'FF'                                                            
EOT      EQU   X'FF'                                                            
*                                                                               
COMMAND  DC    C'        '                                                      
OPEN     DC    C'OPEN    '                                                      
CLOSE    DC    C'CLOSE   '                                                      
ADD      DC    C'ADD     '                                                      
GETREC   DC    C'GETREC  '                                                      
DMREAD   DC    C'DMREAD  '                                                      
DMRDHI   DC    C'DMRDHI  '                                                      
DMRSEQ   DC    C'DMRSEQ  '                                                      
DMADD    DC    C'DMADD   '                                                      
DMWRT    DC    C'DMWRT   '                                                      
DMGET    DC    C'GETREC  '                                                      
*                                                                               
CONTROL  DC    C'CONTROL '                                                      
CTFILE   DC    C'CTFILE  '                                                      
*                                                                               
ACCOUNT  DC    C'ACCOUNT '                                                      
ACFILEL  DC    C'NACCDIR NACCMST NACCARC X '                                    
ACCDIR   DC    C'ACCDIR  '                                                      
ACCMST   DC    C'ACCMST  '                                                      
*                                                                               
SPOT     DC    C'SPOT    '                                                      
SPFILEL  DC    C'NSPTDIR NSPTFIL NSTAFIL X'                                     
SPTDIR   DC    C'SPTDIR  '                                                      
SPTFIL   DC    C'SPTFIL  '                                                      
STATION  DC    C'STATION '                                                      
*                                                                               
PRINTF   DC    C'PRINT   '                                                      
PRFILEL  DC    C'NPRTDIR NPRTFILENPUBDIR NPUBFILEX'                             
PRTDIR   DC    C'PRTDIR  '                                                      
PRTFIL   DC    C'PRTFILE '                                                      
PUBDIR   DC    C'PUBDIR  '                                                      
PUBFILE  DC    C'PUBFILE '                                                      
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* DSECT FOR STORAGE                                                   *         
***********************************************************************         
                                                                                
PCBD     DSECT                                                                  
       ++INCLUDE ACPAYCHKD                                                      
*                                                                               
APCB     DS    A                                                                
*                                                                               
AIO      DS    A                                                                
AIO1     DS    A                                                                
AIO2     DS    A                                                                
*                                                                               
DATAMGR  DS    A                                                                
DATCON   DS    A                                                                
*                                                                               
CONRD    EQU   1                   READ CONTROL FILE                            
ACCOPN   EQU   2                   OPEN ACCOUNT FILE                            
ACCRD    EQU   3                   READ ACCDIR                                  
ACCHI    EQU   4                   READ HIGH ACCDIR                             
ACCSEQ   EQU   5                   READ SEQ ACCDIR                              
ACCGET   EQU   6                   GETREC ACCMST                                
SPTOPN   EQU   7                   OPEN SPOT FILES                              
SPTRD    EQU   8                   READ SPOTDIR                                 
SPTHI    EQU   9                   READ HIGH SPOTDIR                            
SPTSEQ   EQU   10                  READ SEQ SPOTDIR                             
SPTGET   EQU   11                  GETREC SPOT FILE                             
STARD    EQU   12                  READ STATION FILE                            
STAHI    EQU   13                  READ HIGH STATION FILE                       
PRTOPN   EQU   14                  OPEN PRINT FILES                             
PRTRD    EQU   15                  READ PRINT DIR                               
PRTHI    EQU   16                  READ HIGH PRINT DIR                          
PRTSEQ   EQU   17                  READ SEQ PRINT DIR                           
PRTGET   EQU   18                  GETREC PRINT FILE                            
PUBRD    EQU   19                  READ PUBFILE                                 
PUBHI    EQU   20                  READ HIGH PUBFILE                            
PUBSEQ   EQU   21                  READ SEQUENTIAL PUBFILE                      
PUBGET   EQU   22                  GETREC PUBFILE                               
*                                                                               
DMFLG    DS    X                                                                
DKEY     DS    XL42                                                             
DIR      DS    XL60                                                             
DMBYTE   DS    X                                                                
DA       DS    F                                                                
*                                                                               
WORK     DS    XL64                                                             
DMCB     DS    6F                                                               
DMWORK   DS    XL96                                                             
*                                                                               
SENSPOT  EQU   2                                                                
SENNET   EQU   3                                                                
SENPRNT  EQU   4                                                                
SENACCT  EQU   6                                                                
*                                                                               
AUTL     DS    A                   A(MASTER UTL)                                
ACOPNSE  DS    XL20                SE NUMBERS FOR OPEN ACC FILES                
SAVSE    DS    XL1                                                              
*                                                                               
LOCLRDTE DS    XL2                 LOW CLEARED DATE                             
LOKEY    DS    XL(L'TRNKEY)        FIRST CLEARANCE(PAID ON THIS CHECK)          
*                                                                               
PKAMNT   DS    PL8                 SAVED AREA FOR TRANSACTION AMOUNT            
*                                                                               
ELCODE   DS    XL1                                                              
SVULA    DS    0CL14               SAVED AREA FOR SC ACCOUNT                    
SVUL     DS    0CL2                U/L                                          
SVUNT    DS    CL1                    UNIT                                      
SVLDG    DS    CL1                        LEDGER                                
SVACCT   DS    CL12                             ACCOUNT                         
*                                                                               
IO1      DS    XL2004                                                           
IO2      DS    XL2004                                                           
*                                                                               
PCBX     EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
* DSECT FOR ID TABLE                                                  *         
***********************************************************************         
         SPACE 1                                                                
IDD      DSECT                                                                  
IDALPH   DS    CL2                 ALPHA CODE                                   
IDCTRY   DS    XL1                 COUNTRY                                      
IDSPSE   DS    XL1                 SPOT SE NUMBER                               
IDNESE   DS    XL1                 NET SE NUMBER                                
IDPRSE   DS    XL1                 PRINT SE NUMBER                              
IDACSE   DS    XL1                 ACC SE NUMBER                                
IDLNQ    EQU   *-IDD                                                            
         EJECT                                                                  
***********************************************************************         
* ++INCLUDE'S                                                         *         
***********************************************************************         
         SPACE 1                                                                
* ACPAYSEQD                                                                     
       ++INCLUDE ACPAYSEQD                                                      
         EJECT                                                                  
       ++INCLUDE SPGENCLRST                                                     
         EJECT                                                                  
       ++INCLUDE PPCLRST                                                        
         SPACE 2                                                                
       ++INCLUDE PAGYREC                                                        
         EJECT                                                                  
* ACGENFILE                                                                     
* ACRCVRECD                                                                     
* CTGENFILE                                                                     
* DDCOMFACSD                                                                    
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
       ++INCLUDE ACRCVRECD                                                      
       ++INCLUDE CTGENFILE                                                      
       ++INCLUDE DDCOMFACSD                                                     
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'020ACPAYCHK  11/08/13'                                      
         END                                                                    
