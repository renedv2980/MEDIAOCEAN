*          DATA SET SPREPUDESC AT LEVEL 052 AS OF 05/01/02                      
*CATALP REPUDESC                                                                
         TITLE 'SPREPUDESC - FORMAT UPGRADE DESCRIPTION'                        
*================================================================*              
*        PARAMETER 1   BYTE 0-3   A(SPWORK)                                     
*                  2   BYTE 0-3   A(132 POS. PRINT LINE AREA)                   
*================================================================*              
         SPACE 1                                                                
REPUDESC CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,REPUDESC                                                       
         L     RA,0(R1)                                                         
         LA    RC,2048(RA)                                                      
         LA    RC,2048(RC)                                                      
         USING SPWORKD,RA,RC                                                    
         L     R4,4(R1)            GET ADDRESS OF PRINT LINE                    
         L     R5,ADBUY                                                         
         USING BUYREC,R5                                                        
* FORMAT DEMO UPGRADE ELEMENT DATA *                                            
         SPACE 1                                                                
UPD2     LA    R6,BDELEM                                                        
         MVI   ELCDLO,X'62'                                                     
         MVI   ELCDHI,X'62'                                                     
         BAS   RE,NEXTEL                                                        
         BNE   UPDX                                                             
*                                                                               
         USING UPELEM,R6                                                        
*                                                                               
         MVC   0(132,R4),SPACES                                                 
         MVC   0(13,R4),=C'** UPGRADE **'                                       
         LA    R4,14(R4)                                                        
*                                                                               
         MVC   0(4,R4),=C'UPX='                                                 
         MVC   2(1,R4),UPFILE                                                   
*                                                                               
         L     R1,=A(UPTAB)                                                     
*                                                                               
UPD4     CLI   0(R1),0             TEST E-O-T                                   
         BE    UPD6                YES - MUST BE DEMO UPGRADE                   
         CLC   0(2,R1),UPTYPE      MATCH SPUPTYPE/SPUPSTYP                      
         BE    *+12                                                             
         LA    R1,L'UPTAB(R1)                                                   
         B     UPD4                                                             
         MVC   4(6,R4),2(R1)       MOVE UPGRADE TYPE NAME                       
         B     UPD8                                                             
*                                                                               
UPD6     XC    DUB,DUB             DEMO UPGRADE                                 
         MVC   DUB+1(2),UPTYPE                                                  
         MVI   DUB+3,X'FF'                                                      
         GOTO1 DEMOCON,DMCB,(1,DUB),(6,4(R4)),(C'S',ADBLOCK)                    
*                                                                               
UPD8     LA    R4,10(R4)           FIND END OF UPGRADE NAME                     
         CLI   0(R4),C' '                                                       
         BH    *+8                                                              
         BCT   R4,*-8                                                           
         MVI   1(R4),C'/'          ATTACH DELIMITER                             
         LA    R4,2(R4)                                                         
*                                                                               
         LA    R7,UPTYPE+2         POINT TO FIRST UPGRADE VALUE                 
         LA    R8,3                SET NUMBER OF VALUES                         
*                                                                               
UPD10    OC    0(2,R7),0(R7)       TEST VALUE PRESENT                           
         BZ    UPD14                                                            
         CLC   0(2,R7),=H'500'     TEST BOOK OR VALUE                           
         BNH   UPD12                                                            
         BAS   RE,EDITBK           R4 POINTS TO OUTPUT                          
         B     UPD14                                                            
*                                                                               
UPD12    SR    R0,R0                                                            
         ICM   R0,3,0(R7)                                                       
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  0(4,R4),DUB                                                      
         CLI   0(R4),C'0'                                                       
         BNE   UPD14                                                            
         MVC   0(4,R4),1(R4)                                                    
         LA    R4,5(R4)                                                         
*                                                                               
UPD14    LA    R7,2(R7)            BUMP TO NEXT VALUE                           
         BCT   R8,UPD10            DO FOR MAX VALUES                            
         BCTR  R4,0                OVERWRITE LAST DELIMITER                     
*                                                                               
         OC    UPFBK,UPFBK                                                      
         BZ    UPD24                                                            
         BAS   RE,LDCOMMA2                                                      
         MVC   0(3,R4),=C'BK='                                                  
         LA    R4,3(R4)                                                         
         LA    R7,UPFBK                                                         
         BAS   RE,EDITBK                                                        
*                                                                               
         CLI   UPELEM+1,49                                                      
         BL    *+14                                                             
         OC    UPFBKLST,UPFBKLST   TEST BOOK LIST                               
         BNZ   *+12                                                             
         LA    R4,12(R4)                                                        
         B     UPD24                                                            
*                                                                               
         MVI   0(R4),C'/'                                                       
         LA    R4,1(R4)                                                         
         LA    R0,L'UPFBKLST/2                                                  
         LA    R7,UPFBKLST                                                      
*                                                                               
UPD20    OC    0(2,R7),0(R7)                                                    
         BZ    UPD22                                                            
         BAS   RE,EDITBK                                                        
         MVI   0(R4),C'/'                                                       
         LA    R4,1(R4)                                                         
         LA    R7,2(R7)                                                         
         BCT   R0,UPD20                                                         
*                                                                               
UPD22    BCTR  R4,0                                                             
         MVI   0(R4),C' '                                                       
         LA    R4,2(R4)                                                         
*                                                                               
UPD24    OC    UPDAYTIM,UPDAYTIM                                                
         BZ    UPD26                                                            
         BAS   RE,LDCOMMA2                                                      
         MVC   0(3,R4),=C'DT='                                                  
         GOTO1 CODAY,DMCB,UPDAYTIM,3(R4)                                        
         LA    R4,11(R4)                                                        
         BAS   RE,LDCOMMA2                                                      
         BCTR  R4,0                                                             
         MVI   0(R4),C'/'                                                       
*                                                                               
         GOTO1 UNTIME,DMCB,UPDAYTIM+1,1(R4)                                     
*                                                                               
         OC    UPSTA,UPSTA         TEST STATION OVERRIDE                        
         BZ    UPD26                                                            
         LA    R4,14(R4)                                                        
         BAS   RE,LDCOMMA2                                                      
         BCTR  R4,0                                                             
         MVI   0(R4),C'/'                                                       
         MVC   1(4,R4),UPSTA                                                    
         LA    R4,5(R4)                                                         
*                                                                               
UPD26    CLI   1(R6),41            TEST ELEMENT HAS OLD LENGTH                  
         BNH   UPDX                YES                                          
         CLI   UP2YRP,C'Y'         NO-PUT/SHR 2 YEAR AVERAGING                  
         BNE   UPD28                                                            
         BAS   RE,LDCOMMA2                                                      
         MVC   0(5,R4),=C'PUT=2'                                                
         LA    R4,5(R4)                                                         
*                                                                               
UPD28    CLI   UP2YRS,C'Y'                                                      
         BNE   UPDX                                                             
         BAS   RE,LDCOMMA2                                                      
         MVC   0(5,R4),=C'SHR=2'                                                
*                                                                               
UPDX     XIT1                                                                   
         DROP  R6                                                               
         SPACE 2                                                                
*================================================================*              
* R7 POINTS TO BOOK. PLACE OUTPUT AT 0(R4).                      *              
* R4 POINTS TO NEXT OUTPUT ADDRESS ON EXIT                       *              
*================================================================*              
         SPACE 1                                                                
EDITBK   NTR1                                                                   
         MVC   DUB(2),0(R7)                                                     
         MVI   DUB+2,1                                                          
         NI    DUB+1,X'7F'          TURN OFF OLYM EXCL                          
         GOTO1 DATCON,DMCB,(3,DUB),(6,WORK)                                     
         MVC   WORK+3(2),WORK+4     MOVE YEAR LEFT                              
         MVC   WORK+5(5),SPACES                                                 
         TM    1(R7),X'80'                                                      
         BZ    *+10                                                             
         MVC   WORK+5(3),=C'(O)'                                                
*                                                                               
         MVC   0(10,R4),WORK                                                    
         LA    R4,10(R4)                                                        
*                                                                               
         CLI   0(R4),C' '          FIND END OF EDITED VALUE                     
         BH    *+8                                                              
         BCT   R4,*-8                                                           
         MVI   1(R4),C' '                                                       
         LA    R4,2(R4)                                                         
         XIT1 REGS=(R4)                                                         
         EJECT                                                                  
NEXTEL   CLI   0(R6),0                                                          
         BE    NEXTELX                                                          
         ZIC   R0,1(R6)                                                         
         LTR   R0,R0                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R6,R0                                                            
         CLI   0(R6),0                                                          
         BE    NEXTELX                                                          
         CLC   ELCDLO,0(R6)                                                     
         BH    NEXTEL                                                           
         CLC   ELCDHI,0(R6)                                                     
         BL    NEXTEL                                                           
         CR    RB,RB                                                            
         B     *+6                                                              
NEXTELX  LTR   RB,RB                                                            
         BR    RE                                                               
         EJECT                                                                  
LDCOMMA  DS    0H                                                               
         LA    R4,8(R4)            POINT TO END (MAX LEN IS 8)                  
*                                                                               
LDCOMMA2 OI    0(R4),C' '                                                       
         CLI   0(R4),C' '                                                       
         BNE   *+8                                                              
         BCT   R4,LDCOMMA2                                                      
*                                                                               
         MVI   1(R4),C','                                                       
         LA    R4,2(R4)                                                         
         BR    RE                                                               
*                                                                               
ELCDLO   DS    C                                                                
ELCDHI   DS    C                                                                
*                                                                               
UPTAB    DS    0XL8                SPUPTYPE/SPUTSTYP/UPGRADE NAME               
         DC    AL1(SPUPTRTG),X'0',C'RATING'                                     
         DC    AL1(SPUPTHUT),X'0',C'HUT   '                                     
         DC    AL1(SPUPTPUT),C'P',C'PUT   '                                     
         DC    AL1(SPUPTNDX),X'0',C'INDEX '                                     
         DC    AL1(SPUPTHPT),X'0',C'HPT   '                                     
         DC    AL1(SPUPTPIN),C'N',C'PIN   '                                     
         DC    AL1(SPUPTPIM),C'M',C'PIM   '                                     
         DC    AL1(SPUPTPIY),C'Y',C'PIY   '                                     
         DC    AL1(SPUPTPIQ),C'Q',C'PIQ   '                                     
         DC    AL1(SPUPTPIB),C'B',C'PIB   '                                     
         DC    AL1(SPUPTSIN),C'N',C'SIN   '                                     
         DC    AL1(SPUPTSIM),C'M',C'SIM   '                                     
         DC    AL1(SPUPTSIY),C'Y',C'SIY   '                                     
         DC    AL1(SPUPTSIQ),C'Q',C'SIQ   '                                     
         DC    AL1(SPUPTSIB),C'B',C'SIB   '                                     
         DC    AL1(SPUPTPAV),C'N',C'PAVG  '                                     
         DC    AL1(SPUPTPAY),C'Y',C'PAY   '                                     
         DC    AL1(SPUPTPAQ),C'Q',C'PAQ   '                                     
         DC    AL1(SPUPTPAB),C'B',C'PAB   '                                     
         DC    AL1(SPUPTSAV),C'N',C'SAVG  '                                     
         DC    AL1(SPUPTSAY),C'Y',C'SAY   '                                     
         DC    AL1(SPUPTSAQ),C'Q',C'SAQ   '                                     
         DC    AL1(SPUPTSAB),C'B',C'SAB   '                                     
         DC    X'00'                                                            
         EJECT                                                                  
       ++INCLUDE SPDEMUPD                                                       
BUYRECD  DSECT                                                                  
       ++INCLUDE SPGENBUY                                                       
         PRINT OFF                                                              
       ++INCLUDE SPREPWORKD                                                     
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'052SPREPUDESC05/01/02'                                      
         END                                                                    
