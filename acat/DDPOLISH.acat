*          DATA SET DDPOLISH   AT LEVEL 015 AS OF 11/10/80                      
*CATALP POLISH                                                                  
*                                                                               
* CONVERT AN ALBEGRAIC FORMULA TO POLISH                                        
*                        PARAMETER 1 =INPUT STRING                              
*                        PARAMETER 2 =OUTPUT STRING                             
*                                                                               
         PRINT NOGEN                                                            
POLISH   CSECT                                                                  
         NMOD1 300,POLISH                                                       
         USING POLWRKD,RC                                                       
         L     R2,0(R1)            INPUT                                        
         L     R3,4(R1)            OUTPUT                                       
         LA    RE,SOURCE                                                        
         LA    RF,1000                                                          
         XCEF                                                                   
         LA    RE,OSTACK                                                        
         LA    RF,80                                                            
         XCEF                                                                   
         LA    RE,PSTACK                                                        
         LA    RF,1000                                                          
         XCEF                                                                   
         LA    RE,SOURCE                                                        
         MVI   FIRST,1                                                          
         LR    R4,R2               POINT TO INPUT                               
* DECODE INPUT STRING                                                           
DECODE   LA    R5,1(RE)                                                         
         SR    R6,R6                                                            
DECODE1  CLI   0(R4),C' '                                                       
         BE    DECODE1A                                                         
         CLI   0(R4),C','                                                       
         BE    DECODE2                                                          
         CLI   0(R4),C')'                                                       
         BE    DECODE2                                                          
         MVC   0(1,R5),0(R4)                                                    
         LA    R5,1(R5)                                                         
         LA    R6,1(R6)                                                         
         CLI   0(R4),C'('                                                       
         BE    DECODE2                                                          
         LA    R4,1(R4)                                                         
         B     DECODE1                                                          
         SPACE 2                                                                
DECODE1A LTR   R6,R6                                                            
         BZ    DECODEX                                                          
DECODE2  C     R6,=F'1'            OPERATOR                                     
         BE    DECODE3                                                          
DECODE2A MVI   0(RE),X'FF'          NO - FLAG AS OPERAND                        
         CLI   FIRST,1                                                          
         BNE   DCDENXT                                                          
         MVI   FIRST,0                                                          
         CLI   1(R4),C'='                                                       
         BE    DCDENXT                                                          
         LA    RE,9(RE)                                                         
         MVC   0(2,RE),=X'057E'                                                 
         B     DCDENXT                                                          
         B     DCDENXT                                                          
         SPACE 2                                                                
DECODE3  LA    R6,HIERTAB          GET OPERATOR CODE                            
DECODE3A CLC   1(1,RE),0(R6)                                                    
         BE    DECODE3B                                                         
         LA    R6,2(R6)                                                         
         CLI   0(R6),0             OPERATOR NOT FOUND                           
         BE    DECODE2A             MUST BE OPERAND                             
         B     DECODE3A                                                         
         SPACE 2                                                                
DECODE3B MVC   0(1,RE),1(R6)       FOUND CODE - MOVE TO TABLE                   
         B     DCDENXT                                                          
         SPACE 2                                                                
DCDENXT  CLI   0(R4),C')'                                                       
         BNE   DCDENXT3                                                         
DCDENXT1 LA    RE,9(RE)            SAVE LEFT PARENS                             
         MVC   0(2,RE),=X'015D'                                                 
DCDENXT2 LA    R4,1(R4)            BYPASS LEFT PARENS                           
         CLI   0(R4),C','                                                       
         BE    DCDENXT2                                                         
         CLI   0(R4),C')'                                                       
         BE    DCDENXT1                                                         
         B     *+8                                                              
         SPACE 2                                                                
DCDENXT3 LA    R4,1(R4)                                                         
         LA    RE,9(RE)                                                         
         B     DECODE                                                           
DECODEX  DS    0H                                                               
         EJECT                                                                  
* OPERATORS AND OPERANDS ARE DEFINED                                            
*  NOW DO POLISH CONVERSION                                                     
         LA    R2,OSTACK                                                        
         LA    R2,2(R2)                                                         
         LA    R4,PSTACK                                                        
         LA    RE,SOURCE                                                        
POLCONV  CLI   1(RE),0             END OF INPUT                                 
         BE    POLOUT               OUTPUT POLISH FORMULA                       
         LR    R6,R2               SET TO TOP OF STACK                          
         SH    R6,=H'2'                                                         
         CLI   0(RE),X'FF'         OPERAND                                      
         BNE   POLCNV2              NO                                          
         MVC   0(8,R4),1(RE)        YES - MOVE TO OUTPUT STRING                 
         LA    R4,8(R4)                                                         
         LA    RE,9(RE)                                                         
         CLI   1(RE),0                                                          
         BE    POLOUT                                                           
         SPACE 2                                                                
POLCNV2  CLC   0(1,R6),0(RE)                                                    
         BNH   POLCNV3                                                          
         MVC   0(1,R4),1(R6)       BACK UP TILL OPERATOR IN STACK               
         SH    R2,=H'2'             LESS THAN OPERATOR IN INPUT                 
         XC    0(2,R2),0(R2)                                                    
         SH    R6,=H'2'                                                         
         LA    R4,8(R4)                                                         
         B     POLCNV2                                                          
         SPACE 2                                                                
POLCNV3  CLI   0(RE),1                                                          
         BNE   POLCNV4                                                          
         XC    0(2,R6),0(R6)                                                    
         SH    R2,=H'2'                                                         
         SH    R6,=H'2'                                                         
         LA    RE,9(RE)                                                         
         B     POLCONV                                                          
         SPACE 2                                                                
POLCNV4  MVC   0(2,R2),0(RE)       MOVE OPERATOR TO LIST                        
         LA    RE,9(RE)                                                         
         LA    R2,2(R2)                                                         
         LA    R6,2(R6)                                                         
         CLI   1(RE),0                                                          
         BE    POLOUT                                                           
         CLI   0(RE),X'FF'                                                      
         BNE   POLCNV4                                                          
         B     POLCONV                                                          
         SPACE 2                                                                
* CONVERSION IS COMPLETE - NOW OUTPUT IT                                        
POLOUT   LA    R4,PSTACK                                                        
POLOUT1  MVC   0(8,R3),0(R4)                                                    
         LA    R4,8(R4)                                                         
         CLI   0(R3),0             END                                          
         BNE   POLOUT2                                                          
         MVI   0(R3),C' '           YES - CHECK OPERATOR STACK                  
         B     POLOUTO                                                          
         SPACE 2                                                                
POLOUT2  CLI   0(R3),0                                                          
         BNE   POLOUT3                                                          
         MVI   0(R3),C','                                                       
         LA    R3,1(R3)                                                         
         B     POLOUT1                                                          
POLOUT3  LA    R3,1(R3)                                                         
         B     POLOUT2                                                          
         SPACE 2                                                                
* OUTPUT ANY REMAINING OPERATORS                                                
POLOUTO  SH    R2,=H'2'                                                         
         CLI   0(R2),0                                                          
         BE    POLXIT                                                           
POLOUTO1 CLI   0(R2),0                                                          
         BE    POLXIT                                                           
         MVC   0(1,R3),1(R2)                                                    
         MVI   1(R3),C','                                                       
         SH    R2,=H'2'                                                         
         LA    R3,2(R3)                                                         
         B     POLOUTO1                                                         
         SPACE 2                                                                
POLXIT   BCTR  R3,0                                                             
         MVI   0(R3),C' '                                                       
         XMOD1 1                                                                
         LTORG                                                                  
* POLISH CONVERSION TABLES                                                      
HIERTAB  DC    C'(',X'00'                                                       
         DC    C')',X'01'                                                       
         DC    C'=',X'05'                                                       
         DC    C'-',X'07'                                                       
         DC    C'+',X'07'                                                       
         DC    C'X',X'08'                                                       
         DC    C'/',X'08'                                                       
         DC    X'00'                                                            
         EJECT                                                                  
POLWRKD  DSECT                                                                  
SOURCE   DS    1000C               0 = OPERATION TYPE                           
*                                 1+8= OPERAND                                  
OSTACK   DS    80C                 0 = OPERATION CLASS                          
*                                  1 = OPERATION CODE                           
PSTACK   DS    1000C              0-8= OPERANDS                                 
FIRST    DS    C                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'015DDPOLISH  11/10/80'                                      
         END                                                                    
