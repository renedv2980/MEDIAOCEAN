*          DATA SET ACGETUSR   AT LEVEL 021 AS OF 05/01/02                      
*CATALP GETUSR                                                                  
         TITLE 'GETUSR - BUILD TABLE OF USER SELECT FIELDS'                     
GETUSR   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 (MYDX-MYD),**GETU**,RA,RR=R2,CLEAR=YES                           
         USING MYD,RC                                                           
         L     R9,0(R1)                                                         
         USING USRBLOCK,R9                                                      
         ST    R2,RELO                                                          
         OC    USRABUFF,USRABUFF   WE NEED THIS BUFFER TO CONTINUE              
         BZ    DUMP                                                             
         OC    USRBUFFL,USRBUFFL                                                
         BZ    DUMP                                                             
         MVC   USRAJOBT,USRABUFF   JOB TABLE IS AT START OF BUFFER              
         SR    RE,RE                                                            
         L     RF,USRBUFFL                                                      
         D     RE,=F'5'            JOB TABLE LENGTH IS 1/5 OF BUFFER            
         ST    RF,USRJOBTL                                                      
         A     RF,USRAJOBT         NOW WE HAVE START OF REPORT TABLE            
         ST    RF,USRAREPT                                                      
         L     RF,USRBUFFL         BUFFER LENGTH MINUS JOB TABLE LENGTH         
         S     RF,USRJOBTL                                                      
         ST    RF,USRREPTL         EQUALS REPORT TABLE LENGTH                   
         SR    RE,RE                                                            
         D     RE,=AL4(REPLNG)     GET MAXIMUM NUMBER OF RECORDS NOW            
         ST    RF,REPPARMS+8                                                    
         SR    RE,RE                                                            
         L     RF,USRJOBTL                                                      
         D     RE,=AL4(JOBLNG)                                                  
         ST    RF,JOBPARMS+8                                                    
         LA    RF,JOBREC                                                        
         ST    RF,USRAJOBR                                                      
         LA    RF,REPREC                                                        
         ST    RF,USRAREPR                                                      
         MVC   BINSRCH,USRABIN                                                  
         OC    BINSRCH,BINSRCH                                                  
         BNZ   INITIAL                                                          
         L     R2,=V(BINSRCH)                                                   
         LTR   R2,R2                                                            
         BZ    *+8                                                              
         A     R2,RELO                                                          
         ST    R2,BINSRCH                                                       
         SPACE 1                                                                
INITIAL  MVI   SPACES,C' '                                                      
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
         MVC   USRBLOCK(8),=C'**USRB**'                                         
         XC    USRJOBCT,USRJOBCT   CLEAR JOB COUNT                              
         EJECT                                                                  
***********************************************************************         
*                   BUILD  REPORT LEVEL TABLE                         *         
***********************************************************************         
         SPACE 1                                                                
         USING ACKEYD,R6                                                        
         LA    R6,IO                                                            
         USING REPRECD,R2                                                       
         L     R2,USRAREPR                                                      
         CLI   USRSWT,X'00'        FIRST TIME THROUGH ?                         
         BNE   GETALL              NO, TABLE BUILT ALREADY                      
         XC    USRREPCT,USRREPCT   CLEAR RECORD COUNT                           
         MVI   USRSWT,X'01'        YES, SET THE SWITCH                          
         XC    KEY,KEY                                                          
         MVI   KEY,X'2C'                                                        
         MVI   KEY+1,X'10'                                                      
         MVC   KEY+2(1),USRCUL                                                  
         MVC   KEY+3(2),=C'SJ'                                                  
         MVC   REPENTRY(REPLNG),SPACES                                          
         GOTO1 USRADATA,DMCB,(X'08',=C'DMRDHI'),=C'ACCOUNT',KEY,IO              
         B     CHECKRET                                                         
         SPACE 1                                                                
NEXTREC  GOTO1 USRADATA,DMCB,(X'08',=C'DMRSEQ'),=C'ACCOUNT',KEY,IO              
         SPACE 1                                                                
CHECKRET CLI   DMCB+8,0            READ OK ?                                    
         BE    CHECKKEY            YES, CHECK THE KEY                           
         CLI   DMCB+8,X'02'        NO, RECORD DELETED ?                         
         BE    NEXTREC             YES, SKIP IT                                 
         B     GETALL              NO, MUST BE END-OF-FILE                      
         SPACE 1                                                                
CHECKKEY CLC   IO(5),KEY           CONTINUE TO BUILD TABLE UNTIL                
         BNE   GETALL               RECORDS ARE EXHAUSTED                       
         MVC   KEY,IO                                                           
         MVC   REPENTRY(REPLNG),ACUFOG                                          
         MVI   ELCODE,X'A2'                                                     
         BAS   RE,GETELIO                                                       
         B     *+8                                                              
         SPACE 1                                                                
NEXTELM  BAS   RE,NEXTEL                                                        
         BNE   NEXTREC                                                          
         USING ACUFD,R7                                                         
         MVC   REPA2,ACUFEL                                                     
         MVC   REPCODE,ACUFCODE                                                 
         BAS   RE,REPADD                                                        
         B     NEXTELM                                                          
         EJECT                                                                  
***********************************************************************         
*                      BUILD JOB LEVEL TABLE                          *         
***********************************************************************         
         SPACE 1                                                                
GETALL   OC    USRREPCT,USRREPCT                                                
         BZ    EXIT                                                             
         L     R3,USRAJOBR                                                      
         USING JOBRECD,R3                                                       
         MVC   JOBCLI,USRCLI                                                    
         MVC   JOBPRO,USRPRO                                                    
         MVC   JOBJOB,USRJOB                                                    
         SPACE 1                                                                
         XC    REPENTRY(REPLNG),REPENTRY                                        
         BAS   RE,REPGET           LOOK FOR COMPANY                             
         SPACE 1                                                                
GETMG    OC    USRMG,USRMG         LOOK FOR COMPANY, MEDIA GROUP                
         BZ    GETMED                                                           
         XC    REPENTRY(REPLNG),REPENTRY                                        
         MVC   REPMG,USRMG                                                      
         BAS   RE,REPGET                                                        
         SPACE 1                                                                
GETMED   OC    USRMED,USRMED       LOOK FOR COMPANY, MEDIA                      
         BZ    GETOFG                                                           
         XC    REPENTRY(REPLNG),REPENTRY                                        
         MVC   REPMED,USRMED                                                    
         BAS   RE,REPGET                                                        
         SPACE 1                                                                
GETOFG   OC    USROFG,USROFG       LOOK FOR COMPANY, OFFICE GROUP               
         BZ    GETOFF                                                           
         XC    REPENTRY(REPLNG),REPENTRY                                        
         MVC   REPOFG,USROFG                                                    
         BAS   RE,REPGET                                                        
         SPACE 1                                                                
GETOFG2  OC    USRMG,USRMG         LOOK FOR COMPANY, OFFICE GROUP,              
         BZ    GETOFG4              MEDIA GROUP                                 
         MVC   REPMG,USRMG                                                      
         BAS   RE,REPGET                                                        
         SPACE 1                                                                
GETOFG4  OC    USRMED,USRMED       LOOK FOR COMPANY, OFFICE GROUP,              
         BZ    GETOFF               MEDIA                                       
         XC    REPMG,REPMG                                                      
         MVC   REPMED,USRMED                                                    
         BAS   RE,REPGET                                                        
         SPACE  1                                                               
GETOFF   CLC   USROFC,SPACES       LOOK FOR COMPANY, OFFICE                     
         BNH   GETCLI                                                           
         XC    REPENTRY(REPLNG),REPENTRY                                        
         MVC   REPOFC,USROFC                                                    
         BAS   RE,REPGET                                                        
         SPACE 1                                                                
GETOFF2  OC    USRMG,USRMG         LOOK FOR COMPANY, OFFICE,                    
         BZ    GETOFF4              MEDIA GROUP                                 
         MVC   REPMG,USRMG                                                      
         BAS   RE,REPGET                                                        
         XC    REPMG,REPMG                                                      
         SPACE 1                                                                
GETOFF4  OC    USRMED,USRMED       LOOK FOR COMPANY, OFFICE,                    
         BZ    GETCLI               MEDIA                                       
         MVC   REPMED,USRMED                                                    
         BAS   RE,REPGET                                                        
         SPACE 1                                                                
GETCLI   XC    REPENTRY(REPLNG),REPENTRY LOOK FOR COMPANY, CLIENT               
         MVC   REPCLI,USRCLI                                                    
         BAS   RE,REPGET                                                        
         SPACE 1                                                                
GETCLI2  OC    USRMG,USRMG         LOOK FOR COMPANY, CLIENT, MEDIA              
         BZ    GETCLI4              GROUP                                       
         MVC   REPMG,USRMG                                                      
         BAS   RE,REPGET                                                        
         XC    REPMG,REPMG                                                      
         SPACE 1                                                                
GETCLI4  OC    USRMED,USRMED       LOOK FOR COMPANY, CLIENT, MEDIA              
         BZ    GETCLI6                                                          
         MVC   REPMED,USRMED                                                    
         BAS   RE,REPGET                                                        
         XC    REPMED,REPMED                                                    
         SPACE 1                                                                
GETCLI6  MVC   REPPRO,USRPRO       LOOK FOR COMPANY, CLIENT,                    
         BAS   RE,REPGET            PRODUCT                                     
         OC    USRMG,USRMG         LOOK FOR COMPANY, CLIENT,                    
         BZ    GETCLI8              PRODUCT, MEDIA GROUP                        
         MVC   REPMG,USRMG                                                      
         BAS   RE,REPGET                                                        
         XC    REPMG,REPMG                                                      
         SPACE 1                                                                
GETCLI8  OC    USRMED,USRMED       LOOK FOR COMPANY, CLIENT,                    
         BZ    GETCLI10             PRODUCT, MEDIA                              
         MVC   REPMED,USRMED                                                    
         BAS   RE,REPGET                                                        
         XC    REPMED,REPMED                                                    
         SPACE 1                                                                
GETCLI10 MVC   REPJOB,USRJOB       LOOK FOR COMPANY, CLIENT,                    
         OC    REPJOB,SPACES        PRODUCT, JOB                                
         BAS   RE,REPGET                                                        
         EJECT                                                                  
***********************************************************************         
* GET A2 ELEMENTS FOR JOB AND UPDATE THE TABLE. ALSO INDICATE UPDATE  *         
* NEEDED IF:                                                          *         
*   - JOB CONTAINS A2 ELEMENTS THAT ARE NOT IN THE TABLE              *         
*   - TABLE CONTAINS A2 ELEMENTS THAT ARE NOT ON THE JOB              *         
*   - A2 RECORD (LESS DATA) IS DIFFERENT BETWEEN JOB AND TABLE        *         
***********************************************************************         
         USING ACUFD,R7                                                         
USERDATA OC    USRJOBCT,USRJOBCT   MAKE SURE THERE ARE SOME ENTRIES             
         BZ    EXIT                                                             
         MVC   KEY,SPACES                                                       
         MVC   KEY(3),USRCUL       RE-ESTABLISH JOB KEY                         
         MVC   KEY+3(3),USRCLI                                                  
         MVC   KEY+6(3),USRPRO                                                  
         MVC   KEY+9(6),USRJOB                                                  
         GOTO1 USRADATA,DMCB,(X'08',=C'DMRDHI'),=C'ACCOUNT',KEY,IO              
         CLI   DMCB+8,0                                                         
         BE    USED020                                                          
         CLI   DMCB+8,X'02'                                                     
         BNE   DUMP                                                             
USED020  CLC   KEY,IO              MUST GET JOB RECORD AGAIN                    
         BNE   DUMP                                                             
         L     R0,USRJOBCT                                                      
         MVI   USRJOBST,C'N'       SET SWITCH FOR 'UPDATE NOT NEEDED'           
         MVI   ELCODE,X'A2'                                                     
         BAS   RE,GETELIO                                                       
         B     *+8                                                              
         SPACE 1                                                                
USED040  BAS   RE,NEXTEL                                                        
         BNE   USED100                                                          
         MVC   JOBCODE,ACUFCODE                                                 
         BAS   RE,JOBGET           IF CODE NOT IN TABLE,                        
         CLI   DMCB,1                                                           
         BE    USED080              INDICATE UPDATE NEEDED                      
         BCTR  R0,0                SUBTRACT 1 FROM # TABLE ENTRIES              
         L     R6,DMCB             ADDRESS FOUND RECORD                         
         MVC   JOBDATA-JOBRECD(L'JOBDATA,R6),SPACES  CLEAR DATA AREA            
         SR    R1,R1                                                            
         IC    R1,ACUFLEN          GET RECORD LENGTH                            
         SH    R1,=AL2(ACUFDATA-ACUFEL) SUBTRACT LENGTH LESS DATA               
         BZ    USED060             NO DATA, CHECK FOR CHANGES                   
         BCTR  R1,0                RESULT IS LENGTH OF DATA                     
         EX    R1,*+8              MOVE IN DATA FROM A2 ELEMENT                 
         B     *+10                                                             
         MVC   JOBDATA-JOBRECD(0,R6),ACUFDATA                                   
         MVC   JOBA2+1-JOBRECD(1,R6),ACUFLEN    UPDATE LENGTH                   
         SPACE 1                                                                
USED060  CLC   JOBA2-JOBRECD(L'JOBA2,R6),ACUFEL  HAS RECORD CHANGED ?           
         BE    USED040              NO, GET NEXT                                
         SPACE 1                                                                
USED080  MVI   USRJOBST,C'U'        YES, INDICATE UPDATE NEEDED                 
         B     USED040                                                          
         SPACE 1                                                                
USED100  L     R6,USRJOBCT                                                      
         CH    R6,=H'10'           SET SWITCH TO 'M' IF MORE THAN               
         BNH   USED120              10 USER FIELDS                              
         MVI   USRJOBST,C'M'                                                    
         B     EXIT                                                             
         SPACE 1                                                                
USED120  CH    R0,=H'0'            HAVE WE MATCHED ALL ENTRIES?                 
         BE    EXIT                YES, DONE                                    
         MVI   USRJOBST,C'U'       NO, INDICATE UPDATE NEEDED                   
         B     EXIT                                                             
         DROP  R7                                                               
         EJECT                                                                  
***********************************************************************         
*                   ADD ENTRY TO REPORT TABLE                         *         
***********************************************************************         
         SPACE 1                                                                
REPADD   NTR1                                                                   
         LA    R4,USRAREPR                                                      
         LA    R5,REPPARMS                                                      
         BAS   RE,BINADD                                                        
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*           GET ENTRY FROM REPORT TABLE FOR ADD TO JOB TABLE          *         
*           MATCH ON FIRST 22 BYTES ON RECORD ONLY (NOT CODE)         *         
***********************************************************************         
         SPACE 1                                                                
REPGET   NTR1                                                                   
         SPACE 1                                                                
REPG020  LA    R4,USRAREPR                                                      
         LA    R5,REPPARMS                                                      
         BAS   RE,BINGET                                                        
         CLI   DMCB,1                                                           
         BE    REPG080             NONE FOUND                                   
         L     R6,DMCB             ADDRESS THE FOUND RECORD                     
         CLC   0(REPCODE-REPRECD,R2),0(R6)  MATCH THROUGH USER CODE             
         BNE   REPG080                                                          
         OC    REPCUT-REPRECD(L'USRCUT,R6),REPCUT-REPRECD(R6)                   
         BZ    REPG040             NO CUTOFF DATE                               
         CLC   REPCUT-REPRECD(L'USRCUT,R6),USRCUT                               
         BH    REPG040             VALID ENTRY                                  
         MVC   JOBCODE,REPCODE-REPRECD(R6)                                      
         BAS   RE,JOBGET           NOT VALID READ FOR JOB LEVEL                 
         CLI   DMCB,1              FOUND IT ?                                   
         BE    REPG060             NO, DON'T ADD IT                             
         BAS   RE,JOBDEL           DELETE IT                                    
         L     R0,USRJOBCT                                                      
         BCTR  R0,0                MINUS 1 FROM # TABLE ENTRIES                 
         ST    R0,USRJOBCT                                                      
         B     REPG060                                                          
         SPACE 1                                                                
REPG040  MVC   JOBA2,REPA2-REPENTRY(R6) MOVE DATA TO JOB RECORD                 
         MVC   JOBCODE,REPCODE-REPENTRY(R6)                                     
         BAS   RE,JOBADD           ADD IT TO JOB TABLE                          
         SPACE 1                                                                
REPG060  SR    RF,RF                                                            
         ICM   RF,3,REPCODE-REPRECD(R6) GET USER CODE                           
         LA    RF,1(RF)            BUMP IT UP                                   
         STCM  RF,3,REPCODE        STORE IN KEY FOR NEXT                        
         B     REPG020             SEE IF ANY MORE                              
         SPACE 1                                                                
REPG080  XC    REPCODE,REPCODE     CLEAR CODE FOR NEXT ENTRY                    
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*                      ADD ENTRY TO JOB TABLE                         *         
***********************************************************************         
         SPACE 1                                                                
JOBADD   NTR1                                                                   
         LA    R4,USRAJOBR                                                      
         LA    R5,JOBPARMS                                                      
         BAS   RE,BINADD                                                        
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*                    GET ENTRY FROM JOB TABLE                         *         
***********************************************************************         
         SPACE 1                                                                
JOBGET   NTR1                                                                   
         LA    R4,USRAJOBR                                                      
         LA    R5,JOBPARMS                                                      
         BAS   RE,BINGET                                                        
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*                    DELETE ENTRY FROM JOB TABLE                      *         
***********************************************************************         
         SPACE 1                                                                
JOBDEL   NTR1                                                                   
         LA    R4,USRAJOBR                                                      
         LA    R5,JOBPARMS                                                      
         BAS   RE,BINDEL                                                        
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*                    ADD ENTRY TO BINSRCH TABLE                       *         
***********************************************************************         
* REGISTER 4          A(RECORD),A(TABLE),NUMBER OF ENTRIES            *         
* REGISTER 5          A(PARMS)                                        *         
***********************************************************************         
         SPACE 1                                                                
         USING BIND,R4                                                          
         USING BIND2,R5                                                         
BINADD   NTR1                                                                   
         MVC   DMCB(12),BINREC     MOVE IN:A(RECORD),A(TABLE),#ENTRIES          
         MVC   DMCB+12(12),BINLEN  RECORD LENGTH,KEY LENGTH,MAX ENTRIES         
         MVI   DMCB,X'01'          INDICATE ADD IF NOT FOUND                    
         GOTO1 BINSRCH,DMCB                                                     
         OC    DMCB(4),DMCB                                                     
         BNZ   *+6                                                              
         DC    H'0'                BLOW UP IF TABLE IS FULL                     
         MVC   BINNUM,DMCB+8       UPDATE COUNT                                 
         CLI   DMCB,1                                                           
         BE    EXIT                                                             
         L     R6,DMCB             SWAP RECORDS IF ALREADY THERE                
         L     R7,BINREC                                                        
         MVC   0(JOBLNG,R6),0(R7)                                               
         B     EXIT                                                             
         DROP  R4,R5                                                            
         EJECT                                                                  
***********************************************************************         
*                  DELETE AN ENTRY IN BINSRCH TABLE                   *         
***********************************************************************         
* REGISTER 4          A(RECORD),A(TABLE),NUMBER OF ENTRIES            *         
* REGISTER 5          A(PARMS)                                        *         
***********************************************************************         
         SPACE 1                                                                
         USING BIND,R4                                                          
         USING BIND2,R5                                                         
BINDEL   NTR1                                                                   
         MVC   DMCB(12),BINREC     MOVE IN:A(RECORD),A(TABLE),#ENTRIES          
         MVC   DMCB+12(12),BINLEN  RECORD LENGTH,KEY LENGTH,MAX ENTRIES         
         MVI   DMCB,X'80'          INDICATE DELETE                              
         GOTO1 BINSRCH,DMCB                                                     
         CLI   DMCB,X'00'                                                       
         BE    EXIT                                                             
         DC    H'0'                BLOW UP IF ERROR                             
         DROP  R4,R5                                                            
         EJECT                                                                  
***********************************************************************         
*                 RETRIEVE ENTRY FROM BINSRCH TABLE                   *         
***********************************************************************         
* REGISTER 4          A(RECORD),A(TABLE),NUMBER OF ENTRIES            *         
* REGISTER 5          A(PARMS)                                        *         
***********************************************************************         
         SPACE 1                                                                
         USING BIND,R4                                                          
         USING BIND2,R5                                                         
BINGET   NTR1                                                                   
         MVC   DMCB(12),BINREC     MOVE IN:A(RECORD),A(TABLE),#ENTIRES          
         MVC   DMCB+12(12),BINLEN  RECORD LENGTH,KEY LENGTH,MAX ENTRIES         
         MVC   DMCB(1),BINACT      SET TYPE OF ACTION                           
         GOTO1 BINSRCH,DMCB                                                     
         B     EXIT                                                             
         DROP  R4,R5                                                            
         EJECT                                                                  
***********************************************************************         
*                       MISCELLANEOUS ROUTINES                        *         
***********************************************************************         
         SPACE 1                                                                
GETELIO  LA    R7,IO                                                            
         GETEL (R7),DATADISP,ELCODE                                             
         SPACE 3                                                                
EXIT     XIT1                                                                   
         SPACE 3                                                                
DUMP     DC    H'0'                                                             
         EJECT                                                                  
***********************************************************************         
*                  CONSTANTS AND RECORD FORMATS                       *         
***********************************************************************         
         SPACE 1                                                                
RELO     DS    A                                                                
DATADISP DC    H'49'                                                            
         SPACE 3                                                                
REPPARMS DS    0D                                                               
         DC    AL4(REPLNG)         RECORD LENGTH                                
         DC    AL4(REPKEY)         DISP OF KEY/KEY LENGTH                       
         DC    F'0'                MAXIMUM NUMBER OF ENTRIES                    
         DC    AL1(2)              ACTION - READ HIGH                           
         SPACE 3                                                                
JOBPARMS DS    0D                                                               
         DC    AL4(JOBLNG)         RECORD LENGTH                                
         DC    AL4(JOBKEY)         DISP OF KEY/KEY LENGTH                       
         DC    F'0'                MAXIMUM NUMBER OF ENTRIES                    
         DC    AL1(0)              ACTION - EXACT MATCH ONLY                    
         SPACE 3                                                                
JOBREC   DS    (JOBLNG)C           JOB RECORD                                   
REPREC   DS    (REPLNG)C           REPORT RECORD                                
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
MYD      DSECT                                                                  
BINSRCH  DS    V                                                                
DMCB     DS    CL24                                                             
KEY      DS    CL42                                                             
IO       DS    2000C                                                            
ELCODE   DS    CL1                                                              
SPACES   DS    CL132                                                            
MYDX     EQU   *                                                                
         EJECT                                                                  
USRBLOCD DSECT                                                                  
       ++INCLUDE ACUSRBLOCK                                                     
         EJECT                                                                  
* SAVED STORAGE IN USRBLOCK                                                     
         SPACE 3                                                                
         ORG   USRRESRV                                                         
USRSWT   DS    XL1                 FIRST TIME SWITCH                            
USRSTG   DS    CL99                NOTHING                                      
         EJECT                                                                  
       ++INCLUDE ACGETUSRD                                                      
         EJECT                                                                  
BIND     DSECT                                                                  
BINREC   DS    A                   RECORD ADDRESS                               
BINTAB   DS    A                   TABLE ADDRESS                                
BINNUM   DS    F                   NUMBER OF ENTRIES                            
         EJECT                                                                  
BIND2    DSECT                                                                  
BINLEN   DS    F                   RECORD LENGTH                                
BINDISP  DS    CL1                 DISPLACEMENT OF KEY                          
BINKEY   DS    CL3                 KEY LENGTH                                   
BINMAX   DS    F                   MAXIMUM NUMBER OF ENTRIES                    
BINACT   DS    CL1                 ACTION: 00 - EXACT MATCH ONLY                
*                                          01 - ADD IF NOT FOUND                
*                                          02 - READ HIGH                       
*                                          80 - DELETE                          
         EJECT                                                                  
* INCLUDE ACGENBOTH                                                             
         PRINT OFF                                                              
       ++INCLUDE ACGENBOTH                                                      
         PRINT ON                                                               
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'021ACGETUSR  05/01/02'                                      
         END                                                                    
