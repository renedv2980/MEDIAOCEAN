*          DATA SET SPEDUP     AT LEVEL 004 AS OF 09/12/16                      
*CATALP SPEDUP                                                                  
SPEDUP   TITLE 'EDIT SPOT UPGRADE FORMULA'                                      
SPEDUP   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKL,**EDUP**,CLEAR=YES                                         
         USING WORKD,RC            RC=A(WORKING STORAGE)                        
         LR    R2,R1                                                            
         USING SPEDUPD,R2          R2=A(CONTROL BLOCK)                          
         L     R3,SPEDAUPE                                                      
         USING UPELEM,R3           R3=A(UPGRADE ELEMENT)                        
         L     R4,SPEDAOUT         R4=A(OUTPUT AREA)                            
         MVI   0(R4),C' '                                                       
         MVC   1(79,R4),0(R4)                                                   
         MVC   0(4,R4),=C'UPX='                                                 
         MVC   2(1,R4),UPFILE                                                   
                                                                                
         MVC   DBFILE,=C'TP '      INITIALIZE DBLOCK                            
         MVC   DBCOMFCS,SPEDACOM                                                
         MVI   DBSELMED,C'T'                                                    
         CLI   SPEDMEDC,C'R'                                                    
         BNE   *+12                                                             
         MVI   DBSELMED,C'R'                                                    
         B     SPEDUP10                                                         
         CLI   SPEDCTRY,C'C'       TEST CANADA                                  
         BNE   SPEDUP10                                                         
         CLI   SPEDUDEM,C'U'       YES - TEST US DEMOS                          
         BE    SPEDUP10                                                         
         MVI   DBSELMED,C'C'       NO - SET CANADIAN MEDIA                      
                                                                                
SPEDUP10 LA    R1,UPTAB            LOOK FOR UPGRADE TYPE IN TABLE               
         LHI   R0,UPTABN                                                        
SPEDUP20 CLC   0(2,R1),UPTYPE      MATCH SPUPTYPE/SPUPSTYP                      
         BNE   *+14                                                             
         MVC   4(6,R4),2(R1)       MOVE UPGRADE TYPE NAME                       
         B     SPEDUP30                                                         
         AHI   R1,L'UPTAB                                                       
         BCT   R0,SPEDUP20                                                      
                                                                                
         XC    DUB,DUB             MUST BE A DEMO UPGRADE IF NOT FOUND          
         MVC   DUB+1(2),UPTYPE                                                  
         MVI   DUB+3,X'FF'                                                      
         GOTOR SPEDADEM,DMCB,(1,DUB),(6,4(R4)),(C'S',DBLOCK)                    
                                                                                
SPEDUP30 GOTOR ADDDELIM,SLASHQ     DELIMIT UPGRADE NAME                         
                                                                                
         LA    R1,UPTYPE+2         POINT TO FIRST UPGRADE VALUE                 
         LHI   R0,3                SET NUMBER OF VALUES                         
SPEDUP40 OC    0(2,R1),0(R1)       TEST VALUE PRESENT                           
         BZ    SPEDUP60                                                         
         CLC   0(2,R1),=H'500'     TEST BOOK OR VALUE                           
         BNH   SPEDUP52                                                         
         CLC   0(2,R1),=X'C100'    SOME VALUES START WITH LETTERS               
         BL    SPEDUP42                                                         
         MVI   0(R1),0             REMOVE THE LETTER                            
         B     SPEDUP52                                                         
                                                                                
SPEDUP42 CHI   R0,3                FIRST ITERATION?                             
         BE    SPEDUP50            YES, SKIP THIS CODE                          
         LR    R5,R1               WE HAVE MORE THAN 1 MONTH                    
         GOTOR ADDDELIM,SLASHQ     SO DELIMIT THE MONTHS WITH A '/'             
         LR    R1,R5                INSTEAD OF ' '                              
                                                                                
SPEDUP50 GOTOR EDBOOK              EDIT BOOK                                    
         B     SPEDUP60                                                         
                                                                                
SPEDUP52 SR    RF,RF               EDIT INDEX VALUE                             
         ICM   RF,3,0(R1)                                                       
         CVD   RF,DUB                                                           
         OI    DUB+L'DUB-1,X'0F'                                                
         UNPK  0(4,R4),DUB                                                      
         LHI   RF,5                                                             
         CLI   0(R4),C'0'                                                       
         BNE   *+12                                                             
         MVC   0(4,R4),1(R4)                                                    
         BCTR  RF,0                                                             
         AR    R4,RF                                                            
                                                                                
SPEDUP60 AHI   R1,2                BUMP TO NEXT VALUE                           
         BCT   R0,SPEDUP40         DO FOR MAX VALUES                            
                                                                                
         OC    UPFBK,UPFBK                                                      
         BZ    SPEDUP80                                                         
         GOTOR ADDDELIM,COMMAQ                                                  
         MVC   0(3,R4),=C'BK='                                                  
         AHI   R4,3                                                             
         GOTOR EDBOOK,UPFBK                                                     
                                                                                
         CLI   UPELEM+1,UPFBKLST-UPELEM                                         
         BL    SPEDUP80                                                         
         OC    UPFBKLST,UPFBKLST   TEST BOOK LIST SET                           
         BZ    SPEDUP80                                                         
                                                                                
         MVI   0(R4),SLASHQ                                                     
         AHI   R4,1                                                             
         LA    R1,UPFBKLST                                                      
         LA    R0,L'UPFBKLST/2                                                  
SPEDUP70 GOTOR EDBOOK                                                           
         AHI   R1,2                                                             
         OC    0(2,R1),0(R1)       TEST END OF BOOK LIST                        
         BZ    SPEDUP80                                                         
         MVI   0(R4),SLASHQ        NO - INSERT DELIMITER & DO NEXT              
         AHI   R4,1                                                             
         BCT   R0,SPEDUP70                                                      
                                                                                
SPEDUP80 OC    UPDAYTIM,UPDAYTIM                                                
         BZ    SPEDUP90                                                         
         GOTOR ADDDELIM,COMMAQ                                                  
         MVC   0(3,R4),=C'DT='                                                  
         GOTOR SPEDADAY,DMCB,UPDAYTIM,3(R4)                                     
         GOTOR ADDDELIM,SLASHQ                                                  
                                                                                
         GOTOR SPEDATIM,DMCB,UPDAYTIM+1,1(R4)                                   
                                                                                
         OC    UPSTA,UPSTA         TEST STATION OVERRIDE                        
         BZ    SPEDUP90                                                         
         GOTOR ADDDELIM,SLASHQ                                                  
         MVC   1(L'UPSTA-1,R4),UPSTA                                            
         AHI   R4,L'UPSTA                                                       
                                                                                
SPEDUP90 CLI   UPELEM+1,UP2YRP-UPELEM                                           
         BL    SPEDUPX                                                          
         CLI   UP2YRP,C'Y'                                                      
         BNE   SPEDUP99                                                         
         GOTOR ADDDELIM,COMMAQ                                                  
         MVC   0(5,R4),=C'PUT=2'                                                
         AHI   R4,5                                                             
                                                                                
SPEDUP99 CLI   UP2YRS,C'Y'                                                      
         BNE   SPEDUPX                                                          
         GOTOR ADDDELIM,COMMAQ                                                  
         MVC   0(5,R4),=C'RTG=2'                                                
                                                                                
SPEDUPX  XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
* EDIT BOOK INTO OUTPUT AREA                                          *         
*                                                                     *         
* NTRY - R1=A(BOOK VALUE)                                             *         
*        R4=A(CURRENT OUTPUT POINTER)                                 *         
* EXIT - R4=A(UPDATED OUTPUT POINTER)                                 *         
***********************************************************************         
                                                                                
EDBOOK   NTR1  ,                                                                
         LR    R5,R1                                                            
         MVC   DUB(2),0(R5)                                                     
         MVI   DUB+2,1                                                          
         NI    DUB+1,X'7F'         TURN OFF OLYM EXCL                           
         L     RF,SPEDACOM                                                      
         L     RF,CDATCON-COMFACSD(RF)                                          
         GOTOR (RF),DMCB,(3,DUB),(6,WORK)                                       
         MVC   WORK+3(2),WORK+4    MOVE YEAR LEFT                               
         MVI   WORK+5,C' '                                                      
         MVC   WORK+6(4),WORK+5                                                 
         TM    1(R5),X'80'                                                      
         BZ    *+10                                                             
         MVC   WORK+5(3),=C'(O)'                                                
         MVC   0(10,R4),WORK                                                    
         GOTOR ADDDELIM,SPACEQ                                                  
EDBOOKX  XIT1  REGS=(R4)                                                        
                                                                                
**********************************************************************          
* LOCATE END OF OUTPUT STRING AND INSERT DELIMITER (IN R1)           *          
**********************************************************************          
                                                                                
ADDDELIM AHI   R4,14                                                            
         CLI   0(R4),C' '                                                       
         BH    *+8                                                              
         BCT   R4,*-8                                                           
         STC   R1,1(R4)                                                         
         AHI   R4,2                                                             
         BR    RE                                                               
         EJECT                                                                  
COMMAQ   EQU   C','                                                             
SLASHQ   EQU   C'/'                                                             
SPACEQ   EQU   C' '                                                             
                                                                                
         LTORG                                                                  
                                                                                
UPTAB    DS    0XL8                SPUPTYPE/SPUTSTYP/UPGRADE NAME               
         DC    AL1(SPUPTRTG),X'0',C'RATING'                                     
         DC    AL1(SPUPTHUT),X'0',C'HUT   '                                     
         DC    AL1(SPUPTPUT),C'P',C'PUT   '                                     
         DC    AL1(SPUPTNDX),X'0',C'INDEX '                                     
         DC    AL1(SPUPTHPT),X'0',C'HPT   '                                     
         DC    AL1(SPUPTPIN),C'N',C'PIN   '                                     
         DC    AL1(SPUPTPIM),C'M',C'PIM   '                                     
         DC    AL1(SPUPTPIY),C'Y',C'PIY   '                                     
         DC    AL1(SPUPTPIQ),C'Q',C'PIQ   '                                     
         DC    AL1(SPUPTPIB),C'B',C'PIB   '                                     
         DC    AL1(SPUPTSIN),C'N',C'SIN   '                                     
         DC    AL1(SPUPTSIM),C'M',C'SIM   '                                     
         DC    AL1(SPUPTSIY),C'Y',C'SIY   '                                     
         DC    AL1(SPUPTSIQ),C'Q',C'SIQ   '                                     
         DC    AL1(SPUPTSIB),C'B',C'SIB   '                                     
         DC    AL1(SPUPTPAV),C'N',C'PAVG  '                                     
         DC    AL1(SPUPTPAY),C'Y',C'PAY   '                                     
         DC    AL1(SPUPTPAQ),C'Q',C'PAQ   '                                     
         DC    AL1(SPUPTPAB),C'B',C'PAB   '                                     
         DC    AL1(SPUPTSAV),C'N',C'SAVG  '                                     
         DC    AL1(SPUPTSAY),C'Y',C'SAY   '                                     
         DC    AL1(SPUPTSAQ),C'Q',C'SAQ   '                                     
         DC    AL1(SPUPTSAB),C'B',C'SAB   '                                     
UPTABN   EQU   (*-UPTAB)/L'UPTAB                                                
         EJECT                                                                  
WORKD    DSECT                     ** DSECT TO COVER WORKING STORAGE **         
DUB      DS    D                                                                
DMCB     DS    6F                                                               
WORK     DS    XL64                                                             
* DEDBLOCK                                                                      
         PRINT OFF                                                              
       ++INCLUDE DEDBLOCK                                                       
         PRINT ON                                                               
WORKL    EQU   *-WORKD                                                          
                                                                                
* SPEDUPD                                                                       
         PRINT OFF                                                              
       ++INCLUDE SPEDUPD                                                        
         PRINT ON                                                               
* SPGENBUY                                                                      
         PRINT OFF                                                              
       ++INCLUDE SPGENBUY                                                       
         PRINT ON                                                               
* SPDEMUPD                                                                      
         PRINT OFF                                                              
       ++INCLUDE SPDEMUPD                                                       
         PRINT ON                                                               
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004SPEDUP    09/12/16'                                      
         END                                                                    
