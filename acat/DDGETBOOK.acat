*          DATA SET DDGETBOOK  AT LEVEL 009 AS OF 06/26/95                      
*CATALP GETBOOK                                                                 
         TITLE 'GETBOOK - RETURN CARDS FROM CONTROL FILE BOOK'                  
*        CL1   IF NONZERO DO NOT RESOLVE NESTED BOOKS                           
*        AL3   SAVE AREA                                                        
*        CL1   IF NONZERO START SEQUENCE NUMBER                                 
*        AL3   CARD AREA                                                        
*        CL1   ERROR RETURN BYTE                                                
*        AL3   DATAMGR                                                          
*        CL1   TERMINAL # **UK**                                                
*                                                                               
*        BEFORE FIRST CALL USER MUST MOVE TO SAVE AREA THE KEY OF THE           
*        BOOK REQUIRED. THE SUBREF BYTE OF THE KEY MUST BE ZERO.                
         SPACE 2                                                                
GETBOOK  CSECT                                                                  
         NMOD1 150,**GTBK**                                                     
         USING WRKD,RC                                                          
         EJECT                                                                  
         LR    R2,R1               R2=A(PARAMS)                                 
         MVC   TERMNO,12(R2)                                                    
         LM    R3,R4,0(R1)         R3=A(SAVE),R4=A(CARD)                        
         L     RF,8(R1)            RF=A(DATAMGR)                                
         LR    R5,R3               R5=A(SAVE ROW)                               
         USING SAVED,R5                                                         
         MVI   8(R2),0             RESET ERROR BYTE                             
         CLI   SSUB,0                                                           
         BNE   GETB                                                             
         MVI   SLEVEL,0            SET LEVEL TO ZERO ON FIRST CALL              
         SPACE 2                                                                
GETB     SR    R5,R5               POINT TO SAVE ROW INDEXED BY LEVEL           
         IC    R5,SLEVEL-SAVED(R3)                                              
         MH    R5,SLEN                                                          
         AR    R5,R3                                                            
         MVC   KEY,SKEY            SET KEY                                      
         MVC   SUB,SSUB                                                         
         CLI   SUB,0                                                            
         BE    GETB2                                                            
         SR    R6,R6               SET SUBREF FROM INDEX                        
         IC    R6,SUB                                                           
         LA    R6,SNDX(R6)                                                      
         CLI   0(R6),0                                                          
         BE    GETBG                                                            
         MVC   SUB,0(R6)                                                        
         SPACE 2                                                                
GETB2    GOTO1 (RF),DMCB,(08,DMREAD),CTFILE,KEY,IOA,(TERMNO,0)                  
         CLI   8(R1),0                                                          
         BNE   DSKERR                                                           
         SPACE 2                                                                
GETB3    LA    R6,IOA+28           SEARCH FOR ELEMENT                           
         SR    R7,R7                                                            
GETB4    CLI   0(R6),0                                                          
         BE    RECERR                                                           
         CLI   0(R6),X'04'         INDEX ELEMENT                                
         BE    GETB6                                                            
         CLI   0(R6),X'10'         DATA ELEMENT                                 
         BE    GETB8                                                            
         CLI   0(R6),X'11'         NAME ELEMENT                                 
         BE    GETBA                                                            
         IC    R7,1(R6)                                                         
         CH    R7,=H'1'                                                         
         BNH   RECERR                                                           
         AR    R6,R7                                                            
         B     GETB4                                                            
         SPACE 2                                                                
GETB6    MVC   SNDX,2(R6)          R6=A(INDEX ELEMENT)                          
         MVI   SSUB,1              SET SUBREF FOR FIRST CARD                    
         CR    R5,R3               TEST FIRST CALL                              
         BNE   GETB                NO                                           
         CLI   4(R2),0             TEST START SEQ NUM PASSED                    
         BE    GETB                NO                                           
         MVC   SSUB,4(R2)          SET SUBREF TO PASSED VALUE                   
         SR    R0,R0               CHECK FOR EOF ON PASSED VALUE                
         IC    R0,SSUB                                                          
         LA    R6,SNDX+1                                                        
         CLI   0(R6),0                                                          
         BE    EOF                                                              
         LA    R6,1(R6)                                                         
         BCT   R0,*-12                                                          
         B     GETB                                                             
         SPACE 2                                                                
GETB8    MVC   0(80,R4),SPACES     R6=A(DATA ELEMENT)                           
         SR    R7,R7                                                            
         IC    R7,1(R6)                                                         
         SH    R7,=H'3'                                                         
         BM    GETBE                                                            
         EX    R7,*+8                                                           
         B     GETBE                                                            
         MVC   0(0,R4),2(R6)       MOVE DATA ELEMENT TO CARD                    
         SPACE 2                                                                
GETBA    MVC   0(80,R4),SPACES     R6=A(NAME ELEMENT)                           
         LR    RE,R4               BUILD ++INCLUDE CARD                         
         MVC   0(9,RE),=C'++INCLUDE'                                            
         CLI   3(R6),C'S'          SUPPORT FOR SCRIPT BOOKS                     
         BNE   GETBA01             ''                                           
         MVC   10(1,RE),=C'S'      ''                                           
         B     GETBA03             ''                                           
GETBA01  CLI   3(R6),C'Q'          SUPPORT FOR XTRACT BOOKS                     
         BNE   GETBA02             ''                                           
         MVC   10(1,RE),=C'Q'      ''                                           
         B     GETBA03             ''                                           
GETBA02  MVC   10(1,RE),2(R6)                                                   
GETBA03  MVI   11(RE),C','                                                      
         MVC   12(10,RE),16(R6)                                                 
         CLI   0(R2),0                                                          
         BNE   GETBE               DO NOT RESOLVE NESTS                         
GETBC    SR    R7,R7               BUMP NEST LEVEL                              
         IC    R7,SLEVEL-SAVED(R3)                                              
         LA    R7,1(R7)                                                         
         STC   R7,SLEVEL-SAVED(R3)                                              
         CH    R7,SMAX                                                          
         BH    LEVERR                                                           
         IC    R7,SSUB             BUMP SUB REF OF CURRENT ROW                  
         LA    R7,1(R7)                                                         
         STC   R7,SSUB                                                          
         AH    R5,SLEN             POSN TO NEXT ROW                             
         MVC   SKEY,2(R6)          SET KEY                                      
         MVI   SSUB,0                                                           
         MVI   SLEVEL,0                                                         
         B     GETB                                                             
         SPACE 2                                                                
GETBE    SR    R7,R7               BUMP SUB REF                                 
         IC    R7,SSUB                                                          
         LA    R7,1(R7)                                                         
         STC   R7,SSUB                                                          
         LA    R6,72(R4)           POINT TO SEQ NUM FLD ON CARD                 
         IC    R7,SLEVEL-SAVED(R3)                                              
         CVD   R7,DUB              FIRST BYTE IS LEVEL                          
         UNPK  0(1,R6),DUB                                                      
         OI    0(R6),X'F0'                                                      
         IC    R7,SSUB                                                          
         BCTR  R7,0                                                             
         CVD   R7,DUB              NEXT 3 BYTES IS SEQ NUM                      
         UNPK  1(3,R6),DUB                                                      
         OI    3(R6),X'F0'                                                      
         B     EXIT                                                             
         SPACE 2                                                                
GETBG    SR    R7,R7               END OF CURRENT LEVEL BOOK                    
         IC    R7,SLEVEL-SAVED(R3)                                              
         SH    R7,=H'1'                                                         
         BM    EOF                                                              
         STC   R7,SLEVEL-SAVED(R3)                                              
         B     GETB                                                             
         SPACE 2                                                                
DSKERR   OC    8(1,R2),DMCB+8      RETURN DMGR ERROR                            
         TM    DMCB+8,X'02'        DELETED RECS ARE OK                          
         BZ    *+12                IF JUST READ AN INDEX RECORD                 
         CLI   SSUB,0                                                           
         BE    GETB3                                                            
         TM    DMCB+8,X'10'                                                     
         BZ    EXIT                                                             
         LR    R5,R3                                                            
         CLI   SLEVEL,0                                                         
         BNE   *+12                                                             
         CLI   SSUB,0                                                           
* ??     BE    EXIT                NOT FOUND OK IF FIRST CALL I/O               
         BE    EOF                                                              
         SPACE 2                                                                
RECERR   OI    8(R2),X'01'         SET INVALID RECORD FLAG                      
         B     EXIT                                                             
         SPACE 2                                                                
LEVERR   B     RECERR              TOO MANY LEVELS                              
         SPACE 2                                                                
EOF      OI    8(R2),X'80'         SET EOF FOR END OF BOOK                      
         SPACE 2                                                                
EXIT     XMOD1 1                                                                
         EJECT                                                                  
SPACES   DC    CL80' '                                                          
SLEN     DC    H'278'                                                           
SMAX     DC    H'3'                                                             
DMREAD   DC    CL8'DMREAD'                                                      
CTFILE   DC    CL8'CTFILE'                                                      
         SPACE 2                                                                
         LTORG                                                                  
         SPACE 2                                                                
SAVED    DSECT                                                                  
SKEY     DS    CL24                                                             
SSUB     DS    CL1                                                              
SLEVEL   DS    CL1                                                              
SNDX     DS    CL252                                                            
         SPACE 2                                                                
WRKD     DSECT                                                                  
DUB      DS    D                                                                
DMCB     DS    6F                                                               
KEY      DS    CL24                                                             
SUB      DS    CL1                                                              
TERMNO   DS    CL1                                                              
IOA      DS    1024C                                                            
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'009DDGETBOOK 06/26/95'                                      
         END                                                                    
