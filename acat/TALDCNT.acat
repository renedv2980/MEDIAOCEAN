*          DATA SET TALDCNT    AT LEVEL 026 AS OF 09/26/13                      
*CATALP TALDDCNT                                                                
         TITLE 'LDCOUNT - TALENT - DIRECTORY REC TYPE COUNT/PRINT'              
*        PARAMS VIA R1                                                          
*        XL1   COUNT/PRINT  X'00'=COUNT  X'FF'=PRINT                            
*                           X'81'=SUBTRACT OUT                                  
*        AL3   A(RECORD)                                                        
*                                                                               
         PRINT NOGEN                                                            
LDCOUNT  CSECT                                                                  
         NMOD1 WRKX-WRKD,TALDDCNT                                               
         USING WRKD,RC                                                          
*                                                                               
         CLI   0(R1),X'FF'                                                      
         BE    PRINT                                                            
         SPACE 2                                                                
COUNT    L     R2,0(R1)            R2=A(RECORD)                                 
         LA    R3,RECTBL           R3=A(RECORD TYPE TABLE)                      
                                                                                
COUNT1   CLC   0(2,R3),=X'FFFF'    TEST END OF TABLE                            
         BE    COUNT1D                                                          
         CLI   0(R3),TLPUCDQ       X'24' RECORDS                                
         BNE   COUNT1A                                                          
         CLC   0(2,R2),0(R3)       TEST MATCH ON RECORD AND SUBTYPE             
         BE    COUNT2                                                           
         BNE   COUNT1B                                                          
                                                                                
COUNT1A  CLC   0(1,R2),0(R3)       TEST MATCH ON RECORD TYPE                    
         BE    COUNT2                                                           
                                                                                
COUNT1B  LA    R3,L'RECTBL(R3)                                                  
         B     COUNT1                                                           
         SPACE 1                                                                
COUNT1D  L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
******   MVC   P(7),=C'UNKNOWN'                                                 
******   MVC   P+10(32),0(R2)                                                   
******   GOTO1 =V(HEXOUT),DMCB,(R2),P+60,32,=C'TOG'                             
******   GOTO1 =V(PRINTER)                                                      
         SPACE 1                                                                
COUNT2   CLI   0(R2),TLW4NCDQ      W4 RECORD?                                   
         BNE   COUNT2B                                                          
         USING TLW4PD,R2                                                        
         TM    TLW4NSTA,TLW4NSAK   WANT TO RECORD AKA NAMES                     
         BNO   COUNT2B                                                          
         LA    R3,L'RECTBL(R3)                                                  
         SPACE 1                                                                
COUNT2B  L     RF,20(R3)           BUMP TOTAL RECORD COUNT                      
         LA    RF,1(RF)                                                         
         CLI   0(R1),X'81'         (OPTIONAL SUBTRACT)                          
         BNE   *+8                                                              
         SH    RF,=H'2'                                                         
         ST    RF,20(R3)                                                        
         LA    R4,24(R3)                                                        
*                                                                               
         USING TLRCD,R2                                                         
         TM    TLRCSTAT,X'C0'      TEST ACTIVE                                  
         BZ    COUNT3A                                                          
         LA    R4,32(R3)                                                        
         TM    TLRCSTAT,X'80'      TEST DELETED                                 
         BO    COUNT3A                                                          
         LA    R4,28(R3)                                                        
*                                                                               
COUNT3A  L     RF,0(R4)            BUMP ACTIVE/LAPSED/DELETED COUNT             
         LA    RF,1(RF)                                                         
         ST    RF,0(R4)                                                         
*                                                                               
EXIT     XMOD1 1                                                                
         EJECT                                                                  
*              PRINT DIRECTORY TOTALS                                           
         SPACE 3                                                                
PRINT    L     RA,=V(CPRINT)       PRINT HEADING                                
         USING DPRINT,RA                                                        
         ZAP   LINE,=P'99'                                                      
         MVC   MID1,HDRL1                                                       
         MVC   MID2,ULINE                                                       
         LA    R3,RECTBL           R3=A(RECORD TABLE ENTRY)                     
*                                                                               
PRINT1   L     RF,20(R3)           TEST TOTAL RECORD COUNTER                    
         LTR   RF,RF                                                            
         BZ    PRINT3              DONT PRINT IF ZERO                           
         MVC   PLNAME,4(R3)                                                     
         CVD   RF,DUB                                                           
         UNPK  DUB1,DUB                                                         
         OI    DUB1+7,C'0'                                                      
         MVC   PLRTOT,DUB1                                                      
         LA    R4,24(R3)           R4=A(RECORD COUNTER)                         
         LA    R5,PLRACT           R5=A(PLINE DISPLAY FIELD)                    
         LA    R0,3                                                             
*                                                                               
PRINT2   L     RF,0(R4)            TEST RECORD COUNTER                          
         LTR   RF,RF                                                            
         BNZ   PRINT2A                                                          
         MVC   0(8,R5),=8C'.'      SET DOTS IF ZERO                             
         B     PRINT2B                                                          
PRINT2A  CVD   RF,DUB                                                           
         UNPK  DUB1,DUB                                                         
         OI    DUB1+7,C'0'                                                      
         MVC   0(8,R5),DUB1                                                     
PRINT2B  LA    R4,4(R4)            BUMP TO NEXT RECORD COUNTER                  
         LA    R5,9(R5)                                                         
         BCT   R0,PRINT2                                                        
PRINT2C  MVC   P,PLINE                                                          
         GOTO1 =V(PRINTER)                                                      
*                                                                               
PRINT3   CLC   0(2,R3),=X'FFFF'    EXIT IF END OF TABLE                         
         BE    PRINTX                                                           
         LA    R3,L'RECTBL(R3)                                                  
         B     PRINT1                                                           
*                                                                               
PRINTX   B     EXIT                                                             
         EJECT                                                                  
*              HEADINGS ETC                                                     
         SPACE 3                                                                
HDRL1    DC   CL132'DIRECTORY TYPE   TOT RECS  ACTIVE   LAPSED   DELETE*        
               D'                                                               
ULINE    DC   CL132'---------------- -------- -------- -------- -------*        
               -'                                                               
         SPACE 2                                                                
PLINE    DC    CL132' '                                                         
         ORG   PLINE                                                            
PLNAME   DS    CL16                                                             
         DS    CL1                                                              
PLRTOT   DS    CL8                                                              
         DS    CL1                                                              
PLRACT   DS    CL8                                                              
         DS    CL1                                                              
PLRLAP   DS    CL8                                                              
         DS    CL1                                                              
PLRDEL   DS    CL8                                                              
         ORG                                                                    
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
       ++INCLUDE TALDRECTB                                                      
         EJECT                                                                  
*DDDPRINT                                                                       
*TAGENFILE                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDDPRINT                                                       
       ++INCLUDE TAGENFILE                                                      
         PRINT ON                                                               
         SPACE 3                                                                
WRKD     DSECT                                                                  
DUB      DS    D                                                                
DUB1     DS    D                                                                
DMCB     DS    6F                                                               
*                                                                               
WRKX     EQU   *                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'026TALDCNT   09/26/13'                                      
         END                                                                    
