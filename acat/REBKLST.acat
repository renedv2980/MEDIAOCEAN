*          DATA SET REBKLST    AT LEVEL 016 AS OF 09/26/96                      
*CATALP REBKLST                                                                 
         TITLE 'BUILD LIST OF FROM-TO BOOKS'                                    
***********************************************************************         
*  HISTORY OF CHANGES                                                 *         
***********************************************************************         
*  SEP26/96 (GL ) --- VALIDATE ONE TO THREE BYTES OF SOURCE           *         
*                                                                     *         
*  DEC22/93 (BU ) --- FORCE SERVICE DEFAULT TO NSI WITH DEMISE OF ARB *         
*                                                                     *         
*                                                                     *         
*                                                                     *         
*                                                                     *         
*                                                                     *         
***********************************************************************         
*              PARA 1    BYTE 1-3  A(FIELD HEADER)                              
*              PARA 2    BYTE 1-3  A(OUTPUT LIST) RAVL BKS                      
*              PARA 3    BYTE 1-3  A(BOOKVAL)                                   
*              PARA 4    BYTE 1-3  A(SCANNER)                                   
*        AFTER BKLST -                                                          
*              PARA 1    BYTE 0    NUMBER OF LIST ENTRIES                       
*         ENTRIES CONSIST OF 7 BYTE PAIRS OF BOOK EXPRESSIONS                   
*                                                                               
         SPACE 2                                                                
BKLST    CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 70,BKLST                                                         
         USING BKLD,RC                                                          
         MVC   PARM(16),0(R1)                                                   
         ST    R1,APARM                                                         
         L     R2,FIELD                                                         
         ZIC   R3,5(R2)                                                         
         SPACE 1                                                                
         XC    WORK,WORK                                                        
         ZIC   R1,0(R2)                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   WORK(0),0(R2)       SAVE HEADER AND FIELD                        
         LA    R4,WORK+8                                                        
         SPACE 1                                                                
BKLST1   CLC   0(2,R4),=C'--'      N81--EM82  BECOMES                           
         BNE   BKLST2              N81=-EM82                                    
         MVI   0(R4),C'='                                                       
         LA    R4,2(R4)                                                         
         SH    R3,=H'2'                                                         
         BNP   BKLST3                                                           
         B     BKLST1                                                           
         SPACE 1                                                                
BKLST2   CLI   0(R4),C'-'          N81-EM82  BECOMES                            
         BNE   *+8                                                              
         MVI   0(R4),C'='                                                       
         LA    R4,1(R4)                                                         
         BCT   R3,BKLST1                                                        
         EJECT                                                                  
BKLST3   GOTO1 SCANNER,DMCB,WORK,(10,BLOCK),0                                   
         CLI   DMCB+4,0                                                         
         BE    ERROR                                                            
         ZIC   R3,DMCB+4                                                        
         LA    R4,BLOCK                                                         
         MVI   SRC,C'N'            SET DEFAULT TO NSI                           
******>  MVI   SRC,C'B'            SET DEFAULT TO BOTH ARB,NSI                  
         CLI   1(R4),0                                                          
         BNE   BKLST5                                                           
                                                                                
         DS    0H                  FIRST INPUT MAY BE A RTG SVCE                
         CLI   0(R4),3              IF L(INPUT) > 3,                            
         BH    BKLST5                THEN IT CAN'T BE A RTG SVCE                
                                                                                
         ZIC   R1,0(R4)                                                         
         BCTR  R1,0                 R1 USED FOR EXECUTED LENGTH                 
         MVI   SRC,C'M'                                                         
         LA    RF,=C'MFX'           ASSUME MEDIAFAX                             
         EX    R1,EXCLCSRC                                                      
         BE    BKLST4                (TREAT MFX SAME AS ARB)                    
         MVI   SRC,C'A'                                                         
         LA    RF,=C'ARB'           ASSUME ARBITRON                             
         EX    R1,EXCLCSRC                                                      
         BE    BKLST4                                                           
         MVI   SRC,C'N'                                                         
         LA    RF,=C'NSI'           ASSUME NIELSEN                              
         EX    R1,EXCLCSRC                                                      
         BE    BKLST4                                                           
         MVI   SRC,C'S'                                                         
         LA    RF,=C'SRC'           ASSUME STRATEGY                             
         EX    R1,EXCLCSRC                                                      
         BE    BKLST4                                                           
                                                                                
         DS    0H                  COULDN'T FIND A MATCHING RTG SVCE            
         MVI   SRC,C'N'             DEFAULT TO NSI AGAIN                        
         CLI   0(R4),3              IF L(INPUT) = 3                             
         BE    BKLST4                IT'S POSSIBLE THAT INPUT IS A BOOK         
         B     ERROR                FOR LEN < 3, IT MUST BE AN ERROR            
         SPACE 1                                                                
EXCLCSRC CLC   12(0,R4),0(RF)      R4-->INPUT, RF-->A VALID RTG SVCE            
                                                                                
*                                                                               
BKLST4   LA    R4,32(R4)                                                        
         SH    R3,=H'1'                                                         
         BNP   ERROR                                                            
         SPACE 1                                                                
BKLST5   L     R5,LIST                                                          
         SR    R0,R0                                                            
         SPACE 1                                                                
BKLST6   ZIC   R1,0(R4)                                                         
         LTR   R1,R1               DEFEND AGAINST DOUBLE COMMA                  
         BZ    ERROR                                                            
         XC    WORK,WORK                                                        
         STC   R1,WORK+5           DUMMY HEADER FOR BOOKVAL                     
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   WORK+8(0),12(R4)                                                 
         LA    R1,9(R1)                                                         
         STC   R1,WORK                                                          
         MVI   WORK2,0                                                          
         GOTO1 BOOKVAL,DMCB,(C'A',WORK),(1,(R5)),(C'B',SCANNER),WORK2           
         CLI   DMCB+4,0                                                         
         BE    ERROR                                                            
         MVC   3(1,R5),WORK2       BOOK TYPE                                    
         SPACE 1                                                                
         CLI   SRC,C'M'                                                         
         BE    BKLST7              ONLY MFX (TREAT LIKE ARB)                    
         CLI   SRC,C'A'                                                         
         BE    BKLST7              ONLY ARB                                     
         OI    0(R5),X'40'                                                      
         CLI   SRC,C'N'                                                         
         BE    BKLST7              ONLY NSI                                     
         CLI   SRC,C'S'                                                         
         BNE   BKLST6G                                                          
         TM    0(R5),X'0A'         SRC CAN'T BE TP OR SPECIAL SURVEY            
         BNZ   ERROR                                                            
         OI    0(R5),X'01'                                                      
         B     BKLST7              ONLY SRC                                     
BKLST6G  MVC   7(4,R5),0(R5)                                                    
         NI    7(R5),X'BF'         BOTH = NSI AND ARB                           
         SPACE 1                                                                
BKLST7   MVC   4(3,R5),0(R5)       TO BOOK SAME AS FROM                         
         MVC   11(3,R5),7(R5)      FOR BOTH BOOKS                               
         AH    R0,=H'1'            COUNT OUTPUT LIST                            
         OC    7(3,R5),7(R5)                                                    
         BZ    *+8                 ONLY ONE BOOK                                
         AH    R0,=H'1'            BOTH BOOKS                                   
         SPACE 1                                                                
         CLI   1(R4),0             NO TO BOOK ,FROM IS DEFAULT                  
         BE    BKLST10                                                          
         ZIC   R1,1(R4)                                                         
         TM    3(R4),X'C0'         DEFEND AGAINST LEADING BLANK                 
         BNZ   *+12                                                             
         CLI   22(R4),C' '                                                      
         BE    ERROR                                                            
*                                                                               
         XC    WORK,WORK                                                        
         STC   R1,WORK+5           DUMMY HEADER FOR BOOKVAL                     
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   WORK+8(0),22(R4)                                                 
         LA    R1,9(R1)                                                         
         STC   R1,WORK                                                          
         GOTO1 BOOKVAL,DMCB,(C'A',WORK),(1,4(R5)),SCANNER                       
         CLI   DMCB+4,0                                                         
         BE    ERROR                                                            
         SPACE 1                                                                
         CLI   SRC,C'M'                                                         
         BE    BKLST10             ONLY MFX (TREAT LIKE ARB)                    
         CLI   SRC,C'A'                                                         
         BE    BKLST10             ONLY ARB                                     
         OI    4(R5),X'40'                                                      
         CLI   SRC,C'N'                                                         
         BE    BKLST10             ONLY NSI                                     
         CLI   SRC,C'S'                                                         
         BNE   BKLST9                                                           
         TM    4(R5),X'0A'         SRC CAN'T BE TP OR SPECIAL SURVEY            
         BNZ   ERROR                                                            
         OI    4(R5),X'01'                                                      
         B     BKLST10             ONLY SRC                                     
BKLST9   MVC   11(3,R5),4(R5)                                                   
         NI    11(R5),X'BF'        BOTH = NSI AND ARB                           
         SPACE                                                                  
BKLST10  LA    R5,7(R5)            NEXT OUTPUT AREA                             
         CLI   SRC,C'B'                                                         
         BNE   *+8                                                              
         LA    R5,7(R5)            BOTH = ARB AND NSI                           
         LA    R4,32(R4)           NEST BLOCK                                   
         BCT   R3,BKLST6                                                        
         EJECT                                                                  
EXIT     L     R1,APARM                                                         
         STC   R0,0(R1)            RETURN NUMBER OF VALID ENTRIES               
EXXMOD   XMOD1 1                                                                
ERROR    SR    R0,R0                                                            
         B     EXIT                                                             
         EJECT                                                                  
BKLD     DSECT                                                                  
APARM    DS    F                                                                
PARM     DS    0F                                                               
FIELD    DS    F                                                                
LIST     DS    F                                                                
BOOKVAL  DS    A                                                                
SCANNER  DS    A                                                                
         SPACE 1                                                                
BLOCK    DS    10CL32                                                           
         SPACE 1                                                                
WORK     DS    CL64                                                             
DMCB     DS    6F                                                               
SRC      DS    CL1        A=ARB, N=NSI, S=SRC, B=ARB AND NSI, M=MFX             
WORK2    DS    CL1                                                              
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'016REBKLST   09/26/96'                                      
         END                                                                    
