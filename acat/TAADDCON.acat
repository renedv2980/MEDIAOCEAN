*          DATA SET TAADDCON   AT LEVEL 062 AS OF 05/01/02                      
*CATALP TAADDCON                                                                
         TITLE 'TAADDCON - ADDRESS CONVERTER FOR TALENT'                        
         PRINT NOGEN                                                            
         SPACE 1                                                                
TAADDCON CSECT                                                                  
         NMOD1 MYEND-MYSTART,**ADCN**                                           
         USING MYD,RC                                                           
         L     RA,0(R1)            P1=A(ADDCOND)                                
         USING ADDCOND,RA                                                       
         XC    TADCDIAG,TADCDIAG   PRE CLEAR RETURN AREAS                       
         XC    TADCERRS,TADCERRS                                                
         MVC   TADCADD,SPACES                                                   
         MVC   TADCADD2,SPACES                                                  
         MVC   TADCCITY,SPACES                                                  
         MVC   TADCSTAT,SPACES                                                  
         MVC   TADCZIP,SPACES                                                   
         BAS   RE,EXTRACT          EXTRACT FROM ADDRESS ELEMENT                 
         BAS   RE,GETZIP           STRIP OUT ZIP                                
         BAS   RE,GETSTATE         STRIP OUT STATE                              
         BAS   RE,DEDCITY          DEDUCE CITY                                  
         BAS   RE,DEDADD           DEDUCE STREET                                
         BAS   RE,TESTRES          TEST RESULTS                                 
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO SPACE OUT ADDRESS LINES                               
         SPACE 3                                                                
EXTRACT  NTR1                                                                   
         L     R6,TADCAADD                                                      
         MVC   WORKADD,SPACES                                                   
         MVC   WORKL1,0(R6)                                                     
         MVC   WORKL2,30(R6)                                                    
         MVC   WORKL3,60(R6)                                                    
         MVC   WORKL4,90(R6)                                                    
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO GET ZIP                                               
         SPACE 3                                                                
*              OUTPUT              NUMERIC ZIP RETURNED IN TADCZIP              
*                                  IF FOUND, REMOVED FROM WORKADD               
         SPACE 1                                                                
GETZIP   NTR1                                                                   
         TM    TADCSTA2,X'04'      FOREIGN ADDRESS                              
         BO    XIT                                                              
         BAS   RE,GETZIP1                                                       
         CLI   TADCZIP,C' '                                                     
         BH    XIT                                                              
         SPACE 1                                                                
         CLI   LWORD,0                                                          
         BE    XIT                                                              
         MVC   WORK(9),=9C'0'      INVALID ZIP                                  
         ZIC   R1,LWORD                                                         
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVZ   WORK(0),WORD                                                     
         CLC   WORK(9),=9C'0'      IF VALID NUMERIC                             
         BNE   XIT                                                              
         BAS   RE,REMWORD             GET RID OF REMNANT                        
         B     XIT                                                              
         SPACE 1                                                                
GETZIP1  NTR1                                                                   
         BAS   RE,GETWORD                                                       
         L     R1,AWORD                                                         
         LTR   R1,R1                                                            
         BZ    XIT                                                              
         CLI   LWORD,9             MAYBE 9 CHARACTER ZIP W/O DASH?              
         BNE   GETZIP2                                                          
         MVC   WORK(9),=9C'0'                                                   
         MVZ   WORK(9),WORD                                                     
         CLC   WORK(9),=9C'0'                                                   
         BNE   XIT                                                              
         MVC   WORK(4),WORD+5      (REPAIR TO 99999-9999)                       
         MVI   WORD+5,C'-'                                                      
         MVC   WORD+6(4),WORK                                                   
         MVI   LWORD,10                                                         
         B     GETZIP10                                                         
         SPACE 1                                                                
GETZIP2  CLI   LWORD,7             MAYBE SSZZZZZ?                               
         BNE   GETZIP4                                                          
         MVC   WORK(9),=9C'0'                                                   
         MVZ   WORK(5),WORD+2                                                   
         CLC   WORK(9),=9C'0'                                                   
         BNE   XIT                                                              
         CLI   WORD,C'Z'                                                        
         BH    XIT                                                              
         CLI   WORD+1,C'Z'                                                      
         BH    XIT                                                              
         MVI   TADCSPZS,1          INDICATE ZIP SPLIT                           
         L     R1,AWORD            (REPAIR TO SS 99999)                         
         MVI   2(R1),C' '                                                       
         MVC   3(5,R1),WORD+2                                                   
         BAS   RE,GETWORD          NOW ISOLATE THE ZIP                          
         B     GETZIP10                                                         
         SPACE 1                                                                
GETZIP4  CLI   LWORD,12            MAYBE SSZZZZZ-ZZZZ?                          
         BNE   GETZIP10                                                         
         MVC   WORK(9),=9C'0'                                                   
         MVZ   WORK(5),WORD+2                                                   
         CLC   WORK(9),=9C'0'                                                   
         BNE   XIT                                                              
         CLI   WORD,C'Z'                                                        
         BH    XIT                                                              
         CLI   WORD+1,C'Z'                                                      
         BH    XIT                                                              
         MVI   TADCSPZS,1          INDICATE ZIP SPLIT                           
         L     R1,AWORD            (REPAIR TO SS 99999-9999)                    
         MVI   2(R1),C' '                                                       
         MVC   3(10,R1),WORD+2                                                  
         BAS   RE,GETWORD          NOW ISOLATE THE ZIP                          
         SPACE 1                                                                
GETZIP10 MVC   WORK(9),=9C'0'                                                   
         MVZ   WORK(5),WORD                                                     
         CLI   WORD+5,C' '         5 CHARACTER ZIP?                             
         BE    GETZIP12                                                         
         CLI   WORD+5,C'-'         ZIP CAN ALSO BE 99999-9999                   
         BNE   XIT                                                              
         MVZ   WORK+5(4),WORD+6                                                 
         SPACE 1                                                                
GETZIP12 CLC   WORK(9),=9C'0'      CHECK VALID NUMERIC                          
         BNE   XIT                                                              
         MVC   TADCZIP,WORD        GOOD ZIP - RETURN                            
         BAS   RE,REMWORD          REMOVE                                       
         B     XIT                                                              
         SPACE 1                                                                
REMWORD  NTR1                                                                   
         L     R2,AWORD                                                         
         ZIC   R1,LWORD                                                         
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     XIT                                                              
         MVC   0(0,R2),SPACES                                                   
         EJECT                                                                  
*              ROUTINE TO GET STATE                                             
         SPACE 3                                                                
GETSTATE NTR1                                                                   
         TM    TADCSTA2,X'04'      FOREIGN ADDRESS                              
         BO    XIT                                                              
         BAS   RE,GETWORD                                                       
         CLI   LWORD,0                                                          
         BE    XIT                                                              
         CLI   LWORD,2                                                          
         BL    GETST20             (1 CHARACTER CAN BE P/O 'N Y')               
         BNE   GETST10                                                          
         LA    R1,STATETBL         CHECK FOR 2 CHARACTER STATE                  
         SPACE 1                                                                
GETST2   CLC   6(2,R1),WORD                                                     
         BE    GETST4                                                           
         LA    R1,10(R1)                                                        
         CLI   0(R1),X'FF'                                                      
         BNE   GETST2                                                           
         B     GETST10                                                          
         SPACE 1                                                                
GETST4   MVC   TADCSTAT,WORD       FOUND - RETURN                               
         BAS   RE,REMWORD                                                       
         B     XIT                                                              
         SPACE 1                                                                
GETST10  LA    R1,ONETABLE         TRY ONE WORD TABLE                           
         ZIC   RF,LWORD                                                         
         BCTR  RF,0                                                             
         SPACE 1                                                                
GETST12  EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   2(0,R1),WORD                                                     
         BE    GETST14                                                          
         LA    R1,16(R1)                                                        
         CLI   0(R1),X'FF'                                                      
         BNE   GETST12                                                          
         B     GETST20                                                          
         SPACE 1                                                                
GETST14  MVC   TADCSTAT,0(R1)      FOUND - RETURN                               
         TM    0(R1),X'40'         40 BIT OFF MEANS                             
         BO    GETST16                                                          
         OI    TADCSTAT,X'40'                                                   
         MVI   TADCSTDE,1          STATE WAS DEDUCED                            
         B     XIT                                                              
         SPACE 1                                                                
GETST16  MVI   TADCSTTR,1          INDICATE STATE TRANSLATED                    
         BAS   RE,REMWORD                                                       
         B     XIT                                                              
         SPACE 1                                                                
GETST20  MVC   WORD2,WORD          SAVE THE SECOND WORD                         
         L     R2,AWORD                 AND ADDRESS                             
         ZIC   R1,LWORD                 AND LENGTH                              
         BAS   RE,REMWORD          REMOVE IT FOR THE MOMENT                     
         BAS   RE,GETWORD          NOW GET THE FIRST OF TWO                     
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),WORD2       PUT SECOND WORD                              
*                                  PUT THE TWO WORDS TOGETHER                   
         GOTO1 TADCASQ,PARAS,WORD,64                                            
         LA    R1,TWOTABLE         TRY TWO WORD TABLE                           
         SPACE 1                                                                
GETST22  CLC   2(14,R1),WORD                                                    
         BE    GETST24                                                          
         LA    R1,16(R1)                                                        
         CLI   0(R1),X'FF'                                                      
         BNE   GETST22                                                          
         B     XIT                                                              
         SPACE 1                                                                
GETST24  MVC   TADCSTAT,0(R1)      FOUND - RETURN                               
         TM    0(R1),X'40'         40 BIT OFF MEANS                             
         BO    GETST26                                                          
         OI    TADCSTAT,X'40'                                                   
         MVI   TADCSTDE,1          STATE WAS DEDUCED                            
         B     XIT                                                              
         SPACE 1                                                                
GETST26  MVI   TADCSTTR,1          INDICATE STATE TRANSLATED                    
         BAS   RE,GETWORD          ADDRESS THE SECOND                           
         BAS   RE,REMWORD          AND REMOVE                                   
         BAS   RE,GETWORD          ADDRESS THE FIRST                            
         BAS   RE,REMWORD          AND REMOVE THAT TOO                          
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO DEDUCE CITY                                           
         SPACE 3                                                                
*                                  CITY IS FIRST 25 CHARACTERS                  
*                                  OF LAST CURRENT ADDRESS LINE                 
*                                  AFTER WE HAVE STRIPPED STATE/ZIP             
*              OUTPUT              RETURN TADCCITY                              
*                                  REMOVE CITY FROM ADDRESS                     
         SPACE 1                                                                
DEDCITY  NTR1                                                                   
         TM    TADCSTA2,X'04'      FOREIGN ADDRESS                              
         BNO   DEDCITY1                                                         
         LA    R1,WORKL4                                                        
         CLC   0(30,R1),SPACES     ONLY BOTHER IF 4 LINES                       
         BNE   DEDCITY2                                                         
         B     XIT                                                              
         SPACE 1                                                                
DEDCITY1 BAS   RE,GETWORD          (DROP TRAILING COMMA, PERIOD)                
         LA    R1,WORKL4                                                        
         CLC   0(30,R1),SPACES                                                  
         BNE   DEDCITY2                                                         
         LA    R1,WORKL3                                                        
         CLC   0(30,R1),SPACES                                                  
         BNE   DEDCITY2                                                         
         LA    R1,WORKL2                                                        
         CLC   0(30,R1),SPACES                                                  
         BNE   DEDCITY2                                                         
         MVI   TADCBCTY,1          MISSING                                      
         B     XIT                                                              
         SPACE 1                                                                
DEDCITY2 MVC   TADCCITY,0(R1)                                                   
         CLC   25(5,R1),SPACES                                                  
         BE    *+8                                                              
         MVI   TADCBCTY,1          TOO LONG                                     
         MVC   0(30,R1),SPACES                                                  
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO DEDUCE STREET ADDRESS                                 
         SPACE 3                                                                
DEDADD   NTR1                                                                   
         MVC   TADCADD,WORKL1                                                   
         MVC   TADCADD2,WORKL2                                                  
         MVC   TADCADD3,WORKL3                                                  
         CLC   WORKL4,SPACES                                                    
         BE    XIT                 EASY - FITS INTO 3 LINES                     
         SPACE 1                                                                
*                                  TRY AND SQUASH/CHOP                          
         MVC   SQSHADD,WORKADD     SQUASH WHAT'S LEFT                           
         GOTO1 TADCASQ,PARAS,SQSHADD,132                                        
         GOTO1 TADCACHP,PARAS,(132,SQSHADD),(30,ADDBLOCK),4                     
         MVC   TADCADD,ADDBL1                                                   
         MVC   TADCADD2,ADDBL2                                                  
         MVC   TADCADD3,ADDBL3                                                  
         CLC   ADDBL4,SPACES                                                    
         BE    XIT                 OK - NOW FITS INTO 3 LINES                   
         SPACE 1                                                                
         BAS   RE,ADDRADJ          TRY AND TRIM ADDRESS                         
         MVC   SQSHADD,WORKADD     SQUASH WHAT'S LEFT                           
         GOTO1 TADCASQ,PARAS,SQSHADD,132                                        
         GOTO1 TADCACHP,PARAS,(132,SQSHADD),(30,ADDBLOCK),4                     
         MVC   TADCADD,ADDBL1                                                   
         MVC   TADCADD2,ADDBL2                                                  
         MVC   TADCADD3,ADDBL3                                                  
         CLC   ADDBL4,SPACES                                                    
         BE    XIT                 OK - NOW FITS INTO TWO LINES                 
         MVI   TADCTLNG,1          INDICATE ADDRESS TOO LONG                    
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO TEST RESULTS AND SET ERRORS                           
         SPACE 3                                                                
TESTRES  NTR1                                                                   
         TM    TADCSTA2,X'04'      FOREIGN ADDRESS                              
         BO    XIT                 SKIP CHECKS                                  
         CLI   TADCZIP,C' '        CHECK ON ZIP                                 
         BH    *+8                                                              
         MVI   TADCBZIP,1                                                       
         MVC   TADCW4ST,TADCSTAT   RETURN CURRENT STATE - MAY CHANGE            
         CLI   TADCSTAT,C' '       CHECK ON STATE                               
         BH    *+8                                                              
         MVI   TADCBSTA,1                                                       
         CLI   TADCZIP,C' '        IF WE HAVE ZIP                               
         BNH   XIT                                                              
         CLI   TADCSTAT,C' '                  AND STATE                         
         BNH   XIT                                                              
         LA    R1,STATETBL         LOOK UP IN TABLE                             
         SPACE 1                                                                
ZIPCHK   CLC   TADCZIP(3),0(R1)         FOR MATCH ON ZIP RANGE                  
         BL    ZIPCHK2                                                          
         CLC   TADCZIP(3),3(R1)                                                 
         BH    ZIPCHK2                                                          
         CLC   TADCSTAT,6(R1)      DID OUR STATE MATCH?                         
         BE    XIT                                                              
         MVC   TADCSTAT,6(R1)      IF NOT, USE THIS STATE                       
         MVI   TADCNMAT,1          INDICATE NO MATCH                            
         B     XIT                                                              
         SPACE 1                                                                
ZIPCHK2  LA    R1,10(R1)                                                        
         CLI   0(R1),X'FF'                                                      
         BNE   ZIPCHK                                                           
         MVI   TADCBZIP,1          INDICATE BAD ZIP                             
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO FIND THE LAST 'WORD' IN WORK ADDRESS                  
         SPACE 3                                                                
*              OUTPUT              WORD FILLED, LEFT ALIGNED                    
*                                  LWORD SET TO L'WORD                          
*                                  AWORD SET TO A(WORD)                         
         SPACE 1                                                                
GETWORD  NTR1                                                                   
         MVC   WORD,SPACES                                                      
         MVI   LWORD,0                                                          
         XC    AWORD,AWORD                                                      
         LA    R1,ENDADD                                                        
         LA    R0,132                                                           
         SPACE 1                                                                
GETWORD2 CLI   0(R1),C','          DROP ENDING COMMA                            
         BNE   *+8                                                              
         MVI   0(R1),C' '                                                       
         CLI   0(R1),C'.'               OR PERIOD                               
         BNE   *+8                                                              
         MVI   0(R1),C' '                                                       
         CLI   0(R1),C' '          FIND LAST NON-SPACE                          
         BNE   GETWORD4                                                         
         BCTR  R1,0                                                             
         BCT   R0,GETWORD2                                                      
         B     XIT                                                              
         SPACE 1                                                                
GETWORD4 LR    R2,R1               R2 NOW HAS A(LAST BYTE)                      
         BCTR  R1,0                                                             
         BCT   R0,GETWORD6                                                      
         B     XIT                                                              
         SPACE 1                                                                
GETWORD6 CLI   0(R1),C','          COMMA IS DELIMITER                           
         BNE   *+8                                                              
         MVI   0(R1),C' '                SO MAKE IT A SPACE                     
         CLI   0(R1),C'.'          PERIOD IS ALSO                               
         BNE   *+8                                                              
         MVI   0(R1),C' '                SO MAKE IT A SPACE                     
         CLI   0(R1),C' '          NOW FIND PREVIOUS SPACE                      
         BE    GETWORD8                                                         
         BCTR  R1,0                                                             
         BCT   R0,GETWORD6                                                      
         B     XIT                                                              
         SPACE 1                                                                
GETWORD8 SR    R2,R1               FIGURE OUT LENGTH                            
         STC   R2,LWORD                                                         
         LA    R1,1(R1)                                                         
         ST    R1,AWORD            SAVE ITS ADDRESS                             
         CH    R2,=H'32'                                                        
         BL    *+8                                                              
         LA    R2,32                                                            
         BCTR  R2,0                                                             
         EX    R2,*+8                                                           
         B     XIT                                                              
         MVC   WORD(0),0(R1)                                                    
         EJECT                                                                  
*              ADJUST CURRENT ADDRESS TO GET RID OF REDUNDANT                   
         SPACE 3                                                                
ADDRADJ  NTR1                                                                   
         LA    R2,WORKADD          START AT FIRST ADDRESS LINE                  
         LA    R3,3                LOOP FOR FIRST 3 LINES                       
         BAS   RE,ADJUST           ADJUST IF THIS LINE NOT NEEDED               
         LA    R2,31(R2)           BUMP TO NEXT LINE                            
         BCT   R3,*-8                                                           
         B     XIT                                                              
         SPACE 3                                                                
ADJUST   NTR1                                                                   
         LA    RF,ADDRTAB          SEE IF LINE STARTS SPECIALLY                 
         XR    R1,R1                                                            
         SPACE 1                                                                
ADJ2     CLI   0(RF),X'FF'                                                      
         BE    XIT                 NO                                           
         IC    R1,0(RF)                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R2),1(RF)       MATCH ON LITERAL                             
         BE    *+12                                                             
         LA    RF,2(R1,RF)         ELSE BUMP TO NEXT TABLE ENTRY                
         B     ADJ2                                                             
         SPACE 1                                                                
         MVC   0(30,R2),31(R2)     YES - SHUFFLE UP REMAINING LINES             
         LA    R2,31(R2)                                                        
         BCT   R3,*-10             R3=N'LINES REMAINING (SEE ABOVE)             
         MVC   0(30,R2),SPACES     CLEAR LAST LINE                              
         MVI   TADCLREM,1          INDICATE A LINE WAS REMOVED                  
         B     XIT                                                              
         SPACE 1                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
*              LTORG ETC                                                        
         SPACE 3                                                                
         LTORG                                                                  
         SPACE 1                                                                
SPACES   DC    CL132' '                                                         
         SPACE 1                                                                
ADDRTAB  DS    0C                  1ST BYTE IS LENGTH-1 OF LITERAL              
         DC    AL1(3),C'FSO '                                                   
         DC    AL1(4),C'F/S/O'                                                  
         DC    AL1(4),C'SUITE'                                                  
         DC    AL1(2),C'APT'                                                    
         DC    AL1(3),C'ROOM'                                                   
         DC    AL1(1),C'C/'                                                     
         DC    AL1(3),C'P.O.'                                                   
         DC    AL1(2),C'BOX'                                                    
         DC    AL1(3),C'ATTN'                                                   
         DC    AL1(0),C'%'                                                      
         DC    AL1(0),C'-'                                                      
         DC    AL1(0),C'#'                                                      
         DC    X'FF'                                                            
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
*              ZIP/STATE LOOK-UP TABLE                                          
*              FORMAT OF STATETBL                                               
*          POS 1-3    FIRST 3 ZIP DIGITS FROM                                   
*              4-6    FIRST 3 ZIP DIGITS TO                                     
*              7-8    STATE NAME ABBR (OFFICIAL US POSTAL CODE)                 
*              9-10   STATE CODE                                                
         SPACE 1                                                                
STATETBL DS    0F                                                               
         DC    CL10'008008VIVI'                                                 
         DC    CL10'006009PRPR'                                                 
         DC    CL10'010027MA25'                                                 
         DC    CL10'028029RI44'                                                 
         DC    CL10'030038NH33'                                                 
         DC    CL10'039049ME23'                                                 
         DC    CL10'050059VT50'                                                 
         DC    CL10'060069CT09'                                                 
         DC    CL10'070089NJ34'                                                 
         DC    CL10'090149NY36'                                                 
         DC    CL10'150196PA42'                                                 
         DC    CL10'197199DE10'                                                 
         DC    CL10'200205DC11'                                                 
         DC    CL10'206219MD24'                                                 
         DC    CL10'220246VA51'                                                 
         DC    CL10'247268WV54'                                                 
         DC    CL10'270289NC37'                                                 
         DC    CL10'290299SC45'                                                 
         DC    CL10'300319GA13'                                                 
         DC    CL10'320349FL12'                                                 
         DC    CL10'350369AL01'                                                 
         DC    CL10'370385TN47'                                                 
         DC    CL10'386397MS28'                                                 
         DC    CL10'400427KY21'                                                 
         DC    CL10'430458OH39'                                                 
         DC    CL10'460479IN18'                                                 
         DC    CL10'480499MI26'                                                 
         DC    CL10'500528IA19'                                                 
         DC    CL10'530549WI55'                                                 
         DC    CL10'550567MN27'                                                 
         DC    CL10'570577SD46'                                                 
         DC    CL10'580588ND38'                                                 
         DC    CL10'590599MT30'                                                 
         DC    CL10'600629IL17'                                                 
         DC    CL10'630658MO29'                                                 
         DC    CL10'660679KS20'                                                 
         DC    CL10'680693NE31'                                                 
         DC    CL10'700714LA22'                                                 
         DC    CL10'716729AR05'                                                 
         DC    CL10'730749OK40'                                                 
         DC    CL10'750799TX48'                                                 
         DC    CL10'800816CO08'                                                 
         DC    CL10'820831WY56'                                                 
         DC    CL10'832838ID16'                                                 
         DC    CL10'840847UT49'                                                 
         DC    CL10'850865AZ04'                                                 
         DC    CL10'870884NM35'                                                 
         DC    CL10'890898NV32'                                                 
         DC    CL10'900966CA06'                                                 
         DC    CL10'967968HI15'                                                 
         DC    CL10'970979OR41'                                                 
         DC    CL10'980994WA53'                                                 
         DC    CL10'995999AK02'                                                 
         DC    X'FF'                                                            
         EJECT                                                                  
*              ONE WORD STATE TABLE                                             
         SPACE 3                                                                
ONETABLE DS    0D                                                               
         DC    C'AL',CL14'ALABAMA'                                              
         DC    C'AK',CL14'ALASKA'                                               
         DC    C'AZ',CL14'ARIZONA'                                              
         DC    C'AR',CL14'ARKANSAS'                                             
         DC    C'CA',CL14'CALIFORNIA'                                           
         DC    C'CO',CL14'COLORADO'                                             
         DC    C'CT',CL14'CONNECTICUT'                                          
         DC    C'DE',CL14'DELAWARE'                                             
         DC    C'DC',CL14'D.C'                                                  
         DC    C'FL',CL14'FLA'                                                  
         DC    C'FL',CL14'FLORIDA'                                              
         DC    C'GA',CL14'GEORGIA'                                              
         DC    C'GU',CL14'GUAM'                                                 
         DC    C'HI',CL14'HAWAII'                                               
         DC    C'HI',CL14'HW'                                                   
         DC    C'HI',CL14'KAUAI'                                                
         DC    C'HI',CL14'MAUI'                                                 
         DC    C'ID',CL14'IDAHO'                                                
         DC    C'IL',CL14'ILLINOIS'                                             
         DC    C'IN',CL14'INDIANA'                                              
         DC    C'IA',CL14'IOWA'                                                 
         DC    C'KS',CL14'KANSAS'                                               
         DC    C'KY',CL14'KENTUCKY'                                             
         DC    C'LA',CL14'LOUISIANA'                                            
         DC    C'ME',CL14'MAINE'                                                
         DC    C'MD',CL14'MARYLAND'                                             
         DC    C'MA',CL14'MASSATUCETS'                                          
         DC    C'MI',CL14'MICHIGAN'                                             
         DC    C'MN',CL14'MINISOTA'                                             
         DC    C'MS',CL14'MISSISSIPPI'                                          
         DC    C'MO',CL14'MISSOURI'                                             
         DC    C'MT',CL14'MONTANA'                                              
         DC    C'NE',CL14'NEBRASKA'                                             
         DC    C'NJ',CL14'N.J'                                                  
         DC    C'NV',CL14'NEVADA'                                               
         DC    C'NH',CL14'N.H'                                                  
         DC    C'NJ',CL14'N.J'                                                  
         DC    C'NM',CL14'N.M'                                                  
         DC    C'NY',CL14'N.Y'                                                  
         DC    C'OH',CL14'OHIO'                                                 
         DC    C'OK',CL14'OKLAHOMA'                                             
         DC    C'OR',CL14'OREGON'                                               
         DC    C'PA',CL14'PENNSYLVANIA'                                         
         DC    C'TN',CL14'TENNESEE'                                             
         DC    C'TX',CL14'TEXAS'                                                
         DC    C'UT',CL14'UTAH'                                                 
         DC    C'VI',CL14'VI'                                                   
         DC    C'VA',CL14'VIRGINIA'                                             
         DC    C'VT',CL14'VERMONT'                                              
         DC    C'WA',CL14'WASHINGTON'                                           
         DC    C'WI',CL14'WISCONSIN'                                            
         DC    C'WY',CL14'WYOMING'                                              
         SPACE 1                                                                
         DC    X'95E8',CL14'LI'                                                 
         DC    X'FF'                                                            
         DC    X'FF'                                                            
         EJECT                                                                  
*              TWO WORD STATE TABLE                                             
         SPACE 3                                                                
TWOTABLE DS    0D                                                               
         DC    C'DC',CL14'D C'                                                  
         DC    C'NH',CL14'NEW HAMPSHIRE'                                        
         DC    C'NH',CL14'N H'                                                  
         DC    C'NJ',CL14'NEW JERSEY'                                           
         DC    C'NJ',CL14'N J'                                                  
         DC    C'NM',CL14'NEW MEXICO'                                           
         DC    C'NM',CL14'N M'                                                  
         DC    C'NY',CL14'NEW YORK'                                             
         DC    C'NY',CL14'N Y'                                                  
         DC    C'NC',CL14'NORTH CAROLINA'                                       
         DC    C'NC',CL14'N CAROLINA'                                           
         DC    C'NC',CL14'N C'                                                  
         DC    C'ND',CL14'NORTH DAKOTA'                                         
         DC    C'ND',CL14'N DAKOTA'                                             
         DC    C'ND',CL14'N D'                                                  
         DC    C'PR',CL14'PUERTO RICO'                                          
         DC    C'PR',CL14'P R'                                                  
         DC    C'RI',CL14'RHODE ISLAND'                                         
         DC    C'RI',CL14'R I'                                                  
         DC    C'SC',CL14'SOUTH CAROLINA'                                       
         DC    C'SC',CL14'S CAROLINA'                                           
         DC    C'SC',CL14'S C'                                                  
         DC    C'SD',CL14'SOUTH DAKOTA'                                         
         DC    C'SD',CL14'S DAKOTA'                                             
         DC    C'SD',CL14'S D'                                                  
         DC    C'VI',CL14'VIRGIN ISLANDS'                                       
         DC    C'VI',CL14'V I'                                                  
         DC    C'WV',CL14'WEST VIRGINIA'                                        
         DC    C'WV',CL14'W VIRGINIA'                                           
         SPACE 1                                                                
         DC    X'83C1',CL14'LOS ANGELES'                                        
         DC    X'95E8',CL14'LONG ISLAND'                                        
         DC    X'FF'                                                            
         EJECT                                                                  
ADDCONDD DSECT                                                                  
       ++INCLUDE TAADDCOND                                                      
         EJECT                                                                  
*              MY STORAGE                                                       
         SPACE 3                                                                
MYD      DSECT                                                                  
MYSTART  DS    0D                                                               
DUB      DS    D                                                                
PARAS    DS    6A                                                               
WORK     DS    CL32                                                             
         SPACE 1                                                                
WORD     DS    CL32                SET BY GETWORD                               
WORD2    DS    CL32                                                             
LWORD    DS    XL1                                                              
AWORD    DS    A                                                                
         SPACE 1                                                                
WORKADD  DS    0CL132                                                           
WORKL1   DS    CL30                                                             
         DS    CL1                                                              
WORKL2   DS    CL30                                                             
         DS    CL1                                                              
WORKL3   DS    CL30                                                             
         DS    CL1                                                              
WORKL4   DS    CL30                                                             
ENDADD   DS    CL1                                                              
         DS    CL8                                                              
SQSHADD  DS    CL132                                                            
         SPACE 1                                                                
ADDBLOCK DS    0CL120                                                           
ADDBL1   DS    CL30                                                             
ADDBL2   DS    CL30                                                             
ADDBL3   DS    CL30                                                             
ADDBL4   DS    CL30                                                             
         SPACE 1                                                                
MYEND    DS    0D                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'062TAADDCON  05/01/02'                                      
         END                                                                    
