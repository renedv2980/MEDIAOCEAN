*          DATA SET ACWLEVEL   AT LEVEL 004 AS OF 03/29/93                      
*CATALP WLEVEL                                                                  
*                                                                               
WLEVEL   TITLE 'ACWLEVEL - TELLS YOU WHAT LEVELS A BUDGET RECORD HAS'           
*                                                                               
WLEVEL   CSECT                                                                  
         PRINT NOGEN                                                            
*                                                                               
         DS    4096X               SO WE CAN DO A REPLACE LOAD                  
         ORG   *-4096                                                           
*                                                                               
         NMOD1 WORKDX-WORKD,**WLVL**,RA,RR=R2,CLEAR=YES                         
*                                                                               
         ST    R2,RELO                                                          
         USING WORKD,RC                                                         
*                                                                               
         L     R9,0(R1)            R9 = A(WLVLBLCK)                             
         USING WLVLBLCK,R9                                                      
*                                                                               
         MVI   WLVACCLV,0          LEVELS NOT DETERMINED YET                    
         MVI   WLVCACLV,0                                                       
*                                                                               
         MVC   COMFACS,WLVCMFAC    A(COMFACS)                                   
         ICM   R1,15,COMFACS                                                    
         BZ    MAIN10                                                           
*                                                                               
         MVI   OFFLINE,C'N'                                                     
         USING COMFACSD,R1                                                      
         MVC   DATAMGR,CDATAMGR                                                 
         B     MAIN20                                                           
         DROP  R1                                                               
*                                                                               
MAIN10   MVI   OFFLINE,C'Y'                                                     
         MVC   DATAMGR,=V(DATAMGR)                                              
*                                                                               
MAIN20   L     R6,WLVBDGRC         R6 = A(BUDGET RECORD)                        
         USING BUDRECD,R6                                                       
         GOTO1 GTLEVEL,DMCB,BUDKACT,WLVACCUL    DETERMINE THE ACCOUNT           
         MVC   WLVACCLV,0(R1)                       LEVELS                      
         GOTO1 GTLEVEL,DMCB,BUDKCACT,WLVCACUL                                   
         MVC   WLVCACLV,0(R1)                                                   
         DROP  R6                                                               
*                                                                               
MAINX    B     XIT                                                              
*                                                                               
YES      SR    RC,RC                                                            
NO       LTR   RC,RC                                                            
XIT      XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* THIS ROUTINE DETERMINES THE LEVEL OF THE ACCOUNT                              
*                                                                               
* ON ENTRY:    PARAM 1             A(ACCOUNT CODE)                              
*              PARAM 2             A(ACCOUNT U/L REC)                           
*                                                                               
* ON EXIT:     PARAM 1, BYTE 0     LEVEL OF ACCOUNT                             
***********************************************************************         
GTLEVEL  NTR1                                                                   
         L     R2,0(R1)            R2 = A(ACCOUNT CODE)                         
         L     R6,4(R1)            R6 = A(ACCOUNT U/L REC)                      
*                                                                               
         MVC   DATADISP,=Y(ACCORFST)  LOOK FOR THE ACCOUNT LENGTHS ELEM         
         MVI   ELCODE,ACLELQ                                                    
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING ACLELD,R6                                                        
         SR    R4,R4               CURRENT LEVEL NUMBER                         
         LR    R5,R4               PREVIOUS LENGTH OF LEVEL                     
         ZIC   R3,ACLLN                                                         
         SH    R3,=Y(ACLLN1Q)                                                   
         BNP   GTLVL20                                                          
         LA    R6,ACLVALS          R6 = A(FIRST LEVEL LENGTH)                   
         USING ACLVALS,R6                                                       
*                                                                               
GTLVL10  ZIC   RE,ACLVLEN                                                       
         SR    RE,R5                                                            
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R2),SPACES      NOTHING AT THIS LEVEL?                       
         BE    GTLVL20             YES, THEN WE GOT THE LEVEL                   
*                                                                               
         SH    R3,=Y(L'ACLVALS)                                                 
         BNP   GTLVL20                                                          
*                                                                               
         ZIC   RE,ACLVLEN                                                       
         AR    R2,RE                                                            
*                                                                               
         IC    R5,ACLVLEN                                                       
         LA    R4,1(R4)                                                         
         LA    R6,L'ACLVALS(R6)                                                 
         B     GTLVL10                                                          
         DROP  R6                                                               
*                                                                               
GTLVL20  STC   R4,DMCB                                                          
*                                                                               
GTLVLX   B     XIT                                                              
         EJECT                                                                  
RELO     DS    A                                                                
*                                                                               
         GETEL R6,DATADISP,ELCODE                                               
*                                                                               
         LTORG                                                                  
SPACES   DC    132C' '                                                          
         EJECT                                                                  
* DDCOMFACSD                                                                    
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACSD                                                     
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
       ++INCLUDE ACWLEVELD                                                      
         EJECT                                                                  
WORKD    DSECT                                                                  
DMWORK   DS    12D                                                              
DUB      DS    D                                                                
DMCB     DS    6F                                                               
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
BYTE2    DS    X                                                                
*                                                                               
COMFACS  DS    A                                                                
DATAMGR  DS    A                                                                
*                                                                               
DATADISP DS    H                                                                
ELCODE   DS    X                                                                
*                                                                               
WORK     DS    XL64                                                             
*                                                                               
KEY      DS    CL(L'ACCKEY)                                                     
KEYSAVE  DS    CL(L'ACCKEY)                                                     
*                                                                               
OFFLINE  DS    C                   OFFLINE (Y/N)?                               
*                                                                               
ACCNTCOD DS    CL12                ACCOUNT COMPANY, U/L, & CODE                 
CACCTCOD DS    CL12                CONTRA ACCOUNT COMPANY, U/L, & CODE          
*                                                                               
         DS    CL226               SPARE                                        
*                                                                               
NIOS     EQU   3                   NUMBER OF I/O AREAS                          
LIOS     EQU   2000                LENGTH OF EACH I/O AREA                      
IOAREAS  DS    (NIOS)CL(LIOS)      I/O AREAS                                    
*                                                                               
WORKDX   EQU   *                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004ACWLEVEL  03/29/93'                                      
         END                                                                    
