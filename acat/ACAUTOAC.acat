*          DATA SET ACAUTOAC   AT LEVEL 007 AS OF 07/09/18                      
*CATALP AUTOAC                                                                  
         TITLE 'AC - SPECIAL CASHFLOW'                                          
**********************************************************************          
* PROGRAM OPTIONS :                                                  *          
*                                                                    *          
*         QOPT1 - 'Y'  ADD SR ACCOUNT DETAILS TO THE TABLE           *          
*         QOPT2 - 'Y'  EXPAND MULTIPLE TRANSACTIONS                  *          
*         QOPT5 - 'Y'  RUN OFF SS/SR TRANSACTIONS NOT POINTERS       *          
*         QOPT6 - 'Y'  CALC CASH AVAILABILITY BY MOS NOT BY ESTIMATE *          
*         QOPT8 - 'Y'  SUPPRESS DISBURSED DETAILS/TRANSACTIONS       *          
*      PROFILE1 - 'Y'  APPROVE BY MOS                                *          
*      PROFILE2 - 'M'  RUN BY SYSTEM OR MEDIA                        *          
**********************************************************************          
*RGUP 007 06JUL18  <SPEC-20692> NEED ADDITIONAL MEDIA FOR DIGIAL AUDIO          
**********************************************************************          
AUTOAC   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 AUTOL,**AUTOAC,RR=RE                                             
         USING AUTOD,RC                                                         
*                                                                               
         MVC   CALLRD,4(RD)                                                     
*                                                                               
         ST    RE,RELO                                                          
         MVC   SAVERD,4(RD)                                                     
         ST    RB,ABASE                                                         
*                                                                               
         BASR  RA,0                                                             
         AHI   RA,GLOBALS-*                                                     
         USING GLOBALS,RA          RA=A(GLOBALS)                                
*                                                                               
         GOTOR INIT                INITIALIZATION ROUTINE                       
         TM    AUTERR,AUTMRNG      IS MOS RANGE > 12 MONTHS                     
         BO    AUTOAP10                                                         
*                                                                               
*        GOTOR BLDPAYTB            BUILD PAYABLES TABLE                         
*        CLI   BCTSERRS,0                                                       
*        BE    *+14                                                             
*        MVC   AUTERR,BCTSERRS     PASS ERROR BACK TO MARKER                    
*        B     AUTOAP10                                                         
*                                                                               
*        GOTOR FIXTAB              MARK DISBURSED ONLY ITEMS                    
*                                                                               
*        GOTOR BLDSRBUK            FILL IN SR TRAN BUCKETS                      
*                                                                               
*        GOTOR CASHAVL             CHECK CASH AVAILABLILITY                     
*                                                                               
AUTOAP10 DS    0H                                                               
         GOTOR SRPNTRTB            BUILD SR TRANSACTION TABLE FRM PNTR          
         GOTOR RESETBLK            RESET AUTOAPPR BLK W/ TABLE ADDRS            
*                                                                               
EQXIT    CR    RB,RB                                                            
         J     EXIT                                                             
NEQXIT   LTR   RB,RB                                                            
EXIT     XMOD1 1                                                                
         EJECT                                                                  
**********************************************************************          
* INITIALIZATION ROUTINE                                             *          
**********************************************************************          
         SPACE 1                                                                
INIT     NTR1  BASE=*,LABEL=*                                                   
         ST    R1,AAUTBLK          SAVE A(CALLER'S AUTO BLOCK)                  
*                                                                               
         LA    R1,ADCONS           RELOCATE ADDRESS CONSTANTS                   
         LHI   R0,ADCONN                                                        
         LA    R2,VTYPES                                                        
         BASR  RE,0                                                             
         ICM   RF,15,0(R1)                                                      
         BZ    *+12                DON'T RELOCATE IF NOT LINKED                 
         A     RF,RELO                                                          
         ST    RF,0(R2)                                                         
         AHI   R1,L'ADCONS                                                      
         AHI   R2,L'ADCONS                                                      
         BCTR  R0,RE                                                            
*                                                                               
         L     RE,AAUTBLK         COPY CALLER'S AUTBLK INTO LOCAL W/S           
         LHI   R1,AUTBLKL                                                       
         LA    R0,AUTBLK                                                        
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
         OC    AUTPCNT,AUTPCNT                                                  
         BNZ   *+10                                                             
         ZAP   AUTPCNT,=P'10000'   DEFAULT IS FULLY MATCH                       
*                                                                               
         USING COMFACSD,RF                                                      
         ICM   RF,15,AUTCOMF       TEST COMFACS RESOLVED                        
         BZ    INITX                                                            
         MVC   DATCON,CDATCON     SET EXTERNAL ADDRESSES FROM COMFACS           
         MVC   BINSRC31,AUTABIN                                                 
         MVC   VHELLO,CHELLO                                                    
         MVC   DATAMGR,CDATAMGR                                                 
         MVC   VADDAY,CADDAY                                                    
         MVC   VGETDAY,CGETDAY                                                  
         MVC   ADMASTC,CMASTC                                                   
         DROP  RF                                                               
*                                                                               
         MVI   FLAG,0              INITIALIZE FLAG                              
         XC    LSTSRKEY,LSTSRKEY                                                
         XC    SVSRPKEY,SVSRPKEY                                                
         XC    CURRMED,CURRMED     CLEAR CURRENT MEDIA                          
*                                                                               
         LA    RE,IO               RE=A(IO AREA)                                
         LA    RF,IOLNQ            RF=(LENGTH OF IO AREA)                       
         SR    R1,R1                                                            
         MVCL  RE,R0               CLEAR IO AREA                                
*                                                                               
         GOTO1 ASETMNTH,DMCB,(RC)  SETUP REQUESTED MONTHS TABLE                 
*                                                                               
         CLI   AUTOFSW,C'Y'        ARE WE RUNNING IT OFFLINE                    
         BNE   *+8                                                              
         BRAS  RE,ALLOCBUF         ALLOCATE BUFFER FILES FOR BUFFERIN           
*        BRAS  RE,MAIN             GETMAIN STORAGE                              
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAINI,1 INITIALIZE FIRST BUFFER                 
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 ATSAR,DMCB,(RC),TSAINI,2 INITIALIZE SECND BUFFER                 
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
INITX    J     EXIT                                                             
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* BUILD PAYABLES TABLE                                               *          
**********************************************************************          
         SPACE 1                                                                
BLDPAYTB NTR1  BASE=*,LABEL=*                                                   
         CLI   AUTOPT2,C'Y'                                                     
         BE    *+8                                                              
         MVI   AUTOPT2,C'N'                                                     
         MVI   FLAG,0              INITIALIZE FLAG                              
         XC    LSTSRKEY,LSTSRKEY                                                
         XC    SVSRPKEY,SVSRPKEY                                                
         XC    CURRMED,CURRMED                                                  
         XC    SVPSSWRK,SVPSSWRK                                                
*                                                                               
         LA    RE,IO               RE=A(IO AREA)                                
         LA    RF,IOLNQ            RF=(LENGTH OF IO AREA)                       
         SR    R1,R1                                                            
         MVCL  RE,R0               CLEAR IO AREA                                
*                                                                               
         CLI   AUTOPT5,C'Y'        ARE WE READING FILE                          
         BNE   BLDPAY10                                                         
*                                                                               
         GOTOR READPAYT            READ PAYABLES TRANSACTIONS                   
         B     BLDPAYX                                                          
*                                                                               
BLDPAY10 GOTO1 ASSPNTTB,DMCB,(RC)  BUILD SS TABLE USING POINTERS                
*                                                                               
BLDPAYX  J     EXIT                                                             
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* BUILD PAYABLES TABLE BY READING FILE/TRANSACTIONS                  *          
**********************************************************************          
         SPACE 1                                                                
         USING TRNKEY,R4                                                        
READPAYT NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         XC    SVKEY,SVKEY         BUILD KEY TO READ PAYABLE RECORDS            
         XC    SVSYS,SVSYS         CLEAR SYSTEM CODE                            
         XC    SVMED,SVMED         CLEAR MEDIA CODE                             
*                                                                               
         LA    R4,SVKEY                                                         
         MVC   TRNKEY,SPACES       INIT KEY                                     
*                                                                               
         MVC   TRNKCPY,AUTCPY      COMPANY CODE                                 
         MVI   TRNKUNT,C'S'        ALWAYS UNIT S                                
         CLC   AUTLDGR,SPACES                                                   
         BNH   RDPAY10                                                          
         MVC   TRNKLDG,AUTLDGR     MOVE IN LEDGER CODE                          
*                                                                               
RDPAY10  GOTO1 ADMRDHI,DMCB,(RC)                                                
         B     RDPAY30                                                          
*                                                                               
RDPAY20  GOTO1 ADMRDSEQ,DMCB,(RC)                                               
RDPAY30  CLC   SVKEY(TRNKLDG-TRNKEY),IOKEY                                      
         BNE   RDPAYX              EXIT IF CHANGE IN UNIT                       
*                                                                               
         LA    R4,IO                                                            
         CLC   AUTLDGR,SPACES      WAS LEDGER PASSED TO US                      
         BNH   RDPAY40                                                          
         CLC   TRNKLDG,AUTLDGR     DO WE HAVE A MATCHING LEDGER                 
         BNE   RDPAYX              EXIT IF LEDGER NOT FOUND                     
*                                                                               
RDPAY40  CLC   TRNKACT,SPACES      ARE WE AT AN ACCOUNT LEVEL                   
         BNH   RDPAY20                                                          
*                                                                               
         L     RF,ALDSYTAB         CHECK TABLE OF LEDGER/SYSTEM B/C             
RDPAY50  CLI   0(RF),X'FF'         ONLY WANT CERTAIN LEDGERS                    
         BE    RDPAY20             DON'T READ IF NOT IN LIST                    
         CLC   TRNKLDG,0(RF)                                                    
         BE    RDPAY60                                                          
         LA    RF,2(RF)                                                         
         B     RDPAY50                                                          
*                                                                               
RDPAY60  MVC   SVSYS,1(RF)         SAVE SYSTEM FROM TABLE                       
*                                                                               
         CLI   SVSYS,C'S'          IF IT'S SPOT CHECK TO SEE IF IT'S            
         BNE   RDPAY70             REALLY NET BY CHECKING THE 1ST CHAR          
         CLI   TRNKACT,C'N'        OF THE ACCOUNT FOR 'N' (SSN)                 
         BNE   RDPAY70                                                          
         MVI   SVSYS,C'N'                                                       
*                                                                               
RDPAY70  DS    0H                                                               
         CLC   AUTSYSTM,SPACES     WAS SYSTEM PASSED FROM REQUEST               
         BNH   RDPAY80                                                          
         CLC   AUTSYSTM,SVSYS      IS THIS A MATCHING SYSTEM CODE               
         BNE   RDPAY20             READ NEXT                                    
*                                                                               
RDPAY80  DS    0H                                                               
         CLC   TRNKDATE,SPACES                                                  
         BNH   RDPAY20                                                          
         CLC   TRNKREF,SPACES      IS IT A TRANSACTION                          
         BNH   RDPAY20                                                          
         MVC   DSKADDR,TRNKDA      DSKADDR OF CURRENT TRANSACTION               
*                                                                               
         GOTO1 ADMGET,DMCB,(RC),0 GET RECORD                                    
         BE    *+6                                                              
         DC    H'0'                                                             
         LA    R4,IO               POINT TO TRANSACTION RECORD                  
*                                                                               
         USING PSSAD,R3                                                         
         LA    R3,PSSAWRK                                                       
         XC    PSSAWRK,PSSAWRK                                                  
         BRAS  RE,ZAPSSBUK         ZAP ALL BUCKETS                              
         XC    AELMNTS(AELMNTSQ),AELMNTS                                        
         NI    FLAG,X'FF'-(FLGDATE+FLGBRD)                                      
*                                                                               
         MVC   PSSKSYS,SVSYS       MOVE SYSTEM TO TABLE                         
         CLI   PSSKSYS,C'N'        IS THIS NET SYSTEM ?                         
         BNE   *+12                                                             
         CLI   AUTPRF2,C'S'        RUN BY SYSTEM OR SYS/MED ?                   
         BE    *+10                                                             
         MVC   PSSKMED,TRNKACT     SAVE OFF MEDIA CODE                          
*                                                                               
         MVC   PSSKCLI,TRNKCACT+9  LAST THREE CHAR OF CONTRA IS CLI             
         MVC   PSSKPRO,TRNKREF     FIRST THREE CHAR OF REFERENCE IS PRO         
*                                                                               
         OC    AUTALST,AUTALST     DO WE HAVE A CLIENT LIST FROM REQST          
         BZ    RDPAY90                                                          
         MVC   CLNT,PSSKCLI                                                     
         BRAS  RE,CHKCLT           CHECK IF IT IS A VALID CLIENT                
         BNE   RDPAY20             READ NEXT TRANSACTION                        
         B     RDPAY110            MATCHING CLIENT FOUND                        
RDPAY90  CLC   AUTCLNT,SPACES      IS CLIENT REQUESTED                          
         BNH   RDPAY100                                                         
         CLC   AUTCLNT,PSSKCLI                                                  
         BNE   RDPAY20                                                          
RDPAY100 CLC   AUTPROD,SPACES      IS PRODUCT REQUESTED                         
         BNH   RDPAY110                                                         
         CLC   AUTPROD,PSSKPRO                                                  
         BNE   RDPAY20                                                          
*                                                                               
RDPAY110 MVC   PSSKSTN,TRNKULA     SS ACCOUNT IS STATION                        
         MVC   PSSKOFF,TRNKOFF     OFFICE CODE                                  
         MVC   PSSKDA,DSKADDR      SAVE DISK ADDRESS TO MARK REC LATER          
*                                                                               
         LR    R5,R4                                                            
         AH    R5,=Y(TRNRFST-TRNKEY) POINT TO FIRST ELEMENT                     
         CLI   0(R5),X'44'         MAKE SURE FIRST ELEMENT IS 44                
         BE    RDPAY140                                                         
         DC    H'0'                                                             
RDPAY120 CLI   0(R5),0                                                          
         BE    RDPAY240                                                         
         CLI   0(R5),GDAELQ        IS IT X'E5' ELEMENT                          
         BE    RDPAY180                                                         
         CLI   0(R5),MBIELQ        IS IT X'F3' ELEMENT                          
         BE    RDPAY190                                                         
         CLI   0(R5),OTHELQ        IS IT X'23' ELEMENT                          
         BE    RDPAY200                                                         
         CLI   0(R5),XPYELQ        IS IT X'46' ELEMENT                          
         BE    RDPAY210                                                         
         CLI   0(R5),MDTELQ        IS IT X'1A' ELEMENT                          
         BE    RDPAY220            FOR FUTURE USE                               
         CLI   0(R5),MDPELQ        IS IT X'6A' ELEMENT                          
         BE    RDPAY230            FOR FUTURE USE                               
*                                                                               
RDPAY130 SR    R1,R1                                                            
         IC    R1,1(R5)            GET LENGTH OF ELEMENT                        
         AR    R5,R1                                                            
         B     RDPAY120                                                         
*                                                                               
         USING TRNELD,R5                                                        
RDPAY140 TM    TRNSTAT,TRNSDR      IS IT DEBIT                                  
         BO    RDPAY20             DISCARD ALL DEBITS                           
*                                                                               
         ZAP   PSSCLRB,TRNAMNT     CLEARED AMOUNT                               
*                                                                               
         TM    TRNRSTA2,TRNSUSED   HAS IT BEEN USED                             
         BNO   RDPAY150                                                         
         ZAP   PSSDISBB,TRNAMNT    CREDIT AND USED DATE (DISBURSED)             
         MVI   PSSKSTAT,PSSKDISB   DISBURSED ITEM                               
         B     RDPAY130                                                         
*                                                                               
RDPAY150 DS    0H                                                               
         ZAP   PSSUDISB,TRNAMNT    ALL OF UNDISBURSED AMOUNT                    
*                                                                               
         TM    TRNSTAT,TRNSHOLD    IS THIS PAYMENT ON HOLD                      
         BNO   RDPAY160                                                         
         ZAP   PSSHELDB,TRNAMNT    HELD AMOUNT                                  
         MVI   PSSKSTAT,PSSKHELD   ADD TO HELD ITEM LINE                        
         B     RDPAY130                                                         
*                                                                               
RDPAY160 TM    TRNSTAT,TRNSAPPR    SELECTED FOR PAYMENT                         
         BNO   RDPAY170                                                         
         ZAP   PSSAPPRB,TRNAMNT    APPROVED AMOUNT                              
         MVI   PSSKSTAT,PSSKPREV   ADD TO PREVIOUSLY APPROVED ITEM              
         B     RDPAY130                                                         
*                                                                               
RDPAY170 ZAP   PSSUNAPB,TRNAMNT    UNAPPROVED AMOUNT                            
         MVI   PSSKSTAT,PSSKUNAP   ADD TO UNAPPROVED LINE                       
         B     RDPAY130                                                         
*                                                                               
         USING GDAELD,R5                                                        
RDPAY180 CLI   GDATYPE,GDAMMOS     MEDIA MOS (PAYABLES)                         
         BNE   RDPAY130                                                         
         ST    R5,AGDAELD          SAVE ADDRESS OF X'E5' ELEMENT                
         B     RDPAY130                                                         
*                                                                               
RDPAY190 ST    R5,AMBIELD                                                       
         B     RDPAY130                                                         
*                                                                               
         USING OTHELD,R5                                                        
RDPAY200 CLI   OTHPROF-3,C' '                                                   
         BH    RDPAY130                                                         
         ST    R5,AOTHELD          SAVE ADDRESS OF X'23' ELEMENT                
         B     RDPAY130                                                         
*                                                                               
RDPAY210 ST    R5,AXPYELD          SAVE ADDRESS OF X'46' ELEMENT                
         B     RDPAY130                                                         
*                                                                               
RDPAY220 ST    R5,AMDTELD          SAVE ADDRESS OF X'1A' ELEMENT                
         B     RDPAY130                                                         
*                                                                               
RDPAY230 ST    R5,AMDPELD          SAVE ADDRESS OF X'6A' ELEMENT                
         B     RDPAY130                                                         
*                                                                               
         USING GDAELD,R5                                                        
RDPAY240 DS    0H                                                               
         OC    AGDAELD,AGDAELD     X'E5'                                        
         BZ    RDPAY250                                                         
         L     R5,AGDAELD                                                       
         MVC   PSSKMOS,GDAYYMM                                                  
         OI    FLAG,FLGDATE        MOS FOUND IN THIS ELEMENT                    
         B     RDPAY270                                                         
*                                                                               
         USING MBIELD,R5                                                        
RDPAY250 OC    AMBIELD,AMBIELD     X'F3'                                        
         BZ    RDPAY260                                                         
         L     R5,AMBIELD                                                       
         MVC   PSSKMOS,MBIMOS                                                   
         MVC   PSSKEST,SPACES                                                   
         MVC   PSSKEST(L'MBIEST),MBIEST                                         
         OI    FLAG,FLGDATE        MOS FOUND IN THIS ELEMENT                    
         B     RDPAY270                                                         
*                                                                               
         USING OTHELD,R5                                                        
RDPAY260 OC    AOTHELD,AOTHELD     X'23'                                        
         BZ    RDPAY270                                                         
         L     R5,AOTHELD                                                       
         MVC   PSSKMOS,OTHDATE     MOVE MOS                                     
         OI    FLAG,FLGDATE        MOS FOUND IN THIS ELEMENT                    
*                                                                               
         USING XPYELD,R5                                                        
RDPAY270 OC    AXPYELD,AXPYELD     X'46'                                        
         BNZ   RDPAY280                                                         
         MVI   PSSKTYP,AAVPNEST    NO INVOICE NUMBER                            
         B     RDPAY350                                                         
RDPAY280 L     R5,AXPYELD                                                       
         CLI   PSSKSYS,C'N'        IS SYSTEM NETWORK                            
         BNE   RDPAY290                                                         
         CLC   XPYSMED,SPACES      IF NO MEDIA/SUBMEDIA THEN LEAVE              
         BNH   RDPAY290            MEDIA AS 'N' FOR NOW                         
         CLI   AUTPRF2,C'S'        RUN BY SYSTEM OR SYS/MED ?                   
         BE    *+10                                                             
         MVC   PSSKMED,XPYSMED     MOVE IN MEDIA FOR SYSTEM C'N' NETWRK         
RDPAY290 TM    FLAG,FLGDATE        DO WE HAVE MOS                               
         BO    RDPAY310                                                         
*                                                                               
         CLI   TRNKCULA+3,C'N'       IF NETWORK                                 
         BE    *+8                                                              
         OI    FLAG,FLGBRD           GET DATE FROM BROADCAST CAL                
         MVC   WORK(L'XPYPER),XPYPER                                            
         LA    R2,WORK                                                          
         CLI   WORK+6,C' '                                                      
         BNH   *+8                                                              
         LA    R2,WORK+6                                                        
         CLI   WORK+6,C'-'                                                      
         BNE   *+8                                                              
         LA    R2,WORK+7                                                        
         TM    FLAG,FLGBRD                                                      
         BNO   RDPAY300                                                         
         GOTO1 VGETBRD,DMCB,(1,(R2)),DTE1,VGETDAY,VADDAY                        
         LA    R2,DTE2                                                          
RDPAY300 GOTO1 DATCON,DMCB,(0,(R2)),(1,SVMOS)                                   
         MVC   PSSKMOS,SVMOS                                                    
*                                                                               
RDPAY310 MVC   PSSKINV,XPYINV      MOVE INVOICE # TO TABLE                      
         MVC   PSSKEST,SPACES                                                   
         OC    XPYEST,XPYEST                                                    
         BZ    RDPAY320                                                         
         EDIT  XPYEST,(3,PSSKEST),FILL=0                                        
         CLC   PSSKEST+3(3),SPACES                                              
         BNH   RDPAY350                                                         
         MVI   PSSKTYP,AAVPBEST    BAD ESTIMATE                                 
         B     RDPAY350                                                         
*                                                                               
         USING MDTELD,R5                                                        
RDPAY320 L     R5,AMDTELD          LOAD ADDRESS OF 1A                           
         OC    AMDTELD,AMDTELD                                                  
         BNZ   RDPAY330                                                         
         OC    AMDPELD,AMDPELD                                                  
         BNZ   *+12                                                             
         MVI   PSSKTYP,AAVPNEST    NO ESTIMATE OR MEDIA CODE                    
         B     RDPAY350                                                         
         L     R5,AMDPELD          LOAD ADDRESS OF 6A                           
RDPAY330 DS    0H                                                               
         CLI   MDTSYS,C'J'                                                      
         BE    RDPAY20             SKIP THIS TRANSACTION                        
*                                                                               
         MVC   PSSKEST,MDTEST                                                   
         CLC   PSSKEST+3(3),SPACES                                              
         BE    *+12                                                             
         MVI   PSSKTYP,AAVPBEST    BAD ESTIMATE                                 
         B     RDPAY350                                                         
*                                                                               
         LA    RE,PSSKEST          POINT TO ESTIMATE                            
         LHI   R1,3                                                             
         LA    RE,MDTEST                                                        
*                                                                               
RDPAY340 CLI   0(RE),X'F0'         MUST BE BETWEEN ZERO AND NINE                
         BL    *+12                                                             
         CLI   0(RE),X'F9'                                                      
         BNH   *+12                                                             
         MVI   PSSKTYP,AAVPBEST    BAD ESTIMATE                                 
         B     RDPAY350                                                         
         LA    RE,1(RE)            BUMP TO NEXT BYTE IN ESTIMATE                
         BCT   R1,RDPAY340                                                      
*                                                                               
RDPAY350 DS    0H                                                               
         CLI   PSSKSYS,C'N'        IS THIS NET SYSTEM ?                         
         BNE   *+12                                                             
         CLI   AUTPRF2,C'S'        RUN BY SYSTEM OR SYS/MED ?                   
         BE    RDPAY360                                                         
         CLC   AUTMEDIA,SPACES                                                  
         BNH   RDPAY360                                                         
         CLC   AUTMEDIA,PSSKMED   DOES MEDIA MATCH REQUEST                      
         BNE   RDPAY20            GET NEXT TRANSACTION                          
*                                                                               
RDPAY360 CLC   AUTEST,SPACES                                                    
         BNH   RDPAY370                                                         
         CLC   AUTEST,PSSKEST     DOES ESTIMATE MATCH REQUEST                   
         BNE   RDPAY20            GET NEXT TRANSACTION                          
*                                                                               
RDPAY370 CLC   PSSKMOS,AUTSMOS    IS MOS <  START DATE                          
         BL    RDPAY410           ADD TO PRIOR ENTRY AND TOTALS                 
         CLC   PSSKMOS,AUTEMOS    IS MOS > END DATE                             
         BH    RDPAY430           NO, ADD IT AS DETAIL ENTRY                    
*                                                                               
         MVI   PSSKRTYP,X'05'     DETAIL RECORD BY MOS                          
         CLI   PSSKSTAT,PSSKUNAP                                                
         BE    RDPAY380                                                         
         CLI   AUTOPT2,C'Y'       DO WE WANT TO EXPAND                          
         BE    RDPAY380           YES                                           
         XC    PSSKDA,PSSKDA      DON'T EXPAND,LEAVE IT UNIQUE                  
RDPAY380 GOTO1 ATSAR,DMCB,(RC),TSAADD,2    ADD TO BUFFER # 2                    
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLI   AUTOPT2,C'Y'        SHOW PREV APPR & UNAPPR AS 2 LINES           
         BE    RDPAY400                                                         
         CLI   PSSKSTAT,PSSKPREV   PREVIOUSLY APPROVED TO TOTAL                 
         BE    RDPAY390                                                         
         CLI   PSSKSTAT,PSSKUNAP   UNAPPROVED TO TOTALS                         
         BNE   RDPAY400                                                         
RDPAY390 LA    RE,PSSAWRK+(PSSKSTAT-PSSAKEY) CLEAR STATUS ONWARDS.              
         XC    0(PSSKLNQ2,RE),0(RE)                                             
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2    ADD TO BUFFER # 2                    
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
RDPAY400 LA    RE,PSSAWRK+(PSSKSTN-PSSAKEY) CLEAR STATUS ONWARDS.               
         XC    0(PSSKLNQ3,RE),0(RE)                                             
         MVI   PSSKRTYP,X'03'     MOS TOTAL                                     
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2    ADD TO BUFFER # 2                    
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
         B     RDPAY440           ADD TO TOTALS, DON'T ADD TO PRIORS            
*                                                                               
RDPAY410 DS    0H                                                               
         CP    PSSUNAPB,=P'0'                                                   
         BZ    RDPAY420                                                         
         OI    PSSFLAG,PSSPREV    UNAPPR ITEM OLDER THAN REQESTED MOS           
*                                                                               
RDPAY420 LA    RE,PSSAWRK+(PSSKRTYP-PSSAKEY) CLEAR REC TYPE ONWARDS.            
         XC    0(PSSKLNQ1,RE),0(RE)                                             
         MVI   PSSKRTYP,X'01'      PRIOR MOS'S TOTALS                           
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2    ADD TO BUFFER # 2                    
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
         B     RDPAY440                                                         
*                                                                               
RDPAY430 LA    RE,PSSAWRK+(PSSKRTYP-PSSAKEY) CLEAR REC TYPE ONWARDS.            
         XC    0(PSSKLNQ1,RE),0(RE)                                             
         MVI   PSSKRTYP,X'04'      FUTURE MOS'S TOTALS                          
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2    ADD TO BUFFER # 2                    
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLI   AUTOPT6,C'Y'        APPROVING BY MOS?                            
         BE    RDPAY440                                                         
         CP    PSSUNAPB,=P'0'     IS THIS UNAPPROVED ITEM                       
         BNZ   RDPAY20            EXCLUDE POST REQUEST UNAPPROVED ITEMS         
*                                                                               
RDPAY440 DS    0H                                                               
         LA    RE,PSSAWRK+(PSSKRTYP-PSSAKEY) CLEAR REC TYPE ONWARDS.            
         XC    0(PSSKLNQ1,RE),0(RE)                                             
         MVI   PSSKRTYP,X'00'      ALL TOTALS                                   
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2    ADD TO BUFFER # 2                    
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
         B     RDPAY20                                                          
*                                                                               
RDPAYX   J     EXIT                                                             
         DROP  R3,R4,R5                                                         
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* ACQUIRE BUFFERS PAYBUF AND RCVBUF                                  *          
* THIS ROUTINE ALLOCATES BUFFERS DYNAMICALLY SO THERE IS NO NEED TO  *          
* PUT DD STATEMENT IN SOON JCLS (DDS.PROCLIB(MONSTA))                *          
* GENERATES RCVBUFD DD   UNIT=SYSDA,SPACE=(CYL,(400,200))            *          
*           PAYBUFD DD   UNIT=SYSDA,SPACE=(CYL,(200,200))            *          
* IF YOU NEED MORE SPACE ASSIGN BIGGER NUMBER TO CYLINDERS           *          
**********************************************************************          
         SPACE 1                                                                
ALLOCBUF NTR1  BASE=*,LABEL=*                                                   
         GOTO1 ADYNALOC,DMCB,(X'80',=CL8'PAYBUFD '),                   +        
               (X'40',=XL6'0003200000C8')                                       
         GOTO1 ADYNALOC,DMCB,(X'80',=CL8'RCVBUFD '),                   +        
               (X'40',=XL6'0001900000C8')                                       
ALBUFX   J     EXIT                                                             
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* ACQUIRE CORE FOR BINARY TABLES                                     *          
**********************************************************************          
         SPACE 1                                                                
MAIN     NTR1  BASE=*,LABEL=*                                                   
         L     R0,=A(LENBUFF)            ACQUIRE AN ADDITIONAL BUFFER           
         GETMAIN RU,LV=(0),LOC=(ANY,ANY)                                        
         ST    R1,ABUFF                  SAVE A(BUFFER)                         
         A     R1,=A(LENBUFF1)           PARTITION BIG BUFFER INTO TWO          
         ST    R1,ABUFF2                 SECOND BUFFER STARTS HERE              
*                                                                               
MAINX    J     EXIT                                                             
         LTORG                                                                  
         EJECT                                                                  
*********************************************************************           
* FREE UP ACQUIRED CORE                                                         
*********************************************************************           
         SPACE 1                                                                
WRAP     NTR1                                                                   
         L     R1,ABUFF                                                         
         L     R0,=A(LENBUFF)                                                   
         FREEMAIN RU,A=(1),LV=(0)                                               
*                                                                               
         L     RE,ADMASTC                                                       
         USING MASTD,RE                                                         
         XC    MCUSRDMP(8),MCUSRDMP CLEAR OUT EXTRA DUMP AREA                   
         J     EXIT                                                             
         DROP  RE                                                               
*                                                                               
*                                                                               
*                                                                               
*&&DO                                                                           
**********************************************************************          
* READ SR TRANSACTIONS AND MATCH WITH SS BINTABLE TRANSACTIONS       *          
* IF YOU FIND MATCHING CLI/PRO/EST/MOS THEN UPDATE TWO BUCKETS       *          
**********************************************************************          
         USING TSRAD,R4                                                         
         USING PSSAD,R2                                                         
*                                                                               
BLDSRBUK NTR1  BASE=*,LABEL=*                                                   
*                                                                               
BLDSR10  CLI   AUTOPT5,C'Y'        ARE WE READING FILE                          
         BE    BLDSR20                                                          
         GOTOR SRPNTRTB            BUILD SR TRANSACTION TABLE FRM PNTR          
         B     BLDSR30                                                          
BLDSR20  GOTOR BLDSRTAB            BUILD SR TRANSACTION TABLE FRM FILE          
*                                                                               
BLDSR30  DS    0H                                                               
         XC    TSRAWRK,TSRAWRK                                                  
         GOTO1 ATSAR,DMCB,(RC),TSARDH,1     GET RECORD IN TSRAWRK               
         TM    BCTSERRS,TSEEOF              NO MORE SR RECORDS                  
         BO    BLDSRX                                                           
*                                                                               
         LA    R4,TSRAWRK                                                       
         NI    FLAG,X'FF'-FLGNTFND INIT RECORD NOT FOUND FLAG.                  
         NI    FLAG1,X'FF'-PSSDISB NOT A DISBURSED ONLY ITEM                    
*                                                                               
BLDSR40  DS    0H                                                               
         TM    TSRFLAG,TSRFLGDR    NEED ONLY DEBIT ENTRIES                      
         BNO   BLDSR120            GET NEXT SR ENTRY                            
         LA    R2,PSSAWRK                                                       
         XC    PSSAWRK,PSSAWRK     BUILD WORK AREA TO UPDT SR BUCKETS           
         BRAS  RE,ZAPSSBUK         ZAP ALL BUCKETS                              
*                                                                               
         MVI   PSSKTYP,X'00'       UPDATE GOOD ESTIMATES ONLY                   
         MVC   PSSKSYS,TSRKSYS     MOVE IN SYSTEM                               
*                                                                               
         CLI   PSSKSYS,C'N'        IS THIS NET SYSTEM ?                         
         BNE   *+12                                                             
         CLI   AUTPRF2,C'S'        RUN BY SYSTEM OR SYS/MED ?                   
         BE    *+10                                                             
         MVC   PSSKMED,TSRKMED     MOVE IN MEDIA CODE                           
*                                                                               
         MVC   PSSKCLI,TSRKCLI     CLIENT CODE                                  
         MVC   PSSKPRO,TSRKPRO     PRODUCT CODE                                 
         OC    SVSRPKEY,SVSRPKEY   ARE WE READING POINTERS                      
         BZ    BLDSR45                                                          
         CLC   TSRKEST,SVSRPKEY+(AARPEST-AARPASD)                               
         BNE   BLDSR120                                                         
*                                                                               
         CLI   PSSKSYS,C'N'        IS THIS NET SYSTEM ?                         
         BNE   *+12                                                             
         CLI   AUTPRF2,C'S'        RUN BY SYSTEM OR SYS/MED ?                   
         BE    BLDSR45                                                          
         CLC   TSRKMED,CURRMED     CURRENT MEDIA TABLE IS PROCESSING            
         BNE   BLDSR120                                                         
BLDSR45  MVC   PSSKEST,TSRKEST     ESTIMATE                                     
*                                                                               
         ZAP   PSSBILLB,TSRADR     BILLED AMOUNT                                
         BZ    *+8                                                              
         OI    PSSFLAG,PSSBZERO    PRINT ZERO'S FOR IN'S AND OUT'S              
         ZAP   PSSRCVDB,TSRACR     RECEIVED AMOUNT, CALCULATED CREDIT           
         BZ    *+8                                                              
         OI    PSSFLAG,PSSRZERO    PRINT ZERO'S FOR IN'S AND OUT'S              
*                                                                               
         CLI   TSRKRTYP,X'01'      IS THIS AN SR ESTIMATE TOTAL                 
         BNE   BLDSR70                                                          
         NI    FLAG1,X'FF'-PSSDISB NOT A DISBURSED ONLY ITEM                    
         NI    FLAG,X'FF'-FLGNTFND SET AS RECORD WAS FOUND                      
         MVI   PSSKRTYP,X'00'                                                   
         XC    TEMPWRK,TEMPWRK                                                  
         MVC   TEMPWRK(L'PSSAWRK),PSSAWRK   SAVE IT OFF                         
         GOTO1 ATSAR,DMCB,(RC),TSARDH,2     READ INTO PSSAWRK                   
         CLI   BCTSERRS,0                                                       
         BE    BLDSR50            NO ERRORS, RECORD WAS FOUND                   
         OI    FLAG,FLGNTFND      RECORD NOT FOUND                              
         B     BLDSR120                                                         
BLDSR50  TM    PSSFLAG,PSSDISB       IS IT DISBURSED ONLY ITEM                  
         BNO   BLDSR60                                                          
         OI    FLAG1,PSSDISB                                                    
BLDSR60  OC    TEMPWRK+(PSSFLAG-PSSAD)(PSSFLNQ),PSSAWRK+(PSSFLAG-PSSAD)         
         MVC   PSSAWRK,TEMPWRK                                                  
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2  UPDATE SR BUCKETS                      
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
         B     BLDSR110            GET NEXT SR ENTRY                            
*                                                                               
BLDSR70  DS     0H                                                              
         TM    FLAG,FLGNTFND      WAS TOTAL RECORD FOUND                        
         BO    BLDSR120           NO,GET NXT DONT WASTE TIME SEARCHING          
         TM    FLAG1,PSSDISB      IS THIS A DISB ONLY ENTRY                     
         BNO   *+8                                                              
         OI    PSSFLAG,PSSDISB     MRK IT SO IT'S EASY TO SKIP PRINTING         
*                                                                               
         CLC   TSRKMOS,AUTSMOS    IS MOS <  START DATE                          
         BL    BLDSR80            ADD TO PRIOR ENTRY                            
         CLC   TSRKMOS,AUTEMOS    IS MOS > END DATE                             
         BH    BLDSR90            ADD TO FUTURE ENTRY                           
*                                                                               
         MVC   PSSKMOS,TSRKMOS     MONTH OF SERVICE                             
*        MVC   PSSKOFF,TSRKOFF     OFFICE CODE                                  
         MVI   PSSKRTYP,X'03'      ADD TO MOS BUCKETS                           
         B     BLDSR100                                                         
BLDSR80  MVI   PSSKRTYP,X'01'      ADD TO PRIOR BUCKET                          
         B     BLDSR100                                                         
BLDSR90  MVI   PSSKRTYP,X'04'      ADD TO FUTURE BUCKET                         
*                                                                               
BLDSR100 DS    0H                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2                                         
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
         B     BLDSR120                                                         
*                                                                               
BLDSR110 CLI   AUTOPT1,C'Y'        WANT TO BUILD EXPANDED SR TABLE ?            
         BNE   BLDSR120                                                         
*                                                                               
         TM    FLAG,FLGNTFND       WAS RECORD FOUND                             
         BO    BLDSR120            NO,GET NXT DONT ADD THIS SR TO TBL           
*                                                                               
         CLI   TSRKRTYP,X'00'      TOTALS ENTRY ?                               
         BE    BLDSR120            SKIP                                         
         CLI   TSRKRTYP,X'02'      DETAILS BY MOS?                              
         BE    BLDSR120            SKIP                                         
*                                                                               
         BRAS  RE,ZAPSSBUK                  ZAP ALL BUCKETS                     
         MVC   PSSSRAC,TSRKACT              SR TO SS TABLE                      
         MVC   PSSSRCAC,TSRKCAC             SR CONTRA TO SS TABLE               
         MVC   PSSSRBNO,TSRKBNO             BILL NO. TO SS TABL.                
         MVC   PSSSRBDA,TSRKBDA             BILL DATE TO SS TABL.               
         ZAP   PSSBILLB,TSRADR              DEBIT FROM SS TAB                   
         ZAP   PSSRCVDB,TSRACR              CREDIT FROM SS TAB                  
         ZAP   PSSDISBB,TSRAPCNT            PERCENT IN DISBURSED BUCKET         
         XC    PSSKMOS,PSSKMOS              ADDING BY ESTIMATE TOTAL            
*        XC    PSSKOFF,PSSKOFF              CLEAR OFFICE                        
         MVI   PSSKRTYP,X'02'                                                   
         TM    FLAG1,PSSDISB                IS THIS A DISB ONLY ENTRY           
         BNO   *+8                                                              
         OI    PSSFLAG,PSSDISB     MRK IT SO IT'S EASY TO SKIP PRINTING         
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2                                         
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
BLDSR120 GOTO1 ATSAR,DMCB,(RC),TSANXT,1   GET NXT RECORD IN TSRAWRK             
         TM    BCTSERRS,TSEEOF     IS THIS END OF FILE                          
         BNO   BLDSR40                                                          
         B     BLDSR10                                                          
*                                                                               
BLDSRX   DS    0H                                                               
*                                                                               
         J     EXIT                                                             
         DROP  R2,R4                                                            
         LTORG                                                                  
         EJECT                                                                  
*&&                                                                             
*                                                                               
*                                                                               
*                                                                               
**********************************************************************          
* READ SR PASSIVE PNTRS AND BUILD TABLE BY REFERENCE NUMBER          *          
* APPLY PERCENT TO EACH DEBIT ENTRY BY BILL NUMBER/EST/MOS           *          
**********************************************************************          
         USING AARPKEY,R4                                                       
*                                                                               
SRPNTRTB NTR1  BASE=*,LABEL=*                                                   
         XC    SVKEY,SVKEY         BUILD KEY TO READ SR RECDS                   
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAINI,1 REINIT FIRST BUFFER                     
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
SRPNT10  DS    0H                                                               
         LA    R4,SVKEY                                                         
         XC    AARPKEY,AARPKEY     INIT KEY                                     
         MVI   AARPTYP,AARPTYPQ    AA RCV PASSIVE POINTER                       
         MVI   AARPSUB,AARPSUBQ    AA RCV PASSIVE POINTER SUB TYPE              
         MVC   AARPCPY,AUTCPY      COMPANY                                      
         MVI   AARPIND,AARPIEST    ESTIMATE CODE POINTER                        
*                                                                               
         GOTO1 ADMRDHI,DMCB,(RC)                                                
         B     SRPNT30                                                          
*                                                                               
SRPNT20  GOTO1 ADMRDSEQ,DMCB,(RC)    READ NEXT SR POINTER                       
*                                                                               
SRPNT30  DS    0H                                                               
         MVI   SKIPCHK,C'Y'        NEW PP - INDICATE NEED SKIP CHECK            
*                                                                               
         CLC   SVKEY(AARPCLT-AARPKEY),IOKEY                                     
         BNE   SRPNT210                                                         
*                                                                               
         CLI   AUTSYSTM,C' '                                                    
         BNH   *+14                                                             
         CLC   IOKEY+AARPSYS-AARPKEY(L'AARPSYS),AUTSYSTM                        
         BNE   SRPNT20                                                          
*                                                                               
         OC    AUTALST,AUTALST     DO WE HAVE A CLIENT LIST FROM REQST          
         BZ    SRPNT33                                                          
*                                                                               
         MVC   CLNT,IOKEY+AARPCLT-AARPKEY                                       
         BRAS  RE,CHKCLT           CHECK IF IT IS A VALID CLIENT                
         BNE   SRPNT20             READ NEXT TRANSACTION                        
         B     SRPNT35             MATCHING CLIENT FOUND                        
*                                                                               
SRPNT33  DS    0H                                                               
         CLC   AUTCLNT,SPACES                                                   
         BNH   *+14                                                             
         CLC   IOKEY+AARPCLT-AARPKEY(L'AARPCLT),AUTCLNT                         
         BNE   SRPNT20                                                          
*                                                                               
SRPNT35  DS    0H                                                               
         MVC   SVSRPKEY,IOKEY      SAVE SR POINTER KEY                          
*                                                                               
         GOTO1 ADMGET,DMCB,(RC),0 GET RECORD                                    
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLC   IOKEY+1(2),=C'SR'                                                
         BNE   SRPNT20                                                          
*                                                                               
         CLI   IOKEY+(L'IOKEY-1),X'00'  DEBIT?                                  
         BE    SRPNT60                                                          
*                                                                               
         MVC   SVKEY,IOKEY                                                      
         MVI   SVKEY+(L'SVKEY-1),X'00'  MAY NOT BE THERE                        
         GOTO1 ADMRDHI,DMCB,(RC)        READ HIGH SEQUENCE 00                   
*        BE    SRPNT50                                                          
         CLC   SVKEY(L'SVKEY-1),IOKEY                                           
         BE    SRPNT50                                                          
         DC    H'0'                                                             
*                                                                               
SRPNT40  GOTO1 ADMRDSEQ,DMCB,(RC)                                               
         CLC   SVKEY(L'TRNKEY-1),IOKEY                                          
         BNE   SRPNT160                                                         
*                                                                               
SRPNT50  GOTO1 ADMGET,DMCB,(RC),0 GET RECORD                                    
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLC   IOKEY+1(2),=C'SR'                                                
         BNE   SRPNT20                                                          
*                                                                               
SRPNT60  DS    0H                                                               
         LA    R2,TSRAWRK                                                       
         USING TSRAD,R2                                                         
         LA    R4,IO                                                            
         USING TRNRECD,R4                                                       
*                                                                               
         CLC   AUTACCT,SPACES                                                   
         BNH   *+14                                                             
         CLC   TRNKACT,AUTACCT                                                  
         BNE   SRPNT150                                                         
*                                                                               
         XC    TSRAWRK,TSRAWRK     INITIAZIZE WORK AREA                         
         ZAP   SVTRNAMT,=P'0'                                                   
         ZAP   SVCOMAMT,=P'0'                                                   
         MVI   SVBILTYP,X'00'                                                   
         XC    SVCLNET,SVCLNET                                                  
*                                                                               
         MVC   TSRKBNO,TRNKREF         SR TRANSACTION NUMBER                    
         MVC   TSRKBDA,TRNKDATE        SR TRANSACTION DATE                      
*                                                                               
         LR    R5,R4                                                            
         AH    R5,=Y(TRNRFST-TRNKEY)                                            
*                                                                               
SRPNT70  CLI   0(R5),0                                                          
         BE    SRPNT140                                                         
*                                                                               
         CLI   0(R5),TRNELQ        IS IT X'44' ELEMENT                          
         BE    SRPNT90                                                          
*                                                                               
         CLI   0(R5),TRSELQ        IS IT X'60' ELEMENT                          
         BE    SRPNT100                                                         
*                                                                               
         CLI   0(R5),MDTELQ        IS IT X'1A' ELEMENT                          
         BE    SRPNT110                                                         
*                                                                               
         CLI   0(R5),MDPELQ        IS IT X'6A' ELEMENT                          
         BE    SRPNT110            CLI/PRO/EST/MOS IN X'6A'=X'1A'               
*                                                                               
         CLI   0(R5),MBIELQ        IS IT X'F3' ELEMENT                          
         BE    SRPNT130                                                         
*                                                                               
         CLI   0(R5),RALELQ        IS IT X'D9' ELEMENT                          
         BE    SRPNT135                                                         
*                                                                               
         CLI   0(R5),FFTELQ        IS IT X'DB' ELEMENT                          
         BE    SRPNT136                                                         
*                                                                               
SRPNT80  SR    R1,R1                                                            
         IC    R1,1(R5)            GET LENGTH OF ELEMENT                        
         AR    R5,R1                                                            
         B     SRPNT70                                                          
*                                                                               
         USING TRNELD,R5                                                        
SRPNT90  DS     0H                                                              
         TM    TRNSTAT,TRNSDR      IS IT A DEBIT                                
         BO    *+8                                                              
         OI    TSRFLAG,TSRFLGCR                                                 
*                                                                               
         ZAP   SVTRNAMT,TRNAMNT                                                 
         B     SRPNT80                                                          
*                                                                               
         USING TRSELD,R5                                                        
SRPNT100 DS    0H                                                               
*        TM    TRSSTAT,TRSSOFFS    IS IT AN OFFSET                              
*        BO    SRPNT150            SKIP OFFSETS                                 
         B     SRPNT80                                                          
         DROP  R5                                                               
*                                                                               
         USING MDTELD,R5           PROCESS 1A OR 6A                             
SRPNT110 DS    0H                                                               
         CLI   MDTSYS,C'J'                                                      
         BE    SRPNT150            SKIP THIS TRANSACTION                        
*                                                                               
*        TM    TSRFLAG,TSRFLGCR                                                 
*        BO    SRPNT112                                                         
*        CLC   SVSRPKEY+AARPEST-AARPKEY(L'AARPEST),SPACES                       
*        BNH   SRPNT112                                                         
*        CLC   SVSRPKEY+AARPEST-AARPKEY(L'MDTEST),MDTEST                        
*        BNE   SRPNT150            SKIP THIS TRANSACTION                        
*                                                                               
SRPNT112 DS    0H                                                               
         MVC   SVSYS,MDTSYS        SYSTEM                                       
         MVC   SVMED,MDTMED        MEDIA                                        
         GOTOR GETSYSTM            GET SYSTEM BASED ON MEDIA                    
         MVC   TSRKSYS,SVSYS                                                    
*                                                                               
         CLI   AUTMEDIA,C' '                                                    
         BNH   *+14                                                             
         CLC   SVMED,AUTMEDIA                                                   
         BNE   SRPNT150                                                         
*                                                                               
         MVC   TSRKMED,MDTMED                                                   
*                                                                               
         CLC   AUTCLNT,SPACES                                                   
         BNH   *+14                                                             
         CLC   MDTCLI,AUTCLNT                                                   
         BNE   SRPNT150            WRONG CLIENT                                 
*                                                                               
         MVC   TSRKCLI,MDTCLI          MOVE IN CLIENT CODE TO WORK              
         MVC   SVCLI,MDTCLI                                                     
*                                                                               
         CLI   MDTMOS+1,X'13'      IS THIS THE 13TH MONTH                       
         BE    SRPNT150            SKIP THIS TRANSACTION                        
         MVC   SVMOS(L'MDTMOS),MDTMOS                                           
         GOTOR GTINSRTN            GET INSERTION DATE                           
*                                                                               
         BRAS  RE,FIXPMOS                                                       
         MVC   TSRKMOS,SVMOS                                                    
*                                                                               
SRPNT115 DS    0H                                                               
         CLI   0(R5),X'6A'                                                      
         BE    SRPNT120                                                         
         ICM   R1,15,MDTCOM        GET NET/COMMISSION AMOUNT                    
         CVD   R1,DUB                                                           
         SP    SVTRNAMT,DUB        SUBTRACT NET FROM GROSS                      
         ZAP   SVCOMAMT,DUB                                                     
         B     SRPNT140                                                         
*                                                                               
         USING MDPELD,R5                                                        
SRPNT120 SP    SVTRNAMT,MDPCOM                                                  
         ZAP   SVCOMAMT,MDPCOM                                                  
         B     SRPNT140                                                         
*                                                                               
         USING MBIELD,R5           PROCESS X'F3'                                
SRPNT130 DS    0H                                                               
*        TM    TSRFLAG,TSRFLGCR                                                 
*        BO    SRPNT132                                                         
*        CLC   SVSRPKEY+AARPEST-AARPKEY(L'AARPEST),SPACES                       
*        BNH   *+14                                                             
*        CLC   SVSRPKEY+AARPEST-AARPKEY(L'MBIEST),MBIEST                        
*        BNE   SRPNT150            SKIP THIS TRANSACTION                        
*                                                                               
SRPNT132 DS    0H                                                               
         MVC   SVSYS,MBISYS        SYSTEM                                       
         MVC   SVMED,MBIMED        MEDIA                                        
*                                                                               
         CLI   AUTMEDIA,C' '                                                    
         BNH   *+14                                                             
         CLC   SVMED,AUTMEDIA                                                   
         BNE   SRPNT150                                                         
*                                                                               
         GOTOR GETSYSTM            GET SYSTEM BASED ON MEDIA                    
         MVC   TSRKSYS,SVSYS           PUT SYSTEM IN TABLE                      
*                                                                               
         CLI   TSRKSYS,C'N'            IS THIS NET SYSTEM ?                     
         BNE   *+12                                                             
         CLI   AUTPRF2,C'S'        RUN BY SYSTEM OR SYS/MED ?                   
         BE    *+10                                                             
         MVC   TSRKMED,SVMED           PUT MEDIA CODE IN TABLE                  
*                                                                               
         CLC   AUTCLNT,SPACES                                                   
         BNH   *+14                                                             
         CLC   MBICLI,AUTCLNT                                                   
         BNE   SRPNT150            WRONG CLIENT                                 
*                                                                               
         MVC   TSRKCLI,MBICLI          MOVE IN CLIENT CODE TO WORK              
         MVC   SVCLI,MBICLI                                                     
*                                                                               
         CLI   MBIMOS+1,X'13'      IS THIS THE 13TH MONTH                       
         BE    SRPNT150            SKIP THIS TRANSACTION                        
         MVC   SVMOS(L'MBIMOS),MBIMOS                                           
         GOTOR GTINSRTN            GET INSERTION DATE                           
*                                                                               
         BRAS  RE,FIXPMOS                                                       
*                                                                               
         MVC   TSRKMOS,SVMOS           MOVE IN MONTH OF SERVICE                 
         B     SRPNT80                                                          
*                                                                               
         USING RALELD,R5           PROCESS X'D9'                                
SRPNT135 DS    0H                                                               
         CLI   RALTYPE,RALTOFS                                                  
         BNE   *+14                SKIP OFFSETS                                 
         MVC   TSRKCDA,RALODAT                                                  
         B     SRPNT80                                                          
*                                                                               
         MVC   TSRKCDA,RALADAT                                                  
         B     SRPNT80                                                          
*                                                                               
         USING FFTELD,R5           PROCESS X'DB'                                
SRPNT136 DS    0H                                                               
         CLI   FFTTYPE,FFTTCNET    CALCULATED NET                               
         BNE   *+14                                                             
         MVC   SVCLNET,FFTDATA                                                  
         B     SRPNT80                                                          
*                                                                               
         CLI   FFTTYPE,FFTTMXTY    MX ELEMENT AND BILL TYPES                    
         BNE   SRPNT80                                                          
*                                                                               
         LA    RE,XBILTYPS                                                      
SRPNT137 CLI   0(RE),X'FF'                                                      
         BE    SRPNT80                                                          
         LLC   RF,0(RE)            FFTELD ELEM DATA LENGTH                      
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   1(0,RE),FFTMXTB     SAME BILL TYPE AS IN FFTELD?                 
         BNE   SRPNT138            NO - CHECK NEXT TABLE ENTRY                  
         MVI   SVBILTYP,C'X'       YES - INDICATE X-TYPE BILL                   
         B     SRPNT80                                                          
*                                                                               
SRPNT138 DS    0H                  GO TO NEXT LINE IN XBILTYPS                  
         LLC   RF,0(RE)                                                         
         LA    RE,1(RE,RF)                                                      
         B     SRPNT137                                                         
*                                                                               
XBILTYPS DC    AL1(4),C'REGX'                                                   
         DC    AL1(4),C'ADJX'                                                   
         DC    AL1(5),C'AORXM'                                                  
         DC    AL1(5),C'AORXA'                                                  
         DC    X'FF'                                                            
*                                                                               
*                                                                               
* TRANSACTION RECORD PROCESSED                                                  
*                                                                               
SRPNT140 DS    0H                                                               
         CLI   SVBILTYP,C'X'       X-TYPE BILL?                                 
         BNE   SRPNT142                                                         
         ICM   RF,15,SVCLNET       IS THERE CALCULATED NET?                     
         BZ    SRPNT142                                                         
         CVD   RF,DUB                                                           
         SP    SVTRNAMT,DUB        SUBTRACT NET FROM GROSS                      
*                                                                               
SRPNT142 DS    0H                                                               
         CLI   SKIPCHK,C'Y'        NEED SKIP CHECK?                             
         BNE   SRPNT143                                                         
*                                                                               
         MVI   SKIPCHK,C'N'                                                     
         MVC   TSRAWRK2,TSRAWRK    SAVE RECORD                                  
         XC    TSRKMOS(TSRKEND-TSRKMOS),TSRKMOS                                 
         MVC   TSRKACT,TRNKACT                                                  
         MVC   WORK,TSRAWRK                                                     
         GOTO1 ATSAR,DMCB,(RC),TSARDH,1                                         
         TM    BCTSERRS,TSEEOF                                                  
         BO    *+14                EOF = REF NOT YET PROCESSED                  
         CLC   TSRKEY(TSRKMOS-TSRKEY),WORK                                      
         BE    SRPNT160            SET ALREADY USED - SKIP IT                   
*                                                                               
* RECORD NOT THERE - ADD IT                                                     
*                                                                               
         MVC   TSRAWRK,TSRAWRK2                                                 
         BRAS  RE,ZAPSRBUK         ZAP ALL BUCKETS                              
         XC    TSRKMOS(TSRKEND-TSRKMOS),TSRKMOS                                 
         MVC   TSRKACT,TRNKACT                                                  
         MVI   TSRKTYP,TTACCTQ                                                  
         GOTO1 ATSAR,DMCB,(RC),TSAADD,1                                         
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   TSRAWRK,TSRAWRK2    RESTORE RECORD                               
*                                                                               
SRPNT143 DS    0H                                                               
         BRAS  RE,ZAPSRBUK         ZAP ALL BUCKETS                              
         TM    TSRFLAG,TSRFLGCR                                                 
         BZ    *+14                                                             
         ZAP   TSRAWRK+(TSRACR-TSRAD)(TSRABKLN),SVTRNAMT                        
         B     *+10                                                             
         ZAP   TSRAWRK+(TSRADR-TSRAD)(TSRABKLN),SVTRNAMT                        
*                                                                               
         ZAP   TSRAWRK+(TSRACOM-TSRAD)(TSRABKLN),SVCOMAMT                       
*                                                                               
* RECORD IS BUILT NOW - ADD IT TO BUFFER                                        
* DEBIT, CREDIT ARE PROCESSED SEPARATELY                                        
*                                                                               
         TM    TSRFLAG,TSRFLGCR        CREDIT?                                  
         BZ    SRPNT145            NO - GO TO DEBIT PROCESSING                  
*                                                                               
* CREDIT HERE                                                                   
*                                                                               
         CLI   TSRKSYS,0                                                        
         BNE   *+10                                                             
         MVC   TSRKSYS,SVSYS                                                    
         CLI   TSRKMED,0                                                        
         BNE   *+10                                                             
         MVC   TSRKMED,SVMED                                                    
         CLI   TSRKCLI,0                                                        
         BNE   *+10                                                             
         MVC   TSRKCLI,SVCLI                                                    
*                                                                               
         MVI   TSRKTYP,TTCRQ                                                    
         GOTO1 ATSAR,DMCB,(RC),TSAADD,1                                         
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         B     SRPNT148               UPDATE TOTALS BY REF#                     
*                                                                               
* DEBIT HERE                                                                    
*                                                                               
SRPNT145 DS    0H                                                               
*                                                                               
* ADD THE DEBIT                                                                 
*                                                                               
         MVI   TSRKTYP,TTDRQ                                                    
         GOTO1 ATSAR,DMCB,(RC),TSAADD,1                                         
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
* ADD/UPDATE TOTALS BY REF#                                                     
*                                                                               
SRPNT148 DS    0H                                                               
         XC    TSRKMOS(TSRKEND-TSRKMOS),TSRKMOS                                 
         MVI   TSRKTYP,TTDRREFQ                                                 
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAADD,1                                         
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
* READ FOR NXT SEQUENTIAL SR TRAN                                               
*                                                                               
SRPNT150 MVC   SVKEY,IOKEY                                                      
         GOTO1 ADMREAD,DMCB,(RC)   READ FOR NXT SEQUENTIAL SR TRAN              
         B     SRPNT40                                                          
*                                                                               
SRPNT160 DS    0H                                                               
         MVC   SVKEY,SVSRPKEY                                                   
         GOTO1 ADMREAD,DMCB,(RC)   READ FOR NEXT SEQUENTIAL PNTR                
         B     SRPNT20             READ NEXT SR POINTER                         
*                                                                               
* ALL TRANSACTIONS PROCESSED HERE                                               
*                                                                               
SRPNT210 DS    0H                                                               
*                                                                               
* READ ALL RECORDS.  FOR EVERY DEBIT TRANSACTION FOUND                          
* READ EVERY CR FOR THAT REF#, AND ADD A DR-CR LINE                             
*                                                                               
         XC    TSRAWRK,TSRAWRK     START AT THE BEGINNING                       
*                                                                               
SRPNT229 GOTO1 ATSAR,DMCB,(RC),TSARDH,1                                         
         B     SRPNT231                                                         
*                                                                               
* OUTER LOOP - READ DEBITS                                                      
*                                                                               
SRPNT230 GOTO1 ATSAR,DMCB,(RC),TSANXT,1   GET RECORD IN TSRAWRK                 
SRPNT231 TM    BCTSERRS,TSEEOF              IS THIS END OF FILE                 
         BO    SRPNT250            ALL CREDITS EXPANDED                         
*                                                                               
         CLI   TSRKTYP,TTDRREFQ    HAVE REF# TOTALS?                            
         BNE   SRPNT233                                                         
*                                                                               
         CP    TSRADR,=P'0'        ZERO-DOLLAR REF#?                            
         BNE   *+14                                                             
         MVC   TSRKMOS,=X'FFFF'    READ NEXT REF#                               
         B     SRPNT229                                                         
*                                                                               
         MVC   TOTREFDR,TSRADR     SAVE REF# TOTAL DR, CR, COMMISSION           
         MVC   TOTREFCR,TSRACR                                                  
         MVC   TOTREFCM,TSRACOM                                                 
         B     SRPNT230                                                         
*                                                                               
SRPNT233 DS    0H                                                               
         CLI   TSRKTYP,TTDRQ       HAVE DEBIT?                                  
         BNE   SRPNT230            NO - READ NEXT                               
*                                                                               
         MVI   CRFOUND,C'N'                                                     
*                                                                               
* FOUND A DEBIT ENTRY HERE                                                      
* LOOK FOR CORRESPONDING CREDITS                                                
*                                                                               
         MVC   TSRAWRK2,TSRAWRK    SAVE THE DEBIT KEY                           
*                                                                               
* UPDATE TOTALS BY MOS-BILLDATE                                                 
*                                                                               
         XC    TSRKBNO,TSRKBNO                                                  
         MVI   TSRKTYP,TTREPQ                                                   
         GOTO1 ATSAR,DMCB,(RC),TSAADD,1                                         
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   TSRAWRK,TSRAWRK2                                                 
         XC    TSRKMOS(TSRKEND-TSRKMOS),TSRKMOS XC KEY FROM MOS ONWARD          
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSARDH,1                                         
         B     SRPNT241                                                         
*                                                                               
* INNER LOOP - READ ALL CREDITS FOR THE GIVEN DEBIT(SAVED IN TSRAWRK2)          
*                                                                               
SRPNT240 GOTO1 ATSAR,DMCB,(RC),TSANXT,1   GET RECORD IN TSRAWRK                 
SRPNT241 TM    BCTSERRS,TSEEOF              IS THIS END OF FILE                 
         BO    SRPNT245            YES: DONE, RETURN TO OUTER LOOP              
*                                                                               
         CLC   TSRAWRK(TSRKMOS-TSRKEY),TSRAWRK2 SAME S/M/CLT/REF#?              
         BNE   SRPNT245            NO: DONE, RETURN TO OUTER LOOP               
*                                                                               
         CLI   TSRKTYP,TTCRQ       HAVE CREDIT?                                 
         BNE   SRPNT240            NO - READ NEXT                               
*                                                                               
* CALCULATE THE % OF COMMISSION AMOUNT FOR THIS REF#                            
* LOWER THE CREDIT AMOUNT BY THE NUMBER OBTAINED                                
*                                                                               
         CP    TOTREFCR,=P'0'                                                   
         BE    SRPNT242                                                         
*                                                                               
         ZAP   PL16,TSRACR         CR                                           
         MP    PL16,TOTREFCM       CR*TOT_COMM                                  
         DP    PL16,TOTREFCR       (CR*TOT_COM)/TOT_CR                          
         SP    TSRACR,PL16(8)      CALCULATED CREDIT-COMMISION                  
*                                                                               
SRPNT242 MVI   CRFOUND,C'Y'                                                     
         MVC   TSRALAST,TSRAWRK    LAST CREDIT READ IN                          
*                                                                               
* CALCULATE (DR/DR TOT)*CR                                                      
*                                                                               
         ZAP   PL16,TSRACR         CR                                           
         MP    PL16,TSRAWRK2+TSRADR-TSRAD(8)  CR*DR                             
         DP    PL16,TOTREFDR       (CR*DR)/TOT_DR                               
*                                                                               
* ADD EXPANDED CREDITS BY MOS-BILLDATE-CKDATE                                   
*                                                                               
         MVC   TSRAWRK,TSRAWRK2    DEBIT                                        
*                                                                               
* SINCE ONE DR TRANSACTION CAN HAVE MULTIPLE CREDITS APPLIED TO IT              
* THE DR AMOUNT IS SAVED IN THE MOS-BILLDATE RECORD, AND NOT                    
* REPEATED IN EACH MOS-BILLDATE-CKDATE LINE                                     
*                                                                               
         ZAP   TSRACR,PL16(8)      CALCULATED CREDIT                            
         MVC   TSRKCDA,TSRALAST+TSRKCDA-TSRKEY CHECK DATE                       
         MVI   TSRKTYP,TTREPQ                                                   
         MVI   TSRFLAG,0                                                        
         XC    TSRKBNO,TSRKBNO                                                  
*                                                                               
         CP    TSRACR,=P'0'                                                     
         BE    SRPNT244                                                         
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAADD,1                                         
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
* RESTORE INNER LOOP READ SEQUENCE, AND GO LOOK FOR NEXT DEBIT                  
*                                                                               
SRPNT244 DS    0H                                                               
         MVC   TSRAWRK,TSRALAST                                                 
         GOTO1 ATSAR,DMCB,(RC),TSARDH,1                                         
         B     SRPNT240                                                         
*                                                                               
* ALL CREDITS FOR THIS S/M/CLT/REF HAVE BEEN PROCESSED                          
* RESTORE OUTER LOOP READ SEQUENCE                                              
* RETURN TO OUTER LOOP TO LOOK FOR NEXT DEBIT                                   
*                                                                               
SRPNT245 DS    0H                                                               
         CLI   CRFOUND,C'Y'        FOUND ANY CREDITS FOR THIS DR?               
         BE    SRPNT247            YES - ALL OK                                 
*                                                                               
* HERE WE HAVE A DEBIT W/O MATCHING CREDITS                                     
* ADD DR/CR LINE WITH TODAY AS CHECK DATE AND $0 CR AMOUNT                      
*                                                                               
         MVC   TSRAWRK,TSRAWRK2    SAVED DEBIT                                  
         ZAP   TSRACR,=P'0'                                                     
         MVC   TSRKCDA,AUTTODAY                                                 
         MVI   TSRKTYP,TTREPQ                                                   
         MVI   TSRFLAG,TSRFLGNC                                                 
         XC    TSRKBNO,TSRKBNO                                                  
         GOTO1 ATSAR,DMCB,(RC),TSAADD,1                                         
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
SRPNT247 DS    0H                                                               
         MVC   TSRAWRK,TSRAWRK2                                                 
         GOTO1 ATSAR,DMCB,(RC),TSARDH,1                                         
         B     SRPNT230                                                         
*                                                                               
SRPNT250 DS    0H                                                               
*                                                                               
SRPNTX   DS    0H                                                               
*                                                                               
         J     EXIT                                                             
         DROP  R2,R4,R5                                                         
         LTORG                                                                  
*                                                                               
*                                                                               
*                                                                               
*                                                                               
TRACEIO  NTR1  BASE=*,LABEL=*                                                   
         LA    R1,IOKEY                                                         
         ICM   R1,8,=AL1(L'IOKEY)                                               
         BRAS  RE,TRACE                                                         
         J     EQXIT                                                            
         LTORG                                                                  
*                                                                               
*                                                                               
**********************************************************************          
* R1 = A(STRING TO HEXOUT)                                                      
* R1 HOB = STRING LENGTH                                                        
**********************************************************************          
TRACE    NTR1  BASE=*,LABEL=*                                                   
         L     RF,TRCHOOK                                                       
         L     RE,CALLRD           BACK NMOD LINK                               
         LM    R2,RC,28(RE)        CALLER'S R2-RC                               
         BASR  RE,RF                                                            
         J     EQXIT                                                            
         LTORG                                                                  
*                                                                               
*                                                                               
*                                                                               
*&&DO                                                                           
**********************************************************************          
* READ SR TRANSACTIONS AND BUILD TABLE BY REFERENCE NUMBER           *          
* APPLY PERCENT TO EACH DEBIT ENTRY BY BILL NUMBER/EST/MOS           *          
**********************************************************************          
         USING TRNKEY,R4                                                        
         USING TSRAD,R2                                                         
         SPACE 1                                                                
BLDSRTAB NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         XC    SVKEY,SVKEY         BUILD KEY TO READ SR RECDS                   
         XC    SVSYS,SVSYS         CLEAR SYSTEM                                 
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAINI,1 REINIT FIRST BUFFER                     
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R2,TSRAWRK                                                       
         XC    TSRAWRK,TSRAWRK     CLEAR WORK AREA                              
         ZAP   SVTRNAMT,=P'0'                                                   
         NI    FLAG,X'FF'-FLGSR    CLEAR FLAG                                   
*                                                                               
         LA    R4,SVKEY                                                         
         MVC   TRNKEY,SPACES       INIT KEY                                     
*                                                                               
         CLI   LSTSRKEY,X'FF'      IS THIS LAST TIME IN                         
         BE    BSRTBX                                                           
*                                                                               
         OC    LSTSRKEY,LSTSRKEY   IS THIS FIRST TIME IN                        
         BZ    *+14                YES                                          
         MVC   TRNKEY,LSTSRKEY     NOT FIRST TIME IN                            
         B     BSRTB10                                                          
         MVC   TRNKCPY,AUTCPY             COMPANY CODE                          
         MVC   TRNKUNT(2),=C'SR'          ALWAYS SR                             
*                                                                               
BSRTB10  GOTO1 ADMRDHI,DMCB,(RC)                                                
         B     BSRTB30                                                          
*                                                                               
BSRTB20  GOTO1 ADMRDSEQ,DMCB,(RC)                                               
BSRTB30  CLC   SVKEY(TRNKACT-TRNKEY),IOKEY                                      
         BE    BSRTB40                                                          
         MVI   LSTSRKEY,X'FF'      U/L CHNGD, INDICATE END OF TRANS.            
         B     BSRTB190            PROCESS LAST AND EXIT                        
*                                                                               
BSRTB40  LA    R4,IO                                                            
         CLC   TRNKDATE,SPACES                                                  
         BNH   BSRTB20                                                          
         CLC   TRNKREF,SPACES      IS IT A TRANSACTION                          
         BNH   BSRTB20                                                          
*                                                                               
         OC    LSTSRKEY,LSTSRKEY   IS THIS FIRST TIME IN                        
         BNZ   BSRTB50                                                          
         MVC   LSTSRKEY,IOKEY      SAVE OFF SR KEY                              
         B     BSRTB60                                                          
BSRTB50  CLC   LSTSRKEY(L'LSTSRKEY-1),IOKEY  ARE WE DOING THE SAME ACC?         
         BE    BSRTB60                                                          
         MVC   LSTSRKEY,IOKEY      SAVE OFF SR ACCOUNT                          
         B     BSRTB190            AND EXIT                                     
*                                                                               
BSRTB60  GOTO1 ADMGET,DMCB,(RC),0 GET RECORD                                    
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R4,IO                                                            
         USING TRNELD,R5                                                        
         XC    TSRAWRK,TSRAWRK     INITIAZIZE WORK AREA                         
*                                                                               
*        MVC   TSRKSYS,SVSYS       MOVE IN SYSTEM                               
         MVC   TSRKACT,TRNKULA     SR ACCOUNT                                   
         MVC   TSRKCAC,TRNKULC     SR CONTRA ACCOUNT                            
         MVC   TSRKBNO,TRNKREF     SR TRANSACTION NUMBER                        
         MVC   TSRKBDA,TRNKDATE    SR TRANSACTION DATE                          
*        MVC   TSRKOFF,TRNKOFF     SR OFFICE                                    
         XC    TSRFLAG,TSRFLAG     INIT FLAG                                    
*                                                                               
         LR    R5,R4                                                            
         AH    R5,=Y(TRNRFST-TRNKEY)                                            
BSRTB70  CLI   0(R5),0                                                          
         BE    BSRTB170                                                         
         CLI   0(R5),TRNELQ        IS IT X'44' ELEMENT                          
         BE    BSRTB90                                                          
         CLI   0(R5),TRSELQ        IS IT X'60' ELEMENT                          
         BE    BSRTB100                                                         
         CLI   0(R5),MDTELQ        IS IT X'1A' ELEMENT                          
         BE    BSRTB110                                                         
         CLI   0(R5),MDPELQ        IS IT X'6A' ELEMENT                          
         BE    BSRTB110            CLI/PRO/EST/MOS IN X'6A'=X'1A'               
         CLI   0(R5),MBIELQ        IS IT X'F3' ELEMENT                          
         BE    BSRTB140                                                         
*                                                                               
BSRTB80  SR    R1,R1                                                            
         IC    R1,1(R5)            GET LENGTH OF ELEMENT                        
         AR    R5,R1                                                            
         B     BSRTB70                                                          
*                                                                               
BSRTB90  DS    0H                                                               
         TM    TRNSTAT,TRNSDR      IS IT A DEBIT                                
         BNO   *+8                                                              
         OI    TSRFLAG,TSRFLGDR                                                 
         ZAP   SVTRNAMT,TRNAMNT                                                 
         B     BSRTB80                                                          
*                                                                               
         USING TRSELD,R5                                                        
BSRTB100 DS    0H                                                               
         TM    TRSSTAT,TRSSOFFS    IS IT AN OFFSET                              
         BO    BSRTB180            SKIP OFFSETS                                 
         B     BSRTB80                                                          
         DROP  R5                                                               
*                                                                               
BSRTB110 DS    0H                                                               
         USING MDTELD,R5           PROCESS 1A OR 6A                             
         CLI   MDTSYS,C'J'                                                      
         BE    BSRTB180            SKIP THIS TRANSACTION                        
*                                                                               
         MVC   SVSYS,MDTSYS        SYSTEM                                       
         MVC   SVMED,MDTMED        MEDIA                                        
         GOTOR GETSYSTM            GET SYSTEM BASED ON MEDIA                    
         MVC   TSRKSYS,SVSYS       MOVE IN SYSTEM                               
*                                                                               
         CLI   TSRKSYS,C'N'        IS THIS NET SYSTEM ?                         
         BNE   *+12                                                             
         CLI   AUTPRF2,C'S'        RUN BY SYSTEM OR SYS/MED ?                   
         BE    *+10                                                             
         MVC   TSRKMED,SVMED       PUT MEDIA CODE IN TABLE                      
*                                                                               
         MVC   TSRKCLI,MDTCLI      MOVE IN CLIENT CODE TO WORK                  
         MVC   TSRKPRO,MDTPRD      MOVE IN PRODUCT TO WORK                      
         MVC   TSRKEST,MDTEST                                                   
*                                                                               
         CLI   MDTMOS+1,X'13'      IS THIS THE 13TH MONTH                       
         BE    BSRTB180            SKIP THIS TRANSACTION                        
         MVC   SVMOS(L'MDTMOS),MDTMOS                                           
         GOTOR GTINSRTN            GET INSERTION DATE                           
         MVC   TSRKMOS,SVMOS       MOVE IN MONTH OF SERVICE                     
*                                                                               
         CLI   0(R5),X'6A'                                                      
         BE    BSRTB135                                                         
         ICM   R1,15,MDTCOM        GET NET/COMMISSION AMOUNT                    
         CVD   R1,DUB                                                           
         SP    SVTRNAMT,DUB        SUBTRACT NET FROM GROSS                      
         B     BSRTB170                                                         
         USING MDPELD,R5                                                        
BSRTB135 SP    SVTRNAMT,MDPCOM                                                  
*                                                                               
         B     BSRTB170                                                         
*                                                                               
BSRTB140 DS    0H                                                               
         USING MBIELD,R5           PROCESS X'F3'                                
*                                                                               
         MVC   SVSYS,MBISYS        SYSTEM                                       
         MVC   SVMED,MBIMED        MEDIA                                        
         GOTOR GETSYSTM            GET SYSTEM BASED ON MEDIA                    
         MVC   TSRKSYS,SVSYS                                                    
*                                                                               
         CLI   TSRKSYS,C'N'        IS THIS NET SYSTEM ?                         
         BNE   *+12                                                             
         CLI   AUTPRF2,C'S'        RUN BY SYSTEM OR SYS/MED ?                   
         BE    *+10                                                             
         MVC   TSRKMED,SVMED       PUT MEDIA CODE IN TABLE                      
*                                                                               
         MVC   TSRKCLI,MBICLI      MOVE IN CLIENT CODE TO WORK                  
         MVC   TSRKPRO,MBIPRD      MOVE IN PRODUCT TO WORK                      
         MVC   TSRKEST(3),MBIEST   MOVE IN ESTIMATE                             
*                                                                               
         CLI   MBIMOS+1,X'13'      IS THIS THE 13TH MONTH                       
         BE    BSRTB180            SKIP THIS TRANSACTION                        
         MVC   SVMOS(L'MBIMOS),MBIMOS                                           
         GOTOR GTINSRTN            GET INSERTION DATE                           
         MVC   TSRKMOS,SVMOS       MOVE IN MONTH OF SERVICE                     
*        MVC   TSRKMOS,MBIMOS      MOVE IN MONTH OF SERVICE                     
         B     BSRTB80                                                          
*                                                                               
BSRTB170 BRAS  RE,ZAPSRBUK         ZAP ALL BUCKETS                              
         TM    TSRFLAG,TSRFLGDR                                                 
         BZ    *+14                                                             
         ZAP   TSRAWRK+(TSRADR-TSRAD)(TSRABKLN),SVTRNAMT                        
         B     *+10                                                             
         ZAP   TSRAWRK+(TSRACR-TSRAD)(TSRABKLN),SVTRNAMT                        
*                                                                               
         OI    FLAG,FLGSR          BUILDING SR TABLE                            
         MVI   TSRKRTYP,X'02'      DETAILS BY MOS                               
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAADD,1                                         
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    RE,TSRAWRK+(TSRKRTYP-TSRKEY)                                     
         XC    0(TSRKLNQ2,RE),0(RE)      CLEAR RECORD TYP  ONWARDS              
         MVI   TSRKRTYP,X'01'      TOTALS BY ESTIMATE                           
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAADD,1                                         
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    RE,TSRAWRK+(TSRKSYS-TSRKEY)                                      
         XC    0(TSRKLNQ1,RE),0(RE)      CLEAR BILL DATE ONWARDS                
         MVI   TSRKRTYP,X'00'      TOTALS BY BILL NUMBER.                       
         NI    TSRFLAG,X'FF'-TSRFLGDR                                           
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAADD,1                                         
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
BSRTB180 NI    FLAG,X'FF'-FLGSR       RESET BEFORE EXIT OR READING NXT          
         NI    TSRFLAG,X'FF'-TSRFLGDR                                           
         ZAP   SVTRNAMT,=P'0'                                                   
         B     BSRTB20                                                          
         DROP  R2                                                               
*                                                                               
BSRTB190 DS    0H                                                               
         XC    TSRAWRK,TSRAWRK                                                  
         GOTO1 ATSAR,DMCB,(RC),TSARDH,1     GET RECORD IN TSRAWRK               
         TM    BCTSERRS,TSEEOF              IS THIS END OF FILE                 
         BNO   BSRTB200                                                         
*                                  CHECK IF IT IS A LAST TRANSACION             
         CLI   LSTSRKEY,X'FF'      NOTHING ADDED TO TABLE FOR THIS SR           
*                                  CHECK IF IT IS A LAST TRANSACION             
         BE    BSRTBX              LAST SR TRANSACTION                          
*                                                                               
         MVC   SVKEY,LSTSRKEY      READ LAST KEY AND START FROM THERE           
         B     BSRTB10                                                          
*                                                                               
         USING TSRAD,R2                                                         
BSRTB200 LA    R2,TSRAWRK                                                       
*        DROP  R1                                                               
BSRTB210 DS    0H                                                               
         OC    TSRKSYS(TSRKLNQ1),TSRKSYS   SAME BILL DATE AS PREV ?             
         BNZ   BSRTB220                    YES, CALCULATE CREDIT                
         ZAP   SVPCNT,=P'0'                CALC PERCENT                         
         CP    TSRACR,=P'0'                IS APPLIED AMT ZERO?                 
         BZ    BSRTB235                    READ NEXT                            
         CP    TSRADR,=P'0'                IS BILLED AMT ZERO?                  
         BZ    BSRTB235                    READ NEXT                            
         ZAP   PL16,TSRACR      GET CREDIT AMOUNT                               
         MP    PL16,=P'10000'   MULTIPLY BY 100                                 
         DP    PL16,TSRADR      GET PERCENT CR/DR X 100                         
         ZAP   SVPCNT,PL16(L'PL16-L'TSRADR)                                     
         CP    SVPCNT,=P'10000'                                                 
         BNH   *+10                                                             
         ZAP   SVPCNT,=P'10000'    DON'T APPLY MORE THAN 100%                   
         BRAS  RE,ZAPSRBUK         ZAP ALL BUCKETS                              
         ZAP   TSRAPCNT,SVPCNT                                                  
         B     BSRTB230                                                         
*                                                                               
BSRTB220 DS    0H                                                               
         CP    SVPCNT,=P'0'                                                     
         BZ    BSRTB235                                                         
         CP    TSRADR,=P'0'                                                     
         BZ    BSRTB235            SKIP CREDIT ONLY ENTRY                       
         OC    TSRKMOS,TSRKMOS     IS THIS AN ESTIMATE TOTAL                    
         BZ    *+12                YES, GET PERCENT                             
         TM    TSRFLAG,TSRFLGDR    IS IT A DEBIT                                
         BZ    BSRTB235                                                         
         ZAP   PL16,TSRADR         GET DEBIT                                    
         MP    PL16,SVPCNT                                                      
         SRP   PL16,3,0                                                         
         DP    PL16,=PL4'10000'                                                 
         SRP   PL16(L'PL16-4),64-3,5 % ROUNDED TO 2 DP                          
         BRAS  RE,ZAPSRBUK         ZAP ALL BUCKETS                              
         ZAP   TSRACR,PL16(L'PL16-4) SAVE OFF CALCULTED CREDIT                  
         ZAP   TSRAPCNT,SVPCNT                                                  
*                                                                               
BSRTB230 GOTO1 ATSAR,DMCB,(RC),TSAADD,1   PUT/WRITE RECD BACK                   
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 ATSAR,DMCB,(RC),TSARDH,1   HIGH FOR RECD JUST WRITTEN            
*                                                                               
BSRTB235 GOTO1 ATSAR,DMCB,(RC),TSANXT,1   GET RECORD IN TSRAWRK                 
         TM    BCTSERRS,TSEEOF            IS THIS END OF FILE                   
         BNO   BSRTB210                                                         
*                                                                               
BSRTBX   J     EXIT                                                             
         DROP  R2,R4,R5                                                         
         LTORG                                                                  
         EJECT                                                                  
*&&                                                                             
*                                                                               
*                                                                               
*                                                                               
*&&DO                                                                           
**********************************************************************          
* LOOP THOUGH THE TABLE AND UPDATE CASH AVAILABLE BUCKET             *          
* MARK TABLE ENTRY IF ENOUGH CASH IS AVAILABLE TO APPROVE ITEM       *          
**********************************************************************          
         USING PSSAD,R2                                                         
         USING MTHD,R4                                                          
         SPACE 1                                                                
CASHAVL  NTR1  BASE=*,LABEL=*                                                   
         XC    PSSAWRK,PSSAWRK     CLEAR WORK AREA                              
         ZAP   SVTRNAMT,=P'0'                                                   
         XC    FLAG1,FLAG1                                                      
         XC    TEMPWRK,TEMPWRK     BUILD WORK AREA TO UPDT CASH AVAIL           
*                                                                               
         LA    R2,PSSAWRK                                                       
*        LA    R2,TEMPWRK                                                       
         XC    PSSAWRK,PSSAWRK                                                  
         GOTO1 ATSAR,DMCB,(RC),TSARDH,2   GET RECORD IN PSSAWRK                 
         TM    BCTSERRS,TSEEOF     IS THIS END OF FILE                          
         BO    CSHAVX                                                           
CSHAV10  DS    0H                                                               
*        MVC   TEMPWRK(L'PSSAWRK),PSSAWRK                                       
         ZAP   PKAVAIL,=P'0'                                                    
         ZAP   SVPKAMT,=P'0'                                                    
         ZAP   PKAMNT,=P'0'                                                     
*                                                                               
         CLI   PSSKTYP,X'00'       IS THIS A GOOD ESTIMATE                      
         BNE   CSHAVX                                                           
*                                                                               
         CLI   PSSKRTYP,X'00'      IS THIS A NEW C/P/E ENTRY                    
         BNE   CSHAV30                                                          
         BRAS  RE,INMTHTB          INIT/RESET FLAG IN MONTHS TABLE              
*                                                                               
CSHAV30  DS    0H                                                               
         CLI   PSSKRTYP,X'02'      ARE THESE SR DETAILS                         
         BE    CSHAV170            GET NEXT RECORD                              
         CLI   PSSKRTYP,X'05'      ALSO UPDATE SS DETAIL RECORD                 
         BNE   CSHAV40                                                          
         OI    FLAG1,FLGSTAT                                                    
         B     CSHAV130                                                         
*                                                                               
CSHAV40  NI    FLAG1,X'FF'-FLGSTAT                                              
         CLI   AUTOPT6,C'Y'        ARE WE APPROVING BY MOS                      
         BNE   CSHAV50                                                          
         CLI   PSSKRTYP,X'03'                                                   
         BNE   CSHAV60                                                          
         OI    FLAG1,FLGSTAT       TURN ON APPROVE/MANUAL STATUS                
CSHAV50  CLI   PSSKRTYP,X'00'                                                   
         BNE   CSHAV60                                                          
         OI    FLAG1,FLGSTAT       TURN ON APPROVE/MANUAL STATUS                
*                                                                               
CSHAV60  DS    0H                                                               
         TM    FLAG1,FLGSTAT                                                    
         BNO   CSHAV90            WE DON'T NEED ANY STATUS ON THIS LINE         
*                                                                               
         LA    RF,RQMNTHS                                                       
         LA    R4,MNTHS                                                         
CSHAV70  CLC   MTHCODE,PSSKMOS    REQUESTED MONTH/00 (EST) ENTRY ?              
         BE    CSHAV80                                                          
         LA    R4,MTHLEN(R4)                                                    
         BCT   RF,CSHAV70                                                       
         DC    H'0'                                                             
*                                                                               
CSHAV80  NI    MTHFLAG,X'FF'-(PSSCSH+PSSMAN) CASH IS NOT AVAILABLE              
CSHAV90  ZAP   PKAVAIL,PSSRCVDB      ADD RECEIVED AMOUNT                        
         SP    PKAVAIL,PSSDISBB      RCVD-DISBURSED                             
         ZAP   SVPKAMT,PKAVAIL       CASH POSITION                              
*                                                                               
         SP    PKAVAIL,PSSAPPRB       AVAILABLE TO APPROVE                      
         BNP   CSHAV130               DON'T HAVE ENOUGH                         
         TM    FLAG1,FLGSTAT          DO WE WANT TO TURN ON STATUS              
         BNO   *+8                                                              
         OI    MTHFLAG,PSSMAN         MANUAL APPROVAL NEEDED                    
         CP    PKAVAIL,PSSUNAPB                                                 
         BNL   CSHAV110               ZERO OR MORE AUTO APPROVE IT              
*                                                                               
         ZAP   PL16,PSSUNAPB          GET UNAPPROVED AMT.                       
         CP    AUTPCNT,=P'0'          0 % THRESHOLD, APPROVE ALL ?              
         BNH   CSHAV100                                                         
         MP    PL16,AUTPCNT           PERCENT FROM PROFILE/REQUEST              
         SRP   PL16,3,0                                                         
         DP    PL16,=PL4'10000'                                                 
         SRP   PL16(L'PL16-4),64-3,5 % ROUNDED TO 2 DP                          
         ZAP   PKAMNT,PL16(L'PL16-4)  THIS AMT CAN BE USED FOR APPROVAL         
         CP    PKAVAIL,PKAMNT                                                   
         BL    CSHAV130               ZERO OR MORE AUTO APPROVE IT              
CSHAV100 TM    FLAG1,FLGSTAT          DO WE WANT TO TURN ON STATUS              
         BNO   *+8                                                              
         OI    MTHFLAG,PSSPCNT        APPLYING PERCENT FOR APPROVING            
*                                                                               
CSHAV110 TM    FLAG1,FLGSTAT          DO WE WANT TO TURN ON STATUS              
         BNO   *+8                                                              
         OI    MTHFLAG,PSSCSH         MARK CASH AVAILABLE                       
         SP    PKAVAIL,PSSUNAPB       LEFT OVER, THIS IS EXTRA CASH.            
*                                                                               
         TM    FLAG1,FLGSTAT          DO WE WANT TO TURN ON STATUS              
         BNO   CSHAV130                                                         
         TM    PSSFLAG,PSSPREV                                                  
         BNO   CSHAV120                                                         
         NI    MTHFLAG,X'FF'-PSSCSH   DON'T AUTO APPROVE IF OLD ITEM            
         OI    MTHFLAG,PSSMAN         MAKE IT MANUAL APPROVAL NEEDED            
         B     CSHAV130                                                         
CSHAV120 NI    MTHFLAG,X'FF'-PSSMAN                                             
*                                                                               
CSHAV130 MVC   PSSAWRK,0(R2)          MOVE TO WORK AREA FROM TEMP               
         BRAS  RE,ZAPSSBUK            ZAP ALL BUCKETS                           
         TM    FLAG1,FLGSTAT                                                    
         BNO   CSHAV160                                                         
*                                                                               
         LA    RF,RQMNTHS          # OF REQUESTED MONTHS + 1                    
         LA    R4,MNTHS                                                         
CSHAV140 CLI   AUTOPT6,C'Y'        APPROVING MY MOS?                            
         BE    CSHAV142                                                         
         CLI   MTHCODE,X'00'       COMPARE WITH 00 ENTRY IF APPR BY EST         
         BE    CSHAV150                                                         
         B     CSHAV145                                                         
CSHAV142 CLC   MTHCODE,PSSKMOS     VALID MONTH                                  
         BE    CSHAV150                                                         
CSHAV145 LA    R4,MTHLEN(R4)                                                    
         BCT   RF,CSHAV140                                                      
         DC    H'0'                                                             
*                                                                               
CSHAV150 OC    PSSAWRK+(PSSFLAG-PSSAD)(1),MTHFLAG   TURN ON STATUSES            
CSHAV160 ZAP   PSSAWRK+(PSSCASHB-PSSAD)(L'PSSCASHB),SVPKAMT                     
         ZAP   PSSAWRK+(PSSAVLB-PSSAD)(L'PSSAVLB),PKAVAIL                       
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2   UPDATE RECORD                         
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 ATSAR,DMCB,(RC),TSARDH,2   HIGH FOR RECD JUST WRITTEN            
CSHAV170 DS    0H                                                               
         GOTO1 ATSAR,DMCB,(RC),TSANXT,2   GET RECORD IN PSSAWRK                 
         TM    BCTSERRS,TSEEOF     IS THIS END OF FILE                          
         BNO   CSHAV10                                                          
*                                                                               
CSHAVX   J     EXIT                                                             
         DROP  R2,R4                                                            
         EJECT                                                                  
**********************************************************************          
* LITERALS FOR CASHAVL  ROUTINE                                      *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
*&&                                                                             
**********************************************************************          
* RESET BLOCK ROUTINE                                                *          
**********************************************************************          
         SPACE 1                                                                
RESETBLK NTR1  BASE=*,LABEL=*                                                   
         L     R0,AAUTBLK          RESTORE CALLER'S BLOCK                       
         LHI   R1,AUTBLKL                                                       
         LA    RE,AUTBLK                                                        
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
RESETX   J     EXIT                                                             
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* ROUTINE VALIDATES CLIENT IN THE CLIENT LIST                        *          
**********************************************************************          
         SPACE 1                                                                
CHKCLT   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         GOTO1 ACLIST,DMCB,AUTALST,CLNT                                         
         CLI   DMCB,0                                                           
         BNE   *+6                                                              
         DC    H'0'                BAD LIST RECORD                              
         CLI   DMCB,C'I'           INCLUDE THIS CLIENT?                         
         XIT1                                                                   
*                                                                               
         EJECT                                                                  
**********************************************************************          
* LITERALS CHKCLT                                                    *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* ROUTINE ZAPS ALL BUCKETS OF PSSATAB BINTABLE                       *          
**********************************************************************          
         SPACE 1                                                                
         USING PSSAD,R2                                                         
ZAPSSBUK NTR1  BASE=*,LABEL=*                                                   
         LA    R0,PSSABKCT         NO. OF BUCKETS                               
         LA    R2,PSSAWRK          POINT TO FIRST BUCKET                        
         LA    R1,PSSABKT                                                       
         ZAP   0(PSSABKLN,R1),=P'0'     CLEAR FIELDS                            
         LA    R1,PSSABKLN(R1)                                                  
         BCT   R0,*-10             NUMBER OF BUCKETS TO LOOP                    
         J     EXIT                                                             
         DROP  R2                                                               
*                                                                               
         EJECT                                                                  
**********************************************************************          
* LITERALS ZAPSSBUK                                                  *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* ROUTINE ZAPS ALL BUCKETS OF TSRTAB BINTABLE                        *          
**********************************************************************          
         SPACE 1                                                                
         USING TSRAD,R2                                                         
ZAPSRBUK NTR1  BASE=*,LABEL=*                                                   
         LA    R0,TSRABKCT         NO. OF BUCKETS                               
         LA    R2,TSRAWRK          POINT TO FIRST BUCKET                        
         LA    R1,TSRABKT                                                       
         ZAP   0(TSRABKLN,R1),=P'0'     CLEAR FIELDS                            
         LA    R1,TSRABKLN(R1)                                                  
         BCT   R0,*-10             NUMBER OF BUCKETS TO LOOP                    
         J     EXIT                                                             
         DROP  R2                                                               
*                                                                               
         EJECT                                                                  
**********************************************************************          
* LITERALS FOR ZAPSRBUK ROUTINE                                      *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* MARK THOSE ITEMS IN THE TABLE THAT ARE COMPLETELY DISBURSED        *          
**********************************************************************          
         SPACE 1                                                                
         USING MTHD,R4                                                          
FIXTAB   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         XC    PSSAWRK,PSSAWRK                                                  
         NI    FLAG1,X'FF'-PSSDISB                                              
*                                                                               
         USING PSSAD,R2                                                         
         LA    R2,PSSAWRK                                                       
         XC    PSSAWRK,PSSAWRK                                                  
         GOTO1 ATSAR,DMCB,(RC),TSARDH,2   GET RECORD IN PSSAWRK                 
         TM    BCTSERRS,TSEEOF     IS THIS END OF FILE                          
         BO    FIXTBX                                                           
FIXTB10  DS    0H                                                               
*                                                                               
         CLI   PSSKTYP,X'00'       IS THIS A GOOD ESTIMATE?                     
         BNE   FIXTBX              BAD ESTIMATES COMING UP,EXIT.                
*                                                                               
         CLI   PSSKRTYP,X'00'      IS THIS A TOTAL RECORD                       
         BNE   FIXTB20                                                          
         BRAS  RE,INMTHTB          INITIALIZE/RESET REQ MNTHS TABLE             
         B     FIXTB30                                                          
FIXTB20  CLI   PSSKRTYP,X'03'      IS THIS A TOTAL BY MOS RECORD                
         BE    FIXTB30                                                          
         CLI   AUTOPT8,C'Y'        SUPPRESS ALL DISBURSED FROM PRINTING         
         BNE   FIXTB70                                                          
         CLI   PSSKRTYP,X'05'      SS DETAILS                                   
         BE    FIXTB60                                                          
         B     FIXTB70                                                          
*                                                                               
FIXTB30  ZIC   R0,RQMNTHS          #OF MONTHS IN REQ + 1                        
         LA    R4,MNTHS            POINT TO MONTHS TABLE                        
FIXTB40  CLC   MTHCODE,PSSKMOS     DO WE HAVE MATCHING MONTHS                   
         BE    FIXTB50                                                          
         LA    R4,MTHLEN(R4)       BUMP TO NEXT MONTH                           
         BCT   R0,FIXTB40                                                       
         DC    H'0'                                                             
FIXTB50  XC    MTHFLAG,MTHFLAG                                                  
FIXTB60  CP    PSSUDISB,=P'0'      IS UNDISBURSED AMT ZERO                      
         BNZ   FIXTB110            GO READ NEXT                                 
         CLI   AUTOPT8,C'Y'                                                     
         BE    FIXTB100                                                         
         OI    MTHFLAG,PSSDISB     SKIP SET,THAT IS ONLY DISBURSED              
         B     FIXTB100                                                         
*                                                                               
FIXTB70  ZIC   R0,RQMNTHS          #OF MONTHS IN REQ + 1                        
         LA    R4,MNTHS            POINT TO MONTH TABLE                         
FIXTB80  CLC   MTHCODE,PSSKMOS                                                  
         BE    FIXTB90                                                          
         LA    R4,MTHLEN(R4)                                                    
         BCT   R0,FIXTB80                                                       
         DC    H'0'                                                             
FIXTB90  TM    MTHFLAG,PSSDISB     DOES THIS BELONG TO DISB ONLY SET            
         BNO   FIXTB110            NO, READ NEXT                                
FIXTB100 OI    PSSFLAG,PSSDISB     SKIP SET,THAT IS ONLY DISBURSED              
         BRAS  RE,ZAPSSBUK                                                      
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2   WRITE RECORD BACK                     
*        GOTO1 ATSAR,DMCB,(RC),TSAWRT,2   WRITE RECORD BACK                     
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 ATSAR,DMCB,(RC),TSARDH,2   HIGH FOR RECD JUST WRITTEN            
*                                                                               
FIXTB110 DS    0H                                                               
         GOTO1 ATSAR,DMCB,(RC),TSANXT,2   GET RECORD IN PSSAWRK                 
         TM    BCTSERRS,TSEEOF     IS THIS END OF FILE                          
         BNO   FIXTB10                                                          
*                                                                               
FIXTBX   DS    0H                                                               
*                                                                               
         J     EXIT                                                             
         DROP  R2,R4                                                            
         EJECT                                                                  
**********************************************************************          
* LITERALS FOR FIXTAB   ROUTINE                                      *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* ROUTINE TO INITIALIZE FLAG IN MONTH TABLE                          *          
**********************************************************************          
         SPACE 1                                                                
         USING MTHD,R3                                                          
INMTHTB  NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         LA    R3,MNTHS                                                         
         SR    R0,R0                                                            
         IC    R0,RQMNTHS                                                       
INMTH10  XC    MTHFLAG,MTHFLAG                                                  
         LA    R3,MTHLEN(R3)                                                    
         BCT   R0,INMTH10                                                       
         J     EXIT                                                             
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* GET SYSTEM IN SVSYS                                                 *         
* ON ENTRY   SVSYS=MDTSYS/MDPSYS/MBISYS                               *         
*            SVMED=MDTMED/MDPMED/MBIMED                               *         
* ON EXIT    SYSYS=CORRECT SYSTEM BASED ON SUBMEDIA FOR NET           *         
***********************************************************************         
         SPACE 1                                                                
GETSYSTM NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         L     RF,APSYSTAB         TABLE OF SUB-SYSTEMS FOR PRINT               
GETSYS10 CLI   0(RF),X'FF'                                                      
         JE    GETSYS20                                                         
         CLC   SVSYS,0(RF)         IF MATCH ON SUB-SYS THAN THE SYSTEM          
         JNE   *+12                MUST BE PRINT                                
         MVI   SVSYS,C'P'                                                       
         J     GETSYSX                                                          
         LA    RF,1(RF)                                                         
         J     GETSYS10                                                         
*                                                                               
GETSYS20 CLI   SVSYS,C'P'          IF PRINT DON'T NEED TO CHK SUB-MEDIA         
         JE    GETSYSX                                                          
*                                                                               
         L     RF,ASMEDTAB         TABLE OF SUB-MEDIA'S FOR NET                 
GETSYS30 CLI   0(RF),X'FF'                                                      
         JE    GETSYSX                                                          
         CLC   SVMED,0(RF)         IF MATCH ON SUB-MED THAN THE SYSTEM          
         JNE   *+12                MUST BE NET                                  
         MVI   SVSYS,C'N'                                                       
         J     GETSYSX                                                          
         LA    RF,1(RF)                                                         
         J     GETSYS30                                                         
*                                                                               
GETSYSX  J     EXIT                                                             
**********************************************************************          
* LITERALS FOR GETSYSTM ROUTINE                                      *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* GET INSERTION DATE IN SVMOS                                         *         
* ON ENTRY   SVMED=MDTMED/MDPMED/MBIMED                               *         
*            SVMOS=MOS FROM MDTMOS/MDPMOS/MBIMOS                      *         
* ON EXIT    SVMOS=CALCULATED INSERTION DATE OR SVMOS                 *         
***********************************************************************         
         SPACE 1                                                                
GTINSRTN NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         CLI   SVSYS,C'P'          INSERTION DATE CALC FOR PRINT ONLY           
         BNE   GETINSX                                                          
*                                                                               
         L     R2,AINSTAB          POINT TO INSERTION DATE TABLE                
         USING INSTABD,R2                                                       
GETINS10 CLI   INSALPH,X'FF'                                                    
         BE    GETINSX                                                          
         CLC   INSALPH,AUTALPHA    COMPARE ON ALPHA ID                          
         BNE   GETINS50                                                         
*                                                                               
         LHI   R0,INSNMED          NUMBER OF MEDIAS TO CHECK.                   
         LA    R3,INSMED           POINT TO FIRST MEDIA                         
GETINS30 CLC   SVMED,0(R3)         IS THIS THE MEDIA WE WANT?                   
         BNE   GETINS40                                                         
*                                                                               
         MVI   SVMOS+2,X'01'       PUT DAY                                      
         GOTO1 DATCON,DMCB,(1,SVMOS),(0,WORK)                                   
         ZIC   R4,1(R3)            NUMBER OF MONTHS TO ADJUST                   
         LTR   R4,R4                                                            
         BZ    GETINSX             NO ADJUSTMENT NEEDED                         
*        LNR   R4,R4               LOAD NEGATIVE TO SUBTRACT MONTHS             
         GOTO1 VADDAY,DMCB,(C'M',WORK),WORK+6,(R4)                              
         GOTO1 DATCON,DMCB,(0,WORK+6),(1,SVMOS)                                 
         B     GETINSX                                                          
*                                                                               
GETINS40 LA    R3,L'INSMINI(R3)    CHECK NEXT MEDIA                             
         BCT   R0,GETINS30                                                      
         B     GETINSX             PRINT MEDIA CODE NOT IN TABLE                
*        DC    H'0'                PRINT MEDIA CODE NOT IN TABLE                
GETINS50 LA    R2,INSTBLNQ(R2)                                                  
         B     GETINS10                                                         
*                                                                               
GETINSX  J     EXIT                                                             
**********************************************************************          
* LITERALS FOR GTINSRTN  ROUTINE                                     *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* BUILD TABLE OF REQUESTED MONTHS                                     *         
***********************************************************************         
         SPACE 1                                                                
SETMNTH  DS    0D                                                               
         NMOD1 0,*SETMTH*                                                       
         L     RC,0(R1)                                                         
*                                                                               
         USING MTHD,R3             BUILD LIST OF PACKED MONTHS                  
         LA    R3,MNTHS            FROM MOS START TO MOS END                    
         XC    MNTHS,MNTHS                                                      
*                                                                               
         MVC   WORK(3),AUTSMOS                                                  
         GOTO1 DATCON,DMCB,(1,WORK),(0,WORK+6) WORK+6=C'YYMMDD'                 
         MVC   WORK(6),WORK+6                                                   
*                                                                               
         LA    R0,MAXMTHS-2                                                     
         LA    R5,1                                                             
         XC    MTHCODE,MTHCODE     SET FIRST MONTH 00 FOR EST TOTALS            
         STC   R5,MTHNUM                                                        
         LA    R5,1(R5)            ADD NUMBER OF REQUEST MONTHS                 
         LA    R3,MTHLEN(R3)       BUMP TO SECOND ENTRY                         
*                                                                               
         MVC   MTHCODE,AUTSMOS     SET FIRST MONTH                              
         STC   R5,MTHNUM                                                        
SETM10   CLC   MTHCODE,AUTEMOS                                                  
         BE    SETM20                                                           
         LA    R5,1(R5)            ADD NUMBER OF REQUEST MONTHS                 
         LA    R3,MTHLEN(R3)                                                    
         GOTO1 VADDAY,DMCB,(C'M',WORK),WORK+6,1   GET NEXT MONTH                
         GOTO1 DATCON,DMCB,(0,WORK+6),(1,WORK+12)                               
         MVC   MTHCODE,WORK+12     PACKED                                       
         XC    MTHFLAG,MTHFLAG                                                  
         STC   R5,MTHNUM                                                        
         MVC   WORK(4),WORK+6                                                   
         BCT   R0,SETM10                                                        
         CLC   MTHCODE,AUTEMOS                                                  
         BE    SETM20                                                           
         CLI   AUTOFSW,C'Y'                                                     
         BNE   *+6                                                              
         DC    H'0'                REQUEST MONTHS RANGE > MAXMNTHS              
         OI    AUTERR,AUTMRNG      SEND ERROR BACK TO MARKER                    
*                                                                               
SETM20   STC   R5,RQMNTHS                                                       
*                                                                               
SETMX    XIT1                                                                   
**********************************************************************          
* LITERALS SETMNTH NMOD1                                             *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* BUILD CLIENT/PRODUCT/ESTIMATE/MOS TABLE FROM VENDOR PASSIVE POINTER*          
**********************************************************************          
         SPACE 1                                                                
         USING AAVPASD,R4                                                       
SSPNTTB  DS    0D                                                               
         NMOD1 0,*SSPNTR*                                                       
         L     RC,0(R1)                                                         
*                                                                               
         XC    SVKEY,SVKEY         BUILD KEY TO READ SR RECDS                   
         XC    LSTVPKEY,LSTVPKEY                                                
         XC    LASTPKEY,LASTPKEY                                                
         NI    FLAG1,X'FF'-(FLGNTDIS+FLGRQMOS) INIT FLAG                        
*                                                                               
         LA    R4,SVKEY                                                         
         XC    AAVPKEY,AAVPKEY     INIT KEY                                     
*                                                                               
         MVI   AAVPTYP,AAVPTYPQ    AA VENDOR PASSIVE POINTER                    
         MVI   AAVPSUB,AAVPSUBQ    AA VENDOR PASSIVE POINTER SUB TYPE           
         MVC   AAVPCPY,AUTCPY      COMPANY                                      
         OC    AUTALST,AUTALST     DO WE HAVE A CLIENT LIST                     
         BNZ   SSPNT10                                                          
         MVC   AAVPCLT,AUTCLNT     3 BYTES CLIENT CODE                          
         MVC   AAVPPRD,AUTPROD     3 BYTES PRODUCT CODE                         
         MVC   AAVPEST,AUTEST      6 BYTES OF ESTIMATE                          
*                                                                               
SSPNT10  GOTO1 ADMRDHI,DMCB,(RC)                                                
         B     SSPNT30                                                          
*                                                                               
SSPNT20  GOTO1 ADMRDSEQ,DMCB,(RC)        READ NEXT POINTER                      
SSPNT30  CLC   SVKEY(AAVPCLT-AAVPKEY),IOKEY                                     
         BNE   SSPNTX                                                           
*                                                                               
         LA    R4,IO                                                            
         CLC   AUTEST,SPACES     WAS ESTIMATE IN REQUEST                        
         BNH   SSPNT40                                                          
         CLC   SVKEY+(AAVPEST-AAVPKEY)(3),AAVPEST                               
         BNE   SSPNT20                                                          
*                                                                               
SSPNT40  OC    AUTALST,AUTALST       ARE WE RUNNING OFF THE CLT LST?            
         BZ    SSPNT50                                                          
         MVC   CLNT,AAVPCLT                                                     
         BRAS  RE,CHKCLT             CHECK IF CLI IS VALID IN LIST              
         BNE   SSPNT20               READ NEXT POINTER                          
         B     SSPNT70                                                          
*                                                                               
SSPNT50  CLC   AUTPROD,SPACES        WAS PRODUCT IN REQUEST                     
         BNH   SSPNT60                                                          
         CLC   SVKEY+(AAVPPRD-AAVPKEY)(3),AAVPPRD                               
         BNE   SSPNTX                                                           
*                                                                               
SSPNT60  CLC   AUTCLNT,SPACES        WAS CLIENT REQUESTED                       
         BNH   SSPNT70                                                          
         CLC   SVKEY+(AAVPCLT-AAVPKEY)(3),AAVPCLT                               
         BNE   SSPNTX                                                           
*                                                                               
SSPNT70  DS    0H                                                               
         CLC   AUTLDGR,SPACES                                                   
         BNH   SSPNT80                                                          
         CLC   AUTLDGR,AAVPACCT    DO WE HAVE A LEDGER MATCH                    
         BNE   SSPNT20                                                          
SSPNT80  DS    0H                                                               
         CLC   AUTSYSTM,SPACES                                                  
         BNH   SSPNT85                                                          
         CLC   AUTSYSTM,AAVPSYS    DO WE HAVE A SYSTEM MATCH                    
         BNE   SSPNT20                                                          
*                                                                               
SSPNT85  L     RF,ALDSYTAB         CHECK TABLE OF LEDGER/SYSTEM B/C             
SSPNT90  CLI   0(RF),X'FF'         ONLY WANT CERTAIN LEDGERS                    
         BE    SSPNT20             READ NEXT POINTER                            
         CLC   AAVPACCT(1),0(RF)                                                
         BE    SSPNT100                                                         
         LA    RF,2(RF)                                                         
         B     SSPNT90                                                          
*                                                                               
SSPNT100 DS    0H                  ** SKIP FULLY DISBURSED ESTIMATES **         
         CLC   LSTVPKEY(AAVPMOS-AAVPKEY),IOKEY  HAS ESTIMATE CHANGED?           
         BE    SSPNT110                                                         
         MVC   LSTVPKEY,IOKEY                                                   
         NI    FLAG1,X'FF'-FLGNTDIS                                             
         B     SSPNT120                                                         
*                                                                               
SSPNT110 TM    FLAG1,FLGNTDIS      DID WE FIND ANY NON DISBURSED EST?           
         BO    SSPNT130                                                         
SSPNT120 OC    AAVPUSED,AAVPUSED   HAS THIS BEEN DISBURSED                      
         BNZ   SSPNT20                                                          
         OI    FLAG1,FLGNTDIS      NOT FULLY DISBURSED ESTIMATE                 
         MVC   SVKEY,LSTVPKEY                                                   
         B     SSPNT10             GO READ AGAIN FOR THIS ESTIMATE              
*                                                                               
SSPNT130 DS    0H        ** SKIP ESTIMATE IF NO DETAILS ARE FOUND ***           
         CLC   LASTPKEY(AAVPMOS-AAVPKEY),IOKEY  HAS ESTIMATE CHANGED?           
         BE    SSPNT140                                                         
         MVC   LASTPKEY,IOKEY                                                   
         NI    FLAG1,X'FF'-FLGRQMOS                                             
         B     SSPNT150                                                         
*                                                                               
SSPNT140 TM    FLAG1,FLGRQMOS      DID WE FIND ANY REQUESTED MOS?               
         BO    SSPNT160                                                         
SSPNT150 CLC   AAVPMOS,AUTSMOS                                                  
         BL    SSPNT20                                                          
         CLC   AAVPMOS,AUTEMOS                                                  
         BH    SSPNT20                                                          
         OI    FLAG1,FLGRQMOS      WE HAVE POINTER WITH REQESTED MOS            
         MVC   SVKEY,LASTPKEY                                                   
         B     SSPNT10             GO READ AGAIN FOR THIS ESTIMATE              
*                                                                               
         USING PSSAD,R3                                                         
SSPNT160 LA    R3,PSSAWRK                                                       
         XC    PSSAWRK,PSSAWRK                                                  
         BRAS  RE,ZAPSSBUK         ZAP ALL BUCKETS                              
*                                                                               
         MVC   PSSKTYP,AAVPFLG1    BAD,GOOD OR NO ESTIMATE                      
         MVC   PSSKSYS,AAVPSYS     MOVE SYSTEM TO TABLE                         
*        MVC   SVSYS,AAVPSYS       SAVE OFF SYSTEM CODE                         
         MVC   PSSKCLI,AAVPCLT     CLT                                          
         MVC   PSSKPRO,AAVPPRD     PRD                                          
         MVC   PSSKEST,AAVPEST     ESTIMATE                                     
         MVC   PSSKMOS,AAVPMOS     MOS                                          
         MVC   PSSKOFF,AAVPOFF     OFFICE                                       
         MVI   PSSKSTN,C'S'        SS ACC IS STATION SP IS PUBLICATION          
         MVC   PSSKSTN+1(L'AAVPACCT),AAVPACCT SS ACCOUNT                        
*                                                                               
         CLI   PSSKSYS,C'N'        IS THIS NET SYSTEM ?                         
         BNE   *+12                                                             
         CLI   AUTPRF2,C'S'        RUN BY SYSTEM OR SYS/MED ?                   
         BE    *+10                                                             
         MVC   PSSKMED,AAVPACCT+1  MOVE IN MEDIA CODE TO TABLE                  
*                                                                               
         MVC   PSSKDA,AAVPKDA      SAVE DISK ADDRESS IN KEY OF TABLE TO         
*                                  EXPAND SS TRANSACTIONS                       
         GOTO1 ADMGET,DMCB,(RC),0 GET RECORD                                    
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING TRNRECD,R2                                                       
         LA    R2,IO                                                            
         LR    R5,R2                                                            
         AH    R5,=Y(TRNRFST-TRNKEY)                                            
SSPNT170 CLI   0(R5),0                                                          
         BE    SSPNT240            NO INVOICE AND EST                           
         CLI   0(R5),TRNELQ        IS IT X'44' ELEMENT                          
         BE    SSPNT190                                                         
         CLI   0(R5),XPYELQ        IS IT X'46' ELEMENT                          
         BE    SSPNT230                                                         
*                                                                               
SSPNT180 SR    R1,R1                                                            
         IC    R1,1(R5)            GET LENGTH OF ELEMENT                        
         AR    R5,R1                                                            
         B     SSPNT170                                                         
*                                                                               
         USING TRNELD,R5                                                        
SSPNT190 ZAP   PSSCLRB,TRNAMNT     CLEARED AMOUNT                               
*                                                                               
         TM    TRNRSTA2,TRNSUSED   HAS IT BEEN USED ?                           
         BNO   SSPNT200                                                         
         ZAP   PSSDISBB,TRNAMNT    CREDIT AND USED DATE (DISBURSED)             
         MVI   PSSKSTAT,PSSKDISB   DISBURSED ITEM                               
         B     SSPNT180                                                         
*                                                                               
SSPNT200 ZAP   PSSUDISB,TRNAMNT    ALL OF UNDISBURSED AMOUNT                    
*                                                                               
         TM    TRNSTAT,TRNSHOLD     IS THIS PAYMENT ON HOLD                     
         BNO   SSPNT210                                                         
         ZAP   PSSHELDB,TRNAMNT    HELD AMOUNT                                  
         MVI   PSSKSTAT,PSSKHELD   ADD TO HELD ITEM LINE                        
         B     SSPNT180                                                         
*                                                                               
SSPNT210 TM    TRNSTAT,TRNSAPPR    SELECTED FOR PAYMENT                         
         BNO   SSPNT220                                                         
         ZAP   PSSAPPRB,TRNAMNT    APPROVED AMOUNT                              
         MVI   PSSKSTAT,PSSKPREV   ADD TO PREVIOUSLY APPROVED ITEM              
         B     SSPNT180                                                         
*                                                                               
SSPNT220 ZAP   PSSUNAPB,TRNAMNT    UNAPPROVED AMOUNT                            
         MVI   PSSKSTAT,PSSKUNAP   ADD TO UNAPPROVED LINE                       
         B     SSPNT180                                                         
         DROP  R5                                                               
*                                                                               
         USING XPYELD,R5                                                        
SSPNT230 DS    0H                                                               
         MVC   PSSKINV,XPYINV      MOVE INVOICE NUMBER TO TABLE                 
         CLI   PSSKSYS,C'N'                                                     
         BNE   SSPNT240                                                         
         CLC   XPYSMED,SPACES      IF NO MEDIA/SUBMEDIA THEN LEAVE              
         BNH   SSPNT240            MEDIA AS 'N' FOR NOW                         
         CLI   AUTPRF2,C'S'        RUN BY SYSTEM OR SYS/MED ?                   
         BE    *+10                                                             
         MVC   PSSKMED,XPYSMED     MOVE IN MEDIA FOR SYSTEM C'N' NETWRK         
         DROP  R5                                                               
*                                                                               
SSPNT240 DS    0H                                                               
         CLI   PSSKSYS,C'N'        IS THIS NET SYSTEM ?                         
         BNE   *+12                                                             
         CLI   AUTPRF2,C'S'        RUN BY SYSTEM OR SYS/MED ?                   
         BE    SSPNT245                                                         
         CLC   AUTMEDIA,SPACES                                                  
         BNH   SSPNT245                                                         
         CLC   AUTMEDIA,PSSKMED   DOES MEDIA MATCH WITH REQUEST                 
         BNE   SSPNT20            GET NEXT POINTER                              
*                                                                               
SSPNT245 CLC   PSSKMOS,AUTSMOS    IS MOS <  START DATE                          
         BL    SSPNT280           ADD TO PRIOR ENTRY AND TOTALS                 
         CLC   PSSKMOS,AUTEMOS    IS MOS > END DATE                             
         BH    SSPNT300                                                         
*                                                                               
         MVI   PSSKRTYP,X'05'     DETAIL RECORD BY MOS                          
         CLI   PSSKSTAT,PSSKUNAP                                                
         BE    SSPNT250                                                         
         CLI   AUTOFSW,C'Y'        ARE WE RUNNING OFFLINE                       
         BE    *+12                                                             
         CLI   PSSKSTAT,PSSKDISB   DON'T ADD DISBURSED DETAILS IF               
         BE    SSPNT270           CALLED FROM MARKER (TO SAVE SPACE)            
         CLI   AUTOPT2,C'Y'       DO WE WANT TO EXPAND                          
         BE    SSPNT250           YES                                           
         XC    PSSKDA,PSSKDA      DON'T EXPAND,LEAVE IT UNIQUE                  
SSPNT250 GOTO1 ATSAR,DMCB,(RC),TSAADD,2                                         
         CLI   BCTSERRS,0                                                       
         BE    *+14                                                             
         CLI   AUTOFSW,C'Y'                                                     
         JNE   ROUTH               EXIT WITH CC SET IF ONLINE                   
         DC    H'0'                DIE IF RUNNING OFFLINE                       
*                                                                               
         CLI   AUTOPT2,C'Y'        SHOW PREV APPR & UNAPPR AS 2 LINES           
         BE    SSPNT270                                                         
         CLI   PSSKSTAT,PSSKPREV   PREVIOUSLY APPROVED TO TOTAL                 
         BE    SSPNT260                                                         
         CLI   PSSKSTAT,PSSKUNAP   UNAPPROVED TO TOTALS                         
         BNE   SSPNT270                                                         
SSPNT260 LA    RE,PSSAWRK+(PSSKSTAT-PSSAKEY) CLEAR STATUS ONWARDS.              
         XC    0(PSSKLNQ2,RE),0(RE)                                             
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2                                         
         CLI   BCTSERRS,0                                                       
         BE    *+14                                                             
         CLI   AUTOFSW,C'Y'                                                     
         JNE   ROUTH               EXIT WITH CC SET IF ONLINE                   
         DC    H'0'                DIE IF RUNNING OFFLINE                       
*                                                                               
SSPNT270 DS    0H                                                               
         LA    RE,PSSAWRK+(PSSKSTN-PSSAKEY) CLEAR STATUS ONWARDS.               
         XC    0(PSSKLNQ3,RE),0(RE)                                             
         MVI   PSSKRTYP,X'03'     MOS TOTAL                                     
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2                                         
         CLI   BCTSERRS,0                                                       
         BE    SSPNT310                                                         
         CLI   AUTOFSW,C'Y'                                                     
         JNE   ROUTH               EXIT WITH CC SET IF ONLINE                   
         DC    H'0'                DIE IF RUNNING OFFLINE                       
*                                                                               
SSPNT280 DS    0H                                                               
         CP    PSSUNAPB,=P'0'                                                   
         BZ    SSPNT290                                                         
         OI    PSSFLAG,PSSPREV    UNAPPROVED ITEM MORE THAN A YEAR OLD          
*                                                                               
SSPNT290 LA    RE,PSSAWRK+(PSSKRTYP-PSSAKEY) CLEAR REC TYPE ONWARDS.            
         XC    0(PSSKLNQ1,RE),0(RE)                                             
         MVI   PSSKRTYP,X'01'      PRIOR MOS'S TOTALS                           
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2                                         
         CLI   BCTSERRS,0                                                       
         BE    SSPNT310                                                         
         CLI   AUTOFSW,C'Y'                                                     
         JNE   ROUTH               EXIT WITH CC SET IF ONLINE                   
         DC    H'0'                DIE IF RUNNING OFFLINE                       
*                                                                               
SSPNT300 LA    RE,PSSAWRK+(PSSKRTYP-PSSAKEY) CLEAR REC TYPE ONWARDS.            
         XC    0(PSSKLNQ1,RE),0(RE)                                             
         MVI   PSSKRTYP,X'04'      FUTURE MOS'S TOTALS                          
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2                                         
         CLI   BCTSERRS,0                                                       
         BE    *+14                                                             
         CLI   AUTOFSW,C'Y'                                                     
         JNE   ROUTH               EXIT WITH CC SET IF ONLINE                   
         DC    H'0'                DIE IF RUNNING OFFLINE                       
*                                                                               
         CLI   AUTOPT6,C'Y'        APPROVING BY MOS?                            
         BE    SSPNT310                                                         
         CP    PSSUNAPB,=P'0'     IS THIS UNAPPROVED ITEM                       
         BNZ   SSPNT20            EXCLUDE POST REQUEST UNAPPROVED ITEMS         
*                                                                               
SSPNT310 DS    0H                                                               
         LA    RE,PSSAWRK+(PSSKRTYP-PSSAKEY) CLEAR REC TYPE ONWARDS.            
         XC    0(PSSKLNQ1,RE),0(RE)                                             
         MVI   PSSKRTYP,X'00'      ALL TOTALS                                   
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2                                         
         CLI   BCTSERRS,0                                                       
         BE    SSPNT20                                                          
         CLI   AUTOFSW,C'Y'                                                     
         JNE   ROUTH               EXIT WITH CC SET IF ONLINE                   
         DC    H'0'                DIE IF RUNNING OFFLINE                       
*                                                                               
SSPNTX   DS    0H                                                               
         XIT1                                                                   
         DROP  R2,R3,R4                                                         
         EJECT                                                                  
**********************************************************************          
* LITERALS FOR SSPNTTB NMOD                                          *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ADD ITEM TO BINSRCH TABLE AND ACCUMULATE TOTALS                     *         
*  P1    A(ITEM TO BE ADDED)                                          *         
*  P2    A(TABLE)                                                     *         
***********************************************************************         
         SPACE 1                                                                
         USING BIND,R5                                                          
BINADD   DS    0D                                                               
         NMOD1 0,**BINA**                                                       
         L     RC,0(R1)                                                         
*                                                                               
         LA    RF,*+10             HIGH CORE TABLE                              
         O     RF,=X'80000000'                                                  
         BSM   0,RF                31 BIT MODE                                  
*                                                                               
         L     R5,8(R1)                                                         
*                                                                               
         L     R2,12(R1)                                                        
         STC   R2,BYTE                                                          
*                                                                               
         MVC   DMCB+8(16),BININ    NUMBER LENGTH,KEY,MAX                        
         LA    R6,BINTAB           A(TABLE)                                     
         L     R3,4(R1)            A(ITEM)                                      
*                                                                               
         LA    R2,DMCB                                                          
         ST    R3,0(R2)            BINSRCH PARAMETER 1                          
         ST    R6,4(R2)            BINSRCH PARAMETER 2                          
         LA    R2,DMCB+12          BINSRCH PARAMETER 4                          
         CLI   BYTE,X'01'          DO WE WANT TO SEARCH ON EXACT MATCH          
         BE    BINA10                                                           
         CLI   BYTE,X'00'          DO WE WANT TO INSERT IF NOT FOUND            
         BE    BINA20                                                           
BINA10   MVI   0(R2),X'00'         BYTE 0 PARAMETER 1 LOOK FOR EX MTCH          
         NI    FLAG,X'FF'-FLGNTFND RECORD WAS FOUND IF NOT SET                  
         B     BINA40                                                           
BINA20   MVI   0(R2),X'01'         BYTE 0 PARAMETER 1 INSERT                    
BINA40   GOTO1 BINSRC31,DMCB                                                    
         OC    DMCB(4),DMCB                                                     
         BNZ   *+6                                                              
         DC    H'0'                TABLE IS FULL                                
         MVC   BININ,DMCB+8        UPDATE COUNT                                 
*                                                                               
         TM    DMCB,X'80'          RECORD WAS ADDED/NOT FOUND                   
         BNO   BINA50                                                           
         CLI   BYTE,X'01'          ARE WE LOOKING FOR EXACT MATCH               
         BNE   BINXIT              NO, ADDING RECORD, EXIT                      
         OI    FLAG,FLGNTFND       RECORD NOT FOUND                             
         B     BINXIT                                                           
*                                                                               
BINA50   LR    R2,R3               SAVE OFF A(ITEM)                             
*                                                                               
         L     R4,DMCB             A(RECORD FOUND)                              
         SR    R0,R0                                                            
         ICM   R0,1,BINNUM         NUMBER OF BUCKETS                            
         BZ    BINXIT              NO BUCKETS - EXIT                            
         SR    R6,R6                                                            
         IC    R6,BINFST           DISPLACEMENT TO FIRST BUCKET                 
         AR    R4,R6               BUMP TO FIRST BUCKET IN TABLE                
         AR    R3,R6               BUMP TO FIRST BUCKET IN NEW ITEM             
BINA60   AP    0(PSSABKLN,R4),0(PSSABKLN,R3)   ADD TO BUCKET                    
         LA    R3,PSSABKLN(R3)    BUMP TO NEXT ENTRY IN NEW ITEM                
         LA    R4,PSSABKLN(R4)    BUMP TO NEXT ENTRY IN TABLE                   
         BCT   R0,BINA60                                                        
         TM    FLAG,FLGSR         ARE WE BUILDING SR TABLE                      
         BO    BINXIT                                                           
*                                                                               
         L     R4,DMCB             A(RECORD FOUND)                              
         LR    RE,R2               A(NEW ITEM)                                  
         LHI   R6,PSSFLAG-PSSAD    DISPLACEMENT TO FLAG                         
         AR    R4,R6               BUMP TO FLAG IN TABLE                        
         AR    RE,R6               BUMP TO FLAG IN NEW ITEM                     
*                                                                               
         CLI   BYTE,X'01'          ARE WE UPDATING SR BUCKETS IN SSTAB          
         BNE   BINA70                                                           
         TM    0(R4),PSSDISB       IS IT DISBURSED ONLY ITEM                    
         BNO   BINA70                                                           
         OI    FLAG1,PSSDISB                                                    
*                                                                               
BINA70   OC    0(PSSFLNQ,R4),0(RE)   UPDATE ALL FLAGS                           
*                                                                               
BINXIT   XIT1                                                                   
         DROP  R5                                                               
         EJECT                                                                  
**********************************************************************          
* LITERALS  BINADD NMOD1                                             *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* TSAR INTERFACE ROUTINE - TSAR USES AIO1                             *         
*                                                                     *         
* NTRY - P1 = (RC)                                                    *         
*      - P2 = TSAR ACTION (DEFINED IN DDTSARD)                        *         
*        P3 = TSAR BUFFER (1 OR 2)                                    *         
*                                                                     *         
* EXIT - CC=EQU - ACTION COMPLETED WITHOUT ERRORS                     *         
*        CC=NEQ - BCTSERRS=TSAR ERROR CODE                            *         
***********************************************************************         
         SPACE 1                                                                
MTSAR    DS    0D                                                               
         NMOD1 0,**MTSAR*                                                       
         L     RC,0(R1)            P1=A(LOCAL WORKING)                          
         STCM  R1,15,BCFULL        SAVE  ORIGINAL PARM LIST                     
         MVC   BCFLAG1,7(R1)       P2=TSAR ACTION                               
         MVC   BCTSINDS,11(R1)     P3=TSAR BUFFER                               
*                                                                               
MTSAR01  NI    TSARSTAT,X'FF'-TSARBUF2 ALWAYS SHUT OFF BIT ON ENTRY             
*                                                                               
         USING TSARD,R3                                                         
         LA    R3,TSRBLK1                                                       
         LA    R1,TSRAWRK          RECEIVABLE TABLE WORK AREA                   
         CLI   BCTSINDS,2          ARE WE READING/WRITING TO BUFF2              
         BNE   MTSAR02                                                          
         LA    R3,TSRBLK2          IF SO USE BLOCK2                             
         OI    TSARSTAT,TSARBUF2   MARK THAT 2ND BUFFER IS IN USE               
         LA    R1,PSSAWRK          PAYABLE TABLE WORK AREA                      
*                                                                               
MTSAR02  DS    0H                                                               
         CLI   AUTOFSW,C'Y'                                                     
         BE    MBUFFRIN                                                         
*                                                                               
         CLI   BCFLAG1,TSAINI                                                   
         BNE   *+10                                                             
         XC    0(L'TSRBLK1,R3),0(R3) CLEAR BLOCK                                
         ST    R1,TSAREC           ADDRESS OF RECORD                            
         MVC   TSACOM,AUTCOMF                                                   
         MVC   TSACTN,BCFLAG1                                                   
*                                                                               
         CLI   BCFLAG1,TSAINI      *** TSAR INITIALIZE ***                      
         BNE   MTSAR10                                                          
*                                  ***TSAR PARAMETERS**                         
         MVI   TSKEYL,TSRKLNQ        KEY LENGTH                                 
         LHI   R1,TSRALNQ                                                       
         CLI   BCTSINDS,2            ARE WE READING/WRITING TO BUFF2            
         BNE   MTSAR03                                                          
         MVI   TSKEYL,PSSAKLNQ       KEY LENGTH                                 
         LHI   R1,PSSALNQ                                                       
MTSAR03  STH   R1,TSRECL             MAX RECORD LENGTH                          
         XC    TSRNUM,TSRNUM         RECORD NUMBER                              
*                                                                               
*                                  ***ONLINE TSAR PARAMETERS****                
         MVI   TSPAGN,TSPEXPN      SET NUMBER OF TEMPEST PAGES                  
         MVI   TSINDS,TSIALLOC+TSIRTNAF                                         
*                                                                               
         CLI   BCTSINDS,2          ARE WE INTERFACING W/ BUFF2                  
         BE    MTSAR06                                                          
*                                  MARKER INITIALIZES TSAR BUFFERS              
*        TM    TSRMODE,TSRMGOT1    DID WE ALREADY INITIALIZE TSAR BUFF1         
*        BNO   MTSARGO                                                          
         LA    R1,TSRFLDS1         POINT TO FLDS FOR BLK 1                      
         B     MTSAR08                                                          
*                                                                               
MTSAR06  OI    TSIND2,TSI2BUF2     USING TSAR BUFFER 2                          
         TM    TSRMODE,TSRMGOT2    DID WE ALREADY INITIALIZE TSAR BUFF2         
         BNO   MTSARGO                                                          
         LA    R1,TSRFLDS2         POINT TO FLDS FOR BLK 2                      
*                                                                               
         USING TSRTSAR1,R1         USE 1ST BLKS FLDS AS DSECT                   
MTSAR08  DS    0H                                                               
         MVC   TSINDS,TSRTSAR1     SET PREVIOUS INDICATORS                      
         OI    TSINDS,TSIREUSE     SET TO REUSE PREVIOUS ALLOC                  
         MVC   TSPAGL,TSRLOWP1     SET PREV LOW PAGE NUMBER                     
         MVC   TSPAGN,TSRNUMP1     SET PREV NUMBER OF PAGES ALLOC               
         B     MTSARGO                                                          
         DROP  R1                                                               
*                                                                               
MTSAR10  CLI   BCFLAG1,TSARES      *** TSAR RESTORE ***                         
         BNE   MTSAR20                                                          
*                                                                               
         LA    R1,TSRFLDS1         POINT TO FLDS FOR BLK 1                      
         CLI   BCTSINDS,2          ARE WE READING/WRITING TO BUFF2              
         BNE   *+8                                                              
         LA    R1,TSRFLDS2         POINT TO FLDS FOR BLK 2                      
         USING TSRTSAR1,R1         USE 1ST BLKS FLDS AS DSECT                   
         MVC   TSINDS,TSRTSAR1     SET PREVIOUS INDICATORS                      
         OI    TSINDS,TSIREUSE     SET TO REUSE PREVIOUS ALLOC                  
         MVC   TSPAGL,TSRLOWP1     SET PREV LOW PAGE NUMBER                     
         MVC   TSPAGN,TSRNUMP1     SET PREV NUMBER OF PAGES ALLOC               
         B     MTSARGO                                                          
         DROP  R1                                                               
*                                                                               
MTSAR20  CLI   BCFLAG1,TSARDH      *** TSAR READ HIGH ***                       
         BNE   MTSAR22                                                          
         XC    TSRNUM,TSRNUM                                                    
         B     MTSARGO                                                          
*                                                                               
MTSAR22  CLI   BCFLAG1,TSAGET      *** TSAR GET ***                             
         BNE   MTSAR30                                                          
         DC    H'0'                USE TSARDH INSTEAD OF GET.                   
*                                  BECAUSE BUFFERIN CAN'T HANDLE                
*                                                                               
MTSAR30  CLI   BCFLAG1,TSADEL      *** TSAR DELETE ***                          
         BNE   MTSARGO                                                          
         XC    TSRNUM,TSRNUM                                                    
*                                                                               
MTSARGO  DS    0H                                                               
         GOTO1 XTSAR,TSARD                                                      
         MVC   BCTSERRS,TSERRS     PASS BACK ERRORS                             
         TM    TSERRS,TSEDUP       DUPLICATE KEY ON ADD                         
         BO    MTSARG30                                                         
         CLI   TSERRS,0                                                         
         JNE   ROUTH               EXIT WITH ERROR                              
*                                                                               
         LA    R1,TSRFLDS1         POINT TO FLDS FOR BLK 1                      
         CLI   BCTSINDS,2          ARE WE READING/WRITING TO BUFF2              
         BE    *+12                                                             
         OI    TSRMODE,TSRMGOT1    SHOW THAT BUFF1 HAS BEEN INITIALIZED         
         B     *+12                                                             
         LA    R1,TSRFLDS2         POINT TO FLDS FOR BLK 2                      
         OI    TSRMODE,TSRMGOT2    SHOW THAT BUFF2 HAS BEEN INITIALIZED         
*                                                                               
         USING TSRTSAR1,R1         USE 1ST BLKS FLDS AS DSECT                   
         MVC   TSRLOWP1,TSPAGL     SAVE LOW TSAR PAGE NUMBER                    
         MVC   TSRNUMP1,TSPAGN     SAVE NUMBER OF PAGES ALLOCATED               
         MVC   TSRTSAR1,TSINDS     SAVE TEMPEST INDICATORS                      
         J     ROUTE                                                            
*                                                                               
MTSARG30 DS    0H                  UPDATE DUPL RECD FOUND DURING ADD            
         XC    TEMPWRK,TEMPWRK     CLEAR TEMPORARY WORK AREA                    
         LA    RE,TEMPWRK                                                       
         LA    RF,PSSAWRK                                                       
         LHI   R1,L'PSSAWRK                                                     
         CLI   BCTSINDS,1                                                       
         BNE   *+12                                                             
         LA    RF,TSRAWRK                                                       
         LHI   R1,L'TSRAWRK                                                     
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RE),0(RF)       SAVE OFF WORK AREA TO TEMP FOR UPD           
         MVI   TSACTN,TSARDH       ONLINE TSAR ACTION FIELD                     
*                                                                               
         GOTO1 XTSAR,TSARD         READ FROM AND INTO PSSAWRK/TSRAWRK           
         MVC   BCTSERRS,TSERRS     PASS BACK ERRORS                             
         CLI   TSERRS,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R2,TEMPWRK          A(ITEM) TO BE ADDED                          
         L     R4,TSAREC           A(RECORD FOUND)                              
         USING BIND,R5                                                          
         L     R5,APSSATAB                                                      
         CLI   BCTSINDS,1                                                       
         BNE   *+8                                                              
         L     R5,ATSRTAB                                                       
         SR    R0,R0                                                            
         ICM   R0,1,BINNUM         NUMBER OF BUCKETS                            
         BZ    MTSARX              NO BUCKETS - EXIT                            
         SR    R6,R6                                                            
         IC    R6,BINFST           DISPLACEMENT TO FIRST BUCKET                 
         AR    R4,R6               BUMP TO FIRST BUCKET IN TSAREC               
         AR    R2,R6               BUMP TO FIRST BUCKET IN NEW ITEM             
MTSARG40 AP    0(PSSABKLN,R4),0(PSSABKLN,R2)   ADD TO BUCKET                    
         LA    R2,PSSABKLN(R2)    BUMP TO NEXT ENTRY IN NEW ITEM                
         LA    R4,PSSABKLN(R4)    BUMP TO NEXT ENTRY IN TABLE                   
         BCT   R0,MTSARG40                                                      
         TM    FLAG,FLGSR         ARE WE BUILDING SR TABLE                      
         BO    MTSARG60           WRITE RECORD BACK                             
*                                                                               
         L     R4,TSAREC           A(RECORD FOUND) PSSAWRK                      
         LA    R2,TEMPWRK          A(NEW ITEM)                                  
         LHI   R6,PSSFLAG-PSSAD    DISPLACEMENT TO FLAG                         
         AR    R4,R6               BUMP TO FLAG IN TABLE                        
         AR    R2,R6               BUMP TO FLAG IN NEW ITEM                     
MTSARG50 OC    0(PSSFLNQ,R4),0(R2)   UPDATE ALL FLAGS                           
*                                                                               
MTSARG60 DS    0H                                                               
         MVI   TSACTN,TSAWRT       ONLINE TSAR ACTION FIELD                     
*                                                                               
         GOTO1 XTSAR,TSARD         WRITE RECORD BACK  PSSAWRK/TSRAWRK           
         MVC   BCTSERRS,TSERRS     PASS BACK ERRORS                             
         CLI   TSERRS,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLI   BCTSINDS,1                                                       
         BNE   *+14                                                             
         MVC   TSRAWRK,TEMPWRK                                                  
         B     *+10                                                             
         MVC   PSSAWRK,TEMPWRK                                                  
         J     MTSARX                                                           
*                                                                               
MBUFFRIN DS    0H                                                               
*        LA    RF,PAYBUF                                                        
         LA    RF,RCVBUF                                                        
         STCM  RF,15,AUTPBUFF      SAVE ADDRESS OF THESE PARMS                  
         CLI   BCTSINDS,1                                                       
         BNE   BUFFER02                                                         
         LA    RF,RCVBUF                                                        
*                                                                               
BUFFER02 ST    R1,BUFFREC          SET A(INPUT RECORD)                          
         MVC   TSARKSAV,0(R1)      SAVE CURRENT RECORD KEY                      
         LR    R3,RF                                                            
         USING BUFFD,R3            R3=A(BUFFERIN CONTROL BLOCK)                 
         LHI   R0,BUFFAINI         CONVERT TSAR ACTION TO BUFFERIN              
         CLI   BCFLAG1,TSAINI                                                   
         JE    BUFFER08                                                         
         LHI   R0,BUFFAPUT                                                      
         CLI   BCFLAG1,TSAADD                                                   
         JE    BUFFER08                                                         
         CLI   BCFLAG1,TSAWRT                                                   
         JE    BUFFER08                                                         
         CLI   BCFLAG1,TSAPUT                                                   
         JE    BUFFER08                                                         
         LHI   R0,BUFFASEQ                                                      
         CLI   BCFLAG1,TSANXT                                                   
         JE    BUFFER08                                                         
         LHI   R0,BUFFARDH                                                      
         CLI   BCFLAG1,TSARDH                                                   
         JE    BUFFER08                                                         
         DC    H'0'                                                             
                                                                                
BUFFER08 CHI   R0,BUFFAINI         TEST INITIALIZATION CALL                     
         JNE   BUFFER10                                                         
         USING BIND,R5                                                          
         L     R5,APSSATAB         POINT TO PAYABLES TABLE                      
         CLI   BCTSINDS,2                                                       
         BE    *+8                                                              
         L     R5,ATSRTAB          POINT TO RECEIVABLE TABLE                    
         SR    RE,RE                                                            
         ICM   RE,7,BINKEY         KEY LENGTH                                   
         SR    RF,RF                                                            
         IC    RF,BINFST           RECORD LENGTH=RECLEN-BUCKETS                 
*        L     RF,BINLEN           RECORD LENGTH                                
         SR    RF,RE               DATA LENGTH                                  
         STCM  RE,3,BUFFLKEY       SET KEY LENGTH                               
         STCM  RF,3,BUFFLCOM       SET COMMENT LENGTH                           
         MVC   BUFFNCOL,BINNUM     NUMBER OF BUCKETS/COLUMNS                    
*                                                                               
BUFFER10 GOTOR AUTBFRIN,DMCB,((R0),BUFFD),BUFFREC,AUTCOMF                       
         MVC   BUFFRET,BUFFERRS-BUFFPARM(R1)                                    
         CHI   R0,BUFFARDH         EMULATE TSAR NOT FOUND ON READ HIGH          
         JNE   BUFFER12                                                         
         L     RF,BUFFREC                                                       
         LH    R1,BUFFLKEY                                                      
         BCTR  R1,0                                                             
         BASR  RE,0                                                             
         EX    R1,8(RE)                                                         
         JE    BUFFER12                                                         
         CLC   TSARKSAV(0),0(RF)                                                
         OI    BUFFRET,BUFFERNF                                                 
                                                                                
BUFFER12 MVC   BCTSERRS,BUFFRET    PASS BACK ERRORS                             
         CLI   BUFFRET,0           SET CONDITION CODE FOR CALLER                
         J     EXIT                                                             
*                                                                               
MTSARX   J     ROUTE                                                            
         DROP  R1,R3,R5                                                         
***********************************************************************         
* EXIT POINTS                                                         *         
***********************************************************************         
         SPACE 1                                                                
ROUTL    MVI   BCDUB,0             SET CC LOW                                   
         J     ROUTCC                                                           
ROUTH    MVI   BCDUB,2             SET CC HIGH                                  
         J     ROUTCC                                                           
ROUTE    MVI   BCDUB,1             SET CC EQUAL                                 
ROUTCC   CLI   BCDUB,1                                                          
ROUTX    XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* TABLES AND CONSTANTS                                               *          
**********************************************************************          
GLOBALS  DS    0D                                                               
*                                                                               
ACCMST   DC    CL8'ACCMST'                                                      
PKROUND  DC    XL2'010C'                                                        
         DS    0F                                                               
DMCB     DS    0XL24               PARAMETER LIST                               
P1       DC    F'0'                                                             
P2       DC    F'0'                                                             
P3       DC    F'0'                                                             
P4       DC    F'0'                                                             
P5       DC    F'0'                                                             
P6       DC    F'0'                                                             
         DC    XL8'00'             N/D                                          
SPACES   DC    CL132' '                                                         
WORK     DC    XL64'00'                                                         
DUB      DC    D'0'                                                             
BYTE     DC    XL1'00'                                                          
RCCOMPFL DC    C' '                                                             
DMREAD   DC    C'DMREAD  '                                                      
DMRDHI   DC    C'DMRDHI  '                                                      
DMRSEQ   DC    C'DMRSEQ  '                                                      
DMADD    DC    C'DMADD   '                                                      
DMWRT    DC    C'DMWRT   '                                                      
DMGET    DC    C'GETREC  '                                                      
DMWORK   DC    XL96'00'                                                         
*                                                                               
RCVBUFQ  EQU   1                   ** RECEIVABLE BUFFER **                      
RCVBUF   BUFFD TYPE=P,KEYLEN=0,COMLEN=0,COLUMNS=0,BUFFERS=100,         +        
               REPCOM=YES                                                       
*                                                                               
PAYBUFQ  EQU   2                   ** PAYABLE BUFFER **                         
PAYBUF   BUFFD TYPE=P,KEYLEN=0,COMLEN=0,COLUMNS=0,BUFFERS=250,         +        
               REPCOM=YES                                                       
         SPACE 2                                                                
**********************************************************************          
* RELOCATACBLES                                                      *          
**********************************************************************          
         SPACE 1                                                                
ADCONS   DS    0F                                                               
         DC    A(SETMNTH)          SET REQUESTED MONTHS TABLE                   
         DC    A(BINADD)           ROUTINE TO ADD TO BINSEARCH TABLE            
         DC    A(TSRTAB)           SR TRANSACTIONS TABLE                        
         DC    A(PSSATAB)          SS TRANSACTIONS TABLE                        
         DC    A(SSPNTTB)          BUILD VENDOR TABLE USING POINTERS            
         DC    A(MAINTAB)          MAIN TABLE                                   
         DC    A(LDSYTAB)          LEDGER SYSTEM TABLE                          
         DC    A(SMEDTAB)          SYSTEM MEDIA TABLE                           
         DC    A(PSYSTAB)          PRINT SYSTEM TABLE                           
         DC    A(INSTAB)           INSERTION DATE CALCULATION TABLE             
         DC    A(MTSAR)            TSAR MAINTENANCE ROUTINE                     
         DC    A(DMSEQDR)          READ SEQUENTIAL                              
         DC    A(DMHIGHDR)         READ HIGH                                    
         DC    A(DMREADDR)         READ                                         
         DC    A(DMGETREC)         GET RECORD                                   
         DC    A(DMWRTDR)          WRITE RECORD                                 
         DC    A(DMPUTREC)         PUT RECORD                                   
         DC    V(PRNTBL)           PRINT DATA                                   
         DC    V(BINSRCH)          BINSRCH (31 BIT MODE)                        
         DC    V(HELLO)            HELLO                                        
         DC    V(ACLIST)           CLIENT LIST                                  
         DC    V(DATCON)           DATE CONTROLLER                              
         DC    V(GETBROAD)         BROADCAST DATE / MOS                         
         DC    V(GETDAY)           GETDAY                                       
         DC    V(ADDAY)            ADD DAY                                      
         DC    V(MASTC)                                                         
         DC    V(DATAMGR)          DATA MANAGER                                 
ADCONN   EQU   (*-ADCONS)/L'ADCONS                                              
         SPACE 2                                                                
         EJECT                                                                  
***********************************************************************         
* GETEL                                                               *         
***********************************************************************         
         SPACE 1                                                                
         GETEL R5,56,ELCODE                                                     
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* DATAMGR INTERFACE                                                  *          
**********************************************************************          
         SPACE 1                                                                
DMSEQDR  NMOD1 0,SEQ               READ SEQUENTIAL                              
         L     RC,0(R1)            RESET RC                                     
         GOTO1 DATAMGR,DMCB,(X'88',DMRSEQ),=C'ACCDIR ',SVKEY,IO,0               
         B     DMX                                                              
*                                                                               
DMHIGHDR NMOD1 0,HIGH              READ HIGH                                    
         L     RC,0(R1)            RESET RC                                     
         GOTO1 DATAMGR,DMCB,(X'88',DMRDHI),=C'ACCDIR ',SVKEY,IO,0               
         B     DMX                                                              
*                                                                               
DMREADDR NMOD1 0,READ              READ                                         
         L     RC,0(R1)            RESET RC                                     
         GOTO1 DATAMGR,DMCB,(X'88',DMREAD),=C'ACCDIR ',SVKEY,IO,0               
         B     DMX                                                              
*                                                                               
DMGETREC NMOD1 0,GREC              GET RECORD                                   
         L     RC,0(R1)            RESET RC                                     
         L     R2,4(R1)                                                         
         LTR   R2,R2               DO WE HAVE DISK ADDRESS                      
         BNZ   DMGETR10            YES                                          
         USING ACCRECD,R3                                                       
         LA    R3,IO                                                            
         LA    R2,ACCKDA                                                        
DMGETR10 GOTO1 DATAMGR,DMCB,DMGET,=C'ACCMST ',(R2),IO,DMWORK                    
         B     DMX                                                              
*                                                                               
DMWRTDR  NMOD1 0,WRT               WRITE BACK TO DIR                            
         L     RC,0(R1)            RESET RC                                     
         GOTO1 DATAMGR,DMCB,(X'00',=C'DMWRT'),=C'ACCDIR',IOKEY,IOKEY            
         B     DMX                                                              
*                                                                               
DMPUTREC NMOD1 0,PREC              PUT RECORD                                   
         L     RC,0(R1)            RESET RC                                     
         GOTO1 DATAMGR,DMCB,=CL8'PUTREC',=C'ACCMST',ACCKDA,IO,DMWORK            
*                                                                               
DMX      XIT1                                                                   
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* TABLES                                                             *          
**********************************************************************          
*                                                                               
* BINTABLE 1 - SR TRANSACTION TABLE BUILT IN PROC TRANSACTION                   
*                                                                               
MAINTAB  DS    0F                                                               
         DC    C'**TSRACCT*'                                                    
TSRTAB   DS    0C                                                               
         DC    AL4(0)                  NUMBER IN TABLE                          
         DC    AL4(TSRALNQ)            LENGTH OF ENTRY                          
         DC    AL1(0)                  DISP. TO KEY                             
         DC    AL3(TSRKLNQ)            KEY LENGTH                               
         DC    AL4(TSRAMAX)            MAX IN TABLE                             
         DC    AL1(TSRABKCT)           NUMBER OF BUCKETS                        
         DC    AL1(TSRABKT-TSRAD)      DISPLACEMENT TO FIRST BUCKET             
         DC    AL4(TSRASIZE)                                                    
*                                                                               
TSRAMAX  EQU   50000                                                            
         SPACE 1                                                                
*                                                                               
* BINTABLE 2 - SS ACCOUNT TABLE BUILT IN ACCOUNT LAST                           
*                                                                               
         DC    C'**PSSACCT*'                                                    
PSSATAB  DS    0C                                                               
         DC    AL4(0)                  NUMBER IN TABLE                          
         DC    AL4(PSSALNQ)            LENGTH OF ENTRY                          
         DC    AL1(0)                  DISP. TO KEY                             
         DC    AL3(PSSAKLNQ)           KEY LENGTH                               
         DC    AL4(PSSAMAX)            MAX IN TABLE                             
         DC    AL1(PSSABKCT)           NUMBER OF BUCKETS                        
         DC    AL1(PSSABKT-PSSAD)      DISPLACEMENT TO FIRST BUCKET             
         DC    AL4(PSSASIZE)                                                    
*                                                                               
         SPACE 1                                                                
TSRASIZE EQU   (TSRAMAX*TSRALNQ)                                                
LENBUFF1 EQU   TSRASIZE                                                         
LENBUFF  EQU   LENBUFF1+LENBUFF2                                                
*---------------------------------------------------------------------*         
*                                  TABLE OF LEDGER AND SYSTEM         *         
*---------------------------------------------------------------------*         
LDSYTAB  DC    C'SS'               LEDGER S - SYS SPOT (BUT MAYBE NET)          
         DC    C'TS'               LEDGER T - SYS SPOT                          
         DC    C'PP'               LEDGER P - SYS PRINT                         
         DC    C'QP'               LEDGER Q - SYS PRINT                         
         DC    C'UN'               LEDGER U - SYS NET                           
         DC    X'FF'                                                            
*---------------------------------------------------------------------*         
*                                  TABLE OF NET SUB-MEDIA'S           *         
*---------------------------------------------------------------------*         
SMEDTAB  DC    C'NCDSOHV'          TABLE OF SUB-MEDIA'S                         
         DC    X'FF'                                                            
*---------------------------------------------------------------------*         
*                                   TABLE OF PRINT SYSTEMS                      
PSYSTAB  DC    C'I'                 INTERACTIVE                                 
         DC    C'L'                 SOCIAL                                      
         DC    C'B'                 MOBILE                                      
         DC    C'V'                 NATIONAL VIDEO                              
         DC    C'W'                 LOCAL VIDEO                                 
         DC    C'D'                 DIGITAL AUDIO                               
         DC    X'FF'                                                            
*---------------------------------------------------------------------*         
*                                  INSERTION DATE TABLE               *         
*---------------------------------------------------------------------*         
INSTAB   DS    0H                                                               
         DC    C'1M'                                                            
         DC    C'M',X'02'          MEDIA MAGAZINE                               
         DC    C'N',X'00'          MEDIA NETWORK                                
         DC    C'T',X'01'          MEDIA TRADE                                  
         DC    C'I',X'01'          MEDIA INTERACTIVE                            
         DC    C'S',X'01'          MEDIA SUPPLEMENTAL                           
         DC    C'O',X'01'          MEDIA OUTDOOR                                
*                                                                               
         DC    C'B$'               IMDB#2669161                                 
         DC    C'M',X'02'          MEDIA MAGAZINE                               
         DC    C'N',X'01'          MEDIA NETWORK                                
         DC    C'T',X'02'          MEDIA TRADE                                  
         DC    C'I',X'01'          MEDIA INTERACTIVE                            
         DC    C'S',X'01'          MEDIA SUPPLEMENTAL                           
         DC    C'O',X'01'          MEDIA OUTDOOR                                
*                                                                               
         DC    C'WI'                                                            
         DC    C'M',X'00'          MEDIA MAGAZINE                               
         DC    C'N',X'00'          MEDIA NETWORK                                
         DC    C'T',X'00'          MEDIA TRADE                                  
         DC    C'I',X'00'          MEDIA INTERACTIVE                            
         DC    C'S',X'00'          MEDIA SUPPLEMENTAL                           
         DC    C'O',X'01'          MEDIA OUTDOOR                                
*                                                                               
         DC    C'NE'                                                            
         DC    C'M',X'01'          MEDIA MAGAZINE                               
         DC    C'N',X'00'          MEDIA NETWORK                                
         DC    C'T',X'01'          MEDIA TRADE                                  
         DC    C'I',X'00'          MEDIA INTERACTIVE                            
         DC    C'S',X'00'          MEDIA SUPPLEMENTAL                           
         DC    C'O',X'00'          MEDIA OUTDOOR                                
*                                                                               
         DC    C'D9'               CALL LOG CL#0155232N                         
         DC    C'M',X'01'          MEDIA MAGAZINE                               
         DC    C'N',X'00'          MEDIA NETWORK                                
         DC    C'T',X'01'          MEDIA TRADE                                  
         DC    C'I',X'00'          MEDIA INTERACTIVE                            
         DC    C'S',X'00'          MEDIA SUPPLEMENTAL                           
         DC    C'O',X'00'          MEDIA OUTDOOR                                
*                                                                               
         DC    C'FM'               CALL LOG CL#0174472N                         
         DC    C'M',X'01'          MEDIA MAGAZINE                               
         DC    C'N',X'00'          MEDIA NETWORK                                
         DC    C'T',X'00'          MEDIA TRADE                                  
         DC    C'I',X'00'          MEDIA INTERACTIVE                            
         DC    C'S',X'00'          MEDIA SUPPLEMENTAL                           
         DC    C'O',X'00'          MEDIA OUTDOOR                                
*                                                                               
         DC    X'FF'                                                            
*                                                                               
*                                                                               
*                                                                               
*                                                                               
* FOR PRINT, MAGAZINE AND TRADE UP THE MOS BY ONE                               
         USING TSRAD,R2                                                         
FIXPMOS  CLI   TSRKSYS,C'P'                                                     
         BNER  RE                                                               
FIXPMOS1 NTR1  BASE=*,LABEL=*                                                   
         CLI   TSRKMED,C'M'        MAGAZINE                                     
         BE    *+12                                                             
         CLI   TSRKMED,C'T'        TRADE                                        
         JNE   EXIT                                                             
*                                                                               
         MVI   SVMOS+2,X'01'                                                    
         GOTO1 DATCON,DMCB,(1,SVMOS),(0,WORK)                                   
         GOTO1 VADDAY,DMCB,(C'M',WORK),WORK+6,1                                 
         GOTO1 DATCON,DMCB,(0,WORK+6),(1,SVMOS)                                 
         J     EXIT                                                             
         LTORG                                                                  
*                                                                               
*                                                                               
*                                                                               
**********************************************************************          
* WORKING STORAGE                                                    *          
**********************************************************************          
*                                                                               
AUTOD    DSECT                                                                  
VTYPES   DS    0A                                                               
ASETMNTH DS    A                   REQUESTED MONTHS TABLE                       
ABINADD  DS    A                   ROUTINE TO ADD TO BINSEARCH TABLE            
ATSRTAB  DS    A                   SS TRANSACTION TABLE                         
APSSATAB DS    A                   SS ACC TABLE BUILT FROM FILE                 
ASSPNTTB DS    A                   BUILD VENDOR TABLE USING POINTERS            
AMAINTAB DS    A                   ADDRESS OF MAIN TABLE                        
ALDSYTAB DS    A                   LEDGER SYSTEM TABLE                          
ASMEDTAB DS    A                   SYSTEM MEDIA TABLE                           
APSYSTAB DS    A                   PRINT SYSTEM TABLE                           
AINSTAB  DS    A                   INSETION DATE CALCULATION TABLE              
ATSAR    DS    A                   ADDRESS OF TSAR MAINTENANCE ROUTINE          
ADMRDSEQ DS    A                   READ SEQUENTIAL                              
ADMRDHI  DS    A                   READ HIGH                                    
ADMREAD  DS    A                   READ                                         
ADMGET   DS    A                   GET RECORD                                   
ADMWRITE DS    A                   WRITE RECORD                                 
ADMPUT   DS    A                   PUT RECORD                                   
APRNTBL  DS    V                   PRINT DATA                                   
BINSRC31 DS    V                   BINSRCH (31 BIT MODE)                        
VHELLO   DS    V                                                                
ACLIST   DS    V                                                                
DATCON   DS    V                                                                
VGETBRD  DS    V                                                                
VGETDAY  DS    V                                                                
VADDAY   DS    V                                                                
ADMASTC  DS    V                                                                
DATAMGR  DS    V                                                                
VTYPLNQ  EQU   *-VTYPES                                                         
*                                                                               
AELMNTS  DS    0A                                                               
AGDAELD  DS    A                   X'E5'                                        
AMBIELD  DS    A                   X'F3'                                        
AOTHELD  DS    A                   X'23'                                        
AXPYELD  DS    A                   X'46'                                        
AMDTELD  DS    A                   X'1A'                                        
AMDPELD  DS    A                   X'6A'                                        
AELMNTSQ EQU   *-AELMNTS                                                        
*                                                                               
ABASE    DS    A                                                                
RELO     DS    A                                                                
SAVERD   DS    A                                                                
AAUTBLK  DS    A                   ADDRESS OF AUTO BLOCK PARAMETER              
CALLRD   DS    F                                                                
*                                                                               
RQMNTHS  DS    XL1                 NUMBER OF MONTHS IN REQUEST                  
MNTHS    DS    CL(MTHLEN*MAXMTHS)  START THRU END YYMM (PACKED)                 
*                                                                               
SVTRNAMT DS    PL8                 SAVED TRANSACTION AMOUNT                     
SVCOMAMT DS    PL8                 SAVED COMMISSION AMOUNT                      
SVPKAMT  DS    PL8                 SAVED AMOUNT                                 
PKAMNT   DS    PL8                                                              
PKAVAIL  DS    PL8                 AVAILABLE AMNT FOR UNAPPROVED ITEMS          
PL16     DS    PL16                                                             
TOTREFDR DS    PL8                                                              
TOTREFCR DS    PL8                                                              
TOTREFCM DS    PL8                                                              
CRFOUND  DS    C                                                                
SVPCNT   DS    PL8                                                              
SVSYS    DS    CL1                 SAVED SYSTEM CODE                            
SVMED    DS    CL1                 SAVED MEDIA CODE                             
SVCLI    DS    CL3                 SAVED CLIENT CODE                            
DSKADDR  DS    XL4                 DISK ADDRESS OF CURRENT TRANSACTION          
SVBILTYP DS    C                                                                
SVCLNET  DS    XL4                                                              
*                                                                               
BCFLAG1  DS    XL1                                                              
BCFULL   DS    F                                                                
BCDUB    DS    D                                                                
BCTSINDS DS    XL1                 TSAR BUFFER INDICATOR                        
BCTSAR1  EQU   1                   - USING TSAR BUFFER 1                        
BCTSAR2  EQU   2                   - USING TSAR BUFFER 2                        
BCTSERRS DS    CL1                 TEMP TSAR ERRORS                             
BCTNUM   DS    XL4                                                              
*                                                                               
*&&DO                                                                           
PROFILES DS    0XL16                                                            
PROF1    DS    XL1                                                              
PROF2    DS    XL1                                                              
PROF3    DS    XL1                                                              
PROF4    DS    XL1                                                              
PROF5    DS    XL1                                                              
PROF6    DS    XL1                                                              
PROF7    DS    XL1                                                              
PROF8    DS    XL1                                                              
PROF9    DS    XL1                                                              
PROF10   DS    XL1                                                              
PROF11   DS    XL1                                                              
PROF12   DS    XL1                                                              
PROF13   DS    XL1                                                              
PROF14   DS    XL1                                                              
PROF15   DS    XL1                                                              
PROF16   DS    XL1                                                              
*&&                                                                             
*                                                                               
MSG      DS    CL10                DUMP MESSAGE                                 
ELCODE   DS    CL1                                                              
ELEM     DS    CL255                                                            
*                                                                               
FLAG     DS    XL1                                                              
FLGDR    EQU   X'80'               DEBIT TRANSACTION                            
FLGDATE  EQU   X'40'               FOUND MOS                                    
FLGBRD   EQU   X'20'               GO TO GET BROADCAST DATE                     
FLGSR    EQU   X'04'                                                            
FLGNTFND EQU   X'02'               RECORD WAS FOUND                             
FLGOFFST EQU   X'01'               OFFSET                                       
*                                                                               
FLAG1    DS    XL1                                                              
FLGSTAT  EQU   X'80'               CASH IS AVAILABLE FOR AUTO APPRVL            
FLGNTDIS EQU   X'40'               NON DISBURSED ESTIMATES                      
FLGRQMOS EQU   X'20'               PASSIVE MOS IS WITHIN REQUESTED MOS          
*                                                                               
SKIPCHK  DS    C                   CHECK WHETHER TO SKIP TRN SET                
*                                                                               
SVKEY    DS    CL42                                                             
LSTVPKEY DS    CL42                LAST VENDOR PASSIVE KEY                      
LASTPKEY DS    CL42                LAST VENDOR PASSIVE KEY                      
*                                                                               
LSTSRKEY DS    CL42                                                             
SVSRPKEY DS    CL42                                                             
CURRMED  DS    CL1                 CURRENT MEDIA WE ARE WORKING WITH.           
CLNT     DS    CL3                 CLIENT                                       
*                                                                               
SVMOS    DS    XL3                                                              
SCKDAT   DS    XL3                                                              
DTE1     DS    CL6                 YYMMDD (EBCDIC)                              
DTE2     DS    CL6                 YYMMDD (EBCDIC)                              
START    DS    XL3                                                              
END      DS    XL3                                                              
TSRAWRK  DS    CL(TSRALNQ)         BINSEARCH WORK AREA - SR TRNS TABLE          
TSRAWRK2 DS    CL(TSRALNQ)         BINSEARCH WORK AREA2- SR TRNS TABLE          
TSRALAST DS    CL(TSRKLNQ)         LAST TSAR RECORD READ IN                     
PSSAWRK  DS    CL(PSSALNQ)         BINSEARCH WORK AREA - SS TABLE               
SVPSSWRK DS    CL(L'PSSAWRK)       SAVED PAYABLES WORK AREA                     
TEMPWRK  DS    CL255               TSAR WORK AREA                               
BUFFREC  DS    A                   BUFFER S/R A(RECORD)                         
BUFFRET  DS    X                   BUFFER S/R RETURN INDICATORS                 
TSARKSAV DS    XL150               TSAR RECORD KEY SAVE AREA                    
*                                                                               
IO       DS    0CL2042                                                          
IOKEY    DS    CL42                KEY                                          
IODATA   DS    CL2000              DATA                                         
IOLNQ    EQU   *-IO                LENGTH                                       
*                                                                               
* ACAUTOAPD                                                                     
       ++INCLUDE ACAUTOAPD                                                      
*                                                                               
AUTOL    EQU   *-AUTOD                                                          
         EJECT                                                                  
**********************************************************************          
* DSECT TO COVER MONTH TABLE BUILT AT REQUEST FIRST                  *          
**********************************************************************          
         SPACE 1                                                                
MTHD     DSECT                                                                  
MTHCODE  DS    0XL2                YYMM                                         
MTHYEAR  DS    XL1                 YY                                           
MTHMNTH  DS    XL1                 MM                                           
MTHNUM   DS    XL1                 MONTH NUMBER                                 
MTHFLAG  DS    XL1                 PSSCASH, PSSMAN, PSSDISB ETC                 
MTHLEN   EQU   *-MTHD                                                           
MAXMTHS  EQU   13                  12 + 1 FOR ESTIMATES TOTAL                   
*                                                                               
**********************************************************************          
* DSECT TO COVER INSERTION DATE CALCULATION TABLE                    *          
**********************************************************************          
         SPACE 1                                                                
INSTABD  DSECT                                                                  
INSALPH  DS    CL2                 AGENCY ALPHA                                 
INSLEN   EQU   *-INSTABD                                                        
*                                                                               
INSMINI  DS    0CL2                MINI ELEMENT: MEDIA AND ADJUSTMENT           
INSMED   DS    CL1                 MEDIA: M,N,T,I,S,O                           
INSADJM  DS    XL1                 NUMBER OF MONTHS TO ADJUST                   
INSMLN   EQU   (*-INSMINI)*INSNMED NUMBER OF MEDIAS TO LOOP                     
*                                                                               
INSTBLNQ EQU   INSMLN+INSLEN       LENGTH OF TABLE                              
INSNMED  EQU   6                    6 MEDIAS                                    
*                                                                               
***********************************************************************         
* DSECT FOR ENTRY IN SS TRANSACTION TABLE                             *         
***********************************************************************         
*                                                                               
TSRAD    DSECT                                                                  
TSRKEY   DS    0X                                                               
*                                                                               
TSRKSYS  DS    CL1                 SYSTEM SPOT/NET/PRINT                        
TSRKMED  DS    CL1                 MEDIA FOR SYSTEM ABOVE                       
TSRKCLI  DS    CL3                 CLIENT                                       
*                                                                               
TSRKBNO  DS    CL6             *** SR REF/BILL NO. ***                          
TSRKACT  DS    CL12                ACCOUNT NUMBER                               
*                                                                               
TSRKMOS  DS    CL2                 TRANSACTION MOS PACKED (YYMM)                
TSRKBDA  DS    CL3                 BILL DATE                                    
*                                                                               
TSRKCDA  DS    XL3                 CHECK DATE                                   
*                                                                               
TSRKTYP  DS    XL1                 RECORD TYPE                                  
TTDRQ    EQU   0                   DEBIT                                        
TTCRQ    EQU   1                   CREDIT                                       
TTDRREFQ EQU   2                   DR/CR TOTALS BY REF#                         
TTREPQ   EQU   3                   REPORT LINE - TOTALLED BY                    
*                                  BILL DATES AND CHECK DATES                   
TTACCTQ  EQU   4                   ACCOUNT-REF#, FOR DUPLICATE CHECKS           
*                                                                               
TSRFLAG  DS    XL1                 FLAG IN KEY PART WILL MAKE SURE THAT         
*                                  CR IS ALWAYS AFTER DR IN TABLE               
TSRFLGCR EQU   X'80'               CREDIT TRANSACTION                           
TSRFLGNC EQU   X'40'               DEBIT WITH NO MATCHING CREDIT                
*                                                                               
TSRKLNQ  EQU   *-TSRAD             LENGTH OF KEY                                
*                                                                               
TSRKEND  DS    0X                  DUMMY FIELD USED FOR LENGTH                  
*                                                                               
TSRABKT  DS    0PL8                BUCKET                                       
*                                                                               
TSRADR   DS    PL8                 DEBIT AMOUNT                                 
TSRABKLN EQU   *-TSRABKT           BUCKET LENGTH                                
TSRACR   DS    PL8                 CREDIT AMOUNT                                
TSRACOM  DS    PL8                 COMMISSION AMOUNT                            
*                                                                               
TSRABKCT EQU   (*-TSRABKT)/TSRABKLN NUMBER OF BUCKETS                           
TSRALNQ  EQU   *-TSRAD             LENGTH OF ENTRY                              
*                                                                               
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
*               ++INCLUDES                                            *         
***********************************************************************         
*                                                                               
* ACAUTOD                                                                       
       ++INCLUDE ACAUTOD                                                        
* ACGENFILE                                                                     
       ++INCLUDE ACGENFILE                                                      
* DDMASTD                                                                       
       ++INCLUDE DDMASTD                                                        
* DDCOMFACS                                                                     
       ++INCLUDE DDCOMFACS                                                      
* DDTSARD                                                                       
       ++INCLUDE DDTSARD                                                        
* DDBUFFD                                                                       
       ++INCLUDE DDBUFFD                                                        
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'007ACAUTOAC  07/09/18'                                      
         END                                                                    
