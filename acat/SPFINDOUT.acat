*          DATA SET SPFINDOUT  AT LEVEL 105 AS OF 04/28/14                      
*CATALP FINDOUT                                                                 
         TITLE 'FINDOUT - MODULE TO RETRIEVE RETAIL OUTLET DATA'                
         PRINT NOGEN                                                            
FINDOUT  CSECT                                                                  
         NMOD1 500,FINDOUT                                                      
         SPACE 2                                                                
         USING WORKD,RC                                                         
         ST    R1,SAVR1                                                         
         L     R3,0(R1)                                                         
         USING GDSECT,R3                                                        
         LA    R8,KEY                                                           
         USING ACKEYD,R8                                                        
         L     RF,GDACOMS                                                       
         MVC   DATAMGR,CDATAMGR-COMFACSD(RF)                                    
         MVC   DATCON,CDATCON-COMFACSD(RF)                                      
         L     RF,GDAUTL                                                        
         MVC   SAVSYS,4(RF)                                                     
         MVC   4(1,RF),GDACCSYS                                                 
*                                                                               
         CLI   GDACCOPN,C'Y'                                                    
         BE    FO4                                                              
*                                                                               
         MVI   GDACCOPN,C'Y'                                                    
         L     R4,GDATABS                                                       
*                                                                               
         LA    R5,ACCLIST                                                       
         GOTO1 DATAMGR,DMCB,=C'DTFADD',=C'ACCOUNT'                              
         L     RE,12(R1)           GET A(ACCOUNT DCB)                           
         TM    ISFTYPE-ISDTF(RE),ISFTEMU                                        
         BO    AOPEN15             ALREADY SET FOR EMULATION                    
         B     AOPEN20                                                          
*                                                                               
AOPEN15  LA    R5,EMULIST                                                       
*                                                                               
AOPEN20  GOTO1 DATAMGR,DMCB,=C'OPEN',=C'ACCOUNT',(R5),(R4)                      
*                                                                               
FO4      DS    0H                                                               
*                                                                               
         SPACE 3                                                                
*        BUILD SCHEME TABLE                                                     
         SPACE 2                                                                
BLDTAB   DS    0H                                                               
         OC    GDNSCHMS,GDNSCHMS                                                
         BNZ   BT20                HAVE ALREADY READ SCHEMES                    
         MVC   GDASCHMS,GDATABS                                                 
         SR    R6,R6                                                            
         L     R7,GDASCHMS                                                      
         USING GDSCHMD,R7                                                       
*                                  READ FOR SCHEMES                             
         MVI   KEY,C' '                                                         
         MVC   KEY+1(41),KEY                                                    
         MVI   KEY,X'0A'                                                        
         MVC   KEY+1(3),GDCOMP     COMP,UNIT,LEDGER                             
         BAS   RE,HIGHAC                                                        
         B     BT4A                                                             
BT4      DS    0H                                                               
         BAS   RE,SEQAC                                                         
BT4A     DS    0H                                                               
         CLC   KEY(4),KEYSAVE                                                   
         BNE   BT8                                                              
*                                                                               
         LA    R2,ACRECORD                                                      
         MVI   ELCODE,X'12'        ANALYSIS ELEM                                
         CLI   0(R2),X'12'                                                      
         BE    BT6                                                              
         BAS   RE,NEXTEL                                                        
         BNE   BT4                                                              
*                                                                               
BT6      DS    0H                                                               
         USING ACANALD,R2                                                       
         CLI   ACANMED,C' '        IF SCHEME HAS MEDIA                          
         BNH   *+14                                                             
         CLC   GDMED,ACANMED       MUST BE RIGHT ONE                            
         BNE   BT4                                                              
*        XC    GDSCHMD(GDSCHMDL),GDSCHMD                                        
         LA    R0,GDSCHMD          CLEAR GDSCHMD                                
         LHI   R1,GDSCHMDL                                                      
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         MVC   GDSCODE,ACANCODE         CODE                                    
         MVC   GDSNAME,ACANDESC         NAME                                    
         MVC   GDSSTAT,ACANSTAT         STATUS                                  
*                                                                               
         LA    R6,1(R6)                 BUMP SCHEME COUNT                       
         LA    R7,GDSCHMDL(R7)          NEXT TABLE ENTRY                        
         B     BT4                                                              
*                                                                               
BT8      DS    0H                                                               
         ST    R6,GDNSCHMS         SET NO. OF SCHEMES                           
         LTR   R6,R6                                                            
         BZ    EXIT                                                             
         SLL   R6,2                X 4                                          
         LA    R6,GDOUTDL(R6)      PLUS DSECT LEN =                             
         ST    R6,GDROWL           LENGTH OF OUTLET TABLE ENTRY                 
         MVI   0(R7),X'FF'                                                      
         LA    R7,1(R7)                                                         
         ST    R7,GDAOUTS          START OF OUTLET TABLE                        
         MVI   GDSAVKEY,C' '                                                    
         MVC   GDSAVKEY+1(41),GDSAVKEY                                          
         MVC   GDSAVKEY(3),GDCOMP                                               
         MVI   GDMGRNAM,C' '                                                    
         MVC   GDMGRNAM+1(71),GDMGRNAM                                          
*                                                                               
*                                  GET LEVEL NAMES                              
         MVI   KEY,C' '                                                         
         MVC   KEY+1(41),KEY                                                    
         MVC   KEY(3),GDCOMP                                                    
         BAS   RE,READAC                                                        
*                                                                               
         LA    R2,ACRECORD                                                      
         MVI   ELCODE,X'16'                                                     
         BAS   RE,NEXTEL                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING ACHEIRD,R2                                                       
*                                                                               
         MVI   GDMGRDSC,C' '                                                    
         MVC   GDMGRDSC+1(44),GDMGRDSC                                          
         LA    R6,ACHRLEVA                                                      
         CLC   GDMGRLEN,ACHRLEVA                                                
         BNH   *+8                                                              
         LA    R6,16(R6)                                                        
         CLI   GDMGRLEN,0          MGR                                          
         BE    *+14                                                             
         MVC   GDMGRDSC,1(R6)                                                   
         LA    R6,16(R6)                                                        
         CLI   GDMKTLEN,0          MKT                                          
         BE    *+14                                                             
         MVC   GDMKTDSC,1(R6)                                                   
         LA    R6,16(R6)                                                        
         MVC   GDOUTDSC,1(R6)      OUTLET                                       
*                                                                               
*                                                                               
BT14     DS    0H                                                               
*                                                                               
BT20     DS    0H                                                               
         CLI   GDOUTNO,0                                                        
         BNE   GETSHR                                                           
         CLI   GDSCHM,C' '         TEST SCHEME GIVEN (OUTLET 0 WITH             
         BH    GETSHR              SCHEME IS FOR 100% TEST)                     
         L     R7,GDAOUTS                                                       
         XC    GDNOUTS,GDNOUTS                                                  
         MVI   GDEFFSW,C'N'         CLEAR EFFECTIVE DATE SWITCH                 
         ZIC   RF,GDMGRLEN                                                      
         LA    RF,KEY+3(RF)                                                     
         ST    RF,AKEYMKT                                                       
         CLI   GDMKTLEN,0                                                       
         BE    *+8                                                              
         LA    RF,4(RF)                                                         
         ST    RF,AKEYOUT                                                       
*                                                                               
         LR    RE,RF                                                            
         LA    R0,KEY+1                                                         
         SR    RE,R0                                                            
         ST    RE,KCLEN                                                         
*                                                                               
*                                                                               
*                                  NOW BUILD OUTLET TABLE                       
*                                  FOR GIVEN CLUSTER                            
         SR    R6,R6                                                            
         USING GDOUTD,R7                                                        
*                                  BUILD ACCT KEY                               
         MVI   KEY,C' '                                                         
         MVC   KEY+1(41),KEY                                                    
         MVC   KEY(3),GDCOMP       COMP,UNIT,LEDGER                             
         CLC   =C'NEXT',GDCLUST                                                 
         BNE   BT23                                                             
*                                                                               
         MVC   KEY,GDSAVKEY                                                     
         B     BT28                                                             
*                                  MKT GROUP                                    
BT23     DS    0H                                                               
         LA    R4,KEY+3                                                         
         CLI   GDMGRLEN,0          TEST PRESENT                                 
         BE    BT24                                                             
         ZIC   RF,GDMGRLEN                                                      
         BCTR  RF,R0                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),GDMGR                                                    
         LA    R4,1(R4,RF)                                                      
*                                  MKT                                          
BT24     DS    0H                                                               
         CLI   GDMKT,0             TEST PRESENT                                 
         BE    BT26                                                             
         MVC   0(4,R4),GDMKT                                                    
         LA    R4,4(R4)                                                         
*                                                                               
BT26     DS    0H                                                               
BT28     DS    0H                                                               
         BAS   RE,HIGHAC                                                        
         B     BT30B                                                            
*                                                                               
BT30     DS    0H                                                               
         BAS   RE,SEQAC                                                         
BT30B    DS    0H                                                               
         CLC   KEY(3),GDCOMP                                                    
         BNE   BT50                                                             
         CLI   KEY+18,C' '                                                      
         BH    BT30                                                             
         CLI   KEY+3,C'A'          SKIP SPECIALS (*'S, ETC)                     
         BL    BT30                                                             
         CLC   =C'NEXT',GDCLUST                                                 
         BNE   BT32                                                             
*                                                                               
         CLI   GDMGRLEN,0                                                       
         BE    BT30D                                                            
         L     RE,AKEYMKT                                                       
         CLI   GDMKTLEN,0                                                       
         BNE   *+8                                                              
         L     RE,AKEYOUT                                                       
         CLI   0(RE),C'A'                                                       
         BNL   BT30D                                                            
*                                                                               
         BAS   RE,SETNAME                                                       
         MVC   GDMGRNAM,WORK       SET (MKT GRP' NAME                           
         CLI   GDMKTLEN,0                                                       
         BNE   BT30                                                             
*                                                                               
BT30D    DS    0H                                                               
         CLI   GDMKTLEN,0                                                       
         BE    BT30F                                                            
         L     RE,AKEYOUT                                                       
         CLI   0(RE),C'A'                                                       
         BNL   BT30F                                                            
*                                                                               
         BAS   RE,SETNAME                                                       
         MVC   GDMKTNAM,WORK       SET 'MKT' NAME                               
*                                                                               
BT30F    DS    0H                                                               
         MVC   KEYSAVE,KEY                                                      
         MVI   GDCLUST,C' '                                                     
         MVC   GDCLUST+1(7),GDCLUST                                             
         CLI   GDMGRLEN,0                                                       
         BE    BT31                                                             
         ZIC   RF,GDMGRLEN                                                      
         BCTR  RF,R0                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   GDMGR(0),KEY+3                                                   
*                                                                               
BT31     DS    0H                                                               
         CLI   GDMKTLEN,0                                                       
         BE    BT33                                                             
         L     RF,AKEYMKT                                                       
         MVC   GDMKT,0(RF)                                                      
         B     BT33                                                             
*                                                                               
BT32     DS    0H                                                               
         L     R4,KCLEN                                                         
         EX    R4,*+8              R4 HAS LENGTH FOR KEY COMP                   
         B     *+10                                                             
         CLC   KEY(0),KEYSAVE                                                   
         BNE   BT50                                                             
*                                                                               
BT33     DS    0H                                                               
         MVI   GDOUTD,C' '                                                      
         MVC   GDOUTD+1(GDOUTDL-1),GDOUTD                                       
         L     RF,GDNSCHMS                                                      
         SLL   RF,2                                                             
         LA    RE,GDOSHRS                                                       
         XCEF                                                                   
*                                                                               
         LA    R2,ACRECORD                                                      
         MVI   ELCODE,X'20'        NAME ELEM                                    
         CLI   0(R2),X'20'                                                      
         BE    BT34                                                             
         BAS   RE,NEXTEL                                                        
         BNE   BT30                                                             
*                                                                               
BT34     DS    0H                                                               
         ZIC   RF,GDOUTLEN                                                      
         BCTR  RF,R0                                                            
         L     RE,AKEYOUT                                                       
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   GDOCODE(0),0(RE)         OUTLET CODE                             
*                                                                               
         USING ACNAMED,R2                                                       
         ZIC   RF,ACNMLEN                                                       
         SH    RF,=H'3'                                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   GDONAME(0),ACNMNAME      NAME                                    
*                                                                               
         LA    R2,ACRECORD                                                      
         MVI   ELCODE,X'22'             ADDRESS ELEM                            
         BAS   RE,NEXTEL                                                        
         BNE   BT36                                                             
*                                                                               
         USING ACADDD,R2                                                        
         ZIC   RF,ACADLEN                                                       
         SH    RF,=H'4'                                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   GDOAL1(0),ACADADD                                                
*                                                                               
BT36     DS    0H                                                               
         LA    R2,ACRECORD                                                      
         MVI   ELCODE,X'23'       'OTHERS' ELEM                                 
         BAS   RE,NEXTEL                                                        
         BNE   BT37                                                             
*                                                                               
         USING ACOTHERD,R2                                                      
         MVC   GDONUM,ACOTNUM      SPECIAL NUMBER                               
         MVC   GDOSTAT,ACOTPROF    STATUS                                       
*                                                                               
BT37     DS    0H                                                               
         MVC   GDOSTRMO(4),=X'0000FFFF'  INITIALIZE START-END MOS               
         LA    R2,ACRECORD                                                      
         MVI   ELCODE,GDAELQ      GENERIC DATE ELEMENT                          
*                                                                               
BT37F    DS    0H                                                               
         BAS   RE,NEXTEL                                                        
         BNE   BT38                                                             
*                                                                               
         USING GDAELD,R2                                                        
         CLI   GDATYPE,GDATRTLS       RETAIL START DATE                         
         BNE   BT37J                                                            
         OC    GDADATE,GDADATE                                                  
         BZ    BT37F                                                            
         GOTO1 DATCON,DMCB,(1,GDADATE),(3,WORK)                                 
         MVC   GDOSTRMO,WORK                                                    
         MVI   GDEFFSW,C'Y'        SET HAVE EFFECTIVE DATE                      
*                                                                               
BT37J    DS    0H                                                               
         CLI   GDATYPE,GDATRTLE       RETAIL END DATE                           
         BNE   BT37F                                                            
         OC    GDADATE,GDADATE                                                  
         BZ    BT37F                                                            
         GOTO1 DATCON,DMCB,(1,GDADATE),(3,WORK)                                 
         MVC   GDOENDMO,WORK                                                    
         MVI   GDEFFSW,C'Y'        SET HAVE EFFECTIVE DATE                      
         B     BT37F                                                            
*                                                                               
BT38     DS    0H                                                               
         LA    R2,ACRECORD                                                      
         MVI   ELCODE,X'62'        DISTRIBUTION ELEM                            
*                                                                               
BT40     DS    0H                                                               
         BAS   RE,NEXTEL                                                        
         BE    BT42                                                             
         CLI   GDOCODE,C' '                                                     
         BNH   *+8                                                              
         LA    R6,1(R6)                                                         
         A     R7,GDROWL                                                        
         MVI   0(R7),X'FF'                                                      
         B     BT30                NEXT OUTLET                                  
*                                                                               
BT42     DS    0H                                                               
         USING ACDISTD,R2                                                       
         CP    ACDIVAL,=P'0'       SKIP NO SHARE                                
         BE    BT40                                                             
*                                  FIND SCHEME IN SCHEME TABLE                  
         L     R1,GDASCHMS                                                      
BT44     DS    0H                                                               
         CLC   0(2,R1),ACDICODE                                                 
         BE    BT46                                                             
         LA    R1,GDSCHMDL(R1)                                                  
         C     R1,GDAOUTS                                                       
         BL    BT44                                                             
         B     BT40                SCHEME NOT FOUND                             
*                                  SCHEME POSITION IN SCHEME TAB                
*                                  SAME AS ITS POS IN SHARE PART                
*                                  OF OUTLET TABLE                              
BT46     DS    0H                                                               
         S     R1,GDASCHMS                                                      
         SR    R0,R0                                                            
         LA    RF,GDSCHMDL                                                      
         DR    R0,RF               R1 = POS IN SCHEME TAB                       
         SLL   R1,2                X 4 = POS IN SHR TAB                         
         LA    R1,GDOSHRS(R1)                                                   
         ZAP   DUB,ACDIVAL                                                      
         CVB   R0,DUB                                                           
         ST    R0,DUB                                                           
         MVC   0(4,R1),DUB                                                      
*                                                                               
         B     BT40                                                             
*                                                                               
BT50     DS    0H                                                               
         LA    R7,1(R7)            NOW BUILD SCH/PRD/EST TAB                    
         MVI   0(R7),X'FF'                                                      
         ST    R7,GDASCHPE                                                      
         ST    R6,GDNOUTS                                                       
         LTR   R6,R6                                                            
         BNP   BT50A9              NO OUTLETS                                   
*                                  PUT TOTAL OF PCTS IN TOTAL ROW               
*                                                                               
         L     R5,GDNSCHMS                                                      
         SR    R4,R4                                                            
BT50A2   DS    0H                                                               
         SR    RE,RE                                                            
         L     R7,GDAOUTS                                                       
         L     R6,GDNOUTS                                                       
BT50A4   DS    0H                                                               
         A     R7,GDROWL           NEXT OUTLET                                  
         A     RE,GDOUTDL(R7,R4)                                                
         BCT   R6,BT50A4                                                        
*                                                                               
         L     R7,GDAOUTS          POINT TO TOTAL ROW                           
         ST    RE,GDOUTDL(R7,R4)   STORE IN RIGHT SLOT                          
         LTR   RE,RE               IF ACTIVE                                    
         BZ    BT50A8                                                           
         CLC   GDCOMP(3),=X'6EF3D3'    AND B+S MILLER 3L                        
         BE    BT50A7                                                           
         CLC   GDCOMP(3),=X'6EF3D1'     OR 3J                                   
         BE    BT50A7                                                           
         CLC   GDCOMP(3),=X'6EF3D7'     OR 3P                                   
         BE    BT50A7                                                           
         CLC   GDCOMP(3),=X'6EF3D9'      OR 3R                                  
         BE    BT50A7                                                           
         CLC   GDCOMP(3),=X'6EF3E2'      OR 3S                                  
         BE    BT50A7                                                           
         CLC   GDCOMP(3),=X'6EF3E4'      OR 3U                                  
         BE    BT50A7                                                           
         CLC   GDCOMP(3),=X'6EF3E5'      OR 3V                                  
         BE    BT50A7                                                           
         CLC   GDCOMP(3),=X'6EF3E7'      OR 3X                                  
         BE    BT50A7                                                           
         CLC   GDCOMP(3),=X'6EF3C1'      OR 3A                                  
         BE    BT50A7                                                           
         CLC   GDCOMP(3),=X'6EF3C3'      OR 3C                                  
         BE    BT50A7                                                           
         CLC   GDCOMP(3),=X'6EF3C4'      OR 3D                                  
         BE    BT50A7                                                           
         CLC   GDCOMP(3),=X'6EF3C6'      OR 3F                                  
         BE    BT50A7                                                           
         CLC   GDCOMP(3),=X'6EF3C7'      OR 3G                                  
         BE    BT50A7                                                           
         CLC   GDCOMP(3),=X'6EF3C9'      OR 3I                                  
         BE    BT50A7                                                           
         CLC   GDCOMP(3),=X'6EF3D4'      OR 3M                                  
         BE    BT50A7                                                           
         CLC   GDCOMP(3),=X'6EF3D6'      OR 3O                                  
         BE    BT50A7                                                           
         CLC   GDCOMP(3),=X'6EF3F1'      OR 31                                  
         BE    BT50A7                                                           
         CLC   GDCOMP(3),=X'6EF3F3'      OR 33                                  
         BE    BT50A7                                                           
         CLC   GDCOMP(3),=X'6EF3F4'      OR 34                                  
         BE    BT50A7                                                           
         CLC   GDCOMP(3),=X'6EF3F6'      OR 36                                  
         BNE   BT50A8                                                           
BT50A7   DS    0H                                                               
         LA    RF,GDOUTDL(R7,R4)                                                
         A     RF,GDROWL                                                        
         OC    0(4,RF),0(RF)       AND FIRST OUTLET IS ZERO                     
         BNZ   BT50A8                                                           
         MVC   0(4,RF),=F'5000'    SET IT TO 50 PCT                             
         A     RE,=F'5000'                                                      
         ST    RE,GDOUTDL(R7,R4)   ADJUST TOTAL ROW                             
*                                                                               
BT50A8   DS    0H                                                               
         LA    R4,4(R4)            NEXT SCHEME                                  
         BCT   R5,BT50A2                                                        
*                                                                               
*                                                                               
BT50A9   DS    0H                                                               
         CLC   =C'NEXT',GDCLUST                                                 
         BNE   BT50B                                                            
         MVC   GDCLUST(4),=C'LAST'                                              
         B     EXIT                                                             
*                                                                               
BT50B    DS    0H                                                               
         MVC   GDSAVKEY,KEY                                                     
         LTR   R6,R6                                                            
         BZ    EXIT                                                             
         ST    R6,GDNOUTS                                                       
         SPACE 2                                                                
         B     EXIT                                                             
         SPACE 3                                                                
*        GET SHARE FOR GIVEN OUTLET/SCHEME                                      
         SPACE 2                                                                
GETSHR   DS    0H                                                               
         MVC   WORK(8),GDOUTAD     SAVE LAST ADDR'S                             
         XC    GDOUTAD,GDOUTAD                                                  
         XC    GDSCHMAD,GDSCHMAD                                                
         XC    GDSHRP(16),GDSHRP   CLEAR SHR%,ACT,GROSS,NET                     
*                                                                               
         ZIC   R1,GDPER            PERIOD NUMBER                                
         MH    R1,=H'6'                                                         
         A     R1,GDAPERL          POINT TO PERLST ENTRY                        
         ST    R1,APERL            SAVE IT                                      
*                                                                               
         ZIC   RF,GDOUTNO                                                       
         C     RF,GDNOUTS                                                       
         BH    EXIT                NO MORE OUTLETS                              
         M     RE,GDROWL                                                        
         A     RF,GDAOUTS                                                       
         ST    RF,GDOUTAD          POINT TO OUTLET DESC                         
*                                                                               
*                                  FIND SCHEME                                  
         L     RE,GDNSCHMS                                                      
         L     RF,GDASCHMS                                                      
GS4      DS    0H                                                               
         CLC   GDSCHM,0(RF)                                                     
         BE    GS6                                                              
         LA    RF,GDSCHMDL(RF)                                                  
         BCT   RE,GS4                                                           
         B     EXIT                NO SCHEME-EXIT WITH SHR ZERO                 
*                                                                               
GS6      DS    0H                                                               
         ST    RF,GDSCHMAD         SET ADDR OF SCHEME DESC                      
         S     RF,GDASCHMS         LESS START                                   
         SR    RE,RE                                                            
         D     RE,=A(GDSCHMDL)     DIV BY LEN                                   
         SLL   RF,2                X 4  = DISP INTO SHRS                        
         ST    RF,ASHRE            SAVE IT                                      
         A     RF,GDOUTAD          PLUS A(OUTLET)                               
         LA    RF,GDOUTDL(RF)      PLUS START OF SHRS = THIS SHR                
*                                                                               
         MVC   GDSHRP,0(RF)        (WILL BE TOTAL OF SHARE %'S IF               
*                                   GDOUTNO=0)                                  
         L     R7,GDOUTAD                                                       
*                                                                               
         CLI   GDOUTNO,0           IF OUTLET 0                                  
         BE    GS6M                USE TOTAL OF SHARES                          
*                                                                               
*                                  ELSE GET THIS OUTLETS SHARE                  
         L     R1,APERL            BUT MUST TEST IF OUTLET 'EFFECTIVE'          
         CLC   0(2,R1),GDOSTRMO    TEST MONTH VS START                          
         BL    GS6C                                                             
         CLC   0(2,R1),GDOENDMO    AND END                                      
         BH    GS6C                                                             
         B     GS6D                                                             
*                                                                               
GS6C     DS    0H                  OUTLET NOT 'EFFECTIVE'                       
         XC    GDSHRP,GDSHRP       CLEAR SHARE %                                
*                                                                               
GS6D     DS    0H                                                               
         CLI   GDTEST,C'Y'         IF TEST MODE                                 
         BE    GS6R                NO CALCS OR CUMES                            
         B     GS6X                                                             
*                                  OUTLET=0                                     
*                                  GET TOTAL OF SHARES                          
GS6M     DS    0H                                                               
         CLI   GDTEST,C'Y'         MUST BE TEST MODE??                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLI   GDEFFSW,C'N'        IF NO OUTLET EFFECTIVE DATES                 
         BE    GS6R                HAVE TOTAL - ELSE MUST GO THRU               
*                                  OUTLETS TO ADD UP EFFECTIVE SHARES           
*                                                                               
         L     R7,GDAOUTS          FIRST OUTLET                                 
         A     R7,GDROWL           SKIP IT ('TOTAL' OUTLET)                     
         SR    RE,RE                                                            
*                                                                               
GS6P     DS    0H                                                               
         CLI   0(R7),X'FF'         END OF OUTLETS                               
         BE    GS6R                                                             
*                                                                               
         L     R1,APERL                                                         
         CLC   0(2,R1),GDOSTRMO    TEST MONTH VS START                          
         BL    GS6P8                                                            
         CLC   0(2,R1),GDOENDMO    AND END                                      
         BH    GS6P8                                                            
*                                                                               
         L     RF,ASHRE            DISP INTO SHARES                             
         AR    RF,R7               PLUS START OF RECORD                         
         LA    RF,GDOUTDL(RF)      PLUS START OF SHARES                         
         A     RE,0(RF)            ADD SHARE                                    
         STCM  RE,15,GDSHRP                                                     
*                                                                               
GS6P8    DS    0H                                                               
         A     R7,GDROWL           NEXT OUTLET                                  
         B     GS6P                                                             
*                                                                               
GS6R     DS    0H                                                               
         B     EXIT                                                             
*                                  FIND SCHEME/PRD/EST                          
GS6X     DS    0H                                                               
         L     R6,GDASCHPE                                                      
         USING GDSCHMD,R6                                                       
GS7      DS    0H                                                               
         CLI   0(R6),X'FF'         EOL                                          
         BE    GS7B                                                             
         CLC   GDSCHM(4),GDSCODE   SCH/PRD/EST                                  
         BNE   GS7A                                                             
         TM    GDCNTL,GDCMKTQ      IF MARKETS IN SCHEME TABLE                   
         BZ    GS7D                                                             
         CLC   GDMKT,GDSMKT        TEST MKT BREAK ALSO                          
         BE    GS7D                                                             
*                                                                               
GS7A     DS    0H                                                               
         LA    R6,GDSCHMDL(R6)                                                  
         B     GS7                                                              
*                                                                               
GS7B     DS    0H                  NOT YET USED                                 
**       L     RE,GDSCHMAD                                                      
**       MVC   0(GDSCHMDL,R6),0(RF)  SET FROM SCHEME TAB                        
         LR    R0,R6                                                            
         LHI   R1,GDSCHMDL                                                      
         LR    RF,R1                                                            
         L     RE,GDSCHMAD                                                      
         MVCL  R0,RE                                                            
         MVC   GDSPRD(2),GDPRD     SET PRD/EST                                  
         TM    GDCNTL,GDCMKTQ      MKTS IN SCHEME TABLE?                        
         BZ    *+10                                                             
         MVC   GDSMKT,GDMKT                                                     
         MVI   GDSCHMDL(R6),X'FF'                                               
*                                                                               
GS7D     DS    0H                                                               
         ST    R6,GDSCHMAD                                                      
         ZIC   R5,GDPER            PERIOD INDEX                                 
         SLL   R5,2                X 4 = DISPLACEMENT                           
         L     R1,GDSCUMP(R5)      CUMUL PCT                                    
         A     R1,GDSHRP                                                        
         ST    R1,GDSCUMP(R5)                                                   
*                                                                               
GS8      DS    0H                                                               
         TM    GDCNTL,GDCNCUMQ        IF NO CUME MODE                           
         BZ    GS9                                                              
         L     R1,GDSHRP               SET SHARES DIRECTLY                      
*                                      (NOT BY SUBTRACTION)                     
         XC    GDSCUMA(25*4),GDSCUMA   FOR ACTUAL'S ONLY                        
*                                                                               
GS9      DS    0H                                                               
         M     R0,GDTOTA           CUMUL PCT X TOTAL (ACT)                      
         L     RF,=F'10000'                                                     
         BAS   RE,DIV                                                           
*                                                                               
         LR    RF,R1                                                            
         S     R1,GDSCUMA(R5)         LESS CUMUL AMT =                          
         ST    R1,GDSHRA          THIS SHARE AMOUNT                             
         ST    RF,GDSCUMA(R5)         SET CUMUL AMT                             
*                                                                               
         L     R1,GDSCUMP(R5)                                                   
         M     R0,GDTOTG           CUMUL PCT X TOTAL (GROSS)                    
         L     RF,=F'10000'                                                     
         BAS   RE,DIV                                                           
*                                                                               
         LR    RF,R1                                                            
         S     R1,GDSCUMG(R5)         LESS CUMUL AMT =                          
         ST    R1,GDSHRG          THIS SHARE AMOUNT                             
         ST    RF,GDSCUMG(R5)         SET CUMUL AMT                             
*                                                                               
         L     R1,GDSCUMP(R5)                                                   
         M     R0,GDTOTN           CUMUL PCT X TOTAL (NET)                      
         L     RF,=F'10000'                                                     
         BAS   RE,DIV                                                           
*                                                                               
         LR    RF,R1                                                            
         S     R1,GDSCUMN(R5)         LESS CUMUL AMT =                          
         ST    R1,GDSHRN          THIS SHARE AMOUNT                             
         ST    RF,GDSCUMN(R5)         SET CUMUL AMT                             
*                                                                               
         B     EXIT                                                             
         SPACE 3                                                                
EXIT     DS    0H                                                               
         L     RF,GDAUTL                                                        
         MVC   4(1,RF),SAVSYS      RESTORE SYS NO.                              
XIT      DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
READAC   DS    0H                                                               
         LA    RF,=C'DMREAD'                                                    
         B     DMAC                                                             
         SPACE 2                                                                
HIGHAC   DS    0H                                                               
         LA    RF,=C'DMRDHI'                                                    
         MVC   KEYSAVE,KEY                                                      
         B     DMAC                                                             
         SPACE 2                                                                
SEQAC    DS    0H                                                               
         LA    RF,=C'DMRSEQ'                                                    
         SPACE 2                                                                
DMAC     DS    0H                                                               
         ST    RF,DMCB                                                          
         LR    R0,RE                                                            
         GOTO1 DATAMGR,DMCB,,=C'ACCOUNT',KEY,KEY,0                              
*                                                                               
         LR    RE,R0                                                            
         TM    DMCB+8,X'FF'                                                     
         BZR   RE                                                               
         DC    H'0'                                                             
         SPACE 3                                                                
NEXTEL   DS    0H                                                               
         SR    R0,R0                                                            
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         CLI   0(R2),0                                                          
         BE    NEXTEL2                                                          
         CLC   ELCODE,0(R2)                                                     
         BER   RE                                                               
         B     NEXTEL                                                           
NEXTEL2  DS    0H                                                               
         LTR   RE,RE                                                            
         BR    RE                                                               
         SPACE 2                                                                
DIV      DS    0H                                                               
         LTR   RF,RF                                                            
         BNZ   *+8                                                              
         SR    R1,R1                                                            
         BR    RE                                                               
*                                                                               
         SLDA  R0,1                                                             
         DR    R0,RF                                                            
         LTR   R1,R1                                                            
         BNP   *+8                                                              
         A     R1,=F'1'                                                         
         SRA   R1,1                                                             
         BR    RE                                                               
         SPACE 2                                                                
SETNAME  NTR1                                                                   
         LA    R2,ACRECORD                                                      
         MVI   ELCODE,X'20'                                                     
         CLI   0(R2),X'20'                                                      
         BE    SN2                                                              
         BAS   RE,NEXTEL                                                        
         BNE   XIT                                                              
SN2      DS    0H                                                               
         USING ACNAMED,R2                                                       
         MVI   WORK,C' '                                                        
         MVC   WORK+1(35),WORK                                                  
         ZIC   RF,ACNMLEN                                                       
         SH    RF,=H'3'                                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   WORK(0),ACNMNAME                                                 
*                                                                               
         B     XIT                                                              
         SPACE 2                                                                
ACCLIST  DC    C'NACCFIL NCTFILE X'                                             
EMULIST  DC    C'NACCDIR NACCMST NACCARC NCTFILE X'                             
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
WORKD    DSECT                                                                  
DATAMGR  DS    V                                                                
DATCON   DS    V                                                                
SAVR1    DS    F                                                                
KCLEN    DS    F                                                                
AKEYMKT  DS    A                                                                
AKEYOUT  DS    A                                                                
DMCB     DS    6F                                                               
KEYSAVE  DS    CL42                                                             
KEY      DS    CL42                                                             
         DS    2000X                                                            
WORK     DS    XL64                                                             
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
ELCODE   DS    X                                                                
SAVSYS   DS    X                                                                
ASHRE    DS    A                                                                
APERL    DS    A                                                                
         SPACE 2                                                                
*                                                                               
       ++INCLUDE SPRTLBLK                                                       
         EJECT                                                                  
*                                                                               
*++INCLUDE ACGENFILE                                                            
*++INCLUDE ACGENBOTH                                                            
*++INCLUDE DDCOMFACS                                                            
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
       ++INCLUDE ACGENBOTH                                                      
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
         SPACE 2                                                                
*                                                                               
       ++INCLUDE DMDTFIS                                                        
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'105SPFINDOUT 04/28/14'                                      
         END                                                                    
