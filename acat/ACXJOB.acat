*          DATA SET ACXJOB     AT LEVEL 030 AS OF 05/01/02                      
*CATALP ACXJOB                                                                  
                                                                                
***********************************************************************         
* ACXJOB  - MODULE CALLED BY TALENT WITH AN SJ XJOB ACCOUNT. VALIDATES*         
*           EXPENSE AND ANALYSIS FIELDS AND BUILDS BUFFER OF XJOB     *         
*           POSTINGS TO PASS BACK TO TALENT TO BE ADDED TO A WORKER   *         
*           FILE                                                      *         
*                                                                     *         
* AT ENTRY - P1=A(ACXJOB INTERFACE BLOCK)                             *         
***********************************************************************         
*                                                                               
         TITLE 'X JOBS EXPENSE AND ANALYSIS POSTING VALIDATION'                 
ACXJOB   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 LWSX-LWSD,*ACXJOB*,R7,CLEAR=YES,RR=RE                            
         USING LWSD,RC             RC=A(LOCAL WORKING STORAGE)                  
         L     R8,0(R1)            R8=A(PARAMETER BLOCK)                        
         USING ACXJOBD,R8                                                       
         L     R9,ACXACOM          R9=A(COMFACS)                                
         USING COMFACSD,R9                                                      
*                                                                               
         ZAP   RECCNT,=P'0'        INITIALIZE RECORD COUNTER                    
         ZAP   RECCASH,=P'0'       INITIALIZE CASH COUNTER                      
         MVI   ACXERRS,0                                                        
         MVI   BIT,0                                                            
         MVI   BIT2,0                                                           
         MVI   BIT3,0                                                           
         MVC   STDISP,=H'0'                                                     
         MVI   MYSPACE,C' '                                                     
         MVC   MYSPACE+1(L'MYSPACE-1),MYSPACE                                   
         MVC   VTYPES(VTYPLNQ),ADCONS                                           
*                                                                               
         GOTO1 CDATCON,DMCB,(5,0),(1,TODAYP)  GET TODAY'S DATE                  
*                                                                               
         EJECT                                                                  
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*         
*  BUILD SJ & SV POSTING ACCOUNT FROM SJ RECORD PASSED FROM CALLER              
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*         
*                                                                               
         USING PSHEADD,R3                                                       
         L     R3,ACXAPROD         R3 POINTS TO SJ REC FROM WORKER FILE         
         MVC   SVCOMP,PSHDACPY                                                  
         MVC   TAXSJAC,PSHDACC     SAVE SJ ACCOUNT                              
         OC    TAXSJAC,MYSPACE                                                  
         MVC   SVWORKCD,PSHDANAL   SAVE WORKCODE                                
         BAS   RE,RDCOMP           READ CO REC FOR SOME INFO                    
         BNE   XIT                 IF FOUND ERRORS GET OUT                      
*                                                                               
         GOTO1 =A(GETLDG),DMCB,(RC),C'SJ'                                       
         BNE   XIT                 GET OUT IF ERRORS                            
*                                                                               
         MVC   TAXSVAC,PSHDSBAC    MOVE SJ CONTRA INTO SV ACCOUNT               
         OC    TAXSVAC,MYSPACE                                                  
         MVC   TAXSVCAC(1),SVCOMP                                               
         MVC   TAXSVCAC+1(2),=C'SJ'  BUILD SV CONTRA W/ SJ CLIENT               
         ZIC   R1,LDGLVA                                                        
         BCTR  R1,0                                                             
         EXMVC R1,TAXSVCAC+3,TAXSJAC+3                                          
         OC    TAXSVCAC,MYSPACE                                                 
         LA    R2,TAXSVCAC                                                      
         GOTO1 =A(GETNAME),DMCB,(RC),(R2)                                       
         BNE   XIT                                                              
         MVC   TAXSVCNM,ACNAME                                                  
*                                                                               
         LA    R3,PSHEADL(R3)      POINT PAST WORKER HEADER TO ELEM             
         USING TRNELD,R3                                                        
TXJ10    CLI   0(R3),0                                                          
         BE    TXJ20                                                            
         CLI   0(R3),TRNELQ        44 TRANSACTION ELEMENT                       
         BE    TXJ12                                                            
         ZIC   R1,1(R3)                                                         
         AR    R3,R1                                                            
         B     TXJ10                                                            
*                                                                               
TXJ12    MVC   SV44ELM(TRNLN1Q),0(R3)       SAVE THE 44 ELEMENT                 
         DROP  R3                                                               
         EJECT                                                                  
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*         
*  CALL GETOPT WITH SJ TO GET SE ACCOUNT AND SE INFO                            
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*         
*                                                                               
TXJ20    DS    0H                                                               
         MVC   SVFINOFF,MYSPACE                                                 
         MVC   SVOFFANL,MYSPACE                                                 
         MVC   SVDEPANL,MYSPACE                                                 
         MVC   SVSTFANL,MYSPACE                                                 
         GOTO1 =A(GETLDG),DMCB,(RC),C'SJ'                                       
         BNE   XIT                                                              
*                                                                               
         L     RE,AGOBLOCK         CLEAR GOBLOCK                                
         L     RF,=A(L'GOBLOCK)                                                 
         XCEFL                                                                  
         L     RE,AGOBLOCK                                                      
         AH    RE,=Y(GOBLOCKX-GOBLOCKD)                                         
         ST    RE,AGOXBLK          EXTENSION BLOCK                              
         L     RE,AGOXBLK          CLEAR THE EXTENSION BLOCK                    
         L     RF,=A(L'GOXBLOCK)                                                
         XCEFL                                                                  
*                                                                               
         USING GOBLOCKD,R3                                                      
         LA    R4,TAXSJAC+3                                                     
         L     R3,AGOBLOCK                                                      
         MVC   GOADM,CDATAMGR                                                   
         MVC   GOAEXT,AGOXBLK      EXTENSION BLOCK                              
         MVC   GOSELCUL(1),SVCOMP                                               
         MVC   GOSELCUL+1(2),TAXSJAC+1 MOVE IN THE SJ LEDGER                    
         ZIC   R1,LDGLVA                                                        
         BCTR  R1,0                                                             
         EXMVC R1,GOSELCLI,0(R4)       CLIENT                                   
         OC    GOSELCLI,MYSPACE                                                 
         LA    R4,1(R1,R4)                                                      
*                                                                               
         ZIC   R1,LDGLVB                                                        
         BCTR  R1,0                                                             
         EXMVC R1,GOSELPRO,0(R4)       PRODUCT                                  
         OC    GOSELPRO,MYSPACE                                                 
         MVC   SVPROD,GOSELPRO     SAVE PRODUCT FOR 23 ELEM                     
         LA    R4,1(R1,R4)                                                      
*                                                                               
         ZIC   R1,LDGLVC                                                        
         BCTR  R1,0                                                             
         EXMVC R1,GOSELJOB,0(R4)       JOB                                      
         OC    GOSELJOB,MYSPACE                                                 
         MVC   SVJOB,GOSELJOB      SAVE JOB FOR 23 ELEM                         
         MVC   GOABINSR,ACXABIN     A(BINSRCH)                                  
         MVC   GOACOVL,ACXACOVL     A(COVAIL)                                   
         MVI   GOWHICH,0                                                        
         MVI   GOANYMED,0                                                       
         GOTO1 ACXAGET,DMCB,AGOBLOCK                                            
*                                                                               
         USING GOXBLKD,R3                                                       
         L     R3,AGOXBLK                                                       
         OC    GOAWOA,GOAWOA       ANY SE ACCOUNT FOUND?                        
         BNZ   *+12                                                             
         OI    ACXERRS,ACXNOSE     SET ERROR AND EXIT                           
         B     XIT                                                              
         MVC   TAXSEAC(1),SVCOMP                                                
         MVC   TAXSEAC+1(14),GOAWOA      SAVE THE SE ACCOUNT                    
         OC    TAXSEAC,MYSPACE                                                  
         MVC   TAXSECAC,TAXSVAC          SAVE THE SE CONTRA ACCOUNT             
         OC    TAXSEAC,MYSPACE                                                  
         LA    R2,TAXSECAC                                                      
         GOTO1 =A(GETNAME),DMCB,(RC),(R2)                                       
         BNE   XIT                                                              
         MVC   TAXSECNM,ACNAME     SAVE SE CONTRA ACCOUNT NAME                  
*                                                                               
         MVC   SVFINOFF,GOAWOFOF   SAVE FINANCIAL OFFICE                        
         MVC   SVOFFANL,GOAWOAOF   SAVE ANALYZED BY OFFICE                      
         MVC   SVDEPANL,GOAWODEP   SAVE ANALYZED BY DEPT                        
         MVC   SVSTFANL,GOAWOSTF   SAVE ANALYZED BY STAFF                       
*                                                                               
* NOW CAN BUILD THE SJ CONTRA WITH THE SE ACCOUNT                               
*                                                                               
         MVC   TAXSJCAC,TAXSEAC                                                 
         LA    R2,TAXSJCAC                                                      
         GOTO1 =A(GETNAME),DMCB,(RC),(R2)                                       
         BNE   XIT                                                              
         MVC   TAXSJCNM,ACNAME                                                  
         DROP  R3                                                               
         EJECT                                                                  
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*         
* READ SE ACCOUNT FOR ANALYSIS AND BALANCE INFO                                 
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*         
*                                                                               
TXJ30    DS    0H                                                               
         LA    R2,TAXSEAC                                                       
         GOTO1 =A(GETSTAT),DMCB,(RC),(R2)                                       
         BE    *+12                                                             
         OI    ACXERRS,ACXNOREC    SET ERROR AND EXIT                           
         B     XIT                                                              
         SPACE 3                                                                
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*         
* READ PRODUCT REC FOR CREDIT OFFICE. NO OFFICE THEN READ CLIENT REC            
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*         
*                                                                               
TXJ40    DS    0H                                                               
*                                                                               
         NI    BIT,X'FF'-CHKCLI                                                 
         MVC   SVCPOFF,MYSPACE                                                  
         GOTO1 =A(GETLDG),DMCB,(RC),C'SJ'                                       
         BNE   XIT                                                              
*                                                                               
         LA    R6,TAXSJAC+3        SJ REC                                       
         LA    R3,ACCNT                                                         
         MVC   ACCNT,MYSPACE                                                    
         ZIC   R1,LDGLVA                                                        
         BCTR  R1,0                                                             
         EXMVC R1,0(R3),0(R6)      CLIENT                                       
         LA    R3,1(R1,R3)                                                      
         LA    R6,1(R1,R6)                                                      
*                                                                               
         ZIC   R1,LDGLVB                                                        
         BCTR  R1,0                                                             
         EXMVC R1,0(R3),0(R6)       PRODUCT                                     
         OC    ACCNT,MYSPACE                                                    
*                                                                               
         USING ACTRECD,R6                                                       
         LA    R6,KEY                                                           
         MVC   ACTKEY,MYSPACE                                                   
         MVC   ACTKCPY,SVCOMP                                                   
         MVC   ACTKUNT(2),=C'SJ'                                                
         MVC   ACTKACT,ACCNT                                                    
TXJ41    MVC   SVKEY,KEY                                                        
         GOTO1 CDATAMGR,DMCB,=C'DMRDHI',=C'ACCDIR',KEY,KEY                      
         CLC   SVKEY(L'ACTKEY),KEY                                              
         BE    *+12                                                             
         OI    ACXERRS,ACXNOREC    SET ERROR AND EXIT                           
         B     XIT                                                              
         MVC   DA,ACTKDA                                                        
         GOTO1 CDATAMGR,DMCB,=C'GETREC',=C'ACCMST',DA,AIO1,WORK                 
         L     R6,AIO1                                                          
         AH    R6,=Y(ACTRFST-ACTRECD)                                           
*                                                                               
         USING PPRELD,R6                                                        
TXJ42    CLI   0(R6),0                                                          
         BE    TXJ46                                                            
         CLI   0(R6),PPRELQ        PRODUCTION PROFILE ELEMENT                   
         BE    TXJ44                                                            
*                                                                               
         SR    RF,RF                                                            
         IC    RF,1(R6)                                                         
         AR    R6,RF                                                            
         B     TXJ42                                                            
*                                                                               
TXJ44    CLC   SVCPOFF,MYSPACE    DO WE ALREADY HAVE THE CREDIT OFFICE          
         BNE   TXJ45                                                            
         OC    PPRGAOFF,PPRGAOFF   CREDIT OFFICE                                
         BZ    *+10                                                             
         MVC   SVCPOFF,PPRGAOFF   SAVE CREDIT OFFICE                            
TXJ45    OC    TAX1CAC,TAX1CAC     DO WE ALREADY HAVE THE 1C                    
         BNZ   TXJ45A                                                           
         OC    PPRCOSTU,PPRCOSTU   COSTING ACCOUNT                              
         BZ    TXJ46                                                            
         MVC   TAX1CAC(1),SVCOMP                                                
         MVC   TAX1CAC+1(14),PPRCOSTU    SAVE 1C UN/LDG/ACCOUNT                 
         OC    TAX1CAC,MYSPACE                                                  
*                                                                               
TXJ45A   OC    SVCPOFF,SVCPOFF                                                  
         BZ    TXJ46                                                            
         OC    TAX1CAC,TAX1CAC                                                  
         BNZ   TXJ48                                                            
*                                                                               
TXJ46    TM    BIT,CHKCLI          HAVE WE CHECKED THE CLIENT REC ALSO?         
         BO    TXJ48               YES THAN DONE                                
*                                                                               
         USING ACTRECD,R6                                                       
         MVC   KEY,SVKEY           RESTORE KEY                                  
         LA    R6,KEY                                                           
         MVC   ACCNT,ACTKACT                                                    
         LA    RF,ACCNT            POINT TO ACCOUNT                             
         ZIC   R1,LDGLVA                                                        
         AR    RF,R1               POINT PAST FIRST LEVEL                       
         ZIC   R1,LDGLVB                                                        
         BCTR  R1,0                                                             
         EXMVC R1,0(RF),MYSPACE    SPACE OUT THE PRODUCT                        
         MVC   ACTKACT,ACCNT                                                    
         OI    BIT,CHKCLI          SET STATUS THAT READING CLIENT               
         B     TXJ41               NOW READ FOR CLIENT                          
*                                                                               
* GET THE 13 CONTRA ACCOUNT                                                     
*                                                                               
TXJ48    TM    BIT,COSTANL      ANALYZED BY COST?                               
         BZ    TXJ50            NO THEN DON'T NEED 13 ACCOUNT                   
*                                                                               
         USING CATD,R1                                                          
         LA    R1,CATBLK                                                        
         MVC   CATDMGR,CDATAMGR    GET 13 CONTRA ACCOUNT FROM CATCALL           
         MVC   CATSEAC,TAXSEAC                                                  
         MVC   CATOFF,SVCPOFF      OFFICE COMES FROM CLI/PROD REC OR            
         OC    SVFINOFF,SVFINOFF   FINANCIAL OFFICE IN OPT/MAINT OR             
         BZ    *+10                ANALYSIS OFFICE IN OPT/MAINT                 
         MVC   CATOFF,SVFINOFF                                                  
         OC    SVOFFANL,SVOFFANL                                                
         BZ    *+10                                                             
         MVC   CATOFF,SVOFFANL                                                  
         MVC   CATDPT,SVDEPANL     ANY DEPT FROM OPT/MAINT                      
         OC    CATDPT,MYSPACE                                                   
         GOTO1 ACXACAT,CATD                                                     
*                                                                               
         LA    R1,CATBLK                                                        
         MVC   TAX1CCAC,CATACC3  SAVE 13 AS THE 1C CONTRA                       
*        LA    R2,TAX1CCAC                                                      
*        GOTO1 =A(GETNAME),DMCB,(RC),(R2)                                       
*        BNE   XIT                                                              
         MVC   TAX1CCNM(L'TAX1CCAC-1),TAX1CCAC+1                                
*                                                                               
         DROP  R1,R6                                                            
         EJECT                                                                  
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*         
* IF EXPENSE ACCT IS ANALYZED TRY TO CREATE SUBSIDIARY POSTING ACCNTS           
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*         
*                                                                               
TXJ50    DS    0H                                                               
         TM    BIT,DEPANL+STAFANL+COSTANL                                       
         BZ    TXJ100              ACCOUNT NOT ANALYZED                         
*                                                                               
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*         
* ANALYZED BY DEPARTMENT - DR TO '2D' AND CR TO '28'                            
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*         
*                                                                               
         TM    BIT,DEPANL         ANALYZED BY DEPT?                             
         BZ    TXJ70                                                            
*                                                                               
         OC    SVDEPANL,SVDEPANL   ANY DEPT CODE FROM OPT/MAINT?                
         BNZ   *+12                                                             
         OI    ACXERRS,ACXNODPT    SET ERROR AND EXIT                           
         B     XIT                                                              
*                                                                               
         GOTO1 =A(GETLDG),DMCB,(RC),C'2D'                                       
         BE    *+12                IF CAN'T FIND LEDGER FOR ANALYSIS            
         OI    ACXERRS,ACXNOREC    SET ERROR AND EXIT                           
         B     XIT                 CONTINUE.                                    
                                                                                
         MVC   TAX2DAC(1),SVCOMP                                                
         MVC   TAX2DAC+1(2),=C'2D'                                              
         LA    R3,ACCNT                                                         
         MVC   ACCNT,MYSPACE                                                    
         LA    RF,SVOFFANL         ANALYSIS OFFICE                              
         OC    SVFINOFF,SVFINOFF   IS THERE A FINANCIAL OFFICE?                 
         BZ    *+8                                                              
         LA    RF,SVFINOFF         THEN USE THAT INSTEAD                        
         OC    SVCPOFF,SVCPOFF     IS THERE A CLIENT/PRODUCT OFFICE?            
         BZ    *+8                                                              
         LA    RF,SVCPOFF          THEN USE THAT INSTEAD                        
         ZIC   R1,LDGLVA                                                        
         BCTR  R1,0                                                             
         EXMVC R1,0(R3),0(RF)       OFFICE                                      
         LA    R3,1(R1,R3)                                                      
*                                                                               
TXJ51    ZIC   R1,LDGLVB                                                        
         BCTR  R1,0                                                             
         EXMVC R1,0(R3),SVDEPANL    DEPARTMENT                                  
         OC    ACCNT,MYSPACE                                                    
         MVC   TAX2DAC+3(12),ACCNT                                              
*                                                                               
TXJ60    DS    0H                                                               
         GOTO1 =A(GETLDG),DMCB,(RC),C'28'                                       
         BE    *+12                IF CAN'T FIND LEDGER FOR ANALYSIS            
         OI    ACXERRS,ACXNOREC    SET ERROR AND EXIT                           
         B     XIT                                                              
*                                                                               
         MVC   TAX28AC,MYSPACE                                                  
         MVC   TAX28AC(1),SVCOMP                                                
         MVC   TAX28AC+1(2),=C'28'                                              
         MVC   TAX28AC+3(12),TAXSEAC+3                                          
*                                                                               
         MVC   TAX28CAC,TAX2DAC  COPY 2D ACCOUNT TO 28 CONTRA                   
         MVC   TAX2DCAC,TAX28AC  COPY 28 ACCOUNT TO 2D CONTRA                   
*                                                                               
         LA    R2,TAX2DCAC                                                      
         GOTO1 =A(GETNAME),DMCB,(RC),(R2)                                       
         BE    *+12                IF CAN'T FIND LEDGER FOR ANALYSIS            
         OI    ACXERRS,ACXNOREC    SET ERROR AND EXIT                           
         B     XIT                                                              
         MVC   TAX2DCNM,ACNAME     SAVE 2D CONTRA ACCOUNT NAME                  
*                                                                               
         LA    R2,TAX28CAC                                                      
         GOTO1 =A(GETNAME),DMCB,(RC),(R2)                                       
         BE    *+12                IF CAN'T FIND LEDGER FOR ANALYSIS            
         OI    ACXERRS,ACXNOREC    SET ERROR AND EXIT                           
         B     XIT                                                              
         MVC   TAX28CNM,ACNAME     SAVE 28 CONTRA ACCOUNT NAME                  
*                                                                               
         EJECT                                                                  
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*         
* ANALYZED BY STAFF - DR TO '2P' AND CR TO '29'                                 
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*         
*                                                                               
TXJ70    DS    0H                                                               
         TM    BIT,STAFANL      ANALYZED BY STAFF?                              
         BZ    TXJ90                                                            
*                                                                               
         OC    SVDEPANL,SVDEPANL   ANALYSIS DEPT DEFINED IN OPT/MAINT?          
         BNZ   *+12                                                             
         OI    ACXERRS,ACXNODPT    NO SET ERROR AND EXIT                        
         B     XIT                                                              
         OC    SVSTFANL,SVSTFANL   STAFF ANALYSIS DEFINED IN OPT/MAINT?         
         BNZ   *+12                                                             
         OI    ACXERRS,ACXNOSTF    NO SET ERROR AND EXIT                        
         B     XIT                                                              
*                                                                               
         GOTO1 =A(GETLDG),DMCB,(RC),C'2P'                                       
         BE    *+12                IF CANT FIND LEDGER FOR ANALYSIS             
         OI    ACXERRS,ACXNOREC    SET ERROR AND EXIT                           
         B     XIT                                                              
*                                                                               
         MVC   TAX2PAC,MYSPACE                                                  
         MVC   TAX2PAC,SVCOMP                                                   
         MVC   TAX2PAC+1(2),=C'2P'                                              
         LA    R3,ACCNT                                                         
         MVC   ACCNT,MYSPACE                                                    
         LA    RF,SVOFFANL         ANALYSIS OFFICE                              
         OC    SVFINOFF,SVFINOFF   IS THERE A FINANCIAL OFFICE?                 
         BZ    *+8                                                              
         LA    RF,SVFINOFF         THEN USE THAT INSTEAD                        
         OC    SVCPOFF,SVCPOFF     IS THERE A CLIENT/PRODUCT OFFICE?            
         BZ    *+8                                                              
         LA    RF,SVCPOFF          THEN USE THAT INSTEAD                        
         ZIC   R1,LDGLVA                                                        
         BCTR  R1,0                                                             
         EXMVC R1,0(R3),SVOFFANL    OFFICE                                      
         LA    R3,1(R1,R3)                                                      
*                                                                               
         ZIC   R1,LDGLVB                                                        
         BCTR  R1,0                                                             
         EXMVC R1,0(R3),SVDEPANL    DEPARTMENT                                  
         LA    R3,1(R1,R3)                                                      
*                                                                               
         ZIC   R1,LDGLVC                                                        
         BCTR  R1,0                                                             
         EXMVC R1,0(R3),SVSTFANL   STAFF                                        
         OC    ACCNT,MYSPACE                                                    
         MVC   TAX2PAC+3(12),ACCNT                                              
*                                                                               
TXJ75    GOTO1 =A(GETLDG),DMCB,(RC),C'29'                                       
         BE    *+12                IF CANT FIND LEDGER FOR ANALYSIS             
         OI    ACXERRS,ACXNOREC    SET ERROR AND EXIT                           
         B     XIT                                                              
         MVC   TAX29AC,MYSPACE                                                  
         MVC   TAX29AC(1),SVCOMP                                                
         MVC   TAX29AC+1(2),=C'29'                                              
         MVC   TAX29AC+3(12),TAX1CAC+3 COPY THE 1C ACC TO THE 29                
*                                                                               
* BUILD 2P CONTRA ACCOUNT                                                       
*                                                                               
         MVC   TAX2PCAC,MYSPACE                                                 
         MVI   TAX2PCAC,C'*'                                                    
         MVC   TAX2PCAC+1(12),TAXSEAC+3                                         
*                                                                               
* CONTRA-ACCOUNT IS *(SE A/C CODE)-(29 A/C CODE) FOR THE TENO OPTION            
* ONLY 'N' BYTES FROM THE RIGHT HAND SIDE OF THE SE A/C CODE ARE USED           
*                                                                               
         LA    R3,L'ACCNT          COMPUTE LENGTH OF SE ACCOUNT CODE            
         LA    RE,TAX2PCAC+L'ACCNT                                              
         CLI   0(RE),C' '                                                       
         BH    *+10                                                             
         BCTR  RE,0                                                             
         BCT   R3,*-10                                                          
*                                                                               
         CLI   SVCPYTNO,X'F0'      TEST FOR USING RHS OF SE CODE                
         BL    TXJ80               NO                                           
*                                                                               
         PACK  DUB,SVCPYTNO(1)                                                  
         CVB   R1,DUB                                                           
         MVC   TAX2PCAC+1(14),MYSPACE                                           
         LR    RE,R3               RE=L'SE ACCOUNT CODE                         
         SR    RE,R1               RE=DISP TO RHS OF CODE                       
         LA    RE,TAXSEAC+3(RE)    RE=A(RHS)                                    
         BCTR  R1,0                                                             
         EX    R1,*+4                                                           
         MVC   TAX2PCAC+1(0),0(RE) EXTRACT RHS OF SE A/C CODE                   
         LA    R3,1(R1)            R3=L'RHS OF SE A/C CODE                      
*                                                                               
TXJ80    LA    RF,TAX2PCAC+1(R3)   RF=A(END OF A/C CODE)                        
         MVI   0(RF),C'-'          INSERT A DASH AFTER IT                       
         LA    R1,TAX2PCAC+L'TAX2PCAC-1  R1=A(END OF A/C)                       
         SR    R1,RF               L'REST OF KEY                                
         BCTR  R1,0                                                             
         EXMVC R1,0(RF),TAX29AC+3  ATTACH 29 A/C CODE                           
*                                                                               
* BUILD 29 CONTRA ACCOUNT                                                       
*                                                                               
         MVC   TAX29CAC,TAX2PCAC   COPY CAC FROM DEBIT POSTING                  
         LA    RF,TAX29CAC+1(R3)   POINT TO END OF EXPENSE CODE                 
         MVI   0(RF),C'-'                                                       
         LA    R1,TAX29CAC+L'TAX29CAC-1                                         
         SR    R1,RF               R1=L'REST OF KEY                             
         BCTR  R1,0                                                             
         EX    R1,*+4                                                           
         MVC   1(0,RF),TAX2PAC+3   ATTACH REST OF 2P CODE                       
*                                                                               
         LA    R2,TAX2PAC                                                       
         GOTO1 =A(GETNAME),DMCB,(RC),(R2)                                       
         BE    *+12                                                             
         OI    ACXERRS,ACXNOREC                                                 
         B     XIT                                                              
         MVC   TAX2PCNM,ACNAME    USE THE 2P A/C NAME AS THE CAC NAME           
         MVC   TAX29CNM,ACNAME                                                  
         EJECT                                                                  
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*         
* COST ANALYZED - POST DR TO 1P AND CR TO 1C                                    
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*         
TXJ90    DS    0H                                                               
         TM    BIT,COSTANL         SE ACCOUNT ANALYZED BY COST?                 
         BZ    TXJ100              NO                                           
*                                                                               
         GOTO1 =A(GETLDG),DMCB,(RC),C'1P'                                       
         BE    *+12                IF CANT FIND LEDGER FOR ANALYSIS             
         OI    ACXERRS,ACXNOREC    SET ERROR AND EXIT                           
         B     XIT                                                              
*                                                                               
         MVC   TAX1PAC,MYSPACE                                                  
         MVC   TAX1PAC(1),SVCOMP                                                
         MVC   TAX1PAC+1(2),=C'1P'                                              
         LA    R2,TAX1PAC+3                                                     
         TM    BIT3,NEWCOST         ON NEW COST?                                
         BZ    TXJ92                                                            
         MVC   0(12,R2),=C'999999999999'                                        
         B     TXJ96                                                            
*                                                                               
TXJ92    LA    R3,=C'9999'                                                      
         TM    BIT3,OFFREQ          TEST OFFICES REQUIRED                       
         BZ    *+8                 NO                                           
         LA    R3,SVOFFANL                                                      
         ZIC   R1,LDGLVA                                                        
         BCTR  R1,0                                                             
         EX    R1,*+4                                                           
         MVC   0(0,R2),0(R3)                                                    
         LA    R2,1(R1,R2)         NEXT KEY POSITION                            
*                                                                               
TXJ94    LA    R3,=C'9999'         DEFAULT DEPARTMENT                           
         OC    SVDEPANL,SVDEPANL   TEST FOR DEPARTMENT ANALYSIS                 
         BZ    *+8                                                              
         LA    R3,SVDEPANL                                                      
         ZIC   R1,LDGLVB                                                        
         BCTR  R1,0                                                             
         EX    R1,*+4                                                           
         MVC   0(0,R2),0(R3)                                                    
         LA    R2,1(R1,R2)                                                      
         MVC   0(1,R2),SVCSTGRP    COSTING GROUP                                
*                                                                               
TXJ96    MVC   TAX1PCAC,TAX1CAC    COPY 1C TO 1P CONTRA                         
         LA    R2,TAX1PCAC                                                      
         GOTO1 =A(GETNAME),DMCB,(RC),(R2)                                       
         BE    *+12                IF CANT FIND LEDGER FOR ANALYSIS             
         OI    ACXERRS,ACXNOREC    SET ERROR AND EXIT                           
         B     XIT                                                              
         MVC   TAX1PCNM,ACNAME     GET CONTRA NAME                              
         EJECT                                                                  
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*         
* CREATE POSTINGS                                                               
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*         
*                                                                               
TXJ100   DS    0H                                                               
         MVI   BIT2,0                                                           
         NI    BIT3,X'FF'-PROC2P                                                
*                                                                               
* SJ ACCOUNT                                                                    
*                                                                               
         OI    BIT2,PROCSJ         SET BIT THAT PROCESSING SJ                   
         MVC   TMPACC,TAXSJAC                                                   
         MVC   TMPCACC,TAXSJCAC    MOVE ACCOUNTS INTO TEMP FIELDS               
         MVC   TMPCNAME,TAXSJCNM                                                
         MVC   TMPANLCD,SVWORKCD   MOVE WORK CODE INTO TEMP FIELD               
         BAS   RE,BLDWRKR                                                       
         BNE   XIT                 IF THERE WAS AN ERROR -EXIT                  
*                                                                               
* SV ACCOUNT                                                                    
*                                                                               
         MVI   BIT2,0                                                           
         NI    BIT3,X'FF'-PROC2P                                                
         OI    BIT2,PROCSV                                                      
         MVC   TMPACC,TAXSVAC                                                   
         MVC   TMPCACC,TAXSVCAC    MOVE ACCOUNT INTO TEMP FIELDS                
         MVC   TMPCNAME,TAXSVCNM                                                
         MVC   TMPANLCD,SVFINOFF   MOVE FINANCIAL OFFICE INTO TEMP              
         OC    SVFINOFF,SVFINOFF   NO FIN OFFICE THEN MOVE IN CLI/PRO           
         BNZ   *+10                OFFICE IN                                    
         MVC   TMPANLCD,SVCPOFF                                                 
         BAS   RE,BLDWRKR                                                       
         BNE   XIT                 IF THERE WAS AN ERROR JUST EXIT              
*                                                                               
* SE ACCOUNT                                                                    
*                                                                               
         MVI   BIT2,0                                                           
         NI    BIT3,X'FF'-PROC2P                                                
         OI    BIT2,PROCSE                                                      
         MVC   TMPACC,TAXSEAC                                                   
         MVC   TMPCACC,TAXSECAC    MOVE ACCOUNTS INTO TEMP FIELDS               
         MVC   TMPCNAME,TAXSECNM                                                
         MVC   TMPANLCD,SVFINOFF   MOVE FINANCIAL OFFICE INTO TEMP FLD          
         OC    SVFINOFF,SVFINOFF   NO FIN OFFICE THEN MOVE IN CLI/PRO           
         BNZ   *+10                OFFICE IN                                    
         MVC   TMPANLCD,SVCPOFF                                                 
         BAS   RE,BLDWRKR                                                       
         BNE   XIT                 IF THERE WAS AN ERROR JUST EXIT              
*                                                                               
         TM    BIT,DEPANL          SE ACCT ANALYZED BY DEPT?                    
         BZ    TXJ105              NO                                           
*                                                                               
* 2D ACCOUNT                                                                    
*                                                                               
         MVI   BIT2,0                                                           
         NI    BIT3,X'FF'-PROC2P                                                
         OI    BIT2,PROC2D                                                      
         MVC   TMPACC,TAX2DAC                                                   
         MVC   TMPCACC,TAX2DCAC    MOVE ACCOUNTS INTO TEMP FIELDS               
         MVC   TMPCNAME,TAX2DCNM                                                
         MVC   TMPANLCD,SVFINOFF   MOVE FINANCIAL OFFICE INTO TEMP FLD          
         OC    SVFINOFF,SVFINOFF   NO FIN OFFICE THEN MOVE IN CLI/PRO           
         BNZ   *+10                OFFICE IN                                    
         MVC   TMPANLCD,SVCPOFF                                                 
         BAS   RE,BLDWRKR                                                       
         BNE   XIT                 IF THERE WAS AN ERROR JUST EXIT              
*                                                                               
* 28 ACCOUNT                                                                    
*                                                                               
         MVI   BIT2,0                                                           
         NI    BIT3,X'FF'-PROC2P                                                
         OI    BIT2,PROC28                                                      
         MVC   TMPACC,TAX28AC                                                   
         MVC   TMPCACC,TAX28CAC    MOVE ACCOUNTS INTO TEMP FIELDS               
         MVC   TMPCNAME,TAX28CNM                                                
         MVC   TMPANLCD,SVFINOFF   MOVE FINANCIAL OFFICE INTO TEMP FLD          
         OC    SVFINOFF,SVFINOFF   NO FIN OFFICE THEN MOVE IN CLI/PRO           
         BNZ   *+10                OFFICE IN                                    
         MVC   TMPANLCD,SVCPOFF                                                 
         BAS   RE,BLDWRKR                                                       
         BNE   XIT                 IF THERE WAS AN ERROR JUST EXIT              
*                                                                               
TXJ105   TM    BIT,STAFANL         SE ACCT ANALYZED BY STAFF?                   
         BZ    TXJ110              NO                                           
*                                                                               
* 2P ACCOUNT                                                                    
*                                                                               
         MVI   BIT2,0                                                           
         NI    BIT3,X'FF'-PROC2P                                                
         OI    BIT3,PROC2P                                                      
         MVC   TMPACC,TAX2PAC                                                   
         MVC   TMPCACC,TAX2PCAC    MOVE ACCOUNTS INTO TEMP FIELDS               
         MVC   TMPCNAME,TAX2PCNM                                                
         MVC   TMPANLCD,SVFINOFF   MOVE FINANCIAL OFFICE INTO TEMP FLD          
         OC    SVFINOFF,SVFINOFF   NO FIN OFFICE THEN MOVE IN CLI/PRO           
         BNZ   *+10                OFFICE IN                                    
         MVC   TMPANLCD,SVCPOFF                                                 
         BAS   RE,BLDWRKR                                                       
         BNE   XIT                 IF THERE WAS AN ERROR JUST EXIT              
*                                                                               
* 29 ACCOUNT                                                                    
*                                                                               
         MVI   BIT2,0                                                           
         NI    BIT3,X'FF'-PROC2P                                                
         OI    BIT2,PROC29                                                      
         MVC   TMPACC,TAX29AC                                                   
         MVC   TMPCACC,TAX29CAC    MOVE ACCOUNTS INTO TEMP FIELDS               
         MVC   TMPCNAME,TAX29CNM                                                
         MVC   TMPANLCD,SVFINOFF   MOVE FINANCIAL OFFICE INTO TEMP FLD          
         OC    SVFINOFF,SVFINOFF   NO FIN OFFICE THEN MOVE IN CLI/PRO           
         BNZ   *+10                OFFICE IN                                    
         MVC   TMPANLCD,SVCPOFF                                                 
         BAS   RE,BLDWRKR                                                       
         BNE   XIT                 IF THERE WAS AN ERROR JUST EXIT              
*                                                                               
TXJ110   TM    BIT,COSTANL         SE ACCT ANALYZED BY STAFF?                   
         BZ    TXJ115                                                           
*                                                                               
* 1P ACCOUNT                                                                    
*                                                                               
         MVI   BIT2,0                                                           
         NI    BIT3,X'FF'-PROC2P                                                
         OI    BIT2,PROC1P                                                      
         MVC   TMPACC,TAX1PAC                                                   
         MVC   TMPCACC,TAX1PCAC    MOVE ACCOUNTS INTO TEMP FIELDS               
         MVC   TMPCNAME,TAX1PCNM                                                
         MVC   TMPANLCD,SVFINOFF   MOVE FINANCIAL OFFICE INTO TEMP FLD          
         OC    SVFINOFF,SVFINOFF   NO FIN OFFICE THEN MOVE IN CLI/PRO           
         BNZ   *+10                OFFICE IN                                    
         MVC   TMPANLCD,SVCPOFF                                                 
         BAS   RE,BLDWRKR                                                       
         BNE   XIT                 IF THERE WAS AN ERROR JUST EXIT              
*                                                                               
* 1C ACCOUNT                                                                    
*                                                                               
         MVI   BIT2,0                                                           
         NI    BIT3,X'FF'-PROC2P                                                
         OI    BIT2,PROC1C                                                      
         MVC   TMPACC,TAX1CAC                                                   
         MVC   TMPCACC,TAX1CCAC    MOVE ACCOUNTS INTO TEMP FIELDS               
         MVC   TMPCNAME,TAX1CCNM                                                
         MVC   TMPANLCD,SVFINOFF   MOVE FINANCIAL OFFICE INTO TEMP FLD          
         OC    SVFINOFF,SVFINOFF   NO FIN OFFICE THEN MOVE IN CLI/PRO           
         BNZ   *+10                OFFICE IN                                    
         MVC   TMPANLCD,SVCPOFF                                                 
         BAS   RE,BLDWRKR                                                       
         BNE   XIT                 IF THERE WAS AN ERROR JUST EXIT              
*                                                                               
TXJ115   DS    0H                                                               
         L     R3,ACXABUFF                                                      
         AH    R3,STDISP           GO TO NEXT AVAILABLE ENTRY                   
         MVC   0(2,R3),=X'0000'    MARK END OF POSTINGS WITH ZERO LEN           
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* READ COMPANY REC FOR INFO FROM 10 AND 30 ELEMENTS                   *         
***********************************************************************         
*                                                                               
         USING ACTRECD,R6                                                       
RDCOMP   NTR1                                                                   
         MVC   SVCSTGRP,MYSPACE                                                 
         MVC   SVCPYTNO,MYSPACE                                                 
         LA    R6,KEY                                                           
         MVC   ACTKEY,MYSPACE                                                   
         MVC   ACTKCPY,SVCOMP                                                   
         MVC   SVKEY,KEY                                                        
         GOTO1 CDATAMGR,DMCB,=C'DMREAD',=C'ACCDIR',KEY,KEY                      
         CLC   SVKEY(L'ACTKEY),KEY       CO REC MUST EXIST                      
         BE    *+12                                                             
         OI    ACXERRS,ACXNOREC    SET ERROR AND EXIT                           
         B     XNO                                                              
         MVC   DA,ACTKDA                                                        
         GOTO1 CDATAMGR,DMCB,=C'GETREC',=C'ACCMST',DA,AIO1,WORK                 
         L     R6,AIO1                                                          
         AH    R6,=Y(ACTRFST-ACTRECD)                                           
*                                                                               
RD10     CLI   0(R6),0                                                          
         BE    RD40                                                             
         CLI   0(R6),CPYELQ        COMPANY ELEMENT                              
         BE    RD20                                                             
         CLI   0(R6),RSTELQ        STATUS ELEMENT                               
         BE    RD30                                                             
*                                                                               
RD15     SR    RF,RF                                                            
         IC    RF,1(R6)                                                         
         AR    R6,RF                                                            
         B     RD10                                                             
*                                                                               
         USING CPYELD,R6                                                        
RD20     MVC   SVCPYTNO,CPYTENO        SAVE NUMBER FOR RHS SE CODE              
         TM    CPYSTAT5,CPYSNCST   NEW COST IN USE                              
         BZ    *+8                                                              
         OI    BIT3,NEWCOST                                                     
         TM    CPYSTAT1,CPYSOROE   OFFICE REQUIRED ON EXPENSE                   
         BZ    *+8                                                              
         OI    BIT3,OFFREQ                                                      
         B     RD15                                                             
*                                                                               
         USING RSTELD,R6                                                        
RD30     MVC   SVCSTGRP,RSTCOSTG  SAVE COSTING GROUP                            
         B     RD15                                                             
*                                                                               
RD40     B     XYES                                                             
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
* CHECK WHETHER ACCOUNT IS ELEGIBLE FOR POSTING                       *         
***********************************************************************         
*                                                                               
CHKSTAT  NTR1                                                                   
         TM    ACCSTAT,ACCSABAL    TEST FOR BALANCE ELEMENT                     
         BZ    CHKNO               NO                                           
         TM    ACCSTAT,ACCSLOCK    TEST FOR LOCKED ACCOUNT                      
         BO    CHKNO               NO-ACCOUNT IS OK                             
*                                                                               
         B     XYES                                                             
CHKNO    B     XNO                                                              
         EJECT                                                                  
***********************************************************************         
* ROUTINE BUILDS ELEMENTS IN BUFFER FOR TALENT TO ADD TO WORKER FILE            
***********************************************************************         
*                                                                               
         USING UKRECD,R3                                                        
BLDWRKR  NTR1                                                                   
         L     R3,ACXABUFF                                                      
         AH    R3,STDISP           GO TO NEXT AVAILABLE ENTRY                   
         MVI   4(R3),X'00'                                                      
         MVC   0(4,R3),=X'00050000' FULLWORD RECORD HEADER, H(LENGTH) &         
*                                                                               
         BAS   RE,ELM50HDR                                                      
         BNE   XNO                                                              
         BAS   RE,ELM44                                                         
         BNE   XNO                                                              
         BAS   RE,ELM9D                                                         
         BNE   XNO                                                              
         TM    BIT2,PROCSV                                                      
         BZ    *+8                                                              
         BAS   RE,ELM23                                                         
         BNE   XNO                                                              
         TM    BIT2,PROCSJ                                                      
         BZ    *+8                 50 IS FOR SJ ONLY                            
         BAS   RE,ELM50            SUBSIDIARY CASH INFO ELEMENT                 
         BNE   XNO                                                              
         TM    BIT2,PROCSJ+PROCSV  4C IS FOR SJ AND SV ONLY                     
         BZ    *+8                                                              
         BAS   RE,ELM4C                                                         
         BNE   XNO                                                              
         BAS   RE,ELM60                                                         
         BNE   XNO                                                              
         TM    BIT2,PROCSJ+PROCSV  65 IS FOR SE & ANLYSIS POSTINGS ONLY         
         BNZ   BLDW10                                                           
         OC    SVOFFANL,SVOFFANL   AND ONLY IF ANALYSIS OFFICE DEFINED          
         BZ    *+8                                                              
         BAS   RE,ELM65                                                         
BLDW10   TM    BIT2,PROCSJ+PROCSE+PROC1C   C0 IS FOR SJ,SE AND 1C ONLY          
         BZ    *+12                                                             
         BAS   RE,BLDTAB           BUILD TABLE OF SUBSIDIARY ACCOUNTS           
         BAS   RE,ELMC0                                                         
         BNE   XNO                                                              
*                                                                               
         AP    RECCNT,=P'1'                                                     
*                                                                               
         B     XYES                                                             
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* BUILD POSTING HEADER ELEMENT                                        *         
***********************************************************************         
*                                                                               
         USING PSHEADD,R2                                                       
ELM50HDR NTR1                                                                   
         LA    R2,ELEMENT                                                       
         XC    ELEMENT,ELEMENT                                                  
         MVI   PSHDEL,PSHDELQ      X'50' ELEMENT CODE                           
         MVI   PSHDLEN,PSHEADL     LENGTH X'46'                                 
         MVC   PSHDACC,MYSPACE                                                  
         MVC   PSHDACC,TMPACC      ACCOUNT                                      
         MVC   PSHDANAL,MYSPACE                                                 
         MVC   PSHDANAL,TMPANLCD   ANALYSIS CODE                                
         MVC   PSHDSBAC,MYSPACE                                                 
         MVC   PSHDSBAC,TMPCACC    CONTRA                                       
         MVC   PSHDSBNM,MYSPACE                                                 
         MVC   PSHDSBNM,TMPCNAME   CONTRA NAME                                  
         BAS   RE,ADDEL                                                         
         BNE   XNO                                                              
         B     XYES                                                             
         DROP  R2                                                               
         SPACE 2                                                                
***********************************************************************         
* BUILD 44 TRANSACTION ELEMENT                                        *         
***********************************************************************         
*                                                                               
         USING TRNELD,R2                                                        
ELM44    NTR1                                                                   
         LA    R2,ELEMENT                                                       
         XC    ELEMENT,ELEMENT                                                  
         MVC   ELEMENT(TRNLN1Q),SV44ELM MOVE IN SAVED ELEMENT FROM SJ           
         MVI   TRNLN,TRNLN1Q       SET LENGTH                                   
         TM    BIT2,PROCSV         IF PROCESSING SV DON'T ADD ANY NARR          
         BO    E4402               FOR TALENT                                   
         MVC   TRNNARR(2),MYSPACE   ADD ON 2 BYTES OF NARRATIVE                 
         LA    R1,TRNLN1Q+2                                                     
         STC   R1,TRNLN            RESET LENGTH                                 
E4402    AP    RECCASH,TRNAMNT     KEEP RUNNING TOTAL OF DR'S                   
         TM    BIT2,PROCSJ         IS THIS THE SJ POSTING?                      
         BZ    E4405               YES THEN CLEAR THE AMOUNT FIELD              
         ZAP   SVTRAMT,TRNAMNT     AMT WILL BE STORED IN THE 50 ELEMENT         
         ZAP   TRNAMNT,=P'0'                                                    
E4405    MVC   TRNOFFC,TMPANLCD                                                 
         NI    TRNSTAT,X'FF'-TRNSDR DEFAULT TO CREDIT                           
         TM    BIT2,PROCSV         SV=CREDIT                                    
         BO    E4410                                                            
         TM    BIT2,PROC28         28=CREDIT                                    
         BO    E4410                                                            
         TM    BIT2,PROC1C         1C=CREDIT                                    
         BO    E4410                                                            
         TM    BIT2,PROC29         29=CREDIT                                    
         BO    E4410                                                            
         OI    TRNSTAT,TRNSDR      NO THEN TURN ON BIT FOR DEBIT                
*                                                                               
E4410    BAS   RE,ADDEL                                                         
         BNE   XNO                                                              
         B     XYES                                                             
         DROP  R2                                                               
         SPACE 2                                                                
***********************************************************************         
* BUILD EMPTY 9D TALENT SYSTEM ELEMENT                                *         
***********************************************************************         
*                                                                               
         USING TSDELD,R2                                                        
ELM9D    NTR1                                                                   
         LA    R2,ELEMENT                                                       
         XC    ELEMENT,ELEMENT                                                  
         MVI   TSDEL,TSDELQ                                                     
         MVI   TSDLN,TSDLNQ                                                     
         BAS   RE,ADDEL                                                         
         BNE   XNO                                                              
         B     XYES                                                             
         DROP  R2                                                               
         SPACE 2                                                                
***********************************************************************         
* BUILD 23 'OTHERS' ELEMENT FOR SV ACCOUNT                            *         
***********************************************************************         
*                                                                               
         USING OTHELD,R2                                                        
ELM23    NTR1                                                                   
         LA    R2,ELEMENT                                                       
         XC    ELEMENT,ELEMENT                                                  
         MVI   OTHEL,OTHELQ                                                     
         MVI   OTHLN,OTHLN1Q                                                    
         GOTO1 =A(GETLDG),DMCB,(RC),C'SJ'                                       
         BNE   XNO                                                              
         ZIC   R1,LDGLVB                                                        
         BCTR  R1,0                                                             
         EXMVC R1,OTHNUM,SVPROD                                                 
         ZIC   R1,LDGLVC                                                        
         BCTR  R1,0                                                             
         EXMVC R1,OTHNUM+6,SVJOB                                                
         BAS   RE,ADDEL                                                         
         BNE   XNO                                                              
         B     XYES                                                             
         DROP  R2                                                               
         SPACE 2                                                                
***********************************************************************         
* BUILD 50 SUBSIDIARY CASH FLOW ELEMENT                               *         
***********************************************************************         
*                                                                               
         USING SCIELD,R2                                                        
ELM50    NTR1                                                                   
         LA    R2,ELEMENT                                                       
         XC    ELEMENT,ELEMENT                                                  
         MVI   SCIEL,SCIELQ                                                     
         MVI   SCILN,SCILN1Q                                                    
         MVI   SCITYPE,SCITSJXP                                                 
         ZAP   SCIAMNT,SVTRAMT                                                  
         BAS   RE,ADDEL                                                         
         BNE   XNO                                                              
         B     XYES                                                             
         DROP  R2                                                               
         SPACE 2                                                                
***********************************************************************         
* BUILD 4C SUBSIDIARY POSINTG ELEMENT                                 *         
***********************************************************************         
*                                                                               
         USING SPDELD,R2                                                        
ELM4C    NTR1                                                                   
         LA    R2,ELEMENT                                                       
         XC    ELEMENT,ELEMENT                                                  
         MVI   SPDEL,SPDELQ                                                     
         MVC   SPDLN,=YL1(L'ACTKCULA-1+SPDLN1Q)                                 
         TM    BIT2,PROCSJ         THE SJ GETS THE SV                           
         BZ    *+14                                                             
         MVC   SPDACCS(L'ACTKCULA-1),TAXSVAC+1                                  
         B     *+10                                                             
         MVC   SPDACCS(L'ACTKCULA-1),TAXSEAC+1  THE SV GETS THE SE              
         BAS   RE,ADDEL                                                         
         BNE   XNO                                                              
         B     XYES                                                             
         DROP  R2                                                               
         SPACE 2                                                                
***********************************************************************         
* BUILD EMPTY 60 TRANSACTION STATUS ELEMENT WITH XJOB BIT SET         *         
***********************************************************************         
*                                                                               
         USING TRSELD,R2                                                        
ELM60    NTR1                                                                   
         LA    R2,ELEMENT                                                       
         XC    ELEMENT,ELEMENT                                                  
         MVI   TRSEL,TRSELQ                                                     
         MVI   TRSLN,TRSLNQ                                                     
         OI    TRSSTAT3,TRSSXJOB   SET AS XJOB                                  
         BAS   RE,ADDEL                                                         
         BNE   XNO                                                              
         B     XYES                                                             
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* BUILD 65 ANALYZED OFFICE ELEMENT                                    *         
***********************************************************************         
*                                                                               
         USING ANOELD,R2                                                        
ELM65    NTR1                                                                   
         LA    R2,ELEMENT                                                       
         XC    ELEMENT,ELEMENT                                                  
         MVI   ANOEL,ANOELQ                                                     
         MVI   ANOLN,ANOLNQ                                                     
         MVI   ANOTYPE,ANOTPER     PERSON OFFICE                                
         MVC   ANOOFFC,SVOFFANL                                                 
         BAS   RE,ADDEL                                                         
         BNE   XNO                                                              
         B     XYES                                                             
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* BUILD TABLE OF SUBSIDIARY ACCOUNTS FOR C0 ELEMENT                   *         
***********************************************************************         
*                                                                               
         USING AC0TABD,R4                                                       
BLDTAB   NTR1                                                                   
         L     R4,AAC0TAB                                                       
*                                                                               
         TM    BIT2,PROCSJ         DOING SJ POSTING?                            
         BO    BLD10               YES THAN DON'T ADD SJ ACC TO TABLE           
         OC    TAXSJAC,TAXSJAC                                                  
         BZ    BLD10                                                            
         OI    AC0STAT,AC0DB       TURN ON DEBIT BIT                            
         MVC   AC0ACT,TAXSJAC+1                                                 
         LA    R4,AC0LNQ(R4)       BUMP TABLE                                   
*                                                                               
BLD10    TM    BIT2,PROCSE         DOING SE POSTING?                            
         BO    BLD15               YES THAN DON'T ADD SV ACC TO TABLE           
         OC    TAXSVAC,TAXSVAC                                                  
         BZ    BLD15                                                            
         NI    AC0STAT,X'FF'-AC0DB  MARK AS CREDIT                              
         MVC   AC0ACT,TAXSVAC+1                                                 
         LA    R4,AC0LNQ(R4)                                                    
*                                                                               
BLD15    TM    BIT2,PROC1C         DOING THE 1C POSTING?                        
         BZ    BLD20               NO THEN DON'T ADD SE ACC TO TABLE            
         OC    TAXSEAC,TAXSEAC                                                  
         BZ    BLD20                                                            
         OI    AC0STAT,AC0DB                                                    
         MVC   AC0ACT,TAXSEAC+1                                                 
         LA    R4,AC0LNQ(R4)                                                    
*                                                                               
BLD20    TM    BIT,DEPANL          SE ACCT ANALYZED BY DEPT?                    
         BZ    BLD25               NO                                           
         OC    TAX2DAC,TAX2DAC                                                  
         BZ    BLD25                                                            
         OC    TAX28AC,TAX28AC                                                  
         BZ    BLD25                                                            
         OI    AC0STAT,AC0DB                                                    
         MVC   AC0ACT,TAX2DAC+1    2D                                           
         LA    R4,AC0LNQ(R4)                                                    
         NI    AC0STAT,X'FF'-AC0DB                                              
         MVC   AC0ACT,TAX28AC+1    28                                           
         LA    R4,AC0LNQ(R4)                                                    
*                                                                               
BLD25    TM    BIT,STAFANL         SE ACCT ANALYZED BY STAFF?                   
         BZ    BLD30                                                            
         OC    TAX2PAC,TAX2PAC                                                  
         BZ    BLD30                                                            
         OC    TAX29AC,TAX29AC                                                  
         BZ    BLD30                                                            
         OI    AC0STAT,AC0DB                                                    
         MVC   AC0ACT,TAX2PAC+1    2P                                           
         LA    R4,AC0LNQ(R4)                                                    
         NI    AC0STAT,X'FF'-AC0DB                                              
         MVC   AC0ACT,TAX29AC+1    29                                           
         LA    R4,AC0LNQ(R4)                                                    
*                                                                               
BLD30    TM    BIT,COSTANL         SE ACCT ANALYZED BY COST?                    
         BZ    BLDXX                                                            
         OC    TAX1PAC,TAX1PAC                                                  
         BZ    BLDXX                                                            
         OI    AC0STAT,AC0DB                                                    
         MVC   AC0ACT,TAX1PAC+1    1P                                           
         LA    R4,AC0LNQ(R4)                                                    
         TM    BIT2,PROC1C         PROCESSING 1C ACCCOUNT?                      
         BO    BLDXX               THEN DON'T ADD 1C TO TABLE                   
         OC    TAX1CAC,TAX1CAC                                                  
         BZ    BLDXX                                                            
         OC    TAX1CCAC,TAX1CCAC                                                
         BZ    BLDXX                                                            
         NI    AC0STAT,X'FF'-AC0DB                                              
         MVC   AC0ACT,TAX1CAC+1    1C                                           
         LA    R4,AC0LNQ(R4)                                                    
         OI    AC0STAT,AC0DB                                                    
         MVC   AC0ACT,TAX1CCAC+1    13                                          
         LA    R4,AC0LNQ(R4)                                                    
*                                                                               
BLDXX    MVI   0(R4),X'FF'         MARK END OF TABLE                            
         B     XIT                                                              
         DROP  R4                                                               
         SPACE 2                                                                
***********************************************************************         
* BUILD 4C SUBSIDIARY POSINTG ELEMENT                                 *         
***********************************************************************         
*                                                                               
         USING APEELD,R2                                                        
         USING AC0TABD,R4                                                       
ELMC0    NTR1                                                                   
         LA    R2,ELEMENT                                                       
         XC    ELEMENT,ELEMENT                                                  
         MVI   APEEL,APEELQ                                                     
         MVI   APELN,APELN1Q                                                    
         MVI   APENUM,0                                                         
         L     R4,AAC0TAB           R1=A(TABLE OF ANALYSIS ACCOUNTS)            
*                                                                               
EC005    CLI   0(R4),X'FF'         CHECK FOR END OF TABLE                       
         BE    EC010                                                            
*                                                                               
         SR    RE,RE                                                            
         IC    RE,APELN                                                         
         LA    RE,APEELD(RE)                                                    
         USING APENTRY,RE          RE=A(APEEL ENTRY)                            
         MVC   APENSTAT,AC0STAT                                                 
         MVC   APENACT,AC0ACT                                                   
         LA    RF,APENACT+L'APENACT-1                                           
         CLI   0(RF),C' '          LOCATE END OF ACCOUNT CODE                   
         BH    *+12                                                             
         MVI   0(RF),0             AND DROP TRAILING SPACES                     
         BCT   RF,*-12                                                          
         LA    RF,1(RF)                                                         
         SR    RF,RE               RF=L'SUB-ELEMENT                             
         STC   RF,APENLEN                                                       
         DROP  RE                                                               
*                                                                               
         SR    RE,RE               INCREMENT ELEMENT LENGTH                     
         IC    RE,APELN                                                         
         AR    RE,RF                                                            
         STC   RE,APELN                                                         
         IC    RE,APENUM           INCREMENT NUMBER OF SUB-ELEMENTS             
         LA    RE,1(RE)                                                         
         STC   RE,APENUM                                                        
*                                                                               
         LA    R4,AC0LNQ(R4)                                                    
         B     EC005                                                            
*                                                                               
EC010    BAS   RE,ADDEL                                                         
         BNE   XNO                                                              
         B     XYES                                                             
         DROP  R2,R4                                                            
         EJECT                                                                  
**********************************************************************          
* ROUTINE TO ADD AN ELEMENT IN SEQUENCE                              *          
*         R2=ADDR OF ELEMENT, R3=ADDR OF RECORD                      *          
**********************************************************************          
*                                                                               
ADDEL    NTR1                                                                   
         LR    R4,R3                                                            
         AH    R4,0(R3)                                                         
         BCTR  R4,0                                                             
         CLI   0(R4),X'00'                                                      
         BE    *+12                                                             
         OI    ACXERRS,ACXRECLN    BAD RECORD LENGTH, CANT FIND 00 EL           
         B     XNO                                                              
         SR    R1,R1                                                            
         IC    R1,1(R2)                                                         
         AH    R1,0(R3)                                                         
         LH    RF,ACXBLEN          LENGTH OF BUFFER                             
         CR    R1,RF                                                            
         BNH   *+12                                                             
         OI    ACXERRS,ACXEBUFF      SET ERROR FOR TALENT                       
         B     XNO                                                              
         SR    R1,R1                                                            
         IC    R1,1(R2)                                                         
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(R2)                                                    
         LA    R1,1(R1)                                                         
         AR    R4,R1                                                            
         MVI   0(R4),X'00'                                                      
         LA    R4,1(R4)            BUMP UP ONE MORE TO ADD THE '00' TO          
         L     RF,ACXABUFF         THE TOTAL LENGTH                             
         SR    R4,RF                                                            
         STH   R4,STDISP           SAVE DISPLACEMENT INTO BUFFER                
         LH    R4,0(R3)                                                         
         AR    R4,R1                                                            
         STH   R4,0(R3)                                                         
         B     XYES                                                             
*                                                                               
         EJECT                                                                  
***********************************************************************         
*              COMMON EXIT ROUTINES                                   *         
***********************************************************************         
*                                                                               
XYES     SR    RC,RC                                                            
XNO      LTR   RC,RC                                                            
XIT      XIT1                                                                   
*                                                                               
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*   READ LEDGER RECORD TO GET LEDGER LENGTHS                                    
***********************************************************************         
*                                                                               
GETLDG   NMOD1 0,*GETLDG*                                                       
         L     RC,0(R1)                                                         
*                                                                               
         XC    LDGLEVS,LDGLEVS                                                  
         LA    R4,KEY                                                           
         USING LDGRECD,R4                                                       
         MVC   LDGKEY,MYSPACE                                                   
         MVC   LDGKCPY,SVCOMP      COMPANY                                      
         MVC   LDGKUNT(2),4(R1)    UNIT/LEDGER                                  
         MVC   SVKEY,KEY                                                        
         GOTO1 CDATAMGR,DMCB,=C'DMRDHI',=C'ACCDIR',KEY,KEY                      
         CLC   SVKEY(L'LDGKEY),KEY                                              
         BE    *+12                NO LEDGER FOUND                              
         OI    ACXERRS,ACXNOREC    SET ERROR AND EXIT                           
         B     XNO                                                              
         MVC   DA,LDGKDA                                                        
         GOTO1 CDATAMGR,DMCB,=C'GETREC',=C'ACCMST',DA,AIO1,WORK                 
         L     R4,AIO1                                                          
*                                                                               
         AH    R4,=Y(LDGRFST-LDGRECD)                                           
         SR    R0,R0                                                            
         USING ACLELD,R4                                                        
GETLDG5  CLI   0(R4),0                                                          
         BE    GETLDGX             TEST EOR                                     
         CLI   0(R4),ACLELQ        X'16' ACCOUNT LENGTH ELEMENT                 
         BE    GETLDG10                                                         
         ZIC   RF,1(R4)                                                         
         AR    R4,RF                                                            
         B     GETLDG5                                                          
*                                                                               
GETLDG10 LR    R6,R4                                                            
         ZIC   R1,1(R6)            TOTAL EL LENGTH                              
         SH    R1,=Y(ACLLN1Q)      - CODE AND LENGTH                            
         SR    R0,R0                                                            
         SR    RF,RF                                                            
         LA    RF,L'ACLVALS(RF)    LENGTH OF EACH ACCT DESC AND LENGTH          
         DR    R0,RF                                                            
         STC   R1,NUMLEVS          SAVE NUMBER OF ACCOUNT LEVELS                
*                                                                               
         ZIC   RF,NUMLEVS                                                       
         AH    R6,=Y(ACLLN1Q)      BUMP TO FIRST LENGTH                         
         LA    RE,LDGLEVS          FIELD TO CONTAIN ALL 4 LENGTHS               
         SR    R1,R1               ACUMULATES HOW MUCH TO SUBTRACT              
         ZIC   R2,0(R6)                                                         
GETLDG15 STC   R2,0(RE)                                                         
         AR    R1,R2               ACCUMULATE LENGTHS                           
         LA    RE,1(RE)                                                         
         LA    R6,L'ACLVALS(R6)    NEXT LENGTH                                  
         ZIC   R2,0(R6)                                                         
         SR    R2,R1                                                            
         BCT   RF,GETLDG15                                                      
*                                                                               
GETLDGX  B     XYES                                                             
         DROP  R4                                                               
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*   GET ACCOUNT STATUS                                                          
*   PARM 2 - CO,UNIT,LEDGER AND ACCOUNT CODE                                    
***********************************************************************         
*                                                                               
GETSTAT  NMOD1 0,*GETSTA*                                                       
         L     RC,0(R1)                                                         
*                                                                               
         L     R2,4(R1)                                                         
         MVI   ACCSTAT,0                                                        
         USING ACTRECD,R6                                                       
         LA    R6,KEY                                                           
         MVC   ACTKEY,MYSPACE                                                   
         MVC   ACTKCPY(15),0(R2)                                                
         MVC   SVKEY,KEY                                                        
         GOTO1 CDATAMGR,DMCB,=C'DMREAD',=C'ACCDIR',KEY,KEY                      
         CLC   SVKEY(L'ACTKEY),KEY                                              
         BE    *+12                                                             
         OI    ACXERRS,ACXNOREC    SET ERROR AND EXIT                           
         B     XNO                                                              
         MVC   DA,ACTKDA                                                        
         GOTO1 CDATAMGR,DMCB,=C'GETREC',=C'ACCMST',DA,AIO1,WORK                 
         L     R6,AIO1                                                          
         AH    R6,=Y(ACTRFST-ACTRECD)                                           
*                                                                               
GST10    CLI   0(R6),0                                                          
         BE    GSTXX                                                            
         CLI   0(R6),RSTELQ                                                     
         BE    GST30                                                            
         CLI   0(R6),ABLELQ                                                     
         BE    GST40                                                            
*                                                                               
GST20    SR    RF,RF                                                            
         IC    RF,1(R6)                                                         
         AR    R6,RF                                                            
         B     GST10                                                            
*                                                                               
         USING RSTELD,R6                                                        
GST30    TM    RSTSTAT1,RSTSGPEI   ACCOUNT ANALYZED BY STAFF?                   
         BZ    *+8                                                              
         OI    BIT,STAFANL                                                      
*                                                                               
         TM    RSTSTAT1,RSTSACIC   ACCOUNT CLOSED?                              
         BZ    *+8                                                              
         OI    ACCSTAT,ACCSCLSE                                                 
*                                                                               
         TM    RSTSTAT1,RSTSACIL   ACCOUNT LOCKED?                              
         BZ    *+8                                                              
         OI    ACCSTAT,ACCSLOCK                                                 
*                                                                               
         TM    RSTSTAT1,RSTSEADD   ACCOUNT ANALYZED BY DEPT?                    
         BZ    *+8                                                              
         OI    BIT,DEPANL                                                       
*                                                                               
         TM    RSTSTAT2,RSTSYCST   ACCOUNT MAKES COST POSTINGS?                 
         BZ    *+8                                                              
         OI    BIT,COSTANL                                                      
         B     GST20                                                            
*                                                                               
         USING ABLELD,R6                                                        
GST40    OI    ACCSTAT,ACCSABAL    INDICATE BALANCE ELEMENT                     
         B     GST20                                                            
*                                                                               
GSTXX    B     XYES                                                             
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
*   GET ACCOUNT NAME                                                            
*   PARM 2 - CO,UNIT,LEDGER AND ACCOUNT CODE                                    
***********************************************************************         
*                                                                               
GETNAME  NMOD1 0,*GETNME*                                                       
         L     RC,0(R1)                                                         
*                                                                               
         L     R2,4(R1)                                                         
         USING ACTRECD,R6                                                       
         LA    R6,KEY                                                           
         MVC   ACTKEY,MYSPACE                                                   
         MVC   ACTKCPY(15),0(R2)                                                
         MVC   SVKEY,KEY                                                        
         GOTO1 CDATAMGR,DMCB,=C'DMREAD',=C'ACCDIR',KEY,KEY                      
         CLC   SVKEY(L'ACTKEY),KEY                                              
         BE    *+12                                                             
         OI    ACXERRS,ACXNOREC    SET ERROR AND EXIT                           
         B     XNO                                                              
         MVC   DA,ACTKDA                                                        
         GOTO1 CDATAMGR,DMCB,=C'GETREC',=C'ACCMST',DA,AIO1,WORK                 
         L     R6,AIO1                                                          
         AH    R6,=Y(ACTRFST-ACTRECD)                                           
*                                                                               
GNM10    CLI   0(R6),0                                                          
         BE    GNMXX                                                            
         CLI   0(R6),NAMELQ                                                     
         BE    GNM30                                                            
*                                                                               
GNM20    SR    RF,RF                                                            
         IC    RF,1(R6)                                                         
         AR    R6,RF                                                            
         B     GNM10                                                            
*                                                                               
         USING NAMELD,R6                                                        
GNM30    SR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   ACNAME(0),NAMEREC   SAVE THE NAME                                
         OC    ACNAME,MYSPACE                                                   
*                                                                               
GNMXX    B     XYES                                                             
         DROP  R6                                                               
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*   EXTERNAL ADDRESS LIST                                                       
***********************************************************************         
*                                                                               
ADCONS   DS    0F                                                               
         DC    A(IO1)                                                           
         DC    A(IO2)                                                           
         DC    A(AC0TAB)                                                        
         DC    A(WRKBUF)                                                        
         DC    A(GGOBLOCK)                                                      
         DC    A(GOXBLK)                                                        
         SPACE 5                                                                
***********************************************************************         
*   BUFFERS                                                                     
***********************************************************************         
*                                                                               
IO1      DS    0D                  IOAREA #1                                    
         DS    2000C                                                            
MAXACC   EQU   *-IO1                                                            
*                                                                               
IO2      DS    0D                  IOAREA #2                                    
         DS    2000C                                                            
*                                                                               
AC0TAB   DS    CL150               TABLE OF ACCTS TO BUILD C0 ELEMENT           
*                                                                               
WRKBUF   DS    CL4500              BUFFER FOR WORKER FILE                       
*                                                                               
GGOBLOCK DS    CL(L'GOBLOCK)       GOBLOCK FOR GETOPT                           
GOXBLK   DS    CL(L'GOXBLOCK)      EXTENSION BLOCK FOR GETOPT                   
*                                                                               
         EJECT                                                                  
***********************************************************************         
*   WORKING STORAGE DSECT                                                       
***********************************************************************         
LWSD     DSECT                                                                  
VTYPES   DS    0A                                                               
AIO1     DS    A                   IO AREA 1                                    
AIO2     DS    A                   IO AREA 2                                    
AAC0TAB  DS    A                   A(AC0TAB)                                    
AWRKBUF  DS    A                   ADDRESS OF WORKER BUFFER                     
AGOBLOCK DS    A                   ADDRESS OF GOBLOCK FOR GETOPT                
AGOXBLK  DS    A                   ADDR OF EXTENSION BLOCK FOR GETOPT           
VTYPLNQ  EQU   *-VTYPES                                                         
*                                                                               
ELEMENT  DS    CL256                                                            
WORK     DS    CL64                                                             
COMMAND  DS    CL8                                                              
DUB      DS    XL8                                                              
DMCB     DS    6F                                                               
STDISP   DS    H                                                                
DA       DS    XL4                 DISK ADDRESS                                 
ORIGINUM DS    XL2                 ORIGIN ID NUMBER                             
TODAYP   DS    XL3                 TODAY'S DATE PACKED                          
INDEX    DS    XL16                INDEX ENTRY FOR WKFILE                       
ACCNT    DS    CL12                TEMP ACCOUNT FIELD                           
KEY      DS    XL56                                                             
SVKEY    DS    XL42                                                             
SV44ELM  DS    XL(TRNLN1Q+2)       SAVED 44 ELEM FROM SJ REC + 2 BYTES          
*                                  NARRATIVE                                    
SVCOMP   DS    XL1                 SAVED COMPANY CODE                           
SVTRAMT  DS    PL6                 SAVED TRANSACTION AMOUNT                     
SVFINOFF DS    CL2                 SAVED WO FINANCIAL OFFICE                    
SVWORKCD DS    CL2                 SAVED WORKCODE FROM SJ                       
SVCPOFF  DS    CL2                 SAVED CLIENT OR PROD OFFICE                  
SVVENOFF DS    CL2                 SAVED VENDOR OFFICE                          
SVOFFANL DS    CL2                                                              
SVDEPANL DS    CL3                                                              
SVSTFANL DS    CL6                                                              
SVCPYTNO DS    XL1                 SAVED # OF BYTES FOR RHS OF EXPENSE          
SVCSTGRP DS    XL2                 SAVED COSTING GROUP                          
SVPROD   DS    CL3                 SAVED PRODUCT CODE                           
SVJOB    DS    CL6                 SAVED JOB CODE                               
*                                                                               
TAXSJAC  DS    CL15                SJ ACCOUNT CODE                              
TAXSJCAC DS    CL15                SJ CONTRA ACCOUNT CODE                       
TAXSJCNM DS    CL36                SJ CONTRA ACCOUNT NAME                       
TAXSVAC  DS    CL15                SV ACCOUNT CODE                              
TAXSVCAC DS    CL15                SV CONTRA ACCOUNT CODE                       
TAXSVCNM DS    CL36                SV CONTRA ACCOUNT NAME                       
TAXSEAC  DS    CL15                SE ACCOUNT CODE                              
TAXSECAC DS    CL15                SE CONTRA ACCOUNT CODE                       
TAXSECNM DS    CL36                SE CONTRA ACCOUNT NAME                       
TAX2DAC  DS    CL15                2D ACCOUNT CODE                              
TAX2DCAC DS    CL15                2D CONTRA ACCOUNT CODE                       
TAX2DCNM DS    CL36                2D CONTRA ACCOUNT NAME                       
TAX28AC  DS    CL15                28 ACCOUNT CODE                              
TAX28CAC DS    CL15                28 CONTRA ACCOUNT CODE                       
TAX28CNM DS    CL36                28 CONTRA ACCOUNT NAME                       
TAX1PAC  DS    CL15                1P ACCOUNT CODE                              
TAX1PCAC DS    CL15                1P CONTRA ACCOUNT CODE                       
TAX1PCNM DS    CL36                1P CONTRA ACCOUNT NAME                       
TAX1CAC  DS    CL15                1C ACCOUNT CODE                              
TAX1CCAC DS    CL15                1C CONTRA ACCOUNT CODE                       
TAX1CCNM DS    CL36                1C CONTRA ACCOUNT NAME                       
TAX2PAC  DS    CL15                2P ACCOUNT CODE                              
TAX2PCAC DS    CL15                2P CONTRA ACCOUNT CODE                       
TAX2PCNM DS    CL36                2P CONTRA ACCOUNT NAME                       
TAX29AC  DS    CL15                29 ACCOUNT CODE                              
TAX29CAC DS    CL15                29 CONTRA ACCOUNT CODE                       
TAX29CNM DS    CL36                29 CONTRA ACCOUNT NAME                       
*                                                                               
TMPACC   DS    CL15                TEMP ACCOUNT CODE                            
TMPCACC  DS    CL15                TEMP CONTRA ACCOUNT CODE                     
TMPCNAME DS    CL36                TEMP CONTRA ACCOUNT NAME                     
TMPANLCD DS    CL2                 TEMP WORK CODE FIELD                         
*                                                                               
BIT      DS    XL1                                                              
CHKCLI   EQU   X'80'               ALREADY CHECKED CLIENT REC                   
DEPANL   EQU   X'20'               SE ACCNT ANALYZED BY DEPT                    
STAFANL  EQU   X'10'               SE ACCNT ANALYZED BY STAFF                   
COSTANL  EQU   X'08'               SE ACCNT ANALYZED BY COST                    
*                                                                               
BIT2     DS    XL1                                                              
PROCSJ   EQU   X'80'               PROCESSING SJ ACCOUNT                        
PROCSV   EQU   X'40'               "      "   SV ACCOUNT                        
PROCSE   EQU   X'20'               "      "   SE ACCOUNT                        
PROC2D   EQU   X'10'               "      "   2D ACCOUNT                        
PROC28   EQU   X'08'               "      "   28 ACCOUNT                        
PROC1P   EQU   X'04'               "      "   1P ACCOUNT                        
PROC1C   EQU   X'02'               "      "   1C ACCOUNT                        
PROC29   EQU   X'01'               "      "   29 ACCOUNT                        
*                                                                               
BIT3     DS    XL1                                                              
PROC2P   EQU   X'80'               PROCESSING 2P ACCOUNT                        
NEWCOST  EQU   X'40'               AGENCY ON NEW COST                           
OFFREQ   EQU   X'20'               OFFICE REQUIRED ON EXPENSE(OLD COST)         
*                                                                               
ACCSTAT  DS    XL1                 ACCOUNT STATUS BYTE                          
ACCSABAL EQU   X'80'               ACCOUNT HAS BALANCE ELEMENT                  
ACCSCLSE EQU   X'40'               ACCOUNT IS CLOSED                            
ACCSLOCK EQU   X'20'               ACCOUNT IS LOCKED                            
*                                                                               
MYSPACE  DS    CL132                                                            
*                                                                               
NUMLEVS  DS    XL1                 NUMBER OF LEVELS FOR THIS LEDGER             
LDGLEVS  DS    0XL4                                                             
LDGLVA   DS    X                   LEDGER HIERARCHY                             
LDGLVB   DS    X                                                                
LDGLVC   DS    X                                                                
LDGLVD   DS    X                                                                
ACNAME   DS    CL36                ACCOUNT NAME                                 
RECCNT   DS    PL6                                                              
RECCASH  DS    PL6                                                              
CATBLK   DS    CL(CATLNQ)          CATCALL BLOCK AREA                           
*                                                                               
LWSX     EQU   *                                                                
         EJECT                                                                  
*                                                                               
GOBLOCKD DSECT                                                                  
       ++INCLUDE ACGOBLOCK                                                      
         EJECT                                                                  
GOXBLKD  DSECT                                                                  
       ++INCLUDE ACGOXBLOCK                                                     
*                                                                               
         EJECT                                                                  
       ++INCLUDE ACXJOBD                                                        
         EJECT                                                                  
*                                                                               
* ACCATCALLD                                                                    
* ACGENPOST                                                                     
* ACGENFILE                                                                     
* ACREPWORKD                                                                    
* DMWRKRK                                                                       
* DDMASTD                                                                       
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACCATCALLD                                                     
       ++INCLUDE ACGENPOST                                                      
       ++INCLUDE ACGENFILE                                                      
       ++INCLUDE DMWRKRK                                                        
       ++INCLUDE DDMASTD                                                        
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
         EJECT                                                                  
***********************************************************************         
*   TABLE OF ACCOUNTS FOR C0 ELEMENT DSECT                                      
***********************************************************************         
*                                                                               
AC0TABD  DSECT                                                                  
AC0STAT  DS    XL1                                                              
AC0DB    EQU   X'80'               ON=DEBIT                                     
AC0ACT   DS    CL(L'ACTKCULA-1)     ACCOUNT                                     
AC0LNQ   EQU   *-AC0TABD                                                        
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'030ACXJOB    05/01/02'                                      
         END                                                                    
