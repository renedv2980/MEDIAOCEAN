*          DATA SET DMRCVR     AT LEVEL 018 AS OF 08/12/20                      
*&&      SET   NOP=N                                                            
*CATALP DMRCVR                                                                  
         TITLE 'DMRCVR - ADD RECORDS TO RECOVERY FILE FOR SYSTEM'               
***********************************************************************         
* ON ENTRY R2 POINTS TO CALLERS DMCB AND R1 TO PARM LIST              *         
*                                                                     *         
* PARAM1 NOT USED                                                     *         
* PARAM2 A(REC)                                                       *         
* PARAM3 CONTROL/LENGTH                                               *         
* XL1    EXTERNAL FILE NUMBER (OR SYSTEM FOR EOT)                     *         
* XL1    01=COPY/02=CHANGE/03=ADD/81=PTRCOPY/82=PTRCHNG               *         
*        40=SPECIAL UPDATE,10=EXTRA DSKADDR IN PARAM7                 *         
*        FF=SPECIAL END OF TRANSACTION CALL                           *         
*        FE=SPECIAL COMMIT TRANSACTION CALL                           *         
* XL2    RECORD LENGTH                                                *         
* PARAM4 A(DTFPH) FOR DIRECT ACCESS FILES                             *         
* PARAM5 A(DISK ADDRESS)                                              *         
* PARAM6 A(I/O AREA FOR PUTREC COPIES)                                *         
* PARAM7 A(DISK ADDRESS) TO GO IN RECOVERY HDR IF PARAM3+1=X'10'      *         
***********************************************************************         
         PRINT NOGEN                                                            
DMRCVR   CSECT                                                                  
         NMOD1 WORKX-WORK,DMRCVR,R9,R8                                          
         USING DMCBD,R2            R2=A(CALLERS DMCB)                           
         USING WORK,RC                                                          
         LR    RA,R1               RA=A(PARAM LIST)                             
         USING PARAMS,RA                                                        
         MVC   SVINLEN,PARAM3+2    SAVE INPUT RECORD LENGTH                     
*                                                                               
         MVI   MVSFLAG,0           RESET OFFLINE MVS DATA SET FLAG              
         XC    FILEINFO,FILEINFO   CLEAR FILE INFO                              
         EJECT                                                                  
***********************************************************************         
* WORK OUT WHY WE HAVE BEEN CALLED                                    *         
***********************************************************************         
RCVXDA   TM    PARAM3+1,X'10'      SET EXTRA DISK ADDRESS IF PASSED             
         BZ    RCVXDA1                                                          
         CLI   PARAM3+1,X'FE'                                                   
         BNL   RCVXDA2                                                          
         NI    PARAM3+1,255-X'10'                                               
         L     RF,PARAM7           EXTRA DA IS IN PARAM 7                       
         MVC   XTRADA,0(RF)                                                     
RCVXDA1  TM    PARAM3+1,X'40'      SET SPECIAL UPDATE IF PASSED                 
         BZ    RCVXDA2                                                          
         CLI   PARAM3+1,X'FE'                                                   
         BNL   RCVXDA2                                                          
         NI    PARAM3+1,255-X'40'                                               
         OI    TRAILERS,X'40'      SET SPECIAL UPDATE FLAG IN TRAILER           
*                                                                               
RCVXDA2  MVC   P1(20),PARAM1       COPY FIRST FIVE PARAMS                       
         XC    P1+20(8),P1+20                                                   
         LT    R3,VSSB             SSB GIVES BASIC RECOVERY INFO                
         BNZ   *+8                                                              
         LA    R3,DFLTSSB          POINT TO DEFAULT IF NO SSB                   
         USING SSBD,R3                                                          
*                                                                               
RCVXDA3  CLC   SSBCNTL,NULLS       SET DSPACE VALUE FOR ENQ                     
         BE    RCVXDA4                                                          
         CLI   ENQDSPAC,C'?'       TEST FIRST ONLINE CALL                       
         BNE   RCVXDA5                                                          
         MVC   ENQDSPAC,SSBDSPAC   EXTRACT DSPACE FROM ONLINE SSB               
         XC    RECVHDR,RECVHDR                                                  
         BRAS  RE,BOHDREC          BUILD ONLINE JOB HEADER RECORD               
         B     RCVXDA5                                                          
*                                                                               
RCVXDA4  MVC   ENQDSPAC,SSODSPAC   EXTRACT DSPACE FROM OFFLINE SSB              
*                                                                               
RCVXDA5  CLI   PARAM3+1,X'82'                                                   
         BE    RCVX                EXIT IF POINTER CHANGE                       
         CLI   PARAM3+1,X'FE'                                                   
         BNL   RCVCLOSE            CLOSE/COMMIT TRANSACTION                     
*                                                                               
RCV0     LLC   RE,PARAM3           RE=FILE NUMBER                               
         LR    RF,RE                                                            
         SLL   RF,5                INDEX INTO 32-BYTE FILTAB                    
         A     RF,=V(FILTABL)                                                   
         USING FILTABD,RF                                                       
         MVC   KEYLEN,DMFLKEYL                                                  
         TM    DMFLTYP,DMFLIS      TEST I/S FILE                                
         BO    RCV0A                                                            
         MVC   DAFLAG,DMFLNUM      NO-SET DAFLAG TO FILE NUM IF DA FILE         
         B     RCV0B                                                            
RCV0A    TM    DMFLFLG1,DMFLIPTR                                                
         BZ    RCV0B                                                            
         OI    ISPTRS,DMFLIPTR     SET I/S FILE HAS INDIRECT POINTERS           
         MVC   ISPDSP,DMFLPDSP                                                  
         MVC   ISPARG,DMFLPARG                                                  
RCV0B    SRDL  RE,4                                                             
         SRL   RF,28                                                            
         STC   RE,EXTFILL          SAVE LEFT HALF OF FILE NUMBER                
         STC   RF,EXTFILR          SAVE RIGHT HALF OF FILE NUMBER               
         DROP  RF                                                               
*                                                                               
RCV0C    CLC   SSBCNTL,NULLS       TEST OFFLINE                                 
         BNE   RCVON               NO                                           
         CLI   DAFLAG,0            DA FILES GO TO RCV1 - FULL RECOVERY          
         BNE   RCV1                                                             
         TM    SSOMTIND,SSOFRCVN   NO UNWIND IF GLOBAL - FULL RECOVERY          
         BO    RCV1                                                             
         TM    SSOFLAG1,SSOFRCVR   UNWIND IF GLOBAL - FULL RECOVERY             
         BO    RCV1                                                             
         B     RCVOFF              OTHERWISE OFFLINE                            
         EJECT                                                                  
***********************************************************************         
* $CT UPDATES CTFILE PERSON AND USERID RECORDS WITH LAST LOGON DATA   *         
* IT SETS DMCB1=X'26' TO SAY MAINTENANCE WRITE WITH NO RECOVERY       *         
***********************************************************************         
RCVON    EQU   *                   SPECIAL CODE FOR ONLINE RECOVERY             
         CLI   PARAM3,X'A1'        TEST CTFILE                                  
         BNE   RCV1                                                             
         TM    0(R2),X'26'         TEST MAINT WRITE WITH NO RECOVERY            
         BNO   RCV1                                                             
         L     RF,PARAM2                                                        
RCVON1   CLI   0(RF),C'I'          TEST USERID RECORD                           
         BE    RCVON2                                                           
         CLC   0(2,RF),=X'C604'    TEST PERSON RECORD                           
         BNE   RCV1                                                             
RCVON2   L     RE,SSBTKADR         TEST CONNECT SERVICE REQUEST                 
         CLI   TCBPRG-TCBD(RE),X'10'                                            
         BNE   RCV1                NORMAL ONLINE RECOVERY                       
*                                                                               
RCVX     XC    PARAM3(2),PARAM3    NORMAL EXIT CLEAR ERROR FLAGS                
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* OFFLINE RECOVERY IS DRIVEN BY CONTROL FLAGS IN V(SSB)               *         
*                                                                     *         
* D/A FILE-ALWAYS READ COPY RECORD AS PUTREC ASSUMES THAT THE COPY    *         
* WILL BE LEFT IN THE I/O BUFFER AFTER CALL TO DMRCVR.                *         
*                                                                     *         
* I/S FILE-NO OFFLINE RECOVERY DONE UNLESS SPECIAL CODE FOR INDIV I/S *         
* FILES OR I/S RECORD IS AN INDIRECT POINTER.                         *         
***********************************************************************         
RCVOFF   EQU   *                   SPECIAL CODE FOR OFFLINE I/S FILES           
RCVOACC  CLI   PARAM3,X'61'        TEST ACCFILE                                 
         BNE   RCVOACCX                                                         
         TM    SSBSTAT2,SSBSROLC   TEST OFFLINE COPIES REQUIRED                 
         BO    RCV1                YES                                          
         CLI   PARAM3+1,1          NO IF THIS IS NOT A COPY PROCESS             
         BNE   RCV1                                                             
         L     RE,PARAM6           CLEAR RECOVERY HEADER                        
         AHI   RE,-(HDRQ)                                                       
         XC    0(HDRQ,RE),0(RE)                                                 
RCVOACCX EQU   *                                                                
*                                                                               
RCVOCTL  CLI   PARAM3,X'A1'        TEST CTFILE                                  
         BNE   RCVOCTLX                                                         
         TM    SSBSTAT2,SSBSROLC   TEST OFFLINE COPIES REQUIRED                 
         BO    RCV1                YES                                          
         CLI   PARAM3+1,1          NO IF THIS IS NOT A COPY PROCESS             
         BNE   RCV1                                                             
         L     RE,PARAM6           CLEAR RECOVERY HEADER                        
         AHI   RE,-(HDRQ)                                                       
         XC    0(HDRQ,RE),0(RE)                                                 
RCVOCTLX EQU   *                                                                
*                                                                               
RCVOFF2  L     RF,PARAM2           RF=A(RECOVERY RECORD)                        
*                                                                               
         CLC   0(L'EOFREC,RF),EOFREC CHECK DUMMY END OF JOB RECORD              
         BE    RCV2                ALLOW THIS I/S RECORD                        
         TM    SSBSTAT2,SSOSRWRK   RECOVER TO FACWRK                            
         BO    RCV2                YES CONTINUE WITH I/S FILE RECORDS           
*                                                                               
RCVOFF3  TM    SSBSTAT2,SSOSRSQF   RECOVER TO QSAM                              
         BZ    RCVOFF4             NO                                           
         CLI   SSOXTND,X'FF'       MUST HAVE EXTENDED SSB                       
         JNE   *+2                                                              
         CLC   SSOSQRTN,FONE       RECOVER VIA QSAM USER ROUTINE                
         BNE   RCV2                YES CONTINUE WITH I/S FILE RECORDS           
*                                                                               
RCVOFF4  EQU   *                                                                
*&&UK                                                                           
RCVOARC  CLI   PARAM3,X'69'        TEST ACCDIR                                  
         BNE   RCVOARCX                                                         
         LLC   RE,KEYLEN           KEY LEN = DISP TO I/S STATUS AREA            
         AR    RE,RF               A(STATUS AREA)                               
         TM    0(RE),X'04'         ARCHIVE PTR? (ACGENFILE/TRNSARCH)            
         BO    RCVOFF9             YES CONTINUE WITH I/S FILE RECORDS           
RCVOARCX EQU   *                                                                
*&&                                                                             
         TM    ISPTRS,DMFLIPTR     TEST IF I/S FILE HAS INDIRECT PTRS           
         BZ    RCVX                NO RECOVERY FOR NORMAL I/S RECS              
         LLC   RE,ISPDSP                                                        
         AR    RF,RE               RF=A(BYTE THAT DEFINES INDIRECT PTR)         
         CLC   0(1,RF),ISPARG                                                   
         BNE   RCVX                I/S RECORD IS NOT A DIRECT POINTER           
*                                                                               
RCVOFF9  TM    SSBSTAT2,SSBSROLC   TEST OFFLINE COPIES REQUIRED                 
         BO    RCV2                YES                                          
         CLI   PARAM3+1,1          NO-EXIT IF A COPY RECORD                     
         BE    RCVX                                                             
         B     RCV2                CONTINUE WITH I/S INDIRECT POINTER           
         EJECT                                                                  
***********************************************************************         
* READ FOR UPDATE THE DIRECT ACCESS FILE COPY RECORD                  *         
***********************************************************************         
RCV1     CLI   PARAM3+1,1          TEST COPY                                    
         BE    *+12                                                             
         CLI   PARAM3+1,X'81'      TEST POINTER COPY                            
         BNE   RCV2                                                             
         CLI   DAFLAG,0            TEST DA FILE                                 
         BE    RCV2                                                             
*                                                                               
RCV1B    MVC   SVDM1,0(R2)         SAVE CALLERS DMCB1 BYTE                      
         OI    0(R2),X'80'         SET READ FOR UPDATE                          
         MVC   P1,VREAD                                                         
         MVC   P2,PARAM6                                                        
         XC    P3,P3                                                            
         GOTO1 VDMOD000,P1         READ DA FILE COPY RECORD                     
*                                                                               
         MVC   0(1,R2),SVDM1                                                    
         OC    P3(2),P3            TEST FOR READ ERRORS                         
         BZ    *+14                                                             
         MVC   PARAM3(2),P3        PASS ERRORS TO CALLING PROGRAM               
         B     XMOD                                                             
*&&UK                                                                           
RCV1BS   CLI   PARAM3,X'42'        TEST MEDFILE                                 
         BNE   RCV1BSX                                                          
         L     RE,PARAM6           RE=A(COPY RECORD)                            
*                                                                               
RCV1BS1  CLI   1(RE),C'W'          TEST IF BUY SLEEPER                          
         BNE   RCV1BS2                                                          
         CLI   34(RE),X'FE'        FIRST ELEMENT IS X'FE'                       
         BNE   RCV1BSX                                                          
         MVC   20(2,RE),=H'37'     SET NEW SHORT REC LEN IN REC                 
         NI    33(RE),X'FE'        SET NO ACTIVITY DATA                         
         MVC   34(3,RE),=X'FE0200' SET SHORT FIRST SINGLE ELEMENT               
         MVC   P3+2(2),20(RE)                                                   
         MVC   PARAM3+2(2),20(RE)  SET NEW REC LEN IN PARAM LIST                
         B     RCV1BSX                                                          
*                                                                               
RCV1BS2  CLI   1(RE),C'S'          TEST IF IT WAS ONCE A BUY SLEEPER            
         BNE   RCV1BSX                                                          
         CLC   PARAM3+2(2),=H'1856' SLEEPER LEN=1850 PLUS ACTIVITY              
         BNE   RCV1BSX                                                          
         LLH   R1,20(RE)           GET BUY RECORD LOGICAL LENGTH                
         CHI   R1,1850                                                          
         BNL   RCV1BSX                                                          
         TM    33(RE),X'01'        TEST IF HAS ACTIVITY DATA                    
         BZ    *+8                                                              
         AHI   R1,6                                                             
         STH   R1,P3+2                                                          
         STH   R1,PARAM3+2         SET NEW REC LEN IN PARAM LIST                
*                                                                               
RCV1BSX  EQU   *                                                                
*&&                                                                             
RCV1C    CLC   SSBCNTL,NULLS       CONTINUE IF ONLINE                           
         BNE   RCV2                                                             
         TM    SSBSTAT2,(SSBSROLC+SSOSRWRK+SSOSRSQF)                            
         BNZ   RCV2                CONTINUE IF WANT COPIES/FACWRK/QSAM          
         TM    SSOFLAG1,SSOFRCVR                                                
         BNZ   RCV2                CONTINUE IF GLOBAL FULL RECOVERY             
*                                                                               
RCV1D    L     RE,PARAM6           DA COPY RECORD NOT REQUIRED                  
         AHI   RE,-(HDRQ)                                                       
         XC    0(HDRQ,RE),0(RE)    CLEAR RECOVERY HEADER                        
         B     RCVX                                                             
         EJECT                                                                  
***********************************************************************         
* BUILD RECOVERY FILE HEADER                                          *         
***********************************************************************         
RCV2     TM    SSBSTAT2,SSBSNRCV   EXIT IF RECOVERY NOT REQUIRED                
*&&UK*&& BO    RCVWARN1                                                         
*&&US*&& BO    RCVX                                                             
         XC    RECVHDR,RECVHDR                                                  
         MVC   RFILTY(2),PARAM3    SET FILE NUMBER AND RECORD TYPE              
         CLI   DAFLAG,0                                                         
         BE    RCV2A                                                            
         L     RE,PARAM5                                                        
         MVC   RVCHR(4),0(RE)      SET DA FILE DISK ADDRESS                     
         OC    XTRADA,XTRADA                                                    
         BZ    *+10                                                             
         MVC   RVCHR(4),XTRADA     SET EXTRA DISK ADDRESS IF PASSED             
*                                                                               
RCV2A    CLC   SSBCNTL,NULLS       TEST OFFLINE                                 
         BNE   RCV2E                                                            
         LT    R5,VUTL             SAVE SENUM FROM UTL                          
         BZ    RCV2B                                                            
         USING UTLD,R5                                                          
         MVC   SENUM,TSYS                                                       
         MVC   RSYS,TSYS                                                        
RCV2B    CLI   SSOXTND,X'FF'       TEST IF EXTENDED OFFLINE SSB                 
         BNE   RCV2D                                                            
         MVC   RPRG,SSORPRG        SET PROGRAM FROM SSB                         
         MVC   RAG,SSORAG          SET AGENCY BINARY FROM SSB                   
         MVC   RUSER,SSORUSER      SET USERID NUMBER FROM SSB                   
*&&UK                                                                           
RCV2D    TM    SSOFLAG3,SSO3XUTL                                                
         BZ    *+10                                                             
         MVC   RPERSON,TPERSON     SET PERSON ID IF EXTENDED UTL                
         OI    TRAILERS,X'01'      SET TRAILER ALWAYS REQUIRED                  
*&&                                                                             
*&&US                                                                           
RCV2D    TM    SSOFLAG3,SSO3XUTL                                                
         BZ    RCV2D1                                                           
         MVC   RPERSON,TPERSON     SET PERSON ID IF EXTENDED UTL                
         OI    TRAILERS,X'01'      SET TRAILER REQUIRED IF EXTENDED UTL         
*&&                                                                             
RCV2D1   OC    SSOSIN,SSOSIN       TEST OFFLINE SIN SET                         
         BNZ   *+8                                                              
         BRAS  RE,RCVGTSIN         NO, GET/SET GLOBAL SIN                       
         MVC   RSIN,SSOSIN                                                      
         MVI   RSIN,X'FE'          SET OFFLINE & COPIES PRESENT                 
         TM    SSBSTAT2,SSBSROLC   DO WE WANT COPIES OFFLINE                    
         BO    RCV2T               YES                                          
         MVI   RSIN,X'FD'          SET OFFLINE & NO COPY RECORDS                
         B     RCV2T                                                            
*                                                                               
RCV2E    L     R5,SSBTKADR         ONLINE-GET A(TCB ENTRY)                      
         USING TCBD,R5                                                          
*&&UK                                                                           
RCV2G    CLI   RRECTY,2            TEST CHANGE WITH SAVE COPY                   
         BNE   RCV2H                                                            
         CLI   PARAM3,X'42'        TEST CHANGE TO MEDFILE                       
         BNE   RCV2GX                                                           
         L     RF,PARAM2           RF=A(CHANGED RECORD)                         
         CLI   1(RF),C'S'          IS IT A BUY RECORD                           
         BNE   RCV2GX                                                           
*                                                                               
RCV2G1   L     RE,PARAM6           RE=A(COPY RECORD)                            
         CLI   1(RE),C'W'          IS IT A BUY SLEEPER                          
         BNE   RCV2G2                                                           
         CLC   20(2,RE),=H'37'     IS IT A SHORT SLEEPER                        
         BNE   RCV2GX                                                           
         LLH   R1,20(RF)           GET BUY RECORD LOGICAL LENGTH                
         TM    33(RF),X'01'        TEST IF HAS ACTIVITY DATA                    
         BZ    *+8                                                              
         AHI   R1,6                                                             
         STH   R1,PARAM3+2         SET NEW SHORTER PHYSICAL LENGTH              
         B     RCV2GX                                                           
*                                                                               
RCV2G2   CLC   PARAM3+2(2),=H'1856' SLEEPER LEN=1850 PLUS ACTIVITY              
         BNE   RCV2GX                                                           
         LLH   R1,20(RF)           GET BUY RECORD LOGICAL LENGTH                
         CHI   R1,1850                                                          
         BNL   RCV2GX                                                           
         TM    33(RF),X'01'        TEST IF HAS ACTIVITY DATA                    
         BZ    *+8                                                              
         AHI   R1,6                                                             
         STH   R1,PARAM3+2         SET NEW SHORTER PHYSICAL LENGTH              
*                                                                               
RCV2GX   EQU   *                                                                
*&&                                                                             
RCV2H    OI    TRAILERS,X'01'      SET TRAILER USED IN THIS RECORD              
         MVC   BYTE1,TCBID+6       SAVE TASK ID                                 
*                                                                               
         L     R5,TCBUTL           GET UTL ENTRY ADDRESS                        
         USING UTLD,R5                                                          
         MVC   SENUM,TSYS                                                       
         MVC   RTRM,TCFN           CTFILE TERMINAL NUMBER                       
*                                                                               
RCV2J    MVC   RPERSON,TPERSON     PERSON ID                                    
         OC    RPERSON,RPERSON     IF RPERSON IS ZERO                           
         BNZ   RCV2K                                                            
         MVC   RPERSON,TPASSWD     SET RPERSON TO TPASSWD                       
         OC    RPERSON,RPERSON     IF RPERSON STILL ZERO                        
         BNZ   RCV2K                                                            
         MVC   RPERSON,=H'1'       SET RPERSON TO 1                             
*                                                                               
RCV2K    MVC   RSYS(2),TSYS        SYS/PRG                                      
         MVC   RSIN,TSIN           SIN                                          
         MVC   RTASKID,BYTE1       TASK ID (HOB OF RSIN)                        
         MVC   RAG,TAGYB           AGENCY/COMPANY                               
         MVC   RUSER,TUSER         USER ID NUM                                  
*                                                                               
RCV2T    XC    RECVEXT(RXLENQ),RECVEXT CLEAR TRAILER                            
         MVI   RXLEN,RXLENQ        SET LENGTH OF TRAILER IN LAST BYTE           
         MVC   RDSPAC,ENQDSPAC     SET DSPACE VALUE                             
         MVC   RJOBNUM,JOBBIN      SET BINARY JOB NUMBER                        
         CLC   SSBCNTL,NULLS       TEST OFFLINE                                 
         BNE   RCV2T1              NO-SKIP                                      
         TM    SSOFLAG3,SSO3RCNH   TEST HARP=NO                                 
         BZ    *+8                                                              
         OI    RGESTAT,RGESRCNH    SET HARP=NO IN TRAILER                       
         TM    SSOFLAG3,SSO3XUTL   EXTENDED UTL                                 
         BZ    RCV2T2                                                           
*                                                                               
RCV2T1   MVC   RAGYSEC,TAGYSEC     AGENCY FOR SECURITY (RPERSON)                
         MVC   RSYSIX,SSBSYSIX     REAL SYSTEM ID                               
         MVC   RXPTYPE(3),TXPTYPE  EXTERNAL PROGRAM TYPE AND NUMBER             
         MVC   RCTSTAT,TSTATB      CT STATUS-BOTTOM 4 BITS TSTATB               
         NI    RCTSTAT,X'0F'                                                    
         MVC   RLUID,TLUID         LUID                                         
         CLI   TTICKET,0           TEST IF TICKET NUMBER PRESENT                
         BE    RCV2T1A                                                          
         MVC   RTICKET,TTICKET     OVERWRITE LUID                               
         OI    RCTSTAT,RCTSTTKT    SET FLAG TO SHOW TICKET NUMBER               
         J     RCV2T2                                                           
*                                                                               
RCV2T1A  ICM   R1,15,TUTLXADR      NEW TICKET NUMBER IN XAUTL                   
         JZ    RCV2T2                                                           
         SAM31                                                                  
         MVC   DUB,TTICKETN-XAUTLD(R1)                                          
         MVC   DUB1,TTICKETN+8-XAUTLD(R1)                                       
         SAM24                                                                  
         CLI   DUB,0               TEST IF NEW TICKET NUMBER PRESENT            
         JE    RCV2T2                                                           
         MVC   RTICKET,DUB         OVERWRITE LUID                               
         MVC   RTICKETX,DUB1       + EXTENSION                                  
         OI    RCTSTAT,RCTSTTKT    SET FLAG TO SHOW TICKET NUMBER               
         J     RCV2T2                                                           
*                                                                               
RCV2T2   TM    TRAILERS,X'40'      RESET/SET SPECIAL UPDATE FLAG                
         BZ    *+8                                                              
         OI    RCTSTAT,RCTSTSUP                                                 
*                                                                               
RCV2U    CLC   SSBCNTL,NULLS       MUST HAVE A GIN IF ONLINE                    
         BE    RCV2U1                                                           
         L     RE,SSBTKADR                                                      
         L     RE,TCBUTL-TCBD(RE)                                               
         LA    R4,TGIN-UTLD(RE)    R4=A(GIN IN UTL)                             
         B     RCV2U2                                                           
*                                                                               
RCV2U1   OI    RCTSTAT,RCTSTOFL    SET OFFLINE RECOVERY RECORD                  
         CLI   SSOXTND,X'FF'       NO GIN IF NO EXTENDED OFFLINE SSB            
         BNE   RCV2X                                                            
*                                                                               
         LA    R4,SSOGIN           R4=A(GIN IN OFFLINE EXTENDED SSB)            
RCV2U2   OC    0(L'SSOGIN,R4),0(R4)                                             
         BNZ   RCV2U3                                                           
         LT    RF,=V(GETGIN)       GET NEW GIN IF CURRENT VALUE IS ZERO         
         BZ    RCV2X                                                            
         BASR  RE,RF                                                            
RCV2U3   MVC   RGIN,0(R4)                                                       
*                                                                               
RCV2X    EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
* GET RECOVERY FILE DTF ADDRESS FOR THIS SE                           *         
***********************************************************************         
RCV3     CLC   SSBCNTL,NULLS       TEST OFFLINE                                 
         BE    RCV3A                                                            
         TM    TTEST,X'80'         NO-ONLINE TEST WRITE TO TSTRCVR SET          
         BO    RCVT                                                             
         B     RCVR3B                                                           
*                                                                               
RCV3A    TM    SSBSTAT2,SSOSRWRK   OFFLINE TEST WRITE TO FACWRK SET             
         BO    RCVW                                                             
         TM    SSBSTAT2,SSOSRSQF   OFFLINE TEST WRITE TO SEQ FILE SET           
         BZ    RCVR3B                                                           
         CLI   SSOXTND,X'FF'       MUST HAVE EXTENDED SSB                       
         JNE   *+2                                                              
         CLC   SSOSQRTN,FONE                                                    
         BNE   RCVS                WRITE VIA USER DEFINED ROUTINE               
         OI    MVSFLAG,MVSFWRTQ    SET FLAG WRITING TO MVS DATA SET             
*                                                                               
RCVR3B   XC    P4(4),P4            SET P4 TO RECOVERY FILE NUM                  
         MVC   P4(1),RFILTY                                                     
*&&UK*&& B     RCVR3C                                                           
*&&US                                                                           
         LLC   RE,P4               GET FILE NUMBER                              
         A     RE,=A(RCVOVTAB)     POINT INTO OVERRIDE TABLE                    
         CLI   0(RE),0                                                          
         BE    RCVR3C                                                           
         MVC   P4(1),0(RE)         USE TABLE VALUE IF NON-ZERO                  
         B     RCVR3D                                                           
*&&                                                                             
RCVR3C   NI    P4,X'F0'            RECOVERY FILE NUM IS X'.4'                   
         OI    P4,X'04'                                                         
*                                                                               
RCVR3D   XR    R1,R1               R1=SENUM                                     
         ICM   R1,1,SENUM          IF VALID A(DTF) FROM FILTABL/SYSTABL         
         BNZ   RCVR3E              ELSE GET A(DTF) FROM DATAMGR                 
         GOTO1 VDMOD000,P1,V(DMEXT)                                             
         B     RCVR3F                                                           
*                                                                               
RCVR3E   LLC   RF,P4               RF=FILE NUMBER                               
         SLL   RF,5                INDEX INTO 32-BYTE FILE TABLE                
         A     RF,=V(FILTABL)                                                   
         SLL   R1,4                INDEX INTO 16-BYTE SYSTEM TABLE              
         A     R1,=V(SYSTABL)                                                   
         SR    RE,RE                                                            
         ICM   RE,7,DMSYAFNO-SYSTABD(R1) RE=A(FILE NUM INDEX TABLE)             
         L     R1,DMSYASYF-SYSTABD(R1)   R1=A(SYSFLES LIST ENTRY)               
*                                                                               
         LLC   RF,P4               RF=FILE NUMBER                               
         AR    RE,RF                                                            
         IC    RF,0(RE)            RF=ENTRY NUMBER IN SYSFLES LIST              
         AHI   RF,-1                                                            
         SLL   RF,3                                                             
         LA    RE,4(RF,R1)         INDEX INTO 8-BYTE SYSFLES FILE ENTRY         
         MVC   P4+1(3),SYSFADTF-SYSFLSTD(RE)                                    
*                                                                               
RCVR3F   XR    RE,RE               GET A(DTF) FROM P4                           
         ICM   RE,7,P4+1                                                        
         TM    DTFOPEN-DTFPHD(RE),DTF_RO                                        
*&&UK*&& BO    RCVWARN2            EXIT IF RECOVERY FILE IS READ ONLY           
*&&US*&& BO    RCVX                EXIT IF RECOVERY FILE IS READ ONLY           
*                                                                               
         CLI   EXTFILL,X'0A'       TEST CONCURRENT UPDATES IF CONTROL           
         BE    *+14                                                             
         CLC   SSBCNTL,NULLS       TEST CONCURRENT UPDATES IF OFFLINE           
         BNE   RCV4                                                             
         GOTO1 VDMOD000,P1,V(DMUPDEXT)                                          
         EJECT                                                                  
***********************************************************************         
* RECOVERY RECORD TO RECOVERY FILE FOR THIS SYSTEM                    *         
* IF THIS IS A COPY RECORD IT IS NOT WRITTEN NOW                      *         
* IT WILL BE WRITTEN NEXT TIME ALONG WITH ITS CHANGE RECORD           *         
***********************************************************************         
RCV4     MVI   RECNUM1,0           INIT RECORD(S) WRITTEN INFO                  
         MVI   RECNUM2,0                                                        
         MVC   P1(4),=A(WTCKD)                                                  
         L     RE,P2               DATA ADDRESS                                 
         AHI   RE,-(HDRQ)          BACK UP FOR HEADER                           
         MVC   SAVE,0(RE)          SAVE 24 BYTES                                
         MVC   0(HDRQ,RE),RECVHDR  NOTE-DATE AND TIME NOT YET SET               
         ST    RE,P2               SET AS ADDRESS                               
*                                                                               
         AHI   RE,-4               BACK UP FOR LENGTH                           
         MVC   SAVEHDR,0(RE)       SAVE 4 BYTES                                 
         ST    RE,ASAVE            SAVE ADDRESS FOR RESTORE LATER ON            
*                                                                               
         LH    R1,PARAM3+2         GET LENGTH OF RECORD                         
         AHI   R1,HDRQ             ADJUST FOR RECVHDR                           
         ST    R1,P3                                                            
*                                                                               
         LA    R1,4(R1)            ADJUST FOR LENGTH OF LENGTH                  
         SLL   R1,16                                                            
         STCM  R1,15,0(RE)         SET LENGTH OF RECOVERY RECORD                
*                                                                               
         LA    RE,P7                                                            
         ST    RE,P5               FOR RETURNED DISK ADDRESS                    
*                                                                               
RCV4A    L     RE,P2                                                            
         CLI   RRECTY,1            COPY RECORD?                                 
         BNE   RCV4B               NO                                           
         MVC   18(2,RE),P3+2       SAVE L'COPY RECORD IN DATE FIELD             
         L     RE,ASAVE            RESTORE 4 BYTES USED FOR LENGTH              
         MVC   0(4,RE),SAVEHDR                                                  
         B     XMOD                DONT WRITE COPY RECORD NOW                   
*                                                                               
RCV4B    XC    CPCHDETS,CPCHDETS   COLLECT RECOVERY REC ADDR/LENS               
         CLI   RRECTY,2            TEST IF CHANGE WITH SAVED COPY               
         BNE   RCV4B1                                                           
         L     RE,PARAM6                                                        
         AHI   RE,-(HDRQ)                                                       
         OC    0(4,RE),0(RE)       CHECK FOR SAVED COPY                         
         BZ    RCV4B1                                                           
         ST    RE,CPCHACOP         A(COPY RECORD) IF PRESENT                    
         MVC   CPCHLCOP+2(2),18(RE) LENGTH COPY WAS SAVED IN DATE               
RCV4B1   MVC   CPCHACHG,P2         A(CHANGE/ADD)                                
         LH    RE,PARAM3+2         LENGTH CHANGE/ADD                            
         AHI   RE,HDRQ             DID NOT INCLUDE L'HDR                        
         ST    RE,CPCHLCHG         LENGTH CHANGE/ADD INC HEADER                 
*                                                                               
*                                  TEST FOR OFFLINE JOB HEADER RECORD           
         MVI   OHDRFLAG,OFF        FLAG JOB HEADER NOT PRESENT                  
         MVI   OTRRFLAG,OFF        FLAG JOB TRAILER NOT PRESENT                 
         CLC   SSBCNTL,NULLS       TEST OFFLINE                                 
         BNE   RCV4D                                                            
         CLI   FIRSTIME,ON         TEST FIRST TIME FOR JOB                      
         BNE   RCV4C                                                            
         MVI   FIRSTIME,OFF        SET FIRSTIME FLAG                            
         BRAS  RE,BOHDREC          BUILD OFFLINE JOB HEADER RECORD              
         MVI   OHDRFLAG,ON         FLAG JOB HEADER PRESENT                      
*                                                                               
         OI    SSOFLAG1,SSOFSJOB   FLAG SJOB SENT IN SSB                        
         LT    RE,SSOASSB                                                       
         BZ    *+8                                                              
         OI    SSOFLAG1-SSBD(RE),SSOFSJOB                                       
*                                                                               
         TM    MVSFLAG,MVSFWRTQ    TEST WRITE RECOVERY TO MVS DATA SET          
         BZ    RCV4D                                                            
         BRAS  RE,MVSOPEN          OPEN MVS DATA SET FOR RECOEVRY               
         BRAS  RE,MVSWRITE         WRITE RECOVERY DATA TO MVS DATA SET          
         B     RCV4D               AND WRITE HEADER TO RECOVERY FILE            
                                                                                
***********************************************************************         
* WARNING - IF THE HEADER WRITE IS FORCED TO WAIT FOR A VERMONT BUFFER*         
* THE TIME STAMP IN THE FIRST RECOVERY RECORD WRITTEN TO THE MVS DATA *         
* SET WILL BE AHEAD OF THE ACTUAL WRITE BY THE LENGTH OF THE WAIT.    *         
***********************************************************************         
RCV4C    L     RF,P2               CHECK DUMMY END OF JOB RECORD                
         CLC   HDRQ(L'EOFREC,RF),EOFREC                                         
         BNE   RCV4CA                                                           
         MVI   FIRSTIME,ON         RESET FIRSTIME FLAG                          
         BRAS  RE,BOTRREC          BUILD OFFLINE JOB TRAILER RECORD             
         MVI   OTRRFLAG,ON         FLAG JOB TRAILER PRESENT                     
*                                                                               
         NI    SSOFLAG1,255-SSOFSJOB    CLEAR SJOB FLAG                         
         LT    RE,SSOASSB                                                       
         BZ    *+8                                                              
         NI    SSOFLAG1-SSBD(RE),255-SSOFSJOB                                   
*                                                                               
         TM    MVSFLAG,MVSFWRTQ    TEST WRITE RECOVERY TO MVS DATA SET          
         BZ    RCV4D                                                            
         BRAS  RE,MVSCLOSE         CLOSE MVS DATA SET FOR RECOEVRY              
         B     RCV4D                                                            
*                                                                               
RCV4CA   TM    MVSFLAG,MVSFWRTQ    TEST WRITE RECOVERY TO MVS DATA SET          
         BZ    RCV4D                                                            
         BRAS  RE,MVSWRITE         WRITE RECOVERY DATA TO MVS DATA SET          
         B     XMOD                AND EXIT                                     
         EJECT                                                                  
***********************************************************************         
* DSPACE/SHRMEM AND VERMONT RECOVERY                                  *         
***********************************************************************         
RCV4D    XR    R5,R5               GET A(RECOVERY FILE DTF)                     
         ICM   R5,7,P4+1                                                        
         USING DTFPHD,R5                                                        
         CLC   SSBCNTL,NULLS       TEST OFFLINE                                 
         BNE   RCV4E                                                            
         L     RF,VUTL             GET SYS FROM UTL                             
         MVC   RCVRSYS,TSYS-UTLD(RF)                                            
         B     RCV4F                                                            
*                                                                               
RCV4E    L     RF,SSBTKADR         GET SYSTEM FROM TCB                          
         MVC   RCVRSYS,TCBSEN-TCBD(RF)                                          
*                                                                               
RCV4F    MVI   BUFFM,BLOCALQ                                                    
         TM    DTFFLAG,DTFGLOB     TEST GLOBAL RESOURCE                         
         BZ    RCV4G               NO                                           
         MVI   BUFFM,BGLOBALQ      SET GLOBAL RECOVERY                          
         TM    DTFFLAG,DTFSHMEM    TEST SHR MEM RECOVERY                        
         BZ    *+8                                                              
         MVI   BUFFM,BSHRMEMQ                                                   
RCV4G    TM    DIND,DINDVRX        TEST USING EXTENDED VERMONTS                 
         BZ    RCV7                                                             
         OI    BUFFM,BEXTVMTQ                                                   
         EJECT                                                                  
***********************************************************************         
* RECOVERY TO DATASPACE OR SHARED MEMORY                              *         
***********************************************************************         
         USING DTFPHD,R5                                                        
RCV7     TM    BUFFM,BLOCALQ       TEST IF LOCAL BUFFER                         
         BZ    *+12                                                             
         BRAS  RE,RCVGETL          GET LOCAL DTF                                
         B     *+8                                                              
         BRAS  RE,RCVGETD          GET DSPACE OR SHRMEM                         
         TM    BUFFM,BSHRMEMQ                                                   
         BZ    *+6                                                              
         SAM31                     SET 31-BIT MODE IF SHARED MEMORY             
*                                                                               
         L     R2,ARECBUF                                                       
         USING VRCVHDR,R2                                                       
         OC    VRCVDA,VRCVDA       INITIALISE BLOCK IF ZERO DA                  
         BZ    RCV7INIT                                                         
         XR    RE,RE                                                            
         ICM   RE,3,VRCVNXT        SET RE TO DISP TO NEXT RECORD                
         BZ    RCV7INIT            INITIALISE BLOCK IF ZERO LENGTH              
*                                                                               
RCV7A    BRAS  RE,RCVGETA          GET/SET ACTIVE ENTRY                         
         BZ    RCVWAIT                                                          
         CLC   SSBCNTL,NULLS       TEST OFFLINE                                 
         BNE   *+8                                                              
         OI    DIND,DINDVRA        YES-SET VERMONT ACTIVE FLAG                  
         CLI   VRCVDA+3,250                                                     
         BNL   RCV7INIT            INITIALISE BLOCK IF MAX RECS/BLOCK           
         DROP  R2                                                               
*                                                                               
         LHI   R1,6                INITIALISE RECORD DATA LENGTH                
         CLI   OHDRFLAG,ON         TEST JOB HEADER PRESENT                      
         BNE   RCV7A1                                                           
         LA    R1,DMRCVLNQ(,R1)    ALLOW FOR LENGTH AND LINK ADDRESS            
         AH    R1,OHDRECL                                                       
RCV7A1   CLI   OTRRFLAG,ON         TEST JOB TRAILER PRESENT                     
         BNE   RCV7A2                                                           
         LA    R1,L'DMRCVLN(,R1)   SET EXTRA RECORD DATA LENGTH                 
         AH    R1,OTRRECL                                                       
         B     RCV7A4                                                           
*                                                                               
RCV7A2   TM    MVSFLAG,MVSFWRTQ    WRITE RECOVERY TO MVS DATA SET               
         BO    RCV7A4              IF SO DO NOT WRITE RECOVERY DATA             
         LA    R1,L'DMRCVLN(,R1)   SET FOR ONE RECOVERY REC LEN                 
         LT    RE,CPCHLCOP         TEST IF SAVED COPY RECORD                    
         BZ    RCV7A3                                                           
         LA    R1,DMRCVLNQ(RE,R1)  SET EXTRA RECORD LEN + TTBR + LEN            
         TM    TRAILERS,X'01'                                                   
         BZ    *+8                                                              
         AHI   R1,EXTQ             ADD TRAILER LENGTH                           
*                                                                               
RCV7A3   A     R1,CPCHLCHG         ADD LENGTH OF CHANGE/ADD RECORD              
         TM    TRAILERS,X'01'                                                   
         BZ    *+8                                                              
         AHI   R1,EXTQ             ADD TRAILER LENGTH                           
*                                                                               
RCV7A4   L     R2,ARECBUF          GET LENGTH OF BLOCK AFTER ADD                
         USING VRCVHDR,R2                                                       
         CLI   VRCVDA+3,250                                                     
         BNL   RCV7INIT            INITIALISE BLOCK IF MAX RECS/BLOCK           
         AH    R1,VRCVNXT                                                       
         ST    R1,DUB1                                                          
         CH    R1,DBLKSZ           TEST IF FITS IN THIS BLOCK                   
         BNH   RCV7C                                                            
         CLI   VRCVDA+3,0          TEST FIRST RECORD IN BLOCK                   
         BNE   RCV7INIT            NO INITIALISE NEW BLOCK                      
         DC    H'0'                RECORD'S TOO BIG FOR BLOCK SIZE              
         DROP  R2                                                               
*                                                                               
RCV7C    BRAS  RE,RCVCLOCK         SET DATE & TIMES IN RECOVERY HDRS            
         TM    BUFFM,BGLOBALQ      TEST GLOBAL DSPACE                           
         BZ    RCV7C0                                                           
         SAC   512                 GLOBAL SET TO AR MODE                        
*                                                                               
RCV7C0   CLI   OTRRFLAG,ON         TEST IF OFFLINE JOB TRAILER PRESENT          
         BNE   RCV7C1                                                           
         MVC   RECLEN,OTRRECL                                                   
         LA    RE,OTRREC                                                        
         ST    RE,RECADD           ADD OFFLINE TRAILER                          
         MVC   RDATE-RECVHDR(,RE),RDATE                                         
         MVC   RTIME-RECVHDR(,RE),RTIME                                         
         NI    RTIME-RECVHDR(RE),X'FF'-RTIMEEXT                                 
         MVC   BYTE2,TRAILERS                                                   
         NI    TRAILERS,X'FF'-X'01' SWITCH OFF TRAILERS FOR JOB TRAILER         
         BRAS  RE,RCVADD                                                        
         MVC   TRAILERS,BYTE2                                                   
         B     RCV7C6              BRANCH STRAIGHT TO BLOCK I/O                 
*                                                                               
RCV7C1   LT    RE,CPCHACOP         TEST IF CHANGE WITH SAVED COPY               
         BZ    RCV7C2                                                           
X        USING RECVHDR,RE                                                       
         MVC   X.RDATE,RDATE       SET DATE                                     
         MVC   X.RTIME,RTIME       SET TIME                                     
RCV7C2   L     RE,CPCHACHG         POINT TO CHANGE/ADD                          
         MVC   X.RDATE,RDATE       SET DATE                                     
         MVC   X.RTIME,RTIME       SET TIME                                     
         DROP  X                                                                
*                                                                               
         CLI   OHDRFLAG,ON         TEST IF OFFLINE JOB HEADER PRESENT           
         BNE   RCV7C3                                                           
         MVC   RECLEN,OHDRECL                                                   
         LA    RE,OHDREC                                                        
         ST    RE,RECADD           ADD OFFLINE HEADER                           
         MVC   RDATE-RECVHDR(,RE),RDATE                                         
         MVC   RTIME-RECVHDR(,RE),RTIME                                         
         NI    RTIME-RECVHDR(RE),X'FF'-RTIMEEXT                                 
         MVC   BYTE2,TRAILERS                                                   
         NI    TRAILERS,X'FF'-X'01' SWITCH OFF TRAILERS FOR JOB HEADER          
         BRAS  RE,RCVADD                                                        
         MVC   TRAILERS,BYTE2                                                   
*                                                                               
RCV7C3   LT    RF,=V(DMRCVUSS)     USS QUEUE WRITE                              
         BZ    RCV7C4              NOT LINKED IN                                
         TM    BUFFM,BLOCALQ                                                    
         BO    RCV7C4              DONT SEND LOCAL TO USS                       
         TM    TRAILERS,X'01'                                                   
         BZ    RCV7C4              NEED TRAILERS IF GOING TO USS                
         MVC   P1USS(16),CPCHDETS  ADDR+LEN COPY, ADDR+LEN CHANGE/ADD           
         LA    R1,RECVEXT          A(TRAILER)                                   
         ST    R1,P5USS                                                         
         LA    R1,EXTQ             L'TRAILER                                    
         ST    R1,P6USS                                                         
         SAC   0                                                                
         LA    R1,DMCBUSSQ                                                      
         BASR  RE,RF                                                            
         TM    BUFFM,BGLOBALQ      TEST GLOBAL DSPACE                           
         BZ    RCV7C4                                                           
         SAC   512                 GLOBAL SET TO AR MODE                        
*                                                                               
RCV7C4   TM    MVSFLAG,MVSFWRTQ    TEST WRITE RECOVERY TO MVS DATA SET          
         BO    RCV7C6              IF SO DO NOT WRITE TO RCV FILE               
*                                                                               
         OC    CPCHACOP,CPCHACOP   TEST IF SAVED COPY                           
         BZ    RCV7C5                                                           
         MVC   RECADD,CPCHACOP     A(COPY)                                      
         MVC   RECLEN,CPCHLCOP+2   L'COPY                                       
         BRAS  RE,RCVADD           ADD SAVED COPY RECORD                        
         MVC   RECNUM1,DUB+7                                                    
*                                                                               
RCV7C5   MVC   RECADD,CPCHACHG     A(CHANGE/ADD)                                
         MVC   RECLEN,CPCHLCHG+2   GET RECORD LENGTH                            
         BRAS  RE,RCVADD           ADD (ADD OR CHANGE) RECORD                   
         MVC   RECNUM2,DUB+7                                                    
*                                                                               
RCV7C6   CLI   OTRRFLAG,ON         TEST OFFLINE TRAILER                         
         BE    RCV7C7                                                           
         L     R2,ARECBUF                                                       
         USING VRCVHDR,R2                                                       
         TM    BUFFM,BLOCALQ       STILL HAVE TO WRITE FOR LOCAL                
         BO    RCV7C9                                                           
         OC    VRCVCSIN,VRCVCSIN   IS THIS A NEW BLOCK                          
         BZ    RCV7C9              YES-WRITE IT                                 
         XR    R2,R2               GLOBAL FLAG TO WRITE ON EOT                  
         L     R2,DHASSBG-DMDHDR(,R2)                                           
         TM    SSGSTAT1-FASSBG(R2),SSGSRCVW                                     
         BZ    RCV7C9              NO-WRITE IT                                  
         B     RCV7D               YES SO SKIP WRITE                            
         DROP  R2                                                               
*                                                                               
RCV7C7   TM    BUFFM,BGLOBALQ      TEST GLOBAL DSPACE                           
         BZ    RCV7C8                                                           
         SAC   512                 GLOBAL SET TO AR MODE                        
*                                                                               
RCV7C8   L     R2,ACTVAD                                                        
         TM    BUFFM,BEXTVMTQ      TEST USING EXTENDED VERMONTS                 
         BO    *+14                                                             
         XC    0(VRCVLNQ,R2),0(R2) REMOVE ACTIVE ENTRY                          
         B     *+10                                                             
         XC    0(VRCXLNQ,R2),0(R2) REMOVE EXTENDED ACTIVE ENTRY                 
*                                                                               
         BRAS  RE,RCVUSSCM         WRITE USS COMMIT IF REQUIRED                 
         TM    BUFFM,BGLOBALQ      TEST GLOBAL DSPACE                           
         BZ    *+8                                                              
         SAC   512                 GLOBAL RESET TO AR MODE                      
*                                                                               
RCV7C9   BRAS  RE,RCVWRITE         WRITE RECOVERY RECORD                        
*                                                                               
RCV7D    MVC   P7,DNEXT            SET TTB FOR RECOVERY                         
         L     RE,ASAVE            RESTORE 28 BYTES                             
         MVC   0(HP4Q,RE),SAVEHDR                                               
         OC    P3(2),P3            TEST ERRORS                                  
         BNZ   DEATH                                                            
         XC    PARAM3(2),PARAM3                                                 
*                                                                               
RCVFREE  SAC   0                                                                
         TM    BUFFM,BLOCALQ       TEST FOR LOCAL                               
         BO    RCV7E                                                            
         CLI   RCVRSYS,0                                                        
         BE    RCV7F                                                            
         XC    DUB,DUB             GLOBAL RESOURCE                              
         MVC   DUB+3(1),RCVRSYS                                                 
         OI    DUB,X'10'                                                        
         GOTO1 VLOCKSPC,DUB        UNLOCK RESOURCE                              
         B     XMOD                                                             
*                                                                               
RCV7E    LTR   R5,R5               TEST ANY DTF TO FREE                         
         BZ    *+8                                                              
         NI    DTFDEVF,255-X'20'   UNLOCK LOCAL FILE                            
*                                                                               
RCV7F    B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* WAIT FOR A VERMONT ENTRY TO BE FREE                                 *         
***********************************************************************         
RCVWAIT  SAC   0                                                                
         TM    BUFFM,BLOCALQ       TEST FOR LOCAL                               
         BO    RCVWA1                                                           
         CLI   RCVRSYS,0                                                        
         BE    RCVWA2                                                           
         XC    DUB,DUB             GLOBAL RESOURCE                              
         MVC   DUB+3(1),RCVRSYS                                                 
         OI    DUB,X'11'                                                        
         GOTO1 VLOCKSPC,DUB        UNLOCK RESOURCE (ALL LEVELS)                 
         B     RCVWA2                                                           
*                                                                               
RCVWA1   LTR   R5,R5               TEST ANY DTF TO FREE                         
         BZ    *+8                                                              
         NI    DTFDEVF,255-X'20'   UNLOCK LOCAL FILE                            
*                                                                               
RCVWA2   LT    RF,VADWAIT          WAIT UNTIL ONE IS FREE                       
         BZ    RCVWA8                                                           
         CLC   SSBCNTL,NULLS       IF OFFLINE IN LINE WAIT                      
         BE    RCVWA8                                                           
*                                                                               
         L     R2,ARECBUF          SET UP ECB TO WAIT ON                        
         USING VRCVHDR,R2                                                       
         LA    R1,VRCVECB          POINT TO ECB                                 
*                                                                               
         TM    BUFFM,BLOCALQ       TEST LOCAL RECOVERY BUFFER                   
         BO    RCVWA4                                                           
         TM    BUFFM,BSHRMEMQ      TEST BUFFER IN SHARED MEMORY                 
         BO    RCVWA3                                                           
*                                                                               
         SAC   512                 IF GLOBAL SET TO AR MODE                     
         MVI   VRCVECB,0           CLEAR ECB                                    
         OILH  GR1,X'FE00'         FLAG AS DSPACE ECB                           
         B     RCVWA5                                                           
*                                                                               
RCVWA3   SR    R1,R1               THIS CALL USES SELIST SHRMEM ADDR            
         OILH  GR1,X'FA00'         FLAG AS SHRMEM ECB                           
         B     RCVWA5                                                           
*                                                                               
RCVWA4   XC    VRCVECB,VRCVECB                                                  
         OILH  GR1,X'FF00'         FLAG AS DUMMY ECB                            
         DROP  R2                                                               
*                                                                               
RCVWA5   SAC   0                   RETURN TO NORMAL AMODE                       
         BASR  RE,RF               GO FIND SOMETHING TO DO                      
         B     RCV7                                                             
*                                                                               
RCVWA8   SAC   0                   NO MULTI TASKING SO WAIT                     
         LHI   R1,10               1/10 SEC OR 100 MILLISECS                    
         MHI   R1,384              384 TUS IS 1/100 SEC OR 10 MILLISECS         
         ST    R1,FULL                                                          
         STIMER WAIT,TUINTVL=FULL  WAIT N MILLISECS AND TRY AGAIN               
         B     RCV7                                                             
         EJECT                                                                  
***********************************************************************         
* WRITE RECOVERY RECORD TO DISK                                       *         
***********************************************************************         
RCVWRITE NTR1                                                                   
         L     R2,ARECBUF                                                       
         USING VRCVHDR,R2                                                       
         MVC   P1,=A(WTID)         SET TO WRITE THIS BLOCK                      
         OC    VRCVCSIN,VRCVCSIN   TEST IF WRITTEN BEFORE                       
         BNZ   *+10                VRCVCSIN SET BY LAST TASK TO WRITE           
         MVC   P1,=A(WTCKD)        SET TO ADD NEW INITIALISED BLOCK             
*                                                                               
         ST    R5,P4               SET P4 DTF                                   
         MVC   P7,VRCVDA           SET P7 TO 16-BIT DA TTTTBB00                 
         MVI   P7+3,0                                                           
         LA    RE,P7                                                            
         ST    RE,P5               SET P5 A(DA)                                 
*                                                                               
         CLI   DTFTYPE,DTFTBIGF    TEST 20-BIT RECOVERY FILE                    
         BNE   RCVRW02                                                          
         MVC   P7+3(1),P7+2        CONVERT TTTTTB00 TO TTTTT00B                 
         NC    P7+2(2),=X'F00F'                                                 
*                                                                               
RCVRW02  CLC   SSBCNTL,NULLS       SAVE TASK ID OF THIS WRITE                   
         BE    RCVRW04                                                          
         L     RE,SSBTKADR                                                      
         USING TCBD,RE                                                          
         L     RE,TCBUTL           ONLINE USE TSIN                              
         USING UTLD,RE                                                          
         MVC   VRCVCSIN,TSIN                                                    
         DROP  RE                                                               
         B     RCVRW10                                                          
*                                  OFFLINE USE FF/JOBNUM                        
RCVRW04  MVC   VRCVCSIN,=X'FFFFFFFF'  NOT GLOBAL THEN USE THIS                  
         CLI   RCVRSYS,X'0A'          OFFLINE/VERMONT                           
         BE    *+12                   FOR CONTROL FILE USE JOB                  
         TM    BUFFM,BLOCALQ          MUST BE GLOBAL FILE FOR JOBNUM            
         BO    RCVRW10                                                          
         MVC   VRCVCSIN(2),=X'FF00'                                             
         MVC   VRCVCSIN+2(2),JOBBIN                                             
*                                                                               
RCVRW10  LH    RE,DBLKSZ                                                        
         ST    RE,P3               SET BLOCK SIZE                               
         L     RE,DBLK                                                          
         TM    BUFFM,BSHRMEMQ                                                   
         BZ    *+8                                                              
         L     RE,ARECBUF                                                       
         ST    RE,P2               SET A(RECOVERY FILE BLOCK)                   
*                                                                               
         TM    BUFFM,BGLOBALQ      TEST GLOBAL DSPACE                           
         BZ    RCVW020                                                          
*                                                                               
         LR    R0,R3               SAVE R3 A(SSB)                               
         LH    RF,DBLKSZ                                                        
         LR    R3,RF                                                            
         MVCL  RE,R2               COPY RECORD FROM DSPACE                      
         LR    R3,R0               RESTORE R3 A(SSB)                            
*                                                                               
         L     R1,DBLK             TEST IF VALID BLOCK DATA                     
         LH    R1,4(R1)                                                         
         CH    R1,DBLKSZ                                                        
         JH    *+2                 DIE IF BLOCK OVERFLOW                        
*                                                                               
RCVW020  SAC   0                   DROP AR MODE                                 
*                                                                               
         LA    R1,P1               DO BLOCKED RECOVERY FILE I/O                 
         L     RF,VDADDS                                                        
         TM    BUFFM,BSHRMEMQ                                                   
         BZ    *+8                                                              
         O     RF,=X'80000000'     CALL IN 31-BIT MODE IF SHARED MEMORY         
         CLC   SSBCNTL,NULLS                                                    
         BNE   *+10                                                             
         BASR  RE,RF               OFFLINE WRITE RECORD TO DISK                 
         B     RCVXIT                                                           
*                                                                               
         L     RE,SSBTKADR         MAKE SURE DONT DIE ON RECOVERY I/O           
         MVC   SVIOCNT,TCBIOCNT-TCBD(RE)                                        
         XC    TCBIOCNT-TCBD(3,RE),TCBIOCNT-TCBD(RE)                            
         BASR  RE,RF               ONLINE WRITE RECORD TO DISK                  
         L     RE,SSBTKADR                                                      
         SR    R0,R0                                                            
         ICM   R0,7,SVIOCNT        RESTORE I/O COUNT                            
         AHI   R0,1                                                             
         STCM  R0,7,TCBIOCNT-TCBD(RE)                                           
         B     RCVXIT                                                           
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* INITIALISE NEW RECOVERY RECORD BLOCK TO VERMONT FORMAT              *         
*                                                                     *         
* SEE VRCVHDR DSECT                                                   *         
* 00(3)  DISK ADDR OF THIS BLOCK TTTTBB (16-BIT) OR TTTTTB (20-BIT)   *         
*        ZERO IF BLOCK NOT YET INITIALISED                            *         
* 03(1)  HIGHEST RECORD NUMBER IN BLOCK                               *         
* 04(2)  BYTES USED IN BLOCK / DISP TO FREE SPACE. INITIALLY 96 OR 176*         
* VERMONT HEADER RECORD:                                              *         
* 06(2)  LENGTH OF VERMONT HEADER RECORD (90 OR 170)                  *         
* 08(4)  ECB USED TO CONTROL ACCESS TO VERMONTS                       *         
* 12(4)  SIN/TASK ID OF THE TRANSACTION THAT LAST WROTE THE BLOCK     *         
*        ZEWRO IF BLOCK HAS NEVER BEEN WRITTEN                        *         
* ORIGINAL VERMONTS (VRCVHDR DSECT)                                   *         
* 16(80) TEN VERMONT TRANSACTION ENTRIES EACH 8 BYTES LONG            *         
* 96(24) FIRST LOGICAL RECORD RECOVERY HEADER (DMRCVRD)               *         
* EXTENDED VERMONTS (VRCXHDR DSECT)                                   *         
* 16(160) TEN VERMONT TRANSACTION ENTRIES EACH 16 BYTES LONG          *         
* 176(24) FIRST LOGICAL RECORD RECOVERY HEADER (DMRCVRD)              *         
***********************************************************************         
RCV7INIT XR    R5,R5               GET A(RECOVERY FILE DTF)                     
         ICM   R5,7,P4+1                                                        
         USING DTFPHD,R5                                                        
         L     R2,ARECBUF                                                       
         USING VRCVHDR,R2                                                       
         OC    VRCVDA,VRCVDA       INIT HEADER IF ZERO DA                       
         BNZ   RCV7INI0                                                         
         TM    BUFFM,BEXTVMTQ      TEST USING EXTENDED VERMONTS                 
         BO    *+14                                                             
         XC    0(VRCVBLKQ,R2),0(R2) INIT HEADER FIRST TIME ONLY                 
         B     RCV7INI1                                                         
         XC    0(VRCXBLKQ,R2),0(R2) INIT HEADER FIRST TIME ONLY                 
         B     RCV7INI1                                                         
RCV7INI0 MVC   SAVEPARM(7*4),P1                                                 
         BRAS  RE,RCVWRITE         WRITE OLD BUFFER                             
         MVC   P1(7*4),SAVEPARM                                                 
*                                                                               
RCV7INI1 TM    BUFFM,BGLOBALQ      TEST GLOBAL DSPACE                           
         BZ    RCV7INI2                                                         
         SAC   512                 GLOBAL SET TO AR MODE                        
*                                                                               
RCV7INI2 LA    RE,VRCVBLKQ         HEADER LENGTH                                
         TM    BUFFM,BEXTVMTQ      TEST USING EXTENDED VERMONTS                 
         BZ    *+8                                                              
         LA    RE,VRCXBLKQ         EXTENDED HEADER LENGTH                       
         AR    R2,RE                                                            
         LR    R0,R3               SAVE R3 A(SSB)                               
         LH    R3,DBLKSZ                                                        
         SR    R3,RE                                                            
         XR    RF,RF                                                            
         LR    RE,RF                                                            
         MVCL  R2,RE               ERASE REST OF BUFFER FOR NEW BLOCK           
         LR    R3,R0               RESTORE R3 A(SSB)                            
         SAC   0                   TURN OFF AR MODE                             
*                                                                               
         MVC   Q1(28),P1           SET DADDS PARAMS TO GET NEXT ADDR            
         MVC   Q1,=A(ADDADR)                                                    
         LH    RE,DBLKSZ                                                        
         ST    RE,Q3                                                            
         LA    R1,Q1                                                            
         L     RF,VDADDS                                                        
         BASR  RE,RF               GET DISK ADDR OF NEXT BLOCK                  
         OC    Q3(2),Q3                                                         
         JNZ   *+2                                                              
*                                                                               
         TM    BUFFM,BGLOBALQ      TEST GLOBAL DSPACE                           
         BZ    RCV7INI4                                                         
         SAC   512                 GLOBAL SET TO AR MODE                        
*                                                                               
RCV7INI4 L     R1,Q5               R1=A(DISK ADDR)                              
         L     R2,ARECBUF          R2=A(RECOVERY BUFFER)                        
         USING VRCVHDR,R2                                                       
         MVC   VRCVDA,0(R1)        SET DISK ADDR                                
         XC    VRCVECB(L'VRCVECB+L'VRCVCSIN),VRCVECB CLEAR ECB AND SIN          
         MVC   VRCVNXT,=Y(VRCVBLKQ) SET INITIAL LENGTH OF BUFFER USED           
         MVC   VRCV1STR,=Y(VRCV1STQ) SET VERMONT HEADER LEN                     
         TM    BUFFM,BEXTVMTQ      TEST USING EXTENDED VERMONTS                 
         BZ    RCV7INI5                                                         
         MVC   VRCVNXT,=Y(VRCXBLKQ) SET INITIAL LENGTH OF BUFFER USED           
         MVC   VRCV1STR,=Y(VRCX1STQ) SET VERMONT HEADER LEN                     
*                                                                               
RCV7INI5 CLI   DTFTYPE,DTFTBIGF    TEST 20-BIT RECOVERY FILE                    
         BNE   *+10                                                             
         OC    VRCVDA+2(1),3(R1)   CONVERT TTTTT00B TO TTTTTB00                 
         MVI   VRCVDA+3,0          CLEAR LAST LOGICAL RECORD NUMBER             
*                                                                               
         B     RCV7A               BACK TO MOVE IN RECORD                       
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* GET CLOCK (ONCE) AND SET RDATE, RTIME AND RTIMEMS                   *         
* CALLER MUST RESET AR MODE IF REQUIRED                               *         
* TIME MACRO RETURNS PWOS P'HHMMSSMMMMMM0000YYYYMMDD'                 *         
***********************************************************************         
RCVCLOCK NTR1                                                                   
*                                                                               
         XC    CLOCKWRK(16),CLOCKWRK                                            
         SAC   0                                                                
         TIME  DEC,CLOCKWRK,LINKAGE=SYSTEM,DATETYPE=YYYYMMDD                    
         XC    DUB,DUB                                                          
         LAM   ARE,ARF,DUB         MACRO CAN CLOBBER ANY OF THESE               
         LAM   AR0,AR1,DUB                                                      
         ZAP   DUB,=P'0'           GET RDATE AS BINARY YYMMDD                   
         MVO   DUB+6(2),CLOCKWRK+8+3(1) GET DD                                  
         CVB   R0,DUB                                                           
         STC   R0,RDATE+2                                                       
         MVO   DUB+6(2),CLOCKWRK+8+2(1) GET MM                                  
         CVB   R0,DUB                                                           
         STC   R0,RDATE+1                                                       
         MVO   DUB+5(3),CLOCKWRK+8+0(2) GET YYYY                                
         CVB   R0,DUB                                                           
         AHI   R0,-1900            ADJUST TO BASE 1900                          
         STC   R0,RDATE+0                                                       
*                                  GET TIME IN R1 AS PACKED X'0HHMMSSC'         
         MVO   DUB+4(4),CLOCKWRK(3) DUB=0000 0000 0HHM MSSC                     
         L     R1,DUB+4                                                         
*                                  ADJUST TIME FOR RTIME                        
         CLI   EXTFILL,X'0A'       TEST IF CONTROL SYSTEM FILE                  
         BNE   RCVCL02                                                          
         SRL   R1,4                SHIFT OUT SIGN                               
         SLL   R1,8                SHIFT OUT HIGH ORDER ZERO                    
         O     R1,=X'8000000C'     SET NEW STYLE DATE FLAG (RTIMEFID)           
         CLC   SSBCNTL,NULLS       OFFLINE R1=HHMMSS0+                          
         BE    RCVCL02                                                          
         LLC   R0,SSBSYSID         ONLINE  R1=HHMMSSI+ (I=FAC SYSID)            
         SLL   R0,4                                                             
         OR    R1,R0                                                            
RCVCL02  ST    R1,RTIME            SET TIME IN RCVRHDR                          
         CLI   RRECTY,2            TEST CHANGE WITH SAVE COPY                   
         BE    RCVCL08                                                          
         CLC   SSBCNTL,NULLS       TEST OFFLINE                                 
         BNE   RCVCL04                                                          
         CLI   FIRSTIME,ON         TEST FIRST TIME FOR JOB                      
         BNE   RCVCL08                                                          
         B     RCVCL06                                                          
RCVCL04  L     R1,SSBTKADR         ONLINE-GET A(TCB ENTRY)                      
         USING TCBD,R1                                                          
         OC    TCBRCVC,TCBRCVC     CHECK ANY RECOVERY RECORD WRITTEN            
         BNZ   RCVCL08                                                          
         DROP  R1                                                               
RCVCL06  OI    RTIME+3,X'0F'       SET FIRST TIME FLAG IN RTIME                 
*                                                                               
RCVCL08  TM    TRAILERS,X'01'      TRAILER RECORDS                              
         BZ    RCVXIT                                                           
         OI    RTIME,RTIMEEXT      FLAG TRAILERS USED                           
*                                  GET TIME AS MILLISECS SINCE MIDNIGHT         
         ZAP   DUB,=P'0'                                                        
         MVO   DUB+6(2),CLOCKWRK+0(1) GET HH FROM HHMMSSMMMMMM0000              
         CVB   R1,DUB                                                           
         MHI   R1,60               R1=HH*60=MINUTES                             
         MVO   DUB+6(2),CLOCKWRK+1(1) GET MM FROM HHMMSSMMMMMM0000              
         CVB   R0,DUB                                                           
         AR    R1,R0               R1=HH*60+MM                                  
         MHI   R1,60               R1=(HH*60+MM)*60=SECONDS                     
         MHI   R1,1000             R1=(HH*60+MM)*60*1000=MILLISECS              
         MVC   DUB+5(3),CLOCKWRK+2 GET SSMMMM FROM HHMMSSMMMMMM0000             
         OI    DUB+7,X'0F'         SET SSMMMF                                   
         CVB   R0,DUB                                                           
         AR    R1,R0               R1=(HH*60+MM)*60*1000+SSMMM                  
         STCM  R1,15,RTIMEMS       STORE TIME IN MILLISECONDS                   
         B     RCVXIT                                                           
         EJECT                                                                  
***********************************************************************         
* ACQUIRE SHARED MEMORY BUFFER AND SET ACTIVE ENTRY                   *         
***********************************************************************         
RCVGETD  NTR1                                                                   
         XC    DUB,DUB             DSPACE RESOURCE NUMBER                       
         MVC   DUB+3(1),RCVRSYS                                                 
         GOTO1 VLOCKSPC,DUB        ON RETURN WE OWN THIS RESOURCE               
*                                                                               
         L     R2,4(R1)                                                         
         USING DMSPACED,R2                                                      
         MVC   ARECBUF,DSPECB      SAVE RESOURCE ADDRESS                        
*                                                                               
         TM    DSPTYPE,DSPJOBQ     TEST OFFLINE                                 
         BZ    RCVGETDA                                                         
         MVC   ACTVID(4),=X'FF000000'                                           
         MVC   ACTVID+2(2),DSPJOB  OFFLINE FF/JOBNO                             
         TM    BUFFM,BSHRMEMQ      TEST SHRMEM RECOVERY BUFFER                  
         BO    RCVGETD2                                                         
         B     RCVGETDB                                                         
         DROP  R2                                                               
*                                                                               
RCVGETDA L     R2,SSBTKADR         ONLINE USE FAC/SIN                           
         MVC   ACTVID(4),TCBSIN-TCBD(R2)                                        
         MVC   ACTVID(1),SSBSYSIX                                               
         TM    BUFFM,BSHRMEMQ                                                   
         BO    RCVGETD2                                                         
*                                                                               
RCVGETDB SAC   512                 IF GLOBAL SET TO AR MODE                     
         XC    DUB,DUB                                                          
         LAM   ARE,ARF,DUB         MAKE SURE THESE ARE CLEAR                    
         LAM   AR2,AR2,SSBALET                                                  
         SR    R2,R2                                                            
*                                                                               
         L     R2,ARECBUF          RESOURCE ADDRESS IN ARECBUF ??               
         USING DMSYSHDR,R2                                                      
         L     R2,DSYABUFF                                                      
         ST    R2,ARECBUF          SAVE BUFFER ADDRESS                          
         J     RCVXIT                                                           
         EJECT                                                                  
***********************************************************************         
* ATTACH TO SHARED MEMORY RECOVERY TABLE AND SAVE BUFF ADDR IN SE     *         
* A(C'SHMUSS') MUST PRECEDE REAL PARAM LIST SO CALL POINTS TO Q0      *         
* Q1=DMCB1,Q2=DMCB2,Q3=A(TABLE)                                       *         
***********************************************************************         
RCVGETD2 CLC   SSBCNTL,NULLS       TEST OFFLINE                                 
         BNE   RCVGETD4                                                         
         CLC   RCVDTF(1),RCVRSYS   YES-TEST SAME SYSTEM AS PREVIOUS             
         BNE   RCVGETD3                                                         
         SR    RF,RF                                                            
         ICM   RF,7,RCVDTF+1       GET SAVED DTF ADDRESS                        
         B     RCVGETD5                                                         
*                                                                               
RCVGETD3 BRAS  RE,GETDTF                                                        
         L     RF,ADTF                                                          
         ST    RF,RCVDTF                                                        
         MVC   RCVDTF(1),RCVRSYS                                                
         B     RCVGETD5                                                         
*                                                                               
RCVGETD4 L     RE,SSBTKADR         GET A(RECOVERY FILE) FROM SELIST             
         L     RE,TCBSE-TCBD(RE)                                                
         L     RF,SERCVDTF-SELISTD(RE)                                          
*                                                                               
RCVGETD5 CLI   DBLK-DTFPHD(RF),0   TEST HAVE SHRMEM ADDRESS                     
         BE    RCVGETD6            NO-ATTACH NOW                                
         L     R2,DBLK-DTFPHD(RF)                                               
         ST    R2,ARECBUF                                                       
         B     RCVGETDX                                                         
*                                                                               
RCVGETD6 XR    R0,R0               SET OVERRIDE KEY                             
         ICM   R0,1,RCVRSYS                                                     
         GOTO1 =V(DMSHMUSS),Q1,#ATTACH,#RCVB,0,(R0)                             
         LAM   ARE,AR1,ARZERO      CLEAR AFTER SHMUSS CALL!                     
         L     R2,Q3               GET TABLE ADDRESS                            
         ST    R2,ARECBUF          SAVE TABLE ADDRESS                           
*                                                                               
RCVGETD8 SR    RF,RF                                                            
         ICM   RF,7,RCVDTF+1       GET OFFLINE DTF ADDRESS                      
         LTR   RF,RF               TEST OFFLINE                                 
         BNZ   RCVGETD9            NON-ZERO MEANS OFFLINE                       
*                                                                               
         L     RE,SSBTKADR         GET A(RECOVERY FILE) FROM SELIST             
         L     RE,TCBSE-TCBD(RE)                                                
         L     RF,SERCVDTF-SELISTD(RE)                                          
*                                                                               
RCVGETD9 ST    R2,DBLK-DTFPHD(RF)  SAVE SHRMEM ADDRESS                          
*                                                                               
RCVGETDX B     *+10                PATCH HERE                                   
         XC    0(96,R2),0(R2)      TO CLEAR TABLE FOR TESTING                   
*                                                                               
         SAM31                     SET 31-BIT MODE IF SHARED MEMORY             
         J     RCVXIT                                                           
RCVDTF   DS    A                                                                
         EJECT                                                                  
***********************************************************************         
* GET LOCAL FILE DTF AND SET UP ACTVID                                *         
***********************************************************************         
RCVGETL  NTR1                                                                   
RCVGL    L     R0,=F'1000000'      MAX 1000000 TRIES                            
*                                                                               
RCVGLT   TM    DTFDEVF,X'20'       TEST IF RECOVERY FILE LOCKED                 
         BZ    RCVGL0              NO                                           
         LT    RF,VADWAIT          YES SET TO WAIT UNTIL FREE                   
         BZ    RCVGL0                                                           
         MVI   RCVECB,X'40'        SET ALREADY POSTED ECB                       
         LA    R1,RCVECB                                                        
         OILH  GR1,X'FF00'         SET FF IN ECB TO SHOW ITS US                 
         GOTO1 (RF),(R1)                                                        
         BCT   R0,RCVGLT           TRY 100 TIMES                                
         NI    DTFDEVF,X'DF'                                                    
         DC    H'0'                FREE THE FILE AND ABEND                      
RCVGLC   DC    F'1000000'                                                       
*                                                                               
RCVGL0   CL    R0,RCVGLC                                                        
         BH    *+8                                                              
         ST    R0,RCVGLC                                                        
         OI    DTFDEVF,X'20'       SET RECOVERY FILE LOCKED                     
*                                                                               
RCVGL1   LT    R2,SSBTKADR         ONLINE USE FAC/SIN                           
         BZ    RCVGL2                                                           
         MVC   ACTVID(4),TCBSIN-TCBD(R2)                                        
         MVC   ACTVID(1),SSBSYSIX                                               
         B     RCVGL3                                                           
*                                                                               
RCVGL2   MVC   ACTVID(4),=X'FFFFFFFF'                                           
         CLI   RCVRSYS,X'0A'       OFFLINE/VERMONT                              
         BNE   RCVGL3                                                           
         MVC   ACTVID(2),=X'FF00'  OFFLINE USE FF/JOBNUM                        
         MVC   ACTVID+2(2),JOBBIN                                               
*                                                                               
RCVGL3   L     R2,DBLK             R2=A(CORE BUFF)                              
         ST    R2,ARECBUF          SAVE BUFFER ADDRESS                          
         B     RCVXIT                                                           
         EJECT                                                                  
***********************************************************************         
* GET/SET ACTIVE ENTRY IN HEADER                                      *         
***********************************************************************         
RCVGETA  NTR1                                                                   
         SAC   0                                                                
         TM    BUFFM,BGLOBALQ      TEST GLOBAL DSPACE                           
         BZ    RCVGA002                                                         
         SAC   512                 GLOBAL SET TO AR MODE                        
*                                                                               
RCVGA002 XC    ACTVAD,ACTVAD       LOOK IN HEADER FOR ACTVID                    
         L     R2,ARECBUF                                                       
         USING VRCVHDR,R2          BUFFER                                       
         LA    R2,VRCVNTRY                                                      
         DROP  R2                                                               
         USING VRCVNTRY,R2         VERMONT ENTRY                                
         LA    R0,VRCVMAXQ                                                      
         TM    BUFFM,BEXTVMTQ      TEST EXTENDED VERMONTS                       
         BO    RCVGA012            YES, SKIP                                    
*                                                                               
RCVGA010 CLC   ACTVID,VRCVID#      TEST FOR ID                                  
         BE    RCVGA025                                                         
         OC    VRCVNTRY,VRCVNTRY   IS THIS ONE FREE                             
         BNZ   RCVGA011                                                         
         OC    ACTVAD,ACTVAD       DO WE HAVE ONE ALREADY                       
         BNZ   RCVGA011                                                         
         ST    R2,ACTVAD           NO SO SAVE THIS ONE                          
RCVGA011 LA    R2,VRCVLNQ(,R2)                                                  
         BCT   R0,RCVGA010                                                      
         B     RCVGA018                                                         
*                                                                               
RCVGA012 CLI   ACTVID,X'FF'        TEST OFFLINE                                 
         BNE   RCVGA014            YES, SKIP                                    
         CLC   ACTVID+2(2),VRCXJOB# TEST FOR JOB ID                             
         BE    RCVGA025                                                         
         B     RCVGA015                                                         
RCVGA014 CLC   ACTVID,VRCVID#      TEST FOR FACPAK ID/SIN                       
         BE    RCVGA025                                                         
RCVGA015 OC    VRCXNTRY,VRCXNTRY   IS THIS ONE FREE                             
         BNZ   RCVGA016                                                         
         OC    ACTVAD,ACTVAD       DO WE HAVE ONE ALREADY                       
         BNZ   RCVGA016                                                         
         ST    R2,ACTVAD           NO SO SAVE THIS ONE                          
RCVGA016 LA    R2,VRCXLNQ(,R2)                                                  
         BCT   R0,RCVGA012                                                      
         DROP  R2                                                               
*                                                                               
RCVGA018 OC    ACTVAD,ACTVAD       TASK NOT ACTIVE, TEST FOR FREE ENTRY         
         BNZ   RCVGA019                                                         
         SR    R2,R2               ZERO RETURN                                  
         B     RCVGA090                                                         
*                                                                               
RCVGA019 CLI   PARAM3+1,X'FE'      IF CLOSE/COMMIT CALL                         
         BL    *+10                                                             
         SR    R2,R2               CLEAR R2 IF NOT FOUND                        
         B     RCVGA090                                                         
*                                                                               
         CLI   RCVRSYS,X'0A'       CONTROL                                      
         BNE   RCVGA020                                                         
         CLC   TOKENCT,NULLS       NOT ALREADY ENQ                              
         BNE   RCVGA020                                                         
         BRAS  RE,RCVCTENQ         ENQ VERMONTS AND SET TOKENCT                 
RCVGA020 EQU   *                                                                
*&&UK                                                                           
         CLI   RCVRSYS,X'14'       MEDZ                                         
         BNE   RCVGA021                                                         
         CLC   TOKENMZ,NULLS       NOT ALREADY ENQ                              
         BNE   RCVGA021                                                         
         BRAS  RE,RCVMZENQ         ENQ VERMONTS AND SET TOKENMZ                 
*&&                                                                             
RCVGA021 L     R2,ACTVAD           CREATE NEW ENTRY                             
         USING VRCVNTRY,R2                                                      
         MVC   VRCVID#,ACTVID                                                   
         TM    BUFFM,BEXTVMTQ      TEST EXTENDED VERMONTS                       
         BZ    RCVGA090            NO, SKIP                                     
RCVGA022 CLI   ACTVID,X'FF'        TEST OFFLINE                                 
         BNE   RCVGA090            NO, SKIP                                     
         OC    SSOSIN,SSOSIN       TEST OFFLINE SIN SET (PROBABLY SET           
         BNZ   RCVGA023            ALREADY AT RCV2D1)                           
         BRAS  RE,RCVGTSIN         NO, GET/SET GLOBAL SIN                       
RCVGA023 MVC   VRCVSIN#,SSOSIN+1   SET GLOBAL SIN                               
         MVC   VRCXJOB#,ACTVID+2   JOB ID SEPARATE IN EXTENDED VERMONT          
         B     RCVGA090                                                         
         DROP  R2                                                               
*                                                                               
RCVGA025 ST    R2,ACTVAD           SAVE EXISTING ACTIVE ENTRY ADDRESS           
*                                                                               
RCVGA090 LTR   R2,R2                                                            
         B     RCVXIT                                                           
***********************************************************************         
* GET NEXT GLOBAL SIN FROM TABS INTO SSOSIN - OFFLINE ONLY            *         
***********************************************************************         
RCVGTSIN NTR1                                                                   
RCVGS010 OC    SSOTBLET,SSOTBLET   DO WE HAVE TABS?                             
         JNZ   RCVGS020                                                         
         MVC   DUB(4),=X'20008001' NO, THEN GET IT                              
         XC    DUB+4(4),DUB+4                                                   
         GOTO1 VLOCKSPC,DUB                                                     
         J     RCVGS010                                                         
RCVGS020 LAM   AR4,AR4,SSOTBLET    PICK UP TABS ALET OFFLINE                    
         IAC   R0                                                               
         JH    *+8                 ALREADY AR MODE                              
         SAC   512                                                              
         XR    R4,R4                                                            
         USING FATABSD,R4                                                       
         L     R4,TABSCOMM         GET COMMON AREA                              
         USING TCOMMOND,R4                                                      
RCVGS100 L     RF,TCORSIN                                                       
         LR    R1,RF               MATCH WITH TABSRSIN                          
         AHI   RF,1                UP THE COUNT                                 
         CLFI  R1,X'00FFFFFE'      CHECK LOCK VALUE, DON'T WANT FFFFFF          
         BNE   *+8                 NO, DON'T RESET SIN TO 2                     
         LA    RF,2                AVOID -1, 0 AND 1 VALUES                     
         CS    R1,RF,TCORSIN                                                    
         BNE   RCVGS100            TRY AGAIN                                    
         ST    RF,SSOSIN           SAVE AS OFFLINE SIN                          
         DROP  R4                                                               
         LAM   AR4,AR4,ARZERO                                                   
         TM    BUFFM,BGLOBALQ      TEST GLOBAL DSPACE                           
         BO    RCVXIT              YES, STAY IN AR MODE                         
         SAC   0                   CLEAR AR MODE                                
         J     RCVXIT                                                           
         EJECT                                                                  
***********************************************************************         
* DO SHARED ENQ/DEQ TO CHECK FOR CONTROL DUMP/LOAD                    *         
***********************************************************************         
RCVCTENQ NTR1                                                                   
*                                                                               
RCVCT001 ISGENQ REQUEST=OBTAIN,QNAME=MAJORNQ,RNAME=MINORCT,            X        
               RNAMELEN=MINORCTL,SCOPE=SYSTEMS,CONTROL=SHARED,         X        
               ENQTOKEN=TOKENCT,RESLIST=NO,RNL=NO,COND=YES,            X        
               CONTENTIONACT=WAIT,WAITTYPE=SUSPEND,RETCODE=15,RSNCODE=0         
         LTR   RF,RF                                                            
         BZ    RCVXIT                                                           
         CIJNE RF,4,*+2            ISGENQRC_WARN                                
         NILH  GR0,X'0000'                                                      
         CHI   R0,ISGENQRSN_UNPROTECTEDQNAME                                    
         BE    RCVXIT              WE GOT IT                                    
         DC    H'0'                                                             
                                                                                
RCVCTDEQ NTR1                                                                   
         L     R2,ARECBUF          SCAN BUFFER FOR THIS ADV                     
         USING VRCVHDR,R2                                                       
         LA    R2,VRCVNTRY                                                      
         DROP  R2                                                               
         USING VRCVNTRY,R2                                                      
         LA    R0,VRCVMAXQ                                                      
         TM    BUFFM,BGLOBALQ      TEST GLOBAL DSPACE                           
         BZ    *+8                                                              
         SAC   512                 GLOBAL SET TO AR MODE                        
         LA    R1,VRCVLNQ                                                       
         TM    BUFFM,BEXTVMTQ      TEST EXTENDED VERMONTS                       
         BZ    RCVCTDE1            NO, SKIP                                     
         LA    R1,VRCXLNQ                                                       
*                                                                               
RCVCTDE1 CLC   SSBCNTL,NULLS       IF OFFLINE THEN DEQ                          
         BE    RCVCTDE2                                                         
         CLC   VRCVFID#,SSBSYSIX   TEST FOR THIS FACPAK                         
         BE    RCVXIT              DON'T DEQ IF FOUND                           
         AR    R2,R1                                                            
         BCT   R0,RCVCTDE1         TRY ALL                                      
         DROP  R2                                                               
*                                                                               
RCVCTDE2 SAC   0                                                                
         ISGENQ REQUEST=RELEASE,ENQTOKEN=TOKENCT,                      X        
               COND=YES,RETCODE=15,RSNCODE=0                                    
         LTR   RF,RF                                                            
         JNZ   *+2                                                              
         XC    TOKENCT,TOKENCT                                                  
         B     RCVXIT                                                           
         EJECT                                                                  
*&&UK                                                                           
***********************************************************************         
* DO SHARED ENQ/DEQ TO CHECK FOR MEDZ DUMP/LOAD                       *         
***********************************************************************         
RCVMZENQ NTR1                                                                   
*                                                                               
RCVMZ001 ISGENQ REQUEST=OBTAIN,QNAME=MAJORNQ,RNAME=MINORMZ,            X        
               RNAMELEN=MINORMZL,SCOPE=SYSTEMS,CONTROL=SHARED,         X        
               ENQTOKEN=TOKENMZ,RESLIST=NO,RNL=NO,COND=YES,            X        
               CONTENTIONACT=WAIT,WAITTYPE=SUSPEND,RETCODE=15,RSNCODE=0         
         LTR   RF,RF                                                            
         BZ    RCVXIT                                                           
         CIJNE RF,4,*+2            ISGENQRC_WARN                                
         NILH  GR0,X'0000'                                                      
         CHI   R0,ISGENQRSN_UNPROTECTEDQNAME                                    
         BE    RCVXIT              WE GOT IT                                    
         DC    H'0'                                                             
                                                                                
RCVMZDEQ NTR1                                                                   
         L     R2,ARECBUF          SCAN BUFFER FOR THIS ADV                     
         USING VRCVHDR,R2                                                       
         LA    R2,VRCVNTRY                                                      
         DROP  R2                                                               
         USING VRCVNTRY,R2                                                      
         LA    R0,VRCVMAXQ                                                      
         TM    BUFFM,BGLOBALQ      TEST GLOBAL DSPACE                           
         BZ    RCVMZDE1                                                         
         SAC   512                 GLOBAL SET TO AR MODE                        
         LA    R1,VRCVLNQ                                                       
         TM    BUFFM,BEXTVMTQ      TEST EXTENDED VERMONTS                       
         BZ    RCVMZDE1            NO, SKIP                                     
         LA    R1,VRCXLNQ                                                       
*                                                                               
RCVMZDE1 CLC   SSBCNTL,NULLS       IF OFFLINE THEN DEQ                          
         BE    RCVMZDE2                                                         
         CLC   VRCVFID#,SSBSYSIX   TEST FOR THIS FACPAK                         
         BE    RCVXIT              DON'T DEQ IF FOUND                           
         AR    R2,R1                                                            
         BCT   R0,RCVMZDE1         TRY ALL                                      
         DROP  R2                                                               
*                                                                               
RCVMZDE2 SAC   0                                                                
         ISGENQ REQUEST=RELEASE,ENQTOKEN=TOKENMZ,                      X        
               COND=YES,RETCODE=15,RSNCODE=0                                    
         LTR   RF,RF                                                            
         JNZ   *+2                                                              
         XC    TOKENMZ,TOKENMZ                                                  
         B     RCVXIT                                                           
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* ADD RECORD TO VERMONT RECOVERY BLOCK                                *         
***********************************************************************         
RCVADD   NTR1                                                                   
         L     R2,ACTVAD           POINT TO ACTIVE ENTRY                        
         MVC   DUB,0(R2)           COPY IT TO DUB                               
*                                                                               
         LH    R1,RECLEN           GET RECORD LENGTH IN R1                      
         AHI   R1,DMRCVLNQ         ALLOW FOR LENGTH AND LINK TTBR               
         TM    TRAILERS,X'01'      TEST TRAILER                                 
         BZ    *+8                                                              
         AHI   R1,EXTQ             ADD TRAILER LENGTH                           
         STH   R1,DUB              SAVE IN FIRST TWO BYTES OF DUB               
*                                                                               
         L     R2,ARECBUF          POINT TO BUFFER                              
         USING VRCVHDR,R2                                                       
         LH    R1,VRCVNXT          DISP TO FREE SPACE                           
         DROP  R2                                                               
         AR    R2,R1               POINT TO START OF NEW DATA                   
         MVC   DMRCVLN-DMRCVRD(,R2),DUB SET RECORD LENGTH                       
         MVC   DMRCVLNK-DMRCVRD(,R2),DUB+4 SET OLD TTBR AS LINK                 
         AHI   R2,DMRCVLNQ                                                      
*                                                                               
         L     RE,RECADD                                                        
         LR    R0,R3                                                            
         LH    R3,RECLEN                                                        
         LR    RF,R3                                                            
         MVCL  R2,RE               MOVE RECORD TO DATASPACE                     
         LR    R3,R0                                                            
*                                                                               
         TM    TRAILERS,X'01'      TEST TRAILER                                 
         BZ    RCVA02                                                           
         L     R2,ARECBUF          POINT TO BUFFER                              
         USING VRCVHDR,R2                                                       
         LH    R1,VRCVNXT          DISP TO FREE SPACE                           
         DROP  R2                                                               
         AR    R2,R1               POINT TO EOR                                 
         AHI   R2,DMRCVLNQ                                                      
         AH    R2,RECLEN                                                        
         MVC   0(EXTQ,R2),RECVEXT                                               
*                                                                               
RCVA02   L     R2,ARECBUF                                                       
         USING VRCVHDR,R2                                                       
         LH    R1,VRCVNXT                                                       
         AH    R1,DUB                                                           
         STH   R1,VRCVNXT                                                       
*                                                                               
         IC    RE,VRCVDA+3         UPDATE HIGH RECORD NUMBER                    
         LA    RE,1(RE)                                                         
         STC   RE,VRCVDA+3                                                      
         MVC   DUB+4(4),VRCVDA     SET DA INTO ACTIVE ENTRY                     
         DROP  R2                                                               
         L     R2,ACTVAD                                                        
         USING VRCVNTRY,R2                                                      
         MVC   VRCVLINK,DUB+4                                                   
         TM    BUFFM,BEXTVMTQ      IF USING EXTENDED VERMONTS                   
         BZ    RCVA02A                                                          
         OC    VRCXA1ST,VRCXA1ST   AND FIRST ENTRY DA NOT YET SET               
         BNZ   RCVA02A                                                          
         MVC   VRCXA1ST,DUB+4      SET IT NOW                                   
         DROP  R2                                                               
*                                                                               
RCVA02A  CLC   SSBCNTL,NULLS       SET RECOVERY INFO IN OFFLINE SSB             
         BNE   RCVA02J                                                          
         CLI   SSOXTND,X'FF'       TEST IF EXTENDED OFFLINE SSB                 
         BNE   RCVXIT                                                           
         L     RF,RECADD                                                        
RCVA02B  CLI   1(RF),2             BUMP RECOVERY CHANGE COUNT                   
         BNE   RCVA02C                                                          
         LH    RE,SSORCHG                                                       
         AHI   RE,1                                                             
         STH   RE,SSORCHG                                                       
         B     RCVXIT                                                           
RCVA02C  CLI   1(RF),3             BUMP RECOVERY CHANGE COUNT                   
         BNE   RCVXIT                                                           
         LH    RE,SSORADD                                                       
         AHI   RE,1                                                             
         STH   RE,SSORADD                                                       
         B     RCVXIT                                                           
*                                                                               
RCVA02J  L     R5,SSBTKADR         SET RECOVERY INFO IN TCB ENTRY               
         USING TCBD,R5                                                          
         MVC   TCBRCVL,DUB+4       LAST DISK ADDR TTBR                          
*                                                                               
         OC    TCBRCVF(3),TCBRCVF                                               
         BNZ   *+14                                                             
         MVC   TCBRCVF,TCBRCVL     FIRST DISK ADDR TTBR                         
         OI    TCBINDS1,TCBFUPD    SET UPDATIVE TRANSACTION                     
*                                                                               
RCVA02K  LH    RE,TCBRCVC          BUMP RECOVERY RECORD COUNT                   
         AHI   RE,1                                                             
         STH   RE,TCBRCVC                                                       
         L     RF,RECADD                                                        
RCVA02L  CLI   1(RF),2             BUMP RECOVERY CHANGE COUNT                   
         BNE   RCVA02M                                                          
         LH    RE,TCBRCHG                                                       
         AHI   RE,1                                                             
         STH   RE,TCBRCHG                                                       
         B     RCVA02R                                                          
RCVA02M  CLI   1(RF),3             BUMP RECOVERY ADD COUNT                      
         BNE   RCVA02R                                                          
         LH    RE,TCBRADD                                                       
         AHI   RE,1                                                             
         STH   RE,TCBRADD                                                       
*                                                                               
RCVA02R  L     R5,TCBSE            SET RECOVERY PENDING                         
         OI    SEIND-SELISTD(R5),X'40'                                          
         DROP  R5                                                               
*&&US                                                                           
         TM    SSBSYSFL,FACIREP+FACITST                                         
         BZ    RCVXIT                                                           
         TM    SSBSTAT5,SSB5FAMO   PARM TO TURN ON FAC-MO TRANSFER              
         BZ    RCVXIT                                                           
         L     RF,RECADD                                                        
         CLI   0(RF),X'82'         ONLY REP FILE RECORDS                        
         BNE   RCVXIT                                                           
         CLI   1(RF),1             IGNORE COPIES                                
         BE    RCVXIT                                                           
         TM    1(RF),X'80'         IGNORE POINTER COPIES                        
         BO    RCVXIT                                                           
*                                                                               
         LT    RE,RCVPTAB                                                       
         BZ    RCVA03                                                           
         C     RE,=A(REFAMOT)      SEE IF LOCAL COPY OR IN REGION               
         BNE   RCVA07              CANT CHECK IF NOT IN CORE                    
         CLC   REFAMOS,REFAMOE     DO EYE CATCHERS MATCH ?                      
         BE    RCVA07              YES-SO TABLE IS PROBABLY OK                  
         SAC   0                                                                
         WTO   'REFAMO WARNING - SOMETHING DESTROYED REFAMO TABLE'              
*                                                                               
         MVC   REFAMOS,=2C'**REFAMO'    RESET                                   
         MVC   REFAMOE,=2C'**REFAMO'                                            
*                                                                               
RCVA03   SAC   0                                                                
         LOAD   EP=REFAMO                                                       
         LTR   RF,RF                                                            
         JNZ   *+2                                                              
         ST    R0,RCVPTAB          SAVE THIS JUST INCASE                        
*                                                                               
         NILH  GR1,X'00FF'         CLEAR HIGH ORDER BYTE                        
         SLL   R1,3                LENGTH IS IN DOUBLE WORDS (X8)               
         CHI   R1,REFAMOL          LENGTH OF AREA                               
         BNH   RCVA04              THIS IS OKAY                                 
         WTO   'REFAMO WARNING -> REFAMO AREA TOO SMALL'                        
         B     RCVA05                                                           
*                                                                               
RCVA04   LHI   RF,REFAMOL          SIZE OF AREA                                 
         L     RE,=A(REFAMOT)      WHERE TO MOVE IT TO                          
         ST    RE,RCVPTAB          SAVE NEW ADDRESS OF TABLE                    
         MVCL  RE,R0               R1 = SIZE OF PHASE, R0=A(REFAMO)             
*                                                                               
         DELETE EP=REFAMO          REMOVE IT FROM THE REGION                    
         LTR   RF,RF                                                            
         JNZ   *+2                                                              
*                                                                               
RCVA05   TM    BUFFM,BGLOBALQ      TEST GLOBAL DSPACE                           
         BZ    RCVA06                                                           
         SAC   512                 GLOBAL SET TO AR MODE                        
*                                                                               
RCVA06   L     RE,RCVPTAB                                                       
*                                                                               
RCVA07   L     R0,0(RE)            REP GETS ONE SET                             
         TM    SSBSYSFL,FACIREP                                                 
         BO    *+8                                                              
         L     R0,4(RE)            TEST GETS ANOTHER                            
         AR    RE,R0                                                            
*                                                                               
         CLI   L'RECVHDR(RF),X'0C' CONTRACT RECORDS                             
         BNE   *+12                                                             
         AHI   RF,2+L'RECVHDR      AGY ALPHA IS AT +2 FOR CONTRACTS             
         B     RCVA08                                                           
*                                                                               
         CLI   L'RECVHDR(RF),X'32' CONTRACT TYPE CODES                          
         BNE   *+12                                                             
         AHI   RF,L'RECVHDR+24     AGY ALPHA IS AT +24                          
         B     RCVA08                                                           
*                                                                               
         CLI   L'RECVHDR(RF),X'02' STATION                                      
         BNE   *+12                                                             
         AHI   RF,L'RECVHDR+20     AGY ALPHA IS AT +20                          
         B     RCVA08                                                           
*                                                                               
         CLI   L'RECVHDR(RF),X'08' ADVERTISER                                   
         BNE   *+12                                                             
         AHI   RF,L'RECVHDR+25     AGY ALPHA IS AT +25                          
         B     RCVA08                                                           
*                                                                               
         CLI   L'RECVHDR(RF),X'1A' AGENCY 2NDARY                                
         BNE   *+12                                                             
         AHI   RF,L'RECVHDR+25     AGY ALPHA IS AT +25                          
         B     RCVA08                                                           
*                                                                               
         CLI   L'RECVHDR(RF),X'06' SALESPERSON                                  
         BNE   *+12                                                             
         AHI   RF,L'RECVHDR+22     AGY ALPHA IS AT +22                          
         B     RCVA08                                                           
*                                                                               
         CLI   L'RECVHDR(RF),X'3A' DEV S/P                                      
         BNE   *+12                                                             
         AHI   RF,L'RECVHDR+22     AGY ALPHA IS AT +22                          
         B     RCVA08                                                           
*                                                                               
         CLI   L'RECVHDR(RF),X'3B' DEV CON TYPE                                 
         BNE   *+12                                                             
         AHI   RF,L'RECVHDR+23     AGY ALPHA IS AT +23                          
         B     RCVA08                                                           
*                                                                               
         CLI   L'RECVHDR(RF),X'01' REP RECORD                                   
         BNE   *+12                                                             
         AHI   RF,L'RECVHDR+25     AGY ALPHA IS AT +25                          
         B     RCVA08                                                           
*                                                                               
         CLI   L'RECVHDR(RF),X'0D' CLASS RECORD                                 
         BNE   *+12                                                             
         AHI   RF,L'RECVHDR+23     AGY ALPHA IS AT +23                          
         B     RCVA08                                                           
*                                                                               
         CLI   L'RECVHDR(RF),X'0F' CATEGORY RECORD                              
         BNE   *+12                                                             
         AHI   RF,L'RECVHDR+23     AGY ALPHA IS AT +23                          
         B     RCVA08                                                           
*                                                                               
         CLI   L'RECVHDR(RF),X'07' GROUP RECORD                                 
         BNE   *+12                                                             
         AHI   RF,L'RECVHDR+23     AGY ALPHA IS AT +23                          
         B     RCVA08                                                           
*                                                                               
         CLI   L'RECVHDR(RF),X'2B' MARKET RECORD                                
         BNE   *+12                                                             
         AHI   RF,L'RECVHDR+21     AGY ALPHA IS AT +21                          
         B     RCVA08                                                           
*                                                                               
         CLI   L'RECVHDR(RF),X'04' OFFICE RECORD                                
         BNE   *+12                                                             
         AHI   RF,L'RECVHDR+23     AGY ALPHA IS AT +23                          
         B     RCVA08                                                           
*                                                                               
         CLI   L'RECVHDR(RF),X'05' TEAM RECORD                                  
         BNE   *+12                                                             
         AHI   RF,L'RECVHDR+23     AGY ALPHA IS AT +23                          
         B     RCVA08                                                           
*                                  MORE RECORDS GO HERE                         
         B     RCVXIT                                                           
*                                                                               
RCVA08   CLI   0(RE),X'FF'         MATCH AGY ALPHA IN RECORD TO LIST            
         BE    RCVXIT                                                           
         OC    0(2,RE),0(RE)                                                    
         BZ    RCVXIT                                                           
         CLC   0(2,RE),0(RF)                                                    
         BE    *+12                                                             
         AHI   RE,2                                                             
         B     RCVA08                                                           
         BRAS  RE,FORMOSA          GO SEND MQ MESSAGE FOR MO                    
         B     RCVXIT                                                           
         DS    0D                                                               
         DC    C'ARCVTAB*'                                                      
RCVPTAB  DC    A(0)                                                             
*&&                                                                             
RCVXIT   LAM   AR2,AR2,SSBALET                                                  
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* GET A(DTF) FOR RECOVERY FILE OF RCVRSYS                             *         
***********************************************************************         
GETDTF   SAC   0                                                                
         LLC   RF,RCVRSYS          INDEX INTO 16-BYTE SYSTEM TABLE              
         SLL   RF,4                                                             
         A     RF,=V(SYSTABL)                                                   
         L     RF,DMSYASYF-SYSTABD(RF)                                          
         USING SYSFLSTD,RF         RF=A(SYSFLES LIST ENTRY)                     
*                                                                               
GETD020  LH    R1,SYSF#FLS         R1=NUMBER OF FILES                           
         LA    RF,SYSFLIST         RF=A(FIRST FILE ENTRY)                       
*                                                                               
GETD030  TM    SYSFIND1,SFRCV      TEST IF THIS IS THE RECOVERY FILE            
         BO    GETD040                                                          
         LA    RF,SYSFLNQ(RF)                                                   
         BCT   R1,GETD030                                                       
         DC    H'0'                                                             
*                                                                               
GETD040  ICM   R1,7,SYSFADTF       RETURN A(DTF) IN R1                          
         ST    R1,ADTF                                                          
         BR    RE                                                               
         DROP  RF                                                               
         EJECT                                                                  
***********************************************************************         
* ERRORS ON RECOVERY I/O                                              *         
***********************************************************************         
DEATH    XR    RE,RE               GET DTF ADDRESS                              
         ICM   RE,7,P4+1                                                        
         MVC   DM(8),22(RE)        MOVE FILE NAME TO MSG                        
         MVC   DM+9(L'WRTERR),WRTERR                                            
         TM    P3+1,X'04'          TEST EOF                                     
         BZ    *+10                                                             
         MVC   DM+9(L'EOFERR),EOFERR                                            
         MVC   DM+23(L'PGMABT),PGMABT                                           
         CLC   SSBCNTL,NULLS                                                    
         BNE   DEATH1                                                           
         GOTO1 VDMOD000,DEADMSG                                                 
         DC    H'0'                                                             
*                                                                               
DEATH1   MVC   DM+23(L'SYSSTP),SYSSTP                                           
         L     R5,SSBTKADR                                                      
         USING TCBD,R5                                                          
         L     R5,TCBSE                                                         
         USING SELISTD,R5                                                       
         NI    SEIND,X'7F'         SET SYSTEM NOT STARTED                       
         OI    SEIND,X'03'         SET SYSTEM FORCED NOP ???                    
         GOTO1 VDMOD000,DEADMSG                                                 
         DC    H'0'                                                             
*                                                                               
DEADMSG  DC    V(WCTYPE)                                                        
         DC    A(DM)                                                            
         DC    A(L'DM)                                                          
*                                                                               
DM       DC    CL38'XXXXXXXX WRITE ERROR - SYSTEM STOPPED '                     
WRTERR   DC    CL11'WRITE ERROR'                                                
EOFERR   DC    CL11'END OF FILE'                                                
PGMABT   DC    CL15'PROGRAM ABORTED'                                            
SYSSTP   DC    CL15'SYSTEM STOPPED '                                            
         EJECT                                                                  
***********************************************************************         
* ONLINE TEST MODE RECOVERY - WRITE RECOVERY RECS TO TSTRCVR          *         
***********************************************************************         
         USING UTLD,R5                                                          
RCVT     TM    TTEST,TTESTTAC      EXIT IF NOT IN TEST MODE                     
         BZ    XMOD                                                             
         ICM   R7,15,TACCS                                                      
         BZ    XMOD                EXIT IF NOT DEFINED                          
         USING TSTTABD,R7                                                       
         CLC   TNUM,TSTNUM                                                      
         BNE   XMOD                EXIT IF NOT ACTIVE                           
*                                                                               
RCVT1    OC    TSTLOW,TSTLOW       DOES TESTER WANT RECORDS WRITTEN             
         BZ    XMOD                NO                                           
         TM    TSTFLAGS,TSTFUPDX   TEST INHIBIT FLAG                            
         BO    XMOD                                                             
*                                                                               
         BRAS  RE,RCVCLOCK         SET DATE AND TIMES IN RECOVERY REC           
*                                                                               
         GOTO1 VDMOD000,P1,V(DMEXT),,,X'F9000000'                               
*                                                                               
RCVT2    MVC   P1,=A(ADDAFT)       WRITE RECOVERY RECORD TO TSTRCVR             
         L     RE,P2                                                            
         AHI   RE,-(HDRQ)                                                       
         MVC   SAVE,0(RE)                                                       
         MVC   0(HDRQ,RE),RECVHDR                                               
         ST    RE,P2                                                            
         LH    RE,PARAM3+2                                                      
         LA    RE,HDRQ(RE)                                                      
         ST    RE,P3                                                            
         LA    RE,TSTLAST          SET LAST RECORD ADDED                        
         ST    RE,P5                                                            
         MVC   P6(4),TSTHIGH       SET HIGH TRACK & CAPACITY                    
         GOTO1 VDADDS,P1                                                        
         L     RE,P2                                                            
         MVC   0(HDRQ,RE),SAVE                                                  
         AHI   RE,HDRQ             RESTORE FOR SECOND PASS                      
         ST    RE,P2                                                            
         MVC   TSTCPTY,P6+2        SAVE TRACK CAPACITY                          
*                                                                               
RCVT3    OC    P3(2),P3            TEST FOR ERRORS                              
         BNZ   RCVT4                                                            
         XC    PARAM3(2),PARAM3                                                 
         B     XMOD                                                             
*                                                                               
RCVT4    TM    P3+1,X'04'          END OF CONTROL INTERVAL                      
         BZ    RCVT5               NO                                           
         CLC   TSTLAST(2),TSTLOW   YES TEST CI OUTSIDE EXTENTS                  
         BE    RCVT5                                                            
         XC    TSTLAST,TSTLAST     SET BACK TO FIRST REC IN CI                  
         MVC   TSTLAST(2),TSTLOW                                                
         B     RCVT2               GO DO IT AGAIN                               
*                                                                               
RCVT5    OI    TSTFLAGS,TSTFUPDX   SET INHIBIT FLAG                             
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* OFFLINE SEQUENTIAL OUTPUT FILE RECOVERY                             *         
***********************************************************************         
RCVS     CLI   SSOXTND,X'FF'       MUST HAVE EXTENDED OFFLINE SSB               
         JNE   *+2                                                              
*                                                                               
RCVS1    BRAS  RE,RCVCLOCK         SET DATE AND TIMES IN RECOVERY REC           
*                                                                               
         L     R1,P2               GET ADDRESS OF RECORD                        
         AHI   R1,-(HDRQ+4)        BACK UP FOR HEADER AND RECORD LEN            
         ST    R1,ASAVE                                                         
         MVC   SAVEHDR(HDRQ+4),0(R1)    SAVE 28 BYTES                           
         XC    0(4,R1),0(R1)       CLEAR LENGTH AREA                            
         MVC   4(HDRQ,R1),RECVHDR                                               
         LH    RE,PARAM3+2         GET LENGTH OF RECORD                         
         LA    RE,HDRQ+4(RE)       ADJUST LENGTH FOR HEADER                     
         STCM  RE,3,0(R1)          SET LENGTH OF RECOVERY RECORD                
         TM    TRAILERS,X'01'                                                   
         BZ    RCVS2                                                            
         LA    RF,0(RE,R1)         POINT TO END OF RECORD                       
         AHI   RE,EXTQ             ADD TRAILER LENGTH                           
         STCM  RE,3,0(R1)          SET NEW LENGTH                               
         MVC   SAVETRL,0(RF)       INCASE BUFFER IS TOO SMALL                   
         ST    RF,ASAVEEXT         SAVE START OF TRAILER                        
         MVC   0(EXTQ,RF),RECVEXT                                               
*                                                                               
RCVS2    LT    RF,SSOSQRTN         GET A(SEQUENTIAL OUTPUT ROUTINE)             
         JZ    *+2                                                              
         BASR  RE,RF               GOTO ROUTINE WITH R1=A(RECORD)               
*                                                                               
RCVS3    OC    SSOSQCNT,SSOSQCNT   BUMP RECOVERY RECORD TYPE COUNTS             
         BZ    *+8                                                              
         BRAS  RE,RCVCNT                                                        
*                                                                               
RCVSX    L     R1,ASAVE            RESTORE SAVE AREA                            
         MVC   0(HDRQ+4,R1),SAVEHDR                                             
         TM    TRAILERS,X'01'                                                   
         BZ    RCVSX2                                                           
         L     RE,ASAVEEXT         POINT TO TRAILER AREA                        
         MVC   0(EXTQ,RE),SAVETRL                                               
*                                                                               
RCVSX2   B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* OFFLINE WORK FILE RECOVERY - WRITE RECOVERY RECS TO FACWRK          *         
***********************************************************************         
RCVW     CLI   SSOXTND,X'FF'       MUST HAVE EXTENDED OFFLINE SSB               
         JNE   *+2                                                              
         LT    R7,SSOFWNDX         R7=A(FACWRK INDEX AREA)                      
         USING UKRECD,R7                                                        
         JZ    *+2                                                              
         OC    UKUSRID,UKUSRID     USER ID MUST BE DEFINED IN INDEX             
         JZ    *+2                                                              
         OC    SSOFWBUF,SSOFWBUF   MUST PASS A(FACWRK BUFFER)                   
         JZ    *+2                                                              
*                                                                               
         MVC   SSOSQCNT,=A(TEMPCNT)                                             
*NOP*    OC    SSOSQCNT,SSOSQCNT   BUMP RECOVERY RECORD TYPE COUNTS             
*NOP*    BZ    *+8                                                              
         BRAS  RE,RCVCNT                                                        
*                                                                               
RCVW1    L     RF,P2               GET ADDRESS OF RECORD                        
         AHI   RF,-(HP4Q)          BACK UP FOR HEADER AND RECORD LEN            
         ST    RF,ASAVE                                                         
         MVC   SAVEHDR(HP4Q),0(RF) SAVE 28 BYTES                                
         MVC   4(HDRQ,RF),RECVHDR                                               
         LH    RE,PARAM3+2         GET LENGTH OF RECORD                         
         LA    RE,HP4Q(RE)         ADJUST LENGTH FOR HEADER                     
         SLL   RE,16                                                            
         STCM  RE,15,0(RF)         SET LENGTH OF FACWRK RECORD                  
*                                                                               
         TM    TRAILERS,X'01'      TEST TRAILERS WANTED                         
         BZ    RCVW2                                                            
         OI    4+RTIME-RECVHDR(RF),RTIMEEXT                                     
         SRL   RE,16               RE=4+L'HDR+L'DATA                            
         LA    R1,0(RE,RF)         R1=A(END OF RECORD)                          
         ST    R1,ASAVEEXT         SAVE A(TRAILER)                              
         MVC   SAVETRL,0(R1)                                                    
         MVC   0(EXTQ,R1),RECVEXT                                               
         AHI   RE,EXTQ             RE=4+L'HDR+L'DATA+L'TRAILER                  
         STCM  RE,3,0(RF)          SET NEW LENGTH                               
*                                                                               
RCVW2    LA    R1,=C'FACWRK'       SET PARAM LIST TO WRITE TO FACWRK            
         ST    R1,Q2               A(WORK FILE NAME)                            
         ST    R7,Q3               A(INDEX)                                     
         ST    RF,Q4               A(RECORD-LEN IN 1ST 4 BYTES)                 
         MVC   Q5,SSOFWBUF         A(BUFFER)                                    
*                                                                               
RCVW3    LA    RF,ADD              SET ACTION FROM INDEX DATA                   
         OC    UKFILNO,UKFILNO                                                  
         BNZ   RCVW3A                                                           
         LA    RF,ADD              OPEN IF ZERO FILE NUM IN INDEX               
         B     RCVW4                                                            
*                                                                               
RCVW3A   L     R1,P2                                                            
         CLC   0(L'EOFREC,R1),EOFREC                                            
         BNZ   *+8                                                              
         LA    RF,CLOSE            CLOSE IF DUMMY EOF REC PASSED                
*                                                                               
RCVW4    GOTO1 VDATAMGR,Q1,(RF)    PUT RECOVERY REC TO FACWRK                   
         CLI   8(R1),0                                                          
         JNE   *+2                 ERROR ON FACWRK FILE                         
*                                                                               
RCVWX    L     RF,0(R1)            POINT TO FACWRK ACTION                       
         CLC   CLOSE,0(RF)                                                      
         BE    RCVWXX                                                           
         OI    SSOFLAG2,SSO2AWRK   SET ADD TO FACWRK ISSUED                     
         L     RF,Q4                                                            
         CLI   5(RF),2             ONLY CHANGES OR ADDS                         
         BL    RCVWXX                                                           
*                                                                               
         LT    RF,VUPSOON                                                       
         BZ    RCVWXX                                                           
         L     R0,Q4                                                            
         GOTO1 (RF),Q1,C'PUTR',(R0)                                             
*                                                                               
RCVWXX   L     RF,ASAVE            RESTORE SAVE AREA                            
         MVC   0(HP4Q,RF),SAVEHDR                                               
         TM    TRAILERS,X'01'      TEST TRAILERS PRESENT                        
         BZ    XMOD                                                             
         L     RF,ASAVEEXT                                                      
         MVC   0(EXTQ,RF),SAVETRL  RESTORE TRAILER AREA                         
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* CLOSE/COMMIT TRANSACTION CALL                                       *         
***********************************************************************         
RCVCLOSE CLC   SSBCNTL,NULLS       TEST ONLINE                                  
         BNE   RCVC02              YES                                          
         TM    SSOFLAG1,SSOFSJOB   TEST SJOB SENT                               
         BZ    RCVX                NO, NOTHING TO CLOSE/COMMIT                  
         CLI   PARAM3+1,X'FE'      TEST COMMIT TRANSACTION                      
         BNE   RCVC01              NO                                           
         TM    SSBSTAT2,SSOSRWRK+SSOSRSQF RECOVERING TO FACWRK OR QSAM?         
         BZ    RCVC01              NO                                           
         TM    SSOFLAG1,SSOFRCVR   YES, GLOBAL FULL RECOVERY?                   
         BZ    RCVX                NO, COMMIT (TO CLEAR LOCKTAB) IS OK          
         DC    H'0'                YES, COMMIT NOT SUPPORTABLE (YET)            
RCVC01   MVC   RCVRSYS,PARAM3      OFFLINE SYS MAY BE IN PARAM3                 
         CLI   RCVRSYS,0                                                        
         BNE   RCVC06                                                           
         L     RF,VUTL             OFFLINE SYS FROM UTL                         
         MVC   RCVRSYS,4(RF)                                                    
         B     RCVC06                                                           
*                                                                               
RCVC02   L     R5,SSBTKADR         R5=A(TCB)                                    
         USING TCBD,R5                                                          
         LLC   R0,TCBSWNUM         R0=NUMBER OF ENTRIES+1                       
         LA    R1,TCBSWTAB         R1=A(ENTRIES)                                
         USING TCBSWTAB,R1                                                      
*                                                                               
RCVC04   STM   R0,R1,DUB1          SAVE TABLE POS'N IN R0,R1                    
         CLC   TCBSEN,0(R1)        IS THIS CONNECTED SYSTEM                     
         BNE   *+14                                                             
         OC    TCBRCVF(3),TCBRCVF  TEST CONNECTED SYSTEM RECOVERY               
         B     *+10                                                             
         OC    TCBSWRVF,TCBSWRVF   TEST SWITCHED SYSTEM RECOVERY                
         BZ    RCVC14                                                           
         XC    TCBSWRVF(8),TCBSWRVF                                             
*                                                                               
         MVC   RCVRSYS,0(R1)       SET RCVRSYS                                  
*                                                                               
RCVC06   BRAS  RE,GETDTF           GET DTF FOR RECOVERY                         
         LR    R5,R1               TEST SHARED MEMORY RECOVERY BUFFER           
         TM    DTFFLAG-DTF(R5),DTFSHMEM                                         
         BZ    RCVC06A                                                          
         MVI   BUFFM,BSHRMEMQ                                                   
         SAM31                     SET 31-BIT MODE IF SHARED MEMORY             
         B     RCVC08                                                           
RCVC06A  TM    DTFFLAG-DTF(R5),DTFGLOB                                          
         BZ    RCVC06B                                                          
         MVI   BUFFM,BGLOBALQ                                                   
         B     RCVC08                                                           
RCVC06B  MVI   BUFFM,BLOCALQ                                                    
         TM    DIND-DTF(R5),DINDVRB TEST USING VERMONT BUFFERS                  
         BZ    RCVC12              OBSOLETE?? NO OTHER REFERENCE                
         TM    DIND-DTF(R5),DINDVRX TEST USING EXTENDED VERMONTS                
         BZ    RCVC10                                                           
         OI    BUFFM,BEXTVMTQ                                                   
         B     RCVC10                                                           
*                                                                               
RCVC08   TM    DIND-DTF(R5),DINDVRX TEST USING EXTENDED VERMONTS                
         BZ    *+8                                                              
         OI    BUFFM,BEXTVMTQ                                                   
         BRAS  RE,RCVGETD          GET DSPACE                                   
         BRAS  RE,RCVGETA                                                       
         BZ    RCVC12              EXIT IF NO ENTRY                             
*                                                                               
         BRAS  RE,RCVUSSCM         WRITE USS COMMIT IF REQUIRED                 
         TM    BUFFM,BGLOBALQ      TEST GLOBAL DSPACE                           
         BZ    *+8                                                              
         SAC   512                 GLOBAL RESET TO AR MODE                      
*                                                                               
         L     R2,ACTVAD                                                        
         XC    0(VRCVLNQ,R2),0(R2) REMOVE ACTIVE ENTRY                          
         TM    BUFFM,BEXTVMTQ      TEST EXTENDED VERMONTS                       
         BZ    *+10                NO, SKIP                                     
         XC    0(VRCXLNQ,R2),0(R2) REMOVE WHOLE ACTIVE ENTRY                    
         L     R2,ARECBUF                                                       
         USING VRCVHDR,R2                                                       
         OI    VRCVECB,X'40'       POST WE HAVE A FREE ENTRY                    
         DROP  R2                                                               
*NOP*    CLI   PARAM3+1,X'FE'      COMMITS LEAVE BUFFER WRT TILL LATER          
*NOP*    BE    RCVC12                                                           
         BRAS  RE,RCVWRITE         WRITE THE BUFFER                             
*                                                                               
         CLI   RCVRSYS,X'0A'       CONTROL                                      
         BNE   *+8                                                              
         BRAS  RE,RCVCTDEQ         DEQ CTRL VERMONTS                            
*&&UK                                                                           
         CLI   RCVRSYS,X'14'       MEDZ                                         
         BNE   *+8                                                              
         BRAS  RE,RCVMZDEQ         DEQ MEDZ VERMONTS                            
*&&                                                                             
         B     RCVC12                                                           
*                                                                               
RCVC10   CLI   RCVRSYS,X'0A'       OFFLINE/VERMONT                              
         BE    RCVC11              SLIGHTLY DIFFERENT FOR CONTROL SYS           
         BRAS  RE,RCVGETL          GET LOCAL BUFFER                             
         BRAS  RE,RCVGETA                                                       
         BZ    RCVC12              EXIT IF NO ENTRY                             
         L     R2,ACTVAD                                                        
         XC    0(VRCVLNQ,R2),0(R2) REMOVE ACTIVE ENTRY                          
         TM    BUFFM,BEXTVMTQ      TEST EXTENDED VERMONTS                       
         BZ    *+10                NO, SKIP                                     
         XC    0(VRCXLNQ,R2),0(R2) REMOVE WHOLE ACTIVE ENTRY                    
         L     R2,ARECBUF                                                       
         USING VRCVHDR,R2                                                       
         OI    VRCVECB,X'40'       POST WE HAVE A FREE ENTRY                    
         DROP  R2                                                               
         BRAS  RE,RCVWRITE         WRITE THE BUFFER                             
         B     RCVC12                                                           
*                                                                               
RCVC11   BRAS  RE,RCVGETL          GET LOCAL BUFFER                             
         BRAS  RE,RCVGETA                                                       
         BZ    RCVC12              EXIT IF NO ENTRY                             
         GOTO1 =V(DMENQCTL),Q1,(C'E',=C'CTRL')                                  
         BRAS  RE,RCVGETA          NOW HAVE LATEST RECOVERY BUFFER              
         BZ    RCVC11D             EXIT IF NO ENTRY                             
         L     R2,ACTVAD                                                        
         XC    0(VRCVLNQ,R2),0(R2) REMOVE ACTIVE ENTRY                          
         TM    BUFFM,BEXTVMTQ      TEST EXTENDED VERMONTS                       
         BZ    *+10                NO, SKIP                                     
         XC    0(VRCXLNQ,R2),0(R2) REMOVE WHOLE ACTIVE ENTRY                    
         L     R2,ARECBUF                                                       
         USING VRCVHDR,R2                                                       
         OI    VRCVECB,X'40'       POST WE HAVE A FREE ENTRY                    
         DROP  R2                                                               
         BRAS  RE,RCVWRITE         WRITE THE BUFFER                             
         BRAS  RE,RCVCTDEQ         DEQ CTRL VERMONTS                            
RCVC11D  GOTO1 =V(DMENQCTL),Q1,(C'D',=C'CTRL')                                  
*                                                                               
RCVC12   CLC   SSBCNTL,NULLS       TEST OFFLINE                                 
         BNE   *+8                                                              
         NI    DIND-DTF(R5),255-DINDVRA YES-CLEAR VERMONT ACTIVE FLAG           
*                                                                               
RCVC14   SAC   0                                                                
         TM    BUFFM,BLOCALQ       TEST FOR LOCAL                               
         BO    RCVC15                                                           
         CLI   RCVRSYS,0                                                        
         BE    RCVC15                                                           
         XC    DUB,DUB             GLOBAL RESOURCE                              
         MVC   DUB+3(1),RCVRSYS                                                 
         OI    DUB,X'10'                                                        
         GOTO1 VLOCKSPC,DUB        UNLOCK RESOURCE                              
         B     RCVC16                                                           
*                                                                               
RCVC15   LT    R5,ADTF             TEST ANY DTF TO FREE                         
         BZ    *+8                                                              
         NI    DTFDEVF-DTFPHD(R5),X'DF' UNLOCK LOCAL FILE                       
*                                                                               
RCVC16   CLC   SSBCNTL,NULLS       EXIT IF OFFLINE                              
         BE    XMOD                DMDMGR WILL CLEAR GIN IN SSB                 
         L     R5,SSBTKADR                                                      
         LM    R0,R1,DUB1                                                       
         LTR   R0,R0               TEST FOR NO ENTRIES                          
         BZ    RCVC18                                                           
         LA    R1,L'TCBSWTAB(R1)   NEXT SWITCH SYSTEM                           
         BCT   R0,RCVC04                                                        
*                                                                               
RCVC18   XC    TCBRCV,TCBRCV       CLEAR RECOVERY INFO IN TCB                   
         L     RE,SSBTKADR         CLEAR GIN IN UTL                             
         L     RE,TCBUTL-TCBD(,RE)                                              
         XC    TGIN-UTLD(L'TGIN,RE),TGIN-UTLD(RE)                               
         B     XMOD                                                             
         DROP  R5,R1                                                            
         EJECT                                                                  
***********************************************************************         
* SUBROUTINE TO WRITE COMMIT FOR SYSTEM                               *         
* CANT BE IN AR MODE. CALLER MUST RESET AR MODE IF REQUIRED.          *         
***********************************************************************         
RCVUSSCM LT    RF,=V(DMRCVUSS)                                                  
         BZR   RE                                                               
         CLI   RCVRSYS,1           DONT DO USS COMMIT IF SERVICE SYSTEM         
         BNHR  RE                                                               
         SAC   0                                                                
         XC    DMCBUSSQ,DMCBUSSQ   UNIX QUEUE PARAMETERS                        
         LA    R1,1                COMMAND TO SEND COMMIT                       
         ST    R1,P1USS            P1USS=COMMIT COMMAND                         
         MVC   P2USS+3(1),RCVRSYS  P2USS=SE NUMBER                              
         LA    R1,DMCBUSSQ                                                      
         BR    RF                  GO TO DMRCVUSS AND RETURN VIA RE             
         EJECT                                                                  
***********************************************************************         
* SUBROUTINE TO WRITE WARNINGS TO CONSOLE BEFORE EXITING              *         
***********************************************************************         
*&&UK                                                                           
RCVWARN1 XR    RE,RE               GET A(DTF) FROM P4                           
         ICM   RE,7,P4+1                                                        
         TM    DTFOPEN-DTFPHD(RE),X'80'                                         
         BO    RCVWARN2            IF BOTH CONDITIONS, REPORT 2ND               
         LA    RE,WARNTXT1         NO RECOVERY BECAUSE SSOSNRCV SET             
         MVC   WARNMSG(2),=AL2(L'WARNTXT+L'WARNTXT1)                            
         LAY   RF,C'01'                                                         
         B     RCVWARN                                                          
RCVWARN2 LA    RE,WARNTXT2         NO RECOVERY BECAUSE RCV FILE R/O             
         MVC   WARNMSG(2),=AL2(L'WARNTXT+L'WARNTXT2)                            
         LAY   RF,C'02'                                                         
RCVWARN  CLC   SSBCNTL,NULLS       WARNING IRRELEVANT IF ONLINE                 
         BNE   RCVX                                                             
         CLI   WARNFLAG,0          ISSUE WARNING ONLY ONCE                      
         BNE   RCVX                                                             
         CLI   PARAM3+1,1          AND DONT ISSUE WARNING ON A COPY             
         BE    RCVX                                                             
*XXUK                              FOLLOWING ALWAYS UK ONLY (*&&UK)             
         LT    R1,VUTL                                                          
         BZ    *+12                                                             
         CLI   TSYS-UTLD(R1),X'14' IGNORE ALL MEDZ FILES                        
         BE    RCVX                                                             
         CLI   PARAM3,X'96'        IGNORE MBU FILES                             
         BE    RCVX                                                             
         CLI   PARAM3,X'97'                                                     
         BE    RCVX                                                             
         CLI   PARAM3,X'58'        IGNORE MPL DATABASE FILES                    
         BL    *+12                                                             
         CLI   PARAM3,X'5F'                                                     
         BNH   RCVX                                                             
*XX                                PRECEDING ALWAYS UK ONLY (*&&UK)             
         MVI   WARNFLAG,1                                                       
         MVC   WARNMSG+2(L'WARNTXT),WARNTXT                                     
         STCM  RF,3,WARNMSG+2+7                                                 
         MVC   WARNMSG+2+18(1),SSODSPAC                                         
         MVC   WARNMSG+2+L'WARNTXT(L'WARNTXT1),0(RE)                            
         WTO   TEXT=WARNMSG                                                     
         B     RCVX                                                             
WARNTXT  DC    C'+DMRCVR00+ DSPACE=X MARKER WITH RECOVERY '                     
WARNTXT1 DC    C'DISABLED      '                                                
WARNTXT2 DC    C'FILE READ-ONLY'                                                
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* SUBROUTINE TO BUMP OFFLINE RECOVERY COUNTS                          *         
***********************************************************************         
RCVCNT   LA    RF,4                BUMP RECOVERY RECORD TYPE COUNTS             
         CLI   RRECTY,1                                                         
         BL    RCVCNT1                                                          
         CLI   RRECTY,3                                                         
         BH    RCVCNT1                                                          
         IC    RF,RRECTY           COPY/CHANGE/ADD                              
RCVCNT1  BCTR  RF,0                                                             
         SLL   RF,2                                                             
         A     RF,SSOSQCNT         POINT TO CALLER'S ACCUMULATORS               
         L     R1,0(RF)                                                         
         LA    R1,1(R1)                                                         
         ST    R1,0(RF)                                                         
RCVCNTX  BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* SUBROUTINE TO BUILD OFFLINE JOB HEADER RECORD                       *         
***********************************************************************         
BOHDREC  NTR1                                                                   
         LA    R4,OHDREC                                                        
         USING DMRCJBD,R4                                                       
         MVC   DMRCJBH,RECVHDR                                                  
JBH      USING RECVHDR,DMRCJBH                                                  
         MVI   JBH.RFILTY,X'FF'                                                 
         MVI   JBH.RRECTY,X'FF'                                                 
         MVI   JBH.RTASKID,X'FF'                                                
         XC    JBH.RVCHR,JBH.RVCHR                                              
         DROP  JBH                                                              
         LA    RF,DMRCJBLQ                                                      
         STH   RF,OHDRECL                                                       
*                                                                               
         LA    R5,EXPARMS                                                       
         EXTRACT (5),'S',FIELDS=TIOT                                            
         L     R5,EXPARMS                                                       
         MVC   JOBNAME,0(R5)                                                    
*                                                                               
         SAM31                                                                  
         LA    R7,JOBNUM                                                        
         IAZXJSAB READ,WORKID=(R7) GET JES OR ASCH JOB NUMBER                   
         SAM24                                                                  
         PACK  DUB,JOBNUM+3(5)                                                  
         CVB   R0,DUB                                                           
         STH   R0,JOBBIN                                                        
*                                                                               
         MVC   DMRCJBID,=CL8'**SJOB**'                                          
         MVC   DMRCJBNA,JOBNAME                                                 
         MVC   DMRCJBNO,JOBNUM                                                  
         B     EXIT                                                             
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* SUBROUTINE TO BUILD OFFLINE JOB TRAILER RECORD                      *         
***********************************************************************         
BOTRREC  NTR1                                                                   
         LA    R4,OTRREC                                                        
         USING DMRCJBD,R4                                                       
         MVC   DMRCJBH,RECVHDR                                                  
JBH      USING RECVHDR,DMRCJBH                                                  
         MVI   JBH.RFILTY,X'FF'                                                 
         MVI   JBH.RRECTY,X'FF'                                                 
         MVI   JBH.RTASKID,X'FF'                                                
         XC    JBH.RVCHR,JBH.RVCHR                                              
         DROP  JBH                                                              
         LA    RF,DMRCJBLQ                                                      
         STH   RF,OTRRECL                                                       
*                                                                               
         MVC   DMRCJBID,=CL8'**EJOB**'                                          
         MVC   DMRCJBNA,JOBNAME                                                 
         MVC   DMRCJBNO,JOBNUM                                                  
BOTRX    B     EXIT                                                             
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* SUBROUTINE TO OPEN OFFLINE MVS DATA SET FOR JOB RECOVERY            *         
***********************************************************************         
MVSOPEN  NTR1                                                                   
         TM    MVSFLAG,MVSFOPNQ                                                 
         BZ    *+8                                                              
         BRAS  RE,MVSCLOSE                                                      
*                                                                               
* BUILD RECOVERY TAPE NAME IN OMVSDSN                                           
* TAPE NAME FOLLOWS THE FOLLOWING CONVENTION:                                   
* 1. TSTTAPE (FQATAPE, SPTTAPE, ETC)                                            
* 2. C'RCV'                                                                     
* 3. SPOT1 (SYSTEM, 3-5 CHARACTERS - SPTTU, CON, ETC)                           
* 4. DYYMMDD - DATE STAMP                                                       
* 5. JOB NAME                                                                   
* 6. JNNNNNSS - JOB NUMBER + STEMP NUMBER                                       
* EXAMPLE:                                                                      
* CSCTAPE.RCV.SPTTU.D200129.TZIHSCO.J2419102                                    
*                                                                               
         PUSH USING                                                             
*                                                                               
         MVI   OMVSDSN,C' '        INIT DSN TO SPACES                           
         MVC   OMVSDSN+1(L'OMVSDSN-1),OMVSDSN                                   
         LA    R5,OMVSDSN                                                       
*                                                                               
         LT    R3,VSSB             SSB GIVES BASIC RECOVERY INFO                
         BNZ   *+8                                                              
         LA    R3,DFLTSSB          POINT TO DEFAULT IF NO SSB                   
         USING SSBD,R3                                                          
*                                                                               
         LLC   RF,SENUM                                                         
         SLL   RF,4                INDEX INTO SYSTABD(DMSYSLEN=16)              
         L     R4,=V(SYSTABL)                                                   
         AR    R4,RF                                                            
         USING SYSTABD,R4                                                       
*                                                                               
         MVC   0(3,R5),DMSYNAM    FIRST 3 CHARS OF SYSTEM FROM SYSTAB           
*&&US                                                                           
         CLI   DMSYOVNO,SYSNETQ                                                 
         BNE   *+14                                                             
         MVC   0(3,R5),=C'NET'                                                  
         B     MVSO002                                                          
*&&                                                                             
         CLI   SSODSPAC,C'T'                                                    
         BNE   *+14                                                             
         MVC   0(3,R5),=C'TST'                                                  
         B     MVSO002                                                          
*                                                                               
         CLI   SSODSPAC,C'C'                                                    
         BNE   *+14                                                             
         MVC   0(3,R5),=C'CSC'                                                  
         B     MVSO002                                                          
*                                                                               
         CLI   SSODSPAC,C'Q'                                                    
         BNE   *+14                                                             
         MVC   0(3,R5),=C'FQA'                                                  
         B     MVSO002                                                          
*&&US                                                                           
         CLI   SSODSPAC,C'R'                                                    
         BNE   *+10                                                             
         MVC   0(3,R5),=C'REP'                                                  
*&&                                                                             
MVSO002  LA    R5,3(R5)            MOVE PAST 3-CHAR SYSTEM NAME                 
         MVC   0(9,R5),=C'TAPE.RCV.'                                            
         TM    SSOFLAG3,SSO3RDSK   RECOVER TO MVS DISK                          
         JNO   *+10                                                             
         MVC   0(9,R5),=C'DISK.RCV.'                                            
         LA    R5,9(R5)            MOVE PAST '???TAPE.RCV.'                     
*                                                                               
MVSO003  MVC   0(L'DMSYNAM,R5),DMSYNAM COPY FULL SYSTEM NAME                    
         LA    R5,L'DMSYNAM(R5)    ADVANCE PAST FULL SYSTEM NAME                
*                                                                               
* SYSTEM CAN BE 3-5 CHARACTERS.  FIND WHERE IT ENDS, AND PUT IN THE '.'         
         CLI   0(R5),C' '                                                       
         BH    *+8                                                              
         BCT   R5,*-8                                                           
         MVI   1(R5),C'.'                                                       
         LA    R5,2(R5)            MOVE PAST THE DOT                            
*                                                                               
* GET DATE STAMP                                                                
*                                                                               
         MVI   0(R5),C'D'                                                       
         LA    R5,1(R5)            MOVE PAST THE DOT                            
*                                                                               
         XC    CLOCKWRK(16),CLOCKWRK                                            
         TIME  DEC,CLOCKWRK,LINKAGE=SYSTEM,DATETYPE=YYYYMMDD                    
         XR    RF,RF                                                            
         ICM   RF,7,CLOCKWRK+9     GET X'YYMMDD'                                
         SLL   RF,4                RF=X'0YYMMMDD0                               
         OILL  GRF,X'000C'         SET SIGN                                     
         STCM  RF,15,CLOCKWRK+8                                                 
         UNPK  DUB,CLOCKWRK+8(4)   DUB=C'..YYMMDD'                              
         NI    DUB+7,X'0F'         TURN OFF SIGN NIBBLE                         
         OI    DUB+7,X'F0'         REPLACE IT WITH F                            
         MVC   0(6,R5),DUB+2       GET C'YYMMDD'                                
         LA    R5,6(R5)            MOVE PAST THE DYYMMDD TIMESTAMP              
*                                                                               
         MVI   0(R5),C'.'                                                       
         LA    R5,1(R5)            MOVE PAST THE DOT                            
*                                                                               
         MVC   0(8,R5),OHDREC+32   COPY JOB NAME                                
         LA    R5,8(R5)            ADVANCE PAST JOB NAME                        
*                                                                               
* FIND WHERE JOBNAME ENDS, AND PUT IN THE '.'                                   
         CLI   0(R5),C' '                                                       
         BH    *+8                                                              
         BCT   R5,*-8                                                           
         MVI   1(R5),C'.'                                                       
         LA    R5,2(R5)            MOVE PAST THE DOT                            
*                                                                               
         MVI   0(R5),C'J'          JOB/STEP NUMBER 'JNNNNNSS'                   
         LA    R5,1(R5)            MOVE PAST THE 'J'                            
*                                                                               
         MVC   0(5,R5),OHDREC+43   COPY JOB NUMBER                              
         LA    R5,5(R5)            MOVE PAST THE JOB NUMBER                     
*                                                                               
* OBTAIN STEP NUMBER                                                            
*                                                                               
         XR    RF,RF               SET ADDRESS TO 0 (PSA)                       
         L     RF,PSATOLD-PSA(RF)  A(CURRENT TCB)                               
         L     RF,TCBJSCB-TCB(RF)  A(STEP CONTROL BLOCK)                        
         LLC   RF,JSCBSTEP-IEZJSCB(RF) CURRENT STEP NUMBER                      
         EDIT  (RF),(2,0(R5)),FILL=0 EDIT THE STEP NUMBER                       
*                                                                               
         POP   USING                                                            
*                                                                               
         LA    R1,L'OMVSDSN        ADD LENGTH OF MVS DATA SET NAME              
         AH    R1,OHDRECL          TO RECOVERY HEADER RECORD                    
         STH   R1,OHDRECL                                                       
*                                  DYNAMICALLY ALLOCATE MVS FILE                
         MVC   TXTDSN+6(44),OMVSDSN                                             
         MVC   TXTDD+6(L'MVSFILEC),MVSFILEC                                     
         MVI   TXTDD+5,L'MVSFILEC                                               
         LA    R1,ARBLKCTT         DYNALLOC BLOCK FOR TAPE                      
         TM    SSOFLAG3,SSO3RDSK   RECOVER TO MVS DISK                          
         JNO   *+8                                                              
         LA    R1,ARBLKCTA         DYNALLOC BLOCK FOR DISK                      
         DYNALLOC                                                               
         LTR   RF,RF                                                            
         BZ    MVSO050             DATASET ALLOCATED OK                         
         CLC   RBLKCATA+4(2),=X'0410'                                           
         JNE   *+2                 UNACCEPTABLE ERROR                           
         TM    SSOFLAG3,SSO3RDSK   RECOVER TO MVS DISK                          
         JNO   MVSO004                                                          
         RDJFCB (MVSFILED)         DD CARD ALREADY EXISTS                       
         J     MVSO005                                                          
MVSO004  RDJFCB (MVSFILET)         DD CARD ALREADY EXISTS                       
MVSO005  LTR   RF,RF                                                            
         JNZ   *+2                 MUST FIND DSN                                
         MVC   OMVSDSN,MVSJFCB+JFCBDSNM-INFMJFCB                                
*                                                                               
MVSO050  LA    RF,MVSFILET                                                      
         TM    SSOFLAG3,SSO3RDSK   RECOVER TO MVS DISK                          
         JNO   *+8                                                              
         LA    RF,MVSFILED                                                      
         OPEN  ((RF),OUTPUT)                                                    
         LTR   RF,RF                                                            
         JNZ   *+2                 DIE IF CANT OPEN DATASET                     
         OI    MVSFLAG,MVSFOPNQ                                                 
*                                                                               
         LA    R4,OHDREC                                                        
         USING DMRCJBD,R4                                                       
         MVC   DMRCJBDS,OMVSDSN     COPY DSN HLQS (INC SPACE FILL)              
         DROP  R4                                                               
*                                                                               
MVSOX    B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* SUBROUTINE TO CLOSE OFFLINE MVS DATA SET FOR JOB RECOVERY           *         
***********************************************************************         
MVSCLOSE NTR1                                                                   
         LA    RF,MVSFILET                                                      
         TM    SSOFLAG3,SSO3RDSK   RECOVER TO MVS DISK                          
         JNO   *+8                                                              
         LA    RF,MVSFILED                                                      
         CLOSE ((RF))                                                           
         LTR   RF,RF                                                            
         JNZ   *+2                 DIE IF CANT CLOSE DATASET                    
         MVC   TXTUNDD+6(L'MVSFILEC),MVSFILEC                                   
         MVI   TXTUNDD+5,L'MVSFILEC                                             
JBD      USING DMRCJBD,OHDREC                                                   
         MVC   TXTDSN+6(44),JBD.DMRCJBDS                                        
         DROP  JBD                                                              
         LA    R1,ARBLKUNA         DYNAMIC UNALLOCATION PARAMETER BLOCK         
         DYNALLOC                                                               
         JNZ   *+2                                                              
         NI    MVSFLAG,X'FF'-MVSFOPNQ                                           
MVSCX    B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* SUBROUTINE TO WRITE OFFLINE RECOVERY DATA TO MVS DATA SET           *         
***********************************************************************         
MVSWRITE NTR1                                                                   
MVSW00   BRAS  RE,RCVCLOCK         SET DATE AND TIME IN RECOVERY RECS           
X        USING RECVHDR,RE                                                       
         LT    RE,CPCHACOP         COPY RECORD                                  
         BZ    MVSW02                                                           
         MVC   X.RDATE,RDATE                                                    
         MVC   X.RTIME,RTIME                                                    
MVSW02   LT    RE,ASAVE                                                         
         LA    RE,4(RE)            ADD/CHANGE RECORD                            
         MVC   X.RDATE,RDATE                                                    
         MVC   X.RTIME,RTIME                                                    
         DROP  X                                                                
*                                                                               
MVSW10   LT    RF,=V(DMRCVUSS)     COPY TO USS QUEUE                            
         BZ    MVSW20              NOT LINKED IN                                
         TM    BUFFM,BLOCALQ                                                    
         BO    MVSW20              DONT SEND LOCAL TO USS                       
         TM    TRAILERS,X'01'                                                   
         BZ    MVSW20              NEED TRAILERS IF GOING TO USS                
         MVC   P1USS(16),CPCHDETS  ADDR+LEN COPY, ADDR+LEN CHANGE/ADD           
         LA    R1,RECVEXT          A(TRAILER)                                   
         ST    R1,P5USS                                                         
         LA    R1,EXTQ             L'TRAILER                                    
         ST    R1,P6USS                                                         
         LA    R1,DMCBUSSQ                                                      
         BASR  RE,RF                                                            
*                                                                               
MVSW20   LT    RE,CPCHACOP         CHECK FOR CHANGE WITH COPY                   
         BZ    MVSW30                                                           
         L     R1,CPCHLCOP         GET SAVED LENGTH                             
         TM    TRAILERS,X'01'      SKIP OF NO TRAILER                           
         BZ    MVSW22                                                           
         LA    RF,0(RE,R1)         RF=A(END OF RECORD)                          
         ST    RF,ASAVEEXT         SAVE EOR ADDRESS                             
         MVC   SAVEEXT,0(RF)                                                    
         MVC   0(EXTQ,RF),RECVEXT                                               
         AHI   R1,EXTQ                                                          
MVSW22   AHI   RE,-4                                                            
         MVC   SAVECPY,0(RE)       SAVE PRECEDING 4 BYTES OF CALLER             
         LA    R1,4(R1)                                                         
         SLL   R1,16                                                            
         STCM  R1,15,0(RE)         STORE FULL RECORD LENGTH HERE                
         LR    R4,RE               WRITE COPY RECORD TO MVS DATA SET            
         LA    RF,MVSFILET                                                      
         TM    SSOFLAG3,SSO3RDSK   RECOVER TO MVS DISK                          
         JNO   *+8                                                              
         LA    RF,MVSFILED                                                      
         PUT   (RF),(R4)                                                        
         MVC   0(4,R4),SAVECPY     RESTORE CALLERS WORKING STORE                
         TM    TRAILERS,X'01'      TEST TRAILER ADDED                           
         BZ    MVSW30              NO-SKIP                                      
         L     RF,ASAVEEXT         RESTORE EOR ADDRESS                          
         MVC   0(EXTQ,RF),SAVEEXT  RESTORE CALLERS STORE                        
*                                                                               
MVSW30   LT    R4,ASAVE            WRITE RECORD TO MVS DATA SET                 
         TM    TRAILERS,X'01'      TEST TRAILER NEEDED                          
         BZ    MVSW32              NO-SKIP                                      
         LH    RE,0(R4)            LENGTH                                       
         LA    RF,0(RE,R4)         END OF RECORD                                
         ST    RF,ASAVEEXT         SAVE EOR ADDRESS                             
         MVC   SAVEEXT,0(RF)                                                    
         MVC   0(EXTQ,RF),RECVEXT                                               
         AHI   RE,EXTQ                                                          
         STH   RE,0(R4)                                                         
MVSW32   LA    RF,MVSFILET                                                      
         TM    SSOFLAG3,SSO3RDSK   RECOVER TO MVS DISK                          
         JNO   *+8                                                              
         LA    RF,MVSFILED                                                      
         PUT   (RF),(R4)                                                        
         TM    TRAILERS,X'01'      TEST TRAILER ADDED                           
         BZ    MVSW90              NO-SKIP                                      
         L     RF,ASAVEEXT         RESTORE EOR ADDRESS                          
         MVC   0(EXTQ,RF),SAVEEXT  RESTORE CALLERS STORE                        
*                                                                               
MVSW90   OC    SSOSQCNT,SSOSQCNT   BUMP RECOVERY RECORD TYPE COUNTS             
         BZ    *+8                                                              
         BRAS  RE,RCVCNT                                                        
         L     RE,ASAVE            RESTORE 28 BYTES CALLERS STORAGE             
         MVC   0(HP4Q,RE),SAVEHDR                                               
         B     EXIT                                                             
         EJECT                                                                  
*&&DO                                                                           
         EJECT                                                                  
***********************************************************************         
* SUBMIT TRANSACTION TO FIRE OFF REP - MO FALINK FOR CONTRACT RECS    *         
***********************************************************************         
FORMOSA  NTR1  BASE=*,LABEL=*                                                   
         SAC   0                                                                
*                                                                               
         L     RE,SSBTKADR         SET RECOVERY INFO IN TCB ENTRY               
         L     RF,TCBUTL-TCBD(RE)                                               
         OI    TSTATC-UTLD(RF),TSTCREPU                                         
         XR    R0,R0                                                            
         ICM   R0,3,TMOCNTS-UTLD(RF)                                            
         AHI   R0,1                                                             
         STCM  R0,3,TMOCNTS-UTLD(RF)                                            
         STH   R0,HALF                                                          
*                                                                               
         TM    SSBSTAT4,SSBSAOR    AM I IN AN AOR?                              
         BZ    FOMO01                                                           
         B     FORMOSAA            YES-USE A NEW GETUTL CALL                    
                                                                                
**********************************************************************          
*FOR TOR ONLY ADD TO SE MQ1 LIST.                                    *          
*ALSO, IF IT SPINS TOO LONG, IT WILL TAKE OVER, INSTEAD OF DIEING    *          
**********************************************************************          
FOMO01   GOTO1 =V(LCM),MYPRM,VTGETUTL                                           
         LTR   R5,R1                                                            
         JZ    *+2                                                              
*                                                                               
F        USING UTLD,R5                                                          
         SAM31                                                                  
         MVC   F.TLUID,=CL8'NONENONE'                                           
         MVC   F.TSVCREQ,=XL2'0173' SET SPECIAL S/R                             
         MVI   F.TSTATC,TSTCMQ                                                  
         MVC   F.TMOCNTS,HALF                                                   
*                                                                               
         TIME  TU                                                               
         ST    R0,F.TTIMETU        SET ARRIVAL TIME TO NOW                      
*                                                                               
         L     RF,F.TUTLXADR                                                    
         L     RE,XAAPASSR-XAUTLD(RF)                                           
         ST    RE,F.TBUFF          SET XAPASSR AREA AS DUMMY TBUFF              
*                                                                               
         LH    RF,RECLEN           COPY IN RECORD TO XAPASSR AREA               
         L     R0,RECADD                                                        
         LH    R1,RECLEN                                                        
         MVCL  RE,R0                                                            
         DROP  F                                                                
*                                                                               
         L     R4,=V(SELTMQ1)      R4=A(SELIST-MQ1 IN TOR)                      
FOM0100  LR    R6,R4                                                            
         AHI   R6,-4               R1=A(VSELMQ1L, QUEUE LOCK)                   
*                                                                               
         LA    RF,100                                                           
FOM0110  TS    0(R6)               TRY TO LOCK IT FOR 100X                      
         BZ    FOM0120             YES-I GOT IT                                 
         BCT   RF,FOM0110                                                       
         WTO   'AUTONOTE*US-MF_FAC_NOTIFY:DMRCVR,BAD SELOCK FOR MQ1'            
*                                                                               
FOM0120  DS    0H                  I GOT THE LOCK                               
         USING SELISTD,R4                                                       
         CLC   SEFIRST,NULLS       QUEUE HAS MEMEBERS ALREADY?                  
         BNZ   FOM0130             YES                                          
         ST    R5,SEFIRST          NO-SET FIRST AND LAST                        
         ST    R5,SELAST                                                        
         LHI   RF,1                                                             
         STH   RF,SEQLEN                                                        
         MVI   0(R6),0             UNLOCK THIS MQ SELIST                        
         B     FOM0400                                                          
*                                                                               
FOM0130  L     R7,SELAST           ADD IT TO END OF THE QUEUE                   
         ST    R5,TXNEXTIN-UTLD(R7)                                             
         ST    R5,SELAST                                                        
         LH    RF,SEQLEN                                                        
         AHI   RF,1                                                             
         STH   RF,SEQLEN                                                        
         DROP  R4                                                               
         MVI   0(R6),0             UNLOCK THIS MQ SELIST                        
*                                                                               
FOM0400  J     FOMOEXIT                                                         
                                                                                
***********************************************************************         
*FOR AOR, ADD TO SE MQ1 FIRST, WAIT IF IT IS LOCKED BY ANOTHER AOR.   *         
*JUMP TO SE MQ2 IF SE MQ1 IS LOCKED BY TOR.                           *         
*ALSO, IF IT SPINS TOO LONG, IT WILL DIE                              *         
***********************************************************************         
FORMOSAA LAM   ARE,AR1,ARZERO      CLEAR ALL ARE/F/0/1                          
         GOTO1 =V(LCM),MYPRM,VTGTUTLA                                           
         LTR   R5,R1                                                            
         BZ    FOMA0900                                                         
*                                                                               
         L     R1,VSSB             R1=A(SSB) IN CURRENT AOR                     
A        USING SSBD,R1                                                          
         LAM   AR5,AR5,A.SSBTLET   SWITCH ALET TO TOR                           
         DROP  A                                                                
         TIME  TU                  R0=TIME NOW-STORE IN TTIMETU LATER           
         SAC   512                 IF GLOBAL SET TO AR MODE                     
*                                                                               
TOR      USING UTLD,R5                                                          
         MVC   TOR.TLUID,=CL8'NONENONE'                                         
         MVC   TOR.TSVCREQ,=XL2'0173'  SET SPECIAL S/R                          
         MVI   TOR.TSTATC,TSTCMQ                                                
         ST    R0,TOR.TTIMETU      SET ARRIVAL TIME TO NOW                      
         MVC   TOR.TMOCNTS,HALF                                                 
*                                                                               
         L     RF,TOR.TUTLXADR                                                  
         CPYA  ARF,AR5             COPY TOR'S ALET TO ARF                       
         L     RE,XAAPASSR-XAUTLD(RF)                                           
         ST    RE,TOR.TBUFF        SET XAPASSR AREA AS DUMMY TBUFF              
         CPYA  ARE,AR5             COPY TOR'S ALET TO ARE                       
         CPYA  AR2,AR5             COPY TOR'S ALET TO ARE                       
         LH    RF,RECLEN           COPY IN RECORD TO XAPASSR AREA               
         L     R2,RECADD                                                        
         LH    R3,RECLEN                                                        
         MVCL  RE,R2                                                            
         DROP  TOR                                                              
         LAM   ARE,AR2,ARZERO      CLEAR ALL ARE/F/0/1/2                        
                                                                                
***********************************************************************         
* R5=A(UTL FROM TOR) AND AR5=ALET OF TOR                              *         
***********************************************************************         
         L     R1,VSSB             R1=A(SSB) IN CURRENT AOR                     
         LAM   ARE,ARE,A.SSBALET   ARE=ALET FOR DMGR DATASPACE                  
         L     RE,SSBATOR-SSBD(R1) RE=A(TOR INFO BLOCK IN DMGR DSPACE)          
*                                                                               
         L     R4,TORSEMQ1-TORFACD(RE)        R4=A(SELIST-MQ1 IN TOR)           
         MVC   DUB(4),TORSEMQ2-TORFACD(RE)    DUB=A(SELIST-MQ2 IN TOR)          
         CPYA  AR6,AR5             COPY TOR'S ALET TO AR6                       
         CPYA  AR7,AR5             COPY TOR'S ALET TO AR7                       
*                                                                               
FOMA0100 LR    R6,R4                                                            
         AHI   R6,-4               R1=A(VSELMQ1L/MQ2L, QUEUE LOCK)              
*                                                                               
         LA    RF,100                                                           
FOMA0110 TS    0(R6)               TRY TO LOCK IT FOR 100X                      
         BZ    FOMA0120            YES-I GOT IT                                 
         CLC   1(3,R6),=CL3'TOR'                                                
         BE    FOMA0200            TOR HAS IT, GO TO NEXT MQ2 SELIST            
         BCT   RF,FOMA0110         TRY AGAIN                                    
         DC    H'0'                                                             
*                                                                               
FOMA0120 DS    0H                  I GOT THE LOCK                               
         CPYA  AR4,AR5             COPY TOR'S ALET TO AR4                       
         USING SELISTD,R4                                                       
         CLC   SEFIRST,NULLS       QUEUE HAS MEMEBERS ALREADY?                  
         BNZ   FOMA0123            YES                                          
         ST    R5,SEFIRST          NO-SET FIRST AND LAST ENTRIES                
         ST    R5,SELAST                                                        
         LHI   RF,1                                                             
         STH   RF,SEQLEN                                                        
         MVI   0(R6),0             UNLOCK THIS MQ SELIST                        
         B     FOMA0400                                                         
*                                                                               
FOMA0123 L     R7,SELAST           ADD IT TO END OF THE QUEUE                   
         ST    R5,TXNEXTIN-UTLD(R7)                                             
         ST    R5,SELAST                                                        
         LH    RF,SEQLEN                                                        
         AHI   RF,1                                                             
         STH   RF,SEQLEN                                                        
         DROP  R4                                                               
         MVI   0(R6),0             UNLOCK THIS MQ SELIST                        
         B     FOMA0400                                                         
*                                                                               
FOMA0200 DS    0H                                                               
         C     R4,DUB              AM I ALREADY AT MQ2 SELIST?                  
         JE    *+2                 DIE IF MQ1/2 BOTH TOO BUSY                   
         L     R4,DUB              LOAD A(SELIST-MQ2 IN TOR)                    
         B     FOMA0100                                                         
*                                                                               
FOMA0400 SAC   0                                                                
         LAM   AR4,AR7,ARZERO      CLEAR AR4/5/6/7                              
         J     FOMOEXIT                                                         
*                                                                               
FOMA0900 DC    H'0'                PROBABLY NEEDS SOME ERROR HANDLING           
*                                                                               
FOMOEXIT TM    BUFFM,BGLOBALQ      TEST GLOBAL DSPACE                           
         BZ    *+8                                                              
         SAC   512                 GLOBAL SET TO AR MODE                        
         SAM24                                                                  
         XIT1                                                                   
*&&                                                                             
*&&US                                                                           
***********************************************************************         
* SUBMIT TRANSACTION TO FIRE OFF REP - MO FALINK FOR CONTRACT RECS    *         
***********************************************************************         
FORMOSA  NTR1                                                                   
         SAC   0                                                                
         L     RE,SSBTKADR         SET RECOVERY INFO IN TCB ENTRY               
         L     RF,TCBUTL-TCBD(RE)                                               
         OI    TSTATC-UTLD(RF),TSTCREPU                                         
         XR    R0,R0                                                            
         ICM   R0,3,TMOCNTS-UTLD(RF)                                            
         AHI   R0,1                                                             
         STCM  R0,3,TMOCNTS-UTLD(RF)                                            
*                                                                               
         GOTO1 =V(LCM),MYPRM,VTGETUTL                                           
         LTR   R3,R1                                                            
         BZ    FOMO08                                                           
F        USING UTLD,R3                                                          
         SAM31                                                                  
         MVC   F.TLUID,=CL8'NONENONE'                                           
         MVC   F.TSVCREQ,=XL2'0173' SET SPECIAL S/R                             
         MVI   F.TSTATC,TSTCMQ                                                  
         STCM  R0,3,F.TMOCNTS                                                   
*                                                                               
         TIME  TU                                                               
         ST    R0,F.TTIMETU        SET ARRIVAL TIME TO NOW                      
*                                                                               
         L     RF,F.TUTLXADR                                                    
         L     RE,XAAPASSR-XAUTLD(RF)                                           
         ST    RE,F.TBUFF          SET XAPASSR AREA AS DUMMY TBUFF              
*                                                                               
         LH    RF,RECLEN           COPY IN RECORD TO XAPASSR AREA               
         L     R0,RECADD                                                        
         LH    R1,RECLEN                                                        
         MVCL  RE,R0                                                            
*                                                                               
         L     R4,=V(SELIST)       SE1                                          
         AHI   R4,6                                                             
         USING SELISTD,R4                                                       
         OC    SEFIRST,SEFIRST     QUEUE HAS MEMEBERS ALREADY?                  
         BNZ   FOMO02              YES                                          
         ST    R3,SEFIRST          NO-SET FIRST AND LAST ENTRIES                
         ST    R3,SELAST                                                        
         LHI   RF,1                                                             
         STH   RF,SEQLEN                                                        
         B     FOMO04                                                           
*                                                                               
FOMO02   L     RF,SELAST                                                        
         ST    R3,TXNEXTIN-UTLD(RF)                                             
         ST    R3,SELAST                                                        
         LH    RF,SEQLEN                                                        
         AHI   RF,1                                                             
         STH   RF,SEQLEN                                                        
*                                                                               
FOMO04   TM    BUFFM,BGLOBALQ      TEST GLOBAL DSPACE                           
         BZ    FOMO06                                                           
         SAC   512                 GLOBAL SET TO AR MODE                        
FOMO06   SAM24                                                                  
         XIT1                                                                   
         DROP  F,R4                                                             
*                                                                               
FOMO08   DC    H'0'                PROBABLY NEEDS SOME ERROR HANDLING           
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* EXIT POINTS                                                         *         
***********************************************************************         
EXIT     XIT1                                                                   
*                                                                               
XMOD     MVC   PARAM3+2(2),SVINLEN RESTORE PASSED RECORD LENGTH                 
         XMOD1 1                                                                
         EJECT                                                                  
***********************************************************************         
* CONSTANTS AND EQUATES                                               *         
***********************************************************************         
HDRQ     EQU   RHLENQ              HEADER LENGTH                                
HP4Q     EQU   RHLENQ+4            HEADER PLUS 4 FOR RECORD LENGTH              
EXTQ     EQU   RXLENQ              EXTENSION LENGTH                             
ON       EQU   1                                                                
OFF      EQU   0                                                                
VTGETUTL EQU   2                                                                
VTGTUTLA EQU   15                                                               
*                                                                               
FONE     DC    F'1'                                                             
EOFREC   DC    XL8'FFFFFFFFFFFFFFFF'                                            
*                                                                               
VDATAMGR DC    V(DATAMGR)                                                       
VDMOD000 DC    V(DMOD000)                                                       
VDADDS   DC    V(DADDS)                                                         
VLOCKSPC DC    V(LOCKSPC)                                                       
VADWAIT  DC    V(ADWAIT)                                                        
VSSB     DC    V(SSB)                                                           
VUTL     DC    V(UTL)                                                           
VUPSOON  DC    V(UPSOON)                                                        
VREAD    DC    V(READ)                                                          
*                                                                               
ADD      DC    CL3'ADD'                                                         
CLOSE    DC    CL5'CLOSE'                                                       
MVSFILEC DC    CL7'MVSFILE'                                                     
#ATTACH  DC    CL8'ATTACH'                                                      
#RCVB    DC    CL8'RCVB  '                                                      
         DC    C' '                                                             
*OMVSDSNC DC    CL44'DDS.RECOVERY.'                                             
*OMVSDSNQ EQU   13                  LENGTH OF HLQS IN OMVSDSNC                  
*                                                                               
JOBNAME  DC    CL8' '              MVS JOB NAME                                 
JOBNUM   DC    CL8'JOB00001'       JES/APPC JOB NUMBER JOB12345                 
JOBBIN   DC    XL2'00'             JES/APPC JOB NUMBER IN BINARY                
*                                                                               
MAJORNQ  DC    CL8'DDSENQ'         MAJOR ENQ FOR CTRCVR ENQS                    
ENQDSPAC DS    0CL1                                                             
MINORCT  DC    CL8'?.CTRCVR'       MINOR ENQ D(SPACE).CTRCVR                    
MINORCTL DC    AL1(L'MINORCT)                                                   
TOKENCT  DS    CL32                CTRL TOKEN ON ENQUEUE                        
*&&UK                                                                           
MINORMZ  DC    CL8'?.MEDRCVZ'      MINOR ENQ D(SPACE).MEDRCVZ                   
MINORMZL DC    AL1(L'MINORMZ)                                                   
TOKENMZ  DS    CL32                MEDZ TOKEN ON ENQUEUE                        
*&&                                                                             
ARZERO   DC    16F'0'                                                           
NULLS    DC    XL32'00'                                                         
FIRSTIME DC    AL1(ON)             FIRST TIME RECOVERY RECORD FLAG              
WARNFLAG DC    AL1(0)              NON-ZERO IF WARNING ISSUED BY JOB            
MVSFLAG  DC    AL1(0)              MVS DATA SET RECOVERY STATUS FLAG            
MVSFWRTQ EQU   X'01'               MVS DATA SET WRITING                         
MVSFOPNQ EQU   X'02'               MVS DATA SET OPEN                            
         EJECT                                                                  
         LTORG                                                                  
*                                                                               
DFLTSSB  DS    0F                                                               
*&&UK*&& DC    H'0',X'0002',XL251'00'                                           
*&&US*&& DC    H'0',X'0000',XL251'00'                                           
*                                                                               
         DS   0D                                                                
         DC   CL16'***FWK COUNTS***'                                            
TEMPCNT  DC   5F'0'                                                             
         EJECT                                                                  
***********************************************************************         
* TABLE TO OVERRIDE RECOVERY FILE NUMBERS - NON ZERO VALUE IN TABLE   *         
* OVERRIDES STUPID ASSUMPTION THAT RECOVERY FILE IS SAME SYSTEM AS    *         
* FILE WITH AN X'04'                                                  *         
***********************************************************************         
*&&US                                                                           
*                 0 1 2 3 4 5 6 7 8 9 A B C D E F                               
RCVOVTAB DC    X'00000000000000000000000000000000' 00                           
         DC    X'00000000000000000000000000000000' 01                           
         DC    X'00000000000000000000000000000000' 02                           
         DC    X'00000000000024240000000000000000' 03                           
         DC    X'00000000000000000000000000000000' 04                           
         DC    X'00000000000000000000000000000000' 05                           
         DC    X'00000000000000000000000000000000' 06                           
         DC    X'00000000000000000000000000000000' 07                           
         DC    X'00000000000000000000000000000000' 08                           
         DC    X'00000000000000000000000000000000' 09                           
         DC    X'00000000000000000000000000000000' 0A                           
         DC    X'00000000000000000000000000000000' 0B                           
         DC    X'00000000000000000000000000000000' 0C                           
         DC    X'00000000000000000000000000000000' 0D                           
         DC    X'00000000000000000000000000000000' 0E                           
         DC    X'00000000000000000000000000000000' 0F                           
*&&                                                                             
         EJECT                                                                  
         DS    0D                                                               
*                                                                               
MVSFILED DCB   DDNAME=MVSFILE,MACRF=(PM),DSORG=PS,RECFM=VB,            *        
               LRECL=8200,BLKSIZE=27648,EXLST=MVSEXLST                          
*                                                                               
MVSFILET DCB   DDNAME=MVSFILE,MACRF=(PM),DSORG=PS,RECFM=V,             *        
               LRECL=8200,EXLST=MVSEXLST                                        
*                                                                               
MVSEXLST DS    0F                                                               
         DC    X'87'                                                            
         DC    AL3(MVSJFCB)                                                     
MVSJFCB  DC    176X'00'                                                         
                                                                                
***********************************************************************         
* CREATE MVS DATASETS - DYNAMIC ALLOCATION                            *         
***********************************************************************         
         DS    0F                                                               
ARBLKCTA DC    X'80',AL3(RBLKCATA) R1 POINTS TO THIS BEFORE DYNALLOC            
*                                                                               
RBLKCATA DC    X'1401000000000000',A(ACATALLT),X'0000000000000000'              
*                                                                               
ACATALLT DC    X'00',AL3(TXTDD)                                                 
         DC    X'00',AL3(TXTDSN)                                                
         DC    X'00',AL3(TXTDISP)                                               
         DC    X'00',AL3(TXTNDISP)                                              
*        DC    X'00',AL3(TXTTRK)                                                
         DC    X'00',AL3(TXTCYL)                                                
         DC    X'00',AL3(TXTPRI)                                                
         DC    X'00',AL3(TXTSEC)                                                
         DC    X'00',AL3(TXTRLSE)                                               
         DC    X'80',AL3(TXTUNIT)                                               
                                                                                
***********************************************************************         
* CREATE VTS TAPES - DYNAMIC ALLOCATION                               *         
***********************************************************************         
         DS    0F                                                               
ARBLKCTT DC    X'80',AL3(RBLKCATT) R1 POINTS TO THIS BEFORE DYNALLOC            
*                                                                               
RBLKCATT DC    X'1401000000000000',A(ACATTLLT),X'0000000000000000'              
*                                                                               
ACATTLLT DC    X'00',AL3(TXTDD)                                                 
         DC    X'00',AL3(TXTDSN)                                                
         DC    X'00',AL3(TXTDISP)                                               
         DC    X'00',AL3(TXTNDISP)                                              
         DC    X'00',AL3(TXTRLSE)                                               
         DC    X'80',AL3(TXTUNITV)                                              
                                                                                
***********************************************************************         
* MVSFILE DATASETS - DYNAMIC UNALLOCATION BY DDNAME                   *         
***********************************************************************         
         DS    0F                                                               
ARBLKUNA DC    X'80',AL3(RBLKUNA)  R1 POINTS TO THIS BEFORE DYNALLOC            
*                                                                               
RBLKUNA  DC    X'1402000000000000',A(ACATUNT),X'0000000000000000'               
*                                                                               
ACATUNT  DC    X'80',AL3(TXTUNDD)                                               
                                                                                
***********************************************************************         
* DELETE MVSFILE DATASETS - DYNAMIC ALLOCATION                        *         
***********************************************************************         
         DS    0F                                                               
ARBLKDLA DC    X'80',AL3(RBLKDELA) R1 POINTS TO THIS BEFORE DYNALLOC            
*                                                                               
RBLKDELA DC    X'1401000000000000',A(ADELALLT),X'0000000000000000'              
*                                                                               
ADELALLT DC    X'00',AL3(TXTDSN)                                                
         DC    X'00',AL3(TXTDISPO)                                              
         DC    X'80',AL3(TXTNDISD)                                              
                                                                                
***********************************************************************         
* MVSFILE DATASETS - DYNAMIC UNALLOCATION BY DSN                      *         
***********************************************************************         
         DS    0F                                                               
ARBLKUN2 DC    X'80',AL3(RBLKUNA2) R1 POINTS TO THIS BEFORE DYNALLOC            
*                                                                               
RBLKUNA2 DC    X'1402000000000000',A(ACATUNT2),X'0000000000000000'              
*                                                                               
ACATUNT2 DC    X'00',AL3(TXTDSN)                                                
         DC    X'80',AL3(TXTREMOV)                                              
*                                                                               
TXTDD    DC    AL2(DALDDNAM),X'00010008',CL8' '        DDNAME=........          
TXTDSN   DC    AL2(DALDSNAM),X'0001002C',CL44' '       DSN=............         
TXTDISPS DC    AL2(DALSTATS),X'0001000108'             DISP=(SHR,.....)         
TXTDISP  DC    AL2(DALSTATS),X'0001000104'             DISP=(NEW,.....)         
TXTMDISP DC    AL2(DALSTATS),X'0001000102'             DISP=(MOD,.....)         
TXTNDISP DC    AL2(DALNDISP),X'0001000102'                 =(...,CATLG)         
TXTDISPO DC    AL2(DALSTATS),X'0001000101'             DISP=(OLD,...)           
TXTNDISD DC    AL2(DALNDISP),X'0001000104'                 =(...,DEL)           
*XTTRK   DC    AL2(DALTRK),X'0000'                     SPACE=(TRKS...           
TXTCYL   DC    AL2(DALCYL),X'0000'                     SPACE=(CYL...            
TXTPRI   DC    AL2(DALPRIME),X'00010003',AL3(20)            = ..(20,.           
TXTSEC   DC    AL2(DALSECND),X'00010003',AL3(60)            = .,60),.           
TXTRLSE  DC    AL2(DALRLSE),X'0000'                         = ...,RLSE)         
TXTUNIT  DC    AL2(DALUNIT),X'00010005',C'SYSDA'       UNIT=SYSDA               
*&&US                                                                           
TXTUNITV DC    AL2(DALUNIT),X'00010003',C'VTS'         UNIT=VTS                 
*&&                                                                             
*&&UK                                                                           
TXTUNITV DC    AL2(DALUNIT),X'00010004',C'CART'        UNIT=CART                
*&&                                                                             
TXTUNDD  DC    AL2(DUNDDNAM),X'00010008',CL8' '        DDNAME=........          
TXTREMOV DC    AL2(DUNREMOV),X'0000'                   REMOVE INUSE ATR         
         EJECT                                                                  
*&&US                                                                           
***********************************************************************         
* REP TABLE OF AGENCIES GETS LOADED HERE                              *         
***********************************************************************         
         DS    0D                                                               
REFAMOL  EQU   800                 SIZE OF AREA, THIS HAS BOOK= ETC.            
REFAMOS  DC    C'**REFAMO**REFAMO'                                              
REFAMOT  DC    (REFAMOL)X'00'                                                   
REFAMOE  DC    C'**REFAMO**REFAMO'                                              
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE AND OTHER DSECTS                                    *         
***********************************************************************         
DMCBD    DSECT                                                                  
DMCBW1   DS    F                                                                
DMCBW2   DS    F                                                                
DMCBW3   DS    F                                                                
DMCBW4   DS    F                                                                
DMCBW5   DS    F                                                                
DMCBW6   DS    F                                                                
*                                                                               
PARAMS   DSECT                                                                  
PARAM1   DS    F                                                                
PARAM2   DS    F                                                                
PARAM3   DS    F                                                                
PARAM4   DS    F                                                                
PARAM5   DS    F                                                                
PARAM6   DS    F                                                                
PARAM7   DS    F                                                                
         EJECT                                                                  
WORK     DSECT                                                                  
CCWWORK  DS    24D                                                              
DUB      DS    D                                                                
DUB1     DS    D                                                                
FULL     DS    F                                                                
SAVERE   DS    A                                                                
HALF     DS    H                                                                
HALF1    DS    H                                                                
RCVECB   DS    F                                                                
*                                                                               
P1       DS    F                                                                
P2       DS    F                                                                
P3       DS    F                                                                
P4       DS    F                                                                
P5       DS    F                                                                
P6       DS    F                                                                
P7       DS    F                                                                
*                                                                               
Q0       DS    F                                                                
Q1       DS    F                                                                
Q2       DS    F                                                                
Q3       DS    F                                                                
Q4       DS    F                                                                
Q5       DS    F                                                                
Q6       DS    F                                                                
Q7       DS    F                                                                
*                                                                               
DMCBUSSQ DS    0XL24               UNIX QUEUE PARAMETER LIST                    
P1USS    DS    F                   A(COPY RECORD)       OR COMMAND              
P2USS    DS    F                   L'COPY RECORD        OR SENUM                
P3USS    DS    F                   A(CHANGE/ADD RECORD) OR A(GIN)               
P4USS    DS    F                   L'CHANGE/ADD RECORD  OR N/D                  
P5USS    DS    F                   A(TRAILER)           OR N/D                  
P6USS    DS    F                   L'TRAILER            OR N/D                  
*                                                                               
SAVEPARM DS    7F                                                               
*                                                                               
CLOCKWRK DS    4F                                                               
*                                                                               
CPCHDETS DS    0XL16               RECOVERY RECORD DETAILS                      
*                                  KEEP SAME SEQ/FORM AS P1USS TO P4USS         
CPCHACOP DS    A                   A(SAVED COPY RECORD) ZERO IF NONE            
CPCHLCOP DS    F                   L'SAVED COPY RECORD ZERO IF NONE             
CPCHACHG DS    A                   A(ADD OR CHANGE RECORD HEADER)               
CPCHLCHG DS    F                   L'ADD OR CHANGE RECORD INC HEADER            
*                                                                               
SVINLEN  DS    XL2                 SAVED INPUT REC LEN FROM PARAM3+2            
SVDM1    DS    X                                                                
SVIOCNT  DS    XL3                                                              
RECNUM1  DS    X                   REC NUM IN BLOCK OF CPY REC                  
RECNUM2  DS    X                   REC NUM IN BLOCK OF CHG OR ADD REC           
EXTFILL  DS    X                   LEFT NIBBLE OF FILE NUM (SYS)                
EXTFILR  DS    X                   RIGHT NIBBLE OF FILE NUM (FILE)              
         DS    XL2                 SPARE                                        
*                                                                               
         DS    0F                                                               
FILEINFO DS    0XL17               FILE INFO - INITIALIZED TO ZEROS             
ADTF     DS    A                   ADDRESS OF MY DTF                            
XTRADA   DS    F                   EXTRA DISK ADDRESS PASSED IN P7              
KEYLEN   DS    X                   KEY LENGTH                                   
DAFLAG   DS    X                   SET TO FILE NUMBER IF D/A FILE               
ISPTRS   DS    X                   FLAG FOR I/S FILE INDIRECT POINTERS          
ISPDSP   DS    X                   DISP TO INDIRECT POINTER BYTE                
ISPARG   DS    X                   VALUE OF INDIRECT POINTER BYTE               
RCVRSYS  DS    X                   SET TO SENUM FOR DSPACE                      
TRAILERS DS    X                   SET TO X'01' IF TRAILER TO BE ADDED          
SENUM    DS    X                   SENUM FROM UTL                               
BUFFM    DS    X                   BUFFER MODE LOCAL OR SHARED MEMORY           
BLOCALQ  EQU   X'01'               RECOVERY BUFF IN LOCAL STORAGE               
BSHRMEMQ EQU   X'02'               RECOVERY BUFF IN SHARED MEMORY               
BGLOBALQ EQU   X'04'               RECOVERY BUFF IN GLOBAL DSPACE               
BEXTVMTQ EQU   X'08'               RECOVERY FILE USES EXTENDED VERMONTS         
*                                                                               
MYPRM    DS    4F                                                               
*                                                                               
*DMRCVRHDR                                                                      
       ++INCLUDE DMRCVRHDR                                                      
*DMRCVREXT                                                                      
       ++INCLUDE DMRCVREXT                                                      
*                                                                               
ASAVE    DS    A                                                                
ASAVEEXT DS    A                                                                
SAVEHDR  DS    XL4                                                              
SAVE     DS    XL(HDRQ)                                                         
SAVECPY  DS    XL4                                                              
SAVETRL  DS    0XL(EXTQ)                                                        
SAVEEXT  DS    XL(EXTQ)                                                         
*                                                                               
PSAVE    DS    7F                                                               
SAVERD   DS    F                                                                
*                                                                               
EXPARMS  DS    8F                  MVS EXTRACT MACRO PARAMETER LIST             
*                                                                               
SAVEASC  DS    X                   SAVED FROM IAC INSTRUCTION                   
*                                  B'00000010' IF AR MODE                       
*                                                                               
OHDRFLAG DS    XL1                 OFFLINE JOB HEADER PRESENT FLAG              
         DS    0D                                                               
WARNMSG  DS    0CL80               REUSE JOB HEADER FOR WARNING MESSAGE         
OHDRECL  DS    H                   OFFLINE JOB HEADER LENGTH                    
OHDREC   DS    XL(DMRCJB2Q)        OFFLINE JOB HEADER RECORD (INC DSN)          
OMVSDSN  DS    CL44                OFFLINE JOB HEADER MVS DATA SET NAME         
*                                                                               
OTRRFLAG DS    XL1                 OFFLINE JOB TRAILER PRESENT FLAG             
         DS    0D                                                               
OTRRECL  DS    H                   OFFLINE JOB TRAILER LENGTH                   
OTRREC   DS    XL48                OFFLINE JOB TRAILER RECORD                   
*                                                                               
ARECBUF  DS    A                   RECOVERY BUFFER ADDRESS                      
ACTVAD   DS    A                   ACTIVE RECOVERY ADDRESS                      
ACTVID   DS    XL4                 ACTIVE RECOVERY ID                           
*                                                                               
RECADD   DS    A                   A(RECORD TO ADD)                             
RECLEN   DS    H                   LEN OF RECORD TO ADD                         
BYTE1    DS    X                                                                
BYTE2    DS    X                                                                
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
*DMDTFPH                                                                        
       ++INCLUDE DMDTFPH                                                        
*FASSB                                                                          
       ++INCLUDE FASSB                                                          
         ORG   SSBCNTL                                                          
*FASSBOFF                                                                       
       ++INCLUDE FASSBOFF                                                       
*FACIDTABD                                                                      
       ++INCLUDE FACIDTABD                                                      
*FATCB                                                                          
       ++INCLUDE FATCB                                                          
*SELIST                                                                         
       ++INCLUDE FASELIST                                                       
*FAUTL                                                                          
       ++INCLUDE FAUTL                                                          
*DMSPACED                                                                       
       ++INCLUDE DMSPACED                                                       
*DMDSYSHDR                                                                      
       ++INCLUDE DMDSYSHDR                                                      
*DMDSHDR                                                                        
       ++INCLUDE DMDSHDR                                                        
*FATABSD                                                                        
       ++INCLUDE FATABSD                                                        
*FATABSCOM                                                                      
       ++INCLUDE FATABSCOM                                                      
*DMRCVRD                                                                        
       ++INCLUDE DMRCVRD                                                        
*DMRCVXJOB                                                                      
       ++INCLUDE DMRCVXJOB                                                      
*DMWRKRK                                                                        
       ++INCLUDE DMWRKRK                                                        
*FATSTTAB                                                                       
       ++INCLUDE FATSTTAB                                                       
*DMGREQUS                                                                       
       ++INCLUDE DMGREQUS                                                       
*DMFILTABD                                                                      
       ++INCLUDE DMFILTABD                                                      
*DMSYSTABD                                                                      
       ++INCLUDE DMSYSTABD                                                      
*DMSYSFD                                                                        
       ++INCLUDE DMSYSFD                                                        
*FAPIGFACD                                                                      
       ++INCLUDE FAPIGFACD                                                      
*                                                                               
         DCBD  DSORG=PS,DEVD=DA                                                 
*                                                                               
         IEFZB4D2                  NEEDED FOR TXTDD ETC DEFINITIONS             
*                                                                               
         IAZJSAB DSECT=YES         ALL THESE NEEDED FOR IAZXJSAB                
         IHAASCB                                                                
         IHAASSB                                                                
         IHAPSA                                                                 
         IHASTCB                                                                
         IKJTCB                                                                 
         ISGYENQ                                                                
         ISGYCON                                                                
*                                                                               
         IEFJFCBN LIST=YES                                                      
*                                                                               
* JOB/STEP CONTROL BLOCK                                                        
* DSECT: IEZJSCB                                                                
         IEZJSCB                                                                
*                                                                               
       ++INCLUDE FASYSEQUS                                                      
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'018DMRCVR    08/12/20'                                      
         END                                                                    
