*          DATA SET DDWTO      AT LEVEL 095 AS OF 05/19/20                      
*CATALP DDWTO                                                                   
         SPACE 1                                                                
***********************************************************************         
* WRITE A MESSAGE TO THE CONSOLE - PREFIX WITH +JOBNAME+              *         
*        P1=A(MSGWRK) MSGWRK CL80 OR LESS IF TERMINATED WITH //       *         
*        P2(1)=FLAGS                                                  *         
*        X'80' HARDCOPY ONLY                                          *         
***********************************************************************         
***********************************************************************         
*  IF MESSAGE BEGINS MO#NNNNN                                         *         
*  IF MESSAGE BEGINS <XXXNNNND>                                       *         
* XXX WRN/ERR/SYS/RUL                                                           
*                                                                     *         
* NEW STANDARD FORMAT. SEE DDWTOD FOR EXAMPLES                        *         
*                                                                     *         
* HRD=11 <CCC0001D>_                                                  *         
*                                                                     *         
* LEN MAX 126 TWO LINE                                                *         
* LEN MAX-HDR =126-11 = 115                                           *         
* LEN MAX 119 ONE LINE                                                *         
* LEN MAX-HDR =119-11 = 108                                           *         
* LEN MAX 73  ONE LINE CONSOLE                                        *         
* LEN MAX-HDR = 73-11 = 62                                            *         
*                                                                     *         
***********************************************************************         
         TITLE 'DDWTO - SEND A CONSOLE MESSAGE '                                
         PRINT NOGEN                                                            
DDWTO    CSECT                                                                  
         NMOD1 WORKX-WORKD,**WTOR**                                             
         USING WORKD,RC                                                         
*                                                                               
MAIN     LR    R3,R1               R3=A(PARAMETER LIST)                         
         USING PARMD,R3                                                         
         CLI   INITFLG,C'Y'        INIT ONCE PER JOB                            
         BNE   INIT                                                             
*                                                                               
MAIN000  L     R1,P1                                                            
         CLC   0(3,R1),=C'MO#'     IS THIS AN MO# MESSAGE                       
         JE    MAIN500             NEW MESSAGE CODE AT MAIN500                  
         CLC   0(1,R1),=C'<'       IS THIS A <SYS0001D> MESSAGE                 
         JE    MAIN500             NEW MESSAGE CODE AT MAIN500                  
*                                                                               
MAIN010  L     R1,P1                                                            
         MVC   MSGWRK+2(10),WTONAME                                             
         MVC   MSGWRK+12(80),0(R1)                                              
*                                                                               
         LA    R0,90                                                            
         LA    R1,MSGWRK                                                        
MAIN015  CLC   0(2,R1),=C'//'      TEST FOR A TERMINATOR                        
         BE    MAIN020                                                          
         LA    R1,1(R1)                                                         
         BCT   R0,MAIN015                                                       
*                                                                               
MAIN020  MVC   BYTE,2(R1)          SAVE CHR AFTER //                            
         LA    R1,90                                                            
         SR    R1,R0                                                            
         SHI   R1,2                                                             
         STCM  R1,3,MSGWRK         SET TRUE MSGWRK LENGTH                       
*                                                                               
         LA    R4,MSGWRK                                                        
         TM    P2,X'80'            HARDCOPY ONLY FLAG                           
         BO    MAIN200                                                          
         CLI   BYTE,C'H'           //H MEANS HARDCOPY TOO                       
         BE    MAIN200                                                          
*                                                                               
MAIN100  WTO   TEXT=(R4)                                                        
         J     MAINX                                                            
*                                                                               
MAIN200  WTO   TEXT=(R4),MCSFLAG=HRDCPY                                         
         J     MAINX                                                            
*                                                                               
         EJECT                                                                  
************************************************************                    
*        MESSAGE FORMAT <MSG0001D>/N/S/J/A/MESSAGE //      *                    
************************************************************                    
*                                                                               
MAIN500  MVC   MSGWRK,SPACES                                                    
         XC    MSGWRK(2),MSGWRK                                                 
         LA    R7,MSGWRK+2         R7 = OUTPUT POINTER                          
         XR    R8,R8                                                            
         ICM   R8,7,P1+1           R8 = INPUT POINTER                           
         MVI   FLAGS,0                                                          
*                                                                               
         CLI   0(R8),C'<'          <MSG0001D>                                   
         JE    MAIN505                                                          
*                                                                               
         CLI   8(R8),C'/'                                                       
         JNE   *+2                 MUST START MO#NNNNN/                         
         MVC   DUB(5),3(R8)                                                     
         NC    DUB(5),=C'00000'                                                 
         CLC   DUB(5),=C'00000'                                                 
         JNE   *+2                 MUST BE VALID NUMBER                         
*                                                                               
         MVC   0(8,R7),0(R8)       COPY MO# NUMBER AND BUMP                     
         LA    R7,9(R7)                                                         
         LA    R8,9(R8)                                                         
         J     MAIN510                                                          
*                                                                               
MAIN505  LA    RE,VALTAB           FIND CHR IN VALTAB                           
MAIN506  CLC   1(3,R8),0(RE)       MUST BE <CHR0001D>                           
         JE    MAIN507                                                          
         LA    RE,3(RE)                                                         
         CLC   0(3,RE),=C'XXX'     EOT                                          
         JNE   MAIN506                                                          
         DC    H'0'                INVALID MESSAGE FORMAT                       
*                                                                               
MAIN507  CLC   8(2,R8),=C'D>'      MUST END D>                                  
         JNE   *+2                                                              
         MVC   8(1,R8),DSPACE      REPLACE D WITH DSPACE                        
*                                                                               
         MVC   DUB(4),4(R8)                                                     
         NC    DUB(4),=C'0000'                                                  
         CLC   DUB(4),=C'0000'                                                  
         JNE   *+2                 MUST BE VALID NUMBER                         
*                                                                               
         MVC   0(10,R7),0(R8)      COPY MO# NUMBER AND BUMP                     
         LA    R7,11(R7)                                                        
         LA    R8,11(R8)                                                        
*                                                                               
MAIN510  ST    R8,FULL             SAVE POINTER                                 
*                                                                               
         CLC   0(2,R8),=C'N/'      ADD MVS JOBNAME                              
         JNE   MAIN511                                                          
         MVC   0(8,R7),MVSNAME                                                  
         LA    R8,2(R8)                                                         
         LA    R7,9(R7)                                                         
MAIN511  CLC   0(2,R8),=C'S/'      ADD MVS STEPNAME                             
         JNE   MAIN512                                                          
         MVC   0(8,R7),MVSSTEP                                                  
         LA    R8,2(R8)                                                         
         LA    R7,9(R7)                                                         
MAIN512  CLC   0(2,R8),=C'J/'      ADD MVS JOB NUMBER                           
         JNE   MAIN513                                                          
         MVC   0(8,R7),MVSJOB                                                   
         LA    R8,2(R8)                                                         
         LA    R7,9(R7)                                                         
MAIN513  CLC   0(2,R8),=C'A/'      ADD CPUASID                                  
         JNE   MAIN516                                                          
         MVC   0(8,R7),MVSCPA                                                   
         LA    R8,2(R8)                                                         
         LA    R7,9(R7)                                                         
MAIN516  CLI   1(R8),C'/'          ANY MORE ?                                   
         JNE   MAIN520                                                          
         C     R8,FULL             MAKE SURE WE HAVE PROGRESS                   
         JNE   MAIN510                                                          
         LA    R8,2(R8)            IF NOT SKIP THIS ONE                         
         J     MAIN510                                                          
*                                                                               
MAIN520  LA    R0,200              UP TO 200 CHRS                               
         ST    R8,FULL                                                          
MAIN525  CLC   0(3,R8),=C'//H'     TEST FOR A HARDCOPY TERMINATOR               
         JE    MAIN531                                                          
         CLC   0(3,R8),=C'//R'     TEST FOR A REPLY TERMINATOR                  
         JE    MAIN530                                                          
         CLC   0(2,R8),=C'//'      TEST FOR A TERMINATOR                        
         JE    MAIN532                                                          
         LA    R8,1(R8)                                                         
         JCT   R0,MAIN525                                                       
         J     MAIN531             NOT FOUND AFTER 200 CHRS - HARDCOPY          
*                                                                               
MAIN530  OI    FLAGS,FLGREPLY      FLAG FOR WTOR                                
         J     MAIN532                                                          
*                                                                               
MAIN531  OI    FLAGS,FLGHARD       FLAG HARDCOPY                                
*                                                                               
MAIN532  S     R8,FULL             R8=LEN                                       
         LR    R1,R8               R1=LEN                                       
         AHI   R1,-1                                                            
         L     R8,FULL             R8=ADDR                                      
         EXRL  R1,*+10                                                          
         J     *+10                                                             
         MVC   0(0,R7),0(R8)       COPY IN REMAINING MESSAGE                    
         LA    R1,1(R1)                                                         
         AR    R7,R1               AND BUMP                                     
*                                                                               
MAIN540  LA    R1,MSGWRK           CALCULATE ACTUAL MESSAGE LEN                 
         SR    R7,R1                                                            
         SHI   R7,2                                                             
         STCM  R7,3,MSGWRK                                                      
*                                                                               
         LA    R7,MSGWRK                                                        
         TM    FLAGS,FLGHARD       HARDCOPY ONLY FLAG                           
         JO    MAIN700                                                          
         TM    FLAGS,FLGREPLY      REPLY FLAG                                   
         JO    MAIN800                                                          
*                                                                               
MAIN600  WTO   TEXT=(R7)                CONSOLE MESSAGE                         
         B     MAINX                                                            
*                                                                               
MAIN700  WTO   TEXT=(R7),MCSFLAG=HRDCPY HARDCOPY MESSAGE                        
         B     MAINX                                                            
*                                                                               
MAIN800  XC    WTORECB,WTORECB          MESSAGE WITH REPLY                      
         LA    R9,WTORECB                                                       
         MVC   WTOREPL,SPACES                                                   
         LA    R8,WTOREPL                                                       
         WTOR  TEXT=((R7),(R8),8,(R9))                                          
         WAIT  ECB=(R9)                                                         
         LA    R1,WTOREPL                                                       
         ST    R1,P2                                                            
*                                                                               
MAINX    XMOD1 ,                                                                
         EJECT                                                                  
*************************************************************                   
*        INIT JOB INFO FIELDS                               *                   
*************************************************************                   
         SPACE 1                                                                
INIT     LA    R2,FULL                                                          
         EXTRACT (2),'S',FIELDS=(ASID)                                          
         LA    R1,FULL                                                          
         L     R2,0(R1)                                                         
         LOCASCB ASID=(R2)         GET ASCB ADDRESS INTO R1                     
         L     R2,ASCBASSB-ASCB(R1) R5 = A(ASSB)                                
*                                                                               
         LA    RE,*+10                                                          
         O     RE,=X'80000000'                                                  
         BSM   0,RE                SWITCH TO 31-BIT MODE                        
*                                                                               
         L     R1,ASSBJSAB-ASSB(R2) R2 = A(JSAB)                                
         USING JSAB,R1                                                          
         MVC   MVSJOB,JSABJBID     GET JOBID (E.G., JOB12345)                   
         PACK  DUB,MVSJOB+3(5)                                                  
         CVB   R1,DUB                                                           
         STCM  R1,3,MVSJNUM        CONVERT TO JOBNO                             
*                                                                               
         LA    RE,*+6              SWITCH BACK TO 24 BT MODE                    
         BSM   0,RE                                                             
         DROP  R1                                                               
*                                                                               
         LA    R2,FULL             GET JOB NAME INTO MVSNAME                    
         EXTRACT (2),FIELDS=TIOT                                                
         L     R2,FULL                                                          
         MVC   MVSNAME,0(R2)                                                    
         MVC   MVSSTEP,8(R2)                                                    
*                                                                               
         L     R1,X'10'(,0)        GET CPU ID INTO CPU                          
         L     R1,X'C4'(R1)        R1=A(SMCA) FROM CVT                          
         MVC   MVSCPU,16(R1)       CPU IS C'XXXX' FROM SMCASID                  
*                                                                               
         CLI   MVSCPU+3,C' '       IF CPUID IS 4 CHR THEN SYNNASID              
         JNE   *+8                                                              
         MVI   MVSCPU+3,C'/'       IS CPUID IS 3 CHR THEN SYN/ASID              
*                                                                               
         LA    R2,FULL             GET ASID INTO MVSASID                        
         EXTRACT (2),FIELDS=ASID                                                
         MVC   MVSASID,FULL+2                                                   
         MVC   MVSLOCK+0(2),MVSCPU+2 MVSLOCK=CPUID+ASID                         
         MVC   MVSLOCK+2(2),MVSASID                                             
         XR    RF,RF                                                            
         ICM   RF,3,MVSASID        HEXOUT TO MVSASIDC                           
         BRAS  RE,HEXOUT0                                                       
         MVC   MVSASIDC,DUB+4                                                   
*                                                                               
INIT020  MVI   WTONAME,C'+'                                                     
         MVC   WTONAME+1(8),MVSNAME                                             
         LA    R1,WTONAME                                                       
         LA    R0,10                                                            
INIT021  CLI   0(R1),C' '                                                       
         BNH   *+12                                                             
         LA    R1,1(R1)                                                         
         BCT   R0,INIT021                                                       
*                                                                               
         MVI   0(R1),C'+'                                                       
         OC    WTONAME,SPACES                                                   
*                                                                               
INIT030  ICM   RF,15,=V(SSB)                                                    
         JZ    INIT999                                                          
         USING SSBD,RF                                                          
         OC    SSBCNTL,SSBCNTL     ONLINE IF NON ZERO                           
         JNZ   INIT031                                                          
         CLC   0(3,RF),=X'0000FF'  OFFLINE EXTENDED SSB                         
         JNE   INIT999                                                          
         MVC   DSPACE,SSODSPAC-SSOOFF(RF)                                       
         J     INIT999                                                          
*                                                                               
INIT031  MVC   DSPACE,SSBDSPAC                                                  
         J     INIT999                                                          
         DROP  RF                                                               
*                                                                               
INIT999  J     MAIN000                                                          
         EJECT                                                                  
*************************************************************                   
*                                                           *                   
*************************************************************                   
         SPACE 1                                                                
HEXOUT0  LA    R0,8                QUICK HEXOUT RF TO DUB                       
         ST    RE,SAVERE                                                        
         XR    R4,R4                                                            
         XR    R5,R5                                                            
HEXOUT1  SLDL  R4,8                                                             
         XR    RE,RE                                                            
         SLDL  RE,4                                                             
         IC    R5,HEXTAB(RE)                                                    
         JCT   R0,HEXOUT1                                                       
         STCM  R4,15,DUB                                                        
         STCM  R5,15,DUB+4                                                      
         L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
HEXTAB   DC    C'0123456789ABCDEF'                                              
*************************************************************                   
*        CONSTANTS & LTORG                                  *                   
*************************************************************                   
         SPACE 1                                                                
INITFLG  DC    C'N'                                                             
DSPACE   DC    C'?'                                                             
*                                                                               
MVSINFO  DS    0CL32                                                            
MVSLOCK  DC    CL4'    '           LOCK WORD                                    
MVSNAME  DC    CL8'        '       MVS JOB NAME    N                            
MVSSTEP  DC    CL8'        '       MVS STEP NAME   S                            
MVSJOB   DC    CL8'JOB00000'       JOB ID          J                            
*                                                                               
MVSCPA   DS    0CL8                                                             
MVSCPU   DC    CL4'0000'           CPU ASID A/                                  
MVSASIDC DC    CL4'    '                                                        
*                                                                               
MVSASID  DC    XL2'00'             ADDRESS SPACE ID                             
MVSJNUM  DC    XL2'0000'           NUMERIC PART OF JOBID                        
*                                                                               
WTONAME  DS    CL10                                                             
SPACES   DC    126C' '                                                          
*                                                                               
VALTAB   DC    CL3'SYS'                                                         
         DC    CL3'WRN'                                                         
         DC    CL3'ERR'                                                         
         DC    CL3'RUL'                                                         
         DC    CL3'XXX'                                                         
         LTORG                                                                  
         EJECT                                                                  
*************************************************************                   
*        WORKING STORAGE DSECT                              *                   
*************************************************************                   
         SPACE 1                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
WTORECB  DS    F                                                                
WTOREPL  DS    CL8                                                              
FULL     DS    F                                                                
FULL1    DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
FLAGS    DS    X                                                                
FLGHARD  EQU   X'80'                                                            
FLGREPLY EQU   X'40'                                                            
SAVERE   DS    F                                                                
MSGWRK   DS    CL128                                                            
MSGWRKEX DS    CL45                                                             
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
*************************************************************                   
*        OTHER DSECTS                                       *                   
*************************************************************                   
         SPACE 1                                                                
PARMD    DSECT                                                                  
P1       DS    F                                                                
P2       DS    F                                                                
P3       DS    F                                                                
P4       DS    F                                                                
         SPACE 1                                                                
         EJECT                                                                  
       ++INCLUDE FASSB                                                          
         ORG     SSBD                                                           
       ++INCLUDE FASSBOFF                                                       
         PRINT ON                                                               
         IHAASCB LIST=YES                                                       
         IHAASSB LIST=YES                                                       
         IAZJSAB LIST=YES                                                       
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'095DDWTO     05/19/20'                                      
         END                                                                    
