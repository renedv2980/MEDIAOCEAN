*          DATA SET ACGETCRL   AT LEVEL 001 AS OF 09/08/03                      
*CATALP ACGETCRL                                                                
*                                                                               
***********************************************************************         
* ACGETCRL - GENERATE LIST OF CURRENCIES AND EXCHANGE RATES           *         
* ---------------------------------------------------------           *         
* ON ENTRY, P1  BYTES 0   = NOT USED                                  *         
*               BYTES 1-3 = A(INFO BLOCK) - INFOD                     *         
*           P2  BYTES 0   = NOT USED                                  *         
*               BYTES 1-3 = A(OUTPUT TABLE) - LISTTABD                *         
*           P3  BYTES 0   = NOT USED                                  *         
*               BYTES 1-3 = A(COMFACS)                                *         
* ON EXIT,  P1  BYTES 0   = ERROR CODE OR BINARY ZERO                 *         
*               BYTES 1-3 = AS ON ENTRY                               *         
*           P2  BYTES 0   = AS ON ENTRY                               *         
*               BYTES 1-3 = AS ON ENTRY                               *         
*           P3  BYTES 0   = AS ON ENTRY                               *         
*               BYTES 1-3 = AS ON ENTRY                               *         
***********************************************************************         
* LEVEL CHANGE COMMENTS                                               *         
* ---------------------                                               *         
* TKLU 08SEP03 001 NEW VERSION FOR PRESTO/FALINK USE                  *         
***********************************************************************         
* INFORMATION                                                         *         
* -----------                                                         *         
* THIS PROGRAM WILL PASS BACK A LIST OF CURRENCIES WITH CURRENT EX-   *         
* CHANGE RATES (ALL NON-PROD CURRENCIES, THE COMPANY CURRENCY AND     *         
* CURRENCIES WITH NO (VALID)) EXCHANGE RATE ARE EXCLUDED).            *         
***********************************************************************         
         SPACE 1                                                                
         TITLE 'ACGETCRL - GET CURRENCY/EXCHANGE RATE LIST'                     
         SPACE 1                                                                
ACGETCRL CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**GETCRL,RA,RR=RE,CLEAR=YES                          
         USING WORKD,RC                                                         
         ST    RE,RELO                                                          
         ST    R1,APARM            SAVE PARAMETER BLOCK AND PARAMETERS          
         USING INFOD,RF                                                         
         L     RF,0(R1)                                                         
         MVC   PDATE,INFODAT                                                    
         MVC   PALPHA,INFOALP                                                   
         MVC   PCURR,INFOCUR                                                    
         MVC   PFLAG,INFOFLG                                                    
         DROP  RF                                                               
         ICM   RF,15,4(R1)                                                      
         ST    RF,PAOUT                                                         
         ICM   RF,15,8(R1)                                                      
         ST    RF,PACOMF                                                        
         MVC   VBLDCUR,CBLDCUR-COMFACSD(RF)                                     
         MVC   VEUREKA,CEUREKA-COMFACSD(RF)                                     
         SPACE 1                                                                
         MVI   ERRBYTE,PARAMQ                                                   
         OC    PDATE,PDATE         ENSURE PARAMETERS ARE OK                     
         BZ    LABEL90                                                          
         CLC   PDATE,SPACES                                                     
         BE    LABEL90                                                          
         OC    PALPHA,PALPHA                                                    
         BZ    LABEL90                                                          
         CLC   PALPHA,SPACES                                                    
         BE    LABEL90                                                          
         OC    PCURR,PCURR                                                      
         BZ    LABEL90                                                          
         CLC   PCURR,SPACES                                                     
         BE    LABEL90                                                          
         OC    PACOMF,PACOMF                                                    
         BZ    LABEL90                                                          
         OC    PAOUT,PAOUT                                                      
         BZ    LABEL90                                                          
         MVI   ERRBYTE,NOERRQ      SET ERROR TO OK                              
         SPACE 1                                                                
* BUILD TABLE OF ALL PRODUCTION CURRENCIES INTO TABLE FIRST                     
         SPACE 1                                                                
         GOTO1 VBLDCUR,DMCB,(0,A(0)),(X'50',TABLE),PACOMF                       
         CLI   0(R1),0             ANY ERROR?                                   
         BE    LABEL02                                                          
         MVI   ERRBYTE,FATALQ                                                   
         B     LABEL90                                                          
         SPACE 1                                                                
LABEL02  DS    0H                  CHECK TABLE                                  
         OC    TABLE(CURTABL),TABLE                                             
         BNZ   LABEL04                                                          
         MVI   ERRBYTE,EMPTYQ                                                   
         B     LABEL90                                                          
         SPACE 1                                                                
* BUILD OUT TABLE - THEREFORE READ EXCHANGE RATES                               
         SPACE 1                                                                
         USING LISTTABD,R3                                                      
         USING CURTABD,R4                                                       
LABEL04  DS    0H                                                               
         L     R3,PAOUT            POINT TO OUTPUT TABLE                        
         LA    R4,TABLE            POINT TO LIST OF CURRENCIES                  
         SPACE 1                                                                
LABEL06  OC    CURTCUR,CURTCUR     EOT IN?                                      
         BZ    LABEL80                                                          
         CLC   CURTCUR,PCURR       EXLUDE COMPANY'S OWN CURRENCY                
         BE    LABEL20                                                          
         SPACE 1                                                                
         USING EURKBLKD,R2                                                      
         LA    R2,AREA             PREPARE TO READ FOR RATE                     
         XC    0(EURKBLKL,R2),0(R2)                                             
         MVC   EURKCUFR,PCURR                                                   
         MVC   EURKCUTO,CURTCUR                                                 
         MVC   EURKALPH,PALPHA                                                  
         MVC   EURKDATE,PDATE                                                   
         MVC   EURKAFAC,PACOMF                                                  
         MVI   EURKTYPE,ACCQ                                                    
         TM    PFLAG,ALLOWFTQ      ARE FT RATES PERMITTED?                      
         BZ    LABEL08                                                          
         OI    EURKTYPE,ALLOWFTQ+SWAPQ                                          
         SPACE 1                                                                
LABEL08  GOTO1 VEUREKA,DMCB,('GETQ',EURKBLKD)                                   
         CLI   0(R1),0                                                          
*        DC    H'0'                                                             
         BNE   LABEL20                                                          
         SPACE 1                                                                
*                                                                               
* IS THIS REQUIRED FOR LIST WITH RATES?                                         
*        MVC   EXCVALS,EURKRULE    EXTRACT EXCHANGE RATE VALUES                 
*        XR    RF,RF                                                            
*        ICM   RF,8,EURKRLSH                                                    
*        SRA   RF,32-8                                                          
*        LCR   RF,RF                                                            
*        STC   RF,EXCSHFL          LOCAL --> AGENCY SHIFT                       
*                                                                               
         SPACE 1                                                                
         DS    0H                  NOW PUT DATA INTO OUT TABLE                  
         MVC   LISTCUR,CURTCUR                                                  
         MVC   LISTDPL,CURTDECP                                                 
         MVC   LISTRUL,EURKRULE                                                 
         MVC   LISTNAM,CURTLONG                                                 
         AHI   R3,LISTLNQ          POINT TO NEXT ENTRY IN TABLE OUT             
         DROP  R2                                                               
         SPACE 1                                                                
LABEL20  DS    0H                  PROCESS NEXT ENTRY OF TABLE IN               
         AHI   R4,CURTABL+L'CURTLONG                                            
         B     LABEL06                                                          
         SPACE 1                                                                
* FINALLY CHECK TABLE AND HANDLE ERRORS                                         
         SPACE 1                                                                
LABEL80  DS    0H                  CURR LIST AND RATES RESOLVED                 
         L     R3,PAOUT                                                         
         OC    LISTCUR,LISTCUR     ANYTHING DONE AT ALL?                        
         BNZ   LABEL90                                                          
         MVI   ERRBYTE,EMPTYQ      SET TABLE EMPTY                              
         B     LABEL90                                                          
         DROP  R3,R4                                                            
         SPACE 1                                                                
LABEL90  DS    0H                  PASS ERROR BYTE ON EXIT                      
         L     R1,APARM                                                         
         MVC   0(1,R1),ERRBYTE                                                  
         DS    0H                  PROGRAM EXIT                                 
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* CONSTANTS AND LTORG                                                 *         
***********************************************************************         
         SPACE 1                                                                
SPACES   DC    CL132' '                                                         
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* LOCAL WORKING STORAGE AND OTHER DSECTS                              *         
***********************************************************************         
         SPACE 1                                                                
WORKD    DSECT                                                                  
RELO     DS    A                                                                
APARM    DS    A                                                                
PDATE    DS    XL2                                                              
PALPHA   DS    CL2                                                              
PCURR    DS    CL3                                                              
PFLAG    DS    XL1                                                              
PAOUT    DS    A                                                                
PACOMF   DS    A                                                                
         SPACE 1                                                                
ERRBYTE  DS    CL1                                                              
VBLDCUR  DS    V                                                                
VEUREKA  DS    V                                                                
         SPACE 1                                                                
DUB      DS    D                                                                
DMCB     DS    6F                                                               
WORK     DS    CL64                                                             
         SPACE 1                                                                
AREA     DS    XL(EURKBLKL)        EUREKA AREA                                  
TABLE    DS    2000X               TABLE FOR BLDCUR LIST                        
         SPACE 1                                                                
WORKX    EQU   *                                                                
         SPACE 1                                                                
* ACGETCRLD                                                                     
       ++INCLUDE ACGETCRLD                                                      
         EJECT                                                                  
***********************************************************************         
* INCLUDED BOOKS                                                      *         
***********************************************************************         
         SPACE 1                                                                
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DDCOMFACSD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACSD                                                     
         PRINT ON                                                               
         SPACE 1                                                                
* DDCURTABD                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCURTABD                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DDEUREKAD                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDEUREKAD                                                      
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'001ACGETCRL  09/08/03'                                      
         END                                                                    
