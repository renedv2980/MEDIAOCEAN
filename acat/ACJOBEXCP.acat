*          DATA SET ACJOBEXCP  AT LEVEL 032 AS OF 04/15/09                      
*CATALP ACJBEXC                                                                 
*                                                                               
***********************************************************************         
*        LIST EXCEPTION REASONS FOR A JOB                                       
***********************************************************************         
*    PARAMETER BLOCK COVERED BT ACJOBEXCD                                       
         TITLE 'ACJOBEXCP - LIST EXCEPTION REASONS'                             
ACEXCP   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 JXWORKLN,**JBEX**,RR=R5                                          
         L     R8,0(,R1)                                                        
*                                                                               
         USING ACWORKD,RA                                                       
         USING JXWORKD,RC                                                       
         USING JXCEPTD,R8          INTERFACE BLOCK FROM CALLER                  
*                                                                               
         ST    R1,APARMS                                                        
         TM    JXMODE,JXMWORKD     LOAD ACWORKD FROM JXACWORK                   
         BZ    ACXMODES                                                         
         L     RA,JXACWORK                                                      
         NI    JXMODE,X'FF'-JXMWORKD    TURN OFF THE X'80'                      
         EJECT ,                                                                
         SPACE 1                                                                
ACXMODES MVI   JXTAG,X'CC'                                                      
         MVC   JXDADISP,DATADISP   ASSUME OLD ACC RECORD PASSED                 
         CLI   JXMODE,JXMPRACC     FIRST FOR ACCOUNT                            
         BE    FIRST                                                            
         CLI   JXMODE,JXMPRTRN     PROCESS A TRANSACTION                        
         BE    TRAN                                                             
         CLI   JXMODE,JXMLSACC     PRODUCT EXCEPTION LIST                       
         BE    LAST                                                             
         CLI   JXMODE,JXMLIT       RETURN ADDRESS OF LITERAL                    
         BE    LITERAL                                                          
         CLI   JXMODE,JXMCHK       CHECK IF JXCODE IS IN JXCODES                
         BE    CHECK                                                            
         CLI   JXMODE,JXMADD       ADD JXCODE TO JXCODES                        
         BE    ADD                                                              
         CLI   JXMODE,JXMREM       REMOVE JXCODE FROM JXCODES                   
         BE    REMOVE                                                           
         CLI   JXMODE,JXMREAD      READ TRANSACTIONS HERE                       
         BE    READTRNS                                                         
         CLI   JXMODE,JXMSEQ#      RETURN ADDRESS OF LITERAL BY SEQ#            
         BE    SEQUENCE                                                         
         DC    H'0'                EH?                                          
         EJECT ,                                                                
*---------------------------------------------------------------------          
*        READ TRNS HERE, CALL FIRST, TRAN AND LAST AND RETURN WITH              
*        JXCODES ALL SET                                                        
*---------------------------------------------------------------------          
         SPACE 1                                                                
READTRNS DS    0H                                                               
         MVC   JXDADISP,=Y(ACCRFST-ACCKEY)                                      
         BAS   RE,FIRSTN           INITIALIZE                                   
         MVC   SVAREC,JXAREC       SAVE A(ACCOUNT RECORD)                       
         L     RE,JXAREC                                                        
*                                                                               
         USING ACTRECD,R4                                                       
*                                                                               
         LA    R4,JXKEY                                                         
         XC    ACTKEY,ACTKEY                                                    
         XC    ACTKCULA,0(RE)      EXTRACT JOB FROM ACCOUNT RECORD              
         MVC   THISCULA,ACTKCULA   SAVE CURRENT ACCOUNT                         
         BAS   RE,JXHIGH                                                        
*                                                                               
RT10     CLC   ACTKCULA,THISCULA                                                
         BNE   RT100                                                            
*                                                                               
         USING TRNRECD,R4                                                       
*                                                                               
         CLC   TRNKREF,SPACES      IS THIS A TRANSACTION                        
         BNH   RT50                                                             
*                                                                               
         CLI   QDRAFT,QDRABOTH                                                  
         BE    RT15                                                             
         TM    TRNKSTAT,TRNSDRFT   IS THIS A DRAFT?                             
         BO    RT50                YES, SKIP IT                                 
*                                                                               
RT15     BAS   RE,JXGETREC                                                      
         LA    RE,JXIO                                                          
         ST    RE,JXAREC           PASS TRANSACTION TO TRANN                    
         BAS   RE,TRANN                                                         
*                                                                               
RT50     BAS   RE,JXSEQ                                                         
         B     RT10                                                             
*                                                                               
RT100    MVC   JXAREC,SVAREC       RESTORE ACCOUNT ADDRESS                      
         MVC   JXDADISP,DATADISP   RESTORE DATADISP FOR OLDACC RECS             
         B     LAST                CALL LAST AND EXIT                           
*                                                                               
         EJECT ,                                                                
*---------------------------------------------------------------------          
*        FIRST FOR A JOB, INITIALIZE ACCUMULATORS, STATUS BYTES                 
*---------------------------------------------------------------------          
         SPACE 1                                                                
FIRSTN   NTR1                                                                   
*                                                                               
FIRST    XC    JXCODES,JXCODES                                                  
         XC    JXMISUFS,JXMISUFS                                                
         XC    JXNCODES,JXNCODES                                                
         XC    JXOLDMOS,JXOLDMOS                                                
         MVI   JXHELD,0            ANY HELD TRANS                               
         MVI   JXBLSTAT,0          BILLING STATUS BYTE                          
*                                                                               
         LA    RE,JXACCUMS                                                      
         LA    R0,JXNACCUM                                                      
         ZAP   0(L'JXACCUMS,RE),=P'0'                                           
         LA    RE,L'JXACCUMS(,RE)                                               
         BCT   R0,*-10                                                          
*                                                                               
FIX      B     JEXIT                                                            
         EJECT ,                                                                
*----------------------------------------------------------------------         
*        PROCESS THE TRANSACTION PASSED IN JXAREC, ACCUMULATE TOTALS            
*        FOR JOB                                                                
*----------------------------------------------------------------------         
         SPACE 1                                                                
         USING TRNELD,R4                                                        
         SPACE 1                                                                
TRANN    NTR1                                                                   
*                                                                               
TRAN     DS    0H                                                               
         L     R4,JXAREC                                                        
         AH    R4,JXDADISP                                                      
         CLI   TRNEL,X'44'                                                      
         BNE   JEXIT                                                            
         TM    TRNSTAT,X'04'       HELD ITEMS                                   
         BZ    TR60                                                             
         MVI   JXHELD,C'Y'                                                      
*                                                                               
TR60     CLC   TRNANAL,=C'**'                                                   
         BNE   TR62                                                             
         BAS   RE,ORDIT                                                         
         B     TRX                                                              
*                                                                               
TR62     TM    TRNSTAT,X'80'       IS THIS A DEBIT                              
         BNO   TR70                NO                                           
         L     R4,JXAREC                                                        
         AH    R4,JXDADISP                                                      
         BAS   RE,CKBILLED                                                      
         BE    TR65                YES BILLED SO SKIP COMPARE                   
         OC    JXOLDMOS,JXOLDMOS   IS IT SET AT ALL                             
         BZ    TR64                YES                                          
         CLC   JXOLDMOS,DROLDMOS                                                
         BNH   TR65                NOT AS OLD SO SKIP                           
*                                                                               
TR64     MVC   JXOLDMOS,DROLDMOS                                                
*                                                                               
TR65     AP    JXNETACT,TRNAMNT    ACCUMULATE NET ACTUALS                       
         BAS   RE,SETGROSS         SET GROSS ACCUM (JXGRSACT)                   
         B     TRX                                                              
*                                                                               
TR70     AP    JXBILL,TRNAMNT                                                   
         CLC   TRNNARR(9),=C'CLIENT WP'    $BILL BILL                           
         BE    TR80                                                             
         CLI   TRNNARR,C'A'        ALLOCATED BILL?                              
         BNE   *+8                                                              
*                                                                               
TR80     OI    JXBLSTAT,JXBSCLI    THERE IS CLIENT BILLING HERE                 
         OI    JXBLSTAT,JXBSBLD                                                 
*                                                                               
         USING TRNRECD,R5                                                       
*                                                                               
         L     R5,JXAREC                                                        
         CLI   TRNKCUNT,C'3'       UNIT 3 CONTRA ACCOUNT BILL                   
         BNE   *+8                                                              
         OI    JXBLSTAT,JXBSRET    FLAG AS HAVING RETAIL BILLS                  
*                                                                               
         CLI   TRNLN,121           DOES THIS BILL HAVE BUCKETS?                 
         BNE   TRX                                                              
         AP    JXBILCOM,TRNNARR+15(6)                                           
*                                                                               
TRX      B     JEXIT                                                            
         EJECT ,                                                                
         SPACE 1                                                                
         USING GOBLOCKD,R3                                                      
         SPACE 1                                                                
LAST     DS    0H                                                               
         BAS   RE,JOBVALS                                                       
         L     R3,JXAGOBLK                                                      
         L     R2,AMONACC                                                       
*                                                                               
         USING ACMD,R2                                                          
*                                                                               
         L     R2,ACMAJOBB                                                      
*                                                                               
         USING JBLOCKD,R2                                                       
*                                                                               
         XC    JXMISUFS,JXMISUFS   INIT MISSING USER FIELDS BUFFER              
         MVI   FNDERSW,0           INIT FOUND ERROR SWITCH                      
*                                                                               
         LA    R7,JXCODES                                                       
         CP    JXNETACT,BIGEST                                                  
         BNH   ER20                                                             
         CP    JXEST,=P'0'                                                      
         BE    ER20                                                             
         MVC   0(2,R7),=C'1,'                                                   
         LA    R7,2(,R7)                                                        
         BAS   RE,BUMPNUM          BUMP JXNCODES                                
         OI    FNDERSW,FNDER1      FOUND ERROR 1                                
*                                                                               
ER20     CP    UNBILLED,=P'1'                                                   
         BL    ER30                                                             
         CP    UNBILLED,GOMINBIL                                                
         BH    ER30                                                             
         CLI   GOBILTYP,C'P'       VALID FOR PROGRESSIVE ONLY                   
         BNE   ER30                (SEE BELOW FOR %ESTIMATE)                    
         MVC   0(2,R7),=C'2,'                                                   
         LA    R7,2(,R7)                                                        
         BAS   RE,BUMPNUM          BUMP JXNCODES                                
*                                                                               
ER30     L     R4,JXAREC                                                        
         MVI   ELCODE,RSTELQ                                                    
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING RSTELD,R4                                                        
*                                                                               
         GOTO1 DATCON,DMCB,(1,RSTTDATE),(0,JXWORK)                              
         GOTO1 (RF),(R1),(4,RCDATE),(0,JXWORK+6)                                
         GOTO1 ADDAY,DMCB,(C'M',JXWORK),JXWORK+12,F'6'                          
         CLC   JXWORK+12(6),JXWORK+6                                            
         BH    ER40                                                             
         MVC   0(2,R7),=C'3,'                                                   
         LA    R7,2(,R7)                                                        
         BAS   RE,BUMPNUM          BUMP JXNCODES                                
*                                                                               
         DROP  R4                                                               
*                                                                               
ER40     CLI   GOBILTYP,C'E'                                                    
         BNE   ER50                                                             
         ZAP   PL13,JXEST                                                       
         MP    PL13,PEREST                                                      
         SRP   PL13,64-4,5                                                      
         CP    PL13,GOMINBIL                                                    
         BNL   ER40D                                                            
*                                                                               
         CP    JXEST,=P'0'         IS THERE AN ESTIMATE                         
         BE    ER40D               NO, DON'T FLAG (8/13/91)                     
*                                                                               
         MVC   0(2,R7),=C'2,'                                                   
         LA    R7,2(,R7)                                                        
         BAS   RE,BUMPNUM          BUMP JXNCODES                                
*                                                                               
ER40D    CP    JXNETACT,PL13                                                    
         BNH   ER50                                                             
         MVC   0(2,R7),=C'4,'                                                   
         LA    R7,2(,R7)                                                        
         BAS   RE,BUMPNUM          BUMP JXNCODES                                
*                                                                               
ER50     CLI   GOBILTYP,C'U'                                                    
         BNE   ER60                                                             
         CP    JXNETACT,=P'0'                                                   
         BE    ER60                                                             
         MVC   0(2,R7),=C'5,'                                                   
         LA    R7,2(,R7)                                                        
         BAS   RE,BUMPNUM          BUMP JXNCODES                                
*                                                                               
ER60     CP    JXEST,=P'0'         IS ESTIMATE AMOUNT ZERO ?                    
         BNE   ER70                NO, GO TO NEXT                               
*                                                                               
         CLI   GONEEDES,C'Y'       YES, DO I NEED AN ESTIMATE                   
         BNE   ER70                NO, GO TO NEXT                               
         MVC   0(2,R7),=C'6,'                                                   
         LA    R7,2(,R7)                                                        
         BAS   RE,BUMPNUM          BUMP JXNCODES                                
*                                                                               
ER70     CP    JXEST,=P'0'         IS ESTIMATE AMOUNT ZERO ?                    
         BNE   ER80                NO, GO TO NEXT                               
*                                                                               
         CP    JXNETACT,=P'0'      IS ACTUAL AMOUNT ZERO                        
         BNE   ER80                NO, GO TO NEXT                               
*                                                                               
         CP    JXBILL,=P'0'        IS BILLING AMOUNT ZERO                       
         BNE   ER80                NO, GO TO NEXT                               
         MVC   0(2,R7),=C'7,'                                                   
         LA    R7,2(,R7)                                                        
         BAS   RE,BUMPNUM          BUMP JXNCODES                                
*                                                                               
ER80     CLC   JBEND,JBDATE                                                     
         BNL   ER90                                                             
         MVC   0(2,R7),=C'8,'                                                   
         LA    R7,2(,R7)                                                        
         BAS   RE,BUMPNUM          BUMP JXNCODES                                
*                                                                               
ER90     CLI   GOBILTYP,C'C'       IF NON-CLIENT BILL TYPE AND                  
         BE    ER9R                CLIENT BILLS ON JOB, YOU CAN'T BILL          
         TM    JXBLSTAT,JXBSCLI    IS THERE CLIENT BILLING HERE                 
         BO    ER9BAD              YES, ERROR                                   
*                                                                               
ER9R     TM    JXBLSTAT,JXBSBLD    ANY BILLS ON JOB                             
         BNO   ERA0                NO, ITS OK                                   
*                                                                               
         TM    JXBLSTAT,JXBSSCH+JXBSRET IF A SCHEME BILLS MUST BE RET           
         BNM   ERA0                OK                                           
*                                                                               
ER9BAD   MVC   0(2,R7),=C'9,'      YES, INDICATE ERROR                          
         LA    R7,2(,R7)                                                        
         BAS   RE,BUMPNUM          BUMP JXNCODES                                
*                                                                               
ERA0     CLI   GONEEDES,C'Y'       DO I NEED AN ESTIMATE?                       
         BNE   ERB0                NO, GO TO NEXT                               
*                                                                               
         CLI   JBNEWEST,C'Y'       IS THIS A NEW ESTIMATE ?                     
         BE    ERA02               YES                                          
         CLI   JBNEWEST,JBMCSQ     NO, IS IT BRANDO?                            
         BE    ERA02               YES                                          
*                                                                               
         CLI   APPROVED,C'Y'       NO, HANDLE OLD WAY                           
         BE    ERB0                                                             
         B     ERA0ALL                                                          
*                                                                               
ERA02    CLI   GONEEDAE,C'Y'       NEED APPROVAL TO BILL ?                      
         BNE   ERB0                NO                                           
         CLI   JBNEWEST,JBMCSQ     YES, IS IT BRANDO?                           
         BNE   *+12                YES                                          
         CLI   JBHIREV,0           ANY ESTIMATES?                               
         B     *+8                                                              
         CLI   JBLOWREV,X'00'      ANY ESTIMATES ?                              
         BE    ERB0                NO                                           
         OC    JBHIAPP,JBHIAPP     YES, ANY ESTIMATES APPROVED ?                
         BNZ   ERB0                YES                                          
*                                                                               
ERA0ALL  MVC   0(2,R7),=C'A,'      UNAPPROVED ESTIMATE                          
         LA    R7,2(,R7)                                                        
         BAS   RE,BUMPNUM          BUMP JXNCODES                                
*                                                                               
ERB0     CP    JXBIGBAL,=P'0'                                                   
         BE    ERC0                                                             
         CP    BALNC,JXBIGBAL                                                   
         BL    ERC0                                                             
         MVC   0(2,R7),=C'B,'                                                   
         LA    R7,2(,R7)                                                        
         BAS   RE,BUMPNUM          BUMP JXNCODES                                
*                                                                               
ERC0     DS    0H                                                               
         ZAP   JXDUB,JXORDER       CHARGES AND ORDERS GREATER THAN EST          
         AP    JXDUB,JXNETACT                                                   
         CP    JXDUB,BIGEST                                                     
         BNH   ERD0                                                             
         CP    JXEST,=P'0'                                                      
         BE    ERD0                                                             
         CLI   JXCODES,C'1'                                                     
         BE    ERD0                DONT BOTHER IF TYPE 1 ALREADY                
*                                                                               
ERC2     MVC   0(2,R7),=C'C,'                                                   
         LA    R7,2(,R7)                                                        
         BAS   RE,BUMPNUM          BUMP JXNCODES                                
*                                                                               
ERD0     CLI   JXHELD,C'Y'         HELD ITEMS ON JOB                            
         BNE   ERE0                                                             
         MVC   0(2,R7),=C'D,'                                                   
         LA    R7,2(,R7)                                                        
         BAS   RE,BUMPNUM          BUMP JXNCODES                                
*                                                                               
ERE0     BAS   RE,CHKUSERS         ANY MISSING USERFIELDS                       
         BE    ERF0                NO, SKIP THIS                                
         MVC   0(2,R7),=C'E,'      YES, MOVE IN ERROR CODE                      
         LA    R7,2(,R7)                                                        
         BAS   RE,BUMPNUM          BUMP JXNCODES                                
*                                                                               
ERF0     CLI   JBNEWEST,C'Y'       IS THIS A NEW ESTIMATE ?                     
         BE    ERF1                YES                                          
         CLI   JBNEWEST,JBMCSQ     NO, IS IT BRANDO?                            
         BNE   ERG0                NO                                           
*                                                                               
ERF1     CLI   GONEEDAE,C'Y'       YES, NEED APPROVAL TO BILL ?                 
         BNE   ERG0                NO                                           
         CLI   JBHIAPP,X'00'       YES, DO WE HAVE ANY APPROVED ?               
         BE    ERG0                NO                                           
         CLC   JBHIAPP,JBHIREV     YES, ARE ALL REVISIONS APPROVED ?            
         BE    ERG0                YES                                          
         MVC   0(2,R7),=C'F,'                                                   
         LA    R7,2(,R7)                                                        
         BAS   RE,BUMPNUM          BUMP JXNCODES                                
         OI    FNDERSW,FNDERF      FOUND ERROR F                                
*                                                                               
ERG0     MVI   BILFLTSW,C'Y'       INDICATE BILLABLE                            
         L     RF,ADLVASTA         CHECK FOR BILLING STATUS                     
         BAS   RE,FILTRBL           FOR CLIENT                                  
         L     RF,ADLVBSTA                                                      
         BAS   RE,FILTRBL               PRODUCT                                 
         L     RF,ADACCSTA                                                      
         BAS   RE,FILTRBL               AND JOB                                 
         CLI   BILFLTSW,C'Y'       CAN WE BILL ?                                
         BE    ERH0                YES                                          
         MVC   0(2,R7),=C'G,'      NO                                           
         LA    R7,2(,R7)                                                        
         BAS   RE,BUMPNUM          BUMP JXNCODES                                
*                                                                               
ERH0     CP    OVERAMT,=P'0'       ANY MAXIMUM AMOUNT ?                         
         BE    ERI0                NO, DONE                                     
         ZAP   PL13,JXEST          YES, SEE IF AMOUNT OVER IT                   
         AP    PL13,OVERAMT                                                     
         CP    JXNETACT,PL13                                                    
         BNH   ERI0                                                             
         CP    JXEST,=P'0'                                                      
         BE    ERI0                                                             
         MVC   0(2,R7),=C'H,'                                                   
         LA    R7,2(,R7)                                                        
         BAS   RE,BUMPNUM          BUMP JXNCODES                                
         OI    FNDERSW,FNDERH      FOUND ERROR H                                
*                                                                               
ERI0     CP    JXGRSACT,BIGGREST   TEST GROSS OVER ESTIMATE                     
         BNH   ERJ0                                                             
         CP    JXGEST,=P'0'                                                     
         BE    ERJ0                                                             
         MVC   0(2,R7),=C'I,'                                                   
         LA    R7,2(,R7)                                                        
         BAS   RE,BUMPNUM          BUMP JXNCODES                                
*                                                                               
*RJ0     L     R5,AMONACC                                                       
*        L     R5,ACMAGOX                                                       
*        USING GOXBLKD,R5                                                       
*        CLI   GOINTPST,C'Y'       ARE THEY MAKING INTERCOMPANY PSTINGS         
*        BNE   ERK0                NO                                           
*        CLI   JBXJOB,C'Y'         EXPENSE JOB?                                 
*        BNE   ERK0                NO                                           
*        CLI   GOSTUDIO,C'Y'       STUDIO JOB                                   
*        BE    ERK0                YES                                          
*        XR    R4,R4                                                            
*        ICM   R4,15,ALNKEL        IS THERE A VALID LINK                        
*        BZ    ERK0                NO                                           
*        MVC   0(2,R7),=C'K,'                                                   
*        LA    R7,2(R7)                                                         
*        BAS   RE,BUMPNUM          BUMP JXNCODES                                
*                                                                               
         USING ACMD,R5                                                          
*                                                                               
ERJ0     L     R5,AMONACC                                                       
         L     R5,ACMAGOX                                                       
*                                                                               
         USING GOXBLKD,R5                                                       
*                                                                               
         CLI   GOSTUDIO,C'Y'       STUDIO OFFICE JOB                            
         BNE   ERK0                                                             
         XR    R4,R4                                                            
         ICM   R4,15,ALNKEL        IS THERE A VALID LINK                        
         BZ    ERK0                NONE                                         
*                                                                               
         USING LNKELD,R4                                                        
*                                                                               
         OC    LNKAGJB,LNKAGJB     AGENCY JOB DEFINED                           
         BZ    ERK0                NO, NO VALID LINK                            
         CLI   GOBILTYP,C'E'       PCT OF EST                                   
         BE    ERJ20               BINGO                                        
         CLI   GOBILTYP,C'S'       SPECIAL AMOUNT                               
         BNE   ERK0                NO                                           
*                                                                               
ERJ20    MVC   0(2,R7),=C'J,'                                                   
         LA    R7,2(,R7)                                                        
         BAS   RE,BUMPNUM          BUMP JXNCODES                                
*                                                                               
ERK0     CLI   GOSTUDIO,C'Y'       DEPEND ON R5 SET ABOVE                       
         BNE   ERL0                                                             
         CLI   GOBILTYP,C'C'       IS THIS A CLIENT BILLING JOB                 
         BE    ERL0                YES, DON'T FLAG MISSING LINK                 
         XR    R4,R4                                                            
         ICM   R4,15,ALNKEL        IS THERE A LINK                              
         BZ    ERL0                NO                                           
*                                                                               
         USING LNKELD,R4                                                        
*                                                                               
         OC    LNKSTUD,LNKSTUD     IS THERE A VALID STUDIO TYPE                 
         BZ    ERL0                                                             
         OC    LNKAGJB,LNKAGJB     AGENCY JOB DEFINED                           
         BNZ   ERL0                YES, JOB IS BILLABLE                         
*                                                                               
ERK20    MVC   0(2,R7),=C'K,'                                                   
         LA    R7,2(,R7)                                                        
         BAS   RE,BUMPNUM          BUMP JXNCODES                                
*                                                                               
ERL0     CLC   GODIST,SPACES       RETAIL JOB?                                  
         BNH   ERM0                NO                                           
         CLI   GOSTUDIO,C'Y'       STUDIO OFFICE  (R5 FROM ABOVE)               
         BNE   ERM0                                                             
         XR    R4,R4                                                            
         ICM   R4,15,ALNKEL        IS THERE A VALID LINK                        
         BZ    ERM0                NONE                                         
*                                                                               
         USING LNKELD,R4                                                        
*                                                                               
         OC    LNKAGJB,LNKAGJB     AGENCY JOB DEFINED                           
         BZ    ERM0                NO, JOB IS FINE                              
         MVC   0(2,R7),=C'L,'                                                   
         LA    R7,2(,R7)                                                        
         BAS   RE,BUMPNUM          BUMP JXNCODES                                
*                                                                               
ERM0     CLI   GOBILTYP,C'C'       IS THIS A CLIENT BILLING JOB                 
         BNE   ERO0                NO                                           
         OC    GOASPCT,GOASPCT     ANY CONTINGENCY PERCENT                      
         BZ    ERO0                                                             
         CP    GOASPCT,=P'0'       IS IT ZERO                                   
         BE    ERO0                                                             
*                                                                               
         OC    GOASACC,GOASACC     IS THERE AN ASP ACCOUNT                      
         BNZ   ERN0                YES, COOL                                    
         MVC   0(2,R7),=C'M,'                                                   
         LA    R7,2(,R7)                                                        
         BAS   RE,BUMPNUM          BUMP JXNCODES                                
*                                                                               
ERN0     OC    GOASWRK,GOASWRK     IS THERE AN ASP WORKCODE                     
         BNZ   ERO0                NO                                           
         MVC   0(2,R7),=C'N,'                                                   
         LA    R7,2(,R7)                                                        
         BAS   RE,BUMPNUM          BUMP JXNCODES                                
*                                                                               
ERO0     DS    0H                  FOUND ERRORS   (1 OR H)   AND  F ?           
         TM    FNDERSW,FNDERF      FOUND ERROR     F ?                          
         BZ    ERP0                NO                                           
         TM    FNDERSW,FNDER1+FNDERH     ERROR    (1 OR H) ?                    
         BZ    ERP0                NO                                           
         OC    JXHREST,JXHREST     ANY   HR   ESTIMATE ?                        
         BZ    ERP0                NO                                           
         CLC   JXHREST,=PL6'0'     IS    HR   ESTIMATE  ZERO ?                  
         BE    ERP0                YES                                          
         ZAP   BIGHREST,JXHREST    GET   HR   ESTIMATE                          
         CP    OVEREST,=P'0'       IS    OVER ESTIMATE  ZERO ?                  
         BE    ERO0A               YES,  SKIP                                   
         ZAP   PL13,JXHREST        SET   MAX  OVER HR   EST  ALLOWED            
         MP    PL13,OVEREST                                                     
         SRP   PL13,64-4,5                                                      
         ZAP   BIGHREST,PL13                                                    
*                                                                               
ERO0A    CP    JXNETACT,BIGHREST   ACTUALS    >    HR   ESTIMATE ?              
         BNH   ERP0                NO                                           
         MVC   0(2,R7),=C'O,'                                                   
         LA    R7,2(,R7)                                                        
         BAS   RE,BUMPNUM          BUMP  JXNCODES                               
*                                                                               
ERP0     DS    0H                                                               
*&&DO                                                                           
*                                  FOUND ERRORS 1 AND H ?                       
         TM    FNDERSW,FNDER1+FNDERH                                            
         BNO   ERQ0                NO                                           
         OC    JXHREST,JXHREST     ANY  HR ESTIMATE ?                           
         BZ    ERQ0                NO                                           
         CP    JXNETACT,JXHREST    ACTUALS <= HR ESTIMATE                       
         BNH   ERQ0                NO                                           
         MVC   0(2,R7),=C'P,'                                                   
         LA    R7,2(,R7)                                                        
         BAS   RE,BUMPNUM          BUMP JXNCODES                                
*                                                                               
*&&                                                                             
ERQ0     DS    0H                                                               
         L     R4,JXAREC                                                        
         MVI   ELCODE,JOBELQ                                                    
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                NO JOB STATUS ELEMENT                        
         USING JOBELD,R4                                                        
         TM    JOBSTA2,JOBSFUN     IS JOB FUNDED?                               
         BO    ERXX                YES, OK                                      
         CLI   GOBILFUN,C'Y'       NO, IS FUNDING REQUIRED?                     
         BNE   ERXX                NO, OK                                       
         MVC   0(2,R7),=C'P,'      YES, PRINT ERROR MESSAGE                     
         LA    R7,2(,R7)                                                        
         BAS   RE,BUMPNUM          BUMP JXNCODES                                
*                                                                               
ERXX     BCTR  R7,0                                                             
         CLI   0(R7),C','                                                       
         BNE   *+8                                                              
         MVI   0(R7),0                                                          
         B     JEXIT                                                            
*                                                                               
         DROP  R3,R4,R5                                                         
         EJECT ,                                                                
*---------------------------------------------------------------------          
*        CALCULATE JOB LEVEL VALUES IN WORKING STORAGE                          
*---------------------------------------------------------------------          
         SPACE 1                                                                
         USING ABLELD,R4                                                        
         SPACE 1                                                                
JOBVALS  NTR1                                                                   
         L     R4,JXAREC                                                        
         MVI   ELCODE,ABLELQ       GET BALANCE ELEMENT                          
         BAS   RE,GETEL                                                         
*                                                                               
         ZAP   BALNC,ABLDR         CALCULATE JOB BALANCE                        
         SP    BALNC,ABLCR                                                      
*                                                                               
         L     R4,JXAREC                                                        
         MVI   APPROVED,C'Y'       SET TO APPROVED ESTIMATE                     
*                                                                               
         MVI   ELCODE,JOBELQ                                                    
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                NO JOB STATUS ELEMENT                        
*                                                                               
         USING JOBELD,R4                                                        
*                                                                               
         TM    JOBSTA1,JOBSUEST    TEST FOR UNAPPROVED ESTIMATE                 
         BZ    *+8                                                              
         MVI   APPROVED,C'N'       YES                                          
*                                                                               
         MVC   JBEND,JOBCDATE      SAVE JOB CLOSING DATE                        
*                                                                               
         GOTO1 DATCON,DMCB,(4,RCDATE),(1,JBDATE) AND TODAYS DATE                
*                                                                               
         L     R3,JXAGOBLK                                                      
         USING GOBLOCKD,R3                                                      
         LA    R3,GOBILAM1                                                      
         MVC   JXFULL,0(R3)                                                     
         CLI   JOBLN,JOBLN3Q       IS %EST BILLING STATUS ON THIS JOBEL         
         BL    JV50                NO                                           
*                                                                               
         LA    R0,2                                                             
         LA    RE,X'80'                                                         
         EX    RE,*+8                                                           
         B     *+8                                                              
         TM    JOBBIST,X'00'                                                    
         BZ    JV60                                                             
*                                                                               
JV50     LA    R3,4(,R3)                                                        
         OC    0(4,R3),0(R3)       NEXT PERCENT IS ZERO                         
         BZ    JV60                SO GET OUT                                   
*                                                                               
         SRL   RE,1                SET FOR NEXT PERCENT                         
         L     RF,0(,R3)                                                        
         A     RF,JXFULL           ACCUMULATE BILLED PERCENTAGES                
         ST    RF,JXFULL                                                        
         EX    RE,*+8                                                           
         B     *+8                                                              
         TM    JOBBIST,X'00'                                                    
         BZ    JV60                                                             
         BCT   R0,JV50                                                          
*                                                                               
JV60     L     R1,JXFULL                                                        
         CVD   R1,JXDUB                                                         
         ZAP   PEREST,JXDUB                                                     
*                                                                               
         L     R3,JXAGOBLK                                                      
         USING GOBLOCKD,R3                                                      
         CLC   GODIST,SPACES       DISTRIBUTION SCHEME ON JOB                   
         BNH   *+8                 NO                                           
         OI    JXBLSTAT,JXBSSCH    FLAG JOB HAS A SCHEME                        
*                                                                               
         ZAP   OVEREST,GOOVRPER                                                 
         ZAP   OVERAMT,=P'0'                                                    
         CP    GOOVRAMT,=P'0'      MAXIMUM OVER AMOUNT                          
         BE    *+10                                                             
         ZAP   OVERAMT,GOOVRAMT                                                 
*                                                                               
         ZAP   OVER,JXNETACT                                                    
         SP    OVER,JXEST                                                       
*                                                                               
         ZAP   UNBILLED,JXNETACT                                                
         SP    UNBILLED,JXBILL                                                  
*                                                                               
         ZAP   PL13,JXEST          SET MAXIMUM OVER ESTIMATE ALLOWED            
         MP    PL13,OVEREST                                                     
         SRP   PL13,64-4,5                                                      
         ZAP   BIGEST,PL13                                                      
*                                                                               
         ZAP   PL13,JXGEST         SET GROSS MAX OVER EST ALLOWED               
         MP    PL13,OVEREST                                                     
         SRP   PL13,64-4,5                                                      
         ZAP   BIGGREST,PL13                                                    
*                                                                               
         L     R4,JXAREC                                                        
         XC    ALNKEL,ALNKEL                                                    
         MVI   ELCODE,LNKELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   *+8                                                              
         ST    R4,ALNKEL                                                        
*                                                                               
         B     JEXIT                                                            
*                                                                               
         DROP  R3,R4                                                            
         EJECT ,                                                                
         SPACE 1                                                                
         USING RSTELD,RF                                                        
         SPACE 1                                                                
FILTRBL  CLI   RSTLN,RSTLN2Q       IS THIS AN OLD ELEMENT ?                     
         BLR   RE                  YES                                          
         TM    RSTSTAT2,X'80'      BILLABLE ?                                   
         BZ    *+10                NO                                           
         MVI   BILFLTSW,C'Y'       YES                                          
         BR    RE                                                               
         TM    RSTSTAT2,X'40'      UNBILLABLE ?                                 
         BZR   RE                  NO                                           
         MVI   BILFLTSW,C'N'       YES                                          
         BR    RE                                                               
*                                                                               
         DROP  RF                                                               
         EJECT ,                                                                
*----------------------------------------------------------------------         
*        RETURN THE ADDRESS OF THE LITERAL ENTRY PASSED IN JXCODE               
*----------------------------------------------------------------------         
         SPACE 1                                                                
         USING JXMSGD,R2                                                        
         SPACE 1                                                                
LITERAL  DS    0H                                                               
         LA    R2,EXCPTB                                                        
*                                                                               
LIT10    CLI   JXMCODE,X'FF'       END OF TABLE                                 
         BE    LITNO               FLAG JXCODE NOT FOUND                        
         CLC   JXMCODE,JXCODE                                                   
         BE    LITYES                                                           
         LA    R2,JXMLEN(,R2)                                                   
         B     LIT10                                                            
*                                                                               
LITNO    XC    JXALIT,JXALIT       RETURN A(0)                                  
         B     LITX                                                             
*                                                                               
LITYES   STCM  R2,15,JXALIT        RETURN A(TABLE ENTRY FOR JXCODE)             
         B     LITX                                                             
*                                                                               
LITX     B     JEXIT                                                            
*                                                                               
NOXIT    LTR   RB,RB                                                            
         B     JEXIT                                                            
*                                                                               
YESXIT   CR    RB,RB                                                            
         B     JEXIT                                                            
*                                                                               
JEXIT    XIT1                                                                   
         EJECT ,                                                                
*----------------------------------------------------------------------         
*        RETURN THE ADDRESS OF THE LITERAL ENTRY BY SEQ NUMBER CODE             
*----------------------------------------------------------------------         
         SPACE 1                                                                
         USING JXMSGD,R2                                                        
         SPACE 1                                                                
SEQUENCE LA    R2,EXCPTB                                                        
*                                                                               
SEQ10    CLI   JXMCODE,X'FF'       END OF TABLE                                 
         BE    SEQNO               FLAG JXCODE NOT FOUND                        
         CLC   JXMSEQ,JXCODE                                                    
         BE    SEQYES                                                           
         LA    R2,JXMLEN(,R2)                                                   
         B     SEQ10                                                            
*                                                                               
SEQNO    XC    JXALIT,JXALIT       RETURN A(0)                                  
         B     SEQX                                                             
*                                                                               
SEQYES   STCM  R2,15,JXALIT        RETURN A(TABLE ENTRY FOR JXCODE)             
*                                                                               
SEQX     B     JEXIT                                                            
         EJECT ,                                                                
*----------------------------------------------------------------------         
*        CHECK, RETURN EQUAL IF JXCODE IS IN JXCODES, NEQ IF NOT                
*----------------------------------------------------------------------         
         SPACE 1                                                                
CHECK    BAS   RE,CHECKIT                                                       
         B     JEXIT                                                            
*                                                                               
CHECKIT  NTR1                                                                   
         LA    R2,JXCODES                                                       
*                                                                               
CHK10    CLI   0(R2),0             END OF LIST                                  
         BE    NOXIT               FLAG JXCODE NOT FOUND                        
         CLC   JXCODE,0(R2)                                                     
         BE    YESXIT                                                           
         LA    R2,2(,R2)                                                        
         B     CHK10                                                            
         EJECT ,                                                                
*----------------------------------------------------------------------         
*        REMOVE, REMOVES JXCODE FROM JXCODES                                    
*        USED WHEN A REPORT ONLY REPORTS AN EXCEPTION ON SPECIAL REQ            
*----------------------------------------------------------------------         
         SPACE 1                                                                
REMOVE   LA    R2,JXCODES                                                       
         LA    R1,L'JXCODES                                                     
*                                                                               
REM10    CLI   0(R2),0             END OF LIST                                  
         BE    NOXIT               FLAG JXCODE NOT FOUND                        
         CLC   JXCODE,0(R2)                                                     
         BE    REM20                                                            
         LA    R2,2(,R2)                                                        
         SH    R1,=H'2'                                                         
         B     REM10                                                            
*                                                                               
REM20    SH    R1,=H'3'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),2(R2)                                                    
*                                                                               
         LA    R2,1(R1,R2)         POINT TO OLD EOT                             
         XC    0(2,R2),0(R2)       ERASE                                        
         ZIC   R1,JXNCODES                                                      
         LTR   R1,R1                                                            
         BNZ   *+6                                                              
         DC    H'0'                IF NCODES IS 0, WHY WAS IN LIST?             
         BCTR  R1,0                                                             
         STC   R1,JXNCODES                                                      
         B     YESXIT              RETURN SUCESSS                               
*                                                                               
         EJECT ,                                                                
         SPACE 1                                                                
ADD      BAS   RE,CHECKIT          ALREADY IN LIST                              
         BE    JEXIT               OK                                           
         LA    R2,JXCODES                                                       
*                                                                               
ADD10    CLI   0(R2),0             END OF LIST                                  
         BE    ADD20               ADD A 0(R2)                                  
         LA    R2,2(,R2)                                                        
         B     ADD10                                                            
*                                                                               
ADD20    MVC   0(1,R2),JXCODE                                                   
         MVI   1(R2),C','                                                       
         MVI   2(R2),0             SET NEW END OF LIST                          
         BAS   RE,BUMPNUM          BUMP JXNCODES                                
         B     YESXIT                                                           
*                                                                               
BUMPNUM  XR    RF,RF                                                            
         IC    RF,JXNCODES                                                      
         LA    RF,1(,RF)                                                        
         STC   RF,JXNCODES                                                      
         BR    RE                                                               
         EJECT ,                                                                
*----------------------------------------------------------------------         
*        BUILD A LIST OF MISSING USER FIELDS NEEDED TO BILL                     
*----------------------------------------------------------------------         
         SPACE 1                                                                
         USING UFSELD,R4                                                        
         SPACE 1                                                                
CHKUSERS NTR1                                                                   
         XC    JXMISUFS,JXMISUFS                                                
         LA    R1,JXMISUFS                                                      
*                                                                               
         L     R4,JXAREC                                                        
         MVI   ELCODE,UFSELQ                                                    
         BAS   RE,GETEL                                                         
*                                                                               
CU02     BNE   CUX                                                              
*                                                                               
         TM    UFSSTAT,UFSSNEBI  NEEDED FOR BILLING                             
         BO    CU05                                                             
*                                                                               
CU04     BAS   RE,NEXTEL                                                        
         B     CU02                                                             
*                                                                               
CU05     LA    R2,UFSDATA-UFSELD    LENGTH UP TO DATA                           
         ZIC   R3,UFSLN             ELEMENT LENGTH                              
         SR    R3,R2                R3=LENGTH OF USER FIELD DATA                
         BNZ   CU04                 DATA IS PRESENT, CHECK NEXT                 
*                                                                               
         MVC   0(2,R1),UFSCODE      SAVE CODE                                   
         MVI   2(R1),C','           COMMA DELIMITER                             
         LA    R1,3(,R1)                                                        
         B     CU04                                                             
*                                                                               
CUX      OC    JXMISUFS,JXMISUFS                                                
         BZ    COXX                                                             
*                                                                               
         BCTR  R1,0                                                             
         MVI   0(R1),0             REMOVE LAST COMMA                            
         CLI   0(R1),X'FF'         SET CC                                       
*                                                                               
COXX     B     JEXIT                                                            
*                                                                               
         EJECT ,                                                                
         SPACE 1                                                                
         USING GOBLOCKD,R3                                                      
         SPACE 1                                                                
ADCOM    NTR1                                                                   
         L     R3,JXAGOBLK                                                      
         ZAP   PL13,JXDUB          ADD COMMISSION TO JXDUB                      
         MP    PL13,GOAGYCOM                                                    
         SRP   PL13,64-6,5         RATE IS FOUR DEC, AMOUNT IS 2                
         AP    JXDUB,PL13                                                       
         B     JEXIT                                                            
*                                                                               
         DROP  R3                                                               
         EJECT ,                                                                
*---------------------------------------------------------------------          
*        ADD THE GROSS AMOUNT OF THIS TRAN TO JXGRSACT                          
*---------------------------------------------------------------------          
         SPACE 1                                                                
         USING TRNELD,R4                                                        
         SPACE 1                                                                
SETGROSS NTR1                                                                   
         L     R4,JXAREC                                                        
         AH    R4,JXDADISP                                                      
         ZAP   JXDUB,TRNAMNT                                                    
         ZAP   CASHD,=P'0'                                                      
*                                                                               
         MVI   ELCODE,X'50'                                                     
*                                                                               
SETG10   BAS   RE,NEXTEL                                                        
         BNE   SETG30                                                           
*                                                                               
         USING SCIELD,R4                                                        
*                                                                               
         CLI   SCITYPE,SCITCDSC                                                 
         BNE   SETG10              TRY AGAIN                                    
         ZAP   CASHD,SCIAMNT                                                    
*                                                                               
         USING TRNELD,R4                                                        
*                                                                               
SETG30   L     R4,JXAREC           ADDRESS TRANSACTION RECORD                   
         AH    R4,JXDADISP                                                      
         TM    TRNSTAT,X'01'       NON-COMM                                     
         BO    SETG40                                                           
         AP    JXDUB,CASHD                                                      
         BAS   RE,ADCOM            ADD COMMISSION TO JXDUB                      
*                                                                               
         SP    JXDUB,CASHD                                                      
*                                                                               
SETG40   AP    JXGRSACT,JXDUB                                                   
         B     JEXIT                                                            
*                                                                               
         DROP  R4                                                               
         EJECT ,                                                                
*---------------------------------------------------------------------          
* SEE IF THIS DEBIT IS UNBILLED OR NOT - R4 = TRNELD X'44'                      
*---------------------------------------------------------------------          
         SPACE 1                                                                
         USING TRNRECD,R2                                                       
         USING TRNELD,R4                                                        
         SPACE 1                                                                
CKBILLED NTR1                                                                   
         ZAP   DRBILLED,=P'0'                                                   
         ZAP   DRAMT,TRNAMNT                                                    
         MVC   DROLDMOS,TRNMOS     SAVE X'44' MOS                               
         LR    R2,R4                                                            
         CLI   JXMODE,JXMPRTRN     OLD KEY POSSIBLE                             
         BE    CKBIL10                                                          
         LA    R2,JXKEY            HAVE DIRECTORY KEY                           
         MVC   DROLDMOS,TRNKSMOS   MOS OF TRANSACTION IN DIR KEY                
         TM    TRNKSTA2,TRNSUSED   IS THIS BILLED ?                             
         BZ    CKBIL20             YES SO SKIP COMPARE                          
         B     CKBIL80             EXIT AS BILLED                               
*                                                                               
         USING ACMD,R2                                                          
*                                                                               
CKBIL10  L     R2,AMONACC                                                       
         MVC   DROLDMOS,ACMMDTE    MOS OF TRANSACTION                           
*                                                                               
         USING TRSELD,R2                                                        
*                                                                               
         LR    R2,R4               POINT TO X'44' ELEMENT                       
*                                                                               
CKBIL12  CLI   0(R2),0             END OF RECORD                                
         BE    CKBIL20                                                          
         CLI   0(R2),TRSELQ        X'60' ELEMENT                                
         BE    CKBIL15                                                          
         SR    R1,R1                                                            
         IC    R1,TRSLN                                                         
         AR    R2,R1                                                            
         B     CKBIL12                                                          
*                                                                               
CKBIL15  OC    TRSUDAT,TRSUDAT     IF USED THEN BILLED                          
         BNZ   CKBIL80                                                          
*                                                                               
CKBIL20  CLI   0(R4),0             END OF RECORD                                
         BE    CKBIL90                                                          
         CLI   0(R4),BNDELQ        X'4B' TYPE PROD ELEMENT                      
         BE    CKBIL40                                                          
         CLI   0(R4),PTAELQ        X'77' NEW PROD ELEMENT                       
         BE    CKBIL60                                                          
*                                                                               
CKBIL30  SR    R1,R1                                                            
         IC    R1,TRNLN                                                         
         AR    R4,R1                                                            
         B     CKBIL20                                                          
*                                                                               
         USING BNDELD,R4                                                        
*                                                                               
CKBIL40  CLC   BNDBNO,SPACES       IGNORE ALLOCATED                             
         BNH   CKBIL30                                                          
         ICM   R1,15,BNDAMNT                                                    
         CVD   R1,JXDUB                                                         
         AP    DRBILLED,JXDUB                                                   
         B     CKBIL30                                                          
*                                                                               
         USING PTAELD,R4                                                        
*                                                                               
CKBIL60  TM    PTASTAT1,PTASPEND   IGNORE PENDING                               
         BNZ   CKBIL30                                                          
         AP    DRBILLED,PTANET                                                  
         B     CKBIL30                                                          
*                                                                               
CKBIL80  ZAP   DRBILLED,DRAMT      SET FOR CC BILLED                            
*                                                                               
CKBIL90  CP    DRBILLED,DRAMT      IF EQUAL THEN BILLED                         
         B     JEXIT                                                            
*                                                                               
         DROP  R2,R4                                                            
         EJECT ,                                                                
*---------------------------------------------------------------------          
* DATAMGR INTERFACE                                                             
*---------------------------------------------------------------------          
         SPACE 1                                                                
JXHIGH   NTR1                                                                   
         LA    R4,JXKEY                                                         
         GOTO1 DATAMGR,DMCB,DMRDHI,=CL8'ACCDIR',(R4),(R4)                       
         B     JEXIT                                                            
*                                                                               
JXSEQ    NTR1                                                                   
         LA    R4,JXKEY                                                         
         GOTO1 DATAMGR,DMCB,DMRSEQ,=CL8'ACCDIR',(R4),(R4)                       
         B     JEXIT                                                            
*                                                                               
         USING ACTRECD,R4                                                       
*                                                                               
JXGETREC NTR1  WORK=(R2,12)                                                     
         LA    R4,JXKEY                                                         
         LA    R3,ACTKDA           ADDRESS DISK ADDRESS FOR GETREC              
         GOTO1 DATAMGR,DMCB,DMGET,=C'ACCMST ',(R3),JXIO,(R2)                    
         B     JEXIT                                                            
         EJECT ,                                                                
         SPACE 1                                                                
         USING OAMELD,R4                                                        
         SPACE 1                                                                
ORDIT    NTR1                      READ ORDER DATA                              
         L     R4,JXAREC           ADDRESS TRANSACTION RECORD                   
         MVI   ELCODE,OAMELQ                                                    
         BAS   RE,GETEL                                                         
*                                                                               
ORD2     BNE   OX                                                               
*                                                                               
         ZAP   JXDUB,OAMAMNT                                                    
         SP    JXDUB,OAMIVAL                                                    
         AP    JXORDER,JXDUB       SAVE NET ORDER AMOUNT                        
         BAS   RE,ADCOM            ADD COMMISSION                               
         AP    JXGRORD,JXDUB       SAVE GROSS ORDR AMOUNT                       
         BAS   RE,NEXTEL                                                        
         B     ORD2                                                             
*                                                                               
OX       B     JEXIT                                                            
         EJECT ,                                                                
         SPACE 1                                                                
         GETEL (R4),JXDADISP,ELCODE                                             
         EJECT ,                                                                
         SPACE 1                                                                
         LTORG                                                                  
         EJECT ,                                                                
         SPACE 1                                                                
EXCPTB   DC    CL1'1',CL40'CAN''T BILL DUE TO OVERSPENDING ESTIMATE'            
         DC    CL70'1 = CAN''T BILL-OVER EST MAX PCT-EXCEPT WHEN %EST,SX        
               PEC AMT,EST NOT REQ'                                             
         DC    CL15'OVER EST%'                                                  
         DC    AL1(1)                                                           
         DC    AL1(JXMTERR)                                                     
*                                                                               
         DC    CL1'2',CL40'CAN''T BILL DUE TO BALANCE BELOW MINIMUM'            
         DC    CL70'2 = CAN''T BILL - BALANCE LESS THAN ''LOW''.'               
         DC    CL15'BAL < MIN'                                                  
         DC    AL1(2)                                                           
         DC    AL1(JXMTERR)                                                     
*                                                                               
         DC    CL1'3',CL40'WARNING - JOB INACTIVE FOR 6 MONTHS'                 
         DC    CL70'3 = WARNING - JOB HAS BEEN INACTIVE FOR 6 MONTHS.'          
         DC    CL15'JOB INACTIVE'                                               
         DC    AL1(3)                                                           
         DC    AL1(JXMTWARN)                                                    
*                                                                               
         DC    CL1'4',CL40'WARNING - ACTUALS EXCEED PCT OF ESTIMATE'            
         DC    CL70'4 = WARNING - BILL TYPE IS %EST; ACTUALS EXCEED PCTX        
               . OF EST.'                                                       
         DC    CL15'CHRGS > %EST'                                               
         DC    AL1(4)                                                           
         DC    AL1(JXMTWARN)                                                    
*                                                                               
         DC    CL1'5',CL40'CAN''T BILL-JOB HAS UNBILLABLE BILL TYPE'            
         DC    CL70'5 = CAN''T BILL - JOB HAS UNBILLABLE BILLING TYPE.'         
         DC    CL15'UNBILLABLE BT'                                              
         DC    AL1(5)                                                           
         DC    AL1(JXMTERR)                                                     
*                                                                               
         DC    CL1'6',CL40'CAN''T BILL-JOB DOES NOT HAVE AN ESTIMATE'           
         DC    CL70'6 = CAN''T BILL - JOB DOES NOT HAVE AN ESTIMATE (ORX        
                CURRENT=0).'                                                    
         DC    CL15'NO ESTIMATE'                                                
         DC    AL1(6)                                                           
         DC    AL1(JXMTERR)                                                     
*                                                                               
         DC    CL1'7',CL40'WARNING - NO ESTIMATE,CHARGES OR BILLING'            
         DC    CL70'7 = WARNING - JOB HAS NO ESTIMATE, CHARGES OR BILLIX        
               NG'                                                              
         DC    CL15'NO EST,CHRG,BIL'                                            
         DC    AL1(7)                                                           
         DC    AL1(JXMTWARN)                                                    
*                                                                               
         DC    CL1'8',CL40'WARNING - JOB IS PAST CLOSING DATE'                  
         DC    CL70'8 = WARNING - JOB IS PAST CLOSING DATE.'                    
         DC    CL15'PAST CLOSE DATE'                                            
         DC    AL1(8)                                                           
         DC    AL1(JXMTWARN)                                                    
*                                                                               
         DC    CL1'9',CL40'CAN''T BILL - CLIENT OR RETAIL CONFLICT'             
         DC    CL70'9 = CAN''T BILL - CLT WP,ALLOCATED BILLING OR RETAIX        
               L/NONRETAIL CONFLICT'                                            
         DC    CL15'BILL CONFLICT'                                              
         DC    AL1(9)                                                           
         DC    AL1(JXMTERR)                                                     
*                                                                               
         DC    CL1'A',CL40'CAN''T BILL - JOB HAS UNAPPROVED ESTIMATE'           
         DC    CL70'A = CAN''T BILL - JOB HAS UNAPPROVED ESTIMATE.'             
         DC    CL15'UNAPP EST'                                                  
         DC    AL1(10)                                                          
         DC    AL1(JXMTERR)                                                     
*                                                                               
         DC    CL1'B',CL40'JOB BALANCE GREATER THAN ''NNN'''                    
         DC    CL70'B = WARNING - JOB HAS BALANCE GREATER THAN ''NNN''.X        
               '                                                                
         DC    CL15'BAL > '                                                     
         DC    AL1(11)                                                          
         DC    AL1(JXMTERR)                                                     
*                                                                               
         DC    CL1'C',CL40'WARNING-CHARGES+ORDERS EXCEED ESTIMATE'              
         DC    CL70'C = WARNING - CHARGES PLUS ORDERS EXCEED ESTIMATE.'         
         DC    CL15'CHGS+PO''S > EST'                                           
         DC    AL1(12)                                                          
         DC    AL1(JXMTWARN)                                                    
*                                                                               
         DC    CL1'D',CL40'WARNING - JOB HAS HELD INVOICES'                     
         DC    CL70'D = WARNING - JOB HAS HELD INVOICES.'                       
         DC    CL15'HELD INV'                                                   
         DC    AL1(13)                                                          
         DC    AL1(JXMTWARN)                                                    
*                                                                               
         DC    CL1'E',CL40'CAN''T BILL - MISSING USER FIELD'                    
         DC    CL70'E = CAN''T BILL -  MISSING USER FIELD'                      
         DC    CL15'NEED USER FLD'                                              
         DC    AL1(14)                                                          
         DC    AL1(JXMTERR)                                                     
*                                                                               
         DC    CL1'F',CL40'WARNING-ADDITIONAL R''S AWAITING APPROVAL'           
         DC    CL70'F = WARNING - ADDITIONAL REVISIONS AWAITING APPROVAX        
               L'                                                               
         DC    CL15'ADD''L UNAPP R''S'                                          
         DC    AL1(15)                                                          
         DC    AL1(JXMTWARN)                                                    
*                                                                               
         DC    CL1'G',CL40'CAN''T BILL - BILLING SELECT SET TO NO'              
         DC    CL70'G = CAN''T BILL - BILLING SELECT SET TO NO'                 
         DC    CL15'BILL SELECT=NO'                                             
         DC    AL1(16)                                                          
         DC    AL1(JXMTERR)                                                     
*                                                                               
         DC    CL1'H',CL40'CAN''T BILL - OVER ESTIMATE MAX AMOUNT'              
         DC    CL70'H = CAN''T BILL-OVER EST MAX AMT-EXCEPT WHEN %EST,SX        
               PEC AMT,EST NOT REQ''D'                                          
         DC    CL15'OVER EST $'                                                 
         DC    AL1(17)                                                          
         DC    AL1(JXMTERR)                                                     
*                                                                               
         DC    CL1'I',CL40'WARNING - OVER ESTIMATE MAX PCT-GROSS'               
         DC    CL70'I = WARNING-OVER EST MAX PCT IN GROSS DOLLARS'              
         DC    CL15'OVER GRS EST%'                                              
         DC    AL1(18)                                                          
         DC    AL1(JXMTWARN)                                                    
*                                                                               
         DC    CL1'J',CL40'CAN''T BILL-LINKED STUDIO JOB, INVALID BT'           
         DC    CL70'J = CAN''T BILL - INVALID BILL TYPE FOR STUDIO JOB X        
               WITH LINK.'                                                      
         DC    CL15'INVALID BT'                                                 
         DC    AL1(19)                                                          
         DC    AL1(JXMTERR)                                                     
*                                                                               
         DC    CL1'K',CL40'CAN''T BILL - STUDIO JOB IS MISSING LINK'            
         DC    CL70'K = CAN''T BILL - STUDIO JOB IS MISSING LINK.'              
         DC    CL15'MISSING LINK'                                               
         DC    AL1(20)                                                          
         DC    AL1(JXMTERR)                                                     
*                                                                               
         DC    CL1'L',CL40'CAN''T RETAIL BILL STUDIO JOB      '                 
         DC    CL70'L = CAN''T BILL RETAIL FOR STUDIO JOB WITH LINK.'           
         DC    CL15'RETAIL STUDIO'                                              
         DC    AL1(21)                                                          
         DC    AL1(JXMTERR)                                                     
*                                                                               
         DC    CL1'M',CL40'CAN''T BILL - MISSING ASP ACCOUNT  '                 
         DC    CL70'M = CAN''T BILL - MISSING ACCOUNT(S) FOR ADMINISTRAX        
               TIVE SURCHARGE.'                                                 
         DC    CL15'NO ASP ACCT'                                                
         DC    AL1(22)                                                          
         DC    AL1(JXMTERR)                                                     
*                                                                               
         DC    CL1'N',CL40'CAN''T BILL - MISSING ASP WORKCODE '                 
         DC    CL70'N = CAN''T BILL - MISSING WORKCODE FOR ADMINISTRATIX        
               VE SURCHARGE.'                                                   
         DC    CL15'NO ASP WC'                                                  
         DC    AL1(23)                                                          
         DC    AL1(JXMTERR)                                                     
*                                                                               
         DC    CL1'O',CL40'CAN''T BILL-ACTLS EXCEED CE,ADD''L UNAPP R'          
         DC   CL70'O = CAN''T BILL - ACTUALS EXCEED MAX CE, ADDITIONAL X        
               R''S NEED APPROVAL.'                                             
         DC    CL15'OVER CE+UNAPP R'                                            
         DC    AL1(24)                                                          
         DC    AL1(JXMTERR)                                                     
*                                                                               
*&&DO                                                                           
         DC    CL1'P',CL40'CAN''T BILL - OVER HR EST MAX AMOUNT'                
         DC    CL70'P = CAN''T BILL - OVER HR EST MAX AMOUNT - NEEDS NEX        
               W HIGHEST REVISION.'                                             
         DC    CL15'NEEDS NEW HR'                                               
         DC    AL1(25)                                                          
         DC    AL1(JXMTERR)                                                     
*&&                                                                             
*                                                                               
         DC    CL1'P',CL40'CAN''T BILL - JOB IS NOT FUNDED'                     
         DC    CL70'P = CAN''T BILL - JOB IS NOT FUNDED'                        
         DC    CL15'NOT FUNDED'                                                 
         DC    AL1(26)                                                          
         DC    AL1(JXMTERR)                                                     
         DC    X'FF'                                                            
         EJECT ,                                                                
         SPACE 1                                                                
JXWORKD  DSECT                                                                  
SVAREC   DS    A                   ADDRESS OF ACCOUNT WHILE READING TRN         
APARMS   DS    A                                                                
SAVERE   DS    F                                                                
JXDADISP DS    H                                                                
JXDUB    DS    D                                                                
JXFULL   DS    F                                                                
JXPACK   DS    PL8                                                              
JXWORK   DS    XL64                                                             
OVERAMT  DS    PL4                 MAXIMUM AMOUNT OVER                          
ALNKEL   DS    A                   ADDRESS OF LNK ELEMENT OR X'00'S             
BILFLTSW DS    C                   BILLING SWITCH INDICATOR                     
PL13     DS    PL13                                                             
THISCULA DS    CL(L'ACTKCULA)      SAVED CURRENT CPY/UN/LDG/ACT                 
OVEREST  DS    PL6                                                              
JBDATE   DS    CL3                                                              
JBSTRT   DS    CL3                                                              
JBEND    DS    CL3                                                              
JBSTAT   DS    CL1                                                              
ELCODE   DS    CL1                                                              
PEREST   DS    PL4                                                              
*                                                                               
UNBILLED DS    PL8                                                              
BILLCOM  DS    PL8                                                              
CASHD    DS    PL6                                                              
*                                                                               
APPROVED DS    CL1                                                              
*                                                                               
OVER     DS    PL8                                                              
BIGEST   DS    PL8                 LARGEST BILL ALLOWED BY ESTIMATE             
BIGGREST DS    PL8                 LARGEST GROSS BILL ALLOWED                   
BIGHREST DS    PL8                 LARGEST HR ESTIMATE ALLOWED                  
*                                                                               
BALNC    DS    PL6                                                              
*                                                                               
DRAMT    DS    PL8                 SINGLE TRANSACTION DR AMOUNT                 
DRBILLED DS    PL8                                                              
DROLDMOS DS    PL2                 MOS OF TRANACTION                            
*                                                                               
FNDERSW  DS   XL1                  FOUND   ERROR SWITCH                         
FNDER1   EQU  X'80'                . FOUND ERROR 1                              
FNDERF   EQU  X'40'                . FOUND ERROR F                              
FNDERH   EQU  X'20'                . FOUND ERROR H                              
*                                                                               
JXKEY    DS    CL(ACCKLEN)                                                      
JXIO     DS    CL(ACCKLEN+2000)                                                 
*                                                                               
JXWORKLN EQU   *-JXWORKD                                                        
         EJECT ,                                                                
         SPACE 1                                                                
GOBLOCKD DSECT                                                                  
* ACGOBLOCK                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGOBLOCK                                                      
         PRINT ON                                                               
         SPACE 3                                                                
GOXBLKD  DSECT                                                                  
* ACGOXBLOCK                                                                    
         PRINT OFF                                                              
       ++INCLUDE ACGOXBLOCK                                                     
         PRINT ON                                                               
         SPACE 3                                                                
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
* ACREPWORKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE ACREPWORKD                                                     
         PRINT ON                                                               
* ACGENMODES                                                                    
         PRINT OFF                                                              
       ++INCLUDE ACGENMODES                                                     
         PRINT ON                                                               
* ACMASTD                                                                       
         PRINT OFF                                                              
       ++INCLUDE ACMASTD                                                        
         PRINT ON                                                               
* DDREPXTRAD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDREPXTRAD                                                     
         PRINT ON                                                               
* DDREPMASTD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDREPMASTD                                                     
         PRINT ON                                                               
         EJECT ,                                                                
         SPACE 1                                                                
JBLOCKD  DSECT                                                                  
       ++INCLUDE ACJOBBLOCK                                                     
         EJECT ,                                                                
         SPACE 1                                                                
       ++INCLUDE ACJOBEXCPD                                                     
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'032ACJOBEXCP 04/15/09'                                      
         END                                                                    
*                                                                               
