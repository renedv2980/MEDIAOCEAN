*          DATA SET REGENBUF   AT LEVEL 002 AS OF 02/15/79                      
*CATALP REGENBUF                                                                
         TITLE 'REGENBUF - REFORMAT K REC (P1) TO BUFFER AREA (P2)'             
* THIS ROUTINE CREATES A BUCKET PER MONTH IN 200-BYTE OUTPUT AREA               
*              P1=A(RCONREC)                                                    
*              P2=A(200-BYTE OUTPUT AREA)                                       
REGENBUF CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 10,REGENBUF                                                      
         USING WORKD,RC                                                         
         L     R8,0(R1)            K REC                                        
         L     R9,4(R1)            OUTPUT BUFFER AREA                           
         USING DCONREC,R8                                                       
         XC    0(200,R9),0(R9)     BUFFER MUST BE 200 BYTES                     
         ST    R1,SAVER1                                                        
         XC    DATE,DATE                                                        
         LA    R9,2(R9)            1ST OUTPUT BUCKET                            
         SR    R5,R5                                                            
         LA    R3,RCONELEM         FIRST K ELEMENT                              
         SR    R4,R4                                                            
R50      IC    R4,1(R3)            ELEM LEN                                     
         AR    R3,R4               NEXT ELEM                                    
*                                                                               
         CLI   0(R3),0             LAST?                                        
         BNE   R100                                                             
         LTR   R5,R5                                                            
         BNZ   R250                                                             
R75      L     R1,SAVER1                                                        
         L     R4,4(R1)            OUTPUT                                       
         SR    R9,R4               GET BUFFER LENGTH                            
         STH   R9,DUB                                                           
         MVC   0(2,R4),DUB         BUFFER LENGTH                                
         XMOD1 1                                                                
*                                                                               
R100     CLI   0(R3),3             K BUCKET?                                    
         BNE   R50                                                              
* BUCKET FOUND                                                                  
         CLC   DATE,2(R3)          SAME MONTH?                                  
         BNE   R200                                                             
* ADD AMOUNT                                                                    
R150     MVC   DUB(4),6(R3)        AMOUNT                                       
         A     R5,DUB                                                           
         MVC   DATE,2(R3)          SAVE DATE                                    
         B     R50                                                              
* NEW MONTH                                                                     
R200     LTR   R5,R5                                                            
         BZ    R150                                                             
* CREATE NEW BUCKET                                                             
R250     MVC   0(2,R9),=X'030E'    ELEM CODE + LEN                              
         MVC   2(2,R9),DATE                                                     
         ST    R5,DUB                                                           
         MVC   6(4,R9),DUB                                                      
         SR    R5,R5                                                            
         LA    R9,14(R9)           NEXT OUTPUT                                  
         CLI   0(R3),0             LAST?                                        
         BE    R75                                                              
         B     R150                                                             
*                                                                               
*                                                                               
WORKD    DSECT                                                                  
DUB      DS    D                                                                
DATE     DS    H                                                                
SAVER1   DS    F                                                                
DCONREC  DSECT                                                                  
       ++INCLUDE REGENCON                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002REGENBUF  02/15/79'                                      
         END                                                                    
