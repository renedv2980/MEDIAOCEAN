*          DATA SET DELDTPTR   AT LEVEL 013 AS OF 03/07/16                      
*PROCESS USING(WARN(15))                                                        
*CATALP DELDTPTR                                                                
LDTPTR   TITLE '-   DEMOS - ACTIVE/PASSIVE POINTER TEST'                        
*        PARAMS VIA R1                                                          
         SPACE 1                                                                
*        XL1   X'00'        RETURN  X'FF'=SKIP KEY REQUIRED                     
*        AL3   A(RECORD)        OR  X'FE'=DO NOT DUMP RECORD                    
         SPACE 1                                                                
* DEIS OBSERVES: P2 IS NEVER REFERENCED IN THIS MODULE! (MAR/2016)              
*        XL1   X'00'        RETURN  X'00'=ACTIVE   (UNUSED)                     
*        AL3   A(SKIP KEY)      OR  X'FF'=PASSIVE                               
         SPACE 1                                                                
*        AL4   A(SELECTIVE PERIOD)                                              
         SPACE 1                                                                
*        AL4   A(BTYPE INDICATOR)                                               
         SPACE 1                                                                
*        AL4   A(MEDIA INDICATOR)                                               
         SPACE 2                                                                
LDTPTR   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,DELDTPTR                                             
         USING WORKD,RC                                                         
         LR    R2,R1               R2=A(PARAMETER LIST)                         
         L     R3,0(R2)            R3=A(RECORD)                                 
*                                                                               
         CLI   FIRST,0             TEST FIRST TIME                              
         BNE   TP100                                                            
         MVI   FIRST,1                                                          
*                                                                               
         LM    R4,R6,8(R2)         R4=A(SELECT PERIOD)                          
*                                  R5=A(BTYPE INDICATOR)                        
*                                  R6=A(MEDIA INDICATOR)                        
*                                                                               
         OC    0(2,R4),0(R4)       TEST SELECTIVE DUMP REQUESTED                
         BZ    TP10                USE DEFAULT                                  
         MVC   DUB(1),0(R4)        YEAR                                         
         ZIC   R1,1(R4)                                                         
         MH    R1,=H'3'                                                         
         STC   R1,DUB+1            END MONTH                                    
         MVC   HBOOK2,DUB                                                       
         MVC   LBOOK,DUB                                                        
         XC    LBOOK,=X'FFFF'                                                   
         SH    R1,=H'2'                                                         
         STC   R1,DUB+1                                                         
         MVC   LBOOK2,DUB                                                       
         MVC   HBOOK,DUB                                                        
         XC    HBOOK,=X'FFFF'                                                   
*                                                                               
TP10     MVC   BTYPE,0(R5)         BTYPE                                        
         MVC   MEDIA,0(R6)         MEDIA                                        
*                                                                               
         EJECT                                                                  
         USING DMKEY,R3            MARKET NAME RECORDS                          
TP100    CLI   DMCODE,DMCODEQU     C'A' KEYS                                    
         BE    TPDEL                                                            
*                                                                               
         USING MRKEY,R3            KEY MPA FILE 'M' RECORDS                     
         CLI   MRCODE,MRCODEQU                                                  
         BNE   TP200                                                            
         CLI   MRMEDIA,MRMEDEQU                                                 
         BNE   TP200                                                            
         CLC   MRMBOOK,LBOOK2                                                   
         BL    TPOUT                                                            
         CLC   MRMBOOK,HBOOK2                                                   
         BH    TPOUT                                                            
*                                                                               
         CLI   MEDIA,0                                                          
         BE    TPXIT                                                            
         CLI   MEDIA,C'T'          TV                                           
         BE    TP120                                                            
         CLC   MRMEDIA,MEDIA       ANY OTHER MEDIA                              
         BNE   TPOUT                                                            
         B     TPXIT                                                            
*                                                                               
TP120    B     TPXIT               (U.S. MPA FILES)                             
*                                                                               
         SPACE 2                                                                
         USING BSKEY,R3            STATION LIST RECORD                          
TP200    CLI   BSCODE,BSCODEQU                                                  
         BNE   TP400                                                            
         CLI   BSIND,BSINDEQU                                                   
         BNE   TP300                                                            
         CLC   BSBOOK,LBOOK                                                     
         BL    TPOUT                                                            
         CLC   BSBOOK,HBOOK                                                     
         BH    TPOUT                                                            
*                                                                               
         CLI   MEDIA,0                                                          
         BE    TPXIT                                                            
         CLI   MEDIA,C'T'          TV                                           
         BE    TP220                                                            
         CLC   BSMEDIA,MEDIA       ANY OTHER MEDIA                              
         BNE   TPOUT                                                            
         B     TPXIT                                                            
*                                                                               
TP220    CLI   BSMEDIA,C'T'        (U.S.)                                       
         BE    *+8                                                              
         CLI   BSMEDIA,C'P'        (U.S. MPA FILES)                             
         BE    *+8                                                              
         CLI   BSMEDIA,C'C'        (CANADA)                                     
         BNE   TPOUT                                                            
         CLI   BTYPE,X'FF'                                                      
         BNE   TP240                                                            
         CLI   BSBTYP,X'E0'        SPILL ONLY                                   
         BNE   TPOUT                                                            
         B     TPXIT                                                            
TP240    CLI   BSBTYP,X'E0'        EVERYTHING EXCEPT SPILL                      
         BE    TPOUT                                                            
         B     TPXIT                                                            
*                                                                               
         EJECT                                                                  
         USING MLKEY,R3            MARKET LIST RECORD                           
TP300    CLI   MLIND,MLINDEQU                                                   
         BNE   TP400                                                            
         CLC   MLBOOK,LBOOK                                                     
         BL    TPOUT                                                            
         CLC   MLBOOK,HBOOK                                                     
         BH    TPOUT                                                            
*                                                                               
         CLI   MEDIA,0                                                          
         BE    TPXIT                                                            
         CLI   MEDIA,C'T'          TV                                           
         BE    TP320                                                            
         CLC   MLMEDIA,MEDIA       ANY OTHER MEDIA                              
         BNE   TPOUT                                                            
         B     TPXIT                                                            
*                                                                               
TP320    CLI   MLMEDIA,C'T'        (U.S.)                                       
         BE    *+8                                                              
         CLI   MLMEDIA,C'P'        (U.S. MPA FILES)                             
         BE    *+8                                                              
         CLI   MLMEDIA,C'C'        (CANADA)                                     
         BNE   TPOUT                                                            
         CLI   BTYPE,X'FF'                                                      
         BNE   TP340                                                            
         CLI   MLBTYP,X'E0'        SPILL ONLY                                   
         BNE   TPOUT                                                            
         B     TPXIT                                                            
TP340    CLI   MLBTYP,X'E0'        EVERYTHING EXCEPT SPILL                      
         BE    TPOUT                                                            
         B     TPXIT                                                            
*                                                                               
         EJECT                                                                  
         USING DRKEY,R3            DEMO RECORDS                                 
TP400    CLI   DRCODE,DRCODEQU     REGULAR DEMO RECORDS                         
         BE    *+12                                                             
         CLI   DRCODE,C'D'         RADIO DAYPART RECORDS                        
         BNE   TP500                                                            
         CLC   DRBOOK,LBOOK                                                     
         BL    TPOUT                                                            
         CLC   DRBOOK,HBOOK                                                     
         BH    TPOUT                                                            
*                                                                               
         CLI   MEDIA,0                                                          
         BE    TPXIT                                                            
         CLI   MEDIA,C'T'          TV                                           
         BE    TPP420                                                           
         CLC   DRMEDIA,MEDIA       ANY OTHER MEDIA                              
         BNE   TPOUT                                                            
         B     TPXIT                                                            
*                                                                               
TPP420   CLI   DRMEDIA,C'T'        (U.S.)                                       
         BE    *+8                                                              
         CLI   DRMEDIA,C'P'        (U.S. MPA FILE)                              
         BE    *+8                                                              
         CLI   DRMEDIA,C'C'        (CANADA)                                     
         BNE   TPOUT                                                            
         CLI   BTYPE,X'FF'                                                      
         BNE   TP440                                                            
         CLI   DRBTYP,X'E0'        SPILL ONLY                                   
         BNE   TPOUT                                                            
         B     TPXIT                                                            
TP440    CLI   DRBTYP,X'E0'        EVERYTHING EXCEPT SPILL                      
         BE    TPOUT                                                            
         B     TPXIT                                                            
*                                                                               
         EJECT                                                                  
         USING SBKEY,R3            STATION/BOOK LIST RECORDS                    
TP500    CLI   SBCODE,SBCODEQU                                                  
         BNE   TP600                                                            
         CLC   SBBOOK,LBOOK2                                                    
         BL    TPOUT                                                            
         CLC   SBBOOK,HBOOK2                                                    
         BH    TPOUT                                                            
*                                                                               
         CLI   MEDIA,0                                                          
         BE    TPXIT                                                            
         CLI   MEDIA,C'T'          TV                                           
         BE    TP520                                                            
         CLC   SBMEDIA,MEDIA       ANY OTHER MEDIA                              
         BNE   TPOUT                                                            
         B     TPXIT                                                            
*                                                                               
TP520    CLI   SBMEDIA,C'T'        (U.S.)                                       
         BE    *+8                                                              
         CLI   SBMEDIA,C'P'        (U.S. MPA FILE)                              
         BE    *+8                                                              
         CLI   SBMEDIA,C'C'        (CANADA)                                     
         BNE   TPOUT                                                            
         CLI   BTYPE,X'FF'                                                      
         BNE   TP540                                                            
         CLI   SBBTYP,X'E0'        SPILL ONLY                                   
         BNE   TPOUT                                                            
         B     TPXIT                                                            
TP540    CLI   SBBTYP,X'E0'        EVERYTHING EXCEPT SPILL                      
         BE    TPOUT                                                            
         B     TPXIT                                                            
*                                                                               
         EJECT                                                                  
TP600    DS    0H                                                               
*&&DO                                                                           
         USING UKEY,R3             UNIVERSE RECORDS                             
         CLI   UCODE,UCODEQU                                                    
         BNE   TP700                                                            
         CLC   UBOOK,LBOOK                                                      
         BL    TPOUT                                                            
         CLC   UBOOK,HBOOK                                                      
         BH    TPOUT                                                            
*                                                                               
         CLI   MEDIA,0                                                          
         BE    TPXIT                                                            
         CLI   MEDIA,C'T'          TV                                           
         BE    TP620                                                            
         CLC   UMEDIA,MEDIA        ANY OTHER MEDIA                              
         BNE   TPOUT                                                            
         B     TPXIT                                                            
*                                                                               
TP620    CLI   UMEDIA,C'T'         (U.S.)                                       
         BE    *+8                                                              
         CLI   UMEDIA,C'P'         (U.S. MPA FILE)                              
         BE    *+8                                                              
         CLI   UMEDIA,C'C'         (CANADA)                                     
         BNE   TPOUT                                                            
         CLI   BTYPE,X'FF'                                                      
         BNE   TP640                                                            
         CLI   UBTYP,X'E0'         SPILL ONLY                                   
         BNE   TPOUT                                                            
         B     TPXIT                                                            
TP640    CLI   UBTYP,X'E0'         EVERYTHING EXCEPT SPILL                      
         BE    TPOUT                                                            
         B     TPXIT                                                            
*                                                                               
*&&                                                                             
TP700    B     TPXIT               UNKNOWN - KEEP IT                            
*                                                                               
         EJECT                                                                  
TPOUT    MVI   0(R2),X'FE'         SET RECORD NOT SELECTED                      
         B     TPXIT                                                            
*                                                                               
         SPACE 2                                                                
TPDEL    MVI   0(R2),X'FF'         SET TO DELETE RECORD                         
         B     TPXIT                                                            
*                                                                               
         SPACE 2                                                                
TPXIT    XIT1                                                                   
*                                                                               
         SPACE 4                                                                
         LTORG                                                                  
         SPACE 1                                                                
FIRST    DC    X'00'                                                            
LBOOK    DC    X'0000'                                                          
HBOOK    DC    X'FFFF'                                                          
LBOOK2   DC    X'0000'                                                          
HBOOK2   DC    X'FFFF'                                                          
         SPACE 1                                                                
BTYPE    DC    X'00'               FF=SPILL ONLY   01=EVERYTHING ELSE           
         SPACE 1                                                                
MEDIA    DC    X'00'               T OR R                                       
         SPACE 1                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
WORKX    EQU   *                                                                
         SPACE 1                                                                
* DEDEMFILE                                                                     
* DEMPAFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE DEDEMFILE                                                      
       ++INCLUDE DEMPAFILE                                                      
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'013DELDTPTR  03/07/16'                                      
         END                                                                    
