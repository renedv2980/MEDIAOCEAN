*          DATA SET DDSCROUT   AT LEVEL 001 AS OF 03/31/77                      
*CATALP SCROUT                                                                  
         TITLE 'SCROUT - OUTPUT A SCREEN FROM A CONTROL FILE RECORD'            
* PARA1  CL1   UNP FIELD FILLER                                                 
*        AL3   A(CONTROL FILE SCREEN RECORD)                                    
* PARA2  CL1   PRT FIELD FILLER                                                 
*        AL3   A(TWA)                                                           
* PARA3  XL1   ERROR RETURN VALUE ZERO=OK ELSE GETMSG NUMBER                    
*        AL3   A(FIRST TWA FIELD) RETURN A(NEXT TWA FIELD)                      
* PARA4  AL4   A(PROFILE) RETURN WITH SCREEN MAP OVERWRITE                      
*                                                                               
* SCREEN MAP HAS ONE THREE BYTE ENTRY PER FIELD WITH FORMAT                     
*        XL1   FIELD NUMBER                                                     
*        BL4   FIELD TYPE                                                       
*        BL12  FIELD HEADER DISPLACEMENT FROM START OF TWA                      
         SPACE 2                                                                
SCROUT   CSECT                                                                  
         NMOD1 040,**SCRO**                                                     
         USING WRKD,RC                                                          
         EJECT                                                                  
         MVI   ERR,0                                                            
         LM    R2,R5,0(R1)         R2=A(SCREEN RECORD)                          
         LA    R3,0(R3)            R3=A(TWA)                                    
         USING CTSREC,R2           R4=A(NEXT TWA FIELD)                         
         SR    R7,R7               R5=A(PROFILE)                                
         LA    R2,CTSDATA                                                       
         SPACE 2                                                                
SO1      CLI   0(R2),0             SEARCH FOR PROFILE/FIELD                     
         BE    ERR1                NO PROFILE/FIELDS                            
         CLI   0(R2),X'52'                                                      
         BE    SO3                                                              
         CLI   0(R2),X'54'                                                      
         BE    SO4                                                              
SO2      IC    R7,1(R2)                                                         
         AR    R2,R7                                                            
         B     SO1                                                              
         SPACE 2                                                                
SO3      CLI   0(R5),X'52'         PROFILE ELEMENT                              
         BE    *+10                                                             
         MVC   0(256,R5),0(R2)     USE RECORD PROFILE                           
         B     SO2                                                              
         SPACE 2                                                                
SO4      CLI   0(R5),X'52'                                                      
         BNE   ERR1                NO PROFILE                                   
         USING CTFLDD,R2           R2=A(FIELD DEFN EL)                          
         USING CTSPRD,R5                                                        
         SPACE 2                                                                
SO5      IC    R7,CTSPRROW         INITIALISE ROW & COLUMN                      
         STH   R7,ROW                                                           
         XC    COL,COL                                                          
         MVC   BA,CTSPRBA                                                       
         XC    PROF,PROF                                                        
         IC    R7,CTSPRLEN                                                      
         LA    RE,0(R7,R5)                                                      
         MVC   0(2,RE),=X'FFFF'    SET END OF PROFILE                           
         LA    R5,CTSPRCTL-2                                                    
         SR    RF,RF                                                            
         SPACE 2                                                                
SO6      LA    R5,2(R5)            TRANSLATE PROFILE TO TABLE                   
         CLI   0(R5),X'FF'                                                      
         BE    SO7                                                              
         IC    R7,0(R5)            R7=FIELD TYPE * 16                           
         LA    RE,PROF(R7)                                                      
         IC    RF,0(RE)            BUMP INDEX                                   
         LA    RF,1(RF)                                                         
         STC   RF,0(RE)                                                         
         AR    RE,RF                                                            
         MVC   0(1,RE),1(R5)       STORE COLUMN DEFAULT                         
         CLI   0(RE),0                                                          
         BNE   *+8                                                              
         MVI   0(RE),X'FF'         SET IGNORE FIELD VALUE                       
         B     SO6                                                              
SO7      L     R5,12(R1)           R5=A(SCREEN MAP ENTRY)                       
         DROP  R5                                                               
         SPACE 2                                                                
SOA      SR    R7,R7               R2=A(FIELD DEFN ELEMENT)                     
         IC    R7,CTFLDTYP                                                      
         LA    RE,PROF(R7)         RE=A(FLD TYPE ROW)                           
         SR    RF,RF                                                            
         IC    RF,0(RE)            RF=LAST USED INDEX                           
         LTR   RF,RF                                                            
         BZ    SOP                 FIELD NOT IN PROFILE                         
         LA    R8,1(RE,RF)                                                      
         CLI   0(R8),0                                                          
         BNE   SOB                                                              
         MVI   0(RE),1             NO NEXT USE FIRST COL                        
         IC    RF,1(RE)                                                         
         B     SOC                                                              
SOB      IC    RF,0(R8)            USE NEXT COL                                 
         IC    R7,0(RE)            BUMP INDEX                                   
         LA    R7,1(R7)                                                         
         STC   R7,0(RE)                                                         
SOC      CH    RF,=H'80'                                                        
         BH    SOP                 IGNORE FIELD                                 
         CLI   CTFLDCOL,0                                                       
         BE    *+8                                                              
         IC    RF,CTFLDCOL         USE COL OVERRIDE                             
         SPACE 2                                                                
SOD      LH    RE,ROW              RE=ROW NUMBER                                
         CH    RF,COL              RF=COL NUMBER                                
         BH    SOE                                                              
         LA    RE,1(RE)            BUMP ROW IF LEQ LAST COL                     
         STH   RE,ROW                                                           
SOE      STH   RF,COL                                                           
         BCTR  RE,0                                                             
         MH    RE,=H'80'                                                        
         AR    RF,RE                                                            
         BCTR  RF,0                                                             
         STH   RF,ADR              STORE SCREEN DISPLACEMENT                    
         SPACE 2                                                                
SOF      XC    LEN,LEN             CHECK FIT ON SCR & TWA                       
         MVC   LEN+1(1),CTFLDLN                                                 
         NI    LEN+1,X'7F'                                                      
         LH    RE,LEN                                                           
         AR    RE,RF                                                            
         CH    RE,=H'1920'                                                      
         BNL   ERR2                SCREEN DISP TOO BIG                          
         LA    RE,2301(R3)                                                      
         LA    RF,8(R4)            RE=END OF TWA                                
         AH    RF,LEN              RF=END OF FIELD                              
         CR    RE,RF                                                            
         BL    ERR3                TOO BIG FOR TWA                              
         SPACE 2                                                                
SOG      XC    0(8,R4),0(R4)       BUILD FLD HDR IN TWA                         
         LH    RE,LEN                                                           
         STC   RE,7(R4)                                                         
         LA    RE,8(RE)                                                         
         STC   RE,0(R4)                                                         
         MVC   1(1,R4),CTFLDAT                                                  
         NI    1(R4),X'3F'                                                      
         MVC   2(2,R4),ADR                                                      
         MVC   6(1,R4),CTFLDAT                                                  
         NI    6(R4),X'C0'                                                      
         LA    R4,8(R4)            BUMP TWA POINTER                             
         SPACE 2                                                                
SOH      MVC   0(1,R4),0(R1)       PAD WITH UNP FILLER                          
         TM    CTFLDAT,X'20'                                                    
         BZ    *+10                                                             
         MVC   0(1,R4),4(R1)       PAD WITH PROT FILLER                         
         LH    RF,LEN                                                           
         SH    RF,=H'2'                                                         
         BM    SOK                                                              
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   1(0,R4),0(R4)                                                    
         SPACE 2                                                                
SOK      LA    RF,CTFLDNAR         MOVE NON REPEAT DATA                         
         SR    RF,R2                                                            
         IC    RE,CTFLDLEN                                                      
         SR    RE,RF                                                            
         CLI   CTFLDNAR,0                                                       
         BE    SOM                                                              
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),CTFLDNAR                                                 
         SPACE 2                                                                
SOL      TM    CTFLDLN,X'80'       MOVE REPEAT DATA                             
         BZ    SOM                                                              
         LA    RF,1(RE,R4)                                                      
         LA    R0,2(RE)                                                         
         LH    RE,LEN                                                           
         SR    RE,R0               REMAINING LENGTH -1                          
         BM    SOM                                                              
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RF),0(R4)       REPEAT MOVE FIRST STRING                     
         SPACE 2                                                                
SOM      LR    RE,R4               BUILD SCREEN MAP ENTRY                       
         SH    RE,=H'8'                                                         
         SR    RE,R3               RE=TWA DISP OF FLD HDR                       
         SRDL  RE,12                                                            
         IC    RE,CTFLDTYP                                                      
         SRL   RE,4                                                             
         SRDL  RE,20                                                            
         STH   RF,DUB                                                           
         MVC   0(1,R5),CTFLDNUM                                                 
         MVC   1(2,R5),DUB                                                      
         LA    R5,3(R5)            BUMP SCREEN MAP POINTER                      
         AH    R4,LEN              BUMP TWA POINTER                             
         SPACE 2                                                                
SOP      SR    RF,RF               BUMP TO NEXT FLD DEFN EL                     
         IC    RF,CTFLDLEN                                                      
         AR    R2,RF                                                            
         CLI   0(R2),X'54'                                                      
         BE    SOA                                                              
         CLI   0(R2),0                                                          
         BNE   SOP                                                              
         SPACE 2                                                                
SOX      ST    R4,8(R1)            RETURN NEXT TWA ADR                          
         MVC   8(1,R1),ERR         RETURN ERROR INFO                            
         XC    0(3,R5),0(R5)       SET END OF SCREEN MAP                        
         XC    0(3,R4),0(R4)       SET END OF TWA                               
         TM    BA,X'80'                                                         
         BZ    *+8                                                              
         MVI   1(R4),1             SET BEFORE                                   
         TM    BA,X'40'                                                         
         BZ    *+8                                                              
         MVI   2(R4),1             SET AFTER                                    
         SPACE 2                                                                
SOEXIT   XMOD1 1                                                                
         SPACE 2                                                                
ERR1     MVI   ERR,NPF             NO PROFILE/FIELDS                            
         B     ERRX                                                             
ERR2     MVI   ERR,EOS             END OF SCREEN                                
         B     ERRX                                                             
ERR3     MVI   ERR,EOT             END OF TWA                                   
         B     ERRX                                                             
ERRX     B     SOX                                                              
         EJECT                                                                  
         LTORG                                                                  
         SPACE 2                                                                
NPF      EQU   22                                                               
EOS      EQU   23                                                               
EOT      EQU   24                                                               
         SPACE 2                                                                
WRKD     DSECT                                                                  
DUB      DS    D                                                                
ROW      DS    H                                                                
COL      DS    H                                                                
ADR      DS    H                                                                
LEN      DS    H                                                                
ERR      DS    C                                                                
BA       DS    C                                                                
PROF     DS    CL256                                                            
         EJECT                                                                  
       ++INCLUDE CTGENREQS                                                      
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'001DDSCROUT  03/31/77'                                      
         END                                                                    
