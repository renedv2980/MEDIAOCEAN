*          DATA SET BUEDRULESS AT LEVEL 032 AS OF 05/01/02                      
*CATALP BUEDRULE                                                                
         SPACE 2                                                                
**********************************************************************          
* SUBROUTINE TO EDIT RULES DATA                                      *          
*                                                                    *          
* P1(4) =  A(GEND)                                                   *          
*                                                                    *          
* P2(4) =  A(FIRST RULES FLDHDR)                                     *          
*                                                                    *          
* P3(4) =  A(LAST RULES FLDHDR)                                      *          
*                                                                    *          
* IO2 IS USED AS SCANNER TABLE AREA                                  *          
*                                                                    *          
* IO2+1000(1000) IS USED TO CONSTRUCT RULES ELEMENTS                 *          
**********************************************************************          
         TITLE 'BUEDRULES - EDIT RULES'                                         
EDRULES  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,EDRULES,RA,RR=R2                                     
*                                                                               
         LR    R8,RC                                                            
         USING WORKD,R8                                                         
         ST    R2,RELO                                                          
         MVC   PARAMS,0(R1)        SAVE PARAMETERS                              
*                                                                               
         L     RC,AGEND                                                         
         USING GEND,RC                                                          
*                                                                               
         L     R9,ASYSD                                                         
         USING SYSD,R9                                                          
*                                                                               
         L     RF,AIO2             CLEAR SCANNER BLOCK                          
         BAS   RE,CLEAR                                                         
         BAS   RE,CLEAR            AND CLEAR ELEMENT AREA                       
*                                                                               
         L     R2,ASTFLD           GET INPUT FIELD HEADER ADDRESS               
         ST    R2,FADDR            SET FIELD HEADER ADDR FOR ERROR              
         LA    R1,8(R2)            SET POINTER TO INPUT STRING                  
         STCM  R1,7,FLAST                                                       
         GOTO1 SCANNER,DMCB,(30,(R2)),(16,AIO2),0                               
*                                                                               
         L     R4,AIO2             POINT TO SCANNER TABLE                       
         MVI   ERROR,MISSING                                                    
         CLI   0(R4),0             EXTRACT ID MUST BE PRESENT                   
         BE    EDERR                                                            
         CLI   1(R4),0             AND EXTRACT DATA MUST BE PRESENT             
         BNE   EDT1                                                             
*                                                                               
         MVI   ERROR,INVALID                                                    
         CLI   0(R4),5             ONLY EXCEPTION IS 'OTHER'                    
         BNE   EDERR                                                            
         CLC   =C'OTHER',12(R4)                                                 
         BNE   EDERR                                                            
         L     R6,AIO2             BUILD DUMMY RULE ELEMENT                     
         LA    R6,1000(R6)                                                      
         USING BURULD,R6                                                        
         MVI   BURULEL,BURULELQ                                                 
         MVI   BURULEN,3                                                        
         MVI   BURULTYP,RUDUMMY                                                 
         B     EXIT                                                             
*                                                                               
EDT1     MVI   ERROR,INVALID                                                    
         CLI   0(R4),2             AT LEAST 2 ID CHARS                          
         BL    EDERR                                                            
*                                                                               
EDT2     MVI   ERROR,INVALID                                                    
         ZIC   RE,0(R4)            GET RULE ID LEN                              
         STC   RE,INPLEN           SAVE INPUT LENGTH                            
         BCTR  RE,0                                                             
         LA    R1,EDTTAB                                                        
         USING EDTTABD,R1                                                       
         LA    R0,(EDTTABX-EDTTAB)/L'EDTTAB                                     
*                                                                               
EDT4     CLC   INPLEN,EDTMINL                                                   
         BL    *+12                                                             
         EX    RE,EDTCLC                                                        
         BE    EDT6                                                             
         LA    R1,L'EDTTAB(R1)                                                  
         BCT   R0,EDT4                                                          
         MVC   XTRA(13),=C'BAD RULE TYPE'                                       
         B     EDERR                                                            
EDTCLC   CLC   12(0,R4),EDTNAME    * EXECUTED *                                 
*                                                                               
EDT6     SR    RF,RF                                                            
         ICM   RF,7,FLAST          GET INPUT POINTER                            
         ZIC   R0,0(R4)            R0=L'RULE KEYWORD                            
         AR    RF,R0                                                            
         LA    RF,1(RF)            ADD 1 FOR FIELD SEPARATOR (=)                
         STCM  RF,7,FLAST                                                       
*                                                                               
EDT7     L     R6,AIO2                                                          
         LA    R6,1000(R6)         POINT TO FIRST ELEM                          
EDT8     CLI   0(R6),0                                                          
         BE    EDT10                                                            
         ZIC   R0,1(R6)                                                         
         AR    R6,R0                                                            
         B     EDT8                                                             
*                                                                               
         USING BURULD,R6                                                        
*                                                                               
EDT10    MVI   BURULEL,BURULELQ                                                 
         MVC   BURULTYP,EDTTYPE                                                 
         MVI   BURULEN,4                                                        
         LA    R7,BURULE                                                        
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,7,EDTROUT                                                     
         A     RF,RELO                                                          
         BR    RF                  BRANCH TO SPECIAL EDIT ROUTINE               
*                                                                               
EDT12    OC    0(2,R4),0(R4)       TEST LAST SCANNER FIELD                      
         BNZ   EDT2                NO - CONTINUE                                
*                                                                               
EXIT     XIT1                                                                   
*                                                                               
EDERR    GOTO1 VCURSERR,PARAS,0                                                 
         EJECT                                                                  
* EDIT SYSTEM/MEDIA - 3 BYTE OUTPUT - SYSNUM/SYSID/MEDIA CODE                   
*                                                                               
EDSYS    DS    0H                                                               
         MVI   ERROR,SYSERR                                                     
         ZIC   RE,1(R4)            GET DATA LEN                                 
         LA    RF,22(R4)           POINT TO SYS/MED                             
*                                                                               
EDSYS2   CLI   0(RF),C'/'          TEST FOR SEPARATOR                           
         BE    EDSYS4                                                           
         LA    RF,1(RF)                                                         
         BCT   RE,EDSYS2                                                        
         B     EDERR                                                            
*                                                                               
EDSYS4   STM   RE,RF,DUB           SAVE LEN AND POINTER                         
         LA    RE,22(R4)           POINT TO INPUT                               
         SR    RF,RE               GET LENGTH OF 'SYS'                          
         BNP   EDERR                                                            
         STC   RF,INPLEN           SAVE INPUT LENGTH                            
         LR    RE,RF                                                            
         BCTR  RE,0                SET FOR EX                                   
         LA    RF,22(R4)           AND POINT TO INPUT AGAIN                     
*                                                                               
EDSYS6   LA    R1,SYSTAB                                                        
         USING SYSTABD,R1                                                       
         LA    R0,(SYSTABX-SYSTAB)/L'SYSTAB                                     
*                                                                               
EDSYS8   CLC   INPLEN,SYSTMINL     TEST IF INPUT L.T. MIN LEN                   
         BL    EDSYS9                                                           
         EX    RE,EDSYSCLC                                                      
         BE    EDSYS10                                                          
EDSYS9   LA    R1,L'SYSTAB(R1)                                                  
         BCT   R0,EDSYS8                                                        
         B     EDERR                                                            
*                                                                               
EDSYS10  DS    0H                                                               
         MVC   BURULE(2),SYSTSN    EXTRACT SYSTEM NUMBER/ALPHA ID               
         MVI   BURULEN,7                                                        
         MVC   BYTE,SYSTB          EXTRACT SYSTEM BIT VALUE                     
         SPACE 1                                                                
* EDIT MEDIA CODE WHICH MUST FOLLOW /'                                          
*                                                                               
EDSYS11  MVI   ERROR,MEDERR                                                     
         LM    RE,RF,DUB           GET POINTER/LENGTH                           
         LA    RF,1(RF)            POINT BEYOND '/'                             
         BCTR  RE,0                ADJUST LEN FOR '/'                           
         LTR   RE,RE                                                            
         BZ    EDERR                                                            
         STC   RE,INPLEN           SAVE INPUT LENGTH                            
         MVI   FNDX,2              SET FIELD SUB-INDEX IF ERROR                 
         BCTR  RE,0                SET FOR EX                                   
*                                                                               
         CLC   =X'06D7',BURULE     TEST ACC PRODUCTION                          
         BNE   *+12                                                             
         BAS   RE,PROMED           EDIT FOR PRODUCTION MEDIA                    
         B     EDSYS22                                                          
*                                                                               
         CLC   =X'06E3',BURULE     TEST ACC TALENT                              
         BE    EDSYS11A                                                         
         CLC   =X'04D4',BURULE     TEST MEDLINE                                 
         BE    EDSYS12                                                          
         B     EDSYS13                                                          
*                                                                               
EDSYS11A CLI   INPLEN,3            TEST INPUT LENGTH OF 3                       
         BNE   EDSYS12                                                          
         CLC   0(3,RF),=C'ALL'     ONLY ALLOW ALL                               
         BE    EDSYS22                                                          
         B     EDERR                                                            
*                                                                               
EDSYS12  LTR   RE,RE               ACCEPT ANY ONE CHAR FIELD                    
         BNZ   EDERR                                                            
         MVC   BURULE+2(1),0(RF)   MOVE MEDIA TO RULE                           
         B     EDSYS22                                                          
*                                                                               
EDSYS13  LA    R1,MEDTAB                                                        
         USING MEDTABD,R1                                                       
         LA    R0,(MEDTABX-MEDTAB)/L'MEDTAB                                     
*                                                                               
EDSYS14  CLC   INPLEN,MEDTMINL     TEST IF L.T. MIN LEN                         
         BL    EDSYS15                                                          
         EX    RE,EDSYSCLC                                                      
         BE    EDSYS20                                                          
EDSYS15  LA    R1,L'MEDTAB(R1)                                                  
         BCT   R0,EDSYS14                                                       
         B     EDERR                                                            
*                                                                               
EDSYS20  MVC   BURULE+2(1),MEDTID  MOVE MEDIA TO RULE                           
         NC    BYTE,MEDTVSYS       TEST IF MEDIA IS COMPATIBLE                  
         BZ    EDERR               WITH SYSTEM (ZERO=NO)                        
*                                                                               
EDSYS22  MVI   FNDX,0              CLEAR FIELD SUB-INDEX                        
         BAS   RE,NEXTFLD                                                       
         B     EDERR               IF MORE DATA, ERROR                          
*                                                                               
EDSYSCLC CLC   0(0,R1),0(RF)  *EXECUTED*                                        
         EJECT                                                                  
* SUB-ROUTINE TO EDIT MEDIA SUB-FIELD FOR PRODUCTION SYSTEM                     
*                                                                               
* AT ENTRY, RF=A(SUB-FIELD DATA), INPLEN=L'SUB-FIELD                            
*                                                                               
PROMED   ST    RE,SAVERE                                                        
         CLI   INPLEN,3            FIRST LOOK FOR 'ALL' MEDIA                   
         BNE   *+14                                                             
         CLC   0(3,RF),=C'ALL'                                                  
         BE    PROMEDX                                                          
*                                                                               
         CLI   INPLEN,1            NOW LOOK ONE MEDIA                           
         BNE   PROMED2                                                          
         LR    R1,RF                                                            
         LA    R0,1                                                             
         BAS   RE,TSTAN            TEST FOR ALPHA MEDIA                         
         MVC   BURULE+2(1),0(RF)   SET MEDIA IN SYSTEM RULE                     
         B     PROMEDX             AND EXIT                                     
*                                                                               
PROMED2  ZIC   R0,INPLEN                                                        
         MVI   NEGSW,NO                                                         
         CLI   0(RF),C'*'          TEST FOR NEGATIVE RULE SYMBOL                
         BNE   PROMED4             NO                                           
         MVI   NEGSW,YES                                                        
         LA    RF,1(RF)            BUMP SUB-FIELD DATA POINTER                  
         BCTR  R0,0                AND ADJUST LENGTH                            
*                                                                               
PROMED4  LR    R1,RF                                                            
         BAS   RE,TSTAN            TEST FOR ALPHA MEDIA                         
         ZIC   RE,BURULEN                                                       
         AR    R6,RE               BUMP TO NEXT ELEMENT POSITION                
         MVI   BURULEL,BURULELQ                                                 
         MVI   BURULTYP,RUMED                                                   
         LA    RE,BURULE-BURULEL   OVERHEAD ELEMENT LENGTH                      
         AR    RE,R0               COMPUTE ELEMENT LENGTH                       
         STC   RE,BURULEN                                                       
         LR    R1,R0                                                            
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     PROMED6                                                          
         MVC   BURULE(0),0(RF)     SET DATA IN ELEMENT                          
*                                                                               
PROMED6  CLI   NEGSW,YES           TEST FOR NEGATIVE RULE                       
         BNE   PROMEDX             NO                                           
         LA    RF,BURULE           RF=A(RULE DATA)                              
         NI    0(RF),X'FF'-X'40'   YES-MAKE ALL FILTERS LOWER CASE              
         LA    RF,1(RF)                                                         
         BCT   R0,*-8                                                           
*                                                                               
PROMEDX  L     RE,SAVERE                                                        
         BR    RE                                                               
         EJECT                                                                  
* SYSTAB ENTRIES DESCRIBED BY SYSTABD                                           
*        INPUT STRING                                                           
*        MINIMUM INPUT LENGTH TO RECOGNIZE SYSTEM                               
*        SYSTEM NUMBER (AS IN ID ELEMENT)                                       
*        SYSTEM ID (ALPHA)                                                      
*        SYSTEM BIT VALUE                                                       
SYSTAB   DS    0CL(SYSTABL)                                                     
*                                                                               
*&&US                                                                           
         DC    CL7'SPOT   ',AL1(1),X'02',C'S',AL1(SPOTB)                        
         DC    CL7'NETWORK',AL1(1),X'03',C'N',AL1(NETB)                         
         DC    CL7'NTWK   ',AL1(1),X'03',C'N',AL1(NETB)                         
         DC    CL7'PRINT  ',AL1(3),X'04',C'P',AL1(PRINTB)                       
         DC    CL7'PRNT   ',AL1(3),X'04',C'P',AL1(PRINTB)                       
         DC    CL7'PRODUCT',AL1(3),X'06',C'P',AL1(PRODB)                        
         DC    CL7'TALENT ',AL1(1),X'06',C'T',AL1(TALB)                         
*&&                                                                             
*                                                                               
*&&UK                                                                           
         DC    CL7'MEDLINE',AL1(1),X'04',C'M',AL1(0)                            
         DC    CL7'PRODUCT',AL1(1),X'06',C'P',AL1(0)                            
*&&                                                                             
SYSTABX  EQU   *                                                                
         SPACE 1                                                                
* MEDTAB ENTRIES ARE DESCRIBED BY MEDTABD                                       
*        INPUT STRING                                                           
*        MINIMUM LENGTH FOR RECOGNIZING MEDIA CODE                              
*        MEDIA ID (ALPHA)                                                       
*        VALID SYSTEM BITS FOR MEDIA                                            
*                                                                               
MEDTAB   DS    0CL(MEDTABL)                                                     
*&&US                                                                           
         DC    CL8'TV      ',AL1(2),C'T',AL1(SPOTB)  TV                         
         DC    CL8'RADIO   ',AL1(1),C'R',AL1(SPOTB)  RADIO                      
         DC    CL8'NETRADIO',AL1(4),C'X',AL1(SPOTB)  NTWK RADIO                 
         DC    CL8'NRADIO  ',AL1(2),C'X',AL1(SPOTB)  NTWK RADIO                 
         DC    CL8'COMBO   ',AL1(2),C'C',AL1(SPOTB)  COMBINED (CANADA)          
         DC    CL8'NETWORK ',AL1(3),C'N',AL1(SPOTB+NETB)  NETWORK               
         DC    CL8'NTWK    ',AL1(3),C'N',AL1(SPOTB+NETB)  NETWORK               
         DC    CL8'MAGAZINE',AL1(1),C'M',AL1(PRINTB) MAGAZINE                   
         DC    CL8'MAGS    ',AL1(1),C'M',AL1(PRINTB) MAGAZINE                   
         DC    CL8'NEWS    ',AL1(3),C'N',AL1(PRINTB) NEWSPAPER                  
         DC    CL8'OUTDOOR ',AL1(1),C'O',AL1(PRINTB) OUTDOOR                    
         DC    CL8'SUPPLEM ',AL1(1),C'S',AL1(PRINTB) SUPPLEMENTS                
         DC    CL8'TRADE   ',AL1(2),C'T',AL1(PRINTB) TRADE                      
*&&                                                                             
MEDTABX  EQU   *                                                                
         EJECT                                                                  
* EDIT AGENCY CODE - 2 BYTE EBCDIC                                              
*                                                                               
EDAGY    MVI   ERROR,INVALID                                                    
         CLI   DDS,YES             TEST DDS TERMINAL                            
         BE    EDAGY9                                                           
*&&US                                                                           
         CLC   AGENCY,=C'DF'       TEST FOR DFS                                 
         BE    EDAGY1                                                           
         CLC   AGENCY,=C'SF'       TEST FOR SFSF                                
         BE    EDAGY1                                                           
         CLC   AGENCY,=C'DW'       TEST FOR NEW DFS                             
         BE    EDAGY1                                                           
         CLC   AGENCY,=C'YN'       TEST FOR YNR                                 
         BE    EDAGY2                                                           
         CLC   AGENCY,=C'BP'       TEST FOR BBDO/ATLANTA                        
         BE    EDAGY3                                                           
         CLC   AGENCY,=C'SC'       TEST FOR SCALI                               
         BE    EDAGY4                                                           
         CLC   AGENCY,=C'BD'       TEST FOR BBDO                                
         BE    EDAGY6                                                           
*&&                                                                             
         B     EDERR                                                            
*&&US                                                                           
*                                                                               
EDAGY1   BAS   RE,TSTAGY                                                        
         CLC   22(2,R4),=C'DF'                                                  
         BE    EDAGY10                                                          
         CLC   22(2,R4),=C'SF'                                                  
         BE    EDAGY10                                                          
         CLC   22(2,R4),=C'DW'                                                  
         BE    EDAGY10                                                          
         CLC   22(2,R4),=C'DT'                                                  
         BE    EDAGY10                                                          
         B     EDERR                                                            
*                                                                               
EDAGY2   BAS   RE,TSTAGY                                                        
         CLC   22(2,R4),=C'AR'                                                  
         BE    EDAGY10                                                          
         CLC   22(2,R4),=C'WW'                                                  
         BE    EDAGY10                                                          
         CLC   22(2,R4),=C'YN'                                                  
         BE    EDAGY10                                                          
         B     EDERR                                                            
*                                                                               
EDAGY3   BAS   RE,TSTAGY                                                        
         CLC   22(2,R4),=C'BP'                                                  
         BE    EDAGY10                                                          
         CLC   22(2,R4),=C'BN'                                                  
         BE    EDAGY10                                                          
         B     EDERR                                                            
*                                                                               
EDAGY4   BAS   RE,TSTAGY                                                        
         CLC   22(2,R4),=C'SC'                                                  
         BE    EDAGY10                                                          
         CLC   22(2,R4),=C'SG'                                                  
         BE    EDAGY10                                                          
         B     EDERR                                                            
*&&                                                                             
*                                                                               
EDAGY6   BAS   RE,TSTAGY                                                        
         CLC   22(2,R4),=C'BD'                                                  
         BE    EDAGY10                                                          
         CLC   22(2,R4),=C'BN'                                                  
         BE    EDAGY10                                                          
         B     EDERR                                                            
*                                                                               
EDAGY9   BAS   RE,TSTAGY                                                        
*                                                                               
EDAGY10  MVC   BURULE(LRUAGY),22(R4) EXTRACT AGENCY CODE                        
         MVI   BURULEN,BURULE-BURULD+LRUAGY                                     
         BAS   RE,NEXTFLD                                                       
         B     EDERR                                                            
         SPACE 1                                                                
TSTAGY   MVI   ERROR,AGYERR                                                     
         CLI   1(R4),2             TEST FOR TWO-BYTE CODE                       
         BER   RE                                                               
         B     EDERR                                                            
         SPACE 2                                                                
* EDIT CLIENT - 3 BYTE SPACE FILLED EBCDIC                                      
*                                                                               
EDCLT    DS    0H                                                               
         MVI   ERROR,CLTERR                                                     
         CLI   1(R4),2                                                          
         BL    EDERR                                                            
         CLI   1(R4),5                                                          
         BH    EDERR                                                            
         MVC   BURULE(5),22(R4)    MOVE CLIENT CODE                             
         MVI   BURULEN,9                                                        
         BAS   RE,NEXTFLD                                                       
         B     EDERR               IF MORE DATA, ERROR                          
         SPACE 2                                                                
* EDIT PRODUCT - 3 BYTE SPACE FILLED EBCDIC                                     
*                                                                               
EDPRD    DS    0H                                                               
         MVI   ERROR,PRDERR                                                     
*&&US*&& CLI   1(R4),2             US-PRODUCT MUST BE 2 CHARS                   
*&&UK*&& CLI   1(R4),1             UK-PRODUCT CAN BE 1 CHARACTER                
         BL    EDERR                                                            
         CLI   1(R4),3                                                          
         BH    EDERR                                                            
         MVC   BURULE(3),22(R4)    MOVE PRODUCT CODE                            
         MVI   BURULEN,7                                                        
         BAS   RE,NEXTFLD                                                       
         B     EDERR               IF MORE DATA, ERROR                          
         EJECT                                                                  
       ++INCLUDE BUEDRULCOD                                                     
         EJECT                                                                  
* EDIT JOB (6 EBCDIC)                                                           
*                                                                               
EDJOB    DS    0H                                                               
         LA    R1,22(R4)                                                        
         ZIC   R0,1(R4)                                                         
         BAS   RE,TSTNEG                                                        
*                                                                               
EDJOB2   BAS   RE,TSTJOB                                                        
         MVC   0(6,R7),DUB                                                      
         CLI   NEGSW,YES           TEST FOR NEGATIVE RULE                       
         BNE   *+8                                                              
         NI    0(R7),X'FF'-X'40'   YES                                          
         LA    R0,6                                                             
         BAS   RE,SETLEN                                                        
* TEST FOR MORE INPUT FIELDS                                                    
         BAS   RE,NEXTFLD                                                       
         LA    R1,12(R4)                                                        
         ZIC   R0,0(R4)                                                         
         B     EDJOB2                                                           
*                                                                               
TSTJOB   MVI   ERROR,JOBERR                                                     
         CH    R0,=H'6'                                                         
         BH    EDERR                                                            
         MVC   DUB(6),0(R1)                                                     
         BR    RE                                                               
         EJECT                                                                  
* EDIT WCD (2 EBCDIC)                                                           
*                                                                               
EDWCD    DS    0H                                                               
         LA    R1,22(R4)                                                        
         ZIC   R0,1(R4)                                                         
         BAS   RE,TSTNEG                                                        
*                                                                               
EDWCD2   BAS   RE,TSTWCD                                                        
         MVC   0(2,R7),DUB                                                      
         CLI   NEGSW,YES                                                        
         BNE   *+8                                                              
         NI    0(R7),X'FF'-X'40'                                                
         LA    R0,2                                                             
         BAS   RE,SETLEN                                                        
* TEST FOR MORE INPUT FIELDS                                                    
         BAS   RE,NEXTFLD                                                       
         LA    R1,12(R4)                                                        
         ZIC   R0,0(R4)                                                         
         B     EDWCD2                                                           
*                                                                               
TSTWCD   MVI   ERROR,WCDERR                                                     
         CH    R0,=H'2'                                                         
         BNE   EDERR                                                            
         MVC   DUB(2),0(R1)                                                     
         BR    RE                                                               
         SPACE 1                                                                
* EDIT ACCOUNT FILTERS (1 BYTE EBCDIC)                                          
*                                                                               
EDAFI    LA    R1,22(R4)                                                        
         ZIC   R0,1(R4)                                                         
         MVI   ERROR,INVALID                                                    
         BAS   RE,TSTNEG                                                        
*                                                                               
EDAFI2   CH    R0,=H'1'                                                         
         BNE   EDERR                                                            
         MVC   0(1,R7),0(R1)                                                    
         CLI   NEGSW,YES                                                        
         BNE   *+8                                                              
         NI    0(R7),X'FF'-X'40'                                                
         LA    R0,1                                                             
         BAS   RE,SETLEN                                                        
         BAS   RE,NEXTFLD                                                       
         LA    R1,12(R4)           NEXT IN FILTER LIST                          
         ZIC   R0,0(R4)                                                         
         B     EDAFI2                                                           
         EJECT                                                                  
* INCREMENT RULE LENGTH AND POINTER *                                           
* AND MAKE SURE ELEMENT NOT TOO BIG *                                           
         SPACE 1                                                                
SETLEN   ZIC   RF,BURULEN          BUMP ELEMENT LENGTH                          
         AR    RF,R0                                                            
         STC   RF,BURULEN                                                       
         AR    R7,R0               BUMP ELEMENT POINTER                         
         CH    RF,=H'256'                                                       
         BLR   RE                                                               
         MVI   ERROR,TOOBIG                                                     
         B     EDERR                                                            
         SPACE 2                                                                
* TEST FOR STAR DENOTING NEGATIVE RULE AT START OF STRING                       
* AT ENTRY, R1=A(STRING START), R0=L'STRING                                     
*                                                                               
TSTNEG   MVI   NEGSW,NO                                                         
         CLI   0(R1),STAR                                                       
         BNER  RE                                                               
         MVI   NEGSW,YES                                                        
         LA    R1,1(R1)            INCREMENT STRING POINTER                     
         SH    R0,=H'1'            DECREMENT STRING LENGTH                      
         BZ    EDERR                                                            
         BR    RE                                                               
         SPACE 2                                                                
* TEST FOR ALPHA-NUMERIC FIELD                                                  
* AT ENTRY, R1=A(FIELD), R0=L'FIELD                                             
*                                                                               
TSTAN    STM   R0,R1,DUB                                                        
*                                                                               
TSTAN1   CLI   0(R1),C'A'                                                       
         BL    EDERR                                                            
         CLI   0(R1),C'I'                                                       
         BNH   TSTAN2              CHARACTER IS BETWEEN A-I                     
         CLI   0(R1),C'J'                                                       
         BL    EDERR                                                            
         CLI   0(R1),C'R'                                                       
         BNH   TSTAN2                                                           
         CLI   0(R1),C'S'                                                       
         BL    EDERR                                                            
         CLI   0(R1),C'Z'                                                       
         BNH   TSTAN2                                                           
         CLI   0(R1),C'0'                                                       
         BL    EDERR                                                            
         CLI   0(R1),C'9'                                                       
         BH    EDERR                                                            
*                                                                               
TSTAN2   LA    R1,1(R1)            NEXT CHARACTER IN FIELD                      
         BCT   R0,TSTAN1                                                        
         LM    R0,R1,DUB           RESTORE REGISTERS AT ENTRY                   
         BR    RE                                                               
         SPACE 2                                                                
* ADVANCE TO NEXT SCANNER FIELD                                                 
* IF NO MORE FIELDS, ADVANCE TO NEXT FLDHDR                                     
* IF LENGTH IS SAME AS CURRENT FIELD, CALL SCANNER                              
*  AND CONTINUE EDIT. ELSE EXIT                                                 
         SPACE 1                                                                
NEXTFLD  DS    0H                                                               
         SR    RF,RF                                                            
         ICM   RF,7,FLAST          GET INPUT POINTER                            
         XC    HALF,HALF                                                        
         MVC   HALF+1(1),1(R4)     GET PARAMETER LENGTH                         
         CLI   1(R4),0             TEST FOR SINGLE FIELD                        
         BNE   *+10                NO                                           
         MVC   HALF+1(1),0(R4)     YES-SO USE FIRST FIELD LENGTH                
         AH    RF,HALF                                                          
         LA    RF,1(RF)            ADD 1 FOR FIELD TERMINATOR (,)               
         STCM  RF,7,FLAST                                                       
         LA    R4,52(R4)           BUMP TO NEXT SCANNER FIELD                   
*                                                                               
NEXTFLD2 CLI   1(R4),0             TEST SPLIT FIELD                             
         BNE   EDT1                YES - GO PROCESS NEW KEYWORD                 
         CLI   0(R4),0             TEST MORE DATA THIS FIELD                    
         BNER  RE                  YES - GO EDIT                                
* ADVANCE TO NEXT FLDHDR                                                        
         MVI   DUB,C'N'            SET NOT CONTINUED SWITCH                     
         ZIC   RF,5(R2)            GET INPUT LENGTH                             
         AR    RF,R2               POINT BEYOND LAST INPUT CHAR                 
         BCTR  RF,0                BACK UP TO LAST CHAR                         
         CLI   0(RF),C','          TEST CONTINUED                               
         BNE   *+8                                                              
         MVI   DUB,C'Y'            SET TO ALLOW CONTINUATION                    
*                                                                               
         ZIC   RF,0(R2)                                                         
         AR    RF,R2               POINT TO NEXT FLDHDR                         
         C     RF,AENDFLD          IS THERE ANOTHER FIELD                       
         BH    EXIT                NO - STOP                                    
*                                                                               
         LR    R2,RF               ADVANCE SCREEN POINTER                       
         ST    R2,FADDR            SET NEW FIELD HEADER ADDR                    
         LA    R1,8(R2)            AND RESET INPUT POINTER                      
         STCM  R1,7,FLAST                                                       
         CLI   5(R2),0             TEST FOR INPUT                               
         BE    EXIT                NO - DONE                                    
*                                                                               
         L     RF,AIO2                                                          
         LR    R0,RE               SAVE CALLING REG                             
         BAS   RE,CLEAR            CLEAR SCANNER BLOCK                          
         GOTO1 SCANNER,DMCB,(30,(R2)),(16,AIO2),0                               
         MVI   ERROR,INVALID                                                    
         CLI   4(R1),0             TEST FOR SCANNER ERROR                       
         BE    EDERR                                                            
         LR    RE,R0               RESTORE RETURN POINT                         
         L     R4,AIO2                                                          
*                                                                               
         CLI   1(R4),0             TEST SPLIT FIELD                             
         BNE   EDT2                YES - GO EDIT                                
         CLI   DUB,C'Y'            TEST CONTINUED ALLOWED                       
         BER   RE                  YES - GO ON                                  
         MVI   ERROR,MISSING       NO - MISSING ID                              
         B     EDERR                                                            
         SPACE 2                                                                
* SUB-ROUTINE TO CLEAR 1000 BYTE AREA                                           
* AT ENTRY, RF =A(AREA)                                                         
*                                                                               
CLEAR    ST    RE,SAVEREG                                                       
         LA    RE,4                                                             
         XC    0(250,RF),0(RF)                                                  
         LA    RF,250(RF)                                                       
         BCT   RE,*-10                                                          
         L     RE,SAVEREG                                                       
         BR    RE                                                               
         EJECT                                                                  
* RULE TYPE EDIT ROUTINES                                                       
         SPACE 1                                                                
         CNOP  2,4                                                              
EDTTAB   DS    0XL(EDTTABL)                                                     
         DC    CL4'SYS ',AL1(RUSYS),AL3(EDSYS),AL1(2)                           
         DC    CL4'AGY ',AL1(RUAGY),AL3(EDAGY),AL1(2)                           
         DC    CL4'AGEN',AL1(RUAGY),AL3(EDAGY),AL1(2)                           
         DC    CL4'CLT ',AL1(RUCLT),AL3(EDCLT),AL1(2)                           
         DC    CL4'CLI ',AL1(RUCLT),AL3(EDCLT),AL1(2)                           
         DC    CL4'PRD ',AL1(RUPRD),AL3(EDPRD),AL1(2)                           
         DC    CL4'PROD',AL1(RUPRD),AL3(EDPRD),AL1(2)                           
*                                                                               
       ++INCLUDE BUEDRULTAB                                                     
*                                                                               
         DC    CL4'JOB ',AL1(RUJOB),AL3(EDJOB),AL1(2)                           
         DC    CL4'WCD ',AL1(RUWCD),AL3(EDWCD),AL1(2)                           
         DC    CL4'AF1 ',AL1(RUAF1),AL3(EDAFI),AL1(3)                           
         DC    CL4'AF2 ',AL1(RUAF2),AL3(EDAFI),AL1(3)                           
         DC    CL4'AF3 ',AL1(RUAF3),AL3(EDAFI),AL1(3)                           
         DC    CL4'AF4 ',AL1(RUAF4),AL3(EDAFI),AL1(3)                           
         DC    CL4'AF5 ',AL1(RUAF5),AL3(EDAFI),AL1(3)                           
EDTTABX  EQU   *                                                                
         EJECT                                                                  
* LITERAL POOL                                                                  
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
       ++INCLUDE BUFILWORKD                                                     
         EJECT                                                                  
       ++INCLUDE BUEXTWORKD                                                     
         EJECT                                                                  
* MODULE WORKING STORAGE                                                        
*                                                                               
WORKD    DSECT                                                                  
RELO     DS    A                                                                
SAVEREG  DS    A                                                                
PARAMS   DS    CL12                                                             
         ORG   PARAMS                                                           
AGEND    DS    A                   A(GENCON STORAGE)                            
ASTFLD   DS    A                   A(FIRST FIELD HEADER)                        
AENDFLD  DS    A                   A(LAST RULES FIELD HEADER)                   
*                                                                               
INPLEN   DS    X                   INPUT LENGTH                                 
NEGSW    DS    C                   NEGATIVE FILTER SWITCH (Y/N)                 
*                                                                               
*&&US                                                                           
DRD      DS    0CL(LRUDRD)                                                      
DIV      DS    CL3                 DIVISION                                     
REG      DS    CL3                 REGION                                       
STDIST   DS    CL3                 START DISTRICT                               
ENDIST   DS    CL3                 END DISTRICT                                 
*&&                                                                             
*                                                                               
WORKX    EQU   *                                                                
         SPACE 2                                                                
* DSECT TO COVER RULES TYPE CODE TABLE                                          
*                                                                               
EDTTABD  DSECT                                                                  
EDTNAME  DS    CL4                 RULE NAME                                    
EDTTYPE  DS    X                   RULE TYPE (SEE BUGENEQUS)                    
EDTROUT  DS    AL3                 RULE VALUE EDITING ROUTINE                   
EDTMINL  DS    X                   MINIMUM LENGTH TO RECOGNIZE RULE             
EDTTABL  EQU   *-EDTTABD           TABLE ENTRY LENGTH                           
         SPACE 2                                                                
* DSECT TO COVER SYSTEM CODE TABLE                                              
*                                                                               
SYSTABD  DSECT                                                                  
SYSTNAME DS    CL7                 SYSTEM NAME (ALPHA)                          
SYSTMINL DS    X                   MINIMUM INPUT LENGTH FOR NAME                
SYSTSN   DS    X                   SYSTEM NUMBER                                
SYSTID   DS    C                   SYSTEM ID (ALPHA)                            
SYSTB    DS    B                   BIT VALUE FOR SYSTEM                         
SYSTABL  EQU   *-SYSTABD           TABLE ENTRY LENGTH                           
         SPACE 2                                                                
* DSECT TO COVER MEDIA CODE TABLE                                               
*                                                                               
MEDTABD  DSECT                                                                  
MEDTNAME DS    CL8                 MEDIA CODE (ALPHA)                           
MEDTMINL DS    X                   MINIMUM INPUT LENGTH FOR NAME                
MEDTID   DS    C                   MEDIA ID (ALPHA)                             
MEDTVSYS DS    B                   VALID SYSTEMS FOR MEDIA (BINARY)             
MEDTABL  EQU   *-MEDTABD           TABLE ENTRY LENGTH                           
         SPACE 2                                                                
* SYSTEM BIT EQUATES                                                            
*                                                                               
SPOTB    EQU   X'80'               SPOT                                         
NETB     EQU   X'40'               NETWORK                                      
PRINTB   EQU   X'20'               PRINT                                        
PRODB    EQU   X'10'               PRODUCTION                                   
TALB     EQU   X'08'               TALENT                                       
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'032BUEDRULESS05/01/02'                                      
         END                                                                    
