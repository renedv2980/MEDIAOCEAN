*          DATA SET REBROWSES  AT LEVEL 039 AS OF 03/29/00                      
*CATALP REBROWSE                                                                
         TITLE 'REBROWSE  - REPPAK BROWSE PROGRAM INTERFACE ROUTINE'            
*              THIS ROUTINE HANDLES CALLING AND SWAPPING TO BROWSE              
*                                                                               
*  PARAMETERS FOR CALLING BROWSE:                                               
*                                                                               
*              PARAMETER 1 =  A(COMFACS)                                        
*                                                                               
*              PARAMETER 2 =  RD OF CALLER BASE PROGRAM                         
*                                                                               
*              PARAMETER 3 =  A(FLD HDR) BROWSE INPUT                           
*                                                                               
*              PARAMETER 4 =  A(FLD HDR) RECORD EXPANSION (OPTIONAL)            
*                                                                               
*              PARAMETER 5 =  BYTE 0: RECORD OPTION FLAGS                       
*                             BYTES 1-3: 3-LETTER REC TYPE                      
*                                                                               
*              PARAMETER 6 =  IF REC TYPE NOT 'PRD'                             
*                             P6 FOR FILE SWITCHING, OR NULLS                   
*                             BYTE 0: SOURCE UTL                                
*                             BYTE 1: ORIGINAL UTL                              
*                             BYTES 2-3: SOURCE REP CODE                        
*                                                                               
*                             IF REC TYPE IS 'PRD'                              
*                             P6 = A(8-BYTE PARM BLOCK)                         
*                             BLOCK BYTE 0: SOURCE UTL                          
*                             BLOCK BYTE 1: ORIGINAL UTL                        
*                             BLOCK BYTES 2-3: SOURCE REP CODE                  
*                             BLOCK BYTES 4-7: ADV CODE FOR PRD LOOKUP          
*                                                                               
*  PARAMETERS FOR RETURNING FROM BROWSE:                                        
*                                                                               
*              PARAMETER 1 =  A(COMFACS)                                        
*                                                                               
*              PARAMETER 2 =  RD OF CALLER BASE PROGRAM                         
*                                                                               
*              PARAMETER 3 =  NULLS (X'00000000')                               
*                                                                               
*                                                                               
REBROWSE CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 (WORKDX-WORKD),*BROWS*,CLEAR=YES                                 
         USING WORKD,RC                                                         
         MVC   PSAVE(24),0(R1)     SAVE PARAMETERS                              
*                                                                               
         L     RE,ACOMFACS                                                      
         USING COMFACSD,RE                                                      
         L     RF,CGLOBBER         RESOLVE A(GLOBBER)                           
         ST    RF,AGLOBBER                                                      
         L     RF,CGETTXT          RESOLVE A(GETTXT)                            
         ST    RF,AGETTXT                                                       
         L     RF,CSWITCH          RESOLVE A(SWITCH)                            
         ST    RF,ASWITCH                                                       
         DROP  RE                                                               
*                                                                               
         OC    BASERD,BASERD       DO WE HAVE A BASE RD?                        
         BNZ   *+6                                                              
         DC    H'0'                MUST HAVE THIS TO WORK                       
*                                                                               
         USING GLVXFRSY,CONTEL                                                  
         USING GLBRWKW,BROWSEL                                                  
*                                                                               
         BAS   RE,CKGLOB           CHECK IF RETURN TO PROG CALL                 
*                                                                               
         TM    STAT,RETURN         RETURN CALL?                                 
         BO    DORET               YES - RETURN CALL ROUTINE                    
*                                                                               
         OC    BRWFLD,BRWFLD       PASSED A BROWSE FIELD?                       
         BNZ   TOBRW               YES - CALL TO INVOKE BROWSE                  
*                                                                               
         GOTO1 AGLOBBER,DMCB,=C'GETD',BROWSEL,GLBRWLNQ,GLRBRWSE                 
         TM    DMCB+8,X'10'        BROWSE ELEM?                                 
         BO    XIT                 NO - GOOD, GET OUT                           
         GOTO1 (RF),DMCB,=C'DELE',,,GLRBRWSE  YES - DELETE IT                   
         B     XIT                                                              
*                                                                               
***********************************************************************         
* DORET - RETURN FROM BROWSE ROUTINE                                            
***********************************************************************         
DORET    DS    0H                                                               
         CLC   GLBRWREC,=C'BAD'    BAD RETURN FROM BROWSE?                      
         BNE   DORET010                                                         
         OI    STAT,BAD                                                         
         GOTO1 AGETTXT,DMCB,725,0,(C'E',0),0,X'44',0                            
         B     DORETX                                                           
*                                                                               
DORET010 DS    0H                                                               
         CLC   GLBRWREC,=C'CAN'    CANCEL REQUEST?                              
         BNE   DORET015                                                         
         OI    STAT,CANCEL                                                      
         GOTO1 AGETTXT,DMCB,162,0,(C'I',0),0,X'44',0                            
         ZICM  R2,GLBRWINP,3                                                    
         AR    R2,RA                                                            
         NI    4(R2),X'FF'-X'20'   NOT PREV VALID                               
         OI    6(R2),X'80'+X'40'   CURSOR HERE & TRANSMIT                       
         B     DORETX                                                           
*                                                                               
DORET015 DS    0H                                                               
         OC    GLBRWKW,SPACES                                                   
         CLC   GLBRWKW,SPACES    NO RECORD RETURNED?                            
         BNE   DORET020                                                         
         GOTO1 AGETTXT,DMCB,633,0,(C'E',0),0,X'44',0                            
         ZICM  R2,GLBRWINP,3                                                    
         AR    R2,RA                                                            
         NI    4(R2),X'FF'-X'20'   NOT PREV VALID                               
         OI    6(R2),X'80'+X'40'   CURSOR HERE & TRANSMIT                       
         B     DORETX                                                           
DORET020 DS    0H                                                               
         OC    GLBRWKW,SPACES                                                   
         LA    R4,GLBRWKW+L'GLBRWKW                                             
         LA    R5,GLBRWKW-1                                                     
DORET025 DS    0H                                                               
         BCTR  R4,0                                                             
         CLI   0(R4),C' '                                                       
         BNE   DORET030                                                         
         CR    R4,R5                                                            
         BH    DORET025                                                         
DORET030 DS    0H                                                               
         SR    R4,R5                                                            
         LTR   R4,R4               THIS IS THE LEN RETURNED BY BROWSE           
         BNZ   *+6                                                              
         DC    H'0'                MUST HAVE SOMETHING TO BE HERE               
*                                                                               
         ZICM  R2,GLBRWINP,3                                                    
         AR    R2,RA                                                            
         ZIC   R3,0(R2)                                                         
         SH    R3,=H'8'                                                         
         TM    1(R2),X'02'         EXT HDR?                                     
         BZ    *+8                                                              
         SH    R3,=H'8'                                                         
         CR    R4,R3                                                            
         BNH   *+6                                                              
         DC    H'0'                BROWSE RETURN KEY TOO LONG FOR FLD           
*                                                                               
         BCTR  R3,0                                                             
         EX    R3,*+8                                                           
         B     *+10                                                             
         XC    8(0,R2),8(R2)                                                    
*                                                                               
         STC   R4,5(R2)                                                         
         BCTR  R4,0                                                             
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   8(0,R2),GLBRWKW                                                  
         NI    4(R2),X'FF'-X'20'                                                
         OI    6(R2),X'80'+X'40'                                                
*                                                                               
         OC    GLBRWEXP,GLBRWEXP   USING REC EXPANSION FLD?                     
         BZ    DORET080            NO - DONE                                    
         OC    GLBRWRD,SPACES                                                   
         LA    R4,GLBRWRD+L'GLBRWRD                                             
         LA    R5,GLBRWRD-1                                                     
DORET050 DS    0H                                                               
         BCTR  R4,0                                                             
         CLI   0(R4),C' '                                                       
         BNE   DORET055                                                         
         CR    R4,R5                                                            
         BH    DORET050                                                         
DORET055 DS    0H                                                               
         SR    R4,R5                                                            
         LTR   R4,R4               THIS IS THE LEN RETURNED BY BROWSE           
         BNZ   *+6                                                              
         DC    H'0'                MUST HAVE SOMETHING TO BE HERE               
*                                                                               
         ZICM  R2,GLBRWEXP,3                                                    
         AR    R2,RA                                                            
         ZIC   R3,0(R2)                                                         
         SH    R3,=H'8'                                                         
         TM    1(R2),X'02'         EXT HDR?                                     
         BZ    *+8                                                              
         SH    R3,=H'8'                                                         
         CR    R4,R3               BROWSE EXP FLD LONGER THAN SCREEN?           
         BNH   *+6                                                              
         LR    R4,R3               USE LEN OF SCREEN FLD                        
*                                                                               
         BCTR  R3,0                                                             
         EX    R3,*+8                                                           
         B     *+10                                                             
         XC    8(0,R2),8(R2)                                                    
*                                                                               
         STC   R4,5(R2)                                                         
         BCTR  R4,0                                                             
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   8(0,R2),GLBRWRD                                                  
         OI    6(R2),X'80'                                                      
*                                                                               
DORET080 DS    0H                                                               
         GOTO1 AGETTXT,DMCB,161,0,(C'I',0),0,X'44',0                            
DORETX   DS    0H                                                               
         LA    R2,64(RA)                                                        
         ZIC   R3,0(R2)                                                         
         AR    R2,R3                                                            
         MVC   8(4,R2),=C'=RE '                                                 
         MVI   5(R2),4                                                          
         OI    6(R2),X'80'                                                      
         L     RD,BASERD                                                        
         B     XIT                                                              
***********************************************************************         
* TOBRW - CREATE CALL TO BROWSE                                                 
***********************************************************************         
TOBRW    DS    0H                                                               
         BAS   RE,GETPROG          WE'LL NEED THIS FOR GLOBBER                  
*                                                                               
* BUILD GLOBBER BROWSE ELEM                                                     
*                                                                               
         XC    BROWSEL,BROWSEL                                                  
*                                                                               
*  KEYWORD FIELD:                                                               
         OC    BRWFLD,BRWFLD                                                    
         BNZ   *+6                                                              
         DC    H'0'                BROWSE FIELD NOT PASSED                      
         L     R2,BRWFLD           BROWSE FIELD                                 
         CLI   8(R2),C'='                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
         ZIC   R3,5(R2)                                                         
*                                                                               
         CHI   R3,L'GLBRWKW                                                     
         BL    *+8                                                              
         LA    R3,L'GLBRWKW                                                     
*                                                                               
         BCTR  R3,0                                                             
         LA    R4,L'GLBRWKW                                                     
         CR    R3,R4                                                            
         BNH   *+6                                                              
         DC    H'0'                KEYWORD STRING TOO LONG                      
         LTR   R3,R3                                                            
         BZ    *+6                                                              
         BCTR  R3,0                                                             
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   GLBRWKW(0),9(R2)                                                 
*                                                                               
* RECORD TYPE & OPTIONS                                                         
         OC    TYPEOPT+1(3),TYPEOPT+1                                           
         BNZ   *+6                                                              
         DC    H'0'                REC TYPE NOT PASSED                          
         MVC   GLBRWREC,TYPEOPT+1                                               
         MVC   GLBRWFLG,TYPEOPT                                                 
*                                                                               
* SCREEN FIELD DISPLACEMENTS                                                    
         L     R2,BRWFLD                                                        
         SR    R2,RA                                                            
         STCM  R2,7,GLBRWINP                                                    
         OC    EXPFLD,EXPFLD                                                    
         BZ    TOBRW050                                                         
         L     R2,EXPFLD                                                        
         SR    R2,RA                                                            
         STCM  R2,7,GLBRWEXP                                                    
*                                                                               
TOBRW050 DS    0H                                                               
         CLC   GLBRWREC,=C'PRD'    PRD REC REQUEST?                             
         BE    TOBRW070            YES - PARAMS ARE SLIGHTLY DIFFERENT          
*                                                                               
* FILE SWITCHING INFO                                                           
         OC    SWFILE,SWFILE                                                    
         BZ    TOBRW100                                                         
         CLI   SWFILE,0                                                         
         BNE   *+6                                                              
         DC    H'0'                SOURCE UTL MISSING                           
         MVC   GLBRWSF,SWFILE                                                   
         CLI   SWFILE+1,0                                                       
         BNE   *+6                                                              
         DC    H'0'                ORIGINAL UTL MISSING                         
         MVC   GLBRWOF,SWFILE+1                                                 
         OC    SWFILE+2(2),SWFILE+2                                             
         BNZ   *+6                                                              
         DC    H'0'                MISSING SOURCE REP CODE                      
         MVC   GLBRWSR,SWFILE+2                                                 
         B     TOBRW100                                                         
*                                                                               
TOBRW070 DS    0H                                                               
         OC    PSAVE+20(4),PSAVE+20                                             
         BNZ   *+6                                                              
         DC    H'0'                REQ'D PARM                                   
         L     RF,PSAVE+20                                                      
* FILE SWITCHING INFO                                                           
         OC    0(4,RF),0(RF)       FILE SWITCHING?                              
         BZ    TOBRW080            NO                                           
         CLI   0(RF),0                                                          
         BNE   *+6                                                              
         DC    H'0'                SOURCE UTL MISSING                           
         MVC   GLBRWSF,0(RF)                                                    
         CLI   1(RF),0                                                          
         BNE   *+6                                                              
         DC    H'0'                ORIGINAL UTL MISSING                         
         MVC   GLBRWOF,1(RF)                                                    
         OC    2(2,RF),2(RF)                                                    
         BNZ   *+6                                                              
         DC    H'0'                MISSING SOURCE REP CODE                      
         MVC   GLBRWSR,2(RF)                                                    
TOBRW080 DS    0H                                                               
         OC    4(4,RF),=4C' '                                                   
         CLC   4(4,RF),=4C' '                                                   
         BNE   *+6                                                              
         DC    H'0'                ADV CODE REQUIRED                            
         MVC   GLBRWADV,4(RF)                                                   
*                                                                               
* ADD BROWSE ELEM                                                               
TOBRW100 DS    0H                                                               
         GOTO1 AGLOBBER,DMCB,=C'PUTD',BROWSEL,GLBRWLNQ,GLRBRWSE                 
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
* BUILD GLOBBER CONTROL ELEM                                                    
*                                                                               
         XC    CONTEL,CONTEL                                                    
         MVC   GLVXFRSY,SYSTEM                                                  
         MVC   GLVXFRPR,PROG                                                    
         MVC   GLVXTOSY,=C'REP'                                                 
         MVC   GLVXTOPR,=C'BRO'                                                 
         MVI   GLVXFLG1,GLV1GOTO+GLV1SEPS   INVOKE BASE                         
         GOTO1 AGLOBBER,DMCB,=C'PUTD',CONTEL,GLVXLENQ,GLVXCTL                   
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     RD,BASERD           ALL THE WAY OUT                              
         B     XIT                                                              
***********************************************************************         
* CKGLOB - CHECK FOR INCOMING GLOBBER CALL                                      
***********************************************************************         
CKGLOB   NTR1                                                                   
         GOTO1 AGLOBBER,DMCB,=C'GETD',CONTEL,24,GLVXCTL                         
         TM    DMCB+8,X'10'        NO VARIABLES FOUND, SKIP                     
         BO    XIT                                                              
*                                                                               
         CLC   GLVXFRSY(6),=C'REPBRO'   RETURN FROM REP BROWSE?                 
         BNE   XIT                                                              
*                                                                               
         OI    STAT,RETURN         THIS MUST BE A RETURN CALL                   
*                                                                               
         GOTO1 (RF),DMCB,=C'DELE',,,GLVXCTL                                     
*                                                                               
         GOTO1 (RF),DMCB,=C'GETD',BROWSEL,GLBRWLNQ,GLRBRWSE                     
         TM    DMCB+8,X'10'        NO BROWSE ELEM?                              
         BZ    *+6                                                              
         DC    H'0'                MUST HAVE A BROWSE ELEMENT                   
*                                                                               
         GOTO1 (RF),DMCB,=C'DELE',,,GLRBRWSE                                    
         B     XIT                                                              
***********************************************************************         
* GETPROG - GET NAME OF CALLER PROGRAM                                          
***********************************************************************         
GETPROG  NTR1                                                                   
         GOTO1 ASWITCH,DMCB,X'00FFFFFF'                                         
         L     R5,0(R1)                                                         
         USING TCBD,R5                                                          
         MVC   SYSTEM,TCBLNSYS                                                  
         MVC   PROG,TCBLNPRG                                                    
         DROP  R5                                                               
         SR    R5,R5                                                            
         XC    DMCB,DMCB                                                        
         B     XIT                                                              
***********************************************************************         
* COMMON ROUTINES                                                               
***********************************************************************         
XIT      XIT1                                                                   
SPACES   DC    80C' '                                                           
         LTORG                                                                  
***********************************************************************         
* WORKING STORAGE                                                               
***********************************************************************         
WORKD    DSECT                                                                  
DMCB     DS    6F                                                               
*                                                                               
PSAVE    DS    0F                  SAVE AREA FOR DMCB PARAMS                    
ACOMFACS DS    A                                                                
BASERD   DS    F                                                                
BRWFLD   DS    F                                                                
EXPFLD   DS    F                                                                
TYPEOPT  DS    F                                                                
SWFILE   DS    F                                                                
*                                                                               
FULL     DS    F                                                                
DUB      DS    D                                                                
PROG     DS    CL3                                                              
SYSTEM   DS    CL3                                                              
AGLOBBER DS    A                                                                
AGETTXT  DS    A                                                                
ASWITCH  DS    A                                                                
*                                                                               
STAT     DS    X                   PROGRAM MODE/STATUS                          
RETURN   EQU   X'80'               THIS IS A RETURN CALL                        
CANCEL   EQU   X'40'               RETURN CALL WITH CANCEL                      
BAD      EQU   X'20'               BAD RETURN CALL                              
*                                                                               
BROWSEL  DS    CL(GLBRWLNQ)                                                     
CONTEL   DS    CL(GLVXLENQ)                                                     
WORKDX   EQU   *                                                                
*                                                                               
       ++INCLUDE DDGLVXCTLD                                                     
       ++INCLUDE REGLBRW                                                        
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE DDGLOBEQUS                                                     
       ++INCLUDE FATCB                                                          
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'039REBROWSES 03/29/00'                                      
         END                                                                    
