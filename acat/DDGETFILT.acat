*          DATA SET DDGETFILT  AT LEVEL 006 AS OF 05/01/02                      
*CATALP GETFILT                                                                 
         TITLE 'GET FILTER VALUES FROM THE CONTROL FILE'                        
GETFILT  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKLNQ-WORKD,**GETFLT                                           
         USING WORKD,RC            RC=A(W/S)                                    
*                                                                               
         USING PARMD,RA            RA=A(PARAMETER LIST)                         
         LR    RA,R1                                                            
*                                                                               
         USING CTVREC,R9           R9=A(KEY)                                    
         LA    R9,KEY                                                           
*                                                                               
         ZIC   RF,PMAXFILT         CLEAR OUTPUT BLOCK                           
         MHI   RF,60                                                            
         L     RE,PABLOCK                                                       
         XCEF                                                                   
*                                  BUILD KEY OF RECORD                          
         L     R1,PAKEY                                                         
         XC    CTVKEY,CTVKEY                                                    
         MVI   CTVKTYP,C'V'                                                     
         MVI   CTVKREC,C'F'                                                     
         MVC   CTVKSYS,PSYS                                                     
         CLI   CTVKSYS,C'A'                                                     
         BNE   *+10                                                             
         MVC   CTVKKEY(3),0(R1)                                                 
         CLI   CTVKSYS,C'M'                                                     
         BNE   *+10                                                             
         MVC   CTVKKEY(3),0(R1)                                                 
         MVI   THISFILT,1          SET CURRENT FILTER NUMBER                    
*                                                                               
GETF2    CLC   THISFILT,PMAXFILT   ALL FILTERS DONE                             
         BH    EXIT                                                             
         BAS   RE,GETREC                                                        
         BAS   RE,GETVAL                                                        
         ZIC   R1,THISFILT         BUMP FILTER NUMBER                           
         LA    R1,1(,R1)                                                        
         STC   R1,THISFILT                                                      
         B     GETF2                                                            
*                                                                               
EXIT     XMOD1 1                                                                
         EJECT                                                                  
*              GET A RECORD FROM THE CONTROL FILE                               
*                                                                               
GETREC   NTR1                                                                   
         MVC   CTVKNUM,THISFILT    SET FILTER NUMBER                            
         LA    R1,IOAREA1                                                       
         ST    R1,AREC                                                          
         L     R1,PABUFF           A(BUFFER PASSED)                             
         LTR   R1,R1                                                            
         BZ    GETR4                                                            
         L     R0,0(,R1)           YES - IS RECORD IN BUFFER                    
         LA    R1,4(,R1)                                                        
*                                                                               
GETR2    ST    R1,AREC                                                          
         LTR   R0,R0                                                            
         BZ    GETR4               NO - GO READ INTO BUFFER                     
         CLC   KEY,0(R1)                                                        
         BE    EXIT                YES - RETURN WITH A(RECORD)                  
         LA    R1,1000(,R1)        NEXT RECORD                                  
         TM    PAIND,PAI2PAGE      TWO PAGES OF FILTERS ?                       
         BZ    *+8                 NO                                           
         LA    R1,1000(,R1)        YES                                          
         SHI   R0,1                                                             
         B     GETR2                                                            
*                                  READ A RECORD                                
GETR4    L     R0,AREC                                                          
         ST    R0,AIOAREA                                                       
         GOTO1 PADATMGR,DMCB,(0,=C'DMREAD'),=C'CTFILE',KEY,(R0)                 
         CLI   DMCB+8,0            CHECK FOR ERRORS                             
         BE    GETR5                                                            
         XC    AREC,AREC                                                        
         B     GETR9                                                            
*                                                                               
GETR5    TM    PAIND,PAI2PAGE      2ND RECORD TO READ ?                         
         BZ    GETR9                                                            
         MVI   CTVKPAGE,1          SET TO GET 2ND PAGE                          
         LA    R2,IOAREA2                                                       
         GOTO1 PADATMGR,DMCB2,(0,=C'DMREAD'),=C'CTFILE',KEY,(R2)                
         MVI   CTVKPAGE,0          RESET                                        
         CLI   DMCB2+8,0                                                        
         BNE   GETR9                  NO 2ND RECORD                             
*                                                                               
         LA    R3,CTVDATA-CTVREC(R2)  POINT TO FIRST ELEMENT IN 2ND REC         
         L     R4,AREC                A(1ST RECORD)                             
         AHI   R4,CTVDATA-CTVREC                                                
*                                                                               
         XR    RF,RF                                                            
GETR6    CLI   0(R4),0             EOR                                          
         BE    GETR7                                                            
         IC    RF,1(,R4)                                                        
         AR    R4,RF               GET TO END OF RECORD                         
         B     GETR6                                                            
*                                                                               
GETR7    CLI   0(R3),0             EOR                                          
         BE    GETR9               FINISHED APPENDING ELEMENTS                  
         IC    RF,1(,R3)           LENGTH OF CURRENT ELEMENT                    
         CLI   0(R3),X'A1'         VALUE ELEMENTS ?                             
         BNE   GETR8                                                            
         EXMVC RF,0(R4),0(R3)      APPEND TO EOR, MAY OVERWRITE IOAREA2         
         AR    R4,RF               NEW END OF RECORD                            
         XC    0(4,R4),0(R4)       MARK END OF RECORD                           
*                                                                               
GETR8    AR    R3,RF               BUMP TO NEXT ELEMENT                         
         B     GETR7                                                            
*                                                                               
GETR9    L     R1,PABUFF           A(BUFFER PASSED)                             
         LTR   R1,R1                                                            
         BZ    EXIT                                                             
         L     R0,0(,R1)           YES - BUMP RECORD COUNT                      
         AHI   R0,1                                                             
         ST    R0,0(,R1)                                                        
         CLI   DMCB+8,0                                                         
         BE    EXIT                                                             
         L     R1,AIOAREA                                                       
         XC    0(L'KEY+5,R1),0(R1)                                              
         MVC   0(L'KEY,R1),KEY                                                  
         B     EXIT                                                             
         EJECT                                                                  
*              EXTRACT VALUE FROM RECORD & POST TO OUTPUT BLOCK                 
*                                                                               
GETVAL   NTR1                                                                   
         L     R9,AREC             WAS RECORD FOUND                             
         LTR   R9,R9                                                            
         BZ    EXIT                                                             
         L     RE,PAFILT           YES - GET FILTER VALUE                       
         ZIC   RF,THISFILT                                                      
         LA    RE,0(RE,RF)                                                      
         BCTR  RE,0                                                             
         MVC   THISVAL,0(RE)                                                    
         CLI   THISVAL,X'41'                                                    
         BNH   EXIT                                                             
         BCTR  RF,0                                                             
         MHI   RF,60                                                            
         L     RE,PABLOCK                                                       
         LA    RF,0(RE,RF)         RF=A(OUTPUT BLOCK ENTRY)                     
         LA    R1,CTVDATA          R1=A(ELEMENT)                                
         SR    RE,RE                                                            
GETV2    CLI   0(R1),0                                                          
         BE    EXIT                                                             
         CLI   0(R1),X'A0'                                                      
         BE    GETV6                                                            
         CLI   0(R1),X'A1'                                                      
         BE    GETV8                                                            
GETV4    IC    RE,1(,R1)                                                        
         AR    R1,RE                                                            
         B     GETV2                                                            
*                                                                               
         USING CTVNMD,R1           NAME ELEMENT                                 
GETV6    MVC   SAVENAME,SPACES                                                  
         IC    RE,CTVNMLEN                                                      
         SHI   RE,3                                                             
         EX    RE,*+8                                                           
         B     GETV4                                                            
         MVC   SAVENAME(0),CTVNMNAM                                             
*                                                                               
         USING CTVVLD,R1           VALUE ELEMENT                                
GETV8    CLC   THISVAL,CTVVLCHR                                                 
         BE    GETV10                                                           
         TM    THISVAL,X'40'                                                    
         BO    GETV4                                                            
         MVC   DUB(1),THISVAL      CHECK FOR -VE FILTER VALUE                   
         NI    DUB,X'BF'                                                        
         CLC   DUB(1),CTVVLCHR                                                  
         BNE   GETV4                                                            
GETV10   MVC   0(16,RF),SAVENAME                                                
         MVC   16(44,RF),SPACES                                                 
         LA    RF,16(,RF)                                                       
         TM    THISVAL,X'40'                                                    
         BO    *+14                                                             
         MVC   0(10,RF),=C'ALL EXCEPT'                                          
         LA    RF,11(,RF)                                                       
         IC    RE,CTVVLLEN                                                      
         SHI   RE,5                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RF),CTVVLNAM                                                 
         B     EXIT                                                             
         EJECT                                                                  
*              LITERALS ETC.                                                    
*                                                                               
         LTORG                                                                  
SPACES   DC    CL80' '                                                          
*                                  DSECT TO COVER TEMP W/S                      
WORKD    DSECT                                                                  
DUB      DS    D                                                                
AREC     DS    A                                                                
AIOAREA  DS    A                                                                
DMCB     DS    6F                                                               
DMCB2    DS    6F                                                               
THISFILT DS    X                                                                
THISVAL  DS    C                                                                
SAVENAME DS    CL16                                                             
KEY      DS    CL25                                                             
IOAREA1  DS    1000C                                                            
IOAREA2  DS    1000C                                                            
WORKLNQ  EQU   *                                                                
*                                  DSECT TO COVER PARM LIST                     
PARMD    DSECT                                                                  
PSYS     DS    0C                  SYSTEM LETTER                                
PAKEY    DS    A                   A(KEY)                                       
PMAXFILT DS    0X                  MAX NUMBER OF FILTERS                        
PAFILT   DS    A                   A(FILTERS)                                   
PABLOCK  DS    A                   A(OUTPUT BLOCK)                              
PAIND    DS    0C                  INDICTORS FOR 2 PAGES FILTERS                
PAI2PAGE EQU   X'80'               YES READ FOR 2ND RECORD                      
PABUFF   DS    A                   A(RECORD BUFFER)                             
PADATMGR DS    A                   A(DATAMGR)                                   
* CTGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006DDGETFILT 05/01/02'                                      
         END                                                                    
