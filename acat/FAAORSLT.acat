*          DATA SET FAAORSLT   AT LEVEL 001 AS OF 04/09/99                      
*CATALP FAAORSLT                                                                
         TITLE 'FAAORNUM - RETURN SLOT IN TORFAC LIST FOR THIS FACPAK'          
         SPACE 1                                                                
***********************************************************************         
* FAAORSLT                                                            *         
* NTRY: R1 = A(RETURN SLOT FOR A(AOR SLOT) - MUST BE A 8 BYTES)       *         
* EXIT: RETURN SLOT HOLDS A(AOR/TOR) IN SLOT                          *         
*       IF 0(8,R1) =  0 THIS IS THE TOR                               *         
*       IF 0(8,R1) <> 0 THIS IS AN AOR                                *         
*       ARS ARE PRESERVED, BUT MODE SET SAC 0 ON RETURN               *         
***********************************************************************         
         SPACE 1                                                                
AORSLT   CSECT                                                                  
         NMOD1 0,*AORSLT*                                                       
         STAR  CLEAR=ARZERO,ARS=OFF                                             
         L     RC,VSYSFAC                                                       
         USING SYSFACD,RC                                                       
         L     RA,VSSB                                                          
         USING SSBD,RA                                                          
*                                                                               
         LAM   R2,R2,SSBALET                                                    
         ICM   R2,15,SSBATOR                                                    
         LA    R2,TORFACLQ(R2)     INDEX TO FACPAK EXCHANGE GRID                
         USING SBEXCHD,R2                                                       
         SAC   512                                                              
*                                                                               
         LH    R4,0(,R2)           BXLE R2,R4,R5                                
         L     R5,2(,R2)                                                        
         LA    R2,6(,R2)                                                        
         XR    RF,RF               RF=ZERO BASED COUNTER                        
*                                                                               
AOR02    CLC   SBSTOKEN,SSBSTOKN   OUR SLOT?                                    
         BE    AOR04               YES                                          
         LA    RF,1(RF)                                                         
         BXLE  R2,R4,AOR02                                                      
         DC    H'0'                WHERE IS OUR FACPAK IN LIST                  
*                                                                               
AOR04    SAC   0                                                                
         STCM  R2,15,0(R1)                                                      
         STCM  RF,15,4(R1)                                                      
         REAR  ARS=OFF                                                          
         XMOD1 ,                                                                
         DROP  RA,RC                                                            
*                                                                               
ARZERO   DC    16F'0'                                                           
VSYSFAC  DC    V(SYSFAC)                                                        
         LTORG                                                                  
*                                                                               
       ++INCLUDE FASSB                                                          
         EJECT                                                                  
       ++INCLUDE FAUTL                                                          
         EJECT                                                                  
       ++INCLUDE FASYSFAC                                                       
         EJECT                                                                  
       ++INCLUDE FAPIGFACD                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'001FAAORSLT  04/09/99'                                      
         END                                                                    
