*          DATA SET FAWRTOUT   AT LEVEL 007 AS OF 07/20/12                      
*CATALP WRTOUT                                                                  
***********************************************************************         
* ROUTINE CALLS LCM BASED ON VARIOUS FLAGS IN UTL                     *         
* NTRY: P1     = A(UTL)                                               *         
***********************************************************************         
         PRINT NOGEN                                                            
         TITLE 'INTERFACE TO LCM'                                               
WRTOUT   CSECT                                                                  
         NMOD1 WRTWRKL,**WRTO**                                                 
         USING WRTWRKD,RC                                                       
         L     RE,ASSB             DO NOTHING IF THIS IS AN AOR                 
         TM    SSBSTAT4-SSBD(RE),SSBSAOR                                        
         BO    EXIT                                                             
*                                                                               
         SAM31                                                                  
         USING UTLD,R2                                                          
         L     R2,0(,R1)           GET A(UTL)                                   
         XC    SVOUT,SVOUT         WRITE INDICATOR                              
*                                                                               
         ICM   RF,15,TBUFF         GET A(TBUFF)                                 
         BZ    EXIT                                                             
         AHI   RF,-9               GET A(WRITE INDICATOR)                       
         MVC   SVOUT,0(RF)                                                      
         MVI   0(RF),0             CLEAR VALUE                                  
*                                                                               
         TM    TTORAOR,TTAGOBAK    GOBACK REQUESTED?                            
         BNZ   WT090               YES                                          
         CLI   SVOUT,0             WRITE REQUIRED?                              
         BE    WT040               NO                                           
         GOTO1 ALCM,DMCB,VTWRITE,UTLD,0   CALL LCM TO WRITE BUFFER              
*                                                                               
WT040    DS    0H                                                               
         TM    TMQSTA,TMQSPASS+TMQSFAIL   NEED ANY PASS/FAIL MQ MSG?            
         BZ    WT080                      NO                                    
*                                                                               
         GOTO1 ALCM,DMCB,VTGETUTL                                               
         LTR   R5,R1                                                            
         BNZ   *+6                                                              
         DC    H'0'                 PROBABLY NEEDS SOME ERROR HANDLING          
*                                                                               
NEW      USING UTLD,R5                                                          
         MVC   NEW.TLUID,=CL8'NONENONE'                                         
         MVC   NEW.TSVCREQ,=XL2'016B'    SET SPECIAL S/R $MQP                   
         MVI   NEW.TSTATC,TSTCMQ                                                
         TIME  TU                                                               
         ST    R0,NEW.TTIMETU      SET ARRIVAL TIME TO NOW                      
*                                                                               
NEWXA    USING XAUTLD,RF                                                        
         ICM   RF,15,NEW.TUTLXADR                                               
                                                                                
         USING MQUOWD,RE                                                        
         ICM   RE,15,NEWXA.XAAPASSR                                             
         MVC   0(2,RE),=AL2(MQUOWLNQ)  LENGTH OF MESSAGE PLUS (SRMQP00)         
         AHI   RE,2                                                             
         STCM  RE,15,NEW.TBUFF     SET XAPASSR+2 AREA AS DUMMY TBUFF            
*                                                                               
         USING XAUTLD,R3           ORIGINAL XA UTL                              
         L     R3,TUTLXADR                                                      
         MVC   MQUOWCMD,=CL8'MQDONE'                                            
         MVC   MQUOWID,XAMQID      SAVED OF FROM DDMQIO  (UNIT CALL)            
         MVI   MQUOWID+L'MQUOWID-1,C':'  MARK MSG LABEL WITH ENDING :           
         MVC   MQUOW,=CL4'ERR?'                                                 
         TM    TMQSTA,TMQSFAIL     NEED FAIL MQ UNIT OF WORK                    
         BZ    WT050                                                            
         MVC   MQUOW,=CL4'FAIL'                                                 
         B     WT060                                                            
*                                                                               
WT050    TM    TMQSTA,TMQSPASS     NEED PASS MQ UNIT OF WORK                    
         BZ    WT060                                                            
         MVC   MQUOW,=CL4'PASS'                                                 
*                                                                               
WT060    DS    0H                                                               
         XC    TMQSTA,TMQSTA                                                    
         MVC   MQUOWX1,CRLF                                                     
         MVC   MQUOWX2,CRLF                                                     
         MVC   MQUOWCID,XAMQCOID                                                
         MVC   MQUOWCI2,XAMQCOID                                                
         MVC   MQUOWMID,XAMQMGID                                                
*        MVC   MQUOWSYS,TSYS        COPY TSYS                                   
*        MVC   MQUOWSIN,TSIN        COPY TSIN                                   
*        MVC   MQUOWTUS,TTIMETU     COPY TTIMETU                                
*        MVC   MQUOWTYP,TMQTYPE     COPY TMQTYPE                                
*        MVC   MQUOWSEQ,TMQSEQN     COPY TMQSEQN                                
         DROP  NEW,NEWXA            NEW UTL, NEW XA UTL                         
         XC    XAMQID,XAMQID                                                    
         XC    XAMQCOID,XAMQCOID                                                
         XC    XAMQMGID,XAMQMGID                                                
         DROP  R3,RE                ORIGINAL XA UTL, MESSAGE AREA               
*                                                                               
         USING SELISTD,R6                                                       
         L     R6,=V(SELIST)       SE1 (SERVICE)                                
         AHI   R6,6                PAST BXLE VALUES                             
         OC    SEFIRST,SEFIRST     QUEUE HAS MEMEBERS ALREADY?                  
         BNZ   WT070               YES                                          
         STCM  R5,15,SEFIRST       ONLY ENTRY MAKE                              
         STCM  R5,15,SELAST        FIRST AND LAST THE SAME                      
         LHI   RF,1                                                             
         STH   RF,SEQLEN                                                        
         B     WT080                                                            
*                                                                               
WT070    L     RF,SELAST           ADD IT TO END OF THE QUEUE                   
         STCM  R5,15,TXNEXTIN-UTLD(RF)                                          
         STCM  R5,15,SELAST                                                     
         LH    RF,SEQLEN                                                        
         AHI   RF,1                                                             
         STH   RF,SEQLEN                                                        
         B     WT080                                                            
         DROP  R6                                                               
*                                                                               
WT080    DS    0H                                                               
         TM    TSTATU,TSTATDNE     =DONE ISSUED?                                
         BZ    EXITOK              NO                                           
         NI    TSTATU,255-TSTATDNE                                              
         LHI   R0,VTCLSDST                                                      
         OC    TPRNT,TPRNT         PRINTER?                                     
         BZ    *+8                 NO                                           
         LHI   R0,VTPRSTOP                                                      
         GOTO1 ALCM,DMCB,(R0),UTLD,0                                            
         B     EXITOK                                                           
*                                                                               
WT090    ICM   RF,15,TBUFF         GOBACK PROCESSED HERE                        
         NI    TTORAOR,255-TTAGOBAK                                             
         GOTO1 AMSGQIN,DMCB,ASYSFAC,(RF),UTLD                                   
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* HANDY LITTLE ROUTINES                                               *         
***********************************************************************         
EXITOK   CR    RB,RB                                                            
         B     EXIT                                                             
*                                                                               
EXITL    CLI   *,255                                                            
         B     EXIT                                                             
*                                                                               
EXIT     XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTANTS                                              *         
***********************************************************************         
CRLF     DC    X'0D25'                                                          
BRDMSG   DC    X'07003E',C'=BRD',X'00'                                          
REPLY    DC    CL8'REPLY   '                                                    
*                                                                               
ALCM     DC    V(LCM)                                                           
AMSGQIN  DC    V(MSGQIN)                                                        
AMQIO    DC    V(MQIO)                                                          
ASSB     DC    V(SSB)                                                           
ASYSFAC  DC    V(SYSFAC)                                                        
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE DSECT                                               *         
***********************************************************************         
WRTWRKD  DSECT                                                                  
DUB      DS    D                                                                
DMCB     DS    6F                                                               
SVOUT    DS    X                                                                
WRTWRKL  EQU   *-WRTWRKD                                                        
         EJECT                                                                  
                                                                                
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
*FASELIST                                                                       
         PRINT OFF                                                              
       ++INCLUDE FASELIST                                                       
         PRINT ON                                                               
*FAMQUOWD                             MQ UNIT OF WORK                           
         PRINT OFF                                                              
       ++INCLUDE FAMQUOWD                                                       
         PRINT ON                                                               
*FASRS                                                                          
         PRINT OFF                                                              
       ++INCLUDE FASRS                                                          
         PRINT ON                                                               
*FASSB                                                                          
         PRINT OFF                                                              
       ++INCLUDE FASSB                                                          
         PRINT ON                                                               
*FASYSFAC                                                                       
         PRINT OFF                                                              
       ++INCLUDE FASYSFAC                                                       
         PRINT ON                                                               
*FATBHD                                                                         
         PRINT OFF                                                              
       ++INCLUDE FATBHD                                                         
         PRINT ON                                                               
*FATIOB                                                                         
         PRINT OFF                                                              
       ++INCLUDE FATIOB                                                         
         PRINT ON                                                               
*FAUTL                                                                          
         PRINT OFF                                                              
       ++INCLUDE FAUTL                                                          
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'007FAWRTOUT  07/20/12'                                      
         END                                                                    
