*          DATA SET CXSABC     AT LEVEL 014 AS OF 05/01/02                      
*CATALP CXSABC                                                                  
         TITLE 'CXSABC - ACCESS USERID/GROUP LEVELS'                            
**********************************************************************          
* AL4 A(EXTRACT RECORD)                                              *          
* AL4 A(CONFILE RECORD)                                              *          
**********************************************************************          
         SPACE 1                                                                
         PRINT NOGEN                                                            
CXSABC   CSECT                                                                  
         NMOD1 WORKX-WORKD,*CXSABC*,RR=RA                                       
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RA,RELO                                                          
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         USING CXSABD,R2           R2=A(EXTRACT RECORD)                         
         USING SAASREC,R3          R3=A(ACCESS RECORD)                          
*                                                                               
CXSLEN   XC    CXSABLEN,CXSABLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   CXSABLEN(2),=AL2(CXSABDL)                                        
         MVC   CXSABTYP,CXSABDQ                                                 
         MVI   CXSABDX,MXTRTQ                                                   
         MVI   CXSABDX+1,MXTRTQ                                                 
*                                  GET KEY INFO                                 
CXSAID   MVI   CXSABAID-1,MXTRTQ   AGENCY ALPHA ID                              
         MVC   CXSABAID,SAASAGY                                                 
*                                                                               
CXSUIN   MVI   CXSABUIN-1,MXTRTQ   USER ID NUMBER                               
         SR    RF,RF                                                            
         ICM   RF,3,SAASUID                                                     
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  CXSABUIN,DUB+5(3)                                                
*                                                                               
CXSAGC   MVI   CXSABAGC-1,MXTRTQ   ACCESS GROUP CODE ??                         
         GOTO1 XHEXOUT,DMCB,SAASAGN,CXSABAGC,2,=C'TOG'                          
*                                                                               
CXSSYS   MVI   CXSABSYS-1,MXTRTQ   SYSTEM CODE                                  
         GOTO1 XHEXOUT,DMCB,SAASOVS,CXSABSYS,1,=C'TOG'                          
*                                                                               
CXSPGM   MVI   CXSABPGM-1,MXTRTQ   PROGRAM CODE                                 
         GOTO1 XHEXOUT,DMCB,SAASPGM,CXSABPGM,1,=C'TOG'                          
*                                                                               
         CLI   SAVEACTN,C' '       TEST SAVED ACTION CODE                       
         BE    CXSACT1                                                          
         MVI   CXSABACT,C'A'       GOTO ADD PROCESS                             
         B     CXSREC                                                           
CXSACT1  CLI   CXSABACT,C'C'       IF CHANGE DO 'K' DELETE ALL                  
         BE    CXSACT2                                                          
         CLI   CXSABACT,C'D'       IF DELETE DO 'K' DELETE ALL                  
         BE    CXSACT2                                                          
         B     CXSREC              OTHER ACTIONS GOTO ADD PROCESS               
*                                                                               
CXSACT2  MVC   SAVEACTN,CXSABACT   SAVE THIS ACTION                             
         MVI   CXSABACT,C'K'       OVERIDE WITH 'K' DELETE ALL                  
         MVI   CXSABREC-1,MXTRTQ   SET FIELD SEPS FOR NULL VALUES               
         MVI   CXSABACN-1,MXTRTQ                                                
         MVI   CXSABACV-1,MXTRTQ                                                
         CLI   SAVEACTN,C'D'                                                    
         BNE   RETEXIT             IF NOT DELETE THEN RETURN FOR ADDS           
         B     DELEXIT             ELSE EXIT WITH JUST DELETE                   
*                                                                               
CXSREC   MVI   CXSABREC-1,MXTRTQ   RECORD CODE                                  
         MVI   CXSABACN-1,MXTRTQ   ACTION CODE                                  
*                                                                               
         USING SPATABD,R4                                                       
CXSSPA   ICM   R4,15,ASPATAB       GET ADDRESS SPATAB ENTRY                     
         BNZ   CSPA020             FIRST TIME?                                  
         ICM   R4,15,=V(CXSSPAT)   R4=A(SYSTEM PROGRAM ACCESS TABLE)            
         BNZ   *+6                                                              
         DC    H'00'                                                            
*                                                                               
CSPA010  CLI   SPATOVS,0           GET ADDRESS SPATAB ENTRY FOR                 
         BE    EOFEXIT                                                          
*        BNE   *+6                                                              
*        DC    H'00'                                                            
         CLC   SPATOVPG,SAASOVPG                                                
         BE    *+12                                                             
         LA    R4,SPATLEN(R4)                                                   
         B     CSPA010                                                          
         ST    R4,ASPATAB                                                       
         BAS   RE,MERGSAAS                                                      
         LA    R5,AREC+SAASDATA-SAASREC                                         
         ST    R5,ASAASELM                                                      
         LA    R6,SPATPACL                                                      
         ST    R6,APACL                                                         
         CLI   0(R6),0                                                          
         BE    EOFEXIT                                                          
         B     CXSELM                                                           
*                                                                               
CSPA020  EQU   *                                                                
         ICM   R5,15,ASAASELM      R5=A(LAST RECORD ACTION ELEMENT)             
         BNZ   *+6                                                              
         DC    H'00'                                                            
         ICM   R6,15,APACL         R5=A(LAST ACTION IN LIST)                    
         BNZ   *+6                                                              
         DC    H'00'                                                            
*                                                                               
CSPA022  LA    R6,1(R6)                                                         
         ST    R6,APACL                                                         
*                                                                               
CSPA030  LA    R1,SPATPACL                                                      
         LA    R1,L'SPATPACL(R1)                                                
         CR    R6,R1                                                            
         BNL   CSPA032                                                          
         CLI   0(R6),0                                                          
         BE    CSPA022                                                          
         B     CXSACN                                                           
CSPA032  LA    R6,SPATPACL                                                      
         ST    R6,APACL                                                         
         B     CXSNXT                                                           
*                                                                               
CXSELM   CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         BE    CXSNXTX                                                          
         CLI   0(R5),SAMIXELQ      GET RECORD/ACTION ELEMENT                    
         BE    CXSACN                                                           
CXSNXT   SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R5)                                                         
         CH    RF,=H'1'                                                         
         BNH   EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,RF                                                            
         ST    R5,ASAASELM                                                      
         B     CXSELM                                                           
*                                                                               
         USING SAMIXD,R5                                                        
CXSACN   EQU   *                                                                
         GOTO1 XHEXOUT,DMCB,SAMIXRCD,CXSABREC,1,=C'TOG'                         
         L     RF,APACL                                                         
         GOTO1 XHEXOUT,DMCB,(RF),CXSABACN,1,=C'TOG'                             
         BAS   RE,CONVACT                                                       
         BE    *+6                                                              
         DC    H'00'                                                            
         MVI   CXSABACV-1,MXTRTQ                                                
         XC    BITTABLE,BITTABLE   SET UP BIT TABLE                             
         CLI   SAMIXLN,SAMIXLNQ                                                 
         BE    CXSACNA                                                          
         SR    RE,RE                                                            
         IC    RE,SAMIXLN          R2=L(BIT TABLE)-1                            
         SH    RE,=Y(SAMIXLNQ+1)                                                
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   BITTABLE(0),SAMIXACT                                             
CXSACNA  GOTO1 TESTBIT,BITTABLE                                                 
         BO    CXSAVY                                                           
         B     CXSAVN                                                           
*                                                                               
CXSAVY   MVI   CXSABACV,C'Y'       ACCESS VALUE = YES                           
         B     RETEXIT                                                          
*                                                                               
CXSAVN   MVI   CXSABACV,C'N'       ACCESS VALUE = NO                            
         B     RETEXIT                                                          
*                                                                               
CXSNXTX  B     EOFEXIT                                                          
*                                                                               
RETEXIT  L     R1,APARM                                                         
         OI    8(R1),X'40'         SET RECORD RETURNED                          
         B     EXIT                                                             
*                                                                               
EOFEXIT  XC    ASPATAB,ASPATAB                                                  
         XC    ASAASELM,ASAASELM                                                
         MVI   SAVEACTN,C' '                                                    
         L     R1,APARM                                                         
         OI    8(R1),X'88'         SET EOF RETURN                               
         B     EXIT                                                             
*                                                                               
DELEXIT  XC    ASPATAB,ASPATAB                                                  
         XC    ASAASELM,ASAASELM                                                
         MVI   SAVEACTN,C' '                                                    
         L     R1,APARM                                                         
         OI    8(R1),0             SET EOF AND WRITE 'K' TYPE RECORD            
         B     EXIT                                                             
*                                                                               
DELREC   L     R1,APARM            DELETE THIS RECORD                           
         MVI   8(R1),X'80'                                                      
         B     EXIT                                                             
*                                                                               
EXIT     XMOD1 1                                                                
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO MERGE IN ACCESS RECORD                                   *         
* R4=A(SYSTEM PROGRAM ACCESS TABLE ENTRY)                             *         
***********************************************************************         
         SPACE 1                                                                
MERGSAAS NTR1  ,                                                                
         LA    RE,AREC                                                          
         LA    RF,2048                                                          
         LA    R0,SPATAREC                                                      
         LR    R1,RF                                                            
         MVCL  RE,R0                                                            
         LA    R5,AREC+SAASDATA-SAASREC                                         
         USING SAMIXD,R5           R5=A(RECORD/ACTIONS ELEMENT)                 
*                                                                               
MSAAS2   CLI   SAMIXEL,0           TEST E-O-R                                   
         BE    MSAAS20                                                          
         CLI   SAMIXEL,X'FF'       TEST ELEMENT DELETED                         
         BE    MSAAS18                                                          
*                                                                               
         IC    R2,SAMIXLN          R2=L(BIT TABLE)-1                            
         SH    R2,=Y(SAMIXLNQ+1)                                                
*                                                                               
         XC    BITTABLE,BITTABLE   SET UP BIT TABLE                             
         EX    R2,*+4                                                           
         MVC   BITTABLE(0),SAMIXACT                                             
*                                                                               
         CLI   SAMIXATT,0          TEST RECORD ATTACHED                         
         BE    MSAAS4                                                           
         GOTO1 FINDACTS,DMCB,(SAMIXATT,AREC)                                    
         NC    BITTABLE,FACTBITT                                                
* ??     BZ    MSAAS8                                                           
         GOTO1 FINDACTS,DMCB,(SAMIXRCD,SAASREC)                                 
         NC    FACTBITT,BITTABLE   TURN OFF POSITIVE 'NO'S                      
         XC    BITTABLE,FACTBITT                                                
* ??     BZ    MSAAS8                                                           
         B     MSAAS6                                                           
*                                                                               
MSAAS4   GOTO1 FINDACTS,DMCB,(SAMIXRCD,SAASREC)                                 
         NC    BITTABLE,FACTBITT                                                
* ??     BZ    MSAAS8                                                           
*                                                                               
MSAAS6   EX    R2,*+4              COPY IN NEW BIT TABLE                        
         MVC   SAMIXACT(0),BITTABLE                                             
         B     MSAAS18                                                          
*                                                                               
MSAAS8   MVI   SAMIXEL,X'FF'       DONT 'DELETE' ELEMENT                        
*                                                                               
MSAAS18  XR    RF,RF               BUMP R5 TO NEXT ELEMENT                      
         IC    RF,SAMIXLN                                                       
         BXH   R5,RF,MSAAS2                                                     
*                                                                               
MSAAS20  XIT1                                                                   
         DROP  R5                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO FIND SAMIXD ELEMENT IN RECORD                            *         
*                                                                     *         
* NTRY: P1=(RECORD TYPE, A(ACCESS RECORD))                            *         
*                                                                     *         
* EXIT: IF FOUND:     CC=EQUAL,       FACTBITT=BIT MASK FOR ACTIONS   *         
*                     R6=A(ELEMENT),  RF=L(ELEMENT)                   *         
*       IF NOT FOUND: CC=NOT EQUAL,   FACTBITT=ZERO                   *         
***********************************************************************         
         SPACE 1                                                                
FINDACTS XR    R6,R6                                                            
         ICM   R6,7,1(R1)                                                       
         LA    R6,SAASDATA-SAASREC(R6)                                          
         USING SAMIXD,R6           R6=A(RECORD/ACTIONS ELEMENT)                 
*                                                                               
         XC    FACTBITT,FACTBITT                                                
         XR    RF,RF                                                            
*                                                                               
FACTS2   CLI   SAMIXEL,0           TEST E-O-R                                   
         BE    FINDACTN                                                         
*                                                                               
         IC    RF,SAMIXLN                                                       
         CLI   SAMIXEL,SAMIXELQ    MATCH ON ELEMENT CODE                        
         BNE   FACTS4                                                           
         CLC   SAMIXRCD,0(R1)      MATCH ON RECORD TYPE                         
         BE    FINDACTY                                                         
         BH    FINDACTN                                                         
*                                                                               
FACTS4   BXH   R6,RF,FACTS2        BUMP R6 TO NEXT ELEMENT                      
*                                                                               
FINDACTY SH    RF,=Y(SAMIXLNQ+1)   COPY TABLE                                   
         EX    RF,*+4                                                           
         MVC   FACTBITT(0),SAMIXACT                                             
         LA    RF,SAMIXLNQ+1(RF)                                                
         CR    RE,RE                                                            
         BR    RE                                                               
*                                                                               
FINDACTN LTR   RE,RE               EXIT WITH CC=NOT EQUAL                       
         BR    RE                                                               
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO CONVERT ACTION CODE TO ACTION SEQUENCE NUMBER            *         
*                                                                     *         
* NTRY: APACL=A(ACTION CODE)                                          *         
* EXIT: IF ACTION CODE IS ZERO CC=NOT EQUAL                           *         
*        ELSE R0=ACTION SEQUENCE NUMBER                               *         
***********************************************************************         
         SPACE 1                                                                
CONVACT  ICM   RF,15,APACL         R0=ACTION CODE                               
         BZ    CONVACTN                                                         
         ICM   R0,1,0(RF)                                                       
         BZ    CONVACTN                                                         
*                                                                               
         LA    R1,SPATPACL         R1=A(ACTION CODE LIST)                       
         LA    RF,L'SPATPACL       RF=L(ACTION CODE LIST)                       
*                                                                               
CACT2    CLM   R0,1,0(R1)          MATCH ON ACTION CODE                         
         BE    CACT4                                                            
         LA    R1,1(R1)            BUMP R1 TO NEXT ACTION CODE                  
         BCT   RF,CACT2                                                         
         DC    H'0'                BAD ACTION CODE                              
*                                                                               
CACT4    LA    R0,L'SPATPACL       R0=ACTION SEQUENCE NUMBER                    
         SR    R0,RF                                                            
*                                                                               
CONVACTY CR    RE,RE               SET CC=EQUAL                                 
         BR    RE                                                               
CONVACTN LTR   RE,RE               SET CC=NOT EQUAL                             
         BR    RE                                                               
         EJECT                                                                  
         SPACE 1                                                                
***********************************************************************         
* NTRY: R1=A(BIT TABLE), R0=CODE                                      *         
***********************************************************************         
         SPACE 1                                                                
TESTBIT  LR    RF,R0               R1=A(BYTE OF BIT)                            
         SRL   RF,3                                                             
         AR    R1,RF                                                            
         SPACE 1                                                                
         LA    RF,X'07'            RF=MASK                                      
         NR    RF,R0                                                            
         IC    RF,MASKS(RF)                                                     
         SPACE 1                                                                
         EX    RF,TESTBITM         EX A TM                                      
         BR    RE                                                               
         SPACE 1                                                                
TESTBITM TM    0(R1),0             A TM                                         
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
       ++INCLUDE MXTRT                                                          
       ++INCLUDE CXSRECID                                                       
         SPACE 1                                                                
         DS    0F                                                               
ASPATAB  DC    A(0)                                                             
APACL    DC    A(0)                                                             
ASAASELM DC    A(0)                                                             
XHEXOUT  DC    V(HEXOUT)                                                        
         SPACE 1                                                                
MASKS    DC    X'8040201008040201'                                              
SAVEACTN DC    CL1' '                                                           
*                                                                               
         DS    0D                                                               
AREC     DC    2048X'00'                                                        
         SPACE 1                                                                
         EJECT                                                                  
* INCLUDE CXTRACT LOCAL DSECTS                                                  
         SPACE 1                                                                
       ++INCLUDE CXTRACTD                                                       
         EJECT                                                                  
         SPACE 2                                                                
WORKD    DSECT                                                                  
PARM     DS    0XL16               PARAMETER LIST                               
DMCB     DS    6F                  SUBROUTINE PARAMETER LIST                    
AXREC    DS    A                   A(EXTRACT RECORD)                            
CFREC    DS    A                   A(CONFILE RECORD)                            
         DS    A                                                                
         DS    A                                                                
*                                                                               
DUB      DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
RELO     DS    A                                                                
*                                                                               
BITTABLE DS    0XL8                8 BYTE BIT TABLE                             
BITTABLX DS    XL32                32 BYTE BIT TABLE                            
*                                                                               
FACTBITT DS    XL8                 BIT TABLE RETURNED BY FINDACTS               
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE CXSABD                                                         
         EJECT                                                                  
       ++INCLUDE SEACSFILE                                                      
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'014CXSABC    05/01/02'                                      
         END                                                                    
