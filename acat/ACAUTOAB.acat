*          DATA SET ACAUTOAB   AT LEVEL 014 AS OF 03/24/20                      
*CATALP AUTOAB                                                                  
         TITLE 'AUTO APPROVE PAYABLES'                                          
*--------------------------------------------------------------------*          
* PID  LVL DATE    COMMENTS                                                     
* ---------------------------------                                             
* GHOA 010 18APR19 ITMF-24648 ALWAYS PASS SVAGY TO ABVNDR                       
* GHOA 011 18MAY31 SPEC-20441 NOT WORKING IN CANADA (SPOT & PRINT)              
* RGUP 012 18JUN18 SPEC-20692 NEED ADDITIONAL MEDIA FOR DIGIAL AUDIO            
* VGUP 013 21NOV19 SPEC-41007 AAB IS CRASHING-INCREASE BUFFER SIZE              
* RGUP 014 01DEC19 SPEC-30974 SUPPORT DOLLAR TOLERANCE                          
*--------------------------------------------------------------------*          
**********************************************************************          
* PROGRAM OPTIONS :                                                  *          
*                                                                    *          
*         QOPT1 - 'Y'  ADD SR ACCOUNT DETAILS TO THE TABLE           *          
*         QOPT2 - 'Y'  EXPAND MULTIPLE TRANSACTIONS                  *          
*         QOPT5 - 'Y'  RUN OFF SS/SR TRANSACTIONS NOT POINTERS       *          
*         QOPT6 - 'Y'  CALC CASH AVAILABILITY BY MOS NOT BY ESTIMATE *          
*         QOPT8 - 'Y'  SUPPRESS DISBURSED DETAILS/TRANSACTIONS       *          
*         QOPT9 - 'Y'  APPROVE UP TO CASH POSITION                   *          
*      PROFILE1 - 'Y'  APPROVE BY MOS                                *          
*      PROFILE2 - 'M'  RUN BY SYSTEM OR MEDIA                        *          
*      PROFILE3 - 'Y'  APPROVE UP TO CASH POSITION                   *          
*      PROFILE4 -      PRIOR MONTHS (11/23)                          *          
*      PROFILE5 - 'Y'  USE PRINT BILLABLE DATE                       *          
**********************************************************************          
AUTOAB   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 AUTOL,**AUTOAB,R9,RR=RE                                          
         USING AUTOD,RC                                                         
*                                                                               
         MVC   CALLRD,4(RD)                                                     
*                                                                               
         ST    RE,RELO                                                          
         MVC   SAVERD,4(RD)                                                     
         ST    RB,ABASE                                                         
*                                                                               
         ST    RA,SAVERA                                                        
*                                                                               
         BASR  RA,0                                                             
         AHI   RA,GLOBALS-*                                                     
         USING GLOBALS,RA          RA=A(GLOBALS)                                
*                                                                               
         GOTOR INIT                INITIALIZATION ROUTINE                       
         TM    AUTERR,AUTMRNG      IS MOS RANGE > 12 MONTHS                     
         BO    AUTOAP10                                                         
*                                                                               
         GOTOR BLDPAYTB            BUILD PAYABLES TABLE                         
         CLI   BCTSERRS,0                                                       
         BE    *+14                                                             
         MVC   AUTERR,BCTSERRS     PASS ERROR BACK TO MARKER                    
         B     AUTOAP10                                                         
*                                                                               
         BRAS  RE,GETPNAMS         GET PAYEE NAMES                              
*                                                                               
         GOTOR FIXTAB              MARK DISBURSED ONLY ITEMS                    
*                                                                               
         GOTOR BLDSRBUK            FILL IN SR TRAN BUCKETS                      
*                                                                               
         GOTOR CASHAVL2            CHECK CASH AVAILABLILITY                     
*                                                                               
         BRAS  RE,RESORTSS                                                      
*                                                                               
* TEMP DEBUG                                                                    
         L     RE,ADMASTC                                                       
         USING MASTD,RE                                                         
         CLI   MCTRACE,C'A'                                                     
         BE    AUTOAP05                                                         
         CLI   MCTRACE,C'P'                                                     
         BE    AUTOAP05                                                         
         DROP  RE                                                               
         B     *+8                 NOT TRACING                                  
AUTOAP05 BRAS  RE,DUMPSS                                                        
*                                                                               
* END TEMP DEBUG                                                                
*                                                                               
*                                                                               
AUTOAP10 GOTOR RESETBLK            RESET AUTOAPPR BLK W/ TABLE ADDRS            
*                                                                               
EQXIT    CR    RB,RB                                                            
         J     EXIT                                                             
NEQXIT   LTR   RB,RB                                                            
EXIT     XMOD1 1                                                                
         EJECT                                                                  
**********************************************************************          
* INITIALIZATION ROUTINE                                             *          
**********************************************************************          
         SPACE 1                                                                
INIT     NTR1  BASE=*,LABEL=*                                                   
         ST    R1,AAUTBLK          SAVE A(CALLER'S AUTO BLOCK)                  
*                                                                               
         ZAP   NUMRECS,=P'0'                                                    
*                                                                               
         LA    R1,ADCONS           RELOCATE ADDRESS CONSTANTS                   
         LHI   R0,ADCONN                                                        
         LA    R2,VTYPES                                                        
         BASR  RE,0                                                             
         ICM   RF,15,0(R1)                                                      
         BZ    *+12                DON'T RELOCATE IF NOT LINKED                 
         A     RF,RELO                                                          
         ST    RF,0(R2)                                                         
         AHI   R1,L'ADCONS                                                      
         AHI   R2,L'ADCONS                                                      
         BCTR  R0,RE                                                            
*                                                                               
         L     RE,AAUTBLK         COPY CALLER'S AUTBLK INTO LOCAL W/S           
         LHI   R1,AUTBLKL                                                       
         LA    R0,AUTBLK                                                        
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
         OC    AUTPCNT,AUTPCNT                                                  
         BNZ   *+10                                                             
         ZAP   AUTPCNT,=P'1000000' DEFAULT IS FULLY MATCH                       
*                                                                               
         USING COMFACSD,RF                                                      
         ICM   RF,15,AUTCOMF       TEST COMFACS RESOLVED                        
         BZ    INITX                                                            
         MVC   DATCON,CDATCON     SET EXTERNAL ADDRESSES FROM COMFACS           
         MVC   BINSRC31,AUTABIN                                                 
         MVC   VHELLO,CHELLO                                                    
         MVC   DATAMGR,CDATAMGR                                                 
         MVC   VADDAY,CADDAY                                                    
         MVC   VGETDAY,CGETDAY                                                  
         MVC   ADMASTC,CMASTC                                                   
         DROP  RF                                                               
*                                                                               
         L     RE,ADMASTC                                                       
         USING MASTD,RE                                                         
         MVC   CTRY,MCCTRY         SAVE OFF COUNTRY                             
         DROP  RE                                                               
*                                                                               
         MVI   FLAG,0              INITIALIZE FLAG                              
         XC    LSTSRKEY,LSTSRKEY                                                
         XC    SVSRPKEY,SVSRPKEY                                                
         XC    CURRMED,CURRMED     CLEAR CURRENT MEDIA                          
*                                                                               
         LA    RE,IO                                                            
         LHI   RF,IOLNQ                                                         
         XCEFL                                                                  
*                                                                               
         GOTO1 ASETMNTH,DMCB,(RC)  SETUP REQUESTED MONTHS TABLE                 
*                                                                               
         CLI   AUTOFSW,C'Y'        ARE WE RUNNING IT OFFLINE                    
         BNE   *+8                                                              
         BRAS  RE,ALLOCBUF         ALLOCATE BUFFER FILES FOR BUFFERIN           
*                                                                               
         BRAS  RE,MAIN             GETMAIN STORAGE                              
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAINI,1 INITIALIZE FIRST BUFFER                 
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAINI,2 INITIALIZE SECND BUFFER                 
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAINI,3 RECEIVABLES PP TABLE                    
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         ICM   R0,15,=AL4(VBUFFSZQ) VENDOR BUFFER SIZE                          
         STORAGE OBTAIN,LENGTH=(R0),LOC=31,COND=YES                             
         LTR   RF,RF                                                            
         JNZ   *+2                                                              
         ST    R1,AVENDBUF                                                      
*                                                                               
         MVI   FILTOFF,C'N'        OFFICE FILTER FLAG                           
         CLI   AUTPRF10,C'Y'                                                    
         BNE   INITX                                                            
*                                                                               
         XC    BSPARS2(BSP2LQ),BSPARS2                                          
         ICM   RF,15,AUTEXTRA      A(EXTRA PARMS)                               
         ICM   RF,15,AACMOFCN-AUTBLKX(RF) A(OFFICE TABLE)                       
         BZ    INITX               NO OFFICE TABLE - NO FILTERING               
         OC    0(2,RF),0(RF)       NUMBER OF OFFICES                            
         BZ    INITX               EMPTY TABLE - NO FILTERING                   
         LA    RE,2(RF)            GO PAST NUMBER OF RECORDS                    
         ST    RE,BSP2_2           A(TABLE)                                     
         MVC   BSP2_3+2(2),0(RF)   NUMBER OF RECORDS                            
         MVI   BSP2_4+3,2          REC LEN                                      
         MVI   BSP2_5+3,2          KEY LEN                                      
         MVC   BSP2_6,=AL4(256)    MAX RECORDS                                  
*                                                                               
         ICM   RF,15,AUTEXTRA      A(EXTRA PARMS)                               
         CLI   LDGOPOSP-AUTBLKX(RF),X'00'                                       
         BE    *+8                                                              
         MVI   FILTOFF,C'Y'                                                     
         CLI   LDGOPOSR-AUTBLKX(RF),X'00'                                       
         BE    *+8                                                              
         MVI   FILTOFF,C'Y'                                                     
*                                                                               
INITX    J     EXIT                                                             
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* BUILD PAYABLES TABLE                                               *          
**********************************************************************          
         SPACE 1                                                                
BLDPAYTB NTR1  BASE=*,LABEL=*                                                   
         CLI   AUTOPT2,C'Y'                                                     
         BE    *+8                                                              
         MVI   AUTOPT2,C'N'                                                     
         MVI   FLAG,0              INITIALIZE FLAG                              
         XC    LSTSRKEY,LSTSRKEY                                                
         XC    SVSRPKEY,SVSRPKEY                                                
         XC    CURRMED,CURRMED                                                  
         XC    SVPSSWRK,SVPSSWRK                                                
*                                                                               
         LA    RE,IO                                                            
         LHI   RF,IOLNQ                                                         
         XCEFL                                                                  
*                                                                               
         CLI   AUTOPT5,C'Y'        ARE WE READING FILE                          
         BNE   BLDPAY10                                                         
*                                                                               
         GOTOR READPAYT            READ PAYABLES TRANSACTIONS                   
         B     BLDPAYX                                                          
*                                                                               
BLDPAY10 GOTO1 ASSPNTTB,DMCB,(RC)  BUILD SS TABLE USING POINTERS                
*                                                                               
BLDPAYX  J     EXIT                                                             
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* BUILD PAYABLES TABLE BY READING FILE/TRANSACTIONS                  *          
**********************************************************************          
         SPACE 1                                                                
         USING TRNKEY,R4                                                        
READPAYT NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         XC    SVKEY,SVKEY         BUILD KEY TO READ PAYABLE RECORDS            
         XC    SVSYS,SVSYS         CLEAR SYSTEM CODE                            
         XC    SVMED,SVMED         CLEAR MEDIA CODE                             
*                                                                               
         LA    R4,SVKEY                                                         
         MVC   TRNKEY,SPACES       INIT KEY                                     
*                                                                               
         MVC   TRNKCPY,AUTCPY      COMPANY CODE                                 
         MVI   TRNKUNT,C'S'        ALWAYS UNIT S                                
         CLC   AUTLDGR,SPACES                                                   
         BNH   RDPAY10                                                          
         MVC   TRNKLDG,AUTLDGR     MOVE IN LEDGER CODE                          
*                                                                               
RDPAY10  GOTO1 ADMRDHI,DMCB,(RC)                                                
         B     RDPAY30                                                          
*                                                                               
RDPAY20  GOTO1 ADMRDSEQ,DMCB,(RC)                                               
RDPAY30  CLC   SVKEY(TRNKLDG-TRNKEY),IOKEY                                      
         BNE   RDPAYX              EXIT IF CHANGE IN UNIT                       
*                                                                               
         LA    R4,IO                                                            
*                                                                               
         CLI   TRNKSTYP,X'09'                                                   
         BE    RDPAY20                                                          
*                                                                               
         CLC   AUTLDGR,SPACES      WAS LEDGER PASSED TO US                      
         BNH   RDPAY40                                                          
         CLC   TRNKLDG,AUTLDGR     DO WE HAVE A MATCHING LEDGER                 
         BNE   RDPAYX              EXIT IF LEDGER NOT FOUND                     
*                                                                               
RDPAY40  CLC   TRNKACT,SPACES      ARE WE AT AN ACCOUNT LEVEL                   
         BNH   RDPAY20                                                          
*                                                                               
         L     RF,ALDSYTAB         CHECK TABLE OF LEDGER/SYSTEM B/C             
RDPAY50  CLI   0(RF),X'FF'         ONLY WANT CERTAIN LEDGERS                    
         BE    RDPAY20             DON'T READ IF NOT IN LIST                    
         CLC   TRNKLDG,0(RF)                                                    
         BE    RDPAY60                                                          
         LA    RF,2(RF)                                                         
         B     RDPAY50                                                          
*                                                                               
RDPAY60  MVC   SVSYS,1(RF)         SAVE SYSTEM FROM TABLE                       
*                                                                               
         CLI   SVSYS,C'S'          IF IT'S SPOT CHECK TO SEE IF IT'S            
         BNE   RDPAY70             REALLY NET BY CHECKING THE 1ST CHAR          
         CLI   TRNKACT,C'N'        OF THE ACCOUNT FOR 'N' (SSN)                 
         BNE   RDPAY70                                                          
         MVI   SVSYS,C'N'                                                       
*                                                                               
RDPAY70  DS    0H                                                               
         CLC   AUTSYSTM,SPACES     WAS SYSTEM PASSED FROM REQUEST               
         BNH   RDPAY80                                                          
         CLC   AUTSYSTM,SVSYS      IS THIS A MATCHING SYSTEM CODE               
         BNE   RDPAY20             READ NEXT                                    
*                                                                               
RDPAY80  DS    0H                                                               
         CLC   TRNKDATE,SPACES                                                  
         BNH   RDPAY20                                                          
         CLC   TRNKREF,SPACES      IS IT A TRANSACTION                          
         BNH   RDPAY20                                                          
         MVC   DSKADDR,TRNKDA      DSKADDR OF CURRENT TRANSACTION               
*                                                                               
         GOTO1 ADMGET,DMCB,(RC),0 GET RECORD                                    
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R4,IO               POINT TO TRANSACTION RECORD                  
*                                                                               
         USING PSSAD,R3                                                         
         LA    R3,PSSAWRK                                                       
         XC    PSSAWRK,PSSAWRK                                                  
         BRAS  RE,ZAPSSBUK         ZAP ALL BUCKETS                              
         XC    AELMNTS(AELMNTSQ),AELMNTS                                        
         NI    FLAG,X'FF'-(FLGDATE+FLGBRD)                                      
*                                                                               
         MVC   PSSKSYS,SVSYS       MOVE SYSTEM TO TABLE                         
         CLI   PSSKSYS,C'N'        IS THIS NET SYSTEM ?                         
         BNE   *+12                                                             
         CLI   AUTPRF2,C'S'        RUN BY SYSTEM OR SYS/MED ?                   
         BE    *+10                                                             
         MVC   PSSKMED,TRNKACT     SAVE OFF MEDIA CODE                          
*                                                                               
         MVC   PSSKCLI,TRNKCACT+9  LAST THREE CHAR OF CONTRA IS CLI             
         MVC   PSSKPRO,TRNKREF     FIRST THREE CHAR OF REFERENCE IS PRO         
*                                                                               
         OC    AUTALST,AUTALST     DO WE HAVE A CLIENT LIST FROM REQST          
         BZ    RDPAY90                                                          
         MVC   CLNT,PSSKCLI                                                     
         BRAS  RE,CHKCLT           CHECK IF IT IS A VALID CLIENT                
         BNE   RDPAY20             READ NEXT TRANSACTION                        
         B     RDPAY110            MATCHING CLIENT FOUND                        
RDPAY90  CLC   AUTCLNT,SPACES      IS CLIENT REQUESTED                          
         BNH   RDPAY100                                                         
         CLC   AUTCLNT,PSSKCLI                                                  
         BNE   RDPAY20                                                          
RDPAY100 CLC   AUTPROD,SPACES      IS PRODUCT REQUESTED                         
         BNH   RDPAY110                                                         
         CLC   AUTPROD,PSSKPRO                                                  
         BNE   RDPAY20                                                          
*                                                                               
RDPAY110 MVC   PSSKSTN,TRNKULA     SS ACCOUNT IS STATION                        
         MVC   PSSKOFF,TRNKOFF     OFFICE CODE                                  
         MVC   PSSKDA,DSKADDR      SAVE DISK ADDRESS TO MARK REC LATER          
*                                                                               
         LR    R5,R4                                                            
         AH    R5,=Y(TRNRFST-TRNKEY) POINT TO FIRST ELEMENT                     
         CLI   0(R5),X'44'         MAKE SURE FIRST ELEMENT IS 44                
         BE    RDPAY140                                                         
         DC    H'0'                                                             
*                                                                               
RDPAY120 CLI   0(R5),0                                                          
         BE    RDPAY240                                                         
         CLI   0(R5),GDAELQ        IS IT X'E5' ELEMENT                          
         BE    RDPAY180                                                         
         CLI   0(R5),MBIELQ        IS IT X'F3' ELEMENT                          
         BE    RDPAY190                                                         
         CLI   0(R5),OTHELQ        IS IT X'23' ELEMENT                          
         BE    RDPAY200                                                         
         CLI   0(R5),XPYELQ        IS IT X'46' ELEMENT                          
         BE    RDPAY210                                                         
         CLI   0(R5),MDTELQ        IS IT X'1A' ELEMENT                          
         BE    RDPAY220            FOR FUTURE USE                               
         CLI   0(R5),MDPELQ        IS IT X'6A' ELEMENT                          
         BE    RDPAY230            FOR FUTURE USE                               
*                                                                               
RDPAY130 SR    R1,R1                                                            
         IC    R1,1(R5)            GET LENGTH OF ELEMENT                        
         AR    R5,R1                                                            
         B     RDPAY120                                                         
*                                                                               
         USING TRNELD,R5                                                        
RDPAY140 TM    TRNSTAT,TRNSDR      IS IT DEBIT                                  
         BO    RDPAY20             DISCARD ALL DEBITS                           
*                                                                               
         ZAP   PSSCLRB,TRNAMNT     CLEARED AMOUNT                               
*                                                                               
         TM    TRNRSTA2,TRNSUSED   HAS IT BEEN USED                             
         BNO   RDPAY150                                                         
         ZAP   PSSDISBB,TRNAMNT    CREDIT AND USED DATE (DISBURSED)             
         MVI   PSSKSTAT,PSSKDISB   DISBURSED ITEM                               
         B     RDPAY130                                                         
*                                                                               
RDPAY150 DS    0H                                                               
         ZAP   PSSUDISB,TRNAMNT    ALL OF UNDISBURSED AMOUNT                    
*                                                                               
         TM    TRNSTAT,TRNSHOLD    IS THIS PAYMENT ON HOLD                      
         BNO   RDPAY160                                                         
         ZAP   PSSHELDB,TRNAMNT    HELD AMOUNT                                  
         MVI   PSSKSTAT,PSSKHELD   ADD TO HELD ITEM LINE                        
         B     RDPAY130                                                         
*                                                                               
RDPAY160 TM    TRNSTAT,TRNSAPPR    SELECTED FOR PAYMENT                         
         BNO   RDPAY170                                                         
         ZAP   PSSAPPRB,TRNAMNT    APPROVED AMOUNT                              
         MVI   PSSKSTAT,PSSKPREV   ADD TO PREVIOUSLY APPROVED ITEM              
         B     RDPAY130                                                         
*                                                                               
RDPAY170 ZAP   PSSUNAPB,TRNAMNT    UNAPPROVED AMOUNT                            
         MVI   PSSKSTAT,PSSKUNAP   ADD TO UNAPPROVED LINE                       
         B     RDPAY130                                                         
*                                                                               
         USING GDAELD,R5                                                        
RDPAY180 CLI   GDATYPE,GDAMMOS     MEDIA MOS (PAYABLES)                         
         BNE   RDPAY182                                                         
         ST    R5,AGDAELD          SAVE ADDRESS OF X'E5' ELEMENT                
         B     RDPAY130                                                         
*                                                                               
RDPAY182 CLI   GDATYPE,GDATRECN                                                 
         BNE   RDPAY130                                                         
         MVC   PSSINVCD,GDADATE2   INVOICE CLEARED DATE                         
         B     RDPAY130                                                         
*                                                                               
RDPAY190 ST    R5,AMBIELD                                                       
         B     RDPAY130                                                         
*                                                                               
         USING OTHELD,R5                                                        
RDPAY200 CLI   OTHPROF-3,C' '                                                   
         BH    RDPAY130                                                         
         ST    R5,AOTHELD          SAVE ADDRESS OF X'23' ELEMENT                
         B     RDPAY130                                                         
*                                                                               
RDPAY210 ST    R5,AXPYELD          SAVE ADDRESS OF X'46' ELEMENT                
         B     RDPAY130                                                         
*                                                                               
RDPAY220 ST    R5,AMDTELD          SAVE ADDRESS OF X'1A' ELEMENT                
         B     RDPAY130                                                         
*                                                                               
RDPAY230 ST    R5,AMDPELD          SAVE ADDRESS OF X'6A' ELEMENT                
         B     RDPAY130                                                         
*                                                                               
         USING GDAELD,R5                                                        
RDPAY240 DS    0H                                                               
         OC    AGDAELD,AGDAELD     X'E5'                                        
         BZ    RDPAY250                                                         
         L     R5,AGDAELD                                                       
         MVC   PSSKMOS,GDAYYMM                                                  
         OI    FLAG,FLGDATE        MOS FOUND IN THIS ELEMENT                    
         B     RDPAY270                                                         
*                                                                               
         USING MBIELD,R5                                                        
RDPAY250 OC    AMBIELD,AMBIELD     X'F3'                                        
         BZ    RDPAY260                                                         
         L     R5,AMBIELD                                                       
         MVC   PSSKMOS,MBIMOS                                                   
         MVC   PSSKEST,SPACES                                                   
         MVC   PSSKEST(L'MBIEST),MBIEST                                         
         OI    FLAG,FLGDATE        MOS FOUND IN THIS ELEMENT                    
         B     RDPAY270                                                         
*                                                                               
         USING OTHELD,R5                                                        
RDPAY260 OC    AOTHELD,AOTHELD     X'23'                                        
         BZ    RDPAY270                                                         
         L     R5,AOTHELD                                                       
         MVC   PSSKMOS,OTHDATE     MOVE MOS                                     
         OI    FLAG,FLGDATE        MOS FOUND IN THIS ELEMENT                    
*                                                                               
         USING XPYELD,R5                                                        
RDPAY270 OC    AXPYELD,AXPYELD     X'46'                                        
         BNZ   RDPAY280                                                         
         MVI   PSSKTYP,AAVPNEST    NO INVOICE NUMBER                            
         B     RDPAY350                                                         
*                                                                               
RDPAY280 L     R5,AXPYELD                                                       
         CLI   PSSKSYS,C'P'        PRINT?                                       
         BNE   RDPAY285                                                         
         CLI   AUTPRF5,C'Y'        USING PRINT BILLABLE MOS?                    
         BNE   RDPAY285                                                         
         CLI   XPYLN,XPYLN3Q                                                    
         BL    RDPAY285                                                         
         OC    XPYBLDTE,XPYBLDTE   BILLABLE DATE PRESENT?                       
         BZ    RDPAY285                                                         
         MVC   PSSKMOS,XPYBLDTE                                                 
         OI    FLAG,FLGDATE        MOS FOUND IN THIS ELEMENT                    
*                                                                               
RDPAY285 DS    0H                                                               
         CLI   PSSKSYS,C'N'        IS SYSTEM NETWORK                            
         BNE   RDPAY290                                                         
         CLC   XPYSMED,SPACES      IF NO MEDIA/SUBMEDIA THEN LEAVE              
         BNH   RDPAY290            MEDIA AS 'N' FOR NOW                         
         CLI   AUTPRF2,C'S'        RUN BY SYSTEM OR SYS/MED ?                   
         BE    *+10                                                             
         MVC   PSSKMED,XPYSMED     MOVE IN MEDIA FOR SYSTEM C'N' NETWRK         
*                                                                               
RDPAY290 TM    FLAG,FLGDATE        DO WE HAVE MOS                               
         BO    RDPAY310                                                         
*                                                                               
         CLI   TRNKCULA+3,C'N'       IF NETWORK                                 
         BE    *+8                                                              
         OI    FLAG,FLGBRD           GET DATE FROM BROADCAST CAL                
         MVC   WORK(L'XPYPER),XPYPER                                            
         LA    R2,WORK                                                          
         CLI   WORK+6,C' '                                                      
         BNH   *+8                                                              
         LA    R2,WORK+6                                                        
         CLI   WORK+6,C'-'                                                      
         BNE   *+8                                                              
         LA    R2,WORK+7                                                        
         TM    FLAG,FLGBRD                                                      
         BNO   RDPAY300                                                         
         GOTO1 VGETBRD,DMCB,(1,(R2)),DTE1,VGETDAY,VADDAY                        
         LA    R2,DTE2                                                          
RDPAY300 GOTO1 DATCON,DMCB,(0,(R2)),(1,SVMOS)                                   
         MVC   PSSKMOS,SVMOS                                                    
*                                                                               
RDPAY310 DS    0H                                                               
         CLI   PSSKSYS,C'P'        PRINT?                                       
         BNE   RDPAY315                                                         
         CLI   AUTPRF5,C'Y'        USING PRINT BILLABLE MOS?                    
         BNE   RDPAY315                                                         
         CLI   XPYLN,XPYLN3Q                                                    
         BL    RDPAY315                                                         
         OC    XPYBLDTE,XPYBLDTE   BILLABLE DATE PRESENT?                       
         BO    RDPAY315                                                         
*                                                                               
* MUST OBTAIN BILLABLE DATE USING REVERSE INSTAB LOGIC                          
         MVC   SVMOS(L'PSSKMOS),PSSKMOS                                         
         OI    FLAG,FLGINSRV                                                    
         GOTOR GTINSRTN            GET INSERTION DATE                           
         NI    FLAG,X'FF'-FLGINSRV                                              
         MVC   PSSKMOS,SVMOS                                                    
*                                                                               
RDPAY315 DS    0H                                                               
         MVC   PSSKINV,XPYINV      MOVE INVOICE # TO TABLE                      
*                                                                               
         CLI   PSSKSYS,C'P'                                                     
         BE    *+8                                                              
         MVI   PSSKINV+L'PSSKINV-1,C' '                                         
*                                                                               
         MVC   PSSKEST,SPACES                                                   
         OC    XPYEST,XPYEST                                                    
         BZ    RDPAY320                                                         
         EDIT  XPYEST,(3,PSSKEST),FILL=0                                        
         CLC   PSSKEST+3(3),SPACES                                              
         BNH   RDPAY350                                                         
         MVI   PSSKTYP,AAVPBEST    BAD ESTIMATE                                 
         B     RDPAY350                                                         
*                                                                               
         USING MDTELD,R5                                                        
RDPAY320 L     R5,AMDTELD          LOAD ADDRESS OF 1A                           
         OC    AMDTELD,AMDTELD                                                  
         BNZ   RDPAY330                                                         
         OC    AMDPELD,AMDPELD                                                  
         BNZ   *+12                                                             
         MVI   PSSKTYP,AAVPNEST    NO ESTIMATE OR MEDIA CODE                    
         B     RDPAY350                                                         
         L     R5,AMDPELD          LOAD ADDRESS OF 6A                           
RDPAY330 DS    0H                                                               
         CLI   MDTSYS,C'J'                                                      
         BE    RDPAY20             SKIP THIS TRANSACTION                        
*                                                                               
         MVC   PSSKEST,MDTEST                                                   
         CLC   PSSKEST+3(3),SPACES                                              
         BE    *+12                                                             
         MVI   PSSKTYP,AAVPBEST    BAD ESTIMATE                                 
         B     RDPAY350                                                         
*                                                                               
         LA    RE,PSSKEST          POINT TO ESTIMATE                            
         LHI   R1,3                                                             
         LA    RE,MDTEST                                                        
*                                                                               
RDPAY340 CLI   0(RE),X'F0'         MUST BE BETWEEN ZERO AND NINE                
         BL    *+12                                                             
         CLI   0(RE),X'F9'                                                      
         BNH   *+12                                                             
         MVI   PSSKTYP,AAVPBEST    BAD ESTIMATE                                 
         B     RDPAY350                                                         
         LA    RE,1(RE)            BUMP TO NEXT BYTE IN ESTIMATE                
         BCT   R1,RDPAY340                                                      
*                                                                               
RDPAY350 DS    0H                                                               
         CLI   PSSKSYS,C'N'        IS THIS NET SYSTEM ?                         
         BNE   *+12                                                             
         CLI   AUTPRF2,C'S'        RUN BY SYSTEM OR SYS/MED ?                   
         BE    RDPAY360                                                         
         CLC   AUTMEDIA,SPACES                                                  
         BNH   RDPAY360                                                         
         CLC   AUTMEDIA,PSSKMED   DOES MEDIA MATCH REQUEST                      
         BNE   RDPAY20            GET NEXT TRANSACTION                          
*                                                                               
RDPAY360 CLC   AUTEST,SPACES                                                    
         BNH   RDPAY370                                                         
         CLC   AUTEST,PSSKEST     DOES ESTIMATE MATCH REQUEST                   
         BNE   RDPAY20            GET NEXT TRANSACTION                          
*                                                                               
RDPAY370 CLC   PSSKMOS,AUTSMOS    IS MOS <  START DATE                          
         BL    RDPAY410           ADD TO PRIOR ENTRY AND TOTALS                 
         CLC   PSSKMOS,AUTEMOS    IS MOS > END DATE                             
         BH    RDPAY430           NO, ADD IT AS DETAIL ENTRY                    
*                                                                               
         MVI   PSSKRTYP,X'05'     DETAIL RECORD BY MOS                          
         CLI   PSSKSTAT,PSSKUNAP                                                
         BE    RDPAY380                                                         
         CLI   AUTOPT2,C'Y'       DO WE WANT TO EXPAND                          
         BE    RDPAY380           YES                                           
         XC    PSSKDA,PSSKDA      DON'T EXPAND,LEAVE IT UNIQUE                  
*                                                                               
RDPAY380 GOTO1 ATSAR,DMCB,(RC),TSAADD,2    ADD TO BUFFER # 2                    
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLI   AUTOPT2,C'Y'        SHOW PREV APPR & UNAPPR AS 2 LINES           
         BE    RDPAY400                                                         
         CLI   PSSKSTAT,PSSKPREV   PREVIOUSLY APPROVED TO TOTAL                 
         BE    RDPAY390                                                         
         CLI   PSSKSTAT,PSSKUNAP   UNAPPROVED TO TOTALS                         
         BNE   RDPAY400                                                         
RDPAY390 LA    RE,PSSAWRK+(PSSKSTAT-PSSAKEY) CLEAR STATUS ONWARDS.              
         XC    0(PSSKLNQ2,RE),0(RE)                                             
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2    ADD TO BUFFER # 2                    
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
RDPAY400 LA    RE,PSSAWRK+(PSSKSTN-PSSAKEY) CLEAR STATUS ONWARDS.               
         XC    0(PSSKLNQ3,RE),0(RE)                                             
         MVI   PSSKRTYP,X'03'     MOS TOTAL                                     
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2    ADD TO BUFFER # 2                    
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
         B     RDPAY440           ADD TO TOTALS, DON'T ADD TO PRIORS            
*                                                                               
RDPAY410 DS    0H                                                               
         CP    PSSUNAPB,=P'0'                                                   
         BZ    RDPAY420                                                         
         OI    PSSFLAG,PSSPREV    UNAPPR ITEM OLDER THAN REQESTED MOS           
*                                                                               
RDPAY420 LA    RE,PSSAWRK+(PSSKRTYP-PSSAKEY) CLEAR REC TYPE ONWARDS.            
         XC    0(PSSKLNQ1,RE),0(RE)                                             
         MVI   PSSKRTYP,X'01'      PRIOR MOS'S TOTALS                           
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2    ADD TO BUFFER # 2                    
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
         B     RDPAY440                                                         
*                                                                               
RDPAY430 LA    RE,PSSAWRK+(PSSKRTYP-PSSAKEY) CLEAR REC TYPE ONWARDS.            
         XC    0(PSSKLNQ1,RE),0(RE)                                             
         MVI   PSSKRTYP,X'04'      FUTURE MOS'S TOTALS                          
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2    ADD TO BUFFER # 2                    
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLI   AUTOPT6,C'Y'        APPROVING BY MOS?                            
         BE    RDPAY440                                                         
         CP    PSSUNAPB,=P'0'     IS THIS UNAPPROVED ITEM                       
         BNZ   RDPAY20            EXCLUDE POST REQUEST UNAPPROVED ITEMS         
*                                                                               
RDPAY440 DS    0H                                                               
         LA    RE,PSSAWRK+(PSSKRTYP-PSSAKEY) CLEAR REC TYPE ONWARDS.            
         XC    0(PSSKLNQ1,RE),0(RE)                                             
         MVI   PSSKRTYP,X'00'      ALL TOTALS                                   
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2    ADD TO BUFFER # 2                    
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
         B     RDPAY20                                                          
*                                                                               
RDPAYX   J     EXIT                                                             
         DROP  R3,R4,R5                                                         
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* ACQUIRE BUFFERS PAYBUF AND RCVBUF                                  *          
* THIS ROUTINE ALLOCATES BUFFERS DYNAMICALLY SO THERE IS NO NEED TO  *          
* PUT DD STATEMENT IN SOON JCLS (DDS.PROCLIB(MONSTA))                *          
* GENERATES PAYBUFD DD   UNIT=SYSDA,SPACE=(CYL,(400,200))            *          
*           RCVBUFD DD   UNIT=SYSDA,SPACE=(CYL,(200,100))            *          
* IF YOU NEED MORE SPACE ASSIGN BIGGER NUMBER TO CYLINDERS           *          
**********************************************************************          
         SPACE 1                                                                
ALLOCBUF NTR1  BASE=*,LABEL=*                                                   
         GOTO1 ADYNALOC,DMCB,(X'80',=CL8'PAYBUFD '),                   +        
               (X'40',=XL6'000400000064')                                       
         GOTO1 ADYNALOC,DMCB,(X'80',=CL8'RCVBUFD '),                   +        
               (X'40',=XL6'0000C8000064')                                       
         GOTO1 ADYNALOC,DMCB,(X'80',=CL8'SRPPBUFD'),                   +        
               (X'40',=XL6'000400000064')                                       
*                                                                               
ALBUFX   J     EXIT                                                             
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* ACQUIRE CORE FOR BINARY TABLES                                     *          
**********************************************************************          
         SPACE 1                                                                
MAIN     NTR1  BASE=*,LABEL=*                                                   
         L     R0,=A(PSSRTKLQ*PSSRTNQ)                                          
         GETMAIN RU,LV=(0),LOC=(ANY,ANY)                                        
         ST    R1,ABUFF                  SAVE A(BUFFER)                         
*                                                                               
         LA    R0,TEMPWRK                                                       
         SR    R2,R2                                                            
         LHI   R3,PSSRTKLQ                                                      
         LHI   R4,PSSRTKLQ                                                      
         LHI   R5,PSSRTNQ                                                       
         STM   R0,R5,BSPARS                                                     
*                                                                               
MAINX    J     EXIT                                                             
         LTORG                                                                  
         EJECT                                                                  
*********************************************************************           
* FREE UP ACQUIRED CORE                                                         
*********************************************************************           
         SPACE 1                                                                
WRAP     NTR1                                                                   
         L     R1,ABUFF                                                         
         L     R0,=A(LENBUFF)                                                   
         FREEMAIN RU,A=(1),LV=(0)                                               
*                                                                               
         L     RE,ADMASTC                                                       
         USING MASTD,RE                                                         
         XC    MCUSRDMP(8),MCUSRDMP CLEAR OUT EXTRA DUMP AREA                   
         J     EXIT                                                             
         DROP  RE                                                               
         EJECT                                                                  
**********************************************************************          
* READ SR TRANSACTIONS AND MATCH WITH SS BINTABLE TRANSACTIONS       *          
* IF YOU FIND MATCHING CLI/PRO/EST/MOS THEN UPDATE TWO BUCKETS       *          
**********************************************************************          
         USING TSRAD,R4                                                         
         USING PSSAD,R2                                                         
         SPACE 1                                                                
BLDSRBUK NTR1  BASE=*,LABEL=*                                                   
*                                                                               
BLDSR10  CLI   AUTOPT5,C'Y'        ARE WE READING FILE                          
         BE    BLDSR20                                                          
         GOTOR SRPNTRTB            BUILD SR TRANSACTION TABLE FRM PNTR          
         B     BLDSR30                                                          
*                                                                               
BLDSR20  GOTOR BLDSRTAB            BUILD SR TRANSACTION TABLE FRM FILE          
*                                                                               
BLDSR30  DS    0H                                                               
*        NI    FLAG1,X'FF'-FLG1NOVQ                                             
         XC    TSRAWRK,TSRAWRK                                                  
         GOTO1 ATSAR,DMCB,(RC),TSARDH,1     GET RECORD IN TSRAWRK               
         TM    BCTSERRS,TSEEOF              NO MORE SR RECORDS                  
         BO    BLDSRX                                                           
*                                                                               
         LA    R4,TSRAWRK                                                       
*                                                                               
* SR BUFFER LOOP                                                                
BLDSR40  DS    0H                                                               
         OC    TSRKSYS,TSRKSYS                                                  
         BZ    BLDSR120            SKIP BILL TOTALS                             
*                                                                               
* HAVE VENDOR-LEVEL SR DETAILS HERE                                             
* FIND CORRESPONDING SS DETAIL                                                  
         LA    R2,PSSAWRK                                                       
         XC    PSSAWRK,PSSAWRK                                                  
         BRAS  RE,ZAPSSBUK         ZAP ALL BUCKETS                              
*                                                                               
* BUILD VENDOR-LEVEL SS KEY FROM SR RECORD                                      
         MVI   PSSKTYP,X'00'       UPDATE GOOD ESTIMATES ONLY                   
         MVC   PSSKSYS,TSRKSYS     MOVE IN SYSTEM                               
         MVC   PSSKMED,TSRKMED     MOVE IN MEDIA CODE                           
         MVC   PSSKCLI,TSRKCLI     CLIENT CODE                                  
         MVC   PSSKPRO,TSRKPRO     PRODUCT CODE                                 
         MVC   PSSKEST,TSRKEST     ESTIMATE                                     
         MVI   PSSKRTYP,X'05'                                                   
         MVC   PSSKMOS,TSRKMOS     MONTH OF SERVICE                             
         MVC   PSSKVEN,TSRKVCOD    VENDOR CODE                                  
*                                                                               
* TZIH 03/20/2017 - MEDIA FILTERING MOVED TO SRPNTRTB                           
*        CLC   CURRMED,TSRKMED                                                  
*        BNE   BLDSR120                                                         
*                                                                               
         MVC   PSSAWRK0,PSSAWRK    SAVE TOTALS-BY-VENDOR KEY                    
*                                                                               
* IF MANUAL BILL, THEN DON'T LOOK FOR SS RECORDS                                
* SINCE THERE IS NO VENDOR FOR MANUAL BILLS,                                    
* WE WON'T FIND EITHER VENDOR TOTALS OR PAYABLES BY VENDOR CODE                 
* JUST USE THE SS KEY TO UPDATE MOS AND CPE, TOTALS, AND ADD SR DETAIL          
         MVI   BCTSERRS,0                                                       
         CLC   =C'MANUAL',PSSKVEN                                               
         BE    BLDSR50                                                          
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSARDH,2     READ INTO PSSAWRK                   
         TM    BCTSERRS,TSEEOF     IS THIS END OF FILE                          
         BO    BLDSR45             NO SS FOUND FOR THIS SR                      
         CLC   PSSAWRK(PSSKSTN-PSSAD),PSSAWRK0 SAME C/P/E/MOS/VENDOR?           
         BE    BLDSR52                                                          
*                                                                               
* NO VENDOR INVOICES FOUND FOR THIS RECEIVABLE                                  
* ADD FAKE VENDOR-LEVEL SS ENTRY AND FAKE PAYABLE                               
BLDSR45  DS    0H                                                               
*        OI    FLAG1,FLG1NOVQ      INDICATE SS NOT FOUND                        
         MVC   PSSAWRK,PSSAWRK0    USE VENDOR-LEVEL KEY                         
         MVC   PSSAWRK5,PSSAWRK    FAKE CURRENT SS PAYABLE RECORD               
*                                                                               
         MVC   PSSAWRK,PSSAWRK0    RESTORE VENDOR-LEVEL RECORD                  
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2                                         
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
* ADD FAKE PAYABLE                                                              
         MVC   PSSAWRK+PSSKSTN-PSSAD(4),=C'NONE'                                
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2                                         
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
* NOW THAT FAKE PAYABLE AND FAKE VENDOR-LEVEL SS ENTRY HAVE BEEN ADDED          
* GO AND RE-PROCESS CURRENT SR BUFFER ENTRY                                     
         B     BLDSR40                                                          
*                                                                               
BLDSR50  DS    0H                                                               
         TM    BCTSERRS,TSEEOF     IS THIS END OF FILE                          
         BO    BLDSR120            NEXT SR RECORD                               
*                                                                               
         CLC   PSSAWRK(PSSKSTN-PSSAD),PSSAWRK0 SAME C/P/E/MOS/VENDOR?           
         BNE   BLDSR120            NEXT SR RECORD                               
*                                                                               
BLDSR52  DS    0H                                                               
         OC    PSSKSTN,PSSKSTN     IS THIS VENDOR TOTALS RECORD?                
         BNZ   BLDSR60             NO                                           
*                                                                               
* HAVE SS VENDOR TOTALS RECORD HERE                                             
* UPDATE THE SR DEBIT/CREDIT BUCKETS                                            
*                                                                               
         BRAS  RE,ZAPSSBUK         ZAP ALL BUCKETS                              
         ZAP   PSSBILLB,TSRADR     BILLED AMOUNT                                
         BZ    *+8                                                              
         OI    PSSFLAG,PSSBZERO    PRINT ZERO'S FOR IN'S AND OUT'S              
         ZAP   PSSRCVDB,TSRACR     RECEIVED AMOUNT, CALCULATED CREDIT           
         BZ    *+8                                                              
         OI    PSSFLAG,PSSRZERO    PRINT ZERO'S FOR IN'S AND OUT'S              
*                                                                               
* DON'T UPDATE THE VENDOR TOTALS, IF VENDOR IS "MANUAL"                         
         CLC   =C'MANUAL',PSSKVEN                                               
         BE    BLDSR55             FOR MANUAL BILLS SKIP TO MOS TOTALS          
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2                                         
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
* TEMP DEBUG                                                                    
* RECORD KEY IN HEX, UP TO PSSKSTAT                                             
*        LHI   R3,PSSAKLNQ                                                      
*        GOTO1 =A(TRACE),DMCB,(X'02',(R3)),PSSAWRK,=C'UPD SS'                   
*        GOTO1 =A(TRACE),DMCB,(X'01',(R3)),PSSAWRK,0                            
*                                                                               
* UPDATE SS MOS TOTALS                                                          
BLDSR55  DS    0H                                                               
         MVI   PSSKRTYP,X'03'                                                   
         XC    PSSKVEN(PSSKLVENFWQ),PSSKVEN                                     
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2                                         
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
* UPDATE C/P/E TOTALS                                                           
         MVI   PSSKRTYP,X'00'                                                   
         XC    PSSKRTYP(PSSKLNQ1),PSSKRTYP                                      
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2                                         
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
* READ HIGH FOR THE TOTALS-BY-VENDOR RECORD TO RESTORE READ SEQUENCE            
         XC    PSSAWRK,PSSAWRK                                                  
         MVC   PSSAWRK(PSSAKLNQ),PSSAWRK0 RESTORE TOTALS-BY-VENDOR KEY          
*                                                                               
* IF NO VENDOR INVOICES, JUST ADD SR INFORMATION                                
* JUST AS WE DO FOR MANUAL BILLS                                                
*        TM    FLAG1,FLG1NOVQ      NO VENDOR INVOICES?                          
*        BO    BLDSR57             JUST ADD SR INFORMATION                      
*                                                                               
* IF MANUAL BILL, THERE IS NO VENDOR TOTAL OR PAYABLES                          
* JUST ADD THE SR DETAIL                                                        
         CLC   =C'MANUAL',PSSKVEN                                               
         BNE   *+12                                                             
BLDSR57  BRAS  RE,ZAPSSBUK         ZAP ALL BUCKETS                              
         B     BLDSR65                                                          
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSARDH,2     READ INTO PSSAWRK                   
         CLI   BCTSERRS,0                                                       
         BE    BLDSR80             NEXT SS RECORD                               
         DC    H'0'                TOTALS-BY-VENDOR RECORD NOT FOUND            
*                                                                               
BLDSR60  DS    0H                                                               
         OC    PSSSRAC,PSSSRAC     SR DETAILS?                                  
         BNZ   BLDSR80             YES - SKIP THESE                             
*                                                                               
* SPECIFIC SS PAYABLE (INVOICE) HERE                                            
* FOR EACH SPECIFIC RECORD ADD THE MATCHING SR DETAIL                           
*                                                                               
         MVC   PSSAWRK5,PSSAWRK    SAVE CURRENT SS PAYABLE RECORD               
*                                                                               
* BUILD UP THE KEY WITH SR INFORMATION                                          
*                                                                               
BLDSR65  DS    0H                                                               
         MVC   PSSSRAC,TSRKACT     SR ACCOUNT                                   
         MVC   PSSSRCAC,TSRKCAC    SR CONTRA ACCOUNT                            
         MVC   PSSSRBNO,TSRKBNO    SR BILL NUMBER                               
         MVC   PSSSRBDA,TSRKBDA    SR BILL DATE                                 
* XC DISK ADDRESS, SO "MARKIT" ROUTINE IN ACREPAB02 DOESN'T PROCESS IT          
* 3/2/17 DISK ADDRESS NO LONGER XC'D, TO ALWAYS EXPAND SS TRANSACTIONS          
*        XC    PSSKDA,PSSKDA                                                    
*                                                                               
* UPDATE DATA FIELDS                                                            
         MVC   PSSVNAM,TSRKVNAM    VENDOR NAME                                  
         MVC   PSSSRDDA,TSRKDUDT   CLIENT INVOICE DUE DATE                      
         MVC   PSSSRDPD,TSRKDPDT   CLIENT DEPOSIT DATE                          
*                                                                               
         ZAP   PSSBILLB,TSRADR     BILLED AMOUNT                                
         BZ    *+8                                                              
         OI    PSSFLAG,PSSBZERO    PRINT ZERO'S FOR IN'S AND OUT'S              
         ZAP   PSSRCVDB,TSRACR     RECEIVED AMOUNT, CALCULATED CREDIT           
         BZ    *+8                                                              
         OI    PSSFLAG,PSSRZERO    PRINT ZERO'S FOR IN'S AND OUT'S              
*                                                                               
* IF WE HAVE NO SS DETAILS FOR THIS RECEIVABLE                                  
* PUT 'NONE' IN PSSKSTN, SO THE SS RECORD DOESN'T LOOK LIKE                     
* A VENDOR TOTAL (WHICH HAS THIS FIELD EMPTY)                                   
*        TM    FLAG1,FLG1NOVQ                                                   
*        BZ    *+10                                                             
*        MVC   PSSKSTN(4),=C'NONE'                                              
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2                                         
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
         AP    NUMRECS,=P'1'                                                    
*                                                                               
* IF MANUAL BILL, STOP LOOPING THROUGH SR BUFFER, NOTHING ELSE THERE            
* SR DETAILS FOR "MANUAL" VENDOR HAS BEEN ADDED, WE'RE DONE                     
* WITH THIS SR.  GO READ NEXT BLOCK OF SR TRANSACTIONS                          
         CLC   =C'MANUAL',PSSKVEN                                               
         BE    BLDSR10                                                          
*                                                                               
         XC    PSSAWRK,PSSAWRK                                                  
         MVC   PSSAWRK(PSSAKLNQ),PSSAWRK5 RESTORE INVOICE PAYABLE               
         GOTO1 ATSAR,DMCB,(RC),TSARDH,2                                         
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
* IF WE DON'T HAVE SS INFORMATION FOR CURRENT SR                                
* THEN DON'T LOOP THROUGH SS BUFFER - CONTINUE WITH NEXT SR DETAIL              
*        TM    FLAG1,FLG1NOVQ                                                   
*        BZ    *+12                                                             
*        NI    FLAG1,X'FF'-FLG1NOVQ                                             
*        B     BLDSR120                                                         
*                                                                               
* NEXT SS RECORD FOR THIS C/P/E/MOS/VEN                                         
BLDSR80  DS    0H                                                               
         GOTO1 ATSAR,DMCB,(RC),TSANXT,2        GET NXT RECORD                   
         B     BLDSR50                                                          
*                                                                               
BLDSR120 GOTO1 ATSAR,DMCB,(RC),TSANXT,1   GET NXT RECORD IN TSRAWRK             
         TM    BCTSERRS,TSEEOF     IS THIS END OF FILE                          
         BNO   BLDSR40             PROCESS NEXT SR BUFFER ENTRY                 
         B     BLDSR10             READ NEXT SR TRANSACTION BLOCK               
*                                                                               
BLDSRX   DS    0H                                                               
         J     EXIT                                                             
         DROP  R2,R4                                                            
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* READ SR PASSIVE PNTRS AND BUILD TABLE BY REFERENCE NUMBER          *          
* APPLY PERCENT TO EACH DEBIT ENTRY BY BILL NUMBER/EST/MOS           *          
**********************************************************************          
         USING AARPKEY,R4                                                       
         USING PSSAD,R2                                                         
         SPACE 1                                                                
SRPNTRTB NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         XC    SVKEY,SVKEY         BUILD KEY TO READ SR RECDS                   
         NI    FLAG2,X'FF'-FL2TYP9Q                                             
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAINI,1 REINIT FIRST BUFFER                     
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         OC    SVSRPKEY,SVSRPKEY   IS THIS FIRST TIME IN                        
         BNZ   SRPNT170            PROCESS NEXT SR POINTER                      
*                                                                               
         USING PSSAD,R2                                                         
         XC    PSSAWRK,PSSAWRK                                                  
         GOTO1 ATSAR,DMCB,(RC),TSARDH,2 GET FIRST RECORD IN PSSAWRK             
         TM    BCTSERRS,TSEEOF     IS THIS END OF FILE                          
         BO    SRPNTX                                                           
         MVC   SVPSSWRK,PSSAWRK    SAVE OFF CURRENT SS KEY                      
*                                                                               
SRPNT10  LA    R2,PSSAWRK                                                       
         CLI   PSSKTYP,X'00'       ARE THESE GOOD ESTIMATES RECORD              
         BNE   SRPNTX              NO, BAD ESTIMATES COMING UP, EXIT            
         CLI   PSSKRTYP,X'00'      CPE TOTALS RECORD?                           
         BNE   SRPNT230                                                         
*                                                                               
         LA    R4,SVKEY                                                         
         XC    AARPKEY,AARPKEY     INIT KEY                                     
         MVI   AARPTYP,AARPTYPQ    AA RCV PASSIVE POINTER                       
         MVI   AARPSUB,AARPSUBQ    AA RCV PASSIVE POINTER SUB TYPE              
         MVC   AARPCPY,AUTCPY      COMPANY                                      
         MVI   AARPIND,AARPIEST    ESTIMATE CODE POINTER                        
         MVC   AARPCLT,PSSKCLI     3 BYTES CLIENT CODE                          
         MVC   AARPPRD,PSSKPRO     3 BYTES PRODUCT CODE                         
         MVC   AARPEST,PSSKEST     ESTIMATE CODE                                
         MVC   AARPSYS,PSSKSYS     SYSTEM                                       
*                                                                               
         MVC   CURRMED,PSSKMED     SAVE MEDIA,CURRENTLY WORKING WITH            
*                                                                               
         MVC   SVSYS,PSSKSYS                                                    
         MVC   SVAGY,PSSAGY                                                     
         DROP  R2                                                               
*                                                                               
         GOTO1 ADMRDHI,DMCB,(RC)                                                
         B     SRPNT30                                                          
*                                                                               
SRPNT20  GOTO1 ADMRDSEQ,DMCB,(RC)  READ NEXT SR POINTER                         
*                                                                               
SRPNT30  CLC   SVKEY(AARPOFF-AARPKEY),IOKEY   SAME C/P/E/SYS?                   
         BNE   SRPNT230            GET NEXT ENTRY FROM SS TABLE                 
*                                                                               
         MVC   SVSRPKEY,IOKEY      SAVE SR POINTER KEY                          
*                                                                               
         LA    R2,TSRAWRK                                                       
         XC    TSRAWRK,TSRAWRK     CLEAR WORK AREA                              
         XC    TSRAWRK2,TSRAWRK2   CLEAR SAVED TSRKEY                           
         ZAP   SVTRNAMT,=P'0'                                                   
         NI    FLAG,X'FF'-FLGSR    CLEAR FLAG                                   
*                                                                               
         GOTO1 ADMGET,DMCB,(RC),0 GET RECORD                                    
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
* CHECK IF WE PROCESSED THIS TRANSACTION BLOCK ALREADY                          
*                                                                               
         XC    SRPPWRK,SRPPWRK                                                  
         MVC   SRPPWRK(L'IOKEY-1),IOKEY                                         
         GOTO1 ATSAR,DMCB,(RC),TSARDH,3     GET RECORD IN TSRAWRK               
         CLI   BCTSERRS,0                                                       
         BE    SRPNT170                                                         
*                                                                               
* TZIH 03/20/2017 - MOVED FEW PAGES DOWN, PAST MEDIA FILTERING                  
*                                                                               
*        XC    SRPPWRK,SRPPWRK                                                  
*        MVC   SRPPWRK(L'IOKEY-1),IOKEY                                         
*        ZAP   SRPPWRK+L'IOKEY-1(8),=P'0'                                       
*        GOTO1 ATSAR,DMCB,(RC),TSAADD,3                                         
*        CLI   BCTSERRS,0                                                       
*        BE    *+6                                                              
*        DC    H'0'                                                             
*                                                                               
         MVC   SVKEY,IOKEY                                                      
         CLI   IOKEY+(L'IOKEY-1),X'00'  TRANSACTION SUB-REFERENCE(SEQ)          
         BE    SRPNT60                                                          
*                                                                               
         MVI   SVKEY+(L'SVKEY-1),X'00'  MAY NOT BE THERE                        
         GOTO1 ADMRDHI,DMCB,(RC)        READ HIGH SEQUENCE 00                   
         BE    SRPNT50                                                          
         CLI   DMCB+8,X'02'             DELETED ?                               
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
SRPNT40  GOTO1 ADMRDSEQ,DMCB,(RC)       READ NEXT SR RECORD IN BLOCK            
         CLC   SVKEY(L'TRNKEY-1),IOKEY                                          
         BNE   SRPNT160            END OF TRANSACTION BLOCK                     
*                                                                               
SRPNT50  GOTO1 ADMGET,DMCB,(RC),0 GET RECORD                                    
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
SRPNT60  DS    0H                                                               
         CLI   FILTOFF,C'Y'                                                     
         BNE   SRPNT65                                                          
         ICM   RF,15,AUTEXTRA      A(EXTRA PARMS)                               
         LA    RF,LDGOPOSP-AUTBLKX(RF)                                          
         GOTO1 =A(OFFILT),DMCB,(RF)                                             
         BNE   SRPNT150                                                         
*                                                                               
SRPNT65  DS    0H                                                               
         BRAS  RE,PROCSR           BUILD SR BUFFER ENTRY FROM TRN REC           
         BNE   SRPNT150                                                         
*                                                                               
* TZIH 03/20/2017 - MEDIA FILTERING MOVED HERE FROM BLDSRBUK                    
         CLC   CURRMED,TSRAWRK+TSRKMED-TSRAD                                    
         BNE   SRPNT170                                                         
*                                                                               
         XC    SRPPWRK,SRPPWRK                                                  
         MVC   SRPPWRK(L'IOKEY-1),IOKEY                                         
         ZAP   SRPPWRK+L'IOKEY-1(8),=P'0'                                       
         GOTO1 ATSAR,DMCB,(RC),TSAADD,3                                         
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
* TEMP DEBUG                                                                    
* RECORD KEY IN HEX, UP TO PSSKSTAT                                             
*        LHI   R3,TSRKLNQ                                                       
*        GOTO1 =A(TRACE),DMCB,(X'01',(R3)),TSRAWRK,=C'PROCSR'                   
*                                                                               
         TM    FLAG,FLGDR          DEBIT?                                       
         BZ    SRPNT90                                                          
*                                                                               
* DEBIT TRANSACTION HERE                                                        
* BUILD VENDOR DETAILS, IF NEEDED, GETTING $$ FROM COST OF MEDIA                
*                                                                               
         CLI   IO+TRNRSTYP-TRNRECD,TRNTCALC TYPE 30? (X'1E')                    
         BNE   SRPNT70                                                          
*                                                                               
* HAVE TYPE 30 DEBIT HERE, SPECIAL PROCESSING REQUIRED                          
* DEBIT AMOUNT TAKEN FROM THE TRANSACTION ITSELF                                
* IF WE DON'T HAVE A TYPE 9 IN THIS BLOCK, THEN THIS IS A TRANSFER              
* CLEAR VENDOR BUFFER, AND ADD "MANUAL" FAKE VENDOR TYPE                        
* IF WE ALREADY HAD A TRANSACTION, DO NOT ADD VENDOR DETAILS,                   
* BUT UPDATE BILL TOTALS WITH TRANSACTION AMOUNT                                
*                                                                               
         TM    FLAG2,FL2TYP9Q      HAVE WE SEEN A TYPE 9?                       
         BO    SRPNT90             YES - JUST UPDATE BILL TOTALS                
*                                                                               
* NO TYPE 9 - THIS IS A TRANSFER.                                               
         L     RE,AVENDBUF                                                      
         ICM   RF,15,=AL4(VBUFFSZQ) VENDOR BUFFER SIZE                          
         SAM31                                                                  
         XCEFL                                                                  
         SAM24                                                                  
         B     SRPNT80                                                          
*                                                                               
SRPNT70  DS    0H                                                               
         OI    FLAG2,FL2TYP9Q       INDICATE WE HAVE A TYPE 9                   
*                                                                               
* SAME BILL/S/M/C/P/E/MOS?                                                      
         CLC   TSRAWRK2(TSRKMOSQ),TSRAWRK                                       
         BE    SRPNT150            YES, DON'T ADD VENDOR-LEVEL DETAILS          
*                                                                               
         MVC   TSRAWRK2(TSRKMOSQ),TSRAWRK                                       
*                                                                               
* CLEAR VENDOR BUFFER                                                           
         L     RE,AVENDBUF                                                      
         ICM   RF,15,=AL4(VBUFFSZQ) VENDOR BUFFER SIZE                          
         SAM31                                                                  
         XCEFL                                                                  
         SAM24                                                                  
*                                                                               
* BUILD VENDOR BUFFER                                                           
         LA    R3,TSRAWRK                                                       
         USING TSRAD,R3                                                         
*                                                                               
         GOTO1 DATCON,DMCB,(1,TSRKMOS),(3,WORK)                                 
         MVI   WORK+2,X'01'                                                     
         GOTO1 DATCON,DMCB,(X'31',TSRKMOS),(3,WORK+3),(1,0)                     
*                                                                               
         GOTO1 DATCON,DMCB,(1,AUTSMOS),(3,WORK)                                 
         GOTO1 DATCON,DMCB,(X'31',AUTEMOS),(3,WORK+3),(1,0)                     
*                                                                               
         GOTO1 =V(ABVNDR),DMCB,SAVERA,IO,AVENDBUF,WORK,SVAGY                    
*                                                                               
         DROP  R3                                                               
*                                                                               
SRPNT80  DS    0H                                                               
         BRAS  RE,ADJVEND                                                       
         BRAS  RE,ADDVEND                                                       
*                                                                               
* UPDATE BILL-LEVEL TOTALS                                                      
* DEBITS COME FROM COST OF MEDIA                                                
* CREDITS COME FROM THE CREDIT TRANSACTIONS                                     
* SVTRNAMT EXPECTED TO HAVE DR/CR AMOUNT                                        
SRPNT90  DS    0H                                                               
         MVC   WORK(L'TSRKDPDT),TSRAWRK+TSRKDPDT-TSRAD                          
         XC    TSRAWRK+TSRKSYS-TSRAD(TSRKLNQ1),TSRAWRK+TSRKSYS-TSRAD            
         MVC   TSRAWRK+TSRKDPDT-TSRAD(L'TSRKDPDT),WORK                          
*                                                                               
         TM    FLAG,FLGDR                                                       
         BZ    *+14                                                             
         ZAP   TSRAWRK+(TSRADR-TSRAD)(TSRABKLN),SVTRNAMT                        
         B     *+10                                                             
         ZAP   TSRAWRK+(TSRACR-TSRAD)(TSRABKLN),SVTRNAMT                        
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAADD,1                                         
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
SRPNT150 DS    0H                                                               
         MVC   SVKEY,IOKEY                                                      
         GOTO1 ADMREAD,DMCB,(RC)   READ FOR NXT SEQUENTIAL SR TRAN              
         B     SRPNT40                                                          
*                                                                               
* END OF TRANSACTION BLOCK HERE                                                 
*                                                                               
SRPNT160 DS    0H                                                               
         BRAS  RE,CALCPCNT                                                      
         JE    EQXIT                                                            
*                                                                               
SRPNT170 MVC   SVKEY,SVSRPKEY                                                   
         GOTO1 ADMREAD,DMCB,(RC)   READ FOR NEXT SEQUENTIAL PNTR                
         B     SRPNT20             READ NEXT SR POINTER                         
*                                                                               
SRPNT230 DS    0H                                                               
         MVC   PSSAWRK,SVPSSWRK             RESTORE WORK AREA                   
         GOTO1 ATSAR,DMCB,(RC),TSARDH,2     GET RECORD IN PSSAWRK               
         TM    BCTSERRS,TSEEOF                                                  
         BNO   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLC   PSSAWRK(PSSAKLNQ),SVPSSWRK                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSANXT,2        GET NXT RECORD                   
         TM    BCTSERRS,TSEEOF                                                  
         BO    SRPNTX                                                           
         MVC   SVPSSWRK,PSSAWRK               SAVE OFF CURRENT KEY              
         B     SRPNT10                                                          
*                                                                               
SRPNTX   DS    0H                                                               
*                                                                               
         J     EXIT                                                             
         DROP  R4                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*                                                                               
**********************************************************************          
* R1 = A(STRING TO HEXOUT)                                                      
* R1 HOB = STRING LENGTH                                                        
**********************************************************************          
TRACE    NTR1  BASE=*,LABEL=*                                                   
         ICM   RF,15,TRCHOOK                                                    
         L     RE,CALLRD           BACK NMOD LINK                               
         LM    R2,RC,28(RE)        CALLER'S R2-RC                               
         BASR  RE,RF                                                            
         J     EQXIT                                                            
         LTORG                                                                  
*                                                                               
*                                                                               
*                                                                               
**********************************************************************          
* READ SR TRANSACTIONS AND BUILD TABLE BY REFERENCE NUMBER           *          
* APPLY PERCENT TO EACH DEBIT ENTRY BY BILL NUMBER/EST/MOS           *          
**********************************************************************          
         USING TRNKEY,R4                                                        
         USING TSRAD,R2                                                         
         SPACE 1                                                                
BLDSRTAB NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         XC    SVKEY,SVKEY         BUILD KEY TO READ SR RECDS                   
         XC    SVSYS,SVSYS         CLEAR SYSTEM                                 
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAINI,1 REINIT FIRST BUFFER                     
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R2,TSRAWRK                                                       
         XC    TSRAWRK,TSRAWRK     CLEAR WORK AREA                              
         XC    TSRAWRK2,TSRAWRK2   CLEAR SAVED TSRKEY                           
         ZAP   SVTRNAMT,=P'0'                                                   
         NI    FLAG,X'FF'-FLGSR    CLEAR FLAG                                   
*                                                                               
         LA    R4,SVKEY                                                         
         MVC   TRNKEY,SPACES       INIT KEY                                     
*                                                                               
         CLI   LSTSRKEY,X'FF'      IS THIS LAST TIME IN                         
         BE    BSRTBX                                                           
*                                                                               
         OC    LSTSRKEY,LSTSRKEY   IS THIS FIRST TIME IN                        
         BZ    *+14                YES                                          
         MVC   TRNKEY,LSTSRKEY     NOT FIRST TIME IN                            
         B     BSRTB10                                                          
         MVC   TRNKCPY,AUTCPY             COMPANY CODE                          
         MVC   TRNKUNT(2),=C'SR'          ALWAYS SR                             
*                                                                               
BSRTB10  GOTO1 ADMRDHI,DMCB,(RC)                                                
         B     BSRTB30                                                          
*                                                                               
BSRTB20  GOTO1 ADMRDSEQ,DMCB,(RC)                                               
BSRTB30  CLC   SVKEY(TRNKACT-TRNKEY),IOKEY                                      
         BE    BSRTB40                                                          
         MVI   LSTSRKEY,X'FF'      U/L CHNGD, INDICATE END OF TRANS.            
         B     BSRTB190            PROCESS LAST AND EXIT                        
*                                                                               
BSRTB40  LA    R4,IO                                                            
         CLC   TRNKDATE,SPACES                                                  
         BNH   BSRTB20                                                          
         CLC   TRNKREF,SPACES      IS IT A TRANSACTION                          
         BNH   BSRTB20                                                          
*                                                                               
         OC    LSTSRKEY,LSTSRKEY   IS THIS FIRST TIME IN                        
         BNZ   BSRTB50                                                          
         MVC   LSTSRKEY,IOKEY      SAVE OFF SR KEY                              
         B     BSRTB60                                                          
BSRTB50  CLC   LSTSRKEY(L'LSTSRKEY-1),IOKEY  ARE WE DOING THE SAME ACC?         
         BE    BSRTB60                                                          
         MVC   LSTSRKEY,IOKEY      SAVE OFF SR ACCOUNT                          
         B     BSRTB190            AND EXIT                                     
*                                                                               
BSRTB60  GOTO1 ADMGET,DMCB,(RC),0 GET RECORD                                    
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLI   TRNKSBR,0                                                        
         BNE   BSRTB65                                                          
         GOTO1 =V(ABVNDR),DMCB,SAVERA,IO,AVENDBUF,AUTMOS,SVAGY                  
*                                                                               
BSRTB65  DS    0H                                                               
         BRAS  RE,PROCSR                                                        
         BNE   BSRTB180            SKIP OFFSETS                                 
*                                                                               
* UPDATE BILL-LEVEL TOTALS                                                      
         MVC   TSRAWRK3,TSRAWRK    SAVE KEY                                     
         XC    TSRAWRK+TSRKSYS-TSRAD(TSRKLNQ1),TSRAWRK+TSRKSYS-TSRAD            
         GOTO1 ATSAR,DMCB,(RC),TSAADD,1                                         
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   TSRAWRK,TSRAWRK3 RESTORE KEY FOR VENDOR LEVEL DETAILS            
*                                                                               
* BUILD VENDOR DETAILS, IF NEEDED                                               
* SAME BILL/S/M/C/P/E/MOS?                                                      
         CLC   TSRAWRK2(TSRKMOSQ),TSRAWRK                                       
         BE    BSRTB180            YES, DON'T ADD VENDOR-LEVEL DETAILS          
*                                                                               
         MVC   TSRAWRK2(TSRKMOSQ),TSRAWRK                                       
*                                                                               
* CLEAR VENDOR BUFFER                                                           
         L     RE,AVENDBUF                                                      
         ICM   RF,15,=AL4(VBUFFSZQ) VENDOR BUFFER SIZE                          
         SAM31                                                                  
         XCEFL                                                                  
         SAM24                                                                  
*                                                                               
* BUILD VENDOR BUFFER                                                           
         GOTO1 =V(ABVNDR),DMCB,SAVERA,(R4),AVENDBUF,AUTMOS,SVAGY                
         BRAS  RE,ADJVEND                                                       
         BRAS  RE,ADDVEND                                                       
*                                                                               
BSRTB180 NI    FLAG,X'FF'-FLGSR       RESET BEFORE EXIT OR READING NXT          
         ZAP   SVTRNAMT,=P'0'                                                   
         B     BSRTB20                                                          
         DROP  R2                                                               
*                                                                               
BSRTB190 DS    0H                                                               
         XC    TSRAWRK,TSRAWRK                                                  
         GOTO1 ATSAR,DMCB,(RC),TSARDH,1     GET RECORD IN TSRAWRK               
         TM    BCTSERRS,TSEEOF              IS THIS END OF FILE                 
         BNO   BSRTB200                                                         
*                                  CHECK IF IT IS A LAST TRANSACION             
         CLI   LSTSRKEY,X'FF'      NOTHING ADDED TO TABLE FOR THIS SR           
*                                  CHECK IF IT IS A LAST TRANSACION             
         BE    BSRTBX              LAST SR TRANSACTION                          
*                                                                               
         MVC   SVKEY,LSTSRKEY      READ LAST KEY AND START FROM THERE           
         B     BSRTB10                                                          
*                                                                               
BSRTB200 DS    0H                                                               
         BRAS  RE,CALCPCNT                                                      
*                                                                               
BSRTBX   J     EXIT                                                             
         DROP  R4                                                               
         LTORG                                                                  
*                                                                               
**********************************************************************          
* LOOP THOUGH THE TABLE AND UPDATE CASH AVAILABLE BUCKET             *          
* MARK TABLE ENTRY IF ENOUGH CASH IS AVAILABLE TO APPROVE ITEM       *          
**********************************************************************          
         USING PSSAD,R2                                                         
*                                                                               
CASHAVL2 NTR1  BASE=*,LABEL=*                                                   
         ZAP   SVTRNAMT,=P'0'                                                   
         XC    FLAG1,FLAG1                                                      
         XC    PSSAWRK,PSSAWRK     CURRENT RECORD                               
         XC    PSSAWRK0,PSSAWRK0   CPE TOTALS                                   
         XC    PSSAWRK5,PSSAWRK5   CPE TOTALS                                   
         XC    PSSAWRK3,PSSAWRK3   MOS TOTALS                                   
         ZAP   SVPKAMT,=P'0'                                                    
*                                                                               
CPETOT   USING PSSAD,PSSAWRK0                                                   
MOSTOT   USING PSSAD,PSSAWRK3                                                   
VENTOT   USING PSSAD,PSSAWRK5                                                   
*                                                                               
         LA    R2,PSSAWRK                                                       
         GOTO1 ATSAR,DMCB,(RC),TSARDH,2   GET RECORD IN PSSAWRK                 
         TM    BCTSERRS,TSEEOF     IS THIS END OF FILE                          
         BO    CS2AVX                                                           
*                                                                               
CS2AV10  DS    0H                                                               
         CLI   PSSKTYP,X'00'       IS THIS A GOOD ESTIMATE                      
         BNE   CS2AV200                                                         
*                                                                               
CS2AV20  DS    0H                                                               
         CLI   PSSKRTYP,X'00'      NEW CPE ENTRY?                               
         BNE   CS2AV25                                                          
*                                                                               
         OC    PSSAWRK0,PSSAWRK0                                                
         BZ    *+8                                                              
         BRAS  RE,UPDTCPE                                                       
*                                                                               
         MVC   PSSAWRK0,PSSAWRK    SAVE CPE TOTALS                              
*                                                                               
CS2AV25  DS    0H                                                               
         CLI   PSSKRTYP,X'03'      MOS TOTALS?                                  
         BNE   CS2AV170                                                         
*                                                                               
         OC    PSSAWRK3,PSSAWRK3                                                
         BZ    *+8                                                              
         BRAS  RE,UPDTMOS                                                       
         ZAP   SVPKAMT,=P'0'                                                    
*                                                                               
         MVC   PSSAWRK3,PSSAWRK    SAVE MOS TOTALS                              
*                                                                               
* NOW READ ALL SS DETAILS FOR THIS MOS AND SORT THE KEYS                        
*                                                                               
         L     RE,ABUFF                                                         
         L     RF,=A(PSSRTKLQ*PSSRTNQ)                                          
         SAM31                                                                  
         XCEFL                                                                  
         SAM24                                                                  
*                                                                               
         MVI   PSSKRTYP,X'05'      SS DETAIL                                    
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSARDH,2   GET RECORD IN PSSAWRK                 
         B     CS2AV31                                                          
*                                                                               
CS2AV30  GOTO1 ATSAR,DMCB,(RC),TSANXT,2   GET RECORD IN PSSAWRK                 
CS2AV31  TM    BCTSERRS,TSEEOF     IS THIS END OF FILE                          
         BO    CS2AV50             ALL '05'S READ IN                            
*                                                                               
         CLC   PSSAKEY(PSSKRTYP-PSSAKEY),PSSAWRK3  SAME S/M/C/P/E?              
         BNE   CS2AV50                                                          
         CLC   PSSKMOS,PSSAWRK3+PSSKMOS-PSSAD SAME MOS?                         
         BNE   CS2AV50                                                          
*                                                                               
         CLI   PSSKRTYP,X'05'      STILL SS DETAIL?                             
         BNE   CS2AV50          NO: ALL SS DETAILS FOR THIS MOS ARE IN          
         OC    PSSKSTN,PSSKSTN     SS ACCOUNT BLANK = VENDOR TOTALS             
         BZ    CS2AV30             SKIP VENDOR-LEVEL TOTALS                     
         OC    PSSSRAC,PSSSRAC     SR ACCOUNT NON-BLANK = SR DETAILS            
         BNZ   CS2AV30             SKIP BILL'D SR DETAILS                       
*                                                                               
         CLI   PSSKSTAT,PSSKUNAP   UNAPPROVED?                                  
         BNE   CS2AV30                                                          
*                                                                               
SORTSS   USING PSSRTKEY,TEMPWRK                                                 
*                                                                               
         CP    PSSUNAPB,=P'0'                                                   
         BNL   *+12                                                             
         MVI   SORTSS.PSSRTNZR,X'01' INDICATE NEGATIVE AMOUNT                   
         B     CS2AV40                                                          
*                                                                               
         CP    PSSUNAPB,=P'0'                                                   
         BNE   *+12                                                             
         MVI   SORTSS.PSSRTNZR,X'02' ZERO-BILLED AMOUNT                         
         B     CS2AV40                                                          
*                                                                               
         MVI   SORTSS.PSSRTNZR,X'03' NON-ZERO AMOUNT                            
*                                                                               
CS2AV40  DS    0H                                                               
         MVC   SORTSS.PSSRTDAT,PSSINVCD CLEARED DATE                            
         ZAP   SORTSS.PSSRTAMT,=P'999999999999999'                              
         SP    SORTSS.PSSRTAMT,PSSUNAPB                                         
         MVC   SORTSS.PSSRTKY,PSSKVEN KEY FROM VENDOR FORWARD                   
*                                                                               
         MVI   BSP4,1                                                           
         SAM31                                                                  
         GOTO1 BINSRC31,BSPARS,TEMPWRK                                          
         SAM24                                                                  
         OC    1(3,R1),1(R1)       TEST TABLE FULL                              
         BNZ   CS2AV30             NO - READ NEXT SS DETAIL                     
         DC    H'0'                TABLE FULL                                   
*                                                                               
* ALL SS DETAILS HAVE BEEN ADDED TO BINSRCH TABLE HERE                          
*                                                                               
CS2AV50  DS    0H                                                               
         OC    BSP3,BSP3           NUMBER OF RECORDS IN TABLE                   
         BZ    CS2AV160            DO NEXT MOS                                  
*                                                                               
         MVC   BSP1,BSP2           A(BUFFER) = A(1ST RECORD)                    
*                                                                               
CS2AV60  DS    0H                                                               
*                                                                               
* GET VENDOR TOTALS FOR THIS SS DETAIL                                          
* WE'RE APPROVING OUT OF VENDOR-LEVEL BUCKETS                                   
*                                                                               
         XC    PSSAWRK,PSSAWRK                                                  
         MVC   PSSAWRK(PSSKVEN-PSSAKEY),PSSAWRK3 SAVED MOS KEY                  
         MVI   PSSKRTYP,X'05'      SS DETAIL                                    
         ICM   R1,15,BSP1                                                       
         SAM31                                                                  
         MVC   PSSKVEN,PSSRTKY-PSSRTKEY(R1) VENDOR FROM BINSRCH ENTRY           
         SAM24                                                                  
*                                                                               
         MVC   SVPSSWRK,PSSAWRK                                                 
         GOTO1 ATSAR,DMCB,(RC),TSARDH,2   GET RECORD IN PSSAWRK                 
         CLI   BCTSERRS,TSEEOF                                                  
         BNE   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLC   PSSAWRK(PSSAKLNQ),SVPSSWRK                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         OC    PSSKSTN,PSSKSTN IS THIS VENDOR-LEVEL TOTAL?                      
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   PSSAWRK5,PSSAWRK SAVE IT                                         
*                                                                               
* NOW READ THE SPECIFIC SS DETAIL                                               
         ICM   R1,15,BSP1                                                       
         SAM31                                                                  
         MVC   PSSKVEN(PSSKLVENFWQ),PSSRTKY-PSSRTKEY(R1)                        
         SAM24                                                                  
*                                                                               
         MVC   SVPSSWRK,PSSAWRK                                                 
         GOTO1 ATSAR,DMCB,(RC),TSARDH,2   GET RECORD IN PSSAWRK                 
         CLI   BCTSERRS,TSEEOF                                                  
         BNE   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLC   PSSAWRK(PSSAKLNQ),SVPSSWRK                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
* UNAPPROVED SS DETAIL (X'05') RECORD HERE                                      
*                                                                               
CS2AV70  DS    0H                                                               
         ZAP   PKAMNT,PSSUNAPB                                                  
*                                                                               
         CP    PKAMNT,=P'0'                                                     
         BNH   CS2AV110            ALWAYS APPROVE <=$0 IVOICES                  
*                                                                               
         ZAP   PKAVAIL,VENTOT.PSSRCVDB      CASH RECEIVED                       
         SP    PKAVAIL,VENTOT.PSSDISBB      CASH POSITION                       
*        BNP   CS2AV131                                                         
         BM    CS2AV131                                                         
         SP    PKAVAIL,VENTOT.PSSAPPRB  AVAIL=CASH POS-ALREADY APPROVED         
         CP    PKAVAIL,PKAMNT                                                   
         BNL   CS2AV110            AUTOAPPROVING                                
*                                                                               
* CHECK APPROVAL TOLERANCE FACTOR                                               
*                                                                               
         ZAP   PL16,PSSUNAPB          GET UNAPPROVED AMT.                       
         CP    AUTPCNT,=P'0'          0 % THRESHOLD, APPROVE ALL ?              
         BNH   CS2AV130                                                         
         CLI   AUTPRF6,C'Y'           PERCENTAGE                                
         BE    CS2AV90                                                          
         CLI   AUTPRF6,C'D'           DOLLAR                                    
         BNE   CS2AV90                                                          
         SP    PL16,AUTPCNT           UNAPPROVED AMT - DOLLAR TOLR              
         CP    PKAVAIL,PL16                                                     
         B     CS2AV105                                                         
*                                                                               
CS2AV90  MP    PL16,AUTPCNT           PERCENT FROM PROFILE/REQUEST              
         SRP   PL16,3,0                                                         
         DP    PL16,=PL4'1000000'                                               
         SRP   PL16(L'PL16-4),64-3,5 % ROUNDED TO 2 DP                          
         CP    PKAVAIL,PL16(L'PL16-4)                                           
CS2AV105 BL    CS2AV130               ZERO OR MORE AUTO APPROVE IT              
*                                                                               
* AUTO APPROVING HERE...                                                        
*                                                                               
CS2AV110 DS    0H                                                               
         TM    PSSFLAG,PSSPREV                                                  
         BO    CS2AV130                                                         
*                                                                               
         OI    PSSFLAG,PSSCSH         MARK CASH AVAILABLE                       
         BRAS  RE,ZAPSSBUK                                                      
         ZAP   PSSAPPRB,PKAMNT                                                  
*                                                                               
         AP    SVPKAMT,PKAMNT AMOUNT APPROVED THIS RUN FOR MOS TOTALS           
*                                                                               
         AP    MOSTOT.PSSAPPRB,PKAMNT                                           
         SP    MOSTOT.PSSUNAPB,PKAMNT                                           
         AP    CPETOT.PSSAPPRB,PKAMNT                                           
         SP    CPETOT.PSSUNAPB,PKAMNT                                           
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2 UPDATE RECORD                           
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
* UPDATE VENDOR-LEVEL TOTAL                                                     
*                                                                               
         XC    PSSKSTN(PSSKLSTNFWQ),PSSKSTN CLEAR KEY SS ACC AND FWD            
         BRAS  RE,ZAPSSBUK                                                      
         AP    PSSAPPRB,PKAMNT                                                  
         SP    PSSUNAPB,PKAMNT                                                  
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2   UPDATE RECORD                         
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         B     CS2AV150            PROCESS NEXT DETAIL                          
*                                                                               
* NOT ENOUGH - MANUALL APPROVAL IS NEEDED                                       
*                                                                               
CS2AV130 DS    0H                                                               
         OI    PSSFLAG,PSSMAN         MAKE IT MANUAL APPROVAL NEEDED            
*                                                                               
CS2AV131 DS    0H                                                               
         NI    PSSFLAG,X'FF'-PSSCSH   TURN OFF CASH AVAIL FLAG                  
         BRAS  RE,ZAPSSBUK                                                      
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2   UPDATE RECORD                         
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
* NEXT SS DETAIL FROM BINSRCH TABLE                                             
*                                                                               
CS2AV150 ICM   R0,15,BSP1          A(NEXT RECORD)                               
         AHI   R0,PSSRTKLQ         ADVANCE TO NEXT                              
         STCM  R0,15,BSP1                                                       
         ICM   R0,15,BSP3                                                       
         AHI   R0,-1                                                            
         STCM  R0,15,BSP3                                                       
         BP    CS2AV60                                                          
*                                                                               
* ALL BINSRCH KEYS PROCESSED HERE, DO NEXT MOS                                  
*                                                                               
CS2AV160 XC    PSSAWRK,PSSAWRK                                                  
         MVC   PSSAWRK(PSSAKLNQ),PSSAWRK3                                       
         GOTO1 ATSAR,DMCB,(RC),TSARDH,2   GET RECORD IN PSSAWRK                 
         CLI   BCTSERRS,0                                                       
         BNO   *+6                                                              
         DC    H'0'                                                             
         CLC   PSSAWRK(PSSAKLNQ),PSSAWRK3                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
CS2AV170 DS    0H                                                               
         GOTO1 ATSAR,DMCB,(RC),TSANXT,2   GET RECORD IN PSSAWRK                 
         TM    BCTSERRS,TSEEOF     IS THIS END OF FILE                          
         BNO   CS2AV10                                                          
*                                                                               
CS2AV200 DS    0H                                                               
         OC    PSSAWRK3,PSSAWRK3                                                
         BZ    *+8                                                              
         BRAS  RE,UPDTMOS                                                       
         OC    PSSAWRK0,PSSAWRK0                                                
         BZ    *+8                                                              
         BRAS  RE,UPDTCPE                                                       
*                                                                               
CS2AVX   J     EXIT                                                             
         EJECT                                                                  
**********************************************************************          
* LITERALS FOR CASHAVL  ROUTINE                                      *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*                                                                               
UPDTMOS  NTR1  BASE=*,LABEL=*                                                   
* CASH POSITION                                                                 
         ZAP   PKAMNT,MOSTOT.PSSRCVDB      CASH RECEIVED                        
         SP    PKAMNT,MOSTOT.PSSDISBB      LESS DISBURSED (=CASH POS)           
         ZAP   MOSTOT.PSSCASHB,PKAMNT      MOS TOTAL CASH POSITION              
* AVAIL TO APPROVE                                                              
         ZAP   MOSTOT.PSSAVLB,MOSTOT.PSSCASHB  $$ POSITION                      
         SP    MOSTOT.PSSAVLB,MOSTOT.PSSAPPRB  MINUS ALREADY APPROVED           
* APPROVED                                                                      
         ZAP   MOSTOT.PSSAPPRB,SVPKAMT                                          
* UPDATE THE BUFFER                                                             
         MVC   SVPSSWRK,PSSAWRK                                                 
         MVC   PSSAWRK,PSSAWRK3                                                 
         BRAS  RE,ZAPSSBUK                                                      
         ZAP   PSSCASHB,MOSTOT.PSSCASHB                                         
         ZAP   PSSAVLB,MOSTOT.PSSAVLB                                           
         ZAP   PSSAPPRB,MOSTOT.PSSAPPRB                                         
         BRAS  RE,UPDTOT                                                        
* RESTORE READ SEQUENCE                                                         
         MVC   PSSAWRK,SVPSSWRK                                                 
         GOTO1 ATSAR,DMCB,(RC),TSARDH,2                                         
*                                                                               
         J     EXIT                                                             
         LTORG                                                                  
*                                                                               
*                                                                               
UPDTCPE  NTR1  BASE=*,LABEL=*                                                   
* CASH POSITION                                                                 
         ZAP   PKAMNT,CPETOT.PSSRCVDB      CASH RECEIVED                        
         SP    PKAMNT,CPETOT.PSSDISBB      LESS DISBURSED (=CASH POS)           
         ZAP   CPETOT.PSSCASHB,PKAMNT      MOS TOTAL CASH POSITION              
* AVAIL TO APPROVE                                                              
         ZAP   CPETOT.PSSAVLB,CPETOT.PSSCASHB                                   
         SP    CPETOT.PSSAVLB,CPETOT.PSSAPPRB                                   
* UPDATE THE BUFFER                                                             
         MVC   SVPSSWRK,PSSAWRK                                                 
         MVC   PSSAWRK,PSSAWRK0                                                 
         BRAS  RE,ZAPSSBUK                                                      
         ZAP   PSSCASHB,CPETOT.PSSCASHB                                         
         ZAP   PSSAVLB,CPETOT.PSSAVLB                                           
         ZAP   PSSAPPRB,CPETOT.PSSAPPRB                                         
         BRAS  RE,UPDTOT                                                        
* RESTORE READ SEQUENCE                                                         
         MVC   PSSAWRK,SVPSSWRK                                                 
         GOTO1 ATSAR,DMCB,(RC),TSARDH,2   HIGH FOR RECD JUST WRITTEN            
*                                                                               
         J     EXIT                                                             
         LTORG                                                                  
*                                                                               
*                                                                               
*                                                                               
UPDTOT   NTR1  BASE=*,LABEL=*                                                   
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2   UPDATE RECORD                         
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 ATSAR,DMCB,(RC),TSARDH,2   HIGH FOR RECD JUST WRITTEN            
         J     EXIT                                                             
         LTORG                                                                  
         DROP  R2                                                               
*                                                                               
*                                                                               
**********************************************************************          
* LOOP THOUGH THE TABLE AND UPDATE CASH AVAILABLE BUCKET             *          
* MARK TABLE ENTRY IF ENOUGH CASH IS AVAILABLE TO APPROVE ITEM       *          
**********************************************************************          
         USING PSSAD,R2                                                         
         USING MTHD,R4                                                          
         SPACE 1                                                                
CASHAVL  NTR1  BASE=*,LABEL=*                                                   
         XC    PSSAWRK,PSSAWRK     CLEAR WORK AREA                              
         ZAP   SVTRNAMT,=P'0'                                                   
         XC    FLAG1,FLAG1                                                      
         XC    TEMPWRK,TEMPWRK     BUILD WORK AREA TO UPDT CASH AVAIL           
*                                                                               
         LA    R2,PSSAWRK                                                       
         XC    PSSAWRK,PSSAWRK                                                  
         GOTO1 ATSAR,DMCB,(RC),TSARDH,2   GET RECORD IN PSSAWRK                 
         TM    BCTSERRS,TSEEOF     IS THIS END OF FILE                          
         BO    CSHAVX                                                           
*                                                                               
CSHAV10  DS    0H                                                               
         ZAP   PKAVAIL,=P'0'                                                    
         ZAP   SVPKAMT,=P'0'                                                    
         ZAP   PKAMNT,=P'0'                                                     
*                                                                               
         CLI   PSSKTYP,X'00'       IS THIS A GOOD ESTIMATE                      
         BNE   CSHAVX                                                           
*                                                                               
         CLI   PSSKRTYP,X'00'      IS THIS A NEW C/P/E ENTRY                    
         BNE   CSHAV30                                                          
         BRAS  RE,INMTHTB          INIT/RESET FLAG IN MONTHS TABLE              
*                                                                               
CSHAV30  DS    0H                                                               
         CLI   PSSKRTYP,X'02'      ARE THESE SR DETAILS                         
         BE    CSHAV170            GET NEXT RECORD                              
         CLI   PSSKRTYP,X'05'      ALSO UPDATE SS DETAIL RECORD                 
         BNE   CSHAV40                                                          
         OI    FLAG1,FLGSTAT                                                    
         B     CSHAV130                                                         
*                                                                               
CSHAV40  NI    FLAG1,X'FF'-FLGSTAT                                              
         CLI   AUTOPT6,C'Y'        ARE WE APPROVING BY MOS                      
         BNE   CSHAV50                                                          
         CLI   PSSKRTYP,X'03'                                                   
         BNE   CSHAV60                                                          
         OI    FLAG1,FLGSTAT       TURN ON APPROVE/MANUAL STATUS                
CSHAV50  CLI   PSSKRTYP,X'00'                                                   
         BNE   CSHAV60                                                          
         OI    FLAG1,FLGSTAT       TURN ON APPROVE/MANUAL STATUS                
*                                                                               
CSHAV60  DS    0H                                                               
         TM    FLAG1,FLGSTAT                                                    
         BNO   CSHAV90            WE DON'T NEED ANY STATUS ON THIS LINE         
*                                                                               
         LA    RF,RQMNTHS                                                       
         LA    R4,MNTHS                                                         
CSHAV70  CLC   MTHCODE,PSSKMOS    REQUESTED MONTH/00 (EST) ENTRY ?              
         BE    CSHAV80                                                          
         LA    R4,MTHLEN(R4)                                                    
         BCT   RF,CSHAV70                                                       
         DC    H'0'                                                             
*                                                                               
CSHAV80  NI    MTHFLAG,X'FF'-(PSSCSH+PSSMAN) CASH IS NOT AVAILABLE              
CSHAV90  ZAP   PKAVAIL,PSSRCVDB      ADD RECEIVED AMOUNT                        
         SP    PKAVAIL,PSSDISBB      RCVD-DISBURSED                             
         ZAP   SVPKAMT,PKAVAIL       CASH POSITION                              
*                                                                               
         SP    PKAVAIL,PSSAPPRB       AVAILABLE TO APPROVE                      
         BNP   CSHAV130               DON'T HAVE ENOUGH                         
         TM    FLAG1,FLGSTAT          DO WE WANT TO TURN ON STATUS              
         BNO   *+8                                                              
         OI    MTHFLAG,PSSMAN         MANUAL APPROVAL NEEDED                    
         CP    PKAVAIL,PSSUNAPB                                                 
         BNL   CSHAV110               ZERO OR MORE AUTO APPROVE IT              
*                                                                               
         ZAP   PL16,PSSUNAPB          GET UNAPPROVED AMT.                       
         CP    AUTPCNT,=P'0'          0 % THRESHOLD, APPROVE ALL ?              
         BNH   CSHAV100                                                         
         CLI   AUTPRF6,C'Y'           PERCENTAGE                                
         BE    CSHAV093                                                         
         CLI   AUTPRF6,C'D'           DOLLAR                                    
         BNE   CSHAV093                                                         
*                                                                               
         SP    PL16,AUTPCNT           UNAPPROVED AMT - DOLLAR TOLR              
         CP    PKAVAIL,PL16                                                     
         B     CSHAV099                                                         
*                                                                               
CSHAV093 MP    PL16,AUTPCNT           PERCENT FROM PROFILE/REQUEST              
         SRP   PL16,3,0                                                         
         DP    PL16,=PL4'1000000'                                               
         SRP   PL16(L'PL16-4),64-3,5 % ROUNDED TO 2 DP                          
         CP    PKAVAIL,PL16(L'PL16-4)                                           
CSHAV099 BL    CSHAV130               ZERO OR MORE AUTO APPROVE IT              
*                                                                               
CSHAV100 TM    FLAG1,FLGSTAT          DO WE WANT TO TURN ON STATUS              
         BNO   *+8                                                              
         OI    MTHFLAG,PSSPCNT        APPLYING PERCENT FOR APPROVING            
*                                                                               
CSHAV110 TM    FLAG1,FLGSTAT          DO WE WANT TO TURN ON STATUS              
         BNO   *+8                                                              
         OI    MTHFLAG,PSSCSH         MARK CASH AVAILABLE                       
         SP    PKAVAIL,PSSUNAPB       LEFT OVER, THIS IS EXTRA CASH.            
*                                                                               
         TM    FLAG1,FLGSTAT          DO WE WANT TO TURN ON STATUS              
         BNO   CSHAV130                                                         
         TM    PSSFLAG,PSSPREV                                                  
         BNO   CSHAV120                                                         
         NI    MTHFLAG,X'FF'-PSSCSH   DON'T AUTO APPROVE IF OLD ITEM            
         OI    MTHFLAG,PSSMAN         MAKE IT MANUAL APPROVAL NEEDED            
         B     CSHAV130                                                         
CSHAV120 NI    MTHFLAG,X'FF'-PSSMAN                                             
*                                                                               
CSHAV130 MVC   PSSAWRK,0(R2)          MOVE TO WORK AREA FROM TEMP               
         BRAS  RE,ZAPSSBUK            ZAP ALL BUCKETS                           
         TM    FLAG1,FLGSTAT                                                    
         BNO   CSHAV160                                                         
*                                                                               
         LA    RF,RQMNTHS          # OF REQUESTED MONTHS + 1                    
         LA    R4,MNTHS                                                         
CSHAV140 CLI   AUTOPT6,C'Y'        APPROVING MY MOS?                            
         BE    CSHAV142                                                         
         CLI   MTHCODE,X'00'       COMPARE WITH 00 ENTRY IF APPR BY EST         
         BE    CSHAV150                                                         
         B     CSHAV145                                                         
CSHAV142 CLC   MTHCODE,PSSKMOS     VALID MONTH                                  
         BE    CSHAV150                                                         
CSHAV145 LA    R4,MTHLEN(R4)                                                    
         BCT   RF,CSHAV140                                                      
         DC    H'0'                                                             
*                                                                               
CSHAV150 OC    PSSAWRK+(PSSFLAG-PSSAD)(1),MTHFLAG   TURN ON STATUSES            
CSHAV160 ZAP   PSSAWRK+(PSSCASHB-PSSAD)(L'PSSCASHB),SVPKAMT                     
         ZAP   PSSAWRK+(PSSAVLB-PSSAD)(L'PSSAVLB),PKAVAIL                       
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2   UPDATE RECORD                         
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 ATSAR,DMCB,(RC),TSARDH,2   HIGH FOR RECD JUST WRITTEN            
CSHAV170 DS    0H                                                               
         GOTO1 ATSAR,DMCB,(RC),TSANXT,2   GET RECORD IN PSSAWRK                 
         TM    BCTSERRS,TSEEOF     IS THIS END OF FILE                          
         BNO   CSHAV10                                                          
*                                                                               
CSHAVX   J     EXIT                                                             
         DROP  R2,R4                                                            
         EJECT                                                                  
**********************************************************************          
* LITERALS FOR CASHAVL  ROUTINE                                      *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*                                                                               
*                                                                               
**********************************************************************          
* RESET BLOCK ROUTINE                                                *          
**********************************************************************          
         SPACE 1                                                                
RESETBLK NTR1  BASE=*,LABEL=*                                                   
         L     R0,AAUTBLK          RESTORE CALLER'S BLOCK                       
         LHI   R1,AUTBLKL                                                       
         LA    RE,AUTBLK                                                        
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
RESETX   J     EXIT                                                             
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* ROUTINE VALIDATES CLIENT IN THE CLIENT LIST                        *          
**********************************************************************          
         SPACE 1                                                                
CHKCLT   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         GOTO1 ACLIST,DMCB,AUTALST,CLNT                                         
         CLI   DMCB,0                                                           
         BNE   *+6                                                              
         DC    H'0'                BAD LIST RECORD                              
         CLI   DMCB,C'I'           INCLUDE THIS CLIENT?                         
         XIT1                                                                   
*                                                                               
         EJECT                                                                  
**********************************************************************          
* LITERALS CHKCLT                                                    *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* ROUTINE ZAPS ALL BUCKETS OF PSSATAB BINTABLE                       *          
**********************************************************************          
         SPACE 1                                                                
         USING PSSAD,R2                                                         
ZAPSSBUK NTR1  BASE=*,LABEL=*                                                   
         LA    R0,PSSABKCT         NO. OF BUCKETS                               
         LA    R2,PSSAWRK          POINT TO FIRST BUCKET                        
         LA    R1,PSSABKT                                                       
         ZAP   0(PSSABKLN,R1),=P'0'     CLEAR FIELDS                            
         LA    R1,PSSABKLN(R1)                                                  
         BCT   R0,*-10             NUMBER OF BUCKETS TO LOOP                    
         J     EXIT                                                             
         DROP  R2                                                               
*                                                                               
         EJECT                                                                  
**********************************************************************          
* LITERALS ZAPSSBUK                                                  *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* ROUTINE ZAPS ALL BUCKETS OF TSRTAB BINTABLE                        *          
**********************************************************************          
         SPACE 1                                                                
         USING TSRAD,R2                                                         
ZAPSRBUK NTR1  BASE=*,LABEL=*                                                   
         LA    R0,TSRABKCT         NO. OF BUCKETS                               
         LA    R2,TSRAWRK          POINT TO FIRST BUCKET                        
         LA    R1,TSRABKT                                                       
         ZAP   0(TSRABKLN,R1),=P'0'     CLEAR FIELDS                            
         LA    R1,TSRABKLN(R1)                                                  
         BCT   R0,*-10             NUMBER OF BUCKETS TO LOOP                    
         J     EXIT                                                             
         DROP  R2                                                               
*                                                                               
         EJECT                                                                  
**********************************************************************          
* LITERALS FOR ZAPSRBUK ROUTINE                                      *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* MARK THOSE ITEMS IN THE TABLE THAT ARE COMPLETELY DISBURSED        *          
**********************************************************************          
         SPACE 1                                                                
         USING MTHD,R4                                                          
FIXTAB   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         XC    PSSAWRK,PSSAWRK                                                  
         NI    FLAG1,X'FF'-PSSDISB                                              
*                                                                               
         USING PSSAD,R2                                                         
         LA    R2,PSSAWRK                                                       
         XC    PSSAWRK,PSSAWRK                                                  
         GOTO1 ATSAR,DMCB,(RC),TSARDH,2   GET RECORD IN PSSAWRK                 
         TM    BCTSERRS,TSEEOF     IS THIS END OF FILE                          
         BO    FIXTBX                                                           
*                                                                               
FIXTB10  DS    0H                                                               
         CLI   PSSKTYP,X'00'       IS THIS A GOOD ESTIMATE?                     
         BNE   FIXTBX              BAD ESTIMATES COMING UP,EXIT.                
*                                                                               
         CLI   PSSKRTYP,X'00'      IS THIS A TOTAL RECORD                       
         BNE   FIXTB20                                                          
         BRAS  RE,INMTHTB          INITIALIZE/RESET REQ MNTHS TABLE             
         B     FIXTB30                                                          
*                                                                               
FIXTB20  CLI   PSSKRTYP,X'03'      IS THIS A TOTAL BY MOS RECORD                
         BE    FIXTB30                                                          
         CLI   AUTOPT8,C'Y'        SUPPRESS ALL DISBURSED FROM PRINTING         
         BNE   FIXTB70                                                          
         CLI   PSSKRTYP,X'05'      SS DETAILS                                   
         BE    FIXTB60                                                          
         B     FIXTB70                                                          
*                                                                               
FIXTB30  ZIC   R0,RQMNTHS          #OF MONTHS IN REQ + 1                        
         LA    R4,MNTHS            POINT TO MONTHS TABLE                        
FIXTB40  CLC   MTHCODE,PSSKMOS     DO WE HAVE MATCHING MONTHS                   
         BE    FIXTB50                                                          
         LA    R4,MTHLEN(R4)       BUMP TO NEXT MONTH                           
         BCT   R0,FIXTB40                                                       
         DC    H'0'                                                             
*                                                                               
FIXTB50  XC    MTHFLAG,MTHFLAG                                                  
FIXTB60  CP    PSSUDISB,=P'0'      IS UNDISBURSED AMT ZERO                      
         BNE   FIXTB110            GO READ NEXT                                 
         TM    PSSFLAG1,PSS1ZERO   $0 INVOICES PRESENT?                         
         BO    FIXTB110            DON'T MARK MOS AS FULLY DISBURSED            
         CLI   AUTOPT8,C'Y'                                                     
         BE    FIXTB100                                                         
         OI    MTHFLAG,PSSDISB     SKIP SET,THAT IS ONLY DISBURSED              
         B     FIXTB100                                                         
*                                                                               
FIXTB70  ZIC   R0,RQMNTHS          #OF MONTHS IN REQ + 1                        
         LA    R4,MNTHS            POINT TO MONTH TABLE                         
FIXTB80  CLC   MTHCODE,PSSKMOS                                                  
         BE    FIXTB90                                                          
         LA    R4,MTHLEN(R4)                                                    
         BCT   R0,FIXTB80                                                       
         DC    H'0'                                                             
FIXTB90  TM    MTHFLAG,PSSDISB     DOES THIS BELONG TO DISB ONLY SET            
         BNO   FIXTB110            NO, READ NEXT                                
*                                                                               
* UNAPPROVED $0 INVOICES HAVE $0 IN UNDISBURSED BUCKET,                         
* RESULTING IN THE INVOICE MARKED AS "DISBURSED-ONLY", AND                      
* REPORTED AS "UNAPPROVED" ON THE AB REPORT                                     
* NEXT 6 LINES OF CODE DEAL WITH THIS SPECIFIC SCENARIO                         
*                                                                               
         CLI   PSSKRTYP,X'05'      SS DETAILS                                   
         BNE   FIXTB100                                                         
         TM    PSSKSTAT,PSSKUNAP   UNAPPROVED TRANSACTION?                      
         BZ    FIXTB100            NO, PROCEED AS USUAL                         
         CP    PSSUNAPB,=P'0'      $0 INVOICE?                                  
         BE    FIXTB110   DON'T MARK IT DISBURSED-ONLY, GO READ NEXT            
*                                                                               
FIXTB100 OI    PSSFLAG,PSSDISB     SKIP SET,THAT IS ONLY DISBURSED              
         BRAS  RE,ZAPSSBUK                                                      
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2   WRITE RECORD BACK                     
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 ATSAR,DMCB,(RC),TSARDH,2   HIGH FOR RECD JUST WRITTEN            
*                                                                               
FIXTB110 DS    0H                                                               
         GOTO1 ATSAR,DMCB,(RC),TSANXT,2   GET RECORD IN PSSAWRK                 
         TM    BCTSERRS,TSEEOF     IS THIS END OF FILE                          
         BNO   FIXTB10                                                          
*                                                                               
FIXTBX   DS    0H                                                               
*                                                                               
         J     EXIT                                                             
         DROP  R2,R4                                                            
         EJECT                                                                  
**********************************************************************          
* LITERALS FOR FIXTAB   ROUTINE                                      *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* ROUTINE TO INITIALIZE FLAG IN MONTH TABLE                          *          
**********************************************************************          
         SPACE 1                                                                
         USING MTHD,R3                                                          
INMTHTB  NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         LA    R3,MNTHS                                                         
         SR    R0,R0                                                            
         IC    R0,RQMNTHS                                                       
INMTH10  XC    MTHFLAG,MTHFLAG                                                  
         LA    R3,MTHLEN(R3)                                                    
         BCT   R0,INMTH10                                                       
         J     EXIT                                                             
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* GET SYSTEM IN SVSYS                                                 *         
* ON ENTRY   SVSYS=MDTSYS/MDPSYS/MBISYS                               *         
*            SVMED=MDTMED/MDPMED/MBIMED                               *         
* ON EXIT    SYSYS=CORRECT SYSTEM BASED ON SUBMEDIA FOR NET           *         
***********************************************************************         
         SPACE 1                                                                
GETSYSTM NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         L     RF,APSYSTAB         TABLE OF SUB-SYSTEMS FOR PRINT               
GETSYS10 CLI   0(RF),X'FF'                                                      
         JE    GETSYS20                                                         
         CLC   SVSYS,0(RF)         IF MATCH ON SUB-MED THAN THE SYSTEM          
         JNE   *+12                MUST BE NET                                  
         MVI   SVSYS,C'P'                                                       
         J     GETSYSX                                                          
         LA    RF,1(RF)                                                         
         J     GETSYS10                                                         
*                                                                               
GETSYS20 CLI   SVSYS,C'P'          IF PRINT DON'T NEED TO CHK SUB-MEDIA         
         JE    GETSYSX                                                          
*                                                                               
         L     RF,ASMEDTAB         TABLE OF SUB-MEDIA'S FOR NET                 
GETSYS30 CLI   0(RF),X'FF'                                                      
         BE    GETSYSX                                                          
         CLC   SVMED,0(RF)         IF MATCH ON SUB-MED THAN THE SYSTEM          
         BNE   *+12                MUST BE NET                                  
         MVI   SVSYS,C'N'                                                       
         B     GETSYSX                                                          
         LA    RF,1(RF)                                                         
         B     GETSYS30                                                         
*                                                                               
GETSYSX  J     EXIT                                                             
**********************************************************************          
* LITERALS FOR GETSYSTM ROUTINE                                      *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* GET INSERTION DATE IN SVMOS                                         *         
* ON ENTRY   SVMED=MDTMED/MDPMED/MBIMED                               *         
*            SVMOS=MOS FROM MDTMOS/MDPMOS/MBIMOS                      *         
* ON EXIT    SVMOS=CALCULATED INSERTION DATE OR SVMOS                 *         
***********************************************************************         
         SPACE 1                                                                
GTINSRTN NTR1  BASE=*,LABEL=*                                                   
         CLI   SVSYS,C'P'          INSERTION DATE CALC FOR PRINT ONLY           
         BNE   GETINSX                                                          
*                                                                               
         L     R2,AINSTAB          POINT TO INSERTION DATE TABLE                
         USING INSTABD,R2                                                       
GETINS10 CLI   INSALPH,X'FF'                                                    
         BE    GETINSX                                                          
         CLC   INSALPH,AUTALPHA    COMPARE ON ALPHA ID                          
         BNE   GETINS50                                                         
*                                                                               
         LHI   R0,INSNMED          NUMBER OF MEDIAS TO CHECK.                   
         LA    R3,INSMED           POINT TO FIRST MEDIA                         
GETINS30 CLC   SVMED,0(R3)         IS THIS THE MEDIA WE WANT?                   
         BNE   GETINS40                                                         
*                                                                               
         MVI   SVMOS+2,X'01'       PUT DAY                                      
         GOTO1 DATCON,DMCB,(1,SVMOS),(0,WORK)                                   
         ZIC   R4,1(R3)            NUMBER OF MONTHS TO ADJUST                   
         LTR   R4,R4                                                            
         BZ    GETINSX             NO ADJUSTMENT NEEDED                         
*                                                                               
         TM    FLAG,FLGINSRV                                                    
         BZ    *+6                                                              
         LNR   R4,R4               LOAD NEGATIVE TO SUBTRACT MONTHS             
*                                                                               
         GOTO1 VADDAY,DMCB,(C'M',WORK),WORK+6,(R4)                              
         GOTO1 DATCON,DMCB,(0,WORK+6),(1,SVMOS)                                 
         B     GETINSX                                                          
*                                                                               
GETINS40 LA    R3,L'INSMINI(R3)    CHECK NEXT MEDIA                             
         BCT   R0,GETINS30                                                      
         B     GETINSX             PRINT MEDIA CODE NOT IN TABLE                
*        DC    H'0'                PRINT MEDIA CODE NOT IN TABLE                
GETINS50 LA    R2,INSTBLNQ(R2)                                                  
         B     GETINS10                                                         
*                                                                               
GETINSX  J     EXIT                                                             
**********************************************************************          
* LITERALS FOR GTINSRTN  ROUTINE                                     *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* BUILD TABLE OF REQUESTED MONTHS                                     *         
***********************************************************************         
         SPACE 1                                                                
SETMNTH  DS    0D                                                               
         NMOD1 0,*SETMTH*                                                       
         L     RC,0(R1)                                                         
*                                                                               
         USING MTHD,R3             BUILD LIST OF PACKED MONTHS                  
         LA    R3,MNTHS            FROM MOS START TO MOS END                    
         XC    MNTHS,MNTHS                                                      
*                                                                               
         MVC   WORK(3),AUTSMOS                                                  
         GOTO1 DATCON,DMCB,(1,WORK),(0,WORK+6) WORK+6=C'YYMMDD'                 
         MVC   WORK(6),WORK+6                                                   
*                                                                               
         LA    R0,MAXMTHS-2                                                     
         LA    R5,1                                                             
         XC    MTHCODE,MTHCODE     SET FIRST MONTH 00 FOR EST TOTALS            
         STC   R5,MTHNUM                                                        
         LA    R5,1(R5)            ADD NUMBER OF REQUEST MONTHS                 
         LA    R3,MTHLEN(R3)       BUMP TO SECOND ENTRY                         
*                                                                               
         MVC   MTHCODE,AUTSMOS     SET FIRST MONTH                              
         STC   R5,MTHNUM                                                        
SETM10   CLC   MTHCODE,AUTEMOS                                                  
         BE    SETM20                                                           
         LA    R5,1(R5)            ADD NUMBER OF REQUEST MONTHS                 
         LA    R3,MTHLEN(R3)                                                    
         GOTO1 VADDAY,DMCB,(C'M',WORK),WORK+6,1   GET NEXT MONTH                
         GOTO1 DATCON,DMCB,(0,WORK+6),(1,WORK+12)                               
         MVC   MTHCODE,WORK+12     PACKED                                       
         XC    MTHFLAG,MTHFLAG                                                  
         STC   R5,MTHNUM                                                        
         MVC   WORK(4),WORK+6                                                   
         BCT   R0,SETM10                                                        
         CLC   MTHCODE,AUTEMOS                                                  
         BE    SETM20                                                           
         CLI   AUTOFSW,C'Y'                                                     
         BNE   *+6                                                              
         DC    H'0'                REQUEST MONTHS RANGE > MAXMNTHS              
         OI    AUTERR,AUTMRNG      SEND ERROR BACK TO MARKER                    
*                                                                               
SETM20   STC   R5,RQMNTHS                                                       
*                                                                               
SETMX    XIT1                                                                   
**********************************************************************          
* LITERALS SETMNTH NMOD1                                             *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* BUILD CLIENT/PRODUCT/ESTIMATE/MOS TABLE FROM VENDOR PASSIVE POINTER*          
**********************************************************************          
         SPACE 1                                                                
         USING AAVPASD,R4                                                       
SSPNTTB  DS    0D                                                               
         NMOD1 0,*SSPNTR*                                                       
         L     RC,0(R1)                                                         
*                                                                               
         XC    SVSYS,SVSYS                                                      
         XC    SVMED,SVMED                                                      
*                                                                               
         XC    SVKEY,SVKEY         BUILD KEY TO READ SR RECDS                   
         XC    LSTVPKEY,LSTVPKEY                                                
         XC    LASTPKEY,LASTPKEY                                                
         NI    FLAG1,X'FF'-(FLGNTDIS+FLGRQMOS) INIT FLAG                        
*                                                                               
         LA    R4,SVKEY                                                         
         XC    AAVPKEY,AAVPKEY     INIT KEY                                     
*                                                                               
         MVI   AAVPTYP,AAVPTYPQ    AA VENDOR PASSIVE POINTER                    
         MVI   AAVPSUB,AAVPSUBQ    AA VENDOR PASSIVE POINTER SUB TYPE           
         MVC   AAVPCPY,AUTCPY      COMPANY                                      
         OC    AUTALST,AUTALST     DO WE HAVE A CLIENT LIST                     
         BNZ   SSPNT10                                                          
         MVC   AAVPCLT,AUTCLNT     3 BYTES CLIENT CODE                          
         MVC   AAVPPRD,AUTPROD     3 BYTES PRODUCT CODE                         
         MVC   AAVPEST,AUTEST      6 BYTES OF ESTIMATE                          
*                                                                               
SSPNT10  GOTO1 ADMRDHI,DMCB,(RC)                                                
         B     SSPNT30                                                          
*                                                                               
SSPNT20  GOTO1 ADMRDSEQ,DMCB,(RC)        READ NEXT POINTER                      
SSPNT30  CLC   SVKEY(AAVPCLT-AAVPKEY),IOKEY                                     
         BNE   SSPNTX                                                           
*                                                                               
         LA    R4,IO                                                            
         CLC   AUTEST,SPACES     WAS ESTIMATE IN REQUEST                        
         BNH   SSPNT40                                                          
         CLC   SVKEY+(AAVPEST-AAVPKEY)(3),AAVPEST                               
         BNE   SSPNT20                                                          
*                                                                               
SSPNT40  OC    AUTALST,AUTALST       ARE WE RUNNING OFF THE CLT LST?            
         BZ    SSPNT50                                                          
         MVC   CLNT,AAVPCLT                                                     
         BRAS  RE,CHKCLT             CHECK IF CLI IS VALID IN LIST              
         BNE   SSPNT20               READ NEXT POINTER                          
         B     SSPNT70                                                          
*                                                                               
SSPNT50  CLC   AUTPROD,SPACES        WAS PRODUCT IN REQUEST                     
         BNH   SSPNT60                                                          
         CLC   SVKEY+(AAVPPRD-AAVPKEY)(3),AAVPPRD                               
         BNE   SSPNTX                                                           
*                                                                               
SSPNT60  CLC   AUTCLNT,SPACES        WAS CLIENT REQUESTED                       
         BNH   SSPNT70                                                          
         CLC   SVKEY+(AAVPCLT-AAVPKEY)(3),AAVPCLT                               
         BNE   SSPNTX                                                           
*                                                                               
SSPNT70  DS    0H                                                               
         CLC   AUTLDGR,SPACES                                                   
         BNH   SSPNT80                                                          
         CLC   AUTLDGR,AAVPACCT    DO WE HAVE A LEDGER MATCH                    
         BNE   SSPNT20                                                          
SSPNT80  DS    0H                                                               
         CLC   AUTSYSTM,SPACES                                                  
         BNH   SSPNT85                                                          
         CLC   AUTSYSTM,AAVPSYS    DO WE HAVE A SYSTEM MATCH                    
         BNE   SSPNT20                                                          
*                                                                               
SSPNT85  L     RF,ALDSYTAB         CHECK TABLE OF LEDGER/SYSTEM B/C             
SSPNT90  CLI   0(RF),X'FF'         ONLY WANT CERTAIN LEDGERS                    
         BE    SSPNT20             READ NEXT POINTER                            
         CLC   AAVPACCT(1),0(RF)                                                
         BE    SSPNT100                                                         
         LA    RF,2(RF)                                                         
         B     SSPNT90                                                          
*                                                                               
SSPNT100 DS    0H                  ** SKIP FULLY DISBURSED ESTIMATES **         
         CLC   LSTVPKEY(AAVPMOS-AAVPKEY),IOKEY  HAS ESTIMATE CHANGED?           
         BE    SSPNT110                                                         
         MVC   LSTVPKEY,IOKEY                                                   
         NI    FLAG1,X'FF'-FLGNTDIS                                             
         B     SSPNT120                                                         
*                                                                               
SSPNT110 TM    FLAG1,FLGNTDIS      DID WE FIND ANY NON DISBURSED EST?           
         BO    SSPNT130                                                         
SSPNT120 OC    AAVPUSED,AAVPUSED   HAS THIS BEEN DISBURSED                      
         BNZ   SSPNT20                                                          
         OI    FLAG1,FLGNTDIS      NOT FULLY DISBURSED ESTIMATE                 
         MVC   SVKEY,LSTVPKEY                                                   
         B     SSPNT10             GO READ AGAIN FOR THIS ESTIMATE              
*                                                                               
SSPNT130 DS    0H        ** SKIP ESTIMATE IF NO DETAILS ARE FOUND ***           
         CLC   LASTPKEY(AAVPMOS-AAVPKEY),IOKEY  HAS ESTIMATE CHANGED?           
         BE    SSPNT140                                                         
         MVC   LASTPKEY,IOKEY                                                   
         NI    FLAG1,X'FF'-FLGRQMOS                                             
         B     SSPNT150                                                         
*                                                                               
SSPNT140 TM    FLAG1,FLGRQMOS      DID WE FIND ANY REQUESTED MOS?               
         BO    SSPNT160                                                         
*                                                                               
SSPNT150 DS    0H                                                               
         CLI   AUTPRF5,C'Y'        USING PRINT BILLABLE MOS?                    
         BNE   *+12                NO, PROCEED NORMALLY                         
         CLI   AAVPSYS,C'P'        PRINT SYSTEM?                                
         BE    SSPNT160            POSTPONE FILTERING AFTER GETREC              
*                                                                               
         CLC   AAVPMOS,AUTSMOS                                                  
         BL    SSPNT20                                                          
         CLC   AAVPMOS,AUTEMOS                                                  
         BH    SSPNT20                                                          
         OI    FLAG1,FLGRQMOS      WE HAVE POINTER WITH REQESTED MOS            
         MVC   SVKEY,LASTPKEY                                                   
         B     SSPNT10             GO READ AGAIN FOR THIS ESTIMATE              
*                                                                               
         USING PSSAD,R3                                                         
SSPNT160 LA    R3,PSSAWRK                                                       
         XC    PSSAWRK,PSSAWRK                                                  
         BRAS  RE,ZAPSSBUK         ZAP ALL BUCKETS                              
*                                                                               
         MVC   PSSKTYP,AAVPFLG1    BAD,GOOD OR NO ESTIMATE                      
         MVC   PSSKSYS,AAVPSYS     MOVE SYSTEM TO TABLE                         
         MVC   SVSYS,AAVPSYS       SAVE OFF SYSTEM CODE                         
         MVC   PSSKCLI,AAVPCLT     CLT                                          
         MVC   PSSKPRO,AAVPPRD     PRD                                          
         MVC   PSSKEST,AAVPEST     ESTIMATE                                     
         MVC   PSSKMOS,AAVPMOS     MOS                                          
         MVC   PSSKOFF,AAVPOFF     OFFICE                                       
         MVI   PSSKSTN,C'S'        SS ACC IS STATION SP IS PUBLICATION          
         MVC   PSSKSTN+1(L'AAVPACCT),AAVPACCT SS ACCOUNT                        
*                                                                               
         CLI   PSSKSYS,C'N'        IS THIS NET SYSTEM ?                         
         BNE   *+12                                                             
         CLI   AUTPRF2,C'S'        RUN BY SYSTEM OR SYS/MED ?                   
         BE    *+10                                                             
         MVC   PSSKMED,AAVPACCT+1  MOVE IN MEDIA CODE TO TABLE                  
         MVC   SVMED,AAVPACCT+1    MOVE IN MEDIA CODE TO TABLE                  
*                                                                               
         MVC   PSSKDA,AAVPKDA      SAVE DISK ADDRESS IN KEY OF TABLE TO         
*                                  EXPAND SS TRANSACTIONS                       
* TEMP DEBUG                                                                    
         MVC   TEMPKEY,IO                                                       
* END TEMP DEBUG                                                                
*                                  EXPAND SS TRANSACTIONS                       
         GOTO1 ADMGET,DMCB,(RC),0 GET RECORD                                    
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLI   FILTOFF,C'Y'                                                     
         BNE   SSPNT165                                                         
*                                                                               
         ICM   RF,15,AUTEXTRA      A(EXTRA PARMS)                               
         LA    RF,LDGOPOSP-AUTBLKX(RF)                                          
         GOTO1 =A(OFFILT),DMCB,(RF)                                             
         BNE   SSPNT20                                                          
*                                  EXPAND SS TRANSACTIONS                       
SSPNT165 DS    0H                                                               
         USING TRNRECD,R2                                                       
         LA    R2,IO                                                            
*                                                                               
         CLI   TRNRSTYP,X'09'                                                   
         BE    SSPNT20                                                          
*                                                                               
         BRAS  RE,GETVEN           GET VENDOR FROM PAYABLE TRN RECORD           
         MVC   PSSKVEN,WORK                                                     
*                                                                               
         MVC   PSSINVD,TRNKDATE                                                 
         NI    FLAG1,X'FF'-FLG1ZERO RESET $0 INVOICE FLAG                       
*                                                                               
         LR    R5,R2                                                            
         AH    R5,=Y(TRNRFST-TRNKEY)                                            
*                                                                               
SSPNT170 CLI   0(R5),0                                                          
         BE    SSPNT240            NO INVOICE AND EST                           
         CLI   0(R5),TRNELQ        IS IT X'44' ELEMENT                          
         BE    SSPNT190                                                         
         CLI   0(R5),XPYELQ        IS IT X'46' ELEMENT                          
         BE    SSPNT230                                                         
         CLI   0(R5),TRSELQ        IS IT X'60' ELEMENT                          
         BE    SSPNT185                                                         
*        CLI   0(R5),GDAELQ        IS IT X'E5' ELEMENT                          
*        BE    SSPNT238                                                         
*                                                                               
SSPNT180 SR    R1,R1                                                            
         IC    R1,1(R5)            GET LENGTH OF ELEMENT                        
         AR    R5,R1                                                            
         B     SSPNT170                                                         
*                                                                               
SSPNT185 DS    0H                                                               
         USING TRSELD,R5           X'60', TRANSACTION STATUS ELEMENT            
         GOTO1 DATCON,DMCB,(2,TRSDATE),(1,PSSINVCD)                             
         B     SSPNT180                                                         
         DROP  R5                                                               
*                                                                               
         USING TRNELD,R5                                                        
SSPNT190 ZAP   PSSCLRB,TRNAMNT     CLEARED AMOUNT                               
*                                                                               
         TM    TRNRSTA2,TRNSUSED   HAS IT BEEN USED ?                           
         BNO   SSPNT200                                                         
         ZAP   PSSDISBB,TRNAMNT    CREDIT AND USED DATE (DISBURSED)             
         MVI   PSSKSTAT,PSSKDISB   DISBURSED ITEM                               
         B     SSPNT180                                                         
*                                                                               
SSPNT200 ZAP   PSSUDISB,TRNAMNT    ALL OF UNDISBURSED AMOUNT                    
*                                                                               
         TM    TRNSTAT,TRNSHOLD     IS THIS PAYMENT ON HOLD                     
         BNO   SSPNT210                                                         
         ZAP   PSSHELDB,TRNAMNT    HELD AMOUNT                                  
         MVI   PSSKSTAT,PSSKHELD   ADD TO HELD ITEM LINE                        
         B     SSPNT180                                                         
*                                                                               
SSPNT210 TM    TRNSTAT,TRNSAPPR    SELECTED FOR PAYMENT                         
         BNO   SSPNT220                                                         
         ZAP   PSSAPPRB,TRNAMNT    APPROVED AMOUNT                              
         MVI   PSSKSTAT,PSSKPREV   ADD TO PREVIOUSLY APPROVED ITEM              
         B     SSPNT180                                                         
*                                                                               
SSPNT220 ZAP   PSSUNAPB,TRNAMNT    UNAPPROVED AMOUNT                            
         MVI   PSSKSTAT,PSSKUNAP   ADD TO UNAPPROVED LINE                       
         CP    PSSUNAPB,=P'0'                                                   
         BNE   SSPNT180                                                         
         OI    FLAG1,FLG1ZERO      $0 INVOICE FOUND                             
         B     SSPNT180                                                         
         DROP  R5                                                               
*                                                                               
         USING XPYELD,R5                                                        
SSPNT230 DS    0H                                                               
         MVC   PSSKINV,XPYINV      MOVE INVOICE NUMBER TO TABLE                 
*                                                                               
         CLI   PSSKSYS,C'P'                                                     
         BE    *+8                                                              
         MVI   PSSKINV+L'PSSKINV-1,C' '                                         
*                                                                               
         MVC   SVAGY,XPYAGY        MEDIA SYSTEM AGENCY CODE                     
*                                                                               
         CLI   PSSKSYS,C'P'        PRINT?                                       
         BNE   SSPNT235                                                         
         CLI   AUTPRF5,C'Y'        USING PRINT BILLABLE MOS?                    
         BNE   SSPNT235                                                         
         CLI   XPYLN,XPYLN3Q       HAVE BILLABLE DATE?                          
         BL    SSPNT232                                                         
         OC    XPYBLDTE,XPYBLDTE   BILLABLE DATE PRESENT?                       
         BZ    SSPNT232                                                         
         MVC   PSSKMOS,XPYBLDTE                                                 
         B     SSPNT233            HAVE MOS - DO DATE FILTERING                 
*                                                                               
* MUST OBTAIN BILLABLE DATE USING REVERSE INSTAB LOGIC                          
*                                                                               
SSPNT232 MVC   SVMOS(L'PSSKMOS),PSSKMOS                                         
         OI    FLAG,FLGINSRV                                                    
         GOTOR GTINSRTN            GET INSERTION DATE                           
         NI    FLAG,X'FF'-FLGINSRV                                              
         MVC   PSSKMOS,SVMOS                                                    
*                                                                               
SSPNT233 TM    FLAG1,FLGRQMOS      DID WE FIND ANY REQUESTED MOS?               
         BO    SSPNT235                                                         
*                                                                               
         CLC   PSSKMOS,AUTSMOS                                                  
         BL    SSPNT20                                                          
         CLC   PSSKMOS,AUTEMOS                                                  
         BH    SSPNT20                                                          
         OI    FLAG1,FLGRQMOS      WE HAVE POINTER WITH REQESTED MOS            
         MVC   SVKEY,LASTPKEY                                                   
         B     SSPNT10             GO READ AGAIN FOR THIS ESTIMATE              
*                                                                               
SSPNT235 DS    0H                                                               
         CLI   PSSKSYS,C'N'                                                     
         BNE   SSPNT180                                                         
         CLC   XPYSMED,SPACES      IF NO MEDIA/SUBMEDIA THEN LEAVE              
         BNH   SSPNT180                                                         
         CLI   AUTPRF2,C'S'        RUN BY SYSTEM OR SYS/MED ?                   
         BE    *+10                                                             
         MVC   PSSKMED,XPYSMED     MOVE IN MEDIA FOR SYSTEM C'N' NETWRK         
         B     SSPNT180                                                         
         DROP  R5                                                               
*&&DO                                                                           
         USING GDAELD,R5                                                        
SSPNT238 CLI   GDATYPE,GDATRECN                                                 
         BNE   SSPNT180                                                         
         MVC   PSSINVCD,GDADATE2   INVOICE CLEARED DATE                         
         B     SSPNT180                                                         
*&&                                                                             
*                                                                               
* END OF RECORD                                                                 
* ALL ELEMENTS PROCESSED ON THIS TRANSACTION RECORD                             
SSPNT240 DS    0H                                                               
         CLI   PSSKSYS,C'N'        IS THIS NET SYSTEM ?                         
         BNE   *+12                                                             
         CLI   AUTPRF2,C'S'        RUN BY SYSTEM OR SYS/MED ?                   
         BE    SSPNT245                                                         
         CLC   AUTMEDIA,SPACES                                                  
         BNH   SSPNT245                                                         
         CLC   AUTMEDIA,PSSKMED   DOES MEDIA MATCH WITH REQUEST                 
         BNE   SSPNT20            GET NEXT POINTER                              
*                                                                               
SSPNT245 CLC   PSSKMOS,AUTSMOS    IS MOS <  START DATE                          
         BL    SSPNT280           ADD TO PRIOR ENTRY AND TOTALS                 
         CLC   PSSKMOS,AUTEMOS    IS MOS > END DATE                             
         BH    SSPNT300                                                         
*                                                                               
         MVI   PSSKRTYP,X'05'     DETAIL RECORD BY MOS+VENDOR                   
* 3/2/17 - 3 LINES BELOW COMMENTED OUT TO ALWAYS EXPAND TRANSACTIONS            
*        CLI   PSSKSTAT,PSSKUNAP                                                
*        BE    *+10                                                             
*        XC    PSSKDA,PSSKDA                                                    
*                                                                               
         MVC   PSSAGY,SVAGY        MEDIA SYSTEM AGENCY CODE                     
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2                                         
         CLI   BCTSERRS,0                                                       
         BE    *+14                                                             
         CLI   AUTOFSW,C'Y'                                                     
         JNE   ROUTH               EXIT WITH CC SET IF ONLINE                   
         DC    H'0'                DIE IF RUNNING OFFLINE                       
*                                                                               
         AP    NUMRECS,=P'1'                                                    
*                                                                               
         CLI   AUTOPT2,C'Y'        SHOW PREV APPR & UNAPPR AS 2 LINES           
         BE    SSPNT270                                                         
*                                                                               
         CLI   PSSKSTAT,PSSKPREV   PREVIOUSLY APPROVED TO TOTAL                 
         BE    SSPNT260                                                         
         CLI   PSSKSTAT,PSSKUNAP   UNAPPROVED TO TOTALS                         
         BNE   SSPNT270                                                         
*                                                                               
SSPNT260 LA    RE,PSSAWRK+(PSSKSTAT-PSSAKEY) CLEAR STATUS ONWARDS.              
         XC    0(PSSKLNQ2,RE),0(RE)                                             
         MVC   PSSAGY,SVAGY        MEDIA SYSTEM AGENCY CODE                     
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2                                         
         CLI   BCTSERRS,0                                                       
         BE    *+14                                                             
         CLI   AUTOFSW,C'Y'                                                     
         JNE   ROUTH               EXIT WITH CC SET IF ONLINE                   
         DC    H'0'                DIE IF RUNNING OFFLINE                       
*                                                                               
* UPDATE VENDOR-LEVEL TOTALS                                                    
*                                                                               
SSPNT270 DS    0H                                                               
         LA    RE,PSSAWRK+(PSSKSTN-PSSAKEY) CLEAR SS ACCOUNT ONWARDS            
         XC    PSSKSTN(PSSKLSTNFWQ),PSSKSTN                                     
         MVC   PSSAGY,SVAGY        MEDIA SYSTEM AGENCY CODE                     
         TM    FLAG1,FLG1ZERO                                                   
         BZ    *+8                                                              
         OI    PSSFLAG1,PSS1ZERO                                                
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2                                         
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                DIE IF RUNNING OFFLINE                       
*                                                                               
* UPDATE MOS TOTALS                                                             
*                                                                               
         MVI   PSSKRTYP,X'03'     MOS TOTALS                                    
         XC    PSSKVEN(PSSKLVENFWQ),PSSKVEN                                     
         MVC   PSSAGY,SVAGY        MEDIA SYSTEM AGENCY CODE                     
         TM    FLAG1,FLG1ZERO                                                   
         BZ    *+8                                                              
         OI    PSSFLAG1,PSS1ZERO                                                
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2                                         
         CLI   BCTSERRS,0                                                       
         BE    SSPNT310                                                         
         DC    H'0'                DIE IF RUNNING OFFLINE                       
*                                                                               
* UPDATE TOTAL                                                                  
*                                                                               
         MVI   PSSKRTYP,X'00'     TOTAL                                         
         XC    PSSKRTYP(PSSKLNQ1),PSSKRTYP                                      
         MVC   PSSAGY,SVAGY        MEDIA SYSTEM AGENCY CODE                     
         TM    FLAG1,FLG1ZERO                                                   
         BZ    *+8                                                              
         OI    PSSFLAG1,PSS1ZERO                                                
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2                                         
         CLI   BCTSERRS,0                                                       
         BE    SSPNT310                                                         
         DC    H'0'                DIE IF RUNNING OFFLINE                       
*                                                                               
SSPNT280 DS    0H                                                               
         CP    PSSUNAPB,=P'0'                                                   
         BZ    SSPNT290                                                         
         OI    PSSFLAG,PSSPREV    UNAPPROVED ITEM MORE THAN A YEAR OLD          
*                                                                               
SSPNT290 LA    RE,PSSAWRK+(PSSKRTYP-PSSAKEY) CLEAR REC TYPE ONWARDS.            
         XC    0(PSSKLNQ1,RE),0(RE)                                             
         MVI   PSSKRTYP,X'01'      PRIOR MOS'S TOTALS                           
         MVC   PSSAGY,SVAGY        MEDIA SYSTEM AGENCY CODE                     
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2                                         
         CLI   BCTSERRS,0                                                       
         BE    SSPNT310                                                         
         CLI   AUTOFSW,C'Y'                                                     
         JNE   ROUTH               EXIT WITH CC SET IF ONLINE                   
         DC    H'0'                DIE IF RUNNING OFFLINE                       
*                                                                               
SSPNT300 LA    RE,PSSAWRK+(PSSKRTYP-PSSAKEY) CLEAR REC TYPE ONWARDS.            
         XC    0(PSSKLNQ1,RE),0(RE)                                             
         MVI   PSSKRTYP,X'04'      FUTURE MOS'S TOTALS                          
         MVC   PSSAGY,SVAGY        MEDIA SYSTEM AGENCY CODE                     
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2                                         
         CLI   BCTSERRS,0                                                       
         BE    *+14                                                             
         CLI   AUTOFSW,C'Y'                                                     
         JNE   ROUTH               EXIT WITH CC SET IF ONLINE                   
         DC    H'0'                DIE IF RUNNING OFFLINE                       
*                                                                               
         CLI   AUTOPT6,C'Y'        APPROVING BY MOS?                            
         BE    SSPNT310                                                         
         CP    PSSUNAPB,=P'0'     IS THIS UNAPPROVED ITEM                       
         BNZ   SSPNT20            EXCLUDE POST REQUEST UNAPPROVED ITEMS         
*                                                                               
SSPNT310 DS    0H                                                               
         LA    RE,PSSAWRK+(PSSKRTYP-PSSAKEY) CLEAR REC TYPE ONWARDS.            
         XC    0(PSSKLNQ1,RE),0(RE)                                             
         MVI   PSSKRTYP,X'00'      ALL TOTALS                                   
         MVC   PSSAGY,SVAGY        MEDIA SYSTEM AGENCY CODE                     
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2                                         
         CLI   BCTSERRS,0                                                       
         BE    SSPNT20                                                          
         CLI   AUTOFSW,C'Y'                                                     
         JNE   ROUTH               EXIT WITH CC SET IF ONLINE                   
         DC    H'0'                DIE IF RUNNING OFFLINE                       
*                                                                               
SSPNTX   DS    0H                                                               
         XC    SVSYS,SVSYS                                                      
         XIT1                                                                   
         DROP  R2,R3,R4                                                         
         EJECT                                                                  
**********************************************************************          
* LITERALS FOR SSPNTTB NMOD                                          *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ADD ITEM TO BINSRCH TABLE AND ACCUMULATE TOTALS                     *         
*  P1    A(ITEM TO BE ADDED)                                          *         
*  P2    A(TABLE)                                                     *         
***********************************************************************         
         SPACE 1                                                                
         USING BIND,R5                                                          
BINADD   DS    0D                                                               
         NMOD1 0,**BINA**                                                       
         L     RC,0(R1)                                                         
* TEMP DEBUG                                                                    
         DC    H'0'                                                             
* END TEMP DEBUG                                                                
         LA    RF,*+10             HIGH CORE TABLE                              
         O     RF,=X'80000000'                                                  
         BSM   0,RF                31 BIT MODE                                  
*                                                                               
         L     R5,8(R1)                                                         
*                                                                               
         L     R2,12(R1)                                                        
         STC   R2,BYTE                                                          
*                                                                               
         MVC   DMCB+8(16),BININ    NUMBER LENGTH,KEY,MAX                        
         LA    R6,BINTAB           A(TABLE)                                     
         L     R3,4(R1)            A(ITEM)                                      
*                                                                               
         LA    R2,DMCB                                                          
         ST    R3,0(R2)            BINSRCH PARAMETER 1                          
         ST    R6,4(R2)            BINSRCH PARAMETER 2                          
         LA    R2,DMCB+12          BINSRCH PARAMETER 4                          
         CLI   BYTE,X'01'          DO WE WANT TO SEARCH ON EXACT MATCH          
         BE    BINA10                                                           
         CLI   BYTE,X'00'          DO WE WANT TO INSERT IF NOT FOUND            
         BE    BINA20                                                           
BINA10   MVI   0(R2),X'00'         BYTE 0 PARAMETER 1 LOOK FOR EX MTCH          
         NI    FLAG,X'FF'-FLGNTFND RECORD WAS FOUND IF NOT SET                  
         B     BINA40                                                           
BINA20   MVI   0(R2),X'01'         BYTE 0 PARAMETER 1 INSERT                    
BINA40   GOTO1 BINSRC31,DMCB                                                    
         OC    DMCB(4),DMCB                                                     
         BNZ   *+6                                                              
         DC    H'0'                TABLE IS FULL                                
         MVC   BININ,DMCB+8        UPDATE COUNT                                 
*                                                                               
         TM    DMCB,X'80'          RECORD WAS ADDED/NOT FOUND                   
         BNO   BINA50                                                           
         CLI   BYTE,X'01'          ARE WE LOOKING FOR EXACT MATCH               
         BNE   BINXIT              NO, ADDING RECORD, EXIT                      
         OI    FLAG,FLGNTFND       RECORD NOT FOUND                             
         B     BINXIT                                                           
*                                                                               
BINA50   LR    R2,R3               SAVE OFF A(ITEM)                             
*                                                                               
         L     R4,DMCB             A(RECORD FOUND)                              
         SR    R0,R0                                                            
         ICM   R0,1,BINNUM         NUMBER OF BUCKETS                            
         BZ    BINXIT              NO BUCKETS - EXIT                            
         SR    R6,R6                                                            
         IC    R6,BINFST           DISPLACEMENT TO FIRST BUCKET                 
         AR    R4,R6               BUMP TO FIRST BUCKET IN TABLE                
         AR    R3,R6               BUMP TO FIRST BUCKET IN NEW ITEM             
BINA60   AP    0(PSSABKLN,R4),0(PSSABKLN,R3)   ADD TO BUCKET                    
         LA    R3,PSSABKLN(R3)    BUMP TO NEXT ENTRY IN NEW ITEM                
         LA    R4,PSSABKLN(R4)    BUMP TO NEXT ENTRY IN TABLE                   
         BCT   R0,BINA60                                                        
         TM    FLAG,FLGSR         ARE WE BUILDING SR TABLE                      
         BO    BINXIT                                                           
*                                                                               
         L     R4,DMCB             A(RECORD FOUND)                              
         LR    RE,R2               A(NEW ITEM)                                  
         LHI   R6,PSSFLAG-PSSAD    DISPLACEMENT TO FLAG                         
         AR    R4,R6               BUMP TO FLAG IN TABLE                        
         AR    RE,R6               BUMP TO FLAG IN NEW ITEM                     
*                                                                               
         CLI   BYTE,X'01'          ARE WE UPDATING SR BUCKETS IN SSTAB          
         BNE   BINA70                                                           
         TM    0(R4),PSSDISB       IS IT DISBURSED ONLY ITEM                    
         BNO   BINA70                                                           
         OI    FLAG1,PSSDISB                                                    
*                                                                               
BINA70   OC    0(PSSFLNQ,R4),0(RE)   UPDATE ALL FLAGS                           
*                                                                               
BINXIT   XIT1                                                                   
         DROP  R5                                                               
         EJECT                                                                  
**********************************************************************          
* LITERALS  BINADD NMOD1                                             *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* TSAR INTERFACE ROUTINE - TSAR USES AIO1                             *         
*                                                                     *         
* NTRY - P1 = (RC)                                                    *         
*      - P2 = TSAR ACTION (DEFINED IN DDTSARD)                        *         
*        P3 = TSAR BUFFER (1 OR 2)                                    *         
*                                                                     *         
* EXIT - CC=EQU - ACTION COMPLETED WITHOUT ERRORS                     *         
*        CC=NEQ - BCTSERRS=TSAR ERROR CODE                            *         
***********************************************************************         
         SPACE 1                                                                
MTSAR    DS    0D                                                               
         NMOD1 0,**MTSAR*                                                       
         L     RC,0(R1)            P1=A(LOCAL WORKING)                          
         STCM  R1,15,BCFULL        SAVE  ORIGINAL PARM LIST                     
         MVC   BCFLAG1,7(R1)       P2=TSAR ACTION                               
         MVC   BCTSINDS,11(R1)     P3=TSAR BUFFER                               
*                                                                               
MTSAR01  NI    TSARSTAT,X'FF'-TSARBUF2 ALWAYS SHUT OFF BIT ON ENTRY             
*                                                                               
         USING TSARD,R3                                                         
*                                                                               
         CLI   BCTSINDS,1                                                       
         BNE   MTSAR01A                                                         
         LA    R3,TSRBLK1                                                       
         LA    R1,TSRAWRK          RECEIVABLE TABLE WORK AREA                   
*                                                                               
MTSAR01A DS    0H                                                               
         CLI   BCTSINDS,2          ARE WE READING/WRITING TO BUFF2              
         BNE   MTSAR01B                                                         
         LA    R3,TSRBLK2          IF SO USE BLOCK2                             
         OI    TSARSTAT,TSARBUF2   MARK THAT 2ND BUFFER IS IN USE               
         LA    R1,PSSAWRK          PAYABLE TABLE WORK AREA                      
*                                                                               
MTSAR01B DS    0H                                                               
         CLI   BCTSINDS,3          RECEIVABLE PP BUFFER?                        
         BNE   MTSAR02                                                          
         LA    R3,TSRBLK3                                                       
         LA    R1,SRPPWRK                                                       
*                                                                               
MTSAR02  DS    0H                                                               
         CLI   AUTOFSW,C'Y'                                                     
         BE    MBUFFRIN                                                         
*                                                                               
         CLI   BCFLAG1,TSAINI                                                   
         BNE   *+10                                                             
         XC    0(L'TSRBLK1,R3),0(R3) CLEAR BLOCK                                
         ST    R1,TSAREC           ADDRESS OF RECORD                            
         MVC   TSACOM,AUTCOMF                                                   
         MVC   TSACTN,BCFLAG1                                                   
*                                                                               
         CLI   BCFLAG1,TSAINI      *** TSAR INITIALIZE ***                      
         BNE   MTSAR10                                                          
*                                  ***TSAR PARAMETERS**                         
         MVI   TSKEYL,TSRKLNQ        KEY LENGTH                                 
         LHI   R1,TSRALNQ                                                       
         CLI   BCTSINDS,2            ARE WE READING/WRITING TO BUFF2            
         BNE   MTSAR03                                                          
         MVI   TSKEYL,PSSAKLNQ       KEY LENGTH                                 
         LHI   R1,PSSALNQ                                                       
MTSAR03  STH   R1,TSRECL             MAX RECORD LENGTH                          
         XC    TSRNUM,TSRNUM         RECORD NUMBER                              
*                                                                               
*                                  ***ONLINE TSAR PARAMETERS****                
         MVI   TSPAGN,TSPEXPN      SET NUMBER OF TEMPEST PAGES                  
         MVI   TSINDS,TSIALLOC+TSIRTNAF                                         
*                                                                               
         CLI   BCTSINDS,2          ARE WE INTERFACING W/ BUFF2                  
         BE    MTSAR06                                                          
*                                  MARKER INITIALIZES TSAR BUFFERS              
*        TM    TSRMODE,TSRMGOT1    DID WE ALREADY INITIALIZE TSAR BUFF1         
*        BNO   MTSARGO                                                          
         LA    R1,TSRFLDS1         POINT TO FLDS FOR BLK 1                      
         B     MTSAR08                                                          
*                                                                               
MTSAR06  OI    TSIND2,TSI2BUF2     USING TSAR BUFFER 2                          
         TM    TSRMODE,TSRMGOT2    DID WE ALREADY INITIALIZE TSAR BUFF2         
         BNO   MTSARGO                                                          
         LA    R1,TSRFLDS2         POINT TO FLDS FOR BLK 2                      
*                                                                               
         USING TSRTSAR1,R1         USE 1ST BLKS FLDS AS DSECT                   
MTSAR08  DS    0H                                                               
         MVC   TSINDS,TSRTSAR1     SET PREVIOUS INDICATORS                      
         OI    TSINDS,TSIREUSE     SET TO REUSE PREVIOUS ALLOC                  
         MVC   TSPAGL,TSRLOWP1     SET PREV LOW PAGE NUMBER                     
         MVC   TSPAGN,TSRNUMP1     SET PREV NUMBER OF PAGES ALLOC               
         B     MTSARGO                                                          
         DROP  R1                                                               
*                                                                               
MTSAR10  CLI   BCFLAG1,TSARES      *** TSAR RESTORE ***                         
         BNE   MTSAR20                                                          
*                                                                               
         LA    R1,TSRFLDS1         POINT TO FLDS FOR BLK 1                      
         CLI   BCTSINDS,2          ARE WE READING/WRITING TO BUFF2              
         BNE   *+8                                                              
         LA    R1,TSRFLDS2         POINT TO FLDS FOR BLK 2                      
         USING TSRTSAR1,R1         USE 1ST BLKS FLDS AS DSECT                   
         MVC   TSINDS,TSRTSAR1     SET PREVIOUS INDICATORS                      
         OI    TSINDS,TSIREUSE     SET TO REUSE PREVIOUS ALLOC                  
         MVC   TSPAGL,TSRLOWP1     SET PREV LOW PAGE NUMBER                     
         MVC   TSPAGN,TSRNUMP1     SET PREV NUMBER OF PAGES ALLOC               
         B     MTSARGO                                                          
         DROP  R1                                                               
*                                                                               
MTSAR20  CLI   BCFLAG1,TSARDH      *** TSAR READ HIGH ***                       
         BNE   MTSAR22                                                          
         XC    TSRNUM,TSRNUM                                                    
         B     MTSARGO                                                          
*                                                                               
MTSAR22  CLI   BCFLAG1,TSAGET      *** TSAR GET ***                             
         BNE   MTSAR30                                                          
         DC    H'0'                USE TSARDH INSTEAD OF GET.                   
*                                  BECAUSE BUFFERIN CAN'T HANDLE                
*                                                                               
MTSAR30  CLI   BCFLAG1,TSADEL      *** TSAR DELETE ***                          
         BNE   MTSARGO                                                          
         XC    TSRNUM,TSRNUM                                                    
*                                                                               
MTSARGO  DS    0H                                                               
         GOTO1 XTSAR,TSARD                                                      
         MVC   BCTSERRS,TSERRS     PASS BACK ERRORS                             
         TM    TSERRS,TSEDUP       DUPLICATE KEY ON ADD                         
         BO    MTSARG30                                                         
         CLI   TSERRS,0                                                         
         JNE   ROUTH               EXIT WITH ERROR                              
*                                                                               
         LA    R1,TSRFLDS1         POINT TO FLDS FOR BLK 1                      
         CLI   BCTSINDS,2          ARE WE READING/WRITING TO BUFF2              
         BE    *+12                                                             
         OI    TSRMODE,TSRMGOT1    SHOW THAT BUFF1 HAS BEEN INITIALIZED         
         B     *+12                                                             
         LA    R1,TSRFLDS2         POINT TO FLDS FOR BLK 2                      
         OI    TSRMODE,TSRMGOT2    SHOW THAT BUFF2 HAS BEEN INITIALIZED         
*                                                                               
         USING TSRTSAR1,R1         USE 1ST BLKS FLDS AS DSECT                   
         MVC   TSRLOWP1,TSPAGL     SAVE LOW TSAR PAGE NUMBER                    
         MVC   TSRNUMP1,TSPAGN     SAVE NUMBER OF PAGES ALLOCATED               
         MVC   TSRTSAR1,TSINDS     SAVE TEMPEST INDICATORS                      
         J     ROUTE                                                            
*                                                                               
MTSARG30 DS    0H                  UPDATE DUPL RECD FOUND DURING ADD            
         XC    TEMPWRK,TEMPWRK     CLEAR TEMPORARY WORK AREA                    
         LA    RE,TEMPWRK                                                       
         LA    RF,PSSAWRK                                                       
         LHI   R1,L'PSSAWRK                                                     
         CLI   BCTSINDS,1                                                       
         BNE   *+12                                                             
         LA    RF,TSRAWRK                                                       
         LHI   R1,L'TSRAWRK                                                     
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RE),0(RF)       SAVE OFF WORK AREA TO TEMP FOR UPD           
         MVI   TSACTN,TSARDH       ONLINE TSAR ACTION FIELD                     
*                                                                               
         GOTO1 XTSAR,TSARD         READ FROM AND INTO PSSAWRK/TSRAWRK           
         MVC   BCTSERRS,TSERRS     PASS BACK ERRORS                             
         CLI   TSERRS,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R2,TEMPWRK          A(ITEM) TO BE ADDED                          
         L     R4,TSAREC           A(RECORD FOUND)                              
         USING BIND,R5                                                          
         L     R5,APSSATAB                                                      
         CLI   BCTSINDS,1                                                       
         BNE   *+8                                                              
         L     R5,ATSRTAB                                                       
         SR    R0,R0                                                            
         ICM   R0,1,BINNUM         NUMBER OF BUCKETS                            
         BZ    MTSARX              NO BUCKETS - EXIT                            
         SR    R6,R6                                                            
         IC    R6,BINFST           DISPLACEMENT TO FIRST BUCKET                 
         AR    R4,R6               BUMP TO FIRST BUCKET IN TSAREC               
         AR    R2,R6               BUMP TO FIRST BUCKET IN NEW ITEM             
MTSARG40 AP    0(PSSABKLN,R4),0(PSSABKLN,R2)   ADD TO BUCKET                    
         LA    R2,PSSABKLN(R2)    BUMP TO NEXT ENTRY IN NEW ITEM                
         LA    R4,PSSABKLN(R4)    BUMP TO NEXT ENTRY IN TABLE                   
         BCT   R0,MTSARG40                                                      
         TM    FLAG,FLGSR         ARE WE BUILDING SR TABLE                      
         BO    MTSARG60           WRITE RECORD BACK                             
*                                                                               
         L     R4,TSAREC           A(RECORD FOUND) PSSAWRK                      
         LA    R2,TEMPWRK          A(NEW ITEM)                                  
         LHI   R6,PSSFLAG-PSSAD    DISPLACEMENT TO FLAG                         
         AR    R4,R6               BUMP TO FLAG IN TABLE                        
         AR    R2,R6               BUMP TO FLAG IN NEW ITEM                     
MTSARG50 OC    0(PSSFLNQ,R4),0(R2)   UPDATE ALL FLAGS                           
*                                                                               
MTSARG60 DS    0H                                                               
         MVI   TSACTN,TSAWRT       ONLINE TSAR ACTION FIELD                     
*                                                                               
         GOTO1 XTSAR,TSARD         WRITE RECORD BACK  PSSAWRK/TSRAWRK           
         MVC   BCTSERRS,TSERRS     PASS BACK ERRORS                             
         CLI   TSERRS,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLI   BCTSINDS,1                                                       
         BNE   *+14                                                             
         MVC   TSRAWRK,TEMPWRK                                                  
         B     *+10                                                             
         MVC   PSSAWRK,TEMPWRK                                                  
         J     MTSARX                                                           
*                                                                               
MBUFFRIN DS    0H                                                               
         LA    RF,PAYBUF                                                        
         STCM  RF,15,AUTPBUFF      SAVE ADDRESS OF THESE PARMS                  
*                                                                               
         CLI   BCTSINDS,1                                                       
         BNE   *+8                                                              
         LA    RF,RCVBUF                                                        
*                                                                               
         CLI   BCTSINDS,2                                                       
         BNE   *+8                                                              
         LA    RF,PAYBUF                                                        
*                                                                               
         CLI   BCTSINDS,3                                                       
         BNE   *+8                                                              
         LA    RF,SRPPBUF                                                       
*                                                                               
BUFFER02 ST    R1,BUFFREC          SET A(INPUT RECORD)                          
         MVC   TSARKSAV,0(R1)      SAVE CURRENT RECORD KEY                      
         LR    R3,RF                                                            
         USING BUFFD,R3            R3=A(BUFFERIN CONTROL BLOCK)                 
         LHI   R0,BUFFAINI         CONVERT TSAR ACTION TO BUFFERIN              
         CLI   BCFLAG1,TSAINI                                                   
         JE    BUFFER08                                                         
         LHI   R0,BUFFAPUT                                                      
         CLI   BCFLAG1,TSAADD                                                   
         JE    BUFFER08                                                         
         CLI   BCFLAG1,TSAWRT                                                   
         JE    BUFFER08                                                         
         CLI   BCFLAG1,TSAPUT                                                   
         JE    BUFFER08                                                         
         LHI   R0,BUFFASEQ                                                      
         CLI   BCFLAG1,TSANXT                                                   
         JE    BUFFER08                                                         
         LHI   R0,BUFFARDH                                                      
         CLI   BCFLAG1,TSARDH                                                   
         JE    BUFFER08                                                         
         DC    H'0'                                                             
                                                                                
BUFFER08 CHI   R0,BUFFAINI         TEST INITIALIZATION CALL                     
         JNE   BUFFER10                                                         
*                                                                               
         USING BIND,R5                                                          
*                                                                               
         CLI   BCTSINDS,1                                                       
         BNE   *+8                                                              
         L     R5,ATSRTAB          POINT TO RECEIVABLE TABLE                    
*                                                                               
         CLI   BCTSINDS,2                                                       
         BNE   *+8                                                              
         L     R5,APSSATAB         POINT TO PAYABLES TABLE                      
*                                                                               
         CLI   BCTSINDS,3                                                       
         BNE   *+8                                                              
         L     R5,ASRPPTAB         POINT TO SR PP TABLE                         
*                                                                               
         SR    RE,RE                                                            
         ICM   RE,7,BINKEY         KEY LENGTH                                   
         SR    RF,RF                                                            
         IC    RF,BINFST           RECORD LENGTH=RECLEN-BUCKETS                 
*        L     RF,BINLEN           RECORD LENGTH                                
         SR    RF,RE               DATA LENGTH                                  
         STCM  RE,3,BUFFLKEY       SET KEY LENGTH                               
         STCM  RF,3,BUFFLCOM       SET COMMENT LENGTH                           
         MVC   BUFFNCOL,BINNUM     NUMBER OF BUCKETS/COLUMNS                    
*                                                                               
BUFFER10 GOTOR AUTBFRIN,DMCB,((R0),BUFFD),BUFFREC,AUTCOMF                       
         MVC   BUFFRET,BUFFERRS-BUFFPARM(R1)                                    
         CHI   R0,BUFFARDH         EMULATE TSAR NOT FOUND ON READ HIGH          
         JNE   BUFFER12                                                         
         L     RF,BUFFREC                                                       
         LH    R1,BUFFLKEY                                                      
         BCTR  R1,0                                                             
         BASR  RE,0                                                             
         EX    R1,8(RE)                                                         
         JE    BUFFER12                                                         
         CLC   TSARKSAV(0),0(RF)                                                
         OI    BUFFRET,BUFFERNF                                                 
                                                                                
BUFFER12 MVC   BCTSERRS,BUFFRET    PASS BACK ERRORS                             
         CLI   BUFFRET,0           SET CONDITION CODE FOR CALLER                
         J     EXIT                                                             
*                                                                               
MTSARX   J     ROUTE                                                            
         DROP  R1,R3,R5                                                         
***********************************************************************         
* EXIT POINTS                                                         *         
***********************************************************************         
         SPACE 1                                                                
ROUTL    MVI   BCDUB,0             SET CC LOW                                   
         J     ROUTCC                                                           
ROUTH    MVI   BCDUB,2             SET CC HIGH                                  
         J     ROUTCC                                                           
ROUTE    MVI   BCDUB,1             SET CC EQUAL                                 
ROUTCC   CLI   BCDUB,1                                                          
ROUTX    XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* TABLES AND CONSTANTS                                               *          
**********************************************************************          
GLOBALS  DS    0D                                                               
*                                                                               
ACCMST   DC    CL8'ACCMST'                                                      
PKROUND  DC    XL2'010C'                                                        
         DS    0F                                                               
DMCB     DS    0XL24               PARAMETER LIST                               
P1       DC    F'0'                                                             
P2       DC    F'0'                                                             
P3       DC    F'0'                                                             
P4       DC    F'0'                                                             
P5       DC    F'0'                                                             
P6       DC    F'0'                                                             
         DC    XL8'00'             N/D                                          
SPACES   DC    CL132' '                                                         
WORK     DC    XL64'00'                                                         
DUB      DC    D'0'                                                             
BYTE     DC    XL1'00'                                                          
CTRY     DS    X                                                                
CTRYCAN  EQU   X'08'               CANADA                                       
RCCOMPFL DC    C' '                                                             
DMREAD   DC    C'DMREAD  '                                                      
DMRDHI   DC    C'DMRDHI  '                                                      
DMRSEQ   DC    C'DMRSEQ  '                                                      
DMADD    DC    C'DMADD   '                                                      
DMWRT    DC    C'DMWRT   '                                                      
DMGET    DC    C'GETREC  '                                                      
DMWORK   DC    XL96'00'                                                         
*                                                                               
RCVBUFQ  EQU   1                   ** RECEIVABLE BUFFER **                      
RCVBUF   BUFFD TYPE=P,KEYLEN=0,COMLEN=0,COLUMNS=0,BUFFERS=100,         +        
               REPCOM=YES                                                       
*                                                                               
PAYBUFQ  EQU   2                   ** PAYABLE BUFFER **                         
PAYBUF   BUFFD TYPE=P,KEYLEN=0,COMLEN=0,COLUMNS=0,BUFFERS=250,         +        
               REPCOM=YES                                                       
*                                                                               
SRPPBUFQ EQU   3                   ** SR PP BUFFER **                           
SRPPBUF  BUFFD TYPE=P,KEYLEN=0,COMLEN=0,COLUMNS=0,BUFFERS=100                   
*                                                                               
**********************************************************************          
* RELOCATACBLES                                                      *          
**********************************************************************          
         SPACE 1                                                                
ADCONS   DS    0F                                                               
         DC    A(SETMNTH)          SET REQUESTED MONTHS TABLE                   
         DC    A(BINADD)           ROUTINE TO ADD TO BINSEARCH TABLE            
         DC    A(TSRTAB)           SR TRANSACTIONS TABLE                        
         DC    A(PSSATAB)          SS TRANSACTIONS TABLE                        
         DC    A(SSPNTTB)          BUILD VENDOR TABLE USING POINTERS            
         DC    A(MAINTAB)          MAIN TABLE                                   
         DC    A(LDSYTAB)          LEDGER SYSTEM TABLE                          
         DC    A(SMEDTAB)          SYSTEM MEDIA TABLE                           
         DC    A(PSYSTAB)          PRINT SYSTEM TABLE                           
         DC    A(INSTAB)           INSERTION DATE CALCULATION TABLE             
         DC    A(MTSAR)            TSAR MAINTENANCE ROUTINE                     
         DC    A(DMSEQDR)          READ SEQUENTIAL                              
         DC    A(DMHIGHDR)         READ HIGH                                    
         DC    A(DMREADDR)         READ                                         
         DC    A(DMGETREC)         GET RECORD                                   
         DC    A(DMWRTDR)          WRITE RECORD                                 
         DC    A(DMPUTREC)         PUT RECORD                                   
         DC    V(PRNTBL)           PRINT DATA                                   
         DC    V(BINSRCH)          BINSRCH (31 BIT MODE)                        
         DC    V(HELLO)            HELLO                                        
         DC    V(ACLIST)           CLIENT LIST                                  
         DC    V(DATCON)           DATE CONTROLLER                              
         DC    V(GETBROAD)         BROADCAST DATE / MOS                         
         DC    V(GETDAY)           GETDAY                                       
         DC    V(ADDAY)            ADD DAY                                      
         DC    V(MASTC)                                                         
         DC    V(DATAMGR)          DATA MANAGER                                 
         DC    A(SRPPTAB)          SR PROCESSED KEY TABLE                       
ADCONN   EQU   (*-ADCONS)/L'ADCONS                                              
         SPACE 2                                                                
         EJECT                                                                  
***********************************************************************         
* GETEL                                                               *         
***********************************************************************         
         SPACE 1                                                                
         GETEL R5,56,ELCODE                                                     
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* DATAMGR INTERFACE                                                  *          
**********************************************************************          
         SPACE 1                                                                
DMSEQDR  NMOD1 0,SEQ               READ SEQUENTIAL                              
         L     RC,0(R1)            RESET RC                                     
         GOTO1 DATAMGR,DMCB,(X'88',DMRSEQ),=C'ACCDIR ',SVKEY,IO,0               
         B     DMX                                                              
*                                                                               
DMHIGHDR NMOD1 0,HIGH              READ HIGH                                    
         L     RC,0(R1)            RESET RC                                     
         GOTO1 DATAMGR,DMCB,(X'88',DMRDHI),=C'ACCDIR ',SVKEY,IO,0               
         B     DMX                                                              
*                                                                               
DMREADDR NMOD1 0,READ              READ                                         
         L     RC,0(R1)            RESET RC                                     
         GOTO1 DATAMGR,DMCB,(X'88',DMREAD),=C'ACCDIR ',SVKEY,IO,0               
         B     DMX                                                              
*                                                                               
DMGETREC NMOD1 0,GREC              GET RECORD                                   
         L     RC,0(R1)            RESET RC                                     
         L     R2,4(R1)                                                         
         LTR   R2,R2               DO WE HAVE DISK ADDRESS                      
         BNZ   DMGETR10            YES                                          
         USING ACCRECD,R3                                                       
         LA    R3,IO                                                            
         LA    R2,ACCKDA                                                        
DMGETR10 GOTO1 DATAMGR,DMCB,DMGET,=C'ACCMST ',(R2),IO,DMWORK                    
         B     DMX                                                              
*                                                                               
DMWRTDR  NMOD1 0,WRT               WRITE BACK TO DIR                            
         L     RC,0(R1)            RESET RC                                     
         GOTO1 DATAMGR,DMCB,(X'00',=C'DMWRT'),=C'ACCDIR',IOKEY,IOKEY            
         B     DMX                                                              
*                                                                               
DMPUTREC NMOD1 0,PREC              PUT RECORD                                   
         L     RC,0(R1)            RESET RC                                     
         GOTO1 DATAMGR,DMCB,=CL8'PUTREC',=C'ACCMST',ACCKDA,IO,DMWORK            
*                                                                               
DMX      XIT1                                                                   
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* TABLES                                                             *          
**********************************************************************          
*                                                                               
* BINTABLE 1 - SR TRANSACTION TABLE BUILT IN PROC TRANSACTION                   
*                                                                               
MAINTAB  DS    0F                                                               
         DC    C'**TSRACCT*'                                                    
TSRTAB   DS    0C                                                               
         DC    AL4(0)                  NUMBER IN TABLE                          
         DC    AL4(TSRALNQ)            LENGTH OF ENTRY                          
         DC    AL1(0)                  DISP. TO KEY                             
         DC    AL3(TSRKLNQ)            KEY LENGTH                               
         DC    AL4(TSRAMAX)            MAX IN TABLE                             
         DC    AL1(TSRABKCT)           NUMBER OF BUCKETS                        
         DC    AL1(TSRABKT-TSRAD)      DISPLACEMENT TO FIRST BUCKET             
         DC    AL4(TSRASIZE)                                                    
*                                                                               
TSRAMAX  EQU   50000                                                            
         SPACE 1                                                                
*                                                                               
* BINTABLE 2 - SS ACCOUNT TABLE BUILT IN ACCOUNT LAST                           
*                                                                               
         DC    C'**PSSACCT*'                                                    
PSSATAB  DS    0C                                                               
         DC    AL4(0)                  NUMBER IN TABLE                          
         DC    AL4(PSSALNQ)            LENGTH OF ENTRY                          
         DC    AL1(0)                  DISP. TO KEY                             
         DC    AL3(PSSAKLNQ)           KEY LENGTH                               
         DC    AL4(PSSAMAX)            MAX IN TABLE                             
         DC    AL1(PSSABKCT)           NUMBER OF BUCKETS                        
         DC    AL1(PSSABKT-PSSAD)      DISPLACEMENT TO FIRST BUCKET             
         DC    AL4(PSSASIZE)                                                    
*                                                                               
         SPACE 1                                                                
TSRASIZE EQU   (TSRAMAX*TSRALNQ)                                                
LENBUFF1 EQU   TSRASIZE                                                         
LENBUFF  EQU   LENBUFF1+LENBUFF2                                                
*                                                                               
* BINTABLE 3 - RECEIVABLES PASSIVE POINTERS TABLE                               
*                                                                               
         DC    C'**SRPPTAB*'                                                    
SRPPTAB  DS    0C                                                               
         DC    AL4(0)                  NUMBER IN TABLE                          
         DC    AL4(L'TRNKEY-1+8)       LENGTH OF ENTRY                          
         DC    AL1(0)                  DISP. TO KEY                             
         DC    AL3(L'TRNKEY-1)         KEY LENGTH                               
         DC    AL4(SRPPMAX)            MAX IN TABLE                             
         DC    AL1(1)                  NUMBER OF BUCKETS                        
         DC    AL1(L'TRNKEY-1)         DISPLACEMENT TO FIRST BUCKET             
         DC    AL4(0)                                                           
*                                                                               
SRPPMAX  EQU   50000                                                            
*                                                                               
*---------------------------------------------------------------------*         
*                                  TABLE OF LEDGER AND SYSTEM         *         
*---------------------------------------------------------------------*         
LDSYTAB  DC    C'SS'               LEDGER S - SYS SPOT (BUT MAYBE NET)          
         DC    C'TS'               LEDGER T - SYS SPOT                          
         DC    C'PP'               LEDGER P - SYS PRINT                         
         DC    C'QP'               LEDGER Q - SYS PRINT                         
         DC    C'UN'               LEDGER U - SYS NET                           
         DC    X'FF'                                                            
*---------------------------------------------------------------------*         
*                                  TABLE OF NET SUB-MEDIA'S           *         
*---------------------------------------------------------------------*         
SMEDTAB  DC    C'NCDSOHV'                                                       
         DC    X'FF'                                                            
*---------------------------------------------------------------------*         
*                                  TABLE OF PRINT MEDIAS                        
*---------------------------------------------------------------------*         
PSYSTAB  DC    C'I'                 INTERACTIVE                                 
         DC    C'L'                 SOCIAL                                      
         DC    C'B'                 MOBILE                                      
         DC    C'V'                 NATIONAL VIDEO                              
         DC    C'W'                 LOCAL VIDEO                                 
         DC    C'D'                 DIGITAL AUDIO                               
         DC    X'FF'                                                            
*---------------------------------------------------------------------*         
*                                  INSERTION DATE TABLE               *         
*---------------------------------------------------------------------*         
INSTAB   DS    0H                                                               
         DC    C'1M'                                                            
         DC    C'M',X'02'          MEDIA MAGAZINE                               
         DC    C'N',X'00'          MEDIA NETWORK                                
         DC    C'T',X'01'          MEDIA TRADE                                  
         DC    C'I',X'01'          MEDIA INTERACTIVE                            
         DC    C'S',X'01'          MEDIA SUPPLEMENTAL                           
         DC    C'O',X'01'          MEDIA OUTDOOR                                
*                                                                               
         DC    C'B$'               IMDB#2669161                                 
         DC    C'M',X'02'          MEDIA MAGAZINE                               
         DC    C'N',X'01'          MEDIA NETWORK                                
         DC    C'T',X'02'          MEDIA TRADE                                  
         DC    C'I',X'01'          MEDIA INTERACTIVE                            
         DC    C'S',X'01'          MEDIA SUPPLEMENTAL                           
         DC    C'O',X'01'          MEDIA OUTDOOR                                
*                                                                               
         DC    C'WI'                                                            
         DC    C'M',X'00'          MEDIA MAGAZINE                               
         DC    C'N',X'00'          MEDIA NETWORK                                
         DC    C'T',X'00'          MEDIA TRADE                                  
         DC    C'I',X'00'          MEDIA INTERACTIVE                            
         DC    C'S',X'00'          MEDIA SUPPLEMENTAL                           
         DC    C'O',X'01'          MEDIA OUTDOOR                                
*                                                                               
         DC    C'NE'                                                            
         DC    C'M',X'01'          MEDIA MAGAZINE                               
         DC    C'N',X'00'          MEDIA NETWORK                                
         DC    C'T',X'01'          MEDIA TRADE                                  
         DC    C'I',X'00'          MEDIA INTERACTIVE                            
         DC    C'S',X'00'          MEDIA SUPPLEMENTAL                           
         DC    C'O',X'00'          MEDIA OUTDOOR                                
*                                                                               
         DC    C'D9'               CALL LOG CL#0155232N                         
         DC    C'M',X'01'          MEDIA MAGAZINE                               
         DC    C'N',X'00'          MEDIA NETWORK                                
         DC    C'T',X'01'          MEDIA TRADE                                  
         DC    C'I',X'00'          MEDIA INTERACTIVE                            
         DC    C'S',X'00'          MEDIA SUPPLEMENTAL                           
         DC    C'O',X'00'          MEDIA OUTDOOR                                
*                                                                               
         DC    C'FM'               CALL LOG CL#0174472N                         
         DC    C'M',X'01'          MEDIA MAGAZINE                               
         DC    C'N',X'00'          MEDIA NETWORK                                
         DC    C'T',X'00'          MEDIA TRADE                                  
         DC    C'I',X'00'          MEDIA INTERACTIVE                            
         DC    C'S',X'00'          MEDIA SUPPLEMENTAL                           
         DC    C'O',X'00'          MEDIA OUTDOOR                                
*                                                                               
         DC    C'H7'               CALL LOG CL#0293502N                         
         DC    C'M',X'01'          MEDIA MAGAZINE                               
         DC    C'N',X'00'          MEDIA NETWORK                                
         DC    C'T',X'01'          MEDIA TRADE                                  
         DC    C'I',X'00'          MEDIA INTERACTIVE                            
         DC    C'S',X'00'          MEDIA SUPPLEMENTAL                           
         DC    C'O',X'00'          MEDIA OUTDOOR                                
*                                                                               
         DC    C'M2'               CALL LOG CL#0293502N                         
         DC    C'M',X'01'          MEDIA MAGAZINE                               
         DC    C'N',X'00'          MEDIA NETWORK                                
         DC    C'T',X'01'          MEDIA TRADE                                  
         DC    C'I',X'00'          MEDIA INTERACTIVE                            
         DC    C'S',X'00'          MEDIA SUPPLEMENTAL                           
         DC    C'O',X'00'          MEDIA OUTDOOR                                
*                                                                               
         DC    C'M$'               CALL LOG 0351367N                            
         DC    C'M',X'02'          MAGAZINE                                     
         DC    C'N',X'00'          NEWSPAPER                                    
         DC    C'O',X'01'          OUDTOOR                                      
         DC    C'I',X'00'          INTERACTIVE                                  
         DC    C'S',X'02'          ALT WEEKLY                                   
         DC    C'T',X'01'          TRADE                                        
*                                                                               
         DC    C'H3'               CALL LOG 0368140N                            
         DC    C'M',X'01'          MEDIA MAGAZINE                               
         DC    C'N',X'01'          MEDIA NETWORK                                
         DC    C'T',X'01'          MEDIA TRADE                                  
         DC    C'I',X'01'          MEDIA INTERACTIVE                            
         DC    C'S',X'01'          MEDIA SUPPLEMENTAL                           
         DC    C'O',X'01'          MEDIA OUTDOOR                                
*                                                                               
         DC    C'M4'               JIRA #DSSUP-1151                             
         DC    C'M',X'01'          MEDIA MAGAZINE                               
         DC    C'N',X'00'          MEDIA NETWORK                                
         DC    C'T',X'00'          MEDIA TRADE                                  
         DC    C'I',X'00'          MEDIA INTERACTIVE                            
         DC    C'S',X'00'          MEDIA SUPPLEMENTAL                           
         DC    C'O',X'00'          MEDIA OUTDOOR                                
*                                                                               
         DC    C'OU'               JIRA #DSFTK-46                               
         DC    C'M',X'01'          MEDIA MAGAZINE                               
         DC    C'N',X'00'          MEDIA NETWORK                                
         DC    C'T',X'00'          MEDIA TRADE                                  
         DC    C'I',X'00'          MEDIA INTERACTIVE                            
         DC    C'S',X'00'          MEDIA SUPPLEMENTAL                           
         DC    C'O',X'01'          MEDIA OUTDOOR                                
*                                                                               
         DC    X'FF'                                                            
*                                                                               
*                                                                               
*                                                                               
* P1 EXPECTED TO ADDRESS OFFICE POSITION BYTE                                   
* IO EXPECTED TO HAVE RECORD TRANSACTION                                        
OFFILT   NTR1  BASE=*,LABEL=*                                                   
         L     R4,0(R1)            A(OFFICE POSITION BYTE)                      
*                                                                               
         CLI   0(R4),X'F0'                                                      
         JH    EQXIT                                                            
*                                                                               
         XR    R3,R3               POINTER TO OFFICE                            
*                                                                               
         CLI   0(R4),C'T'          OFFICE ON TRANSACTION RECORD                 
         BNE   OFFILT10                                                         
         LA    R3,IO+TRNKOFF-TRNRECD POINT R3 TO TRANSACTION KEY                
         B     OFFILT90                                                         
*                                                                               
OFFILT10 DS    0H                                                               
         LLC   RF,0(R4)                                                         
         N     RF,=F'15'           X'0F', ISOLATE DISPLACEMENT (4 LOBS)         
         LA    R3,IO+TRNKACT-TRNRECD(RF)                                        
         B     OFFILT90                                                         
*                                                                               
OFFILT90 DS    0H                                                               
         MVI   BSP2_4,0            FIND A RECORD                                
         SAM31                                                                  
         GOTO1 BINSRC31,BSPARS2,(R3)                                            
         SAM24                                                                  
         TM    BSP2_1,X'80'        TEST IF RECORD NOT FOUND                     
         JO    NEQXIT                                                           
         J     EQXIT                                                            
         LTORG                                                                  
*                                                                               
*                                                                               
*                                                                               
*                                                                               
* ON EXIT SVTRNAMT WILL HAVE TOTAL BILLED FOR THIS C/P/E/MOS,                   
* FOR ALL VENDORS.  AMOUNTS COMING FROM COST OF MEDIA                           
ADDVEND  NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         USING TSRAD,R2                                                         
         LA    R2,TSRAWRK                                                       
         USING BUFFERD,R3                                                       
         L     R3,AVENDBUF                                                      
*                                                                               
* IF STATION = YCKD, THEN THIS IS MANUAL BILLING                                
         SAM31                                                                  
         CLC   =C'YCKD',0(R3)                                                   
         SAM24                                                                  
         BE    ADDV05                                                           
*                                                                               
* CHECK IF ANYTHING RETURNED IN VENDOR BUFFER                                   
         SAM31                                                                  
         OC    0(BUFFERLN,R3),0(R3)                                             
         SAM24                                                                  
         BNZ   ADDV06                                                           
*                                                                               
* VENDOR BUFFER EMPTY: PRESUME MANUALL BILLING                                  
* DOLLARS COME FROM TRANSACTION, ALREADY IN SVTRNAMT - DON'T CLEAR IT!          
ADDV05   DS    0H                                                               
         SAM31                                                                  
         MVC   BUFFVNDR,SPACES                                                  
         MVC   BUFFVNDR(6),=C'MANUAL'                                           
         MVC   BUFFNAME,SPACES                                                  
         MVC   BUFFNAME(6),=C'MANUAL'                                           
         ZAP   BUFFNET,SVTRNAMT                                                 
         SAM24                                                                  
*                                                                               
ADDV06   DS    0H                                                               
         ZAP   SVTRNAMT,=P'0'                                                   
*                                                                               
ADDV10   SAM31                                                                  
         CLI   0(R3),X'00'         END OF VENDOR BUFFER?                        
         SAM24                                                                  
         JE    EQXIT                                                            
*                                                                               
         BRAS  RE,ZAPSRBUK         ZAP ALL BUCKETS                              
*                                                                               
         SAM31                                                                  
         MVC   TSRKVCOD,BUFFVNDR                                                
         MVC   TSRKVNAM,BUFFNAME                                                
         ZAP   TSRADR,BUFFNET                                                   
         SAM24                                                                  
* TEMP DEBUG                                                                    
         CLI   TSRKSYS,C'N'                                                     
         BNE   *+8                                                              
         MVI   TSRKVCOD+4,C'N'                                                  
* END TEMP DEBUG                                                                
*                                                                               
         OC    TSRKVCOD,SPACES                                                  
         AP    SVTRNAMT,TSRADR                                                  
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSAADD,1                                         
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         AHI   R3,BUFFERLN                                                      
         J     ADDV10                                                           
*                                                                               
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
* ADJUST VENDOR TOTALS FOR CANADA SINCE THEY REQUIRE TAXES AND INCOME           
*    AS PART OF THE TOTAL                                                       
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
*                                                                               
ADJVEND  NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         CLI   CTRY,CTRYCAN            IS THIS CANADA?                          
         JNE   ADJVXIT                                                          
*                                                                               
         CP    SVTOTNET,=P'0'          IF THERE IS NO NET AMOUNT                
         BE    ADJVXIT                 LEAVE                                    
         ZAP   SVREMAIN,SVTAXINC                                                
         USING BUFFERD,R3                                                       
         L     R3,AVENDBUF                                                      
*                                                                               
ADJV40   SAM31                                                                  
         OC    0(BUFFERLN,R3),0(R3)    NOTHING IN BUFFER                        
         JZ    ADJVXIT                                                          
         LR    RE,R3                                                            
         AHI   RE,BUFFERLN                                                      
         OC    0(BUFFERLN,RE),0(RE)                                             
         SAM24                                                                  
         JZ    ADJV50                                                           
         SAM31                                                                  
         ZAP   WORK(16),BUFFNET                                                 
         SAM24                                                                  
         MP    WORK(16),=P'1000000'                                             
         DP    WORK(16),SVTOTNET                                                
         SRP   WORK(8),64-1,5                                                   
         ZAP   SVPCTNET,WORK(8)                                                 
*                                                                               
         ZAP   WORK(16),SVPCTNET                                                
         MP    WORK(16),SVTAXINC                                                
         SRP   WORK(16),64-5,5                                                  
         ZAP   SVPCTTNI,WORK(16)                                                
         SP    SVREMAIN,SVPCTTNI                                                
         J     ADJV90                                                           
*                                                                               
ADJV50   DS    0H                                                               
         ZAP   SVPCTTNI,SVREMAIN                                                
*                                                                               
ADJV90   DS    0H                                                               
         SAM31                                                                  
         AP    BUFFNET,SVPCTTNI        ADD TAX AND INCOME                       
         AHI   R3,BUFFERLN                                                      
         J     ADJV40                                                           
*                                                                               
ADJVXIT  J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
* IO EXPECTED TO HAVE TRANSACTION RECORD                                        
* SR BUFFER ENTRY WILL BE CREATED IN TSRAWRK                                    
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
PROCSR   NTR1  BASE=*,LABEL=*                                                   
         XC    SVCLNET,SVCLNET     CALCULATED NET                               
         MVI   SVBILTYP,X'00'      BILL TYPE (X-TYPE OR NOT)                    
         XC    SVSYS,SVSYS                                                      
         XC    SVMED,SVMED                                                      
         XC    SVTRNAMT,SVTRNAMT                                                
         ZAP   SVTAXINC,=P'0'                                                   
         ZAP   SVTOTNET,=P'0'                                                   
         NI    FLAG,X'FF'-FLGDR                                                 
*                                                                               
         LA    R4,IO               R4 = A(TRANSACTION)                          
         USING TRNRECD,R4                                                       
*                                                                               
         XC    TSRAWRK,TSRAWRK     INITIAZIZE WORK AREA                         
         USING TSRAD,R2            SR BUFFER ENTRY                              
         LA    R2,TSRAWRK                                                       
*                                                                               
         MVC   TSRKACT,TRNKULA     SR ACCOUNT                                   
         MVC   TSRKCAC,TRNKULC     SR CONTRA ACCOUNT                            
         MVC   TSRKBNO,TRNKREF     SR TRANSACTION NUMBER                        
         MVC   TSRKBDA,TRNKDATE    SR TRANSACTION DATE                          
*                                                                               
         NI    FLAG1,X'FF'-FLG1A6AQ                                             
*                                                                               
         LR    R5,R4               ELEMENT POINTER                              
         AH    R5,=Y(TRNRFST-TRNKEY)                                            
*                                                                               
PRSR10   CLI   0(R5),0             EOR?                                         
         BE    PRSR400                                                          
*                                                                               
         CLI   0(R5),TRNELQ        IS IT X'44' ELEMENT                          
         BE    PRSR90                                                           
         CLI   0(R5),TRSELQ        IS IT X'60' ELEMENT                          
         BE    PRSR100                                                          
         CLI   0(R5),MDTELQ        IS IT X'1A' ELEMENT                          
         BE    PRSR110                                                          
         CLI   0(R5),MDPELQ        IS IT X'6A' ELEMENT                          
         BE    PRSR110                                                          
         CLI   0(R5),MBIELQ        IS IT X'F3' ELEMENT                          
         BE    PRSR130                                                          
         CLI   0(R5),FFTELQ        IS IT X'DB' ELEMENT                          
         BE    PRSR136                                                          
         CLI   0(R5),RALELQ        IS IT X'D9' ELEMENT                          
         BE    PRSR139                                                          
         CLI   0(R5),DUEELQ        IS IT X'61' ELEMENT                          
         BE    PRSR139D                                                         
         CLI   0(R5),SCIELQ        IS IT X'50' ELEMENT                          
         BE    PRSR150                                                          
*                                                                               
* NEXT ELEMENT IN TRANSACTION RECORD                                            
*                                                                               
PRSR20   LLC   R1,1(R5)            GET LENGTH OF ELEMENT                        
         AR    R5,R1                                                            
         B     PRSR10                                                           
*                                                                               
* PROCESS INDIVIDUAL ELEMENTS                                                   
*                                                                               
         USING TRNELD,R5           X'44', TRANSACTION ELEMENT                   
PRSR90   DS    0H                                                               
         TM    FLAG1,FLG1A6AQ                                                   
         BO    PRSR20                                                           
*                                                                               
         ZAP   SVTRNAMT,TRNAMNT                                                 
         AP    SVTOTNET,TRNAMNT                                                 
*                                                                               
         TM    TRNSTAT,TRNSDR      IS IT A DEBIT                                
         BNO   *+8                                                              
         OI    FLAG,FLGDR                                                       
*                                                                               
         B     PRSR20                                                           
*                                                                               
         USING TRSELD,R5           X'60', TRANSACTION STATUS ELEMENT            
PRSR100  DS    0H                                                               
         TM    FLAG1,FLG1A6AQ                                                   
         BO    PRSR20                                                           
*                                                                               
         TM    TRSSTAT,TRSSOFFS    IS IT AN OFFSET                              
         JO    PRSRNQX             SKIP OFFSETS                                 
         B     PRSR20                                                           
         DROP  R5                                                               
*                                                                               
         USING MDTELD,R5           X'1A'/'6A', MEDIA TRANSFER ELEMENT           
PRSR110  DS    0H                                                               
         TM    FLAG1,FLG1A6AQ                                                   
         BO    PRSR20                                                           
*                                                                               
         CLI   MDTSYS,MDPSPROD                                                  
         JE    PRSRNQX             SKIP THIS TRANSACTION                        
*                                                                               
         MVC   SVSYS,MDTSYS        SYSTEM                                       
         MVC   SVMED,MDTMED        MEDIA                                        
         GOTOR GETSYSTM            GET SYSTEM BASED ON MEDIA                    
         MVC   TSRKSYS,SVSYS                                                    
         MVC   TSRKMED,MDTMED                                                   
*                                                                               
         MVC   TSRKCLI,MDTCLI      MOVE IN CLIENT CODE TO WORK                  
         MVC   TSRKPRO,MDTPRD      MOVE IN PRODUCT TO WORK                      
         MVC   TSRKEST,MDTEST      MOVE IN ESTIMATE CODE                        
*                                                                               
         CLI   MDTMOS+1,X'13'      IS THIS THE 13TH MONTH                       
         JE    PRSRNQX             SKIP THIS TRANSACTION                        
*                                                                               
         MVC   SVMOS(L'MDTMOS),MDTMOS                                           
         GOTOR GTINSRTN            GET INSERTION DATE                           
         MVC   TSRKMOS,SVMOS                                                    
*                                                                               
         CLI   0(R5),MDPELQ        X'6A', PACKED DATA ELEMENT?                  
         BE    PRSR120                                                          
*                                                                               
         ICM   R1,15,MDTCOM        GET NET/COMMISSION AMOUNT                    
         CVD   R1,DUB                                                           
         SP    SVTRNAMT,DUB        SUBTRACT NET FROM GROSS                      
         OI    FLAG1,FLG1A6AQ                                                   
         B     PRSR20                                                           
*                                                                               
         USING MDPELD,R5           X'6A' MED TRANSFER(PACKED) ELEMENT           
PRSR120  SP    SVTRNAMT,MDPCOM                                                  
         OI    FLAG1,FLG1A6AQ                                                   
         B     PRSR20                                                           
*                                                                               
         USING MBIELD,R5           PROCESS X'F3', NEW BILLING XFER              
PRSR130  DS    0H                                                               
         TM    FLAG1,FLG1A6AQ                                                   
         BO    PRSR20                                                           
*                                                                               
         MVC   SVSYS,MBISYS        SYSTEM                                       
         MVC   SVMED,MBIMED        MEDIA                                        
         GOTOR GETSYSTM            GET SYSTEM BASED ON MEDIA                    
         MVC   TSRKSYS,SVSYS       PUT SYSTEM IN TABLE                          
*                                                                               
         MVC   TSRKMED,SVMED       PUT MEDIA CODE IN TABLE                      
         MVC   TSRKCLI,MBICLI      MOVE IN CLIENT CODE TO WORK                  
         MVC   TSRKPRO,MBIPRD      MOVE IN PRODUCT TO WORK                      
         MVC   TSRKEST(L'MBIEST),MBIEST MOVE IN ESTIMATE                        
*                                                                               
         CLI   MBIMOS+1,X'13'      IS THIS THE 13TH MONTH                       
         JE    PRSRNQX             SKIP THIS TRANSACTION                        
         MVC   SVMOS(L'MBIMOS),MBIMOS                                           
         GOTOR GTINSRTN            GET INSERTION DATE                           
         MVC   TSRKMOS,SVMOS       MOVE IN MONTH OF SERVICE                     
         B     PRSR20                                                           
*                                                                               
         USING FFTELD,R5           PROCESS X'DB'                                
PRSR136  DS    0H                                                               
         TM    FLAG1,FLG1A6AQ                                                   
         BO    PRSR20                                                           
         CLI   FFTTYPE,FFTTCNET    CALCULATED NET                               
         BNE   *+14                                                             
         MVC   SVCLNET,FFTDATA                                                  
         B     PRSR20                                                           
*                                                                               
         CLI   FFTTYPE,FFTTMXTY    MX ELEMENT AND BILL TYPES                    
         BNE   PRSR20                                                           
*                                                                               
         LA    RE,XBILTYPS                                                      
PRSR137  CLI   0(RE),X'FF'                                                      
         BE    PRSR20                                                           
         LLC   RF,0(RE)            FFTELD ELEM DATA LENGTH                      
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   1(0,RE),FFTMXTB     SAME BILL TYPE AS IN FFTELD?                 
         BNE   PRSR138             NO - CHECK NEXT TABLE ENTRY                  
         MVI   SVBILTYP,C'X'       YES - INDICATE X-TYPE BILL                   
         B     PRSR20                                                           
*                                                                               
PRSR138  DS    0H                  GO TO NEXT LINE IN XBILTYPS                  
         LLC   RF,0(RE)                                                         
         LA    RE,1(RE,RF)                                                      
         B     PRSR137                                                          
*                                                                               
XBILTYPS DC    AL1(4),C'REGX'                                                   
         DC    AL1(4),C'ADJX'                                                   
         DC    AL1(5),C'AORXM'                                                  
         DC    AL1(5),C'AORXA'                                                  
         DC    X'FF'                                                            
*                                                                               
         USING RALELD,R5                                                        
PRSR139  DS    0H                  PROCESS X'D9', RCV ALLOC ELEMENT             
         CLI   RALTYPE,RALTALC                                                  
         BNE   PRSR20                                                           
         MVC   TSRKDPDT,RALADEP                                                 
         B     PRSR20                                                           
*                                                                               
         USING DUEELD,R5                                                        
PRSR139D DS    0H                 PROCESS X'61', DUE DATE ELEMENT               
         GOTO1 DATCON,DMCB,(2,DUEDATE),(1,TSRKDUDT)                             
         B     PRSR20                                                           
*                                                                               
         USING SCIELD,R5                                                        
PRSR150  DS    0H                  PROCESS X'50', TAX AND INCOME                
         CLI   SCITYPE,SCITINCA    INCOME                                       
         BE    PRSR153                                                          
         CLI   SCITYPE,SCITTTAX    TAX                                          
         BE    PRSR153                                                          
         CLI   SCITYPE,SCITTQST    CANADIAN TAX                                 
         BNE   PRSR20                                                           
PRSR153  AP    SVTAXINC,SCIAMNT                                                 
         SP    SVTOTNET,SCIAMNT                                                 
         B     PRSR20                                                           
*                                                                               
PRSR400  DS    0H                                                               
         CLI   SVBILTYP,C'X'       X-TYPE BILL?                                 
         BNE   PRSR442                                                          
         ICM   RF,15,SVCLNET       IS THERE CALCULATED NET?                     
         BZ    PRSR442                                                          
         CVD   RF,DUB                                                           
*                                                                               
* MAKE SURE CALC NET IS SAME SIGN AS TRANSACTION AMOUNT                         
*                                                                               
         MVC   BYTE,SVTRNAMT+L'SVTRNAMT-1                                       
         NI    BYTE,X'0F'                                                       
         NI    DUB+L'DUB-1,X'F0'                                                
         OC    DUB+L'DUB-1(1),BYTE                                              
*                                                                               
         SP    SVTRNAMT,DUB        SUBTRACT NET FROM GROSS                      
*                                                                               
PRSR442  DS    0H                                                               
         BRAS  RE,ZAPSRBUK         ZAP ALL BUCKETS                              
*                                                                               
*        TM    FLAG,FLGDR                                                       
*        BZ    *+14                                                             
*        ZAP   TSRAWRK+(TSRADR-TSRAD)(TSRABKLN),SVTRNAMT                        
*        B     *+10                                                             
*        ZAP   TSRAWRK+(TSRACR-TSRAD)(TSRABKLN),SVTRNAMT                        
*                                                                               
*        NI    FLAG,X'FF'-FLGDR                                                 
         J     EQXIT                                                            
*                                                                               
PRSRNQX  NI    FLAG,X'FF'-FLGDR                                                 
         J     NEQXIT                                                           
*                                                                               
         LTORG                                                                  
         DROP  R5,R4,R2                                                         
*                                                                               
*                                                                               
*                                                                               
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
* PROCESS SR BUFFER ENTRIES FOR THE CURRENT TRANSACTION BLOCK                   
* CALCULATE PERCENT CREDIT                                                      
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
CALCPCNT NTR1  BASE=*,LABEL=*                                                   
         XC    TSRAWRK,TSRAWRK                                                  
         GOTO1 ATSAR,DMCB,(RC),TSARDH,1     GET 1ST RECD IN TSRAWRK             
         TM    BCTSERRS,TSEEOF              IS THIS END OF FILE                 
         JO    NEQXIT                                                           
*                                                                               
         NI    FLAG2,X'FF'-FL20BILQ                                             
*                                                                               
         USING TSRAD,R2                                                         
         LA    R2,TSRAWRK                                                       
*                                                                               
CALCP190 DS    0H                                                               
         OC    TSRKSYS,TSRKSYS    BLANK SYSTEM = BILL TOTALS RECORD             
         BNZ   CALCP200                                                         
*                                                                               
* HAVE BILL TOTALS HERE                                                         
*                                                                               
         MVC   TSRAWRK2,TSRAWRK    SAVE BILL TOTALS                             
         ZAP   SVPCNT,=P'0'                CALC PERCENT                         
*                                                                               
* PER 2/22/17 CONVERSATION WITH JEANNE, JIM, YANA:                              
* IF $0 BILL, SET CASH RECEIVED AT 100%                                         
* POPULATE CR AMOUNT EQUAL TO DR AMOUNT ON INDIVIDUAL VENDORS                   
         CP    TSRADR,=P'0'                IS BILLED AMT ZERO?                  
         BNE   *+18                                                             
         OI    FLAG2,FL20BILQ                                                   
         ZAP   SVPCNT,=P'1000000'  DON'T APPLY MORE THAN 100%                   
         B     CALCP220                    READ NEXT                            
*                                                                               
         CP    TSRACR,=P'0'                IS APPLIED AMT ZERO?                 
         BE    CALCP220                    READ NEXT                            
*                                                                               
         ZAP   PL16,TSRACR      GET CREDIT AMOUNT                               
         MP    PL16,=P'1000000' MULTIPLY BY 100                                 
         DP    PL16,TSRADR      GET PERCENT CR/DR X 100                         
         ZAP   SVPCNT,PL16(L'PL16-L'TSRADR)                                     
*                                                                               
         CP    SVPCNT,=P'1000000'                                               
         BNH   *+10                                                             
         ZAP   SVPCNT,=P'1000000'  DON'T APPLY MORE THAN 100%                   
*                                                                               
         BRAS  RE,ZAPSRBUK         ZAP ALL BUCKETS                              
*                                                                               
         ZAP   TSRAPCNT,SVPCNT                                                  
         B     CALCP210            PUT PERCENT AND GET NEXT                     
*                                                                               
* HAVE VENDOR-LEVEL DETAILS HERE                                                
*                                                                               
CALCP200 DS    0H                                                               
         TM    FLAG2,FL20BILQ      BILL TOTALS AMT BILLED = 0?                  
         BZ    CALCP205                                                         
*                                                                               
* PER 2/22/17 CONVERSATION WITH JEANNE, JIM, YANA:                              
* IF $0 BILL, SET CASH RECEIVED AT 100%                                         
* POPULATE CR AMOUNT EQUAL TO DR AMOUNT ON INDIVIDUAL VENDORS                   
* DR AMOUNTS NO LONGER NEGATED                                                  
         ZAP   TSRACR,TSRADR                                                    
         ZAP   TSRAPCNT,=P'1000000'                                             
*                                                                               
*                                                                               
* NEGATE AMOUNT BILLED, SO IT TOTALS UP TO ZERO                                 
*        ZAP   PL8,TSRADR                                                       
*        BRAS  RE,ZAPSRBUK                                                      
*        SP    TSRADR,PL8                                                       
*        B     CALCP210            UPDATE RECORD                                
*                                                                               
CALCP205 DS    0H                                                               
         CP    SVPCNT,=P'0'                                                     
         BZ    CALCP220                                                         
         CP    TSRADR,=P'0'                                                     
         BZ    CALCP220            SKIP CREDIT ONLY ENTRY                       
*                                                                               
         ZAP   PL16,TSRADR         GET DEBIT                                    
         MP    PL16,SVPCNT                                                      
         SRP   PL16,3,0                                                         
         DP    PL16,=PL4'1000000'                                               
         SRP   PL16(L'PL16-4),64-3,5 % ROUNDED TO 2 DP                          
         BRAS  RE,ZAPSRBUK         ZAP ALL BUCKETS                              
         ZAP   TSRACR,PL16(L'PL16-4) SAVE OFF CALCULTED CREDIT                  
         ZAP   TSRAPCNT,SVPCNT                                                  
         MVC   TSRKDPDT,TSRAWRK2+TSRKDPDT-TSRAD DEPOSIT DATE                    
*                                                                               
CALCP210 GOTO1 ATSAR,DMCB,(RC),TSAADD,1   PUT/WRITE RECD BACK                   
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 ATSAR,DMCB,(RC),TSARDH,1   HIGH FOR RECD JUST WRITTEN            
*                                                                               
CALCP220 GOTO1 ATSAR,DMCB,(RC),TSANXT,1   GET RECORD IN TSRAWRK                 
         TM    BCTSERRS,TSEEOF     IS THIS END OF FILE                          
         BNO   CALCP190                                                         
         J     EQXIT                                                            
         LTORG                                                                  
*                                                                               
*                                                                               
*                                                                               
PROCSS   NTR1  BASE=*,LABEL=*                                                   
         J     EQXIT                                                            
         LTORG                                                                  
*                                                                               
*                                                                               
*                                                                               
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *           
* GET VENDOR CODE FROM THE TRANSACTION RECORD'S PAYABLE ACCOUNT CODE            
* IO EXPECTED TO HAVE THE TRANSACTION RECORD                                    
* ON EXIT THE VENDOR CODE WILL BE IN WORK                                       
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *           
GETVEN   NTR1  BASE=*,LABEL=*                                                   
         XC    WORK,WORK                                                        
         USING TRNRECD,R2                                                       
         LA    R2,IO                                                            
*                                                                               
         CLI   SVSYS,C'P'          PRINT?                                       
         BE    GETV200                                                          
*                                                                               
         CLI   TRNRSTYP,34         TYPE 34 (X'22') TRANSACTION = REP            
         BNE   *+14                                                             
         MVC   WORK(5),TRNKCACT+1  COPY STATION NAME FROM CONTRA ACCT           
         B     GETVX                                                            
*                                                                               
         MVC   WORK(5),TRNKACT+1   COPY STATION NAME                            
         B     GETVX                                                            
*                                                                               
GETV200  DS    0H                                                               
         CLI   TRNRSTYP,50         TYPE 50 (X'32') TRANSACTION = REP            
         BNE   *+14                                                             
         MVC   WORK(10),TRNKULC                                                 
         B     GETVX                                                            
*                                                                               
         CLI   TRNRSTYP,13         TYPE 13 (X'0D') TRANSACTION                  
         BNE   GETV220                                                          
*                                                                               
         CLI   TRNKCULC,C' '       CHECK 1ST CHARACTER OF CONTRA ACC            
         BNH   GETV220             BLANK - COPY PUB FROM ACCOUNT                
         MVC   WORK(10),TRNKULC    NONBLANK - THIS IS A REP                     
         B     GETVX                                                            
*                                                                               
GETV220  DS    0H                                                               
         MVC   WORK(10),TRNKACT+1  COPY PUB NUMBER                              
         B     GETVX                                                            
*                                                                               
GETVX    OC    WORK,SPACES                                                      
         J     EQXIT                                                            
         LTORG                                                                  
*                                                                               
*                                                                               
*                                                                               
**********************************************************************          
* POPULATE PAYABLES BUFFER WITH PAYEE NAMES                          *          
**********************************************************************          
GETPNAMS NTR1  BASE=*,LABEL=*                                                   
         XC    PSSAWRK,PSSAWRK                                                  
         LA    R2,PSSAWRK                                                       
         USING PSSAD,R2                                                         
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSARDH,2   GET RECORD IN PSSAWRK                 
         B     GETPN11                                                          
*                                                                               
GETPN10  GOTO1 ATSAR,DMCB,(RC),TSANXT,2   GET RECORD IN PSSAWRK                 
GETPN11  TM    BCTSERRS,TSEEOF     IS THIS END OF FILE                          
         BO    GETPNX                                                           
*                                                                               
         CLI   PSSKRTYP,X'05'      SS DETAIL?                                   
         BNE   GETPN10                                                          
         OC    PSSKSTN,PSSKSTN     VENDOR-LEVEL TOTAL?                          
         BZ    GETPN10             YES, SKIP IT                                 
*                                                                               
         USING ACTKEY,R4                                                        
         LA    R4,SVKEY                                                         
*                                                                               
         MVC   ACTKEY,SPACES                                                    
         MVC   ACTKCPY,AUTCPY      COMPANY CODE                                 
         MVC   ACTKULA,PSSKSTN                                                  
*                                                                               
         GOTO1 ADMRDHI,DMCB,(RC)                                                
         CLC   SVKEY,IOKEY                                                      
         BNE   GETPN10                                                          
*                                                                               
         GOTO1 ADMGET,DMCB,(RC),0 GET RECORD                                    
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R5,IO                                                            
         AH    R5,=Y(ACTRFST-ACTRECD)                                           
*                                                                               
GETPN20  CLI   0(R5),0                                                          
         BE    GETPN10                                                          
         CLI   0(R5),NAMELQ        X'20', GENERAL NAME ELEMENT                  
         BE    GETPN30                                                          
*                                                                               
         LLC   R1,1(R5)                                                         
         AR    R5,R1                                                            
         B     GETPN20                                                          
*                                                                               
         USING NAMELD,R5           X'20', GENERAL NAME ELEMENT                  
GETPN30  DS    0H                                                               
         LLC   RF,NAMLN                                                         
         AHI   RF,-3                                                            
         CHI   RF,23                                                            
         BNH   *+8                                                              
         LHI   RF,23                                                            
*                                                                               
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   PSSPNAM(0),NAMEREC                                               
         OC    PSSPNAM,SPACES                                                   
*                                                                               
         BRAS  RE,ZAPSSBUK         ZAP ALL BUCKETS                              
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2    ADD TO BUFFER # 2                    
         CLI   BCTSERRS,0                                                       
         JNE   *+2                                                              
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSARDH,2   GET RECORD IN PSSAWRK                 
         CLI   BCTSERRS,0                                                       
         BE    GETPN10                                                          
*                                                                               
         DC    H'0'                RECORD UPDATE FAILED                         
*                                                                               
GETPNX   J     EQXIT                                                            
         LTORG                                                                  
*                                                                               
*                                                                               
*                                                                               
**********************************************************************          
* DUMP PAYABLES BUFFER                                               *          
**********************************************************************          
DUMPSS   NTR1  BASE=*,LABEL=*                                                   
         XC    PSSAWRK,PSSAWRK                                                  
         LA    R2,PSSAWRK                                                       
         USING PSSAD,R2                                                         
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSARDH,2   GET RECORD IN PSSAWRK                 
         B     DUMPSS11                                                         
*                                                                               
DUMPSS10 GOTO1 ATSAR,DMCB,(RC),TSANXT,2   GET RECORD IN PSSAWRK                 
DUMPSS11 TM    BCTSERRS,TSEEOF     IS THIS END OF FILE                          
         BO    DUMPSSX                                                          
*                                                                               
* PRINT A BLANK LINE                                                            
         GOTO1 =A(TRACE),DMCB,0,0,=C' '                                         
*                                                                               
* RECORD IN PRINTABLE FORMAT, UP TO THE BUCKETS                                 
         LHI   R3,PSSABKT-PSSAD    RECORD UP TO THE BUCKETS                     
         GOTO1 =A(TRACE),DMCB,(X'02',(R3)),PSSAKEY,0                            
*                                                                               
* RECORD KEY IN HEX, UP TO PSSKSTAT                                             
         LHI   R3,PSSKSTAT-PSSAKEY                                              
         GOTO1 =A(TRACE),DMCB,(X'01',(R3)),PSSAKEY,0                            
*                                                                               
* RECORD KEY IN HEX, PSSKSTAT TO END OF KEY                                     
         LHI   R3,PSSFLAG-PSSKSTAT                                              
         GOTO1 =A(TRACE),DMCB,(X'01',(R3)),PSSKSTAT,0                           
*                                                                               
* NON-KEY RECORD DATA IN HEX                                                    
         LHI   R3,PSSABKT-PSSFLAG                                               
         GOTO1 =A(TRACE),DMCB,(X'01',(R3)),PSSFLAG,0                            
*                                                                               
* BUCKETS IN HEX                                                                
         LHI   R3,PSSABKCT*PSSABKLN                                             
         GOTO1 =A(TRACE),DMCB,(X'01',(R3)),PSSABKT,0                            
*                                                                               
         B     DUMPSS10                                                         
*                                                                               
DUMPSSX  J     EQXIT                                                            
         LTORG                                                                  
*                                                                               
*                                                                               
*                                                                               
**********************************************************************          
* READ THE PAYABLES SR DETAILS, AND ADD X'06'                        *          
* RECORDS, WITH SR DETAILS HIGH IN THE KEY                           *          
**********************************************************************          
RESORTSS NTR1  BASE=*,LABEL=*                                                   
         XC    PSSAWRK,PSSAWRK                                                  
         XC    PSSAWRK3,PSSAWRK3   PREVIOUS KEY                                 
         XC    PSSAWRK5,PSSAWRK5   LAST PAYABLE                                 
         XC    SVPSSWRK,SVPSSWRK                                                
* SET FLAG, SO THE VERY FIRST PAYABLE DOESN'T GENERATE BAD 06 ENTRY             
         OI    FLAG1,FLG106Q                                                    
*                                                                               
RESORT05 USING PSSAD,PSSAWRK       X'05' RECORD                                 
RESORT06 USING PSSAD,PSSAWRK0      X'06' RECORD                                 
*                                                                               
         GOTO1 ATSAR,DMCB,(RC),TSARDH,2   GET RECORD IN PSSAWRK                 
         B     RESORT11                                                         
*                                                                               
RESORT10 GOTO1 ATSAR,DMCB,(RC),TSANXT,2   GET RECORD IN PSSAWRK                 
RESORT11 TM    BCTSERRS,TSEEOF     IS THIS END OF FILE                          
         BO    RESORT90                                                         
*                                                                               
         CLI   RESORT05.PSSKRTYP,X'05'                                          
         BNE   RESORT10                                                         
*                                                                               
* SKIP ESTIMATE, MOS AND VENDOR TOTALS                                          
         OC    RESORT05.PSSKINV(PSSKINV-PSSKSTN),RESORT05.PSSKINV               
         BZ    RESORT10                                                         
*                                                                               
* IF PAYABLE KEY CHANGED, CHECK IF WE ADDED 06 ENTRY                            
* FOR PREVIOUS PAYABLE                                                          
         CLC   RESORT05.PSSAD(PSSSRAC-PSSAD),PSSAWRK3                           
         BNE   RESORT15                                                         
         CLC   RESORT05.PSSKDA,PSSAWRK3+PSSKDA-PSSAD                            
         BE    RESORT18                                                         
*                                                                               
* KEY HAS CHANGED: NEW PAYABLE HERE                                             
RESORT15 DS    0H                                                               
         MVC   PSSAWRK3,RESORT05.PSSAD SAVE KEY                                 
*                                                                               
         TM    FLAG1,FLG106Q       ADDED 06 ENTRY FOR PREV PAYABLE?             
         BO    RESORT18            YES                                          
*                                                                               
* WE DID NOT ADD 06 ENTRY FOR PREVIOUS PAYABLE,                                 
* BECAUSE THERE WERE NO MATCHING RECEIVABLES FOR IT.                            
* WE'LL NEED TO ADD THE 06 ENTRY ANYWAY, BUILDING IT FROM                       
* LAST PAYABLE RECORD(RECEIVABLE FIELDS WILL BE EMPTY)                          
         MVC   RESORT05.PSSAD(PSSALNQ),PSSAWRK5 LAST PAYABLE                    
         B     RESORT20            GO TO CODE THAT ADDS 06 ENTRIES              
*                                                                               
* KEY HAS =NOT= CHANGED, WORKING ON SAME VENDOR INVOICE                         
RESORT18 DS    0H                                                               
         OC    RESORT05.PSSSRAC,RESORT05.PSSSRAC                                
         BNZ   RESORT20            SR DETAILS - PROCESS THEM                    
*                                                                               
* SR INFORMATION NOT FILLED IN: WE HAVE PAYABLE HERE                            
         MVC   SVFLAG,RESORT05.PSSFLAG SAVE STATUS BYTE                         
         MVC   PSSAWRK5,RESORT05.PSSAD SAVE PAYABLE                             
         NI    FLAG1,X'FF'-FLG106Q RE-SET "06 ENTRY ADDED" FLAG                 
         B     RESORT10            SKIP PAYABLES                                
*                                                                               
* HAVE SR DETAILS HERE                                                          
* ADD 06 ENTRY TO THE PAYABLES BUFFER                                           
RESORT20 DS    0H                                                               
* SAVE CURRENT X'05' KEY TO RESTORE READ SEQUENCE LATER                         
         MVC   SVPSSWRK,RESORT05.PSSAD                                          
*                                                                               
         OI    FLAG1,FLG106Q       INDICATE WE'VE ADDED 06 ENTRY                
*                                                                               
* BUILD X'06' IN PSSAWRK0, USING CURRENT X'05' AS A BASE                        
         MVC   PSSAWRK0,RESORT05.PSSAD                                          
*                                                                               
* RE-ARRANGE THE KEY FIELDS                                                     
         MVC   RESORT06.PS6KSYS,RESORT05.PSSKSYS                                
         MVC   RESORT06.PS6KMED,RESORT05.PSSKMED                                
         MVC   RESORT06.PS6KCLI,RESORT05.PSSKCLI                                
         MVC   RESORT06.PS6KPRO,RESORT05.PSSKPRO                                
         MVC   RESORT06.PS6KEST,RESORT05.PSSKEST                                
         MVI   RESORT06.PS6KRTYP,X'06'                                          
         MVC   RESORT06.PS6KMOS,RESORT05.PSSKMOS                                
         MVC   RESORT06.PS6SRAC,RESORT05.PSSSRAC                                
         MVC   RESORT06.PS6SRCAC,RESORT05.PSSSRCAC                              
         MVC   RESORT06.PS6SRBNO,RESORT05.PSSSRBNO                              
         MVC   RESORT06.PS6SRBDA,RESORT05.PSSSRBDA                              
         MVC   RESORT06.PS6KVEN,RESORT05.PSSKVEN                                
         MVC   RESORT06.PS6KSTN,RESORT05.PSSKSTN                                
         MVC   RESORT06.PS6KINV,RESORT05.PSSKINV                                
         MVC   RESORT06.PS6KOFF,RESORT05.PSSKOFF                                
         MVC   RESORT06.PSSFLAG,SVFLAG FLAG COMES FROM PAYABLE RECORD           
*        MVC   RESORT06.PSSFLAG,RESORT05.PSSFLAG                                
*                                                                               
* CALCULATE PERCENTAGE RECEIVED                                                 
         CP    RESORT06.PSSBILLB,=P'0'                                          
         BE    RESORT50                                                         
         CP    RESORT06.PSSRCVDB,=P'0'                                          
         BE    RESORT50                                                         
*                                                                               
         ZAP   PL16,RESORT06.PSSRCVDB AMOUNT RECEIVED                           
         SRP   PL16,4,0 TIMES 1000 (PERCENTAGE TO 2 DECIMAL PLACES)             
         DP    PL16,RESORT06.PSSBILLB AMOUNT BILLED                             
         ZAP   RESORT06.PSSRPCNT,PL16(8)                                        
*                                                                               
RESORT50 DS    0H                                                               
         MVC   PSSAWRK,PSSAWRK0    MOVE NEWLY BUILT X'06' TO PSSAWRK            
         GOTO1 ATSAR,DMCB,(RC),TSAADD,2    ADD TO BUFFER # 2                    
         CLI   BCTSERRS,0                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   PSSAWRK,SVPSSWRK           SAVED 05                              
         GOTO1 ATSAR,DMCB,(RC),TSARDH,2   RESTORE READ SEQUENCE                 
         CLI   BCTSERRS,0                                                       
         BE    RESORT10                                                         
*                                                                               
         DC    H'0'                                                             
*                                                                               
RESORT90 DS    0H                                                               
         TM    FLAG1,FLG106Q       ADDED 06 ENTRY FOR LAST PAYABLE?             
         BO    RESORTX             YES                                          
*                                                                               
         MVC   RESORT05.PSSAD(PSSALNQ),PSSAWRK5 LAST PAYABLE KEY                
*        MVI   SVFLAG,X'00'                                                     
         B     RESORT20            GO TO CODE THAT ADDS 06 ENTRIES              
*                                                                               
RESORTX  J     EQXIT                                                            
         LTORG                                                                  
*                                                                               
*                                                                               
*                                                                               
**********************************************************************          
* WORKING STORAGE                                                    *          
**********************************************************************          
        SPACE 1                                                                 
AUTOD    DSECT                                                                  
VTYPES   DS    0A                                                               
ASETMNTH DS    A                   REQUESTED MONTHS TABLE                       
ABINADD  DS    A                   ROUTINE TO ADD TO BINSEARCH TABLE            
ATSRTAB  DS    A                   SS TRANSACTION TABLE                         
APSSATAB DS    A                   SS ACC TABLE BUILT FROM FILE                 
ASSPNTTB DS    A                   BUILD VENDOR TABLE USING POINTERS            
AMAINTAB DS    A                   ADDRESS OF MAIN TABLE                        
ALDSYTAB DS    A                   LEDGER SYSTEM TABLE                          
ASMEDTAB DS    A                   SYSTEM MEDIA TABLE                           
APSYSTAB DS    A                   PRINT SYSTEM TABLE                           
AINSTAB  DS    A                   INSETION DATE CALCULATION TABLE              
ATSAR    DS    A                   ADDRESS OF TSAR MAINTENANCE ROUTINE          
ADMRDSEQ DS    A                   READ SEQUENTIAL                              
ADMRDHI  DS    A                   READ HIGH                                    
ADMREAD  DS    A                   READ                                         
ADMGET   DS    A                   GET RECORD                                   
ADMWRITE DS    A                   WRITE RECORD                                 
ADMPUT   DS    A                   PUT RECORD                                   
APRNTBL  DS    V                   PRINT DATA                                   
BINSRC31 DS    V                   BINSRCH (31 BIT MODE)                        
VHELLO   DS    V                                                                
ACLIST   DS    V                                                                
DATCON   DS    V                                                                
VGETBRD  DS    V                                                                
VGETDAY  DS    V                                                                
VADDAY   DS    V                                                                
ADMASTC  DS    V                                                                
DATAMGR  DS    V                                                                
ASRPPTAB DS    A                                                                
VTYPLNQ  EQU   *-VTYPES                                                         
*                                                                               
AELMNTS  DS    0A                                                               
AGDAELD  DS    A                   X'E5'                                        
AMBIELD  DS    A                   X'F3'                                        
AOTHELD  DS    A                   X'23'                                        
AXPYELD  DS    A                   X'46'                                        
AMDTELD  DS    A                   X'1A'                                        
AMDPELD  DS    A                   X'6A'                                        
AELMNTSQ EQU   *-AELMNTS                                                        
*                                                                               
ABASE    DS    A                                                                
RELO     DS    A                                                                
SAVERD   DS    A                                                                
SAVERA   DS    A                                                                
AAUTBLK  DS    A                   ADDRESS OF AUTO BLOCK PARAMETER              
AVENDBUF DS    A                   VENDOR BUFFER                                
CALLRD   DS    F                                                                
*                                                                               
RQMNTHS  DS    XL1                 NUMBER OF MONTHS IN REQUEST                  
MNTHS    DS    CL(MTHLEN*MAXMTHS)  START THRU END YYMM (PACKED)                 
*                                                                               
NUMRECS  DS    PL8                 RECORDS IN PSSAWRK                           
SVTRNAMT DS    PL8                 SAVED TRANSACTION AMOUNT                     
SVPKAMT  DS    PL8                 SAVED AMOUNT                                 
PKAMNT   DS    PL8                                                              
PKAVAIL  DS    PL8                 AVAILABLE AMNT FOR UNAPPROVED ITEMS          
PL16     DS    PL16                                                             
PL8      DS    PL8                                                              
SVPCNT   DS    PL8                                                              
SVTAXINC DS    PL8                 SAVED TAX AND INCOME                         
SVTOTNET DS    PL8                 SAVED TOTAL NET                              
SVPCTNET DS    PL8                                                              
SVPCTTNI DS    PL8                                                              
SVREMAIN DS    PL8                                                              
SVSYS    DS    CL1                 SAVED SYSTEM CODE                            
SVMED    DS    CL1                 SAVED MEDIA CODE                             
SVAGY    DS    CL2                 SAVED AGENCY CODE                            
DSKADDR  DS    XL4                 DISK ADDRESS OF CURRENT TRANSACTION          
SVBILTYP DS    C                                                                
SVCLNET  DS    XL4                                                              
SVFLAG   DS    X                                                                
*                                                                               
BCFLAG1  DS    XL1                                                              
BCFULL   DS    F                                                                
BCDUB    DS    D                                                                
BCTSINDS DS    XL1                 TSAR BUFFER INDICATOR                        
BCTSAR1  EQU   1                   - USING TSAR BUFFER 1                        
BCTSAR2  EQU   2                   - USING TSAR BUFFER 2                        
BCTSERRS DS    CL1                 TEMP TSAR ERRORS                             
BCTNUM   DS    XL4                                                              
*                                                                               
*&&DO                                                                           
PROFILES DS    0XL16                                                            
PROF1    DS    XL1                                                              
PROF2    DS    XL1                                                              
PROF3    DS    XL1                                                              
PROF4    DS    XL1                                                              
PROF5    DS    XL1                                                              
PROF6    DS    XL1                                                              
PROF7    DS    XL1                                                              
PROF8    DS    XL1                                                              
PROF9    DS    XL1                                                              
PROF10   DS    XL1                                                              
PROF11   DS    XL1                                                              
PROF12   DS    XL1                                                              
PROF13   DS    XL1                                                              
PROF14   DS    XL1                                                              
PROF15   DS    XL1                                                              
PROF16   DS    XL1                                                              
*&&                                                                             
*                                                                               
MSG      DS    CL10                DUMP MESSAGE                                 
ELCODE   DS    CL1                                                              
ELEM     DS    CL255                                                            
*                                                                               
FLAG     DS    XL1                                                              
FLGDR    EQU   X'80'               DEBIT TRANSACTION                            
FLGDATE  EQU   X'40'               FOUND MOS                                    
FLGBRD   EQU   X'20'               GO TO GET BROADCAST DATE                     
FLGSR    EQU   X'04'                                                            
FLGNTFND EQU   X'02'               RECORD WAS FOUND                             
FLGINSRV EQU   X'01'               REVERSE INSTAB LOGIC IN GETINSRTN            
*                                                                               
FLAG1    DS    XL1                                                              
FLGSTAT  EQU   X'80'               CASH IS AVAILABLE FOR AUTO APPRVL            
FLGNTDIS EQU   X'40'               NON DISBURSED ESTIMATES                      
FLGRQMOS EQU   X'20'               PASSIVE MOS IS WITHIN REQUESTED MOS          
FLG1A6AQ EQU   X'10'               X'1A' OR X'6A' ELEMENT ALREADY FOUND         
FLG106Q  EQU   X'08'               X'06' RECORD ADDED TO BUFFER                 
FLG1NOVQ EQU   X'04'               NO VENDOR INVOICES FOUND FOR CLT BIL         
FLG1ZERO EQU   X'02'               $0 UNAPPROVED INVOICE FOUND                  
*                                                                               
FLAG2    DS    XL1                                                              
FL20BILQ EQU   X'80'               AMT BILLED TOTALS TO ZERO                    
FL2TYP9Q EQU   X'40'               TYPE 9 TRANSACTION FOR THIS REF#             
*                                                                               
SVKEY    DS    CL42                                                             
TEMPKEY  DS    CL42                TEMP DEBUG                                   
LSTVPKEY DS    CL42                LAST VENDOR PASSIVE KEY                      
LASTPKEY DS    CL42                LAST VENDOR PASSIVE KEY                      
*                                                                               
LSTSRKEY DS    CL42                                                             
SVSRPKEY DS    CL42                                                             
CURRMED  DS    CL1                 CURRENT MEDIA WE ARE WORKING WITH.           
CLNT     DS    CL3                 CLIENT                                       
*                                                                               
SVMOS    DS    XL3                                                              
DTE1     DS    CL6                 YYMMDD (EBCDIC)                              
DTE2     DS    CL6                 YYMMDD (EBCDIC)                              
START    DS    XL3                                                              
END      DS    XL3                                                              
*                                                                               
TSRAWRK  DS    CL(TSRALNQ)         BINSEARCH WORK AREA - SS TRNS TABLE          
TSRAWRK2 DS    CL(TSRALNQ)                                                      
TSRAWRK3 DS    CL(TSRALNQ)                                                      
*                                                                               
PSSAWRK  DS    CL(PSSALNQ)         BINSEARCH WORK AREA - SS TABLE               
PSSAWRK0 DS    CL(PSSALNQ)         BINSEARCH WORK AREA 2 - SS TABLE             
PSSAWRK3 DS    CL(PSSALNQ)         BINSEARCH WORK AREA 3 - SS TABLE             
PSSAWRK5 DS    CL(PSSALNQ)         BINSEARCH WORK AREA 5 - SS TABLE             
SVPSSWRK DS    CL(PSSALNQ)         SAVED PAYABLES WORK AREA                     
*                                                                               
TEMPWRK  DS    CL255               TSAR WORK AREA                               
BUFFREC  DS    A                   BUFFER S/R A(RECORD)                         
BUFFRET  DS    X                   BUFFER S/R RETURN INDICATORS                 
TSARKSAV DS    XL255               TSAR RECORD KEY SAVE AREA                    
*                                                                               
SRPPWRK  DS    CL(L'TRNKEY-1+8)                                                 
*                                                                               
FILTOFF  DS    C                                                                
*                                                                               
BSPARS   DS    6F                  BINSRCH PARAMETERS                           
         ORG   BSPARS                                                           
BSP1     DS    F                                                                
BSP2     DS    F                                                                
BSP3     DS    F                                                                
BSP4     DS    F                                                                
BSP5     DS    F                                                                
BSP6     DS    F                                                                
         ORG                                                                    
*                                                                               
BSPARSA  DS    6F                  BINSRCH PARAMETERS                           
         ORG   BSPARSA                                                          
BSPA1    DS    F                                                                
BSPA2    DS    F                                                                
BSPA3    DS    F                                                                
BSPA4    DS    F                                                                
BSPA5    DS    F                                                                
BSPA6    DS    F                                                                
         ORG                                                                    
*                                                                               
BSPARS2  DS    6F                  BINSRCH PARAMETERS 2 - OFFICE LIST           
         ORG   BSPARS2                                                          
BSP2_1   DS    F                                                                
BSP2_2   DS    F                                                                
BSP2_3   DS    F                                                                
BSP2_4   DS    F                                                                
BSP2_5   DS    F                                                                
BSP2_6   DS    F                                                                
BSP2LQ   EQU   *-BSPARS2                                                        
         ORG                                                                    
*                                                                               
*                                                                               
* ACAUTOAPD                                                                     
       ++INCLUDE ACAUTOAPD                                                      
*                                                                               
IO       DS    0X                                                               
IOKEY    DS    CL42                KEY                                          
IODATA   DS    CL4096              DATA                                         
IOLNQ    EQU   *-IO                LENGTH                                       
*                                                                               
AUTOL    EQU   *-AUTOD                                                          
         EJECT                                                                  
**********************************************************************          
* DSECT TO COVER MONTH TABLE BUILT AT REQUEST FIRST                  *          
**********************************************************************          
         SPACE 1                                                                
MTHD     DSECT                                                                  
MTHCODE  DS    0XL2                YYMM                                         
MTHYEAR  DS    XL1                 YY                                           
MTHMNTH  DS    XL1                 MM                                           
MTHNUM   DS    XL1                 MONTH NUMBER                                 
MTHFLAG  DS    XL1                 PSSCASH, PSSMAN, PSSDISB ETC                 
MTHLEN   EQU   *-MTHD                                                           
MAXMTHS  EQU   25                  24 + 1 FOR ESTIMATES TOTAL                   
*                                                                               
**********************************************************************          
* DSECT TO COVER INSERTION DATE CALCULATION TABLE                    *          
**********************************************************************          
         SPACE 1                                                                
INSTABD  DSECT                                                                  
INSALPH  DS    CL2                 AGENCY ALPHA                                 
INSLEN   EQU   *-INSTABD                                                        
*                                                                               
INSMINI  DS    0CL2                MINI ELEMENT: MEDIA AND ADJUSTMENT           
INSMED   DS    CL1                 MEDIA: M,N,T,I,S,O                           
INSADJM  DS    XL1                 NUMBER OF MONTHS TO ADJUST                   
INSMLN   EQU   (*-INSMINI)*INSNMED NUMBER OF MEDIAS TO LOOP                     
*                                                                               
INSTBLNQ EQU   INSMLN+INSLEN       LENGTH OF TABLE                              
INSNMED  EQU   6                    6 MEDIAS                                    
*                                                                               
***********************************************************************         
* DSECT FOR ENTRY IN SS TRANSACTION TABLE                             *         
***********************************************************************         
         SPACE 1                                                                
TSRAD    DSECT                                                                  
TSRKEY   DS    0X                                                               
TSRKACT  DS    CL14                SR ACCOUNT                                   
TSRKCAC  DS    CL14                SR CONTRA U/L/AC                             
TSRKBNO  DS    CL6                 SR REF/BILL NO.                              
TSRKBDA  DS    PL3                 SR TRNDATE/BILL DATE                         
TSRKSYS  DS    CL1                 SYSTEM SPOT/NET/PRINT                        
TSRKMED  DS    CL1                 MEDIA FOR SYSTEM ABOVE                       
TSRKCLI  DS    CL3                 CLEINT FROM CONTRA OF SS                     
TSRKPRO  DS    CL3                 PRODUCT FROM 1ST 3 BYTES OF REF              
TSRKEST  DS    CL6                 ESTIMATE                                     
TSRKMOS  DS    PL2                 TRANSACTION MOS PACKED (YYMM)                
*                                                                               
TSRKMOSQ EQU   *-TSRKEY            KEY LENGTH UP TO VENDOR LEVEL                
*                                                                               
TSRKVCOD DS    CL12                VENDOR CODE                                  
TSRKVNAM DS    CL24                VENDOR NAME                                  
TSRKDUDT DS    PL3                 CLIENT INVOICE DUE DATE                      
*                                                                               
TSRKLNQ  EQU   *-TSRAD             LENGTH OF KEY                                
TSRKLNQ1 EQU   *-TSRKSYS           SYSTEM ONWARDS (FOR BILL TOTALS)             
*                                                                               
TSRKDPDT DS    PL3                 DEPOSIT DATE                                 
*                                                                               
TSRABKT  DS    0PL8                BUCKET                                       
*                                                                               
TSRADR   DS    PL8                 DEBIT AMOUNT                                 
TSRABKLN EQU   *-TSRABKT           BUCKET LENGTH                                
TSRACR   DS    PL8                 CREDIT AMOUNT                                
TSRAPCNT DS    PL8                 CALCULATED PERCENT                           
TSRAWRTF DS    PL8                 WRITE OFF                                    
TSRATXFR DS    PL8                 TRANSFER                                     
TRAOFFST DS    PL8                 OFFSET                                       
*                                                                               
TSRABKCT EQU   (*-TSRABKT)/TSRABKLN NUMBER OF BUCKETS                           
TSRALNQ  EQU   *-TSRAD             LENGTH OF ENTRY                              
         EJECT                                                                  
*                                                                               
*                                                                               
*                                                                               
* SS DETAILS RE-SORT KEY                                                        
*                                                                               
PSSRTKEY DSECT                                                                  
PSSRTNZR DS    X                   X'01' = NEGATIVE AMOUNT                      
*                                  X'02' = ZERO AMOUNT                          
*                                  X'03' = NON-ZERO AMOUNT                      
PSSRTDAT DS    PL3                 INVOICE CLEARANCE DATE                       
PSSRTAMT DS    PL8                 AMOUNT                                       
PSSRTKY  DS    XL(PSSABKT-PSSKVEN)                                              
PSSRTKLQ EQU   *-PSSRTKEY                                                       
*                                                                               
PSSRTNQ  EQU   2000                                                             
*                                                                               
*                                                                               
*                                                                               
*                                                                               
BUFFERD  DSECT                                                                  
BUFFVNDR DS    CL12                                                             
BUFFGRS  DS    PL6                                                              
BUFFNET  DS    PL6                                                              
BUFFNAME DS    CL24                                                             
BUFFERLN EQU   *-BUFFERD                                                        
*                                                                               
VBUFFSZQ EQU   BUFFERLN*1500+1     FOR 1500 VENDOR ENTRIES + 1 BYTE EOT         
*                                                                               
*                                                                               
***********************************************************************         
*               ++INCLUDES                                            *         
***********************************************************************         
*                                                                               
* ACAUTOD                                                                       
       ++INCLUDE ACABAUTOD                                                      
*                                                                               
* ACGENFILE                                                                     
       ++INCLUDE ACGENFILE                                                      
* DDMASTD                                                                       
       ++INCLUDE DDMASTD                                                        
* DDCOMFACS                                                                     
       ++INCLUDE DDCOMFACS                                                      
* DDTSARD                                                                       
       ++INCLUDE DDTSARD                                                        
* DDBUFFD                                                                       
       ++INCLUDE DDBUFFD                                                        
* AUTBLKX                                                                       
       ++INCLUDE ACAUTOAPDX                                                     
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'014ACAUTOAB  03/24/20'                                      
         END                                                                    
