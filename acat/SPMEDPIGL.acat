*          DATA SET SPMEDPIGL  AT LEVEL 004 AS OF 05/01/02                      
*CATALP MEDPIGL                                                                 
         TITLE 'BUILD LIST OF SINGLE AND PIGGYBACK PRDS/LENGTHS'                
*                                                                               
*  NOTE - SPMEDPIGL IS A CLONE OF SPMEDPSL.                                     
*         IT BUILDS A LIST OF ALL SINGLE PRODUCT/LENGTHS                        
*         AND PIGGYBACK PAIRS.                                                  
*         IT CREATES A LIST WITH 4-BYTE ENTRIES, RATHER THAN                    
*         2 LIKE MEDPSL.                                                        
*     **  IT HANDLES ONLY POL BUY RECORDS **                                    
*                                                                               
*   PARAMETERS                                                                  
*  WORD 1 = A(SPWORKC)                                                          
*  WORD 2 = A(PRODUCT/SPOT LENGTH LIST)                                         
*                                                                               
*  LIST FORMAT = 1 BYTE PRODUCT CODE - PRD 1                                    
*                1 BYTE SPOT LENGTH  - PRD 1                                    
*                1 BYTE PRODUCT CODE - PRD 2                                    
*                1 BYTE SPOT LENGTH  - PRD 2                                    
*                                                                               
MEDPIGL  CSECT                                                                  
         NMOD1 0,MEDPIGL                                                        
         L     RA,0(R1)                                                         
         LA    RC,2048(RA)                                                      
         LA    RC,2048(RC)                                                      
         USING SPWORKD,RA,RC                                                    
         L     R3,ADBUY                                                         
         USING BUYRECD,R3                                                       
         MVC   HLFA,BUYRLEN        SET FOR END OF BXLE LOOP                     
         LH    RF,HLFA                                                          
         LA    RF,0(R3,RF)                                                      
         BCTR  RF,0                                                             
         LA    R2,BDELEM                                                        
         USING REGELEM,R2                                                       
FNDEL    CLI   RCODE,6                                                          
         BL    NXTEL                                                            
         XC    X,X                                                              
         CLI   RCODE,X'0D'                                                      
         BH    NXTEL                                                            
         TM    RSTATUS,X'04'       HIATUS                                       
         BO    NXTEL                                                            
         CLI   RCODE,X'0B'         BRAND ELEMENT                                
         BL    BRNDREC                                                          
*                                  POL                                          
         CLI   RLEN,12             UNALLOCATED                                  
         BNL   FNDEL1                                                           
         MVI   X,219                                                            
         MVC   X+1(1),BDSEC                                                     
         BAS   R9,INSERT                                                        
         B     NXTEL                                                            
*                                                                               
FNDEL1   MVC   X(2),RPPRD          SET 1ST PRODUCT AND LENGTH                   
         CLI   RLEN,15             PIGGYBACK                                    
         BL    *+10                                                             
         MVC   X+2(2),RPPRD+4      SET 2ND PRODUCT AND LENGTH                   
         BAS   R9,INSERT                                                        
         B     NXTEL                                                            
*                                                                               
BRNDREC  B     NXTEL               **NO-OP NON-POL PROCESSING**                 
*                                                                               
         DROP  R2                                                               
         USING REGELEM,R2                                                       
NXTEL    SR    RE,RE                                                            
         IC    RE,RLEN                                                          
         LTR   RE,RE               CHECK FOR END                                
         BZ    EXIT                                                             
         BXLE  R2,RE,FNDEL                                                      
*                                                                               
EXIT     XMOD1 1                                                                
*                                                                               
INSERT   DS    0H                                                               
         L     R8,4(R1)            GET LIST ADDRESS                             
*                                                                               
INSERT6  CLI   0(R8),0                                                          
         BNE   *+12                                                             
         MVC   0(4,R8),X                                                        
         BR    R9                                                               
         CLC   X(4),0(R8)                                                       
         BER   R9                                                               
         LA    R8,4(R8)                                                         
         B     INSERT6                                                          
*                                                                               
HLFA     DS    H                                                                
X        DS    F                                                                
         LTORG                                                                  
         PRINT OFF                                                              
         EJECT                                                                  
* SPREPWORKD                                                                    
       ++INCLUDE SPREPWORKD                                                     
BUYRECD  DSECT                                                                  
       ++INCLUDE SPGENBUY                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004SPMEDPIGL 05/01/02'                                      
         END                                                                    
