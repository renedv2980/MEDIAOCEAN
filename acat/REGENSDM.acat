*          DATA SET REGENSDM   AT LEVEL 054 AS OF 10/06/00                      
*CATALP REGENSDM                                                                
***********************************************************************         
*                                                                     *         
*         REGENSDM --- SCREEN DUMP UTILITY                            *         
*                                                                     *         
* ------------------------------------------------------------------- *         
* UPDATE HISTORY:                                                     *         
*                                                                     *         
* 26SEP00 (SKU) --- DATE OF BIRTH                                     *         
*                                                                     *         
***********************************************************************         
         TITLE 'REGENSDM - SCREEN DUMP UTILITY'                                 
***********************************************************************         
*  INPUT:                                                                       
*              PARAMETER 1 =       A(ROUTINES ADDRESS BLOCK)                    
*                                  WORD 1 = A(TWA)                              
*                                  WORD 2 = A(DATAMGR)                          
*                                  WORD 3 = A(HELLO)                            
*                                  WORD 4 = A(DATCON)                           
*              PARAMETER 2 =       A(INFO ELEMENT)                              
***********************************************************************         
REGENSDM CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,REGENSDM                                             
         USING WORKD,RC                                                         
*                                                                               
         L     RE,0(R1)                                                         
         L     RA,0(RE)                                                         
         MVC   VDATAMGR,4(RE)                                                   
         MVC   VHELLO,8(RE)                                                     
         MVC   VDATCON,12(RE)                                                   
*                                                                               
         XC    INFOELEM,INFOELEM                                                
         L     RE,4(R1)                                                         
         ZIC   R1,1(RE)                                                         
         LTR   R1,R1                                                            
         BZ    MAIN10                                                           
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   INFOELEM(0),0(RE)                                                
*                                                                               
MAIN10   DS    0H                                                               
         MVC   DATADISP,=H'34'                                                  
*                                                                               
         BAS   RE,DUMPSCRN                                                      
*                                                                               
EXIT     DS    0H                                                               
         XMOD1                                                                  
*                                                                               
*********************************************************************           
* DUMP CURRENT SCREEN TO X'55' RECORD                                           
*********************************************************************           
DUMPSCRN DS    0H                                                               
         XC    COUNT,COUNT                                                      
         MVC   MAXCOUNT,=XL4'FFFFFFFF'                                          
*                                                                               
         XC    KEY,KEY                                                          
         MVI   KEY,X'55'           SCREEN DUMP RECORD                           
*                                                                               
         BAS   RE,HIGH                                                          
         CLC   KEY(27),KEYSAVE                                                  
         BNE   DS100                                                            
*                                                                               
         BAS   RE,GETREC                                                        
         LA    R6,IOAREA                                                        
         MVI   ELCODE,X'01'                                                     
         BAS   RE,GETEL                                                         
         BNE   DSX                                                              
*                                                                               
         USING RSCNIDCD,R6                                                      
         CLC   RSCNID#,RSCNIDMX    MAX REACHED, STOP WRITTING TO FILE           
         BNL   EXIT                                                             
         MVC   COUNT,RSCNID#                                                    
         MVC   LASTDATE,RSCNDATE                                                
         MVC   MAXCOUNT,RSCNIDMX                                                
         DROP  R6                                                               
*                                                                               
DS100    DS    0H                                                               
         XCEFL IOAREA,3972                                                      
*                                                                               
         LA    R6,IOAREA                                                        
         USING RSCNREC,R6                                                       
         MVI   RSCNKTYP,RSCNTYPQ                                                
         MVC   RSCNLEN,=H'35'                                                   
         DROP  R6                                                               
*                                                                               
         XC    ELEM,ELEM                                                        
         LA    R6,ELEM                                                          
         USING RSCNIDCD,R6                                                      
         MVI   RSCNIDCD,X'01'                                                   
         MVI   RSCNIDLN,RSCNIDLQ                                                
         L     R1,COUNT                                                         
         AHI   R1,1                                                             
         ST    R1,COUNT                                                         
         MVC   RSCNID#,COUNT                                                    
*                                                                               
         MVC   RSCNIDMX,MAXCOUNT                                                
*                                                                               
         GOTO1 VDATCON,DMCB,(5,0),(3,RSCNDATE)                                  
         CLC   LASTDATE,RSCNDATE                                                
         BE    DS105                                                            
         MVC   RSCNID#,=F'1'       NEW DAY, RESET COUNTER                       
*                                                                               
DS105    DS    0H                                                               
         THMS  DDSTIME=YES                                                      
         ST    R0,DUB              ACTUAL TIME ADJUSTMENT                       
         ST    R1,DUB+4            DDS TIME                                     
         AP    DUB(4),DUB+4(4)                                                  
         ICM   R1,15,DUB                                                        
         SRL   R1,4                SHIFT OFF SECONDS AND SIGN                   
         STCM  R1,7,RSCNTIME                                                    
         DROP  R6                                                               
*                                                                               
         GOTO1 VHELLO,DMCB,(C'P',=C'REPFILE'),IOAREA,ELEM,=C'ADD=END'           
         CLI   DMCB+12,0                                                        
         BNE   DS120                                                            
*                                                                               
         OC    INFOELEM,INFOELEM                                                
         BZ    DS108                                                            
         GOTO1 VHELLO,DMCB,(C'P',=C'REPFILE'),IOAREA,INFOELEM,         X        
               =C'ADD=END'                                                      
         CLI   DMCB+12,0                                                        
         BNE   DS120                                                            
*                                                                               
DS108    DS    0H                                                               
         LA    R2,64(RA)                                                        
*                                                                               
         LA    R4,RSCNIDLQ                                                      
*                                                                               
DS110    DS    0H                                                               
         CLI   0(R2),0                                                          
         BE    DS120                                                            
*                                                                               
         XC    ELEM,ELEM                                                        
         LA    R6,ELEM                                                          
         USING RSCNFLDE,R6                                                      
         MVI   RSCNFLDE,X'10'                                                   
*                                                                               
         ZIC   R1,0(R2)                                                         
         AHI   R1,2                OVERHEAD FOR LENGTH                          
         STC   R1,RSCNFELN                                                      
*                                                                               
         AR    R4,R1                                                            
*                                                                               
         SHI   R1,3                OVERHEAD FOR MOVE                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   RSCNFLD(0),0(R2)                                                 
*                                                                               
         GOTO1 VHELLO,DMCB,(C'P',=C'REPFILE'),IOAREA,ELEM,=C'ADD=END'           
         CLI   DMCB+12,0                                                        
         BNE   DS120                                                            
*                                                                               
         ZIC   R1,0(R2)                                                         
         AR    R2,R1                                                            
         B     DS110                                                            
*                                                                               
DS120    DS    0H                                                               
         CLC   KEY(27),KEYSAVE                                                  
         BNE   DS130                                                            
         BAS   RE,PUTREC                                                        
         B     DSX                                                              
*                                                                               
DS130    DS    0H                                                               
         BAS   RE,ADDREC                                                        
*                                                                               
DSX      DS    0H                                                               
         B     EXIT                                                             
*                                                                               
***********************************************************************         
* DATAMGR CALLS                                                                 
***********************************************************************         
HIGH     NTR1                                                                   
         MVC   COMMAND,=C'DMRDHI'                                               
         B     DIRCTRY                                                          
*                                                                               
SEQ      NTR1                                                                   
         MVC   COMMAND,=C'DMRSEQ'                                               
                                                                                
DIRCTRY  DS    0H                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 VDATAMGR,DMCB,(X'88',COMMAND),=C'REPDIR',KEYSAVE,KEY             
         B     EXIT                                                             
*                                                                               
GETREC   NTR1                                                                   
         LA    R3,KEY+28                                                        
         MVC   COMMAND,=C'GETREC'                                               
         B     FILE                                                             
*                                                                               
PUTREC   NTR1                                                                   
         LA    R3,KEY+28                                                        
         MVC   COMMAND,=C'PUTREC'                                               
         B     FILE                                                             
*                                                                               
ADDREC   NTR1                                                                   
         LA    R3,KEY                                                           
         MVC   COMMAND,=C'ADDREC'                                               
*                                                                               
FILE     DS    0H                                                               
         GOTO1 VDATAMGR,DMCB,(X'80',COMMAND),=C'REPFILE',(R3),IOAREA,  X        
               DMWORK                                                           
         B     EXIT                                                             
*                                                                               
         GETEL R6,DATADISP,ELCODE                                               
         EJECT                                                                  
WORKD    DSECT                                                                  
DMWORK   DS    12D                                                              
COMMAND  DS    CL6                                                              
KEY      DS    CL32                                                             
KEYSAVE  DS    CL32                                                             
DMCB     DS    6F                                                               
DUB      DS    D                                                                
VDATAMGR DS    A                   A(DATAMGR)                                   
VHELLO   DS    A                   A(HELLO)                                     
VDATCON  DS    A                   A(DATCON)                                    
DATADISP DS    H                                                                
ELCODE   DS    X                                                                
COUNT    DS    F                                                                
MAXCOUNT DS    F                                                                
LASTDATE DS    XL3                                                              
ELEM     DS    XL256                                                            
INFOELEM DS    XL256                                                            
IOAREA   DS    3972C                                                            
WORKX    EQU   *                                                                
*                                                                               
       ++INCLUDE REGENSCN                                                       
       ++INCLUDE DDFLDHDR                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'054REGENSDM  10/06/00'                                      
         END                                                                    
