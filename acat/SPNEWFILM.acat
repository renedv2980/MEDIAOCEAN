*          DATA SET SPNEWFILM  AT LEVEL 024 AS OF 05/01/02                      
*CATALP NEWFILM                                                                 
         TITLE 'SPGETFILM - BUILD COMMERCIAL LIST'                              
********************************************************************            
* PARAMETER LIST --                                                *            
*   +0    A(SPWORK)                                                *            
*                                                                  *            
*   +4    A(INPUT PARMS)  PRD(1)/SLN(1)/PRD2(1)/SLN2(1)            *            
*                         COPY(1)/STDT(2)/ENDT(2)                  *            
*                         STATION TYPE(1)                          *            
*                         STATION AFFL(3)                          *            
*                                                                  *            
*   +8    A(OUTPUT PARMS) STDT1(2)/ENDT1(2)                        *            
*                         FILM1 ALPHA(8)/SEQNUM(2)/RELFREQ(1)      *            
*                         FILM2 ALPHA(8)/SEQNUM(2)/RELFREQ(1)      *            
*                         .... REPEAT FOR EACH FILM                *            
*                         11X'00' TERMINATES THESE DATES           *            
*                                                                  *            
*                         STDT2(2)/ENDT2(2)                        *            
*                         FILM1 ALPHA(8)/SEQNUM(2)/RELFREQ(1)      *            
*                         FILM2 ALPHA(8)/SEQNUM(2)/RELFREQ(1)      *            
*                         .... .....                               *            
*                         11X'00' TERMINATOR                       *            
*                                                                  *            
*                         X'00'   LIST TERMINATOR                  *            
********************************************************************            
         SPACE 2                                                                
GETFILM  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,GETFILM,RR=R8                                                  
         LA    RC,2048(RB)                                                      
         LA    RC,2048(RC)                                                      
         USING GETFILM+4096,RC                                                  
*                                                                               
         ST    R8,RELO                                                          
*                                                                               
         L     RA,0(R1)                                                         
         LA    R9,2048(RA)                                                      
         LA    R9,2048(R9)                                                      
         USING SPWORKD,RA,R9                                                    
*                                                                               
         MVC   SAVEP2(8),4(R1)     SAVE INPUT/OUTPUT PARAMETERS                 
*                                                                               
         L     RE,SAVEP3           GET OUTPUT AREA ADDRESS                      
         XC    0(12,RE),0(RE)      PRESET LIST TERMINATOR                       
*                                                                               
         L     R4,SAVEP2           GET INPUT PARAMETERS                         
         XC    SVTABLE,SVTABLE     CLEAR SAVE AREA                              
         MVC   SVTBPRD(2),0(R4)    PRD1/SLN1                                    
         MVC   SVTBPRD2(2),2(R4)   PRD2/SLN2                                    
         MVC   SVTBCOPY,4(R4)                                                   
         GOTO1 DATCON,DMCB,(2,5(R4)),(3,SVTBSTR)                                
         GOTO1 (RF),(R1),(2,7(R4)),(3,SVTBEND)                                  
         MVC   SVTBTYPE,9(R4)                                                   
         MVC   SVTBAFFL,10(R4)                                                  
         B     GETPTNS                                                          
*                                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
*************************                                                       
*  READ PATTERN RECORDS *                                                       
*************************                                                       
         SPACE 1                                                                
GETPTNS  DS    0H                                                               
         LA    R4,PTNLST                                                        
         ST    R4,NEXTADDR                                                      
         ST    R4,SVTBAPTN                                                      
         USING PTNLISTD,R4                                                      
         XC    0(L'PTNLIST+1,R4),0(R4)    CLEAR 1 ENTRY + 1 BYTE                
*                                                                               
GETP2    XC    KEY,KEY                                                          
         MVC   KEY(2),=X'0A22'                                                  
         MVC   KEY+2(1),BAGYMD                                                  
         MVC   KEY+3(2),BCLT                                                    
         MVC   KEY+5(2),SVTBPRD    PRD/SLN                                      
         MVC   KEY+7(2),SVTBPRD2   PTR/SLN                                      
         MVC   KEY+9(1),SVTBCOPY   COPY CODE                                    
*                                                                               
         GOTO1 HIGH                                                             
         B     GETP6                                                            
*                                                                               
GETP4    DS    0H                                                               
         SR    RE,RE                                                            
         ICM   RE,7,KEY+10         GET REF/SUBL                                 
         SRL   RE,10               DROP SUBLINE                                 
         LA    RE,1(RE)            BUMP LINE NUMBER                             
         SLL   RE,10                                                            
         STCM  RE,7,KEY+10                                                      
         OC    KEY+10(3),KEY+10    TEST REACHED END                             
         BE    GETP10                                                           
         GOTO1 HIGH                                                             
*                                                                               
GETP6    CLC   KEY(10),KEYSAVE                                                  
         BNE   GETP10                                                           
*                                                                               
         MVC   AREC,ADBUY          SET I/O AREA ADDRESS                         
         BAS   RE,BLDLIST          BUILD PATTERN TABLE ENTRY                    
         B     GETP4                                                            
*                                                                               
GETP10   DS    0H                                                               
         BAS   RE,BLDDATES         BUILD PATTERN DATE LIST                      
         CLI   ERROR,0                                                          
         BNE   EXIT                                                             
         EJECT                                                                  
**********************                                                          
* BUILD OUTPUT LIST  *                                                          
**********************                                                          
         SPACE 1                                                                
         LA    R4,PTNLST                                                        
         USING PTNLISTD,R4                                                      
*                                                                               
         OC    PTNLIST,PTNLIST                                                  
         BZ    EXIT                                                             
*                                                                               
         L     R5,SAVEP3           GET OUTPUT AREA ADDREES                      
*                                                                               
GETP12   DS    0H                                                               
         GOTO1 DATCON,DMCB,(3,PTNLSTR),(2,0(R5))                                
         GOTO1 (RF),(R1),(3,PTNLEND),(2,2(R5))                                  
         LA    R5,4(R5)                                                         
         SPACE 1                                                                
* READ THE PATTERN RECORD *                                                     
         SPACE 1                                                                
         XC    KEY,KEY                                                          
         MVC   KEY+14(4),PTNLDSKA                                               
         L     R6,ADBUY                                                         
         ST    R6,AREC                                                          
         GOTO1 GET                                                              
*                                                                               
         L     R6,AREC                                                          
         MVI   ELCODE,X'30'        GET COMMERCIAL LIST ELEMENT                  
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   SVEL30,0(R6)                                                     
*                                                                               
         ZIC   RE,SVEL30+1         GET ELEM LEN                                 
         SRL   RE,4                DIVIDE BY 16                                 
         STC   RE,NUMCMLS          SAVE NUMBER OF CMMLS                         
*                                                                               
         L     R6,AREC                                                          
         MVI   ELCODE,X'32'        GET PATTERN ELEMENT                          
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   SVEL32,0(R6)                                                     
*                                                                               
         MVI   CMLNUM,1            SET CMML NUMBER IN PROCESS                   
         MVI   CMLALPHA,C'A'                                                    
         SPACE 1                                                                
* COUNT RELATIVE FREQUENCY *                                                    
         SPACE 1                                                                
GETP14   LA    RE,SVEL32                                                        
         ZIC   RF,1(RE)                                                         
         SH    RF,=H'2'                                                         
         LA    RE,2(RE)                                                         
         SR    R0,R0                                                            
GETP16   CLC   CMLALPHA,0(RE)                                                   
         BNE   *+6                                                              
         BCTR  R0,0                                                             
         LA    RE,1(RE)                                                         
         BCT   RF,GETP16                                                        
         LPR   R0,R0                                                            
         BZ    GETP20                                                           
         STC   R0,10(R5)           SET RELATIVE FREQ                            
         SPACE 1                                                                
* FIND COMMERCIAL IN LIST ELEMENT *                                             
         SPACE 1                                                                
         ZIC   RF,CMLNUM                                                        
         BCTR  RF,0                                                             
         SLL   RF,4                X 16                                         
         LA    RF,SVEL30+2(RF)     POINT TO COMMERCIAL                          
         CLI   0(RF),C'*'          TEST DELETED                                 
         BE    GETP20                                                           
         MVC   0(8,R5),0(RF)       MOVE ALPHA CMML TO OUTPUT                    
         SPACE 1                                                                
* READ COMMERCIAL RECORD TO GET SEQUENCE NUM *                                  
         SPACE 1                                                                
         XC    KEY,KEY                                                          
         MVC   KEY(2),=X'0A21'                                                  
         MVC   KEY+2(1),BAGYMD                                                  
         MVC   KEY+3(2),BCLT                                                    
         MVC   KEY+5(8),0(RF)      CMML ID                                      
         GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 GET                                                              
*                                                                               
         L     R6,AREC                                                          
         MVI   ELCODE,X'10'        GET CMML DATA ELEMENT                        
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING CMLDTAEL,R6                                                      
         MVC   8(2,R5),CMLSEQ+1    SET CMML SEQ NUM                             
         TM    CMLSTAT,X'80'       TEST CMML DELETED                            
         BO    GETP20                                                           
         LA    R5,11(R5)           NEXT OUTPUT POSITION                         
         DROP  R6                                                               
*                                                                               
GETP20   XC    0(12,R5),0(R5)      SET EOL FLAG                                 
         ZIC   RE,CMLNUM                                                        
         LA    RE,1(RE)                                                         
         STC   RE,CMLNUM           SET REL CML NUM                              
         LA    RE,CMLTAB-1(RE)     SET ALPHA EQUIVALENT                         
         MVC   CMLALPHA,0(RE)                                                   
*                                                                               
         CLC   CMLNUM,NUMCMLS      COMPARE TO NUMBER OF CMLS                    
         BNH   GETP14                                                           
         EJECT                                                                  
* NEXT PATTERN LIST ENTRY *                                                     
         SPACE 1                                                                
         LA    R5,11(R5)           LEAVE AN EMPTY ENTRY AT DATE END             
         LA    R4,L'PTNLIST(R4)                                                 
         CLI   0(R4),0                                                          
         BNE   GETP12                                                           
         MVI   0(R5),0             0 DATE IS EOL FLAG                           
         B     EXIT                                                             
CMLTAB   DC    C'ABCDEFGHIJKLMNOPQRSTUVWXYZ'                                    
*                                                                               
         EJECT                                                                  
****************************************************************                
* SUBROUTINE TO BUILD A PATTERN LIST ENTRY FROM PATTERN RECORD *                
* ON ENTRY NEXTADDR POINTS TO NEXT PATTERN LIST SLOT AND       *                
*               AND HIGH ORDER BYTE IS NEXT SEQUENCE NUMBER    *                
****************************************************************                
         SPACE 1                                                                
BLDLIST  NTR1                                                                   
         L     R4,NEXTADDR                                                      
         XC    0(L'PTNLIST+1,R4),0(R4)    CLEAR 1 ENTRY + 1 BYTE                
*                                                                               
         GOTO1 GET                                                              
         L     R6,AREC                                                          
         MVI   ELCODE,X'10'                                                     
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING PATDTAEL,R6                                                      
*                                                                               
         CLC   SVTBEND,PATSTART    LAST TLCST BEFORE PATTERN START              
         BL    EXIT                                                             
         CLC   SVTBSTR,PATEND      FIRST TLCST AFTER PATTERN END                
         BH    EXIT                                                             
         CLI   1(R6),38            TEST EXTENDED ELEMENT                        
         BNH   *+12                NO                                           
         TM    PATSTAT,X'80'       TEST STATUS = DELETED                        
         BO    EXIT                YES - IGNORE                                 
         SPACE 1                                                                
* FILTER ON DAYPART LIMIT CODES *                                               
         SPACE 1                                                                
         CLI   SVTBRLC,C'*'        TEST INCLUDE ALL LIMITS                      
         BNE   BLDL2                                                            
         CLI   PATLMTCD,0          TEST NO LIMIT                                
         BE    BLDL4                                                            
         B     EXIT                                                             
*                                                                               
BLDL2    CLC   SVTBRLC,PATLMTCD    ELSE MATCH LIMIT CODES                       
         BNE   EXIT                                                             
         SPACE 1                                                                
* TEST PATTERN APPLIES TO THIS STATION *                                        
         SPACE 1                                                                
BLDL4    DS    0H                                                               
         MVC   SVPTNDTS(17),PATSTART SAVE DATES AND LIMITS                      
         L     R6,AREC                                                          
         MVI   ELCODE,X'20'                                                     
         BAS   RE,GETEL            FIND LIST ELEMENT                            
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING PATLSTEL,R6                                                      
*                                                                               
         CLI   2(R6),C'M'          TEST MARKET LIST                             
         BNE   BLDL10                                                           
         OC    PATLST,PATLST       TEST ALL MARKET PATTERN                      
         BNE   BLDL10                                                           
         MVI   ELCODE,10           SET ALL MKT IND                              
         B     BLDL50                                                           
*                                                                               
BLDL10   ZIC   R0,1(R6)                                                         
         SRDL  R0,32                                                            
         D     R0,=F'5'                                                         
         LR    R0,R1               SET FOR BCT                                  
         LA    R6,2(R6)                                                         
*                                                                               
         CLI   0(R6),C'M'          TEST MKT PATTERN                             
         BNE   BLDL20                                                           
         SPACE 1                                                                
* PROCESS MARKET LIST *                                                         
         SPACE 1                                                                
BLDL12   CLC   BMKTSTA(2),4(R6)    MATCH MARKET CODES                           
         BE    BLDL14                                                           
         LA    R6,5(R6)                                                         
         BCT   R0,BLDL12                                                        
         B     EXIT                                                             
BLDL14   MVI   ELCODE,8            SET MARKET ENTRY IND                         
         B     BLDL50                                                           
*                                                                               
BLDL20   CLI   0(R6),C'A'          TEST AFFILIATE LIST                          
         BNE   BLDL30                                                           
BLDL22   CLC   SVTBAFFL,1(R6)                                                   
         BE    BLDL24                                                           
         LA    R6,5(R6)                                                         
         BCT   R0,BLDL22                                                        
         B     EXIT                                                             
BLDL24   MVI   ELCODE,6            SET AFFILIATE ENTRY IND                      
         B     BLDL50                                                           
*                                                                               
BLDL30   CLI   0(R6),C'T'          TEST STATION TYPE LIMIT                      
         BNE   BLDL40                                                           
BLDL32   CLC   SVTBTYPE,1(R6)                                                   
         BE    BLDL34                                                           
         LA    R6,5(R6)                                                         
         BCT   R0,BLDL32                                                        
         B     EXIT                                                             
BLDL34   MVI   ELCODE,4            SET STATION TYPE ENTRY IND                   
         B     BLDL50                                                           
*                                                                               
BLDL40   CLI   0(R6),C'S'          TEST STATION LIST                            
         BE    *+6                                                              
         DC    H'0'                                                             
BLDL42   CLC   BSTA,1(R6)                                                       
         BE    BLDL44                                                           
         LA    R6,5(R6)                                                         
         BCT   R0,BLDL42                                                        
         B     EXIT                                                             
BLDL44   MVI   ELCODE,2            SET STATION LIST ENTRY IND                   
         EJECT                                                                  
* ADD ENTRY TO PATTERN LIST IF SPACE *                                          
         SPACE 1                                                                
BLDL50   L     R4,NEXTADDR                                                      
         LA    R4,0(R4)            CLEAR HOB                                    
         USING PTNLISTD,R4                                                      
*                                                                               
         L     R0,=A(PTNLSTX)                                                   
         A     R0,RELO                                                          
         SH    R0,=AL2(L'PTNLIST+1)                                             
         CR    R4,R0                     TEST ROOM IN LIST                      
         BNH   *+6                                                              
         DC    H'0'                                                             
         XC    0(L'PTNLIST+1,R4),0(R4)   CLEAR 1 ENTRY + 1 BYTE                 
*                                                                               
BLDL52   ZIC   RE,NEXTADDR         BUMP SEQUENCE NUMBER                         
         LA    RE,1(RE)                                                         
         STC   RE,NEXTADDR         AND SAVE                                     
         CLI   0(RE),X'FF'                                                      
         BL    *+6                                                              
         DC    H'0'                NO MORE PATTERNS                             
*                                                                               
         MVC   PTNLSEQ,NEXTADDR        MOVE SEQUENCE NUMBER                     
         MVC   PTNLTYPE,ELCODE         SET ENTRY TYPE                           
         MVC   PTNLSTR(17),SVPTNDTS    MOVE DTS/LMTS                            
         MVC   PTNLREF,KEY+10          REF/SUBLINE                              
         MVC   PTNLDSKA,KEY+14         AND DISK ADDRESS                         
         SPACE 1                                                                
* FIND X'30' ELEMENT TO TEST PATTERN IS HIATUS *                                
         SPACE 1                                                                
         L     R6,AREC                                                          
         MVI   ELCODE,X'30'                                                     
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   =C'HIATUS',2(R6)                                                 
         BNE   BLDL54                                                           
         OI    PTNLFLAG,PTNLFHIA                                                
         XC    PTNLREF,PTNLREF     CLEAR REF/SUBLINE FOR HIATUS                 
*                                                                               
BLDL54   LA    R4,L'PTNLIST(R4)        NEXT PATTERN LIST ENTRY                  
         STCM  R4,7,NEXTADDR+1                                                  
         B     EXIT                                                             
         DROP  R4                                                               
         EJECT                                                                  
*************************************************************                   
* SUBROUTINE TO PROCESS PATTERN LIST DATA AND BUILD A TABLE *                   
* OF PATTERNS THAT APPLY TO EACH DAY IN THE TELECAST PERIOD *                   
*************************************************************                   
         SPACE 1                                                                
BLDDATES NTR1                                                                   
         LA    R5,PTNLST                                                        
         USING PTNLISTD,R5                                                      
*                                                                               
         CLI   PTNLTYPE,0          TEST NO DATA IN LIST                         
         BE    EXIT                                                             
*                                                                               
BLDDT1   GOTO1 DATCON,DMCB,(3,SVTBSTR),WORK    FIRST TELECAST DATE              
         GOTO1 (RF),(R1),(3,SVTBEND),WORK+6    LAST TELECAST DATE               
*                                                                               
         LA    R6,DATELIST                                                      
         XC    0(183,R6),0(R6)     CLEAR 367 BYTES                              
         XC    183(184,R6),183(R6)                                              
*                                                                               
         MVI   ELCODE,10                                                        
*                                                                               
BLDDT2   CLI   PTNLTYPE,0          TEST FOR EOL                                 
         BE    BLDDT20                                                          
         CLC   PTNLTYPE,ELCODE     ELSE MATCH CODES                             
         BNE   BLDDT12                                                          
         SPACE 1                                                                
* DETERMINE LIMITS OF PATTERN DATES VS TLCST DATES *                            
         SPACE 1                                                                
BLDDT4   LA    R0,PTNLSTR          POINT TO PATTERN START                       
         CLC   SVTBSTR,PTNLSTR     FIRST TLCST DATE TO PATTERN START            
         BL    *+8                 LOW - USE PATTERN START DATE                 
         LA    R0,SVTBSTR          ELSE POINT TO FIRST TLCST DATE               
         GOTO1 DATCON,DMCB,(3,(R0)),WORK+12                                     
         LA    R0,PTNLEND                                                       
         CLC   SVTBEND,PTNLEND     LAST TLCST TO PATTERN END                    
         BH    *+8                                                              
         LA    R0,SVTBEND                                                       
         GOTO1 DATCON,DMCB,(3,(R0)),WORK+18                                     
         SPACE 1                                                                
* GET DISPLACEMENT TO FIRST DATE IN DATELIST (FROM SVTBSTR) *                   
         SPACE 1                                                                
         GOTO1 =V(PERVERT),DMCB,WORK,WORK+12,RR=RELO                            
         LH    RE,8(R1)            GIVES INCLUSIVE DAYS                         
         LA    R6,DATELIST-1(RE)   POINT TO FIRST DATE                          
*                                                                               
BLDDT10  MVC   0(1,R6),PTNLSEQ     MOVE PATTERN SEQUENCE                        
         LA    R6,1(R6)                                                         
*                                                                               
         GOTO1 ADDAY,DMCB,WORK+12,WORK+12,1                                     
         CLC   WORK+12(6),WORK+18  TEST REACHED END LIMIT                       
         BNH   BLDDT10                                                          
*                                                                               
BLDDT12  LA    R5,L'PTNLIST(R5)    NEXT PATTERN LIST ENTRY                      
         B     BLDDT2                                                           
         EJECT                                                                  
* DECREMENT VALUE IN ELCODE AND RE-PROCESS PATTERN LIST *                       
         SPACE 1                                                                
BLDDT20  L     R5,SVTBAPTN         RESET POINTERS                               
         LA    R6,DATELIST                                                      
         ZIC   R0,ELCODE                                                        
         BCTR  R0,0                                                             
         BCTR  R0,0                                                             
         STC   R0,ELCODE                                                        
         LTR   R0,R0                                                            
         BP    BLDDT2                                                           
         SPACE 1                                                                
* ALL LIST ELEMENTS PROCESSED        *                                          
* SEARCH FOR DATES WITHOUT PATTERNS  *                                          
         SPACE 1                                                                
         GOTO1 =V(PERVERT),DMCB,WORK,WORK+6,RR=RELO                             
         LH    R0,8(R1)            NUMBER OF DAYS IN TELECAST PERIOD            
*                                                                               
         CLI   0(R6),0                                                          
         BNE   *+8                                                              
         MVI   0(R6),X'FF'         SET NO PATTERN FLAG                          
         LA    R6,1(R6)                                                         
         BCT   R0,*-16                                                          
         SPACE 1                                                                
* NOW BUILD NEW PATTERN LIST OVER THE OLD ONE - EACH ENTRY WILL  *              
* NOW CONTAIN THE ACTUAL FIRST AND LAST TELECAST DATES. AN ENTRY *              
* CAN APPEAR IN THE NEW TABLE MORE THAN ONCE                     *              
         SPACE 1                                                                
         L     R4,ADBUY                                                         
         LA    R0,8                                                             
         XC    0(250,R4),0(R4)                                                  
         LA    R4,250(R4)                                                       
         BCT   R0,*-10                                                          
*                                                                               
         L     R4,ADBUY                                                         
         LA    R6,DATELIST                                                      
         B     BLDDT46                                                          
*                                                                               
BLDDT42  CLC   1(1,R6),0(R6)       NEXT ENTRY USE SAME PATTERN                  
         BNE   BLDDT44                                                          
         LA    R6,1(R6)                                                         
         B     BLDDT42                                                          
*                                                                               
BLDDT44  ST    R6,ALPD             SAVE END ADDRESS                             
         CLI   0(R6),X'FF'         TEST NO PATTERN                              
         BE    BLDDT45                                                          
         ZIC   RE,0(R6)            GET PATTERN SEQ NUM                          
         BCTR  RE,0                                                             
         MH    RE,=AL2(L'PTNLIST)                                               
         A     RE,SVTBAPTN         POINT TO ENTRY                               
         TM    PTNLFLAG,PTNLFHIA   TEST HIATUS                                  
         BO    BLDDT45                                                          
         MVC   0(L'PTNLIST,R4),0(RE) COPY ENTRY TO BUILD AREA                   
         EJECT                                                                  
* CALCULATE START AND END DATES                                                 
         SPACE 1                                                                
         GOTO1 DATCON,DMCB,(3,SVTBSTR),WORK                                     
         L     R0,AFPD             GET PATTERN START DATE ADDRESS               
         LA    RE,DATELIST                                                      
         SR    R0,RE                                                            
         GOTO1 ADDAY,(R1),WORK,WORK+8,(R0)                                      
         L     R0,ALPD                                                          
         LA    RE,DATELIST                                                      
         SR    R0,RE                                                            
         GOTO1 (RF),(R1),,WORK+16,(R0)                                          
*                                                                               
         GOTO1 DATCON,(R1),WORK+8,(3,WORK+22)                                   
         GOTO1 (RF),(R1),WORK+16,(3,WORK+25)                                    
*                                                                               
         MVC   PTNLSTR-PTNLIST(6,R4),WORK+22                                    
         LA    R4,L'PTNLIST(R4)                                                 
         SPACE 1                                                                
* MAKE SURE THERE IS MORE ROOM *                                                
         SPACE 1                                                                
         L     RE,ADBUY                                                         
         LA    RE,2000-L'PTNLIST(RE)                                            
         CR    R4,RE                                                            
         BL    *+6                                                              
         DC    H'0'                                                             
*                                                                               
BLDDT45  L     R6,ALPD             GET LAST PATTERN DATE ADDRESS                
         LA    R6,1(R6)                                                         
*                                                                               
BLDDT46  ST    R6,AFPD             SET AS NEXT PATTERN START                    
         CLI   0(R6),0             TEST EOL                                     
         BNE   BLDDT42                                                          
         EJECT                                                                  
* NOW MOVE PATTERN DATA BACK OVER ORIGINAL LIST *                               
         SPACE 1                                                                
         L     RE,SVTBAPTN                                                      
         L     RF,ADBUY                                                         
BLDDT50  MVC   0(L'PTNLIST,RE),0(RF)                                            
         LA    RE,L'PTNLIST(RE)                                                 
         LA    RF,L'PTNLIST(RF)                                                 
         CLI   0(RF),0             TEST EOL                                     
         BNZ   BLDDT50                                                          
         MVI   0(RE),0             SET EOL FLAG                                 
         LA    RE,1(RE)            INCLUDE FLAG IN LIST                         
         ST    RE,NEXTADDR         SET CURRENT LIST END ADDRESS                 
         MVI   ERROR,0             RESET ERROR FLAG                             
         B     EXIT                                                             
*                                                                               
BLDDTERR MVI   ERROR,X'FF'         DO NOT BUILD OUTPUT LIST ON ERR              
         B     EXIT                                                             
*                                                                               
         GETEL (R6),24,ELCODE                                                   
         SPACE 2                                                                
         LTORG                                                                  
*                                                                               
         EJECT                                                                  
SVTABLE  DS    0F                                                               
*                                                                               
SVTBDATA DS    0XL40                                                            
SVTBLINE DS    AL4                 A(SCREEN INPUT LINE)                         
SVTBAPTN DS    AL4                 PATTERN LIST ADDRESS                         
SVTBIND  DS    XL1                 X'80'=SAME AS LAST INST                      
*                                  X'40'=PATTERN CHANGE                         
*                                  X'20'=SCHEDULE CHANGE                        
*                                  X'01'=NO PATTERN (ERROR)                     
SVTBSTA  DS    CL5                 STATION CALL LETTERS                         
SVTBPRD  DS    XL1                 PRODUCT CODE                                 
SVTBSLN  DS    XL1                 SPOT LENGTH                                  
SVTBPRD2 DS    XL1                 PARTNER PRODUCT CODE                         
SVTBSLN2 DS    XL1                 PARTNER SPOT LENGTH                          
SVTBSTR  DS    XL3                 FIRST TELECAST DATE                          
SVTBEND  DS    XL3                 LAST TELECAST DATE                           
SVTBRLC  DS    CL3                 RUN LIMIT CODE                               
SVTBCOPY DS    CL1                 COPY CODE                                    
SVTBMKST DS    XL5                 PACKED MARKET/STATION                        
SVTBAFFL DS    CL3                 AFFILIATE CODE                               
SVTBTYPE DS    CL1                 STATION TYPE                                 
         DS    CL3                 SPARE                                        
SVTBLEN  EQU   *-SVTBDATA                                                       
*                                                                               
SVTBNEXT DS    0XL40                                                            
*                                                                               
SVTBNXLN EQU   SVTBLINE+L'SVTBDATA                                              
SVTBNXST EQU   SVTBSTA+L'SVTBDATA                                               
SVTBNXPR EQU   SVTBPRD+L'SVTBDATA                                               
SVTBNXCP EQU   SVTBCOPY+L'SVTBDATA                                              
         SPACE 2                                                                
SVPTNDTS DS    CL17                                                             
NEXTADDR DS    A                                                                
SAVEP2   DS    A                   INPUT PARAMETER ADDRESS                      
SAVEP3   DS    A                   OUPOUT PARAMETER ADDRESS                     
AFPD     DS    A                                                                
ALPD     DS    A                                                                
RELO     DS    A                                                                
ELCODE   DS    C                                                                
ERROR    DS    C                                                                
NUMCMLS  DS    X                   NUMBER OF CMLS THIS PTTN                     
CMLNUM   DS    X                   CURRENT CML NUMBER                           
CMLALPHA DS    C                   CURRENT CML LETTER                           
SVEL30   DS    XL256                                                            
SVEL32   DS    XL128                                                            
DATELIST DS    367C                                                             
*                                                                               
PTNLST   DS    2048C                                                            
PTNLSTX  EQU   *-1                                                              
         SPACE 2                                                                
* DSECT FOR PATTERN LIST ENTRIES *                                              
         SPACE 1                                                                
PTNLISTD DSECT                                                                  
         SPACE 1                                                                
PTNLIST  DS    0XL32                                                            
PTNLTYPE DS    XL1                 10=ALL MKT  8=ONE MKT  6=AFFL                
*                                   4=STA TYPE 2=STATION                        
PTNLSEQ  DS    XL1                 PATTERN SEQ NUM (MIN 1)                      
PTNLSTR  DS    XL3                 PATTERN START DATE                           
PTNLEND  DS    XL3                 PATTERN END DATE                             
PTNLDPT  DS    CL1                 LIMIT DPT CODE                               
PTNLLMT1 DS    XL5                 RUN LIMIT 1                                  
PTNLLMT2 DS    XL5                 RUN LIMIT 2                                  
PTNLREF  DS    XL3                 REF/SUBLINE                                  
PTNLDSKA DS    XL4                 DISK ADDRESS                                 
PTNLFLAG DS    XL1                                                              
PTNLFHIA EQU   X'01'               HIATUS FLAG                                  
         EJECT                                                                  
       ++INCLUDE SPTRKEYS                                                       
        PRINT OFF                                                               
         EJECT                                                                  
       ++INCLUDE SPREPWORKD                                                     
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'024SPNEWFILM 05/01/02'                                      
         END                                                                    
