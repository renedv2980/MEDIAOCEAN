*          DATA SET PPGETPO#   AT LEVEL 003 AS OF 04/11/13                      
*CATALP PPGETPO#                                                                
         TITLE 'PPGETPO# - GET PRINT PO# FROM BUY OR BILL'                      
* PARAMETER LIST                                                                
*                                                                               
*  PARAMETER 1                                                                  
*  BYTE 0      C'B' RECORD IS A BUY                                             
*  BYTE 0      C'E' RECORD IS A BUY,ELEMENT IS A BILLING ELEM                   
*  BYTE 0      C'L' RECORD IS A BILL                                            
*  BYTE 0      C'C' CODE (SEQ#) RECORD IS MEDIA,AGY,CLT,PRD,EST,CODE            
*              FOR THE ABOVE OUTPUT IS THE PO#                                  
*                                                                               
*  BYTE 0      C'P' PO# RECORD IS MEDIA,AGY,CLT,PRD,EST,PO#                     
*              NOTE - PO# SHOULD BE 25 CHARACTERS-LEFT JUSTIFIED                
*                     THIS PROGRAM WILL OR IT WITH SPACES                       
*                                                                               
*              OUTPUT IS PO SEQUENCE # (CODE)                                   
*              X'FF'   ON RETURN IF INVALID PARAMETER                           
*                      OR NO PO# DATA FOUND                                     
*                                                                               
*       1-3    A(RECORD)                                                        
*                                                                               
*  PARAMETER 2                                                                  
*  BYTE 0      C'C'    PO# AT THE CLIENT LEVEL                                  
*              C'P'    PO# AT THE PRODUCT LEVEL                                 
*              C'E'    PO# AT THE ESTIMATE LEVEL                                
*              FROM AN ELEMENT IN THE CLIENT HEADER                             
*              X'00'   TREATED AS ESTIMATE                                      
*                                                                               
*  BYTE 1-3    A(OUTPUT)       SHOULD BE AT LEAST 25 BYTES                      
*                              OUTPUT IS POSEQ# (2) IF P                        
*                              IN FIRST PARAMETER                               
*  PARAMETER 3                                                                  
*  BYTE 0      X'00'                                                            
*  BYTE 1-3    A(COMFACS)                                                       
*                                                                               
*  PARAMETER 4                                                                  
*  BYTE 0      X'00'                                                            
*  BYTE 1-3    A(ELEMENT)    BILLING ELEMENT                                    
*                                                                               
*  ON OUTPUT                                                                    
*                                                                               
*  BYTE 1-3    A(PO#ELEMENT) ADDRESS OF THE ELEMENT IN THE PO# RECORD           
*-----------------------------------------------------------------              
*                                                                               
         SPACE 2                                                                
PPGETPO# CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 GETWORKL,PPGETPO#,CLEAR=YES                                      
         USING GETWORK,RC                                                       
*                                                                               
         MVC   PARS(20),0(R1)       SAVE PARAMTER CALL ARGUMENTS                
         ST    R1,APARMS                                                        
*                                                                               
         L     RF,PARS+8                                                        
         MVC   VDMGR,CDATAMGR-COMFACSD(RF)                                      
*                                                                               
         MVI   SPACES,C' '       SET SPACES                                     
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
*                                                                               
         MVC   TYPE,PARS+4         SAVE PO# TYPE C/P/E/X'00                     
         MVI   PARS+4,0            CLEAR IT                                     
         L     R2,PARS+4           ADDRESS OF OUTPUT                            
         MVC   0(25,R2),SPACES     CLEAR TO SPACES                              
*                                                                               
         CLI   PARS,C'B'           SEE IF PROCESSING A BUYREC                   
         BE    BUYR                                                             
         CLI   PARS,C'E'           SEE IF PROCESSING A BILLING ELEM             
         BE    BILLE                                                            
         CLI   PARS,C'L'           SEE IF PROCESSING A BILL                     
         BE    BILLR                                                            
         CLI   PARS,C'C'           SEE IF PROCESSING A CODE (SEQ#)              
         BE    CODE                                                             
         CLI   PARS,C'P'           SEE IF PROCESSING A PO#                      
         BE    PON                 LOOKING FOR ITS CODE (SEQ#)                  
         DC    H'0'                INVALID PARAMETER                            
         EJECT                                                                  
*                                                                               
BUYR     L     RA,0(R1)            ESTABLISH BUYREC                             
         USING PBUYREC,RA                                                       
*                                                                               
         MVC   WRKMED,PBUYKMED                                                  
         MVC   WRKAGY,PBUYKAGY     SAVE AGENCY CODE                             
         MVC   WRKCLT,PBUYKCLT                                                  
         MVC   WRKPRD,PBUYKPRD                                                  
         MVC   WRKEST,PBUYKEST                                                  
         MVI   ELCODE,PBYPOELQ                                                  
         LA    R5,PBUYREC+33                                                    
         BAS   R9,NEXTEL                                                        
         BNE   NOPO#               NO PO# RETURN                                
         USING PBYPOELD,R5                                                      
*                                                                               
         MVC   SVPOSEQ#,PBYPOSQ#  SAVE PO# SEQ# FOR LOOKUP                      
         TM    PBYPOSTA,BYPOZZZQ  ELEM FOR ZZZ BUY                              
         BZ    *+10                                                             
         MVC   WRKPRD,PBYPOPRD    USE PRD IN ELEMENT                            
         B     RDPOREC                                                          
         DROP  R5                                                               
         DROP  RA                                                               
         EJECT                                                                  
*                                                                               
BILLE    L     RA,0(R1)            ESTABLISH BUYREC                             
         USING PBUYREC,RA                                                       
         L     R8,12(R1)           ADDRESS OF BILLING ELEMENT                   
         USING PBILLED,R8                                                       
         CLI   PBILELEM+1,X'1B'    LENGTH MUST BE 27 TO HAVE A PO#              
         BNE   NOPO#               (MAY WANT TO CHANGE TO BL)                   
         OC    PBPOSEQ,PBPOSEQ     STILL MUST HAVE ONE                          
         BZ    NOPO#                                                            
*                                                                               
         MVC   WRKMED,PBUYKMED                                                  
         MVC   WRKAGY,PBUYKAGY     SAVE AGENCY CODE                             
         MVC   WRKCLT,PBUYKCLT                                                  
         MVC   WRKPRD,PBUYKPRD                                                  
         MVC   WRKEST,PBUYKEST                                                  
         MVI   ELCODE,PBYPOELQ                                                  
         LA    R5,PBUYREC+33                                                    
         BAS   R9,NEXTEL                                                        
         BNE   NOPO#               NO PO# RETURN                                
         USING PBYPOELD,R5                                                      
*                                                                               
*        USE PO# SEQ FROM BILLING ELEMENT                                       
*                                                                               
         MVC   SVPOSEQ#,PBPOSEQ   SAVE PO# SEQ# FOR LOOKUP                      
         TM    PBYPOSTA,BYPOZZZQ  ELEM FOR ZZZ BUY                              
         BZ    *+10                                                             
         MVC   WRKPRD,PBYPOPRD    USE PRD IN ELEMENT                            
         B     RDPOREC                                                          
         DROP  R5                                                               
         DROP  R8                                                               
         DROP  RA                                                               
         EJECT                                                                  
*                                                                               
BILLR    L     RA,0(R1)            ESTABLISH PBILLREC                           
         USING PBILLREC,RA                                                      
*                                                                               
         MVC   WRKMED,PBILKMED                                                  
         MVC   WRKAGY,PBILKAGY     SAVE AGENCY CODE                             
         MVC   WRKCLT,PBILKCLT                                                  
         MVC   WRKPRD,PBILKPRD                                                  
         MVC   WRKEST,PBILKEST                                                  
*                                                                               
*        NOTE - FOR PO# BILLS PBILLJOB(3),=X'FF',C'PO'                          
*        FOLLOWED BY THE PO SEQ#                                                
*        DO I NEED THE STATUS?                                                  
*                                                                               
         CLI   PBILLJOB,X'FF'                                                   
         BNE   NOPO#                                                            
         CLC   PBILLJOB+1(2),=C'PO'     CHECK FOR A PO#                         
         BNE   NOPO#                                                            
         MVC   SVPOSEQ#,PBILLJOB+3      SEQ#                                    
*                                                                               
         B     RDPOREC                                                          
         DROP  RA                                                               
         EJECT                                                                  
CODE     L     RA,0(R1)            ADDRESS OF CODE INFO                         
*                                                                               
         MVC   WRKMED,0(RA)        MEDIA                                        
         MVC   WRKAGY,1(RA)        AGENCY CODE                                  
         MVC   WRKCLT,3(RA)        CLIENT                                       
         MVC   WRKPRD,6(RA)        PRODUCT                                      
         MVC   WRKEST,9(RA)        ESTIMATE                                     
         MVC   SVPOSEQ#,11(RA)     SEQ#                                         
         OC    SVPOSEQ#,SVPOSEQ#   IF NONE                                      
         BZ    NOPO#                                                            
*                                                                               
         B     RDPOREC                                                          
*                                                                               
RDPOREC  XC    KEY,KEY                                                          
         LA    RE,KEY                                                           
         USING PPO#KEY,RE                                                       
         MVC   PPO#KAGY,WRKAGY                                                  
         MVC   PPO#KMED,WRKMED                                                  
         MVI   PPO#KRCD,PPO#KIDQ                                                
         MVC   PPO#KCLT,WRKCLT                                                  
         CLI   TYPE,C'C'          CLIENT LEVEL PO#'S                            
         BE    RDPOR5                                                           
         MVC   PPO#KPRD,WRKPRD                                                  
         CLI   TYPE,C'P'          PRODUCT LEVEL PO#'S                           
         BE    RDPOR5                                                           
         MVC   PPO#KEST,WRKEST                                                  
         CLI   TYPE,C'E'          ESTIMATE LEVEL PO#'S                          
         BE    RDPOR5                                                           
         CLI   TYPE,X'00'         MISSING TYPE - LIKE ESTIMATE                  
         BE    RDPOR5                                                           
         DC    H'0'            UNKNOWN TYPE                                     
*                                                                               
         DROP  RE                                                               
RDPOR5   BAS   RE,HIGH                                                          
         CLC   KEY(L'PPO#KEY),KEYSAVE                                           
         BNE   NOPO#              NO PO# RECORD                                 
         BAS   RE,GET                                                           
*                                                                               
*        PROCESS PO# REC HERE                                                   
         LA    R5,IOA                                                           
         LA    R5,(PO#FIRST-PPO#KEY)(R5)                                        
         USING PO#DELMD,R5                                                      
         MVI   ELCODE,PO#DLIDQ                                                  
         CLI   PO#DELID,PO#DLIDQ   AT LEAST 1 PO# HEADER ELEM?                  
         BE    *+6                                                              
         DC    H'0'                BAD PURCHASE ORDER # RECORD                  
RDPO5    DS    0H                                                               
         CLI   PARS,C'P'           SEARCHING FOR THE SEQ#                       
         BE    RDPO7                                                            
         CLC   SVPOSEQ#,PO#DID     PO SEQ# MATCH THAT OF BUY?                   
         BE    RDPO10              FOUND IT                                     
RDPO6    BAS   R9,NEXTEL                                                        
         BE    RDPO5                                                            
         B     NOPO#                                                            
*                                                                               
RDPO7    DS    0H                                                               
         SR    R3,R3                                                            
         IC    R3,PO#DLEN                                                       
         SHI   R3,PO#DHDLQ         TO GET LENGTH OF PURCHASE ORDER #            
         LR    RE,R3                                                            
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   WORK(0),PO#DPO#    EXECUTED                                      
         OC    WORK,SPACES                                                      
RDP08    CLC   SVPO#,WORK                                                       
         BE    RDPO20                                                           
         B     RDPO6               KEEP LOOKING                                 
*                                                                               
*        DON'T CARE IF INACTIVE IN THIS PROGRAM                                 
*                                                                               
RDPO10   SR    R3,R3                                                            
         IC    R3,PO#DLEN                                                       
         SHI   R3,PO#DHDLQ         TO GET LENGTH OF PURCHASE ORDER #            
         L     R4,PARS+4           R4 TO OUTPUT AREA                            
         LR    RE,R3                                                            
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),PO#DPO#     EXECUTED                                     
*                                                                               
         L     R1,APARMS                                                        
         ST    R5,12(R1)           RETURN ADDRESS OF PO# ELEMENT                
         B     DONE                                                             
*                                                                               
RDPO20   DS    0H                                                               
         L     R4,PARS+4           R4 TO OUTPUT AREA                            
         MVC   0(2,R4),PO#DID      RETURN SEQUENCE #                            
         L     R1,APARMS                                                        
         ST    R5,12(R1)           RETURN ADDRESS OF PO# ELEMENT                
         B     DONE                                                             
*                                                                               
         DROP  R5                                                               
         EJECT                                                                  
PON      L     RA,0(R1)            ADDRESS OF PO# INFO                          
*                                                                               
         MVC   WRKMED,0(RA)        MEDIA                                        
         MVC   WRKAGY,1(RA)        AGENCY CODE                                  
         MVC   WRKCLT,3(RA)        CLIENT                                       
         MVC   WRKPRD,6(RA)        PRODUCT                                      
         MVC   WRKEST,9(RA)        ESTIMATE                                     
         MVC   SVPO#,11(RA)        PO#                                          
         OC    SVPO#,SVPO#         IF NONE                                      
         BZ    NOPO#                                                            
         OC    SVPO#,SPACES                                                     
*                                                                               
         B     RDPOREC                                                          
*                                                                               
NOPO#    DS    0H                                                               
         L     R1,APARMS                                                        
         MVI   0(R1),X'FF'      ERROR RETURN                                    
*                                                                               
DONE     XIT1                                                                   
*                                                                               
         EJECT                                                                  
NEXTEL   CLI   0(R5),0                                                          
         JE    NEXTELX                                                          
         SR    R0,R0                                                            
NEXTEL2  DS    0H                                                               
         IC    R0,1(R5)                                                         
         LTR   R0,R0                                                            
         JNP   NEXTELX                                                          
         AR    R5,R0                                                            
         CLC   ELCODE,0(R5)                                                     
         BCR   8,R9                RETURN WITH CC =                             
         CLI   0(R5),0                                                          
         JNE   NEXTEL2                                                          
NEXTELX  LTR   R5,R5               SET CC TO NOT =                              
         BR    R9                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        DATAMGR CALLS                                                          
*                                                                               
***********************************************************************         
         SPACE 2                                                                
HIGH     DS    0H                                                               
         MVC   KEYSAVE,KEY                                                      
         MVC   COMMAND,=CL8'DMRDHI'                                             
         B     DIR                                                              
         SPACE 2                                                                
SEQ      DS    0H                                                               
         MVC   COMMAND,=CL8'DMRSEQ'                                             
         B     DIR                                                              
*                                                                               
DIR      DS    0H                                                               
         LR    R0,RE                                                            
         MVC   FILE,=CL8'PRTDIR'                                                
         GOTO1 VDMGR,DMCB,(DMINBTS,COMMAND),FILE,KEY,KEY,(0,DMWORK)             
         B     DMCHK                                                            
         SPACE 2                                                                
GET      DS    0H                                                               
         LR    R0,RE                                                            
         MVC   COMMAND,=CL8'GETREC'                                             
         MVC   FILE,=CL8'PRTFILE'                                               
         GOTO1 VDMGR,DMCB,(DMINBTS,COMMAND),FILE,KEY+27,IOA,(0,DMWORK)          
         B     DMCHK                                                            
*                                                                               
DMCHK    DS    0H                                                               
         LR    RE,R0                                                            
         MVC   BYTE,DMCB+8                                                      
         NC    BYTE,DMOUTBTS                                                    
         BZR   RE                                                               
         DC    H'0'                                                             
         LTORG                                                                  
         EJECT                                                                  
         TITLE 'PPGETPO# - DSECTS AND INCLUDES'                                 
*                                                                               
GETWORK  DSECT                                                                  
*                                                                               
BYTE     DS    X                                                                
APARMS   DS    A                   ADDRESS FO PARAMETER LIST                    
PARS     DS    5A                                                               
DMCB     DS    6F                  PARAMETER AREA                               
SPACES   DS    CL30                                                             
WORK     DS    CL30                                                             
ELCODE   DS    C                                                                
TYPE     DS    C                   SAVED FROM PARS+4                            
SVPOSEQ# DS    XL2                                                              
SVPO#    DS    XL25                                                             
KEY      DS    XL32                                                             
KEYSAVE  DS    XL32                                                             
WRKAGY   DS    CL2                                                              
WRKMED   DS    CL2                                                              
WRKCLT   DS    CL3                                                              
WRKPRD   DS    CL3                                                              
WRKEST   DS    CL2                                                              
*                                                                               
VDMGR    DS    A                                                                
DMINBTS  DS    X                                                                
DMOUTBTS DS    X                                                                
COMMAND  DS    CL8                                                              
FILE     DS    CL8                                                              
DMWORK   DS    12D                                                              
IOA      DS    CL4000                                                           
         DS    XL4                                                              
GETWORKL EQU   *-GETWORK           WORKAREA LENGTH                              
*                                                                               
         EJECT                                                                  
REC      DSECT                                                                  
       ++INCLUDE PBUYREC                                                        
         EJECT                                                                  
       ++INCLUDE PBDELEM                                                        
         EJECT                                                                  
       ++INCLUDE PBILLREC                                                       
         EJECT                                                                  
       ++INCLUDE PPGENPO#                                                       
*                                                                               
ELEM     DSECT                                                                  
       ++INCLUDE PPGENBYPO                                                      
*                                                                               
PBILLED  DSECT                                                                  
       ++INCLUDE PBILELEMP                                                      
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACSD                                                     
*                                                                               
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003PPGETPO#  04/11/13'                                      
         END                                                                    
