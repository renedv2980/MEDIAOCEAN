*          DATA SET FAGETFLD   AT LEVEL 010 AS OF 07/18/97                      
***********************************************************************         
* P1 = ACTION REQUIRED                                                *         
*                                                                     *         
*      1 = READ FOR EXACT MATCH                                       *         
*      2 = READ HIGH                                                  *         
*      3 = READ SEQUENTIAL                                            *         
*      4 = INDEX INTO TABLE                                           *         
*      5 = UPDATE TABLE FROM A RECORD                                 *         
*                                                                     *         
***********************************************************************         
* ACTION #1 - READ FOR EXACT MATCH                                    *         
*                                                                     *         
* NTRY: P2 = A(DIRECTORY KEY)                                         *         
*       P3 = 0 OR A(IO AREA - IF GETREC REQUIRED)                     *         
***********************************************************************         
* ACTION #2 - READ HIGH                                               *         
*                                                                     *         
* NTRY: P2 = A(DIRECTORY KEY)                                         *         
*       P3 = 0 OR A(IO AREA - IF RECORD REQUIRED)                     *         
***********************************************************************         
* ACTION #3 - READ SEQUENTIAL                                         *         
*                                                                     *         
* NTRY: P2 = A(DIRECTORY KEY)                                         *         
*       P3 = 0 OR A(IO AREA - IF RECORD REQUIRED)                     *         
***********************************************************************         
* ACTION #4 - GET RECORD BASED ON DISPLACEMENT                        *         
*                                                                     *         
* NTRY: P2 = A(RECORD BUILD AREA)                                     *         
*       P3 = INDEX INTO TABLE                                         *         
***********************************************************************         
* ACTION #5 - UPDATE TABLE FROM RECORD                                *         
*                                                                     *         
* NTRY: P2 = A(FILE RECORD TO UPDATE)                                 *         
***********************************************************************         
         SPACE 2                                                                
*CATALP GETFLD                                                                  
GETFLD   TITLE 'FORMAT INTO AND GET FIELD RECORDS FROM XA STORAGE'              
GETFLD   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 BWORKL,GETFLD**,RA                                               
         USING BWORKD,RC                                                        
         ST    R1,CALLR1         SAVE CALLERS DETAILS                           
         MVC   BPARMS,0(R1)                                                     
*                                                                               
         PROT  OFF                                                              
*                                                                               
         L     RF,=V(SYSFAC)                                                    
         USING SYSFACD,RF                                                       
         MVC   ADATAMGR,VDATAMGR                                                
         MVC   ASSB,VSSB                                                        
         ICM   R9,15,ASSB        NO SSB - RUNNING OFFLINE, DO DMREADS           
         USING SSBD,R9                                                          
         BZ    FILEREAD                                                         
         OC    0(2,R9),0(R9)     TEST FOR NEW OFFLINE SSB                       
         BZ    FILEREAD                                                         
         TM    SSBSYSFL,X'80'                                                   
         BO    FILEREAD          READ FILE IF ON A TEST SYSTEM                  
*                                                                               
         CLI   ACTION,BINDEX     DIRECT INDEX INTO TABLE                        
         BE    BLDIND                                                           
         CLI   ACTION,BUPDTE     UPDATE RECORD                                  
         BE    UPDREC                                                           
*                                                                               
         LA    RF,*+10           SWITCH TO XA MODE                              
         O     RF,=X'80000000'                                                  
         BSM   0,RF                                                             
*                                                                               
         L     RF,SSBGFTAB                                                      
         CLC   FAIL,0(RF)        HAS PREVIOUS INIT FAILED?                      
         BNE   *+10              YES                                            
         LA    RF,FILEREAD                                                      
         BSM   0,RF              SWITCH TO 24-BIT MODE & READ FILE              
*                                                                               
         CLC   DONE,0(RF)        INITIALISATION OK?                             
         BE    BSRCH             YES                                            
         LA    RF,FILEREAD                                                      
         BSM   0,RF              SWITCH TO 24-BIT MODE & READ FILE              
         SPACE 2                                                                
***********************************************************************         
* BINARY SEARCH FOR REQUESTED KEY                                     *         
***********************************************************************         
         SPACE 1                                                                
BUFF     USING HXFDRD,BBUFF      FORMATTED COPY OF RECORD                       
REQ      USING FDRRECD,R1                                                       
BSRCH    L     R1,BPARM2         P2=(DIRECTORY RECORD REQUESTED)                
         XC    BUFF.HXFDRD(HXKEY),BUFF.HXFDRD  INITIALISE KEY TO FIND           
         MVC   BUFF.HXSYS,REQ.FDRKSYS                                           
         MVC   BUFF.HXPRG,REQ.FDRKPRG                                           
         MVC   BUFF.HXREC,REQ.FDRKREC                                           
         MVC   BUFF.HXNUM,REQ.FDRKNUM                                           
         MVC   BUFF.HXCTRY,REQ.FDRKCTRY                                         
         MVC   BUFF.HXSUB,REQ.FDRKSUB                                           
         MVC   BUFF.HXTEST,REQ.FDRKTEST                                         
         DROP  REQ                                                              
*                                                                               
         XR    R1,R1             R1=LOW                                         
         LH    R2,SSBGFUSD       R2=HIGH                                        
         BCTR  R2,0              HIGH=NUMBER OF RECORDS-1                       
         LTR   R2,R2                                                            
         BP    *+6                                                              
         DC    H'0'              EMPTY ARRAY - YOU'RE FUCKED                    
*                                                                               
BSRCH02  CR    R1,R2             WHILE LOW<=HIGH                                
         BH    NOEXACT           R1 POINTS TO INSERTION POINT                   
*                                                                               
         LA    R3,0(R1,R2)       R3=MID                                         
         SRL   R3,1              MID=(LOW+HIGH)/2                               
         LR    R4,R3                                                            
         MH    R4,=Y(HXFDRL)     INDEX INTO TABLE - R4=K(MID)                   
         A     R4,SSBGFIND                                                      
         CLC   BUFF.HXFDRD(HXKEY),0(R4)  IF KEY == K(MID)                       
         BE    EXACT             RETURN (MID);                                  
*                                                                               
         BH    BSRCH04           IF KEY < K(MID)                                
         LR    R2,R3             HIGH = MID-1;                                  
         BCTR  R2,0                                                             
         B     BSRCH02                                                          
*                                                                               
BSRCH04  LA    R1,1(R3)          ELSE LOW = MID+1;                              
         B     BSRCH02                                                          
         DROP  BUFF                                                             
         SPACE 2                                                                
***********************************************************************         
* NO EXACT MATCH FOUND IN ARRAY                                       *         
***********************************************************************         
         SPACE 1                                                                
NOEXACT  CLI   ACTION,BHIGH      READING HIGH?                                  
         BE    *+10              YES                                            
         LA    RF,FILEREAD                                                      
         BSM   0,RF              SWITCH TO 24-BIT MODE & READ FILE              
*                                                                               
         LR    R4,R1             R1 POINTS TO INSERTION POINT                   
         MH    R4,=Y(HXFDRL)     INDEX INTO TABLE                               
         A     R4,SSBGFIND                                                      
         B     BLDDIR            (R4) AT INSERTION POINT                        
         SPACE 2                                                                
***********************************************************************         
* EXACT MATCH FOUND IN ARRAY                                          *         
***********************************************************************         
         SPACE 1                                                                
EXACT    LR    R1,R3             RETURN (MID)                                   
         CLI   ACTION,BREAD      READ FOR THIS RECORD                           
         BE    BLDDIR                                                           
         CLI   ACTION,BHIGH      READ HIGH                                      
         BE    BLDDIR                                                           
         CLI   ACTION,BSEQ       SEQUENTIAL - NEED NEXT RECORD ON               
         BE    *+6                                                              
         DC    H'0'              ACTION NOT VALID                               
*                                                                               
         LA    R4,HXFDRL(R4)     BUMP (INDEX)                                   
         LA    R1,1(R1)          BUMP (MID)                                     
         B     BLDDIR                                                           
         SPACE 2                                                                
***********************************************************************         
* BUILD DIRECTORY RECORD (& FILE RECORD IF REQUESTED)                 *         
***********************************************************************         
         SPACE 1                                                                
         USING HIFDRD,R2         ELEMENT ARRAY IN R2                            
X        USING FDRRECD,R3        A(BUILD AREA) IN R3                            
         USING HXFDRD,R4         POINTER ARRAY IN R4                            
         SPACE 1                                                                
BLDDIR   XR    R2,R2                                                            
         ICM   R2,7,HXADDR       A(RECORD IN TABLE)                             
         A     R2,SSBGFTAB                                                      
         LA    R2,4(R2)          START OF RECORDS                               
*                                                                               
         L     RF,CALLR1                                                        
         ST    R1,8(RF)          SAVE INDEX INTO RECORD IN P3                   
*                                                                               
         L     R3,BPARM2         BUILD DIRECTORY RECORD                         
         XC    X.FDRRECD(FDRKLEN),X.FDRRECD                                     
         MVI   X.FDRKMIN,FDRKMINQ                                               
         MVI   X.FDRKTYP,FDRKTYPQ                                               
         MVC   X.FDRKSYS,HISYS                                                  
         MVC   X.FDRKPRG,HIPRG                                                  
         MVC   X.FDRKREC,HIREC                                                  
         MVC   X.FDRKNUM,HINUM                                                  
         MVC   X.FDRKCTRY,HICTRY                                                
         MVC   X.FDRKSUB,HISUB                                                  
         MVC   X.FDRKTEST,HITEST                                                
         MVC   X.FDRKSTAT,HISTAT                                                
*                                                                               
         ICM   R3,15,BPARM3      WANT FILE RECORD AS WELL?                      
         BNZ   *+10              YES                                            
         LA    RF,EXITOK                                                        
         BSM   0,RF                                                             
*                                                                               
         XC    X.FDRRECD(FDRFIRST),X.FDRRECD                                    
         MVI   X.FDRKMIN,FDRKMINQ                                               
         MVI   X.FDRKTYP,FDRKTYPQ                                               
         MVC   X.FDRKSYS,HISYS   BUILD DUMMY FILE RECORD                        
         MVC   X.FDRKPRG,HIPRG                                                  
         MVC   X.FDRKREC,HIREC                                                  
         MVC   X.FDRKNUM,HINUM                                                  
         MVC   X.FDRKCTRY,HICTRY                                                
         MVC   X.FDRKSUB,HISUB                                                  
         MVC   X.FDRKTEST,HITEST                                                
         MVC   X.FDRFSTAT,HISTAT                                                
         MVC   X.FDRFLEN,=AL2(FDRFIRST+FDRLNQ)                                  
         MVC   X.FDRRECD+FDRFIRST(FDRLNQ),HIFDR                                 
         MVI   FDRFIRST+FDRLNQ(R3),0  SET E.O.R.                                
*                                                                               
         CLI   HIFLTR,FLTRLQ     ANY FILTER ELEMENT?                            
         BE    *+10              YES                                            
         LA    RF,EXITOK                                                        
         BSM   0,RF                                                             
*                                                                               
         MVC   X.FDRFLEN,=AL2(FDRFIRST+FDRLNQ+FLTRLNQ)                          
         MVC   FDRFIRST+FDRLNQ(FLTRLNQ,R3),HIFLTR                               
         MVI   FDRFIRST+FDRLNQ+FLTRLNQ(R3),0  SET E.O.R.                        
         LA    RF,EXITOK                                                        
         BSM   0,RF                                                             
         DROP  X,R2,R4                                                          
         SPACE 2                                                                
***********************************************************************         
* BUILD INDEX RECORD                                                  *         
***********************************************************************         
         SPACE 1                                                                
BLDIND   OC    SSBGFTAB,SSBGFTAB                                                
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    RF,*+10           SWITCH INTO 31 BIT MODE                        
         O     RF,=X'80000000'                                                  
         BSM   0,RF                                                             
*                                                                               
         ICM   R4,15,BPARM3      INDEX INTO RECORD                              
         MH    R4,=Y(HXFDRL)     INDEX INTO TABLE                               
         A     R4,SSBGFIND                                                      
         USING HXFDRD,R4                                                        
*                                                                               
         ICM   R3,7,HXADDR       A(RECORD)                                      
         A     R3,SSBGFTAB                                                      
         LA    R3,4(R3)          START OF RECORDS                               
         USING HIFDRD,R3                                                        
         L     RF,BPARM2         BUILD FILE RECORD                              
X        USING FDRRECD,RF                                                       
         XC    X.FDRRECD(FDRFIRST),X.FDRRECD                                    
         MVI   X.FDRKMIN,FDRKMINQ                                               
         MVI   X.FDRKTYP,FDRKTYPQ                                               
         MVC   X.FDRKSYS,HISYS                                                  
         MVC   X.FDRKPRG,HIPRG                                                  
         MVC   X.FDRKREC,HIREC                                                  
         MVC   X.FDRKNUM,HINUM                                                  
         MVC   X.FDRKCTRY,HICTRY                                                
         MVC   X.FDRKSUB,HISUB                                                  
         MVC   X.FDRKTEST,HITEST                                                
         MVC   X.FDRFSTAT,HISTAT                                                
         MVC   X.FDRFLEN,=AL2(FDRFIRST+FDRLNQ)                                  
         MVC   X.FDRRECD+FDRFIRST(FDRLNQ),HIFDR                                 
         MVI   FDRFIRST+FDRLNQ(RF),0  SET E.O.R.                                
*                                                                               
         CLI   HIFLTR,FLTRLQ     ANY FILTER ELEMENT?                            
         BE    *+10              YES                                            
         LA    RF,EXITOK                                                        
         BSM   0,RF                                                             
*                                                                               
         MVC   X.FDRFLEN,=AL2(FDRFIRST+FDRLNQ+FLTRLNQ)                          
         MVC   FDRFIRST+FDRLNQ(FLTRLNQ,RF),HIFLTR                               
         MVI   FDRFIRST+FDRLNQ+FLTRLNQ(RF),0  SET E.O.R.                        
         LA    RF,EXITOK                                                        
         BSM   0,RF                                                             
*                                                                               
         DROP  X,R3,R4                                                          
         SPACE 2                                                                
***********************************************************************         
* UPDATE ELEMENTS IN ARRAY                                            *         
***********************************************************************         
         SPACE 1                                                                
REQ      USING FDRRECD,R1                                                       
BUFF     USING HXFDRD,BBUFF      FORMATTED COPY OF RECORD                       
*                                                                               
UPDREC   OC    SSBGFTAB,SSBGFTAB TABLE TO UPDATE?                               
         BZ    EXITOK            NO                                             
*                                                                               
         LA    RF,*+10           SWITCH INTO 31 BIT MODE                        
         O     RF,=X'80000000'                                                  
         BSM   0,RF                                                             
*                                                                               
         L     R1,BPARM2         P2 = A(FILE RECORD)                            
         XC    BUFF.HXFDRD(HXKEY),BUFF.HXFDRD                                   
         MVC   BUFF.HXSYS,REQ.FDRKSYS                                           
         MVC   BUFF.HXPRG,REQ.FDRKPRG                                           
         MVC   BUFF.HXREC,REQ.FDRKREC                                           
         MVC   BUFF.HXNUM,REQ.FDRKNUM                                           
         MVC   BUFF.HXCTRY,REQ.FDRKCTRY                                         
         MVC   BUFF.HXSUB,REQ.FDRKSUB                                           
         MVC   BUFF.HXTEST,REQ.FDRKTEST                                         
         DROP  REQ                                                              
*                                                                               
         XR    R1,R1             R1=LOW                                         
         LH    R2,SSBGFUSD       R2=HIGH                                        
         BCTR  R2,0              HIGH=NUMBER OF RECORDS-1                       
         LTR   R2,R2                                                            
         BP    *+6                                                              
         DC    H'0'              EMPTY ARRAY - YOU'RE FUCKED                    
*                                                                               
UPREC02  CR    R1,R2             WHILE LOW<=HIGH                                
         BNH   *+10              R1 POINTS TO INSERTION POINT                   
         LA    RF,EXITOK         NO MATCH - NOTHING TO UPDATE                   
         BSM   0,RF                                                             
*                                                                               
         LA    R3,0(R1,R2)       R3=MID                                         
         SRL   R3,1              MID=(LOW+HIGH)/2                               
         LR    R4,R3                                                            
         MH    R4,=Y(HXFDRL)     INDEX INTO TABLE - R4=K(MID)                   
         A     R4,SSBGFIND                                                      
         CLC   BUFF.HXFDRD(HXKEY),0(R4)  IF KEY == K(MID)                       
         BE    UPDREC06          RETURN (MID);                                  
*                                                                               
         BH    UPREC04           IF KEY < K(MID)                                
         LR    R2,R3             HIGH = MID-1;                                  
         BCTR  R2,0                                                             
         B     UPREC02                                                          
*                                                                               
UPREC04  LA    R1,1(R3)          ELSE LOW = MID+1;                              
         B     UPREC02                                                          
         DROP  BUFF                                                             
*                                                                               
X        USING FDRRECD,R2                                                       
         USING HIFDRD,R3                                                        
         USING HXFDRD,R4                                                        
UPDREC06 L     R2,BPARM2                                                        
         XR    R3,R3                                                            
         ICM   R3,7,HXADDR       DISP TO THIS ENTRY FROM START OF TABLE         
         A     R3,SSBGFTAB                                                      
         LA    R3,4(R3)          START OF RECORDS                               
*                                                                               
         LA    R5,X.FDRRECD+FDRFIRST                                            
         USING FDRELD,R5                                                        
         XR    RF,RF                                                            
*                                                                               
UPDREC08 CLI   FDREL,0           END OF RECORD                                  
         BNE   *+10              NO                                             
         LA    RF,EXITOK                                                        
         BSM   0,RF                                                             
*                                                                               
         CLI   FDREL,FDRELQ      FIELD ELEMENT?                                 
         BNE   *+10              NO                                             
         MVC   HIFDR(FDRLNQ),FDRELD                                             
*                                                                               
         CLI   FDREL,FLTRLQ      FILTER ELEMENT?                                
         BNE   *+10              NO                                             
         MVC   HIFLTR(FLTRLNQ),FDRELD                                           
*                                                                               
         IC    RF,FDRLN                                                         
         LA    R5,0(RF,R5)       NEXT ELEMENT                                   
         B     UPDREC08                                                         
         DROP  X,R3,R4                                                          
         SPACE 2                                                                
***********************************************************************         
* READ RECORD FROM FILE                                               *         
***********************************************************************         
         SPACE 1                                                                
FILEREAD CLI   ACTION,BUPDTE     TEST OR OFFLINE DO NOT UPDATE                  
         BE    EXITOK                                                           
*                                                                               
         L     R2,BPARM2         P2 = A(DIRECTORY RECORD)                       
         USING FDRRECD,R2                                                       
         LA    RF,DMREAD                                                        
         CLI   ACTION,BREAD      READING RECORD?                                
         BE    *+8               YES                                            
         LA    RF,DMRDHI                                                        
*                                                                               
         GOTOX ADATAMGR,PLIST,(RF),GENDIR,(R2),(R2)                             
         BNE   EXITL                                                            
*                                                                               
         CLI   ACTION,BSEQ       READ SEQUENTIAL?                               
         BNE   READ02            NO                                             
         GOTOX ADATAMGR,PLIST,DMRSEQ,GENDIR,(R2),(R2)                           
         BNE   EXITL                                                            
*                                                                               
READ02   ICM   R3,15,BPARM3      FILE RECORD WANTED AS WELL?                    
         BZ    EXITOK                                                           
*                                                                               
         GOTOX ADATAMGR,PLIST,GETREC,GENFILE,FDRKDA,(R3),DMWORK                 
         BE    EXITOK                                                           
         DC    H'0'                                                             
         SPACE 2                                                                
***********************************************************************         
* EXIT POINTS  - ASSUME STILL IN 31 BIT MODE                          *         
***********************************************************************         
         SPACE 1                                                                
EXITL    DS    0H                                                               
         PROT  ON                                                               
         CLI   *,FF              SET CC LOW                                     
         B     EXIT                                                             
*                                                                               
EXITOK   DS    0H                                                               
         PROT  ON                                                               
         CR    RB,RB             SET CC EQUAL                                   
         B     EXIT                                                             
*                                                                               
EXIT     DS    0H                                                               
         XMOD1                                                                  
         SPACE 2                                                                
***********************************************************************         
* LITERALS AND CONSTANTS                                              *         
***********************************************************************         
         SPACE 1                                                                
         LTORG                                                                  
         SPACE 2                                                                
MAXNUM   EQU   5000              MAXIMUM NUMBER OF RECORDS IN TABLE             
BREAD    EQU   1                 EQUATED COMMANDS                               
BHIGH    EQU   2                                                                
BSEQ     EQU   3                                                                
BINDEX   EQU   4                                                                
BUPDTE   EQU   5                                                                
FF       EQU   X'FF'                                                            
GENDIR   DC    CL8'GENDIR'                                                      
GENFILE  DC    CL8'GENFILE'                                                     
DMRDHI   DC    CL8'DMRDHI'                                                      
DMRSEQ   DC    CL8'DMRSEQ'                                                      
DMREAD   DC    CL8'DMREAD'                                                      
GETREC   DC    CL8'GETREC'                                                      
LOCK     DC    CL4'LOCK'                                                        
FAIL     DC    CL4'FAIL'                                                        
DONE     DC    CL4'DONE'                                                        
FF4      DC    XL4'FFFFFFFF'                                                    
         SPACE 2                                                                
***********************************************************************         
* DSECTS                                                              *         
***********************************************************************         
         SPACE 1                                                                
HXFDRD   DSECT                   FORMAT OF FDREL INDEX BLOCK                    
HXSYS    DS    XL(L'FDRKSYS)                                                    
HXPRG    DS    XL(L'FDRKPRG)                                                    
HXREC    DS    XL(L'FDRKREC)                                                    
HXNUM    DS    XL(L'FDRKNUM)                                                    
HXCTRY   DS    XL(L'FDRKCTRY)                                                   
HXSUB    DS    XL(L'FDRKSUB)                                                    
HXTEST   DS    XL(L'FDRKTEST)                                                   
HXKEY    EQU   *-HXFDRD                                                         
HXADDR   DS    XL3               DISP OF RECORD INTO HIFDRD                     
HXFDRL   EQU   *-HXFDRD                                                         
*                                                                               
HIFDRD   DSECT                   FORMAT OF FDREL IN XA STORAGE                  
HISYS    DS    XL(L'FDRKSYS)                                                    
HIPRG    DS    XL(L'FDRKPRG)                                                    
HIREC    DS    XL(L'FDRKREC)                                                    
HINUM    DS    XL(L'FDRKNUM)                                                    
HICTRY   DS    XL(L'FDRKCTRY)                                                   
HISUB    DS    XL(L'FDRKSUB)                                                    
HITEST   DS    XL(L'FDRKTEST)                                                   
HIKEY    EQU   *-HIFDRD                                                         
HISTAT   DS    XL(L'FDRKSTAT)                                                   
HIFDR    DS    XL(FDRLNQ)                                                       
HIFLTR   DS    XL(FLTRLNQ)                                                      
HIFDRL   EQU   *-HIFDRD                                                         
*                                                                               
BWORKD   DSECT                                                                  
CALLR1   DS    A                 A(CALLER'S R1)                                 
BPARMS   DS    0XL24             CALLERS SAVED PARAMS                           
BPARM1   DS    A                                                                
         ORG   *-1                                                              
ACTION   DS    XL1               REQUESTED ACTION                               
BPARM2   DS    A                 A(DIRECTORY RECORD)                            
BPARM3   DS    A                 0 OR A(FILE RECORD)                            
BPARM4   DS    A                                                                
BPARM5   DS    A                                                                
BPARM6   DS    A                                                                
*                                                                               
ADATAMGR DS    A                 DATA MANAGER FROM SYSFAC                       
ASSB     DS    A                 SSB FROM SYSFAC                                
BBUFF    DS    XL(HIKEY)                                                        
KEY      DS    XL(FDRKLEN)                                                      
DMWORK   DS    12D                                                              
PLIST    DS    XL24                                                             
BWORKL   EQU   *-BWORKD                                                         
         SPACE 2                                                                
***********************************************************************         
* INCLUDED BOOKS                                                      *         
***********************************************************************         
         SPACE 1                                                                
*GEGENSCR - HOLDS DSECT FOR FIELD RECORDS                                       
         PRINT OFF                                                              
       ++INCLUDE GEGENSCR                                                       
         PRINT ON                                                               
*FASYSFAC                                                                       
         PRINT OFF                                                              
       ++INCLUDE FASYSFAC                                                       
         PRINT ON                                                               
*FASSB                                                                          
         PRINT OFF                                                              
       ++INCLUDE FASSB                                                          
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'010FAGETFLD  07/18/97'                                      
         END                                                                    
