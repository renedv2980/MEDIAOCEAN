*          DATA SET DDMERGER   AT LEVEL 019 AS OF 07/31/86                      
*CATALP MERGER                                                                  
         TITLE 'MERGER - MULTILPLE SORT FACILITY'                               
MERGER   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 150,**MRGE**                                                     
         USING MERGED,RC                                                        
         LR    R9,R1                                                            
         L     R2,0(R1)                                                         
         CLC   0(3,R2),=C'GET'     SELECT ACTION                                
         BE    GET                                                              
         CLC   0(3,R2),=C'PUT'                                                  
         BE    PUT                                                              
         CLC   0(3,R2),=C'END'                                                  
         BE    END                                                              
         CLC   0(4,R2),=C'SORT'                                                 
         BE    SORT                                                             
         DC    H'0'                                                             
         EJECT                                                                  
*              ROUTINE TO INTERCEPT INITIAL CALL                                
         SPACE 3                                                                
SORT     LM    R2,R5,0(R9)                                                      
         MVC   SORTCARD,0(R2)      SAVE USERS SORTCARD                          
         MVC   RECCARD,0(R3)                  RECORD CARD                       
         ST    R4,SORTER                      A(SORTER)                         
         MVI   DYNANO,C'Y'                                                      
         ST    R5,DYNALLOC                                                      
         LTR   R5,R5                                                            
         BNZ   DYNAYES                                                          
         MVI   DYNANO,C'N'         NO DYNALLOC                                  
DYNAYES  MVI   SORTFRST,C'Y'                                                    
         MVC   SORTTYPE,RECCARD+12                                              
         CLI   SORTTYPE,C'F'                                                    
         BNE   XIT                                                              
         SR    R1,R1               PICK UP FIXED RECORD LENGTH                  
         CLI   RECCARD+22,C' '                                                  
         BE    SORT2                                                            
         LA    R1,1                                                             
         CLI   RECCARD+23,C' '                                                  
         BE    SORT2                                                            
         LA    R1,2                                                             
         SPACE 1                                                                
SORT2    EX    R1,*+8                                                           
         B     *+10                                                             
         PACK  DUB,RECCARD+21(0)                                                
         CVB   R1,DUB                                                           
         STH   R1,RECLEN                                                        
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO INTERCEPT PUTS                                        
         SPACE 3                                                                
PUT      L     R2,4(R9)            A(RECORD)                                    
         MVC   FILENUM,0(R2)       FILENUMBER ASSUMED TO BE FIRST               
         CLI   SORTTYPE,C'F'       LOGICAL BYTE OF RECORD                       
         BE    *+10                                                             
         MVC   FILENUM,4(R2)                                                    
         CLI   4(R9),0             UNLESS OVERRIDE FILE NUMBER PASSED           
         BE    *+10                       IN PARA 2                             
         MVC   FILENUM,4(R9)                                                    
         ZIC   R1,FILENUM          MUST BE 1-32                                 
         CLI   FILENUM,32                                                       
         BNH   *+6                                                              
         DC    H'0'                                                             
         LTR   R1,R1                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         CVD   R1,DBL                                                           
         UNPK  DBL(2),DBL+6(2)                                                  
         MVZ   DBL+1(1),=X'F0'                                                  
         BCTR  R1,0                ADDRESS TO A(DCB) AND COUNT                  
         SLL   R1,3                        FOR THIS FILE NUMBER                 
         LA    R3,DCBDIR(R1)                                                    
         L     R1,4(R3)            ADD 1 TO RECORD COUNT                        
         LA    R1,1(R1)                                                         
         ST    R1,4(R3)                                                         
         CLI   FILENUM,1           FOR FILE 1 PUT DIRECT TO SORT                
         BNE   PUT4                                                             
         CH    R1,=H'1'            (MAY NEED TO INITIALIZE)                     
         BNE   PUT2                                                             
         GOTO1 SORTER,PARAS,SORTCARD,RECCARD                                    
         SPACE 1                                                                
PUT2     GOTO1 SORTER,PARAS,=C'PUT',(R2)                                        
         B     XIT                                                              
         SPACE 1                                                                
PUT4     L     R4,0(R3)            FILES 2-32 GET SAVED                         
         CH    R1,=H'1'            (MAY NEED TO BE OPENED)                      
         BNE   PUT6                                                             
         MVC   DDPARM+5(2),DBL                                                  
         CLI   DYNANO,C'N'         DYNALLOC BEING USED                          
         BE    OPENX                                                            
         GOTO1 DYNALLOC,PARAS,(X'80',DDPARM),(X'80',SPACPARM)                   
OPENX    OPEN  ((R4),(OUTPUT))                                                  
         SPACE 1                                                                
PUT6     CLI   SORTTYPE,C'F'       CHANGED FIXED TO VARIABLE                    
         BNE   PUT8                                                             
         LH    R5,RECLEN                                                        
         MOVE  (IO+4,(R5)),(R2)                                                 
         LA    R5,4(R5)                                                         
         SLL   R5,16                                                            
         ST    R5,IO                                                            
         LA    R2,IO                                                            
         SPACE 1                                                                
PUT8     PUT   (R4),(R2)                                                        
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO INTERCEPT GETS                                        
         SPACE 3                                                                
GET      CLI   SORTFRST,C'Y'       IS THIS THE FIRST GET                        
         BNE   GET2                                                             
         MVI   SORTFRST,C'N'                                                    
         MVI   FILENUM,1                                                        
         OC    DCBDIR+4(4),DCBDIR+4    ON FIRST GET,                            
         BZ    GET4                DID WE PUT ANY RECORDS DIRECTLY              
         SPACE 1                                                                
GET2     LR    R1,R9               GO TO SORTER WITH USERS PARAS                
         GOTO1 SORTER                                                           
         OC    4(4,R1),4(R1)       EOF?                                         
         BNZ   XIT                 IF NOT, USER HAS RECORD NOW                  
         GOTO1 SORTER,PARAS,=C'END'     TIDY UP FOR MEL                         
         SPACE 1                                                                
GET4     ZIC   R2,FILENUM          PRESENT FILE NUMBER                          
         LR    R3,R2                                                            
         BCTR  R3,0                                                             
         SLL   R3,3                                                             
         LA    R3,DCBDIR(R3)                                                    
         SPACE 1                                                                
GET6     CLI   FILENUM,32          NO MORE FILES LEFT                           
         BL    GET8                                                             
         XC    4(4,R9),4(R9)       PASS USER BACK EOF                           
         B     XIT                                                              
         SPACE 1                                                                
GET8     LA    R2,1(R2)            FIND A FILE WITH SOME RECORDS                
         LA    R3,8(R3)                                                         
         STC   R2,FILENUM                                                       
         LM    R4,R5,0(R3)                                                      
         LTR   R5,R5                                                            
         BZ    GET6                                                             
         XC    4(4,R3),4(R3)                                                    
         CLOSE ((R4))                                                           
         OPEN  ((R4),(INPUT))                                                   
         GOTO1 SORTER,PARAS,SORTCARD,RECCARD                                    
         LA    R6,IO                                                            
         CLI   SORTTYPE,C'F'                                                    
         BNE   GET10                                                            
         LA    R6,4(R6)                                                         
         SPACE 1                                                                
         PRINT GEN                                                              
GET10    GET   (R4),IO                       PUT THIS FILE                      
         PRINT NOGEN                                                            
         GOTO1 SORTER,PARAS,=C'PUT',(R6)     TO SORT                            
         BCT   R5,GET10                                                         
         CLOSE ((R4))                                                           
         B     GET2                          GO BACK TO GET FIRST REC.          
         SPACE 1                                                                
END      DS    0H                                                               
         LA    R2,32               CLEAR RECORD CTS FOR NEXT REQUEST            
         LA    R1,DCBDIR                                                        
END1     XC    4(4,R1),4(R1)                                                    
         LA    R1,8(R1)                                                         
         BCT   R2,END1                                                          
         SPACE 1                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
*              DCB DIRECTORY                                                    
         SPACE 3                                                                
DCBDIR   DS    0D                  EACH ENTRY HAS FOLLOWING DATA                
*                                  BYTES 01-04 A(DCB)                           
*                                  BYTES 05-08 F(NUMBER OF RECORDS)             
         SPACE 1                                                                
         DC    A(MERGE01),F'0'                                                  
         DC    A(MERGE02),F'0'                                                  
         DC    A(MERGE03),F'0'                                                  
         DC    A(MERGE04),F'0'                                                  
         DC    A(MERGE05),F'0'                                                  
         DC    A(MERGE06),F'0'                                                  
         DC    A(MERGE07),F'0'                                                  
         DC    A(MERGE08),F'0'                                                  
         DC    A(MERGE09),F'0'                                                  
         DC    A(MERGE10),F'0'                                                  
         DC    A(MERGE11),F'0'                                                  
         DC    A(MERGE12),F'0'                                                  
         DC    A(MERGE13),F'0'                                                  
         DC    A(MERGE14),F'0'                                                  
         DC    A(MERGE15),F'0'                                                  
         DC    A(MERGE16),F'0'                                                  
         DC    A(MERGE17),F'0'                                                  
         DC    A(MERGE18),F'0'                                                  
         DC    A(MERGE19),F'0'                                                  
         DC    A(MERGE20),F'0'                                                  
         DC    A(MERGE21),F'0'                                                  
         DC    A(MERGE22),F'0'                                                  
         DC    A(MERGE23),F'0'                                                  
         DC    A(MERGE24),F'0'                                                  
         DC    A(MERGE25),F'0'                                                  
         DC    A(MERGE26),F'0'                                                  
         DC    A(MERGE27),F'0'                                                  
         DC    A(MERGE28),F'0'                                                  
         DC    A(MERGE29),F'0'                                                  
         DC    A(MERGE30),F'0'                                                  
         DC    A(MERGE31),F'0'                                                  
         DC    A(MERGE32),F'0'                                                  
         EJECT                                                                  
*              CONSTANTS, LTORG ETC                                             
         SPACE 3                                                                
DYNANO   DC    C'Y'                                                             
RELOC    DC    A(*)                                                             
RELO     DS    A                                                                
SORTTYPE DC    C'F'                                                             
DDPARM   DC    CL8'MERGEXX'                                                     
SPACPARM DC    XL6'001000001000'                                                
DBL      DS    D                                                                
ASORTC   DS    V                                                                
SORTER   DS    V                                                                
DYNALLOC DS    V                                                                
SORTCARD DS    CL80                                                             
RECCARD  DS    CL80                                                             
RECLEN   DS    H                                                                
FILENUM  DS    CL1                                                              
SORTFRST DS    CL1                                                              
         LTORG                                                                  
         EJECT                                                                  
*              DCBS                                                             
         SPACE 3                                                                
MERGE01  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE01                                        
MERGE02  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE02                                        
MERGE03  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE03                                        
MERGE04  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE04                                        
MERGE05  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE05                                        
MERGE06  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE06                                        
MERGE07  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE07                                        
MERGE08  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE08                                        
MERGE09  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE09                                        
MERGE10  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE10                                        
MERGE11  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE11                                        
MERGE12  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE12                                        
MERGE13  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE13                                        
MERGE14  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE14                                        
MERGE15  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE15                                        
MERGE16  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE16                                        
MERGE17  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE17                                        
MERGE18  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE18                                        
MERGE19  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE19                                        
MERGE20  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE20                                        
MERGE21  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE21                                        
MERGE22  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE22                                        
MERGE23  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE23                                        
MERGE24  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE24                                        
MERGE25  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE25                                        
MERGE26  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE26                                        
MERGE27  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE27                                        
MERGE28  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE28                                        
MERGE29  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE29                                        
MERGE30  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE30                                        
MERGE31  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE31                                        
MERGE32  DCB   RECFM=VB,BLKSIZE=4000,DSORG=PS,MACRF=(PM,GM),           X        
               LRECL=2000,DDNAME=MERGE32                                        
         SPACE 3                                                                
*              DSECT FOR ODD STORAGE                                            
         SPACE 3                                                                
MERGED   DSECT                                                                  
DUB      DS    D                                                                
WORK     DS    CL32                                                             
PARAS    DS    6F                                                               
IO       DS    2000C                                                            
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'019DDMERGER  07/31/86'                                      
         END                                                                    
