*          DATA SET PUBOUT     AT LEVEL 009 AS OF 04/09/96                      
*CATALP PUBOUT                                                                  
         TITLE 'PUBOUT - DISPLAY PUB NAME'                                      
PUBOUT   CSECT                                                                  
         SPACE 2                                                                
* PARAM1  BYTE  0                                                               
*         BYTES 1-3      A(PUBREC)                                              
* PARAM2  BYTE  0                                                               
*         BYTES 1-3      A(LTLREC)                                              
* PARAM3  BYTE  0        X'00' = NAME ONLY                                      
*                        X'01' = NAME + CODE(S)                                 
*                        X'02' = NAME + EDIT                                    
*                        X'03' = ALL                                            
*         BYTES 1-3      A(OUTPUT)  - P AND P+132                               
* PARAM4  BYTE  0                                                               
*         BYTES 1-3      A(CLIENT CODE) - FOR CLIENT VENDOR NUMBER              
* PARAM5  BYTE  0        PAGYPROF+12                                            
*         BYTES 1-3      V(PUBEDIT)                                             
         SPACE 2                                                                
*        FORMAT OF DISPLAY IS-     ST, CITY  NAME  (1-234-5678,M) -MORN         
*                                      ZONE NAME   (ABCDEFGHIJKL)               
*                                                                               
*                          OR-     MAGAZINE NAME  (1-234-5678)                  
*                                  ZONE NAME      (ABCDEFGHIJKL)                
*                                                                               
         EJECT                                                                  
         NMOD1 10,PUBOUT                                                        
         SPACE 2                                                                
         USING WORKD,RC                                                         
         ST    R1,SAVR1                                                         
         L     RA,0(R1)                                                         
         USING PUBRECD,RA                                                       
         L     R4,8(R1)            A(OUTPUT)                                    
         MVC   BYTE,8(R1)          CONTROL BYTE                                 
         MVC   PUBEDIT,16(R1)                                                   
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(19),SPACES                                              
         OC    PUBNAME,SPACES                                                   
         OC    PUBZNAME,SPACES                                                  
         SPACE 2                                                                
         CLI   PUBKMED,C'N'                                                     
         BE    PO2                                                              
         MVC   0(20,R4),PUBNAME         MAG                                     
         MVC   132(20,R4),PUBZNAME                                              
         B     PO8                                                              
         SPACE 2                                                                
PO2      DS    0H                       NEWS                                    
         LA    R5,132(R4)                                                       
         CLI   PUBCITY,C' '                                                     
         BNH   PO4                                                              
         CLI   PUBSTATE,C' '                                                    
         BNH   PO4                                                              
         LA    R5,4(R5)                                                         
         MVC   0(2,R4),PUBSTATE                                                 
         MVI   2(R4),C','                                                       
         MVC   4(16,R4),PUBCITY                                                 
         LA    R6,4(R4)                                                         
         LA    R4,20(R4)                                                        
         CLI   0(R4),C' '                                                       
         BH    *+8                                                              
         BCT   R4,*-8                                                           
         LR    R7,R4                                                            
         LA    R4,3(R4)                                                         
         SR    R7,R6                                                            
         EX    R7,*+8                                                           
         B     *+10                                                             
         CLC   PUBCITY(0),PUBNAME                                               
         BNE   PO4                                                              
         LR    R4,R6                                                            
PO4      DS    0H                                                               
         MVC   0(20,R4),PUBNAME                                                 
         MVC   0(20,R5),PUBZNAME                                                
PO8      DS    0H                                                               
         LA    R4,20(R4)                                                        
PO8A     DS    0H                                                               
         CLI   0(R4),C' '                                                       
         BH    PO8B                                                             
         CLI   132(R4),C' '                                                     
         BH    PO8B                                                             
         BCT   R4,PO8A                                                          
PO8B     DS    0H                                                               
         TM    BYTE,X'01'                                                       
         BZ    PO12                NO PUB CODE                                  
         LA    R5,132(R4)                                                       
         IC    R2,16(R1)                                                        
         GOTO1 PUBEDIT,DMCB,((R2),PUBKPUB),4(R4)                                
*                                                                               
         MVI   3(R4),C'('                                                       
         LA    R4,21(R4)                                                        
         CLI   0(R4),C' '                                                       
         BH    *+8                                                              
         BCT   R4,*-8                                                           
         MVI   1(R4),C')'                                                       
*                                  TRY CLT VEN NUM                              
         LA    R2,PUBREC+33                                                     
         L     R1,SAVR1                                                         
         L     R7,12(R1)                                                        
         LTR   R7,R7                                                            
         BZ    PO12                NO CLIENT                                    
PO9      DS    0H                                                               
         CLI   0(R2),X'14'                                                      
         BE    PO10                                                             
PO9B     DS    0H                                                               
         SR    R0,R0                                                            
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         CLI   0(R2),0                                                          
         BE    PO12                                                             
         B     PO9                                                              
PO10     DS    0H                                                               
         USING PUBREPEL,R2                                                      
         CLC   PUBRPOFF,0(R7)                                                   
         BNE   PO9B                                                             
         OC    PUBCVEN,PUBCVEN                                                  
         BZ    PO12                                                             
         CLI   PUBCVEN,C' '                                                     
         BNE   *+14                                                             
         CLC   PUBCVEN+1(11),PUBCVEN                                            
         BE    PO12                                                             
         CLI   PUBCVEN,X'FF'                                                    
         BE    PO12                                                             
         CLI   PUBCVEN,X'99'                                                    
         BH    PO11                                                             
*                                                                               
         IC    R3,16(R1)                                                        
         GOTO1 PUBEDIT,DMCB,((R3),PUBCVEN),4(R5)                                
         B     PO11B                                                            
PO11     DS    0H                                                               
         MVC   4(12,R5),PUBCVEN                                                 
PO11B    DS    0H                                                               
         MVI   3(R5),C'('                                                       
         LA    R5,21(R5)                                                        
         CLI   0(R5),C' '                                                       
         BH    *+8                                                              
         BCT   R5,*-8                                                           
         MVI   1(R5),C')'                                                       
PO12     DS    0H                                                               
         TM    BYTE,X'02'                                                       
         BZ    PO14                                                             
         CLI   PUBKPUB+5,0                                                      
         BE    PO14                                                             
         GOTO1 PUBEDIT,DMCB,PUBKPUB,(C'E',4(R4))                                
*                                                                               
         MVI   3(R4),C'-'                                                       
PO14     DS    0H                                                               
         XIT1                                                                   
         SPACE 3                                                                
         LTORG                                                                  
         SPACE 3                                                                
WORKD    DSECT                                                                  
SAVR1    DS    F                                                                
PUBEDIT  DS    V                                                                
DMCB     DS    6F                                                               
BYTE     DS    X                                                                
SPACES   DS    CL20                                                             
         SPACE 3                                                                
PUBRECD  DSECT                                                                  
       ++INCLUDE PUBREC                                                         
         SPACE 2                                                                
*                                                                               
       ++INCLUDE PUBNAMEL                                                       
         SPACE 2                                                                
PUBREPLD DSECT                                                                  
       ++INCLUDE PUBREPEL                                                       
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'009PUBOUT    04/09/96'                                      
         END                                                                    
