*          DATA SET DDTEMPTRC  AT LEVEL 047 AS OF 05/01/02                      
*CATALP TEMPTRC                                                                 
         SPACE 1                                                                
*===================================================================*           
*    THIS MODULE ALLOWS THE TEMPORARY LOGGING TO ADRFILE OF EVENTS  *           
*    SUCH AS THE ALLOCATION OF TEMPEST, FOR DUBUGGING PURPOSES.     *           
*===================================================================*           
TEMPTRC  TITLE 'TEMPORARY EVENT LOGGING'                                        
         PRINT NOGEN                                                            
TEMPTRC  CSECT                                                                  
*                                                                               
         ENTRY TMPBUFF                                                          
*                                                                               
         NMOD1 TMPWORKX-TMPWORKD,**TTRC**,RA,CLEAR=YES                          
         USING TMPWORKD,RC                                                      
         ST    RD,SAVERD                                                        
         LR    R2,R1               R2=PARMS                                     
         USING PARMS,R2                                                         
         L     R9,=V(SYSFAC)       R9=A(SYSTEM FACILITY LIST)                   
         USING SYSFACD,R9                                                       
         L     R4,=A(TMPBUFF)                                                   
*                                                                               
         C     R0,=C'PARM'         TEST FOR SPECIAL PARM TRACE                  
         BNE   TRACE000                                                         
         LA    R2,DUMMY                                                         
         MVC   PCODE,=F'5'         PARMS ARE CODE 5                             
         BAS   RE,PARMEXT                                                       
*        B     TRACEX                                                           
*                                                                               
TRACE000 OC    PCODE,PCODE         TEST FOR SPECIAL CALL                        
         BNZ   TRACE001                                                         
         ST    R4,PCODE            RETURN A(BUFFER) IN P1                       
         LA    R1,TMPLTAB                                                       
         ST    R1,PDATA            RETURN A(TABLE) IN P2                        
         B     TRACEX                                                           
*                                                                               
TRACE001 CLC   0(5,R4),=C'*TTRC'   HAVE WE GOT A BUFFER YET                     
         BE    TRACE010                                                         
*                                                                               
         XC    LASTDA,LASTDA       FIRST BUFFER OF THE DAY                      
         BAS   RE,INITBUFF                                                      
*                                                                               
TRACE010 LA    R1,TMPLTAB          FIND TABLE ENTRY                             
TRACE020 CLC   PCODE+2(2),0(R1)                                                 
         BE    TRACE030                                                         
         LA    R1,L'TMPLTAB(R1)                                                 
         CLC   0(2,R1),=AL2(EOTAB) EXIT IF NOT FOUND                            
         BE    TRACEX                                                           
         BNE   TRACE020                                                         
TRACE030 CLI   3(R1),C'Y'          EXIT IF NOT ENABLED                          
         BNE   TRACEX                                                           
         L     RF,PDATA            LEN IS AT START OF DATA                      
         SR    R1,R1                                                            
         IC    R1,0(RF)                                                         
         LA    R1,16(R1)           PLUS 16 FOR HEADER                           
         STC   R1,LENGTH                                                        
*                                                                               
         L     R0,TMPNEXT          HOW MUCH SPACE LEFT IN BUFFER                
         LA    R1,TMPBUFFX                                                      
         SR    R1,R0               R1=AVAILABLE SPACE                           
         SR    R0,R0                                                            
         IC    R0,LENGTH                                                        
         CR    R1,R0               DO WE HAVE ROOM FOR THIS                     
         BH    TRACE040                                                         
*                                                                               
         BAS   RE,PUTBUFF          IF NOT PUT THIS BUFFER                       
         BAS   RE,INITBUFF         AND INIT A NEW ONE                           
*                                                                               
TRACE040 L     R3,VSSB             PICK UP ACTIVE TASK                          
         ICM   R3,15,SSBTKADR-SSBD(R3)                                          
         BZ    TRACEX                                                           
         USING TCBD,R3                                                          
*                                                                               
         GOTO1 VTICTOC,DUB,C'TGET' GET CURRENT TIME                             
*        TIME  TU                                                               
*                                                                               
         L     R4,TMPNEXT          BUILD RECORD                                 
         USING TMPLRECD,R4                                                      
         MVI   TMPLTYPE,C'T'       "T" TERMINAL                                 
         MVC   TMPLLEN,LENGTH      LENGTH                                       
         MVC   TMPLLUID,TCBSYM     LUID                                         
         MVC   TMPLTIME,DUB        TIME NOW                                     
         MVC   TMPLCODE,PCODE+2    CODE FROM PARMS                              
*                                                                               
         L     RF,PDATA            MOVE IN DATA FOR LENGTH-FIXED                
         SR    R1,R1                                                            
         IC    R1,LENGTH                                                        
         SH    R1,=Y(L'TMPLREC+1)  FIXED REC SECTION LENGTH                     
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   TMPLDATA(0),0(RF)                                                
*                                                                               
         SR    R1,R1               SET NEW BUFFER POINTER                       
         IC    R1,LENGTH                                                        
         A     R1,TMPNEXT                                                       
         ST    R1,TMPNEXT                                                       
         LA    R0,TMPBUFFX         TEST FOR END OF BUFFER                       
         CR    R1,R0                                                            
         BL    TRACEX                                                           
         BAS   RE,PUTBUFF          WRITE BUFFER IF FULL                         
         BAS   RE,INITBUFF         AND INIT A NEW ONE                           
*                                                                               
TRACEX   XMOD1                                                                  
         EJECT                                                                  
**********************************************************************          
* BUFFER SUBROUTINES                                                 *          
**********************************************************************          
         SPACE 1                                                                
PUTBUFF  NTR1                                                                   
         L     RF,VLOGGER                                                       
         L     R1,=A(TMPBUFF)      NOTE R1 POINTS TO BUFFER                     
         BASR  RE,RF               LOGGER INSERTS DISK ADDRESS AT +5            
         MVC   LASTDA,5(R1)        SAVE THE PREVIOUS RECORD ADDRESS             
         B     XIT1                                                             
         SPACE 2                                                                
INITBUFF NTR1                                                                   
         L     R4,=A(TMPBUFF)      SET 'TO' ADDRESS                             
         L     R5,=A(TMPBUFFX-TMPBUFF) SET 'TO' LENGTH                          
         SR    RE,RE               SET 'FROM' ADDRESS = 0                       
         SR    RF,RF               SET 'FROM' LEN = 0                           
         MVCL  R4,RE                                                            
*                                                                               
         L     R4,=A(TMPBUFF)                                                   
         MVC   0(5,R4),=C'*TTRC'   SET BUFFER ID                                
         MVC   5(4,R4),LASTDA      SET D/A OF PREVIOUS                          
         LA    R4,10(R4)                                                        
         ST    R4,TMPNEXT          SET NEXT SLOT ADDRESS                        
*                                                                               
XIT1     XIT1                                                                   
         EJECT                                                                  
**********************************************************************          
* PARAM TRACE                                                        *          
**********************************************************************          
         EJECT                                                                  
PARMEXT  NTR1                                                                   
         LA    R5,PARMWORK         BUILT INTO PARMWORK                          
         ST    R5,PDATA                                                         
         MVI   0(R5),133                                                        
         L     R1,SAVERD           GOTO MAIN REGS                               
         L     R1,4(R1)            GOTO CALLERS REGS                            
         L     R1,4(R1)            GOTO HIS CALLERS REGS                        
         L     RE,12(R1)           RE=CALLERS RE                                
         L     RF,64(R1)           RF=CALLERS RB                                
         CLC   4(2,RF),=X'05B0'                                                 
         BNE   *+14                                                             
         MVC   1(8,R5),22(RF)      SAVE EYECATCHER                              
         B     PARMX05                                                          
*                                                                               
         MVC   1(4,R5),=C'BASE'                                                 
         ST    RF,5(R5)                                                         
*                                                                               
PARMX05  SR    RE,RF                                                            
         STCM  RE,15,9(R5)         SAVE OFFSET                                  
*                                                                               
         L     R1,24(R1)           R1=PARMS                                     
         MVC   13(24,R5),0(R1)     SAVE PARMS                                   
*                                                                               
         L     R3,VSSB                                                          
         LA    RE,37(R5)                                                        
         LA    R0,6                                                             
PARMX10  L     RF,0(R1)            SAVE CONTENTS                                
         C     RF,SSBLOADR-SSBD(R3)                                             
         BNH   PARMX20                                                          
         C     RF,SSBHIADR-SSBD(R3)                                             
         BNL   PARMX20                                                          
         MVC   0(16,RE),0(RF)                                                   
PARMX20  LA    RE,16(RE)                                                        
         LA    R1,4(R1)                                                         
         BCT   R0,PARMX10                                                       
                                                                                
PARMEXTX B     XIT1                                                             
         SPACE 1                                                                
EOTAB    EQU   X'FFFF'                                                          
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        CODE,LEN,ENABLED,NAME                                                  
*                                                                               
         DS    0F                                                               
TMPLTAB  DS    0CL12                                                            
         DC    AL2(TLTEMPES),AL1(0),C'N',C'TEMPEST '                            
         DC    AL2(TIOTRC),AL1(0),C'N',C'IO_TRACE'                              
         DC    AL2(TWAITTRC),AL1(0),C'Y',C'WAIT_TRC'                            
         DC    AL2(TPARMEXT),AL1(0),C'N',C'PARAMS  '                            
         DC    AL2(TSOONTRC),AL1(0),C'Y',C'SOONS   '                            
         DC    AL2(EOTAB)                                                       
         EJECT                                                                  
         DS    0D                                                               
TMPNEXT  DC    A(TMPBUFF)          BUFF-13=POINTER TO NEXT BUFFER SLOT          
         DC    CL8'*TMPBUF*'       BUFF-08=LABEL                                
TMPBUFF  DS    6400C                                                            
TMPBUFFX EQU   *-1                                                              
         EJECT                                                                  
TMPWORKD DSECT                                                                  
SAVERD   DS    A                                                                
SAVERE   DS    A                                                                
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
FLAG     DS    X                                                                
WORK     DS    XL32                                                             
LASTDA   DS    F                                                                
LENGTH   DS    X                                                                
DUMMY    DS    6F                                                               
PARMWORK DS    XL255                                                            
TMPWORKX EQU   *                                                                
         SPACE 2                                                                
PARMS    DSECT                                                                  
PCODE    DS    F                                                                
PDATA    DS    A                                                                
         SPACE 2                                                                
*TEMPREC                                                                        
       ++INCLUDE DDTEMPREC                                                      
*FADSECTS                                                                       
       ++INCLUDE FADSECTS                                                       
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'047DDTEMPTRC 05/01/02'                                      
         END                                                                    
