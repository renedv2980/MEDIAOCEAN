*          DATA SET NEGETNUN   AT LEVEL 011 AS OF 02/09/11                      
*CATALP GETNUN                                                                  
T00A32   TITLE '-   GETNUN - MODULE TO GET NETWORK UNIVERSES'                   
*********************************************************************           
* P1     BYTES 1-3     A(GUVBLOCK)                                  *           
*                                                                   *           
* AFTER MODULE - GUVERROR SHOULD BE TESTED FOR ERROR                *           
* AFTER MODULE - BYTE 0 OF P1 CONTAINS # OF UNIVS RETURNED          *           
*                                                                   *           
* GUVBLOCK COVERED BY DSECT DEFINED IN SOURCE BOOK NEGETNUND        *           
*********************************************************************           
         SPACE 2                                                                
GETNUN   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 NUNDX-NUND,**GTNUN*,CLEAR=YES                                    
         USING NUND,RC                                                          
         L     R9,0(R1)            R9 POINTS TO GUVBLOCK                        
         USING GUVBLOCK,R9                                                      
         ST    R1,SAVEP1                                                        
         MVI   0(R1),0             SET BYTE 0 OF P1 TO ZERO                     
         L     RA,GUVCMFCS         RA POINTS TO COMFACS                         
         USING COMFACSD,RA                                                      
         MVC   DATAMGR,CDATAMGR    DEVELOP VTYPES                               
         MVI   GUVRECSW,0          ZERO AGENCY RECORD RETURNED SWITCH           
         MVI   GUVUTYPE,0          CLEAR UNIVERSE TYPE                          
         EJECT                                                                  
* READ FOR AGENCY UNIVERSE RECORD IF AGENCY ALPHA PASSED IN BLOCK               
*                                                                               
GU2      LA    R4,KEY                                                           
         XC    KEY,KEY                                                          
         OC    GUVAGY,GUVAGY       DID USER WANT AGENCY RECORDS?                
         BZ    GU6                                                              
         USING NUNRECD,R4                                                       
         LA    R2,10                                                            
         MVC   NUNKTYP,=X'0D22'                                                 
         MVC   NUNKAGY,GUVAGY                                                   
         MVC   NUNKEND,GUVDATE     USER CAN PROVIDE DATE                        
         OC    GUVDATE,GUVDATE                                                  
         BNZ   GU4                 TAKE RECD IF ITS DATE IS GE GUVDATE          
         LA    R2,12                                                            
         MVI   NUNKTYPE,1                                                       
         MVC   NUNKCODE,GUVCODE         OR CODE                                 
         SPACE 1                                                                
GU4      BAS   RE,HIGH                                                          
         EX    R2,*+8                                                           
         B     *+10                                                             
         CLC   KEY(0),KEYSAVE      ANY LUCK                                     
         BE    GU10                                                             
         OC    GUVCODE,GUVCODE     NO - ERROR IF USER SPECIFIED CODE            
         BNZ   MISS                                                             
         SPACE 1                                                                
* GET NTI UNIVERSE VALUES ACCORDING TO DATE PASSED IN BLOCK                     
*                                                                               
GU6      OC    GUVDATE,GUVDATE     TEST IF DATE PRESENT                         
         BZ    MISS                NO                                           
         MVC   NETWEEK,GUVNETWK                                                 
         OC    NETWEEK,NETWEEK     TEST IF NETWEEK ADDRESS PASSED               
         BNZ   GU8                 YES                                          
         MVI   DMCB+4,C'R'                                                      
         MVC   DMCB+5(3),=X'000A17'                                             
         GOTO1 CCALLOV,DMCB,0                                                   
         MVC   NETWEEK,DMCB                                                     
         SPACE 1                                                                
GU8      MVC   DATCON,CDATCON                                                   
         MVC   GETDAY,CGETDAY                                                   
         MVC   ADDAY,CADDAY                                                     
         GOTO1 DATCON,DMCB,(2,GUVDATE),WORK                                     
         GOTO1 NETWEEK,DMCB,WORK,GETDAY,ADDAY                                   
         MVC   BOOK+1(1),0(R1)     WEEK-USE HUT WEEK IGNORING BLK WK            
*   YEAR 2000 FIX                                                               
         GOTO1 DATCON,DMCB,(0,WORK),(3,DUB)                                     
         ZIC   R1,DUB                                                           
*****    PACK  DUB,WORK(2)         YEAR                                         
*****    CVB   R1,DUB                                                           
         STC   R1,BOOK                                                          
         CLI   BOOK+1,1            TEST FOR FIRST WEEK                          
         BNE   GU9                                                              
         CLC   WORK+2(2),=C'12'    TEST IF DATE IN DECEMBER                     
         BNE   GU9                 NO                                           
         LA    R1,1(R1)            YES-INCREMENT YEAR                           
         STC   R1,BOOK                                                          
         SPACE                                                                  
GU9      BAS   RE,GETDISP                                                       
         BAS   RE,GETDEM                                                        
         LA    R6,IO               POINT TO FULLWORD LIST                       
         MVI   GUVUTYPE,C'N'       SET UNIVERSE TYPE TO NETWORK                 
         B     GU12                                                             
         SPACE 2                                                                
MISS     MVI   GUVERROR,GUVNTFND                                                
         B     EXIT                                                             
         SPACE 2                                                                
* RETURN UNIVERSE VALUES FROM AGENCY UNIVERSE RECORD                            
*                                                                               
GU10     BAS   RE,GETREC                                                        
         BAS   RE,GETDISP                                                       
         LA    R4,IO                                                            
         LR    R6,R4               R6 POINTS TO RECORD FOR GETEL                
         ICM   RE,15,GUVAREC       AGENCY UNIV. RECORD I/O AREA                 
         BZ    GU11                NONE                                         
         SR    RF,RF                                                            
         ICM   RF,3,NUNRLEN                                                     
         LR    R0,R6               SOURCE ADDRESS                               
         LR    R1,RF               MOVE LENGTH = RECORD LENGTH                  
         MVCL  RE,R0                                                            
         MVI   GUVRECSW,C'Y'       SET RECORD RETURNED SWITCH                   
         SPACE 1                                                                
GU11     MVI   ELCODE,X'02'        FIND UNIVERSE ELEMENT                        
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         ZIC   RE,1(R6)                                                         
         LA    RF,3                                                             
         SR    RE,RF                                                            
         SRL   RE,2                                                             
         STH   RE,UNIVS                                                         
*                                                                               
         MVC   GUVUTYPE,NUNTYPE-NUNEL02(R6)  EXTRACT UNIVERSE TYPE              
         LA    R6,NUNIVES-NUNEL02(R6) BUMP TO FIRST FULLWORD                    
         SPACE 2                                                                
* GENERATE OUTPUT FORMAT - R6 POINTS TO FULLWORDS IN HUNDREDS                   
*                                                                               
GU12     CLI   GUVTYPE,2           TEST FOR FULLWORD HUNDREDS                   
         BE    GU14                                                             
         LR    R1,R6               SCALE TO THOUSANDS                           
         GOTO1 DIVTEN,(R1)                                                      
         CLI   GUVTYPE,4           TEST FOR FULLWORDS (0,000)                   
         BE    GU13                YES                                          
         LR    R1,R6               NO-ELEMENT OUTPUT                            
         GOTO1 DEMELS,(R1)                                                      
         B     EXIT                                                             
         SPACE 1                                                                
GU13     LR    R1,R6               SCALE FROM (000) TO (0000)                   
         GOTO1 DIVTEN,(R1)                                                      
         SPACE                                                                  
GU14     L     RE,GUVAOUT          MOVE FULLWORDS DIRECTLY TO OUTPUT            
         LH    R1,UNIVS                                                         
         SLL   R1,2                                                             
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     EXIT                                                             
         MVC   0(0,RE),0(R6)                                                    
         EJECT                                                                  
* SUB-ROUTINE TO GET DISPLACEMENT TABLE FOR EVN FILE AND TO SET                 
* POINTERS TO START AND END OF UNIVERSES                                        
*                                                                               
GETDISP  NTR1                                                                   
         MVC   TABLIST,TABLISTL                                                 
         GOTO1 CDEMADDR,DMCB,(X'FF',TABLIST),GUVCMFCS                           
         L     R1,ADISPTAB                                                      
         USING DSPHDRD,R1                                                       
         SPACE 1                                                                
GETDISP1 CLC   DSPFILE(2),=XL2'0000' EST FOR E-O-T                              
         BNE   *+6                 NO                                           
         DC    H'0'                                                             
         CLC   DSPFILE(3),=C'EVN'                                               
         BE    GETDISP2                                                         
         ICM   RF,7,DSPAET                                                      
         LA    R1,1(R1,RF)         BUMP TO NEXT ENTRY                           
         B     GETDISP1                                                         
         SPACE 1                                                                
GETDISP2 ICM   R5,7,DSPAET                                                      
         SR    R4,R4                                                            
         ICM   R4,3,DSPLDE         INCREMENT FOR BXLE                           
         LA    R5,0(R1,R5)         LIMIT-END OF DISP TABLE                      
         LA    R1,DSPHDRLN(R1)     POINT TO FIRST TABLE DATA ENTRY              
         USING DSPDTAD,R1                                                       
         SPACE                                                                  
GETDISP3 CLI   DSPMOD,C'U'         TEST FOR UNIVERSE                            
         BNE   GETDISP4                                                         
         OC    AUNIVST,AUNIVST     TEST IF START POINTER SET                    
         BNZ   *+8                 YES                                          
         ST    R1,AUNIVST          NO-SO SET IT                                 
         ST    R1,AUNIVEND                                                      
GETDISP4 BXLE  R1,R4,GETDISP3                                                   
         SPACE 1                                                                
         L     R1,AUNIVEND         NOW BUMP THE END POINTER FOR DEMOUT          
         LA    R1,0(R4,R1)         ADD IN THE DATA ENTRY LENGTH                 
         ST    R1,AUNIVEND                                                      
         S     R1,AUNIVST          DERIVE THE NUMBER OF UNIVERSE CELLS          
         SR    R0,R0               LENGTH OF UNIVERSES IN R1                    
         DR    R0,R4               DIVIDE BY LENGTH OF DATA ENTRY               
         STH   R1,UNIVS                                                         
         L     R4,SAVEP1                                                        
         STC   R1,0(R4)            RETURN # OF UNIVS IN BYTE 0 OF P1            
         B     EXIT                                                             
         DROP  R1                                                               
         EJECT                                                                  
* SUB-ROUTINE TO CONSTRUCT UNIVERSE DEMO ELEMENT - AT ENTRY R1                  
* POINTS TO FULLWORD UNIVERSE VALUES IN (000)                                   
*                                                                               
DEMELS   L     R2,GUVAOUT          PASS BACK X'31' ELEMENT                      
         MVI   0(R2),X'31'                                                      
         LR    R4,R1               PUT A(INPUT FULLWORDS) IN R4                 
         LH    R1,UNIVS                                                         
         LR    R0,R1                                                            
         MH    R1,=H'3'            X 3 FOR LENGTH OF FIELD                      
         LA    R1,3(R1)            PLUS 3 FOR ELEMENT LENGTH                    
         STC   R1,1(R2)                                                         
         MVI   2(R2),X'03'         ENTRY LENGTH OF 3                            
         LA    R2,3(R2)            POINT R2 AT FIRST DATA POSITION              
         SPACE 1                                                                
DEMELS2  MVC   0(3,R2),1(R4)       MOVE LAST 3 BYTES OF FULLWORD                
         LA    R2,3(R2)                                                         
         LA    R4,4(R4)                                                         
         BCT   R0,DEMELS2                                                       
         BR    RE                                                               
         EJECT                                                                  
* SUB-ROUTINE TO LOOK UP THE UNIVERSES ON NTI FILE (OUTPUT TO IO)               
* OUTPUT PRECISION OF HUNDREDS COMES FROM EVN DISPLACEMENT TABLE                
*                                                                               
GETDEM   ST    RE,DUB                                                           
         XC    WORK,WORK           BUILD SKELETON DUMMY RECORD                  
         MVI   WORK,C'P'                                                        
         MVC   WORK+20(2),=H'24'                                                
*                                                                               
         LA    R5,IO+1000                                                       
         USING DBLOCKD,R5                                                       
         XC    DBLOCK,DBLOCK                                                    
         MVC   DBFILE,=C'NTI'                                                   
         MVI   DBSELMED,C'N'                                                    
         MVI   DBSELSRC,C'N'                                                    
         LA    RE,WORK                                                          
         ST    RE,DBAREC                                                        
         LA    RE,23(RE)                                                        
         ST    RE,DBAQUART                                                      
         ST    RA,DBCOMFCS                                                      
         MVC   DBSELBK,BOOK                                                     
         MVC   GUVDEMBK,BOOK                                                    
         SPACE 1                                                                
GETDEM2  GOTO1 CDEMOUT,DMCB,(C'M',AUNIVST),DBLOCK,IO,AUNIVEND                   
         L     RE,DUB              RESTORE RETURN POINT                         
         CLI   DBERROR,0                                                        
         BER   RE                  EXIT TO CALLER FOR NO ERROR                  
***      DC    H'0'                                                             
         B     MISS                                                             
         DROP  R5                                                               
         EJECT                                                                  
* SUB-ROUTINE TO DIVIDE FULLWORD LIST BY TEN - AT ENTRY R1 POINTS               
* TO FULLWORDS                                                                  
*                                                                               
DIVTEN   ST    RE,DUB              SAVE RETURN POINT                            
         LH    R0,UNIVS                                                         
         SPACE 1                                                                
DIVTEN2  ICM   RF,15,0(R1)                                                      
         SR    RE,RE                                                            
         AH    RF,=H'5'                                                         
         D     RE,=F'10'                                                        
         STCM  RF,15,0(R1)                                                      
         LA    R1,4(R1)                                                         
         BCT   R0,DIVTEN2                                                       
         L     RE,DUB                                                           
         BR    RE                  RETURN TO CALLER                             
         EJECT                                                                  
* DATAMGR INTERFACE                                                             
*                                                                               
HIGH     NTR1                                                                   
         MVC   KEYSAVE(13),KEY                                                  
         MVC   DATADISP,=H'24'                                                  
         GOTO1 DATAMGR,DMCB,=C'DMRDHI',=C'SPTDIR',KEY,KEY,0                     
         BAS   RE,CHECK                                                         
         B     EXIT                                                             
         SPACE 2                                                                
GETREC   NTR1                                                                   
         GOTO1 DATAMGR,DMCB,=C'GETREC',=C'SPTFILE',KEY+14,IO,DMWORK             
         BAS   RE,CHECK                                                         
         B     EXIT                                                             
         SPACE 1                                                                
CHECK    CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         LH    R1,GUVIOCNT                                                      
         LA    R1,1(R1)                                                         
         STH   R1,GUVIOCNT                                                      
         BR    RE                                                               
         EJECT                                                                  
* ELEMENT SEARCH ROUTINE                                                        
*                                                                               
         GETEL (R6),DATADISP,ELCODE                                             
         SPACE 1                                                                
* ROUTINE AND MODULE EXIT                                                       
*                                                                               
EXIT     XMOD1 1                                                                
         SPACE 2                                                                
* CONSTANTS                                                                     
*                                                                               
TABLISTL DC    X'D0',3X'00',X'FF'                                               
         SPACE 2                                                                
* LITERAL POOL                                                                  
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*              DSECT FOR MODULE                                                 
         SPACE 3                                                                
NUND     DSECT                                                                  
DUB      DS    D                                                                
WORK     DS    CL32                                                             
DMCB     DS    6F                                                               
DMWORK   DS    CL96                                                             
DATAMGR  DS    A                                                                
NETWEEK  DS    A                                                                
DATCON   DS    A                                                                
GETDAY   DS    A                                                                
ADDAY    DS    A                                                                
AUNIVST  DS    A                                                                
AUNIVEND DS    A                                                                
SAVEP1   DS    A                                                                
UNIVS    DS    H                                                                
BOOK     DS    XL2                                                              
         DS    0F                                                               
TABLIST  DS    0CL5                                                             
ADISPTAB DS    A                                                                
         DS    X                                                                
FILE     DS    CL1                                                              
KEY      DS    CL25                                                             
KEYSAVE  DS    CL25                                                             
ELCODE   DS    CL1                                                              
DATADISP DS    H                                                                
         DS    0D                                                               
IO       DS    0C                                                               
         DS    2000C                                                            
NUNDX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE NEGETNUND                                                      
         EJECT                                                                  
* COMFACSD                                                                      
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
         SPACE 2                                                                
* SPGENUNIV                                                                     
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE SPGENUNIV                                                      
         PRINT ON                                                               
         SPACE 2                                                                
* DEDBLOCK                                                                      
*                                                                               
         PRINT OFF                                                              
DBLOCKD  DSECT                                                                  
       ++INCLUDE DEDBLOCK                                                       
         PRINT ON                                                               
         SPACE 2                                                                
* DEDEMTABD                                                                     
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE DEDEMTABD                                                      
         PRINT ON                                                               
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'011NEGETNUN  02/09/11'                                      
         END                                                                    
