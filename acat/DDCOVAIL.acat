*          DATA SET DDCOVAIL   AT LEVEL 033 AS OF 05/01/02                      
*CATALP COVAIL                                                                  
         TITLE 'COVAIL - VIRTUAL MEMORY ALLOCATION'                             
*HIPO******************************************************************         
*  NUMBER:                                                            *         
*  TITLE:       COVAIL - VIRTUAL CORE MAINT                           *         
*                                                                     *         
*  COMMENTS:    THIS MODULE WILL ALLOW THE USER TO PARTITION          *         
*               THE AMOUNT OF MEMORY WHICH IS PROVIDED ON THE         *         
*               REGION CARD FOR MOST EFFIECIENT OPERATION.            *         
*  CALLED FROM:                                                       *         
*                                                                     *         
*                                                                     *         
*  CALLS TO:    COVAIL                                                *         
*                                                                     *         
*                                                                     *         
*  INPUTS:      SEE PARAMETER DEFINITIONS                             *         
*                                                                     *         
*                                                                     *         
*                                                                     *         
*  OUTPUTS:     F'USER COMMAND'                                       *         
*               A(START OF SPACE REQUESTED) OR A(0) IF ERROR          *         
*               A(LENGTH OF SPACE ALLOCATED OR AVAILABLE)             *         
*               A(REALLOCATED BUFFALO CSECT)                          *         
*                                                                     *         
*  LOCALS:      RMIN  = MINIMUM BUFFER REQUIRED                       *         
*               RMAX  = MAXIMUM BUFFER SIZE REQUESTED                 *         
*               WHERE = START OF SPACE REQUESTED                      *         
*               AMOUNT=LENGTH OF SPACE RETURNED                       *         
*               LEAVE =AMOUNT OF SPACE REQUIRED AFTER ALLOCATION      *         
***********************                                               *         
*  LOGIC:    1. PROGRAM USES THE SYSTEM MACROS GETMAIN AND FREEMAIN   *         
*               TO PERFORM VARIOUS ALLOCATION FUNCTIONS               *         
*                                                                     *         
*            2. PROGRAM REALLOCATES AND MOVE BUFFALO CSECT TO START   *         
*               OF AVAILABLE STORAGE. MAXIMUM ENTRIES IS ALSO CHANGED *         
*               TO REFLECT THE LENGTH OF THE NEW AREA.                *         
*                                                                     *         
*ENDHIPO***************************************************************         
         EJECT                                                                  
**************************INPUT PARAMETERS*****************************         
* P1      P2                     P3                      P4                     
* --      --                     --                      --                     
*                                                                               
* LOOK                                                                          
*                                                                               
* FREE    A(BUFFER)              F'LENGTH OF BUFFER'                            
*                                                                               
* GET     F'MINIMUM ALLOCATION'  F'MAXIMUM ALLOCATION'                          
*                                                                               
* SETB    F'MINIMUM ALLOCATION'  F'AMOUNT OF CORE TO     A(BUFFALOC)            
*                                  BE LEFT UNALLOCATED'                         
*                                                                               
* RESB    A(BUFFER)              A(LENGTH OF BUFFER)     A(BUFFALOC)            
**********************************************************************          
         SPACE 1                                                                
**************************OUTPUTS**************************************         
* P1      P2                     P3                      P4                     
* --      --                     --                      --                     
*                                                                               
* LOOK    A(START OF BUFFER)     F'LENGTH OF BUFFER'     N/U                    
* FREE    N/U                    N/U                     N/U                    
* GET     A(START OF BUFFER)     F'LENGTH OF BUFFER'     N/U                    
* SETB    A(START OF BUFFER)     F'LENGTH OF BUFFER'     A(BUFFALOC)            
* RESB    A(START OF BUFFER)     F'LENGTH OF BUFFER'     A(BUFFALOC)            
*                                                                               
* F'0' IN OUTPUT P2 INDICATES AN ERROR                                          
* N/U = NOT USED-INPUT REMAINS UNCHANGED                                        
*********************************************************************           
         EJECT                                                                  
COVAIL   CSECT                                                                  
         NMOD1 10,**COVA**                                                      
         USING MAP,RC                                                           
         XC    WHERE,WHERE         SET UP VARIABLES                             
         XC    AMOUNT,AMOUNT                                                    
         ST    R1,SAVER1                                                        
         MVC   RMIN,4(R1)          SAVE INPUT PARAMETERS                        
         MVC   RMAX,8(R1)                                                       
         MVC   LEAVE,8(R1)         AMOUNT OF CORE TO LEAVE                      
         MVC   BUFFALO,12(R1)                                                   
         CLC   0(3,R1),=C'LOOK'    CHECK FUNCTION                               
         BE    LOOK                                                             
         SPACE 1                                                                
         CLC   0(3,R1),=C'GET'                                                  
         BE    GET                                                              
         SPACE 1                                                                
         CLC   0(3,R1),=C'FREE'                                                 
         BE    FREE                                                             
         SPACE 1                                                                
         CLC   0(4,R1),=C'SETB'                                                 
         BE    BUFF                                                             
         SPACE 1                                                                
         CLC   0(4,R1),=C'RESB'                                                 
         BE    BUFF2                                                            
         SPACE 1                                                                
         XC    WHERE,WHERE         SET ERROR AND EXIT                           
         XC    AMOUNT,AMOUNT                                                    
         B     COVAILX                                                          
         EJECT                                                                  
* SEE HOW MUCH CORE IS AVAILABLE                                                
LOOK     MVC   RMIN,=F'500'        FORCE MIN AND MAX                            
         MVC   RMAX,=F'9000000'    SEE HOW MUCH IS LEFT                         
         LA    R4,RMIN                                                          
         LA    R5,WHERE                                                         
         GETMAIN VC,LA=(R4),A=(R5)                                              
         L     R0,AMOUNT           FREE IT UP FOR NEXT CALL                     
         L     R1,WHERE                                                         
         FREEMAIN R,LV=(0),A=(1)                                                
         B     COVAILX                                                          
         SPACE 2                                                                
* GET THE AMOUNT OF VIRTUAL MEMORY AS REQUESTED                                 
GET      LA    R4,RMIN                                                          
         LA    R5,WHERE                                                         
         GETMAIN VC,LA=(R4),A=(R5)                                              
         LTR   RF,RF               CHECK FOR OK                                 
         BZ    COVAILX                                                          
         XC    WHERE,WHERE         SET ERROR                                    
         XC    AMOUNT,AMOUNT                                                    
         B     COVAILX                                                          
         SPACE 2                                                                
* FREE UP PREVIOUSLY REQUESTED CORE                                             
FREE     L     R0,RMAX                                                          
         L     R1,RMIN                                                          
         FREEMAIN R,LV=(0),A=(1)                                                
         B     LOOK                RETURN TOTAL AVIALABLE                       
         SPACE 2                                                                
* ALLOCATE A NEW BUFFALO BUFFER                                                 
BUFF     GOTO1 (RB),DMCB,C'LOOK'   FIND AVAILABLE CORE                          
         L     R9,8(R1)            SET AVAILABLE                                
         L     R3,RMIN             GET MINIMUM REQUIRED                         
         A     R3,LEAVE            GET AMOUNT TO LEAVE                          
         CR    R9,R3               IS THERE ENOUGH LEFT                         
         BNL   *+14                                                             
         XC    WHERE,WHERE         NO - SET ERROR                               
         B     COVAILX                                                          
         S     R9,LEAVE            SET AMOUNT TO LEAVE                          
*&&US                                                                           
*                                                                               
* THIS IS ONLY &&US BECAUSE IT IS UNKNOWN (IT MAY BE SAFE FOR UK)               
*                                                                               
         CLC   RMIN,=F'500000'     FORCE TO MIN IF MIN GREATER                  
         BH    BUFF1                THAN 400K                                   
         C     R9,=F'500000'       LIMIT TO 400K IF WE WOULD                    
         BL    BUFF1A              ALLOCATE MORE                                
         L     R9,=F'500000'                                                    
         B     BUFF1A                                                           
BUFF1    L     R9,RMIN                                                          
*&&                                                                             
BUFF1A   MVC   WHERE(8),4(R1)                                                   
         L     R8,RMIN             SET START ADDRESS                            
         GOTO1 (RB),DMCB,C'GET',(R8),(R9)                                       
         OC    4(8,R1),4(R1)                                                    
         BNZ   *+6                                                              
         DC    H'0'                                                             
BUFF2    MVC   WHERE(8),4(R1)      SET START ADDR/LENGTH                        
         L     R6,BUFFALO          POINT TO ORIG.                               
         USING BUFFALOD,R6                                                      
         BAS   RE,CHECKEY          SET INTIAL VARIABLES                         
         L     R9,WHERE            POINT TO NEW BUFFALO                         
         MVC   0(255,R9),0(R6)                                                  
         ST    R9,BUFFALO                                                       
         LR    R6,R9               SET NEW BUFFALO ADDRESS                      
         LA    R1,BUFFAREA         SET THIS BUFFERS ADDRESS                     
         A     R1,BUFFLALL                                                      
         ST    R1,BUFFADDR                                                      
         L     R9,AMOUNT           CALCULATE NEW NO. OF BUFFALO ITEMS           
         S     R9,BUFFLALL         REDUCE BY LENGTH OF ONE RECORD               
         S     R9,=F'256'          AND HEADER                                   
         SR    R8,R8                                                            
         D     R8,BUFFLALL                                                      
         ST    R9,BUFFCRMX                                                      
         L     R1,SAVER1           SET REALLOCATED BUFF ADDR.                   
         MVC   12(4,R1),BUFFALO                                                 
         B     COVAILX                                                          
         EJECT                                                                  
COVAILX  L     R1,SAVER1                                                        
         MVC   4(4,R1),WHERE                                                    
         MVC   8(4,R1),AMOUNT                                                   
         XMOD1 1                                                                
         EJECT                                                                  
CHECKEY  NTR1                                                                   
         LA    R2,BUFFLIST                                                      
         SR    R3,R3                                                            
         SPACE 2                                                                
CK2      CLI   0(R2),X'FF'         END OF LIST                                  
         BE    CK4                                                              
         IC    R3,0(R2)            L'CUMULATIVE KEY                             
         LA    R2,2(R2)                                                         
         B     CK2                                                              
         SPACE 2                                                                
CK4      ST    R3,BUFFLKEY                                                      
         L     R3,BUFFROWS                                                      
         M     R2,BUFFCOLS                                                      
         SLL   R3,2                                                             
         CLI   BUFFFLVR,C'P'                                                    
         BNE   *+8                                                              
         SLL   R3,1                                                             
         CLI   BUFFFLVR,C'D'                                                    
         BNE   *+6                                                              
         SR    R3,R3                                                            
         ST    R3,BUFFLDTA                                                      
         A     R3,BUFFLKEY                                                      
         A     R3,BUFFLCOM                                                      
         ST    R3,BUFFLALL         SET TOTAL LENGTH                             
         XIT1                                                                   
         LTORG                                                                  
         SPACE 2                                                                
MAP      DSECT                                                                  
SAVER1   DS    F                                                                
RMIN     DS    F                   MINIMUM AMOUNT REQUESTED                     
RMAX     DS    F                   MAXIMUM AMOUNT REQUESTED                     
WHERE    DS    F                   ADDRESS OF ALLOCATED BUFFER                  
AMOUNT   DS    F                   LENGTH OF ALLOCATED BUFFER                   
LEAVE    DS    F                   AMOUNT OF CORE TO LEAVE                      
BUFFALO  DS    F                   ADDRESS OF BUFFALO CSECT                     
DMCB     DS    6F                                                               
         EJECT                                                                  
       ++INCLUDE DDBUFFALOD                                                     
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'033DDCOVAIL  05/01/02'                                      
         END                                                                    
