*          DATA SET REGENDTRS  AT LEVEL 044 AS OF 03/04/04                      
*CATALP REGENDTR                                                                
***********************************************************************         
*                                                                     *         
*         REGENDTR --- DARE PASSIVE POINTERS MAINTENCE ROUTINE        *         
*                                                                     *         
* ------------------------------------------------------------------- *         
* UPDATE HISTORY:                                                     *         
*                                                                     *         
* 15OCT02 (HQ ) --- DATE OF BIRTH                                     *         
*                                                                     *         
***********************************************************************         
*  INPUT:                                                                       
*              PARAMETER 1 = BYTE 1 - X'01' INVOKE PTR                          
*                                     X'02' INVOKE ADDPTRS                      
*                                     X'80' DO NOT GENERATE ACTIVE PTR          
*                                     X'40' DO GENERATE ACTIVE PTR              
*                          = BYTES 2-4   A(COMFACS)                             
*                        2 = BYTES 2-4 - A(WORK SPACE)                          
*                        3 = BYTES 2-4 - A(DARE RECORD)                         
*                                        A(WORK SPACE) IF ADDPTRS               
*                        4 = BYTES 2-4 - A(IO AREA) IF PTR                      
*                                        A(D/A OF THE NEW RECORD IF ADD         
*  OUTPUT:                                                                      
*              PARAMETER 1 = BYTE 1 - 0 = OK, X'80' = ERROR                     
***********************************************************************         
         TITLE 'REGENDTR - DARE PASSIVE KEY GENERAL MAINTENANCE'                
REGENDTR CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,REGENDTR,R9,CLEAR=YES                                
         USING WORKD,RC                                                         
         LM    R2,R4,4(R1)                                                      
         MVC   MYFLAG,0(R1)        SAVING CALLING OPTION                        
*                                                                               
         L     R6,0(R1)                                                         
         ST    R6,VCOMFACS                                                      
*                                                                               
         GOTO1 INIT                                                             
*                                                                               
         TM    0(R1),X'01'                                                      
         BZ    MAIN10                                                           
         GOTO1 PTRS,DMCB,(R2),(R3)                                              
         B     EXIT                                                             
*                                                                               
MAIN10   DS    0H                                                               
         TM    0(R1),X'02'                                                      
         BZ    EXIT                R4->D/A OF THE NEW DARE RECORD               
         GOTO1 ADDPTRS,DMCB,(R2),(R3),(R4)                                      
         B     EXIT                                                             
*                                                                               
         MVC   DATADISP,=H'34'                                                  
*                                                                               
EXIT     DS    0H                                                               
         XMOD1                                                                  
***********************************************************************         
* ROUTINE TO BUILD POINTER LIST IN P1                                           
*  P1 - A(WORK SPACE)                                                           
*  P2 - A(DARE RECORD)                                                          
***********************************************************************         
PTRS     NTR1                                                                   
         L     R2,4(R1)            R2->DAR RECORD                               
         USING RDARREC,R2                                                       
*                                                                               
         CLI   RDARKSTA+4,C'A'     AM STATION?                                  
         BE    PTR005              YES - ACCEPT                                 
         CLI   RDARKSTA+4,C'F'     FM STATION?                                  
         BNE   EXIT                NO  - REJECT NON-RADIO RECORDS               
*                                                                               
PTR005   DS    0H                                                               
         CLI   RDARKRT,X'10'       AGENCY HEADER?                               
         BNE   EXIT                NO  - NOTHING TO PROCESS IF NOT HDR          
*                                                                               
         L     R3,0(R1)            R3->WORK SPACE                               
         LR    RE,R3                                                            
         XCEF  (RE),800                                                         
*                                                                               
         MVC   DMCB(2),RDARKREP                                                 
         GOTO1 MSTRAP                                                           
*                                                                               
* BUILD ACTIVE PTR                                                              
         TM    MYFLAG,X'80'                                                     
         BO    PTR010                                                           
         TM    MYFLAG,X'40'                                                     
         BZ    PTRX                                                             
         MVC   0(32,R3),0(R2)                                                   
         LA    R3,32(R3)                                                        
PTR010   DS    0H                                                               
*                                                                               
       ++INCLUDE REDARPAS          BUILD PASSIVE POINTERS                       
*                                                                               
PTRX     DS    0H                  EXIT ALREADY IN REDARPAS                     
*                                                                               
***********************************************************************         
* ROUTINE TO ADD POINTERS TO FILE                                               
*              P1=A(OLD PTR LIST)                                               
*              P2=A(NEW PTR LIST)                                               
*              P3=A(DISK ADDR)                                                  
***********************************************************************         
ADDPTRS  NTR1                                                                   
         LM    R2,R4,0(R1)                                                      
*                                                                               
         MVI   DMOUTBTS,0          FOR PROPER RECOVERY                          
         OI    DMINBTS,X'08'       PASS DELETES                                 
AP25     CLC   0(27,R2),0(R3)      SAME?                                        
         BNE   AP30                                                             
*                                                                               
* SPECIAL CHECK FOR CONFIRMATION (X'41' --> X'51')                              
*                                                                               
         TM    27(R2),X'04'                                                     
         BNO   AP27                                                             
         TM    27(R3),X'04'                                                     
         BO    AP100                                                            
         B     AP30                                                             
*                                                                               
AP27     DS    0H                                                               
         TM    27(R3),X'04'                                                     
         BNO   AP100                                                            
*                                                                               
* DIFFERENT                                                                     
AP30     DS    0H                                                               
         CLI   0(R2),0             ADD?                                         
         BE    AP40                WRITE/RESTORE NEW KEY                        
*                                                                               
         CLI   0(R2),X'41'          CHECK RECORD TYPE                           
         BE    AP30A                                                            
         CLI   0(R2),X'51'                                                      
         BE    AP30A                                                            
         CLC   0(2,R2),=X'4101'                                                 
         BE    AP30A                                                            
         CLI   0(R2),X'D0'                                                      
         BE    AP30A                                                            
         CLI   0(R2),X'D1'                                                      
         BE    AP30A                                                            
         DC    H'0'                                                             
*                                                                               
AP30A    DS    0H                                                               
*                                                                               
* CHANGE                                                                        
* DELETE OLD PTR                                                                
         MVC   KEY,0(R2)                                                        
         OI    DMINBTS,X'08'       PASS DELETES                                 
         OI    DMINBTS,X'80'       READ FOR UPDATE                              
*        MVI   UPDATE,C'Y'                                                      
         GOTO1 HIGH                                                             
         BAS   RE,APCHECK                                                       
         CLC   KEY(27),KEYSAVE                                                  
         BNE   AP40                                                             
         MVI   KEY+27,X'FF'                                                     
         GOTO1 WRITE                                                            
         BAS   RE,APCHECK                                                       
* ADD NEW PTR                                                                   
AP40     DS    0H                                                               
         OC    0(27,R3),0(R3)       DON'T ADD ALL NULL POINTER                  
         BZ    AP100                                                            
*                                                                               
         CLI   0(R3),X'41'          CHECK RECORD TYPE                           
         BE    AP40A                                                            
         CLI   0(R3),X'51'                                                      
         BE    AP40A                                                            
         CLC   0(2,R3),=X'4101'                                                 
         BE    AP40A                                                            
         CLI   0(R3),X'D0'                                                      
         BE    AP40A                                                            
         CLI   0(R3),X'D1'                                                      
         BE    AP40A                                                            
         DC    H'0'                                                             
*                                                                               
AP40A    DS    0H                                                               
         MVC   KEY,0(R3)                                                        
         OI    DMINBTS,X'08'       PASS DELETES                                 
         OI    DMINBTS,X'80'       READ FOR UPDATE                              
*        MVI   UPDATE,C'Y'                                                      
         GOTO1 HIGH                                                             
         BAS   RE,APCHECK                                                       
         CLC   KEY(27),KEYSAVE     KEY ALREADY THERE?                           
         BE    *+14                                                             
         MVC   KEY,KEYSAVE                                                      
         B     AP50                                                             
* UNDELETE OLD PTR                                                              
         MVC   KEY+27(1),27(R3)    RESTORE X'04' CONFIRM BIT,IF PRESENT         
         MVC   KEY+28(4),0(R4)     DISK ADDR                                    
         GOTO1 WRITE                                                            
         BAS   RE,APCHECK                                                       
         B     AP100                                                            
* ADD PTR                                                                       
AP50     DS    0H                                                               
         MVC   KEY+27(1),27(R3)    RESTORE X'04' CONFIRM BIT,IF PRESENT         
         MVC   KEY+28(4),0(R4)     DISK ADDR                                    
         GOTO1 ADD                                                              
         BAS   RE,APCHECK                                                       
*                                                                               
* NEXT POINTER                                                                  
AP100    LA    R2,32(R2)                                                        
         LA    R3,32(R3)                                                        
         CLI   0(R3),0             LAST?                                        
         BNE   AP25                                                             
         CLI   0(R2),0                                                          
         BNE   AP25                                                             
         MVI   DMOUTBTS,X'FD'                                                   
         B     EXXIT                                                            
*                                                                               
APCHECK  TM    DMCB+8,X'FD'        ALL BUT DELETE                               
         BCR   8,RE                                                             
         DC    H'0'                                                             
*                                                                               
EXXIT    DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
*                                                                               
***********************************************************************         
* SET ADDRESSES                                                                 
* RETRIEVE DEMO VALUES, IF ANY                                                  
***********************************************************************         
INIT     NTR1                                                                   
         L     R6,VCOMFACS                                                      
         USING COMFACSD,R6                                                      
         MVC   VDATAMGR,CDATAMGR                                                
         DROP  R6                                                               
         XC    DMINBTS,DMINBTS     PRESET DATAMGR CONTROL BITS                  
         MVI   DMOUTBTS,X'FD'      PRESET DATAMGR ERROR CHECK BITS              
INITX    DS    0H                                                               
         B     EXIT                                                             
******************************************************************              
* TRAP MASTER AND SUBSIDIARY REP                                                
* P1 = TWO BYTE REP CODE                                                        
******************************************************************              
MSTRAP   NTR1  WORK=(R4,500)       REP RECORD                                   
         ST    R4,AIOAREA                                                       
*                                                                               
         CLC   =C'NU',0(R1)        CLEAR CHANNEL?                               
         BNE   MSTR0100                                                         
         MVC   POWERCDS(4),=C'NUK3' SPECIAL CODING FOR CLEAR CHANNEL            
         B     EXIT                FINISHED                                     
*                                                                               
MSTR0100 DS    0H                                                               
         XC    KEY,KEY                                                          
K        USING RREPKEY,KEY                                                      
         MVI   K.RREPKTYP,X'01'                                                 
         MVC   K.RREPKREP,0(R1)                                                 
         DROP  K                                                                
*                                                                               
         GOTO1 HIGH                                                             
         CLC   KEYSAVE(27),KEY                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         GOTO1 GETREC                                                           
*                                                                               
         USING RREPREC,R4                                                       
         CLC   =X'0000',RREPMAST   STAND ALONE?                                 
         BE    EXIT                                                             
         CLC   =X'FFFF',RREPMAST   MASTER? (THIS SHOULDN'T HAPPEN)              
         BE    EXIT                                                             
*                                                                               
         MVC   POWERCDS(L'RREPMAST),RREPKREP                                    
         MVC   POWERCDS+L'RREPMAST(L'RREPMAST),RREPMAST                         
         B     EXIT                                                             
*                                                                               
*&&DO                                                                           
EXITOK   MVI   SVDMCB,0                                                         
         B     *+10                                                             
EXITERR  OI    SVDMCB,X'80'        ERROR                                        
*        MVC   KEY,SVKEY                                                        
*        MVC   AIOAREA,AIO1                                                     
         GOTO1 HIGH                RESTORE SEQUENCE                             
         XIT1                                                                   
*&&                                                                             
***********************************************************************         
* DATAMGR CALLS                                                                 
***********************************************************************         
HIGH     NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         GOTO1 VDATAMGR,DMCB,(DMINBTS,=C'DMRDHI'),=C'REPDIR',          X        
               KEYSAVE,KEY,0                                                    
         B     DMCHECK                                                          
*                                                                               
ADD      NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         GOTO1 VDATAMGR,DMCB,(DMINBTS,=C'DMADD '),=C'REPDIR',          X        
               KEYSAVE,KEY,0                                                    
         B     DMCHECK                                                          
*                                                                               
WRITE    NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         GOTO1 VDATAMGR,DMCB,(DMINBTS,=C'DMWRT '),=C'REPDIR',          X        
               KEYSAVE,KEY,0                                                    
         B     DMCHECK                                                          
*                                                                               
SEQ      NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         GOTO1 VDATAMGR,DMCB,(DMINBTS,=C'DMRSEQ'),=C'REPDIR',          X        
               KEYSAVE,KEY,0                                                    
         B     DMCHECK                                                          
*                                                                               
GETREC   NTR1                                                                   
         GOTO1 VDATAMGR,DMCB,(DMINBTS,=C'GETREC'),=C'REPFILE',KEY+28,  X        
               AIOAREA,DMWORK                                                   
         B     DMCHECK                                                          
DMCHECK  DS    0H                                                               
*                                                                               
         MVI   DMINBTS,X'00'                                                    
         MVI   UPDATE,C'N'                                                      
         MVC   DMBYTE,DMCB+8                                                    
         NC    DMBYTE,DMOUTBTS                                                  
         BZ    EXIT                                                             
         DC    H'0'                                                             
*                                                                               
         B     EXIT                                                             
*                                                                               
         EJECT                                                                  
         GETEL R6,DATADISP,ELCODE                                               
******************************************************************              
* COLCODE, ASSIGEN COLOR CODE TO KEYS                                           
******************************************************************              
       ++INCLUDE REDARSTAT                                                      
MYSPACES DC    40C' '                                                           
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
WORKD    DSECT                                                                  
DMWORK   DS    12D                                                              
*                                                                               
VCOMFACS DS    A                   A(COMFACS)                                   
*                                                                               
VDATAMGR DS    A                   A(DATAMGR)                                   
*                                                                               
KEY      DS    CL32                                                             
KEYSAVE  DS    CL32                                                             
DMCB     DS    6F                                                               
DMINBTS  DS    X                                                                
DMOUTBTS DS    X                                                                
DMBYTE   DS    X                                                                
AIOAREA  DS    A                                                                
DATADISP DS    H                                                                
ELCODE   DS    X                                                                
WORK     DS    CL64                                                             
DARTYPE  DS    X                                                                
MASTCODE DS    CL2                                                              
SAVER1   DS    F                                                                
ADAR0A   DS    A                                                                
UPDATE   DS    C                                                                
DARDA    DS    A                                                                
ALTDUB   DS    CL3                                                              
MASTFIND DS    C                                                                
MYFLAG   DS    X                                                                
POWERCDS DS    5X'00'                                                           
WORKX    EQU   *                                                                
         EJECT                                                                  
*                                                                               
* REGENCON                                                                      
* REGENDAR                                                                      
*                                                                               
       ++INCLUDE REDARDSECT                                                     
       ++INCLUDE REGENREPA                                                      
       ++INCLUDE DDCOMFACS                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'044REGENDTRS 03/04/04'                                      
         END                                                                    
