*          DATA SET NECALCASHP AT LEVEL 003 AS OF 03/28/08                      
*CATALP CALCASHP                                                                
*******************************************************************             
* INPUT: PARAM1: A(UNIT REC)                                                    
*        PARAM2: A(COMFACS)                                                     
*        PARAM3: BYTES 1-3 - A(CORPORATE PRODUCT)                               
*******************************************************************             
         TITLE 'CALCASHP - CALCULATE CASH PERCENTAGE    '                       
CALCASHP CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKL,**CSHP**                                                   
         USING WORKD,RC             RC=A(LOCAL WORKING STORAGE)                 
         LM    R2,R4,0(R1)          R2=A(PARAMETER LIST)                        
         USING NURECD,R2            A(UNIT RECORD)                              
         USING COMFACSD,R3          A(COMFACS)                                  
         L     RF,CHELLO-COMFACSD(R3)                                           
         ST    RF,VHELLO            A(HELLO)                                    
*                                                                               
         XC    CORPRD,CORPRD                                                    
         XC    CASHPCT,CASHPCT                                                  
*                                                                               
         LTR   R4,R4                                                            
         JZ    *+10                                                             
         MVC   CORPRD,0(R4)         CORPORATE PRODUCT                           
*                                                                               
         GOTO1 VHELLO,DMCB,(C'G',=C'UNTFILE '),(X'18',(R2)),0                   
         CLI   12(R1),0                                                         
         JNE   CALCSHEX             NO ELEM - EXIT                              
         L     R5,12(R1)                                                        
         USING NUDTAD,R5                                                        
*                                                                               
         OC    NUDTCASH,NUDTCASH    ANY CASH%?                                  
         JZ    CALCSHEX             NO - EXIT                                   
         MVC   CASHPCT,NUDTCASH     UNIT'S CASH%                                
         DROP  R5                                                               
*                                                                               
* PROCESS ACTUAL COST                                                           
*                                                                               
         XC    CASHAMT,CASHAMT                                                  
*                                                                               
         CLC   CASHPCT,=X'FFFF'     CASH% = 0?                                  
         JNE   CALCP10                                                          
         ICM   R1,15,NUACTUAL                                                   
         LNR   R1,R1                                                            
         STCM  R1,15,CASHAMT        CASH AMOUNT = -(ACTUAL COST)                
         J     CALCP20                                                          
*                                                                               
CALCP10  ZAP   WORK(16),=PL1'0'                                                 
         ICM   R1,15,NUACTUAL       ACTUAL COST                                 
         CVD   R1,WORK+8                                                        
         SR    R1,R1                                                            
         ICM   R1,3,CASHPCT         PERCENTAGE                                  
         CVD   R1,DUB                                                           
*                                                                               
         MP    WORK(16),DUB+4(4)    MULT COST BY PERCENTAGE                     
         AP    WORK(16),=PL2'50'    ROUND                                       
         CP    WORK(16),=P'0'                                                   
         BH    *+10                 CHECK FOR CREDIT                            
         SP    WORK(16),=PL2'100'   COMPENSATE FOR THE 50                       
         DP    WORK(16),=PL2'100'   ROUND                                       
         CVB   R6,WORK+6                                                        
*                                                                               
         ICM   R1,15,NUACTUAL       CREATE CREDIT AMOUNT                        
         SR    R6,R1                                                            
         STCM  R6,15,CASHAMT                                                    
*                                                                               
*  UPDATE BARTER ACTUAL COST ELEMENT                                            
*                                                                               
CALCP20  MVI   ELEM,0                                                           
         MVI   ELEM+1,C'C'                                                      
         GOTO1 VHELLO,DMCB,(C'D',=C'UNTFILE '),(X'03',(R2)),(2,ELEM)            
*                                                                               
         TM    NUUNITST,X'20'       IS THERE AN ACTUAL COST?                    
         BO    *+14                                                             
         OC    NUACTUAL,NUACTUAL                                                
         BZ    CALCP40                                                          
*                                                                               
         XC    ELEM,ELEM            ADD NEW CREDIT ELEM                         
         LA    R5,ELEM                                                          
         USING NUSPRD,R5                                                        
*                                                                               
         MVI   NUSPREL,X'03'                                                    
         MVI   NUSPRLEN,NUSPRLN4                                                
         MVI   NUSPRSEQ,0                                                       
         MVI   NUSPRTYP,C'C'                                                    
         MVI   NUSPRCOM,C'C'                                                    
         MVC   NUSPRAMT,CASHAMT     CASH AMOUNT                                 
         MVC   NUSPRBPC,CORPRD      CORPORATE PRODUCT                           
         DROP  R5                                                               
*                                                                               
         GOTO1 VHELLO,DMCB,(C'P',=C'UNTFILE '),(X'03',(R2)),ELEM,0              
         CLI   12(R1),0                                                         
         JE    CALCP40              EXIT                                        
         J     NO                                                               
*                                                                               
* PROCESS ASSIGNED COST                                                         
*                                                                               
CALCP40  XC    CASHAMT,CASHAMT                                                  
*                                                                               
         CLC   CASHPCT,=X'FFFF'     CASH% = 0?                                  
         JNE   CALCP50                                                          
         ICM   R1,15,NUASSIGN                                                   
         LNR   R1,R1                                                            
         STCM  R1,15,CASHAMT        CASH AMOUNT = -(ASSIGNED COST)              
         J     CALCP60                                                          
*                                                                               
CALCP50  ZAP   WORK(16),=PL1'0'                                                 
         ICM   R1,15,NUASSIGN       ASSIGNED COST                               
         CVD   R1,WORK+8                                                        
         SR    R1,R1                                                            
         ICM   R1,3,CASHPCT         PERCENTAGE                                  
         CVD   R1,DUB                                                           
*                                                                               
         MP    WORK(16),DUB+4(4)    MULT COST BY PERCENTAGE                     
         AP    WORK(16),=PL2'50'    ROUND                                       
         CP    WORK(16),=P'0'                                                   
         BH    *+10                 CHECK FOR CREDIT                            
         SP    WORK(16),=PL2'100'   COMPENSATE FOR THE 50                       
         DP    WORK(16),=PL2'100'   ROUND                                       
         CVB   R6,WORK+6                                                        
*                                                                               
         ICM   R1,15,NUASSIGN       CREATE CREDIT AMOUNT                        
         SR    R6,R1                                                            
         STCM  R6,15,CASHAMT                                                    
*                                                                               
*  UPDATE BARTER ASSIGNED COST ELEMENT                                          
*                                                                               
CALCP60  GOTO1 VHELLO,DMCB,(C'D',=C'UNTFILE '),(X'76',(R2)),0                   
*                                                                               
         TM    NUUNITST,X'08'       IS THERE AN ASSIGNED COST?                  
         BO    *+14                                                             
         OC    NUASSIGN,NUASSIGN                                                
         BZ    CALCSHEX                                                         
*                                                                               
         XC    ELEM,ELEM            ADD NEW ASSIGNED CREDIT ELEM                
         LA    R5,ELEM                                                          
         USING NUASCD,R5                                                        
*                                                                               
         MVI   NUASCEL,X'76'                                                    
         MVI   NUASCLEN,NUASCLN4                                                
         MVI   NUASCTYP,C'F'                                                    
         MVC   NUASCAMT,CASHAMT     CASH AMOUNT                                 
         MVC   NUASCBPC,CORPRD      CORPORATE PRODUCT                           
         DROP  R5                                                               
*                                                                               
         GOTO1 VHELLO,DMCB,(C'P',=C'UNTFILE '),(X'76',(R2)),ELEM,0              
         CLI   12(R1),0                                                         
         JE    CALCSHEX             EXIT                                        
         J     NO                                                               
         DROP  R5                                                               
*                                                                               
CALCSHEX DS    0H                                                               
YES      SR    RC,RC                                                            
NO       LTR   RC,RC                                                            
EXIT     XIT1  ,                                                                
         EJECT                                                                  
         DROP  R2,R3                                                            
         LTORG                                                                  
*                                                                               
WORKD    DSECT                     ** LOCAL WORKING STORAGE **                  
WORK     DS    XL64                                                             
DUB      DS    D                                                                
DMCB     DS    6F                                                               
VHELLO   DS    F                                                                
*                                                                               
CORPRD   DS    CL3                 CORPORATE PRODUCT                            
CASHPCT  DS    XL2                 CASH PERCENTAGE                              
CASHAMT  DS    XL4                 CASH AMOUNT                                  
*                                                                               
ELEMACT  DS    CL1                 A(DD), C(HANGE)                              
ELEM     DS    XL256                                                            
WORKL    EQU   *-WORKD                                                          
*                                                                               
       ++INCLUDE NEGENUNIT                                                      
       ++INCLUDE DDCOMFACSD                                                     
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003NECALCASHP03/28/08'                                      
         END                                                                    
