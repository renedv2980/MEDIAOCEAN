*          DATA SET PPECHOP    AT LEVEL 009 AS OF 03/22/99                      
*CATALP PPECHOP                                                                 
         TITLE 'CHOPS UP A CONTINOUS STRING INTO FIXED BLOCKS'                  
*                                                                               
*  NOTE - THIS MODULE IS LOGICALLY IDENTICAL TO CHOPPER, HOWEVER IT             
*         TESTS FOR MORE "DIVIDING" CHARACTERS THAN SPACE AND HYPHEN,           
*         SUCH AS @ AND / WHICH ARE FOUND IN EMAIL ADDRESSES                    
*                                                                               
PPECHOP  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 1,*PECHOP*                                                       
         USING CHOPD,RC                                                         
         LM    R2,R3,0(R1)         PARAMETER 1 BYTE 0   L'STRING                
         SR    R4,R4                           BYTE 1-3 A(STRING)               
         IC    R4,0(R1)            PARAMETER 2 BYTE 0   WIDTH OF BLOCK          
         SR    R5,R5                           BYTE 1-3 A(BLOCK)                
         IC    R5,4(R1)            PARAMETER 3 MAX NUMBER OF LINES              
         L     R6,8(R1)                        SET TO ACTUAL LINES USED         
         LA    R6,0(R6)                                                         
         XC    DISP,DISP                                                        
         MVC   DISP+1(1),8(R1)                 BYTE 0   0=CONTIGUOUS            
         CLI   8(R1),C'P'                               N=DISPLACEMENT          
         BNE   *+8                                      C'P'=132                
         MVI   DISP+1,132                                                       
         LTR   R4,R4                                                            
         BNZ   CH1                                                              
         CLC   12(4,R1),=C'LEN='   PARAMETER 4 C'LEN='                          
         BNE   CH1                                                              
         L     R4,16(R1)           PARAMETER 5 FULLWORD LENGTH                  
         SPACE 2                                                                
*                                  REGISTERS   2 A(STRING)                      
*                                  ---------   3 A(BLOCK)                       
*                                              4 L'STRING                       
*                                              5 WIDTH OF BLOCK                 
*                                              6 MAXIMUM LINES                  
*                                              7 FIELD POINTER                  
*                                              8 FIELD LENGTH                   
*                                              9 LINES SO FAR                   
         EJECT                                                                  
*              CHECK INPUT AND CLEAR BLOCK                                      
         SPACE 3                                                                
CH1      SR    R9,R9                                                            
         LTR   R4,R4                                                            
         BZ    EXIT                                                             
         CH    R5,=H'2'                                                         
         BL    EXIT                                                             
         LTR   R6,R6                                                            
         BZ    EXIT                                                             
         SPACE 2                                                                
         LR    R7,R3                                                            
         LR    R8,R5                                                            
         SH    R8,=H'2'                                                         
         LR    R9,R6                                                            
         SPACE 2                                                                
CHCLEAR  MVI   0(R7),C' '                                                       
         EX    R8,LINECLR                                                       
         OC    DISP,DISP                                                        
         BZ    *+12                                                             
         AH    R7,DISP                                                          
         B     *+6                                                              
         AR    R7,R5                                                            
         BCT   R9,CHCLEAR                                                       
         B     CHLOOP                                                           
         SPACE 2                                                                
LINECLR  MVC   1(0,R7),0(R7)                                                    
         EJECT                                                                  
*              MAIN DIVIDING LOOP                                               
         SPACE 3                                                                
CHLOOP   CLI   0(R2),C' '          IGNORE LEADING SPACES                        
         BNE   CH2                 AT THE BEGINNING OF A LINE                   
         LA    R2,1(R2)                                                         
         BCT   R4,CHLOOP                                                        
         B     EXIT                                                             
         SPACE 2                                                                
CH2      CR    R4,R5               IF REMAINING BYTES CAN FIT                   
         BH    CH4                 IN A LINE THEN THIS IS THE LAST              
         BCTR  R4,R0                                                            
         EX    R4,MOVELINE                                                      
         LA    R9,1(R9)                                                         
         B     EXIT                                                             
         SPACE 2                                                                
CH4      LA    R7,0(R5,R2)         SET UP TO SCAN BACKWARDS                     
         LR    R8,R5               FOR A DIVIDING CHARACTER                     
         SPACE 2                                                                
CH6      CLI   0(R7),C' '                                                       
         BE    CH8                                                              
         CLI   0(R7),C'-'                                                       
         BE    CH8                                                              
         CLI   0(R7),C'/'          EMAIL                                        
         BE    CH8                                                              
         CLI   0(R7),C'.'          EMAIL                                        
         BE    CH8                                                              
         CLI   0(R7),C'@'          EMAIL                                        
         BE    CH8                                                              
         CLI   0(R7),C'_'          EMAIL                                        
         BE    CH8                                                              
         CLI   0(R7),C','                                                       
         BNE   CH7                                                              
         CLI   1(R7),C'0'          IGNORE INTEGER,INTEGER                       
         BL    CH8                                                              
         LR    RE,R7                                                            
         BCTR  RE,0                                                             
         CLI   0(RE),C'0'                                                       
         BL    CH8                                                              
CH7      BCTR  R7,R0                                                            
         BCT   R8,CH6              IF WE DONT FIND THEN ITS TOUGH               
         LR    R8,R5               WE'LL SET FIELD LENGTH = WIDTH               
         BCTR  R8,0                                                             
         SPACE 2                                                                
CH8      CR    R8,R5                                                            
         BNE   *+14                                                             
         CLI   0(R7),C','          IGNORE COMMA AT WIDTH+1                      
         BE    CH7                 TO PRECLUDE LINE STARTING WITH ONE           
         BCTR  R8,R0                                                            
         EX    R8,MOVELINE                                                      
         LA    R9,1(R9)                                                         
         CR    R9,R6               HAVE WE RUN OUT OF LINES                     
         BE    EXIT                                                             
         LA    R8,1(R8)                                                         
         AR    R2,R8               BUMP STRING BY FIELD LENGTH                  
         SR    R4,R8               REDUCE LENGTH BY FIELD LENGTH                
         OC    DISP,DISP                                                        
         BZ    *+12                                                             
         AH    R3,DISP             BUMP TO NEXT PRINT LINE OR ...               
         B     *+6                                                              
         AR    R3,R5               BUMP TO NEXT LINE IN BLOCK                   
         B     CHLOOP                                                           
         SPACE 2                                                                
MOVELINE MVC   0(0,R3),0(R2)                                                    
         SPACE 2                                                                
EXIT     ST    R9,8(R1)            RETURN NO OF LINES USED                      
         XMOD1 1                                                                
         SPACE 1                                                                
CHOPD    DSECT                                                                  
DISP     DS    H                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'009PPECHOP   03/22/99'                                      
         END                                                                    
