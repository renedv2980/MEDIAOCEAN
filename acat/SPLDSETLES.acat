*          DATA SET SPLDSETLES AT LEVEL 010 AS OF 05/01/02                      
*CATALP SPLDSETL                                                                
         TITLE 'LDSETLEN - SPOTPAK LOAD - ADJUST RECORD LENGTHS'                
***********************************************************************         
*                                                                     *         
* PURPOSE IS TO LOAD RECORD WITH SPARE BYTES TO ALLOW                 *         
* FOR ALLOCATIONS AND AFFIDAVIT INSERTION WITHOUT RECORD OVERFLOW     *         
*                                                                     *         
*  PROGRAM RECEIVES SAME PARAMETER LIST AS DALDDS                     *         
*                                                                     *         
*  RELEVANT FIELDS ARE   P2      = A(REC)                             *         
*                        P3+2(2) = REC LEN (MODIFIED ON OUTPUT)       *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
         PRINT NOGEN                                                            
LDSETLEN CSECT                                                                  
         NMOD1 WORKX-WORKD,**SETL**                                             
         USING WORKD,RC                                                         
*                                                                               
         LR    RA,R1               SAVE PARAM REG                               
         USING PARAMS,RA                                                        
*                                                                               
         L     R3,P2               GET REC ADDRESS                              
         USING RECD,R3                                                          
*                                                                               
         BC    0,SETLEN10                                                       
         MVI   *-3,X'F0'           THIS CODE ONE TIME ONLY                      
         BAS   RE,INITIAL                                                       
*                                                                               
SETLEN10 CLI   REC,X'10'           TEST BUYREC                                  
         BNH   EXIT                                                             
         CLI   REC,X'FF'                                                        
         BE    EXIT                                                             
         MVC   BYTE,REC                                                         
         NI    BYTE,X'0F'          DROP AGY                                     
         CLI   BYTE,1              TEST SPOT TV                                 
         BNE   EXIT                                                             
*                                                                               
         SR    RE,RE                                                            
         ICM   RE,3,REC+13                                                      
         AR    RE,R3               POINT TO EOR                                 
         XC    0(2,RE),0(RE)       AND INSURE NO ELEM CODE/LEN FOLLOWS          
*                                                                               
         XC    CNTRS,CNTRS                                                      
         LA    R6,BDELEM                                                        
*                                                                               
         CLI   REC+3,X'FF'         TEST POL BUY                                 
         BNE   SETLEN40                                                         
         EJECT                                                                  
* POL BUY PROCESSING *                                                          
         SPACE 1                                                                
SETLEN20 SR    R0,R0                                                            
         ICM   R0,1,1(R6)                                                       
         BZ    EXIT                                                             
         AR    R6,R0                                                            
SETLEN22 CLI   0(R6),0                                                          
         BE    SETLEN50                                                         
         CLI   0(R6),X'0B'                                                      
         BL    SETLEN20                                                         
         CLI   0(R6),X'0D'                                                      
         BH    SETLEN20                                                         
* HAVE POOL DATE ELEM                                                           
         CLI   1(R6),10            TEST ALLOCATED                               
         BH    SETLEN24                                                         
         TM    6(R6),X'04'         TEST HIATUS                                  
         BO    SETLEN20                                                         
         L     RE,UNALL                                                         
         LA    RE,1(RE)                                                         
         ST    RE,UNALL                                                         
         B     SETLEN20                                                         
         SPACE 1                                                                
* TEST IF WITHIN PERIOD LIKELY TO GET AFFIDS                                    
         SPACE 1                                                                
SETLEN24 DS    0H                                                               
         OC    4(2,R6),4(R6)       TEST PAID                                    
         BNZ   SETLEN20            YES - IGNORE                                 
         TM    6(R6),X'C0'         TEST MINUS OR MINUSSED                       
         BNZ   SETLEN20                                                         
         CLC   2(2,R6),DATELO                                                   
         BL    SETLEN20                                                         
         CLC   2(2,R6),DATEHI                                                   
         BH    SETLEN20                                                         
         L     RE,NOAFDS                                                        
         LA    RE,1(RE)                                                         
         ST    RE,NOAFDS                                                        
         SPACE 1                                                                
* AFFID(S) SHOULD IMMEDIATELY FOLLOW *                                          
         SPACE 1                                                                
SETLEN26 SR    R0,R0                                                            
         ICM   R0,1,1(R6)                                                       
         BZ    EXIT                                                             
         AR    R6,R0                                                            
         CLI   0(R6),X'10'         TEST AFFID                                   
         BNE   SETLEN22                                                         
         L     RE,NOAFDS                                                        
         BCTR  RE,0                                                             
         ST    RE,NOAFDS                                                        
         B     SETLEN26                                                         
         EJECT                                                                  
* NON-POL PROCESSING *                                                          
         SPACE 1                                                                
SETLEN40 SR    R0,R0                                                            
         ICM   R0,1,1(R6)                                                       
         BZ    EXIT                                                             
         AR    R6,R0                                                            
SETLEN42 CLI   0(R6),0                                                          
         BE    SETLEN50                                                         
         CLI   0(R6),X'06'                                                      
         BL    SETLEN40                                                         
         CLI   0(R6),X'08'                                                      
         BH    SETLEN40                                                         
* HAVE NON-POL DATE ELEM                                                        
         OC    4(2,R6),4(R6)       TEST PAID                                    
         BNZ   SETLEN40            YES - IGNORE                                 
         CLC   2(2,R6),DATELO                                                   
         BL    SETLEN40                                                         
         CLC   2(2,R6),DATEHI                                                   
         BH    SETLEN40                                                         
*                                                                               
         ZIC   R0,7(R6)            GET NUMBER OF SPOTS                          
         TM    6(R6),X'80'         TEST MINUS                                   
         BZ    *+6                                                              
         LNR   R0,R0                                                            
         L     RE,NOAFDS                                                        
         AR    RE,R0                                                            
         ST    RE,NOAFDS                                                        
*                                                                               
SETLEN44 SR    R0,R0                                                            
         ICM   R0,1,1(R6)                                                       
         BZ    EXIT                                                             
         AR    R6,R0                                                            
         CLI   0(R6),X'10'         TEST AFFID                                   
         BNE   SETLEN42                                                         
         L     RE,NOAFDS                                                        
         BCTR  RE,0                                                             
         ST    RE,NOAFDS                                                        
         B     SETLEN44                                                         
         EJECT                                                                  
* ALL SPOTS COUNTED - ADJUST REC LENGTH *                                       
         SPACE 1                                                                
SETLEN50 L     R0,UNALL                                                         
         SLL   R0,2                X 4                                          
         L     R1,NOAFDS                                                        
         MH    R1,=H'6'            X 6                                          
         AR    R0,R1                                                            
         BNP   EXIT                NEVER MAKE RECORD SHORTER                    
         SR    R1,R1                                                            
         LH    R1,P3+2             GET REC LEN                                  
         AR    R1,R0                                                            
         C     R1,=F'1976'         COMPARE TO MAX LEN                           
         BNH   *+8                                                              
         L     R1,=F'1976'                                                      
         STH   R1,P3+2             SET NEW LENGTH                               
*                                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
* INITIALIZATION CODE TO CALCULATE LIMITS FOR AFFIDAVITS *                      
         SPACE 1                                                                
INITIAL  NTR1                                                                   
         LA    R0,L'ADCONS/4       MAKE SURE ALL ADCONS RESOLVED                
         LA    R1,ADCONS                                                        
INIT2    OC    0(4,R1),0(R1)                                                    
         BZ    INIT20                                                           
         LA    R1,4(R1)                                                         
         BCT   R0,INIT2                                                         
*                                                                               
         GOTO1 VDATCON,DMCB,(5,0),TODAY         GET TODAY IN YYMMDD             
         GOTO1 VGETDAY,DMCB,TODAY,WORK                                          
*                                                                               
* ADVANCE TO SUNDAY IF NECESSARY                                                
*                                                                               
         LA    R0,7                                                             
         ZIC   R1,DMCB                                                          
         SR    R0,R1                                                            
         GOTO1 VADDAY,DMCB,TODAY,WORK,(R0)                                      
*                                                                               
* GET BROADCAST MONTH DATES                                                     
*                                                                               
         GOTO1 VGETBRD,DMCB,WORK,WORK+6                                         
         CLC   WORK(6),WORK+12          IS SUNDAY IN THIS BDCST MONTH           
         BL    INIT10                   YES                                     
*                                                                               
* THE SUNDAY WILL END THE BROADCAST MONTH                                       
*                                                                               
         GOTO1 VDATCON,DMCB,WORK+12,(2,DATEHI)                                  
         MVC   WORK(4),WORK+12                 MOVE MONTH END YYMM              
         BAS   RE,BACKUP30                                                      
         GOTO1 VDATCON,DMCB,WORK+6,(2,DATELO)  USE BDCST MONTH START            
         B     SETLEN10                                                         
         SPACE 1                                                                
* THIS WEEK WILL NOT END THE BROADCAST MONTH                                    
         SPACE 1                                                                
INIT10   DS    0H                                                               
         BAS   RE,BACKUP30                                                      
         GOTO1 VDATCON,DMCB,WORK+12,(2,DATEHI)  USE BDCST MONTH END             
*                                                                               
         MVC   WORK(4),WORK+12     NOW BACK UP ANOTHER BDCST MONTH              
         BAS   RE,BACKUP30                                                      
         GOTO1 VDATCON,DMCB,WORK+6,(2,DATELO)                                   
         B     EXIT                                                             
         EJECT                                                                  
* IF UNRESOLVED ADCONS, DISABLE THIS ROUTINE *                                  
         SPACE 1                                                                
INIT20   MVC   LDSETLEN(2),=X'07FE' DISABLE MODULE                              
         B     EXIT                                                             
         SPACE 2                                                                
* SUBROUTINE BACKS UP TO MONTH 30 DAYS PRIOR                                    
* AND RETURNS BROADCAST MONTH START/END DATES IN WORK+6(12)                     
         SPACE 1                                                                
BACKUP30 NTR1                                                                   
         MVC   WORK+4(2),=C'15'                                                 
         LA    R0,30                                                            
         LNR   R0,R0                                                            
         GOTO1 VADDAY,DMCB,WORK,WORK+6,(R0)                                     
         MVC   WORK(6),WORK+6                                                   
         GOTO1 VGETBRD,DMCB,WORK,WORK+6                                         
         B     EXIT                                                             
*                                                                               
DATELO   DC    H'0'                                                             
DATEHI   DC    H'0'                                                             
*                                                                               
         DS    0A                                                               
ADCONS   DS    0XL16                                                            
VDATCON  DC    V(DATCON)                                                        
VADDAY   DC    V(ADDAY)                                                         
VGETDAY  DC    V(GETDAY)                                                        
VGETBRD  DC    V(GETBROAD)                                                      
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
PARAMS   DSECT                                                                  
P1       DS    A                                                                
P2       DS    A                                                                
P3       DS    A                                                                
P4       DS    A                                                                
P5       DS    A                                                                
P6       DS    A                                                                
         SPACE 2                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
BYTE     DS    C                                                                
DMCB     DS    6F                                                               
TODAY    DS    CL6                                                              
WORK     DS    CL32                                                             
*                                                                               
         DS    0F                                                               
CNTRS    DS    0XL8                                                             
NOAFDS   DS    F                                                                
UNALL    DS    F                                                                
WORKX    EQU   *                                                                
         EJECT                                                                  
RECD     DSECT                                                                  
REC      EQU   *                                                                
         ORG   REC                                                              
       ++INCLUDE SPGENBUY                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'010SPLDSETLES05/01/02'                                      
         END                                                                    
