*          DATA SET SPORDAUP   AT LEVEL 004 AS OF 10/23/19                      
*CATALP SPORDAUP                                                                
         TITLE 'SPORDAUP -- SPOT DARE ORDER ACTIVITY UPDATE'                    
*=====================================================================*         
*                                                                               
* HISTORY                                                                       
* -------                                                                       
*       WHEN                                                                    
* WHO  DDMMMYR LVL WHAT                                                         
* ---- ------- --- ----                                                         
* HWON 07SEP16 001 ACCEPT OFFLINE BUY CHANGES, BUILD TABLE OF                   
*                  MED/CLT/PRD/PIG/EST/FLT/STATION/CASH|TRADE/                  
*                                                                               
* HWON 06SEP17 002 SPEC-11868 - IMPROVE PERFORMANCE                             
*                  - ADD'L CARD SUPPORT : COUNT=, DELAY=, SLOWTIME=             
*                                                                               
* HWON 04OCT17 003 SPEC-16055 - FIX ISSUE JOB NOT UPDATING ORDERS               
*                  -FIX ISSUE WHERE OVERNIGHT ACTIVITY CAUSED THE JOB           
*                   TO GET STUCK RESULTING IN NOTHING BEING PROCESSED           
*                                                                               
*=====================================================================*         
*                                                                               
*=====================================================================*         
******************** SPOT DARE ORDER UPDATE PROCESS *******************         
*                                                                               
* ORDAUP_STEP1- INITIALIZATION STEP                                             
*                                                                               
* ORDAUP_STEP2- READ INPUT, PEEL BUY RECORDS AND PUT TO SORTER TO SORT          
*                                                                               
* ORDAUP_STEP3- READ THRU SORTER, PUT ACTIVITY TO WORKFILE                      
*                                                                               
* ORDAUP_STEP4- READ WORKFILE AND PUT TO SORTER TO SORT ACTIVITY                
*                                                                               
* ORDAUP_STEP5- READ SORTED ACTIVITY RECS & UPDATE ORDER RESEND FLAG            
*                                                                               
*=====================================================================*         
                                                                                
ORDAUPDT CSECT                                                                  
         PRINT NOGEN                                                            
         ENTRY UTL                                                              
         ENTRY SSB                                                              
*                                                                               
         NBASE 0,*ORDAUPDT,VREGSAVE                                             
*                                                                               
QSPTFIL  EQU   X'21'                                                            
*                                                                               
         LAY   R9,4096(RB)                                                      
         LAY   RA,8192(RB)                                                      
         USING ORDAUPDT,RB,R9                                                   
         LAY   RC,COMMWORK                                                      
         USING COMMWORK,RC                                                      
*                                                                               
         L     R8,=V(CPRINT)                                                    
         USING DPRINT,R8                                                        
*                                                                               
         MVC   DUB,SPACES                                                       
         MVC   DUB(4),=C'T00A'                                                  
*                                                                               
         MVI   WORK,QRCPACK        LOAD RCPACK                                  
         GOTOR =V(HEXOUT),DMCB,WORK,DUB+4,1,0                                   
         GOTOR =V(LOADER),DMCB,DUB,0                                            
         ICM   RE,15,4(R1)                                                      
         JZ    *+2                                                              
         ST    RE,VRCPACK                                                       
*                                                                               
         MVI   WORK,QSTAPACK       LOAD STAPACK                                 
         GOTOR =V(HEXOUT),DMCB,WORK,DUB+4,1,0                                   
         GOTOR =V(LOADER),DMCB,DUB,0                                            
         ICM   RE,15,4(R1)                                                      
         JZ    *+2                                                              
         ST    RE,VSTAPACK                                                      
*                                                                               
         MVI   WORK,QCABLETB       LOAD CABLETAB                                
         GOTOR =V(HEXOUT),DMCB,WORK,DUB+4,1,0                                   
         GOTOR =V(LOADER),DMCB,DUB,0                                            
         ICM   RE,15,4(R1)                                                      
         JZ    *+2                                                              
         ST    RE,VCABLETB                                                      
*                                                                               
         LAY   R1,STAWORK          PASS A(T00A9E) TO STAPACK                    
         XC    0(32,R1),0(R1)                                                   
         USING STAPACKD,R1                                                      
*                                                                               
         MVI   STAPACT,QSTP_T00A9E                                              
         MVC   STAPACOM,VCABLETB                                                
         GOTO1 VSTAPACK,(R1)                                                    
         DROP  R1                                                               
*                                                                               
         GOTOR =V(STXITER),DMCB,A(DUMPLIST)                                     
         XC    WORK,WORK                                                        
         ST    RB,WORK                                                          
         L     R4,VREGSAVE                                                      
         A     R4,=F'20000'                                                     
         ST    R4,WORK+4                                                        
         OI    WORK+4,X'80'                                                     
         GOTOR =V(STXITER),DMCB,WORK                                            
*                                                                               
         L     RE,=A(COMFACS)                                                   
         USING COMFACSD,RE                                                      
         MVC   CDATAMGR,=V(DATAMGR)                                             
         DROP  RE                                                               
*                                                                               
         B     INITCARD                                                         
*                                                                               
VREGSAVE DC    V(REGSAVE)                                                       
*                                                                               
         EJECT                                                                  
*                                                                               
*======================================================================         
*************************** ORDAUP_STEP1 ******************************         
*----------------------------------------------------------------------         
*  VALIDATE CARDS                                                               
*  OPEN FILES                                                                   
*  INITIALIZE SORTER                                                            
*  INITIALIZE REPORT                                                            
*  BUILD AGENCY TABLES                                                          
*  INITIALIZE ORDER ACTIVITY TABLES                                             
*======================================================================         
*                                                                               
*=========================================================                      
* VALIDATE CARDS INPUT                                                          
*   STEP 1 - VALIDATE FIRST CARDS                                               
*                                                                               
*  REQUIRED CARDS                                                               
*   - SPOTXYAA  = XY SPOT SYSTEM & AA OPTIONAL AGY ALPHA                        
*                                                                               
*  OPTIONAL CARDS -  IF PRESENT MUST PRECEED SPOTXYAA                           
*   - DSPACE=                                                                   
*   - DDSIO=                                                                    
*=========================================================                      
INITCARD DS    0H                                                               
*                                                                               
         CLI   INITSUCC,C'Y'       INIT ALREADY?                                
         BE    INITX                                                            
*                                                                               
         GOTOR =V(CARDS),DMCB,CARD,=C'RE00'                                     
*                                                                               
         MVC   P(80),CARD                                                       
         GOTOR =V(PRINTER)                                                      
*                                                                               
         CLC   =C'DSPACE=',CARD                                                 
         BNE   ICD010                                                           
         LAY   RE,SSB                                                           
         MVC   SSODSPAC-SSOOFF(1,RE),CARD+7                                     
         B     INITCARD                                                         
*                                                                               
ICD010   CLC   =C'DDSIO=',CARD     FOR TESTING                                  
         BNE   ICD020                                                           
         L     RF,=V(DDSIO)                                                     
         MVC   0(8,RF),CARD+6                                                   
         B     INITCARD                                                         
*                                                                               
ICD020   XC    DATLEN,DATLEN                                                    
         MVC   DATLEN(2),=H'10'                                                 
         CLC   =C'SPOT',CARD       SPOTX....                                    
         BNE   INITCERR                                                         
         BRAS  RE,GETSEAA          GET SENUM                                    
         BE    ICD030                                                           
*                                                                               
INITCERR MVC   P(30),=C'** ERROR * SYSTEM NOT VALID **'                         
         GOTOR =V(LOGIO),DMCB,1,(30,P)                                          
         GOTOR =V(PRINTER)                                                      
CANCEL   DC    0H'0'                                                            
         ABEND 999                                                              
*                                                                               
ICD030   MVC   SVSYSCRD,CARD       SPOTXYAA - MODIFIED INPUT CARD               
*                                                                               
         ZAP   LINE,=P'99'                                                      
         GOTOR =V(DATCON),DMCB,(5,0),(3,TODAY)                                  
*                                                                               
*==========================                                                     
* VALIDATE OPTIONAL CARDS                                                       
*==========================                                                     
ICD040   DS    0H                                                               
         GOTOR =V(CARDS),DMCB,CARD,=C'RE00'                                     
*                                                                               
         MVC   P(80),CARD                                                       
         GOTOR =V(PRINTER)                                                      
*                                                                               
         CLC   =C'/*',CARD         NO MORE =V(CARDS)?                           
         BE    INITCARDX                                                        
*                                                                               
         CLI   CARD,C'*'           COMMENTED OUT CARD?                          
         BE    ICD040                                                           
*                                                                               
         CLC   =C'WRITE=',CARD     WRITE TO SPTFILE                             
         BNE   ICD050                                                           
         MVI   WRITESW,C'N'                                                     
         CLI   CARD+6,C'N'                                                      
         BE    ICD040                                                           
         CLI   CARD+6,C'Y'                                                      
         BNE   ICD070                                                           
         MVI   WRITESW,C'Y'                                                     
         B     ICD040                                                           
*                                                                               
ICD050   CLC   =C'TEST=',CARD      WRITE ORDER RECORDS TO OUTPUT FILE           
         BNE   ICD070                                                           
         CLI   CARD+5,C'N'                                                      
         BE    ICD040                                                           
         CLI   CARD+5,C'Y'                                                      
         BNE   ICD070                                                           
         MVI   TESTSW,C'Y'                                                      
         B     ICD040                                                           
*                                                                               
ICD070   CLC   =C'TRACE=',CARD     TRACE SOME I/O                               
         BNE   ICD080                                                           
         CLI   CARD+6,C'N'                                                      
         BE    ICD040                                                           
         CLI   CARD+6,C'Y'                                                      
         BNE   ICD080                                                           
         MVI   TRACSW,C'Y'                                                      
         B     ICD040                                                           
*                                                                               
ICD080   CLC   =C'TODAY=',CARD     FOR TESTING                                  
         BNE   ICD090                                                           
         GOTOR =V(DATCON),DMCB,(4,CARD+6),(3,TODAY)                             
         B     ICD040                                                           
*                                                                               
ICD090   CLC   =C'RECOVER=',CARD   TO RUN UPDATIVE SOON                         
         BNE   ICD100                                                           
         CLI   CARD+8,C'N'                                                      
         BE    ICD040                                                           
         CLI   CARD+8,C'W'                                                      
         BNE   ICD100                                                           
         MVI   MCRECOVR,C'W'                                                    
         B     ICD040                                                           
*                                                                               
ICD100   CLC   =C'FACPAK=',CARD    TO RUN UPDATIVE SOON                         
         BNE   ICD110                                                           
         MVC   MCFACPAK,CARD+7                                                  
         B     ICD040                                                           
*                                                                               
ICD110   CLC   =C'UPDID=',CARD                                                  
         BNE   ICD120                                                           
         GOTOR =V(DATAMGR),DMCB,=C'UPDID'                                       
         L     RF,12(R1)                                                        
         MVC   0(2,RF),CARD+6                                                   
         B     ICD040                                                           
*                                                                               
ICD120   CLC   =C'INPUT=DISK',CARD                                              
         BNE   ICD130                                                           
         MVI   MCINPUT,C'D'                                                     
         BRAS  RE,SETOPS           SET TO WAIT ON OPER ECB TO END               
         B     ICD040                                                           
*                                                                               
ICD130   CLC   =C'INPUT=TAPE',CARD                                              
         BNE   ICD140                                                           
         MVI   MCINPUT,C'T'                                                     
         B     ICD040                                                           
*                                                                               
ICD140   CLC   =C'TIMER=',CARD                                                  
         BNE   ICD150                                                           
*                                                                               
         LA    R0,4                TIMER=MMSS? MAKE SURE 4 DIGITS               
         CLC   CARD+10(2),SPACES   4 OR 6 DIGITS?                               
         BE    *+8                                                              
         LA    R0,6                TIMER=HHMMSS? MAKE SURE 6 DIGITS             
*                                                                               
         LA    R1,CARD+6                                                        
ICD145   CLI   0(R1),C'0'                                                       
         BL    ICDBDTIM            INVALID TIMER, OUTPUT ERROR                  
         CLI   0(R1),C'9'                                                       
         BH    ICDBDTIM            INVALID TIMER, OUTPUT ERROR                  
         LA    R1,1(R1)                                                         
         BCT   R0,ICD145                                                        
*                                                                               
         CLC   CARD+12(L'CARD-12),SPACES  ANY GARBAGE AFTER?                    
         BNE   ICDBDTIM                                                         
*                                                                               
         LA    RE,3                                                             
         CLC   CARD+10(2),SPACES   4 OR 6 DIGITS?                               
         BE    *+8                                                              
         LA    RE,5                                                             
         CLC   CARD+6(0),=C'000000' HAVE TIMER OF 0?                            
         EX    RE,*-6                 YES, OUTPUT ERROR                         
         BE    ICDBDTIM2                                                        
*                                                                               
         MVC   WAITTIME+2(4),CARD+6   MOVE MMSS                                 
         CLC   CARD+10(2),SPACES   4 OR 6 DIGITS?                               
         BE    *+10                                                             
         MVC   WAITTIME(6),CARD+6  MOVE HHMMSS                                  
*                                                                               
         CLC   WAITTIME(4),=C'0060' HAVE TIMER OF GREATER THAN 1HOUR            
         BNL   ICDBDTIM3             YES, OUTPUT ERROR                          
         B     ICD040                                                           
*                                                                               
ICDBDTIM DS    0H                  INV. CARD, BAD TIME, OUTPUT ERROR            
         MVC   P(57),=C'**TIMER MUST BE FOUR DIGIT (MMSS) OR SIX DIGIT +        
               (HHMMSS)**'                                                      
         GOTOR =V(PRINTER)                                                      
         B     EOJX                                                             
*                                                                               
ICDBDTIM2 DS   0H                  INV. CARD, BAD TIME, OUTPUT ERROR            
         MVC   P(24),=C'**TIMER CANNOT BE ZERO**'                               
         GOTOR =V(PRINTER)                                                      
         B     EOJX                                                             
*                                                                               
ICDBDTIM3 DS   0H                  INV. CARD, BAD TIME, OUTPUT ERROR            
         MVC   P(36),=C'**TIMER CANNOT BE MORE THAN 1 HOUR**'                   
         GOTOR =V(PRINTER)                                                      
         B     EOJX                                                             
*                                                                               
ICD150   CLC   =C'COUNT=',CARD     MAX COUNT OVERRIDE?                          
         BNE   ICD160                                                           
*                                                                               
         LA    R0,6                COUNT=NNNNNN? MAKE SURE 6 DIGITS             
         LA    R1,CARD+6                                                        
ICD155   CLI   0(R1),C'0'                                                       
         BL    ICDBDCNT            INVALID COUNT, OUTPUT ERROR                  
         CLI   0(R1),C'9'                                                       
         BH    ICDBDCNT            INVALID COUNT, OUTPUT ERROR                  
         LA    R1,1(R1)                                                         
         BCT   R0,ICD155                                                        
*                                                                               
         CLC   CARD+12(L'CARD-12),SPACES  ANY GARBAGE AFTER?                    
         BNE   ICDBDCNT                                                         
*                                                                               
         PACK  MAXRECS,CARD+6(6)                                                
         NI    MAXRECS+L'MAXRECS-1,X'F0'  CLEAR THE TRAILING F AND              
         OI    MAXRECS+L'MAXRECS-1,X'0C'  CONVERT TO PACK FORMAT                
*                                                                               
         CP    MAXRECS,=PL4'100'   COUNT LESS THAN 100 RECORDS?                 
****     BL    ICDBDCNT2            YES, OUTPUT ERROR                           
         B     ICD040                                                           
*                                                                               
ICDBDCNT DS    0H                  INV. CARD, BAD COUNT, OUTPUT ERROR           
         MVC   P(24),=C'**COUNT NOT SIX DIGITS**'                               
         GOTOR =V(PRINTER)                                                      
         B     EOJX                                                             
*                                                                               
ICDBDCNT2 DS   0H                  INV. CARD, BAD COUNT, OUTPUT ERROR           
         MVC   P(33),=C'**COUNT CANNOT BE LESS THAN 100**'                      
         GOTOR =V(PRINTER)                                                      
         B     EOJX                                                             
*                                                                               
ICD160   CLC   =C'DELAY=',CARD     DELAY PROCESSING OVERRIDE?                   
         BNE   ICD170                                                           
*                                                                               
         LA    R0,6                DELAY=HHMMSS?  MAKE SURE 6 DIGITS            
         LA    R1,CARD+6                                                        
ICD165   CLI   0(R1),C'0'                                                       
         BL    ICDBDDLY            INVALID DELAY, OUTPUT ERROR                  
         CLI   0(R1),C'9'                                                       
         BH    ICDBDDLY            INVALID DELAY, OUTPUT ERROR                  
         LA    R1,1(R1)                                                         
         BCT   R0,ICD165                                                        
*                                                                               
         CLC   CARD+12(L'CARD-12),SPACES  ANY GARBAGE AFTER?                    
         BNE   ICDBDDLY                                                         
*                                                                               
         PACK  DELAY,CARD+6(6)                                                  
         NI    DELAY+L'DELAY-1,X'F0'   CLEAR THE TRAILING F AND                 
         OI    DELAY+L'DELAY-1,X'0C'   CONVERT TO PACK FORMAT                   
*                                                                               
         CP    DELAY,=PL4'3000'        BUFFER >30 MINS?                         
         BH    ICDBDDLY2                YES, INVALID, OUTPUT ERROR              
         B     ICD040                                                           
*                                                                               
ICDBDDLY DS    0H                  INV. CARD, BAD DELAY BUFFER                  
         MVC   P(42),=C'**DELAY BUFFER MUST BE SIX DIGITS HHMMSS**'             
         GOTOR =V(PRINTER)                                                      
         B     EOJX                                                             
*                                                                               
ICDBDDLY2 DS   0H                  INV. CARD, BAD DELAY BUFFER                  
         MVC   P(43),=C'**DELAY BUFFER CANNOT BE MORE THAN 30MINS**'            
         GOTOR =V(PRINTER)                                                      
         B     EOJX                                                             
*                                                                               
ICD170   CLC   =C'SLOWTIME=',CARD  SLOWTIME OVERRIDE?                           
         BNE   ICDUNKN                                                          
*                                                                               
         LA    R0,6                SLOWTIME=HHMMSS? MAKE SURE 6 DIGITS          
         LA    R1,CARD+9                                                        
ICD175   CLI   0(R1),C'0'                                                       
         BL    ICDBDSLT            INVALID SLOW TIME, OUTPUT ERROR              
         CLI   0(R1),C'9'                                                       
         BH    ICDBDSLT            INVALID SLOW TIME, OUTPUT ERROR              
         LA    R1,1(R1)                                                         
         BCT   R0,ICD175                                                        
*                                                                               
         CLC   CARD+15(L'CARD-15),SPACES  ANY GARBAGE AFTER?                    
         BNE   ICDBDSLT                                                         
*                                                                               
         PACK  SLOWTIME,CARD+9(6)                                               
         NI    SLOWTIME+L'SLOWTIME-1,X'F0'   CLEAR THE TRAILING F AND           
         OI    SLOWTIME+L'SLOWTIME-1,X'0C'   CONVERT TO PACK FORMAT             
         B     ICD040                                                           
*                                                                               
ICDBDSLT DS    0H                  INV. CARD, BAD SLOWTIME                      
         MVC   P(39),=C'**SLOW TIME MUST BE SIX DIGITS HHMMSS**'                
         GOTOR =V(PRINTER)                                                      
         B     EOJX                                                             
*                                                                               
ICDUNKN  MVC   P(26),=C'**UNKNOWN PARAMETER CARD**'                             
         GOTOR =V(PRINTER)                                                      
         B     EOJX                                                             
*                                                                               
*                                                                               
INITCARDX DS   0H                  DONE WITH CARD VALIDATION                    
*                                                                               
*=============                                                                  
* OPEN FILES                                                                    
*=============                                                                  
         BRAS  RE,OPENFILE                                                      
*                                                                               
         CLI   MCINPUT,C'D'        TEST DISK INPUT                              
         BE    INIT010              YES                                         
*                                                                               
         LAY   R4,RECVIN           OPEN INPUT TAPE                              
         OPEN  ((4),(INPUT))                                                    
         LTR   RF,RF                                                            
         JNZ   *+2                                                              
*                                                                               
INIT010  CLI   TESTSW,C'Y'         WAS TEST OUTPUT FILE REQUESTED               
         BNE   INIT020              NO                                          
         LAY   R4,TESTOUT                                                       
         OPEN  ((4),(OUTPUT))                                                   
         LTR   RF,RF                                                            
         JNZ   *+2                                                              
*                                                                               
INIT020  CLI   MCRECOVR,C'W'       TEST UDPATIVE SOON                           
         BNE   INIT030                                                          
         BRAS  RE,STUPSOON         SETUP JOB AS UPDATIVE SOON                   
*                                                                               
*====================                                                           
* INITIALIZE SORTER                                                             
*====================                                                           
INIT030  GOTOR =V(SORTER),DMCB,SRTCARDR,RECCARDR                                
*                                                                               
*====================                                                           
* INITIALIZE REPORT                                                             
*====================                                                           
         MVC   TITLE(26),=CL30'SPOTNN BUY ACTIVITY REPORT'                      
         MVC   TITLE(6),SVSYSCRD                                                
         LAY   RE,BUYMID1                                                       
         MVC   MID1(L'BUYMID1),0(RE)                                            
         LAY   RE,BUYMID2                                                       
         MVC   MID2(L'BUYMID2),0(RE)                                            
         XC    OLDAAGY,OLDAAGY                                                  
         ZAP   LINE,=P'99'         FORCE NEW PAGE                               
*                                                                               
*======================================================                         
* INITIALIZE THE AGYTAB                                                         
* READ THE AGENCY HEADERS AND BUILD A TABLE IN AGYTAB                           
* THAT IDENTIFIES AGENCY AS US OR CANADIAN                                      
*======================================================                         
*                                                                               
         BRAS  RE,BLDAG                                                         
*                                                                               
*=========================                                                      
* INITIALIZE ORDA TABLES                                                        
*=========================                                                      
         MVI   INITSUCC,C'Y'       DON'T INIT AGAIN                             
*                                                                               
INITX    LAY   R4,ORDATAB1         THE MAIN ORDA TABLE                          
         BRAS  RE,ORDACLR                                                       
*                                                                               
         EJECT                                                                  
*======================================================================         
*************************** ORDAUP_STEP2 ******************************         
*----------------------------------------------------------------------         
*   PEEL AND SORT INPUT RECOVERY FILE                                           
*======================================================================         
PEELSORT LAY   R4,WORKFILE                                                      
         OPEN  ((4),(OUTPUT))                                                   
*                                                                               
         MVI   RCVEOFSW,C'N'       INIT RECOVER EOF SWITCH                      
*                                                                               
PLSNEXT  CLI   MCINPUT,C'D'        TEST INPUT=DISK                              
         BNE   PLSNEXTT              NO, GO READ INPUT FROM TAPE                
*                                                                               
         BRAS  RE,READRCV          GO READ RECOVERY FILE                        
         BNE   PLSXEND                                                          
         TM    DMCB+8,X'80'        TEST EOF                                     
         BO    PLSXEND                                                          
*                                                                               
         L     RF,OPECB                                                         
         TM    0(RF),X'40'         TEST OPER EC POSTED                          
         BO    PLS010               YES, RELAX RULES & PROC TIL RCV-EOF         
*                                                                               
         CP    RECCOUNT,MAXRECS    READ MAX COUNT RECORDS?                      
         BL    PLS010               NO                                          
*                                                                               
* CHECK IF WE'RE FALLING BEHIND BY AN HOUR AND TO SEND EMAIL                    
*                                                                               
         TIME                                                                   
         SRL   R0,8                SHIFT HHMMSSUU DROPPING MILLISECS            
*                                                                               
         MVC   PACKOF4B,RS$RTIME   TIME 0HHMMSSC WHERE SIGN=F IF 1ST            
         NI    PACKOF4B,X'FF'-RS$RTIMEFID+RS$RTIMEEXT   CLEAR FLAGS             
         AP    PACKOF4B,SLOWTIME   ADJUST, ADD SLOWTIME                         
*                                                                               
         L     RE,PACKOF4B                                                      
         SRL   RE,4                DROP THE SIGN                                
         CR    RE,R0               ARE WE FALLING BEHIND?                       
         BNL   PLSXEND              NO                                          
*                                                                               
* JOB IS FALLING BEHIND, SEND EMAIL OR WTO                                      
*                                                                               
         MVC   PACKOF4B,SLOWTIME                                                
         UNPK  WORK(8),PACKOF4B           FNFNFNFNFNFNCN                        
*                                                                               
         MVC   SLOWMSG+26(2),WORK+2                                             
         MVC   SLOWMSG+29(2),WORK+4                                             
*                                                                               
         LA    R1,EMSG                                                          
         MVC   0(L'AUTOMSG,R1),AUTOMSG                                          
         MVC   EMSG+L'AUTOMSG(L'SLOWMSG),SLOWMSG                                
*                                                                               
         CLI   SLOWEML,C'Y'         SENT ONE EMAIL ALREADY?                     
         BNE   PLS001                                                           
         WTO   TEXT=SLOWMSGH,MCSFLAG=HRDCPY                                     
         B     PLSXEND                                                          
*                                                                               
PLS001   OC    EMSG,SPACES                                                      
         GOTO1 =V(DATAMGR),DMCB,=C'OPMSG',(L'AUTOMSG+L'SLOWMSG,EMSG)            
*                                                                               
         MVI   SLOWEML,C'Y'        EMAIL SENT                                   
         B     PLSXEND                                                          
*                                                                               
PLSNEXTT LA    R0,RS$RECVHDR-4     READ NEXT INPUT FROM TAPE                    
         LAY   R1,RECVIN                                                        
         GET   (1),(0)                                                          
*                                                                               
*================================================                               
* FIX FOR LEFTOVER JUNK IN NEW RECOVERY RECORDS                                 
*================================================                               
R#       USING BUYKEY,RKEY                                                      
PLS010   CLI   RS$RFILTY,QSPTFIL   TEST SPTFILE                                 
         BNE   PLSNEXT               NO, SKIP                                   
         CLI   R#.BUYKAM,X'10'     TEST BUYREC (LOWER BOUND)                    
         BL    PLSNEXT               NO, SKIP                                   
         MVC   BYTE,R#.BUYKAM                                                   
         NI    BYTE,X'0F'                                                       
         CLI   BYTE,X'01'          TV                                           
         BE    PLS020               YES                                         
         CLI   BYTE,X'02'          RADIO?                                       
         BE    PLS020               YES                                         
         CLI   BYTE,X'04'          NETWORK RADIO?                               
         BNE   PLSNEXT              NO                                          
*                                                                               
*===============================================================                
* IN THE INITIAL RELEASE, WE ARE ONLY TRACKING CHANGES MADE BY                  
*   BUY (VIA SBTK) AND LINK (AKA SBTK) PROGRAM                                  
* NOTE: EXPLICIT ONLINE PROG CHECKS INFERS OFFLINES WILL BE SKIPPED             
*===============================================================                
PLS020   CLI   RS$RPRG,X'11'       SPOT BUY PROGRAM CHANGE?                     
         BE    PLS040                YES                                        
         CLI   RS$RPRG,X'1E'       SPOT LINK PROGRAM CHANGE?                    
         BNE   PLSNEXT               NO, IMPLICITLY SKIP OFFLINE                
*                                                                               
PLS040   LA    RE,RS$RECVHDR-4                                                  
         AH    RE,0(RE)                                                         
         XC    0(2,RE),0(RE)       PUT ZEROES AT END OF RECOVER RECORD          
*                                                                               
         LA    RE,R#.BUYREC        POINT TO BUY REC                             
         AH    RE,13(RE)                                                        
         XC    0(2,RE),0(RE)       PUT ZEROES AT END OF BUY RECORD              
*                                                                               
         TM    R#.BUYKAM,X'08'     TEST BUYREC COPY                             
         BO    PLSNEXT               YES, SKIP/BYPASS                           
*                                                                               
         CLI   RS$RTASKID,RS$RBACKOUT DELETED RECOVERY RECORD?                  
         BE    PLSNEXT                 YES, SKIP BACKED OUT RECORDS             
*                                                                               
         CLI   RS$RRECTY,RS$RRECTADD TEST ADD                                   
         BE    PLS060                  YES, GO PROCESS                          
*                                                                               
         OC    RS$RSIN,RS$RSIN     TEST SIN=0 (CHANGE W/O COPY)                 
         BZ    PLSNEXT              YES, SKIP                                   
         CLI   RS$RSIN,RS$ROCHGOLY TEST IF CHANGES W/O COPY                     
         BE    PLSNEXT              YES, SKIP                                   
*                                                                               
*=================================                                              
* TEST RUN LIMITED TO ONE AGENCY                                                
*=================================                                              
PLS060   CLC   SVSYSAA,SPACES      LIMITED RUN AGENCY SPECIFIED?                
         BE    PLS080                                                           
         CLC   SVSYSAA,R#.BUYALPHA MATCH ALPHA AGY CODE                         
         BNE   PLSNEXT                  NO, SKIP                                
*                                                                               
PLS080   LH    RE,RS$RECVHDR-4     GET LENGTH OF RECOVER RECORD                 
         LA    RE,RSORTLNQ(RE)     ADD EXPANSION LENGTH                         
         SLL   RE,16               LEFT ALIGN                                   
         ST    RE,RCVLEN                                                        
         L     RE,MYSEQ                                                         
         LA    RE,1(RE)                                                         
         ST    RE,MYSEQ                                                         
         MVC   RSORTSEQ,MYSEQ+1    SET SEQUENCE NUMBER                          
*                                                                               
         MVC   RSORTAM,R#.BUYKAM     A-M/                                       
         MVC   RSORTCLT,R#.BUYKCLT   CLT                                        
         MVC   RSORTEST,R#.BUYKEST   EST                                        
         MVC   RSORTMST,R#.BUYKMSTA  MKT-STA                                    
         MVC   RSORTPRD,R#.BUYKPRD   PRD                                        
         MVC   RSORTBUY,R#.BUYKBUY   BUY DTL                                    
*                                                                               
         GOTOR =V(SORTER),DMCB,=C'PUT',RCVLEN                                   
*                                                                               
         CLI   TRACSW,C'Y'         TRACE ON?                                    
         BNE   PLSNEXT               NO, GET NEXT                               
*                                                                               
*==========================                                                     
* TRACE IS ON, PRINT RKEY                                                       
*==========================                                                     
         MVC   P(19),=C'STEP2_SORTPUT -SEQ#'                                    
         GOTOR =V(HEXOUT),DMCB,RSORTSEQ,P+19,3,=C'TOG'                          
         MVC   P+26(7),=C'-RCVHDR'                                              
         GOTOR =V(HEXOUT),DMCB,RS$RECVHDR,P+34,L'RS$RECVHDR,=C'TOG'             
***      GOTOR =V(PRINTER)                                                      
                                                                                
***      GOTOR =V(HEXOUT),DMCB,RS$RRECTY,P+25,1,=C'TOG'                         
***      MVI   P+27,C'-'                                                        
***      GOTOR =V(HEXOUT),DMCB,RS$RPRG,P+28,1,=C'TOG'                           
***      MVI   P+30,C'-'                                                        
         MVC   P+83(7),=C'-BUYREC'                                              
         GOTOR =V(HEXOUT),DMCB,RKEY,P+91,20,=C'TOG'                             
         GOTOR =V(PRINTER)                                                      
         B     PLSNEXT                                                          
*                                                                               
*==========================                                                     
* END OF PEEL&SORT (STEP2)                                                      
*==========================                                                     
PLSXTAP  DS    0H                  END OF TAPE INPUT                            
         LAY   R4,RECVIN           DONE WITH TAPE                               
         CLOSE ((4))               CLOSE TAPE                                   
*                                                                               
PLSXEND  DS    0H                  END OF PEEL&SORT                             
         LA    RE,RCVLEN                                                        
         LLH   RF,0(RE)                                                         
         XCEFL                                                                  
         EJECT                                                                  
*                                                                               
*======================================================================         
*************************** ORDAUP_STEP3 ******************************         
*----------------------------------------------------------------------         
*   READ THRU SORTER, PROCESSING FIRST AND LAST CHANGE TO A BUY RECORD          
*   CREATE AN ACTIVITY KEY (WSORT) FOR EACH AND PUT THEM TO WORKFILE            
*======================================================================         
*                                                                               
*===============================================                                
* GET FIRST OUTPUT RECOVERY RECORD FROM SORTER                                  
*===============================================                                
PROUTPUT DS    0H                  PROC OUTPUT RECOVERY REC FROM SORTER         
*                                                                               
         MVI   EOFSW,C'N'          RESET EOF SWITCH                             
*                                                                               
         LAY   R4,ORDATAB1         GET SPARE ORDA TABLE ADDRESS                 
         USING ORDATABD,R4                                                      
*                                                                               
         TIME                                                                   
         SRL   R0,4                SHIFT TIME FOR EDIT                          
         ST    R0,DUB                                                           
         MVC   P(10),=X'402021204B20204B2020'                                   
         ED    P(10),DUB                                                        
*                                                                               
NXT#     USING RCVLEN,R6                                                        
         GOTOR =V(SORTER),DMCB,=C'GET'                                          
         ICM   R6,15,4(R1)         HAVE ANY RECORDS?                            
         BZ    PROPX                 NO, EXIT                                   
*                                                                               
         MVC   P+11(18),=C'BUY ACTIVITY FOUND'                                  
         GOTOR =V(PRINTER)         TIME STAMP THE TRANSACTION                   
*                                                                               
         CLI   TRACSW,C'Y'         TRACE ON?                                    
         BNE   PROP190               NO, GO SAVE RECORD                         
         BRAS  RE,PRTSORTG           YES, PRINT SORT GET KEY                    
         B     PROP190             GO SAVE RECORD                               
                                                                                
PROPX    GOTOR =V(SORTER),DMCB,=C'END'                                          
*                                                                               
         MVC   P+11(16),=C'NO INPUT RECORDS'                                    
         GOTOR =V(PRINTER)                                                      
         B     EOJ                 GO PROCESS EOJ                               
*                                                                               
*=====================================                                          
* GET NEXT OUTPUT RECORD FROM SORTER                                            
*=====================================                                          
PROPNEXT GOTOR =V(SORTER),DMCB,=C'GET'                                          
         ICM   R6,15,4(R1)           TEST ANY MORE RECOVERY RECORDS             
         BNZ   PROPTRC                  YES                                     
         GOTOR =V(SORTER),DMCB,=C'END'  NO, CLOSE SORTER                        
         MVI   EOFSW,C'Y'                  SET EOFSW                            
         B     PROP020                     AND PROCESS LAST REC IMAGE           
*                                                                               
PROPTRC  CLI   TRACSW,C'Y'         TRACE ON?                                    
         BNE   PROP010               NO,                                        
         BRAS  RE,PRTSORTG           YES, PRINT SORT GET KEY                    
*                                                                               
PROP010  CLC   RKEY,NXT#.RKEY      TEST CHANGE OF KEY                           
         BNE   PROP020                                                          
*                                                                               
         CLI   RCVERROR,C'Y'       HAVE RECOVERY ERROR?                         
         BE    PROPNEXT             YES, SKIP UNTIL CHANGE OF KEY               
*                                                                               
         LH    R7,NXT#.RCVLEN      GET 'FROM' REC LEN                           
         LA    RE,RCVLEN           SET 'TO' REC ADDR                            
         LA    RF,2(R7)            SET 'TO' LEN = 'FROM' LEN + 2                
         MVCL  RE,R6               ** WHICH PUTS IN 2X'00' AT EOR               
         B     PROPNEXT            GET NEXT RECOVERY RECORD                     
*                                                                               
*===============================================                                
* ON CHANGE OF KEY PROCESS LAST RECOVERY IMAGE                                  
*===============================================                                
PROP020  CLI   RCVERROR,C'Y'       HAVE RECOVERY ERROR?                         
         BE    PROP180               SKIP IT                                    
*                                                                               
         CLI   RS$RRECTY,RS$RRECTCPY TEST LAST CHG WAS COPY W/O CHG             
         BE    PROP130               RIGHT - PROCESS THE FIRST COPY             
*                                                                               
*==================================================================             
* IN THE INITIAL RELEASE, WE ARE ONLY TRACKING CHANGES MADE BY                  
*   BUY (VIA SBTK) AND LINK (AKA SBTK) PROGRAM                                  
* TO DETERMINE BUY CHANGE WAS MADE VIA SBTK, WE WILL CHECK                      
* IF THERE WAS ACTIVITY (IE A CHANGE) TO THE DTX ACTIVITY ELEMENT               
*==================================================================             
*                                                                               
         CLI   RS$RPRG,X'11'       TEST LAST CHG MADE BY BUY PGM                
         BNE   PROP110               NO                                         
*                                                                               
         XC    WORK,WORK           INIT WORK TO HAVE NO DTX ELEM                
         CLI   ORDAACTN,ORDAADD    FIRST CHANGE WAS AN ADD?                     
         BE    PROP080                                                          
*                                                                               
*==============================================================                 
* TREAT AS COPY/CHANGE. LET'S COMPARE FIRST AND LAST DTX ELEM                   
*==============================================================                 
*                                                                               
*==============================                                                 
* GET DTX ELEM OF FIRST IMAGE                                                   
*==============================                                                 
PROP050  LAY   R2,BUYRECI          POINT TO FIRST IMAGE                         
         LA    R2,BDELEM-BUYREC(R2)                                             
PROP060  CLI   0(R2),0             DID FIRST IMAGE HAVE DTX ELEM?               
         BE    PROP080              NO, IT'S OK, CHECK LAST IMAGE               
         CLI   0(R2),DTXCODEQ      HAVE DTX ELEM?                               
         BE    PROP070               YES                                        
         SR    RF,RF                                                            
         ICM   RF,1,1(R2)                                                       
         LA    R2,0(RF,R2)                                                      
         BNZ   PROP060                                                          
         DC    H'0'                DIE ON ELEMENT LENGTH ZERO!                  
PROP070  SR    RF,RF                                                            
         ICM   RF,1,1(R2)                                                       
         JZ    *+2                 DIE ON ELEMENT LENGTH ZERO!                  
         BCTR  RF,0                                                             
         MVC   WORK(0),0(R2)       SAVE COPY OF DTX IN WORK                     
         EX    RF,*-6                                                           
*                                                                               
*=========================================                                      
* GET DTX ELEM OF LAST IMAGE AND COMPARE                                        
*  IN CASE OF ADD-ONLY, JUST CHK FOR DTX                                        
*=========================================                                      
PROP080  LA    R2,R#.BUYKEY        POINT TO LAST IMAGE                          
         LA    R2,BDELEM-BUYREC(R2)                                             
PROP090  CLI   0(R2),0             DID LAST IMAGE HAVE DXT ELEM?                
         BE    PROP180              NO, CHG NOT BY SBTK, SKIP IT                
         CLI   0(R2),DTXCODEQ      HAVE DTX ELEM?                               
         BE    PROP100              YES                                         
         SR    RF,RF                                                            
         ICM   RF,1,1(R2)                                                       
         LA    R2,0(RF,R2)                                                      
         BNZ   PROP090                                                          
         DC    H'0'                DIE ON ELEMENT LENGTH ZERO!                  
PROP100  SR    RF,RF                                                            
         ICM   RF,1,1(R2)                                                       
         JZ    *+2                 DIE ON ELEMENT LENGTH ZERO!                  
         BCTR  RF,0                                                             
         CLC   0(0,R2),WORK        COMPARE DTX FROM FIRST & LAST IMAGE          
         EX    RF,*-6                                                           
         BE    PROP180              NO CHG. CHG NOT BY SBTK, SKIP IT            
*                                                                               
PROP110  TM    R#.BUYRCNTL,BUYRDEL TEST LAST CHG IS A DELETE                    
         BZ    PROP120              NO                                          
*                                                                               
         BRAS  RE,PRTTAFD           YES PRINT TRACKABLE ACVTIVITY FOUND         
*                                                                               
         B     PROP130               AND PROCESS THE FIRST COPY                 
*                                                                               
*============================================================                   
* CALL ORDAMNT TO PROCESS CHANGE/ADD AND CHECK FOR ACTIVITY                     
*============================================================                   
PROP120  MVI   ORDAACTN,ORDACHG    SET 'CHANGE' (FOR ADDS AS WELL)              
*                                                                               
         CLI   TRACSW,C'Y'                                                      
         BNE   *+8                                                              
         BRAS  RE,PRTTOMNT         PRINT RKEY BEING PUT TO MAINT                
*                                                                               
         GOTOR =V(ORDAMNT),DMCB,(R4),RKEY                                       
         CLI   ORDAERR,ORDATABZ    TABLE TOO SMALL?                             
         JE    *+2                                                              
         CLI   ORDAERR,ORDANOAC    TEST NO ACTIVITY                             
         BE    PROP180               YES                                        
*                                                                               
         BRAS  RE,PRTTAFD          PRINT TRACKABLE ACTIVITY FOUND               
*                                                                               
*=====================================================================          
* FOR EACH PRODUCT IN ORDA TABLE, CREATE WSORT KEY FROM RECOVERY KEY            
*=====================================================================          
P#       USING ORDADATA,R5                                                      
PROP130  LA    R5,ORDADATA         FOR EACH PRODUCT IN ORDA TABLE               
         LH    R3,ORDALEN                                                       
         AR    R3,R5                                                            
         BCTR  R3,0                                                             
*                                                                               
PROP140  CLI   0(R5),0             TEST LAST ENTRY                              
         BE    PROP180               YES                                        
         CR    R5,R3               TEST PAST TABLE END                          
         BNL   PROP180               YES, DONE                                  
*======================================================                         
* CREATE WSORT KEY                                                              
*  ||AM|CLT|PRD|EST|STA|PRD2|FLT|TYPE||AA|DATE|TIME||                           
*======================================================                         
PROP150  MVC   WSRTAM,RSORTAM                                                   
         MVC   WSRTCLT,RSORTCLT                                                 
         MVC   WSRTEST,RSORTEST                                                 
         MVC   WSRTSTA,RSORTSTA                                                 
         CLI   WSRTSTA,X'E8'       IS THIS FOR A CABLE STATION?                 
         BL    *+8                                                              
         NI    WSRTSTA+2,X'80'     YES, WE ONLY WANT SYSCODE LEVEL              
*                                                                               
         OC    P#.ORDASCHD,P#.ORDASCHD ANY ACTIVITY FOR THIS COMBI?             
         BZ    PROP170                  NO                                      
         MVI   SORTSW,X'FF'        SET SORT SWITCH ON                           
*                                                                               
         MVC   WSRTPRD,P#.ORDAPRD                                               
         MVC   WSRTPRD2,P#.ORDAPRD2                                             
         MVC   WSRTFLT,P#.ORDAFLT                                               
         MVC   WSRTTYPE,P#.ORDATYPE                                             
*                                                                               
         MVC   WSRTAA,R#.BUYALPHA  ALPHA AGENCY CODE                            
*                                                                               
         MVC   PACKOF4B,RS$RTIME     TIME 0HHMMSSC WHERE SIGN=F IF 1ST          
         NI    PACKOF4B,X'FF'-RS$RTIMEFID+RS$RTIMEEXT   CLEAR FLAGS             
         AP    PACKOF4B,=P'60000'    ADJUST TO EST, +6 HOURS                    
                                                                                
         GOTO1 =V(DATCON),DMCB,(3,RS$RDATE),(0,DUB)                             
         CP    PACKOF4B,=P'240000'   PAST MIDNIGHT?                             
         BL    PROP155                                                          
         SP    PACKOF4B,=P'240000'   YES, BUMP TO NEXT DAY AND ADJUST           
         GOTOR =V(ADDAY),DMCB,DUB,DUB,F'1'                                      
*                                                                               
PROP155  GOTO1 =V(DATCON),DMCB,(0,DUB),(19,WSRTDATE)                            
*                                                                               
         ICM   R1,15,PACKOF4B                                                   
         SRL   R1,12               GET RID OF SECONDS AND SIGN                  
         STCM  R1,3,WSRTTIME                                                    
*                                                                               
         CLI   TRACSW,C'Y'                                                      
         BNE   *+8                                                              
         BRAS  RE,PRTWSORT                                                      
*                                                                               
PROP160  LAY   R1,WORKFILE                                                      
         PUT   (1),WSRTREC         WRITE 13-BYTE SORT KEY TO VIO FILE           
*                                                                               
PROP170  LA    R5,ORDADLN2(R5)     NEXT PRODUCT                                 
         B     PROP140                                                          
*                                                                               
PROP180  CLI   EOFSW,C'Y'          TEST E-O-F                                   
         BNE   PROP190                                                          
         CLI   SORTSW,X'FF'        TEST ANY RECORDS ARE IN VIO FILE             
         BE    SORTWKEY            SORT BUY KEYS                                
*                                                                               
         TIME                                                                   
         SRL   R0,4                SHIFT TIME FOR EDIT                          
         ST    R0,DUB                                                           
         MVC   P(10),=X'402021204B20204B2020'                                   
         ED    P(10),DUB                                                        
*                                                                               
         MVC   P+11(21),=C'NO TRACKABLE ACTIVITY'                               
         GOTOR =V(PRINTER)                                                      
         B     EOJ                 GO PROCESS EOJ                               
*                                                                               
*=====================================                                          
* SAVE NEW RECORD LOCALLY IN RECVREC                                            
*=====================================                                          
PROP190  DS    0H                                                               
         BRAS  RE,ORDACLR          CLEAR AND INITIALIZE ORDA TABLE              
         MVI   RCVERROR,0                                                       
*                                                                               
         LH    R7,NXT#.RCVLEN      GET 'FROM' REC LEN                           
         LA    RE,RCVLEN           SET 'TO' REC ADDR                            
         LA    RF,2(R7)            SET 'TO' LEN = 'FROM' LEN + 2                
         MVCL  RE,R6               ** WHICH PUTS IN 2X'00' AT EOR               
*                                                                               
*=========================================                                      
* SAVE COPY OF FIRST COPY/ADD IN BUYRECI                                        
*=========================================                                      
*                                                                               
         LA    R2,R#.BUYKEY        POINT TO BUY REC IN RECVREC                  
         LLH   R3,R#.BUYRLEN       GET 'FROM' REC LEN                           
         LAY   RE,BUYRECI          SET 'TO' REC ADDR                            
         LA    RF,2(R3)            SET 'TO' LEN = 'FROM' LEN + 2                
         MVCL  RE,R2               ** WHICH PUTS IN 2X'00' AT EOR               
*                                                                               
*============================                                                   
* CHECK NEW CLTHDR REQUIRED                                                     
*============================                                                   
PROPGCLT LAY   RE,UTL                                                           
         MVC   4(1,RE),SPTUTL                                                   
*                                                                               
         LAY   R5,CLTREC                                                        
         CLC   R#.BUYKAM(3),1(R5)  TEST SAME A-M/CLT                            
         BE    PROPGCX                                                          
         USING CLTHDRD,R2                                                       
         LA    R2,KEY                                                           
         XC    KEY,KEY                                                          
         MVC   CKEYAM(3),R#.BUYKAM                                              
         DROP  R2                                                               
         MVI   DMINBTS,X'24'       FORCE READ FROM DISK                         
         BRAS  RE,HIGH                                                          
         MVI   DMINBTS,0                                                        
         CLC   KEY(13),KEYSAVE                                                  
         BE    PROPGC10                                                         
         LAY   R1,MISSCLT                                                       
         BRAS  RE,PRTBADRC        PRINT INFO ON BAD RECOVERY RECORD KEY         
         B     PROPNEXT                                                         
*                                                                               
PROPGC10 ST    R5,AIO                                                           
         BRAS  RE,GETREC                                                        
         JNE   *+2                                                              
         LAY   RF,L'CLTREC                                                      
         CH    RF,13(R5)           RECORD TOO BIG?                              
         JNH   *+2                                                              
         USING CLTHDRD,R5                                                       
         CLI   CPOLONLY,C'Y'       POL CLIENT?                                  
         JE    PROPGC20             YES, OK TO PROCESS                          
         CLI   CPOLONLY,C'N'       BRAND CLIENT?                                
         JE    PROPGC15             YES, SKIP IT, PRINT ERROR                   
         CLI   CPROF+0,C'0'        BRAND TYPE CLIENT?                           
         JNE   PROPGC20             NO, PROCESS BPOL/TPOL CLIENT                
PROPGC15 LAY   R1,BRDCLT                                                        
         BRAS  RE,PRTBADRC         PRINT INFO ON BRD CLT                        
         B     PROPNEXT                                                         
*                                                                               
PROPGC20 XC    WORK,WORK                                                        
         MVC   WORK(4),=C'SDAR'                                                 
         NI    WORK,X'FF'-X'40'                                                 
         MVC   WORK+4(2),RS$RUSER                                               
*                                                                               
         MVC   BYTE,R#.BUYKAM                                                   
         NI    BYTE,X'0F'          ISOLATE MEDIA                                
         MVI   WORK+6,C'T'                                                      
         CLI   BYTE,X'01'          TV?                                          
         BE    PROPGC30                                                         
         MVI   WORK+6,C'R'                                                      
         CLI   BYTE,X'02'          RADIO?                                       
         BE    PROPGC30                                                         
         MVI   WORK+6,C'X'                                                      
         CLI   BYTE,X'04'          NETWORK RADIO?                               
         JNE   *+2                                                              
*                                                                               
PROPGC30 DS    0H                                                               
         GOTOR =V(CLUNPK),DMCB,(CPROF+6,R#.BUYKCLT),WORK+7                      
*                                                                               
         MVI   WORK+10,C'*'                                                     
         MVC   WORK+11(1),COFFICE                                               
         DROP  R5                                                               
*                                                                               
         GOTO1 =V(GETPROF),DMCB,(X'C0',WORK),A(PROFDAR),V(DATAMGR)              
*                                                                               
         LAY   R2,PROFDAR                                                       
         MVC   WORK(3),6(R2)                                                    
         MVC   WORK+3(3),6(R2)                                                  
         CLI   14(R2),C'Y'         MULTIPLE TRADE CODES?                        
         BNE   PROPGC40              NO                                         
         MVI   WORK+2,C' '         FOR ALPHA TRADE CODE, SCOPE IS               
         MVI   WORK+5,C'9'          AA_ -> AA9                                  
*                                                                               
         CLI   6(R2),C'0'          HAVE NUMERIC TRADE CODE?                     
         BL    PROPGC40             NO, SCOPY IS AA_ -> AA9                     
         MVI   WORK+2,C'0'         NUMERIC TRADE CODE SCOP NN0 -> NN9           
*                                                                               
PROPGC40 LAY   R2,DARREPLO                                                      
         GOTO1 VRCPACK,DMCB,(C'P',WORK),(R2)                                    
         BNE   PROPGCX                                                          
         GOTO1 VRCPACK,DMCB,(C'P',WORK+3),2(R2)                                 
         BE    PROPGCX                                                          
         MVC   0(2,R2),=X'FFFF'    IF ERROR, TREAT ALL BUYS AS CASH             
*                                                                               
PROPGCX  DS    0H                                                               
*                                                                               
*============================                                                   
* CHECK NEW ESTHDR REQUIRED                                                     
*============================                                                   
PROPGEST LAY   RE,UTL                                                           
         MVC   4(1,RE),SPTUTL                                                   
*                                                                               
         MVC   WORK(3),=C'POL'                                                  
         LAY   RF,CLTREC                                                        
         CLI   CPROF-CLTHDRD(RF),C'0'        TRUE POL?                          
         JE    PROPGE10                       YES, READ POL ESTIMATE            
*                                                                               
         LAY   R2,R#.BUYKEY+BDELEM-BUYREC     NO, READ BRAND ESTIMATE           
         CLI   0(R2),BDCODEQ       HAVE X'01' PRIMARY BUY ELEM?                 
         JNE   *+2                                                              
         LA    R1,BDMASPRD-BDELEM(R2)                                           
         BRAS  RE,FINDPRD                                                       
*                                                                               
         USING ESTHDRD,R5                                                       
PROPGE10 LAY   R5,ESTREC                                                        
         CLC   R#.BUYKAM(3),EKEYAM TEST SAME 00/A-M/CLT                         
         BNE   PROPGE20                                                         
         CLC   EKEYPRD,WORK        TEST SAME PRODUCT?                           
         BNE   PROPGE20                                                         
         CLC   R#.BUYKEST,EKEYEST  TEST SAME EST                                
         BE    PROPGEX                                                          
         DROP  R5                                                               
*                                                                               
         USING ESTHDRD,R2                                                       
PROPGE20 LA    R2,KEY                                                           
         XC    KEY,KEY                                                          
         MVC   EKEYAM(3),R#.BUYKAM                                              
         MVC   EKEYPRD,WORK                                                     
         MVC   EKEYEST,R#.BUYKEST                                               
         DROP  R2                                                               
         MVI   DMINBTS,X'24'       FORCE READ FROM DISK                         
         BRAS  RE,HIGH                                                          
         MVI   DMINBTS,0                                                        
         CLC   KEY(13),KEYSAVE                                                  
         BE    PROPGE30                                                         
         LAY   R1,MISSEST                                                       
         BRAS  RE,PRTBADRC        PRINT INFO ON BAD RECOVERY RECORD KEY         
         B     PROPNEXT                                                         
*                                                                               
PROPGE30 ST    R5,AIO                                                           
         BRAS  RE,GETREC                                                        
         JNE   *+2                                                              
         LAY   RF,L'ESTREC                                                      
         CH    RF,13(R5)           RECORD TOO BIG?                              
         JNH   *+2                                                              
*                                                                               
         BRAS  RE,BESTWKTB         BUILD THE ESTIMATE WEEK TABLE                
*                                                                               
PROPGEX  DS    0H                                                               
*                                                                               
*============================                                                   
* CHECK NEW FLTHDR REQUIRED                                                     
*============================                                                   
PROPGFLT LAY   RE,UTL                                                           
         MVC   4(1,RE),SPTUTL                                                   
*                                                                               
         USING DFLKEY,R5                                                        
         LAY   R5,FLTREC                                                        
         CLC   DFLKTYP(2),=AL1(DFLKTYPQ,DFLKSUBQ)  SAME X'0D38'                 
         BNE   PROPGF10                                                         
         CLC   R#.BUYKAM(3),DFLKAGMD  TEST SAME 00/A-M/CLT                      
         BNE   PROPGF10                                                         
         CLC   R#.BUYKEST,DFLKEST  TEST SAME EST                                
         BE    PROPGFX                                                          
         DROP  R5                                                               
                                                                                
PROPGF10 XC    0(256,R5),0(R5)     CLEAR FLTREC                                 
*                                                                               
         LAY   RE,FLTTAB           INIT FLIGHT TABLE                            
         LAY   RF,FLTTABX-FLTTAB                                                
         XCEFL                                                                  
*                                                                               
         XC    KEY,KEY                                                          
         LA    R2,KEY                                                           
         USING DFLKEY,R2                                                        
         MVI   DFLKTYP,DFLKTYPQ    X'0D'                                        
         MVI   DFLKSUB,DFLKSUBQ    X'38'                                        
         MVC   DFLKAGMD(3),R#.BUYKAM  MOVE IN A-M/CLT                           
         MVC   DFLKPRD,=C'POL'     READ FOR POL                                 
         MVC   DFLKEST,R#.BUYKEST  MOVE IN EST                                  
         DROP  R2                                                               
         MVI   DMINBTS,X'24'       FORCE READ FROM DISK                         
         BRAS  RE,HIGH                                                          
         MVI   DMINBTS,0                                                        
         CLC   KEY(13),KEYSAVE                                                  
         BNE   PROPGFX                                                          
*                                                                               
PROPGF20 ST    R5,AIO                                                           
         BRAS  RE,GETREC                                                        
         JNE   *+2                                                              
         LAY   RF,L'FLTREC                                                      
         CH    RF,13(R5)           RECORD TOO BIG?                              
         JNH   *+2                                                              
*                                                                               
         BRAS  RE,BFLTTAB          BUILD THE FLIGHT TABLE                       
*                                                                               
PROPGFX  DS    0H                                                               
*                                                                               
PROP200  DS    0H                                                               
         MVI   ORDAACTN,ORDAADD    SET 'ADD' PROCESSING                         
*                                                                               
         CLI   RS$RRECTY,RS$RRECTCPY  TEST COPY IMAGE                           
         BNE   PROPNEXT                                                         
*                                                                               
         MVI   ORDAACTN,ORDACOPY      SET 'COPY' PROCESSING                     
*                                                                               
         CLI   TRACSW,C'Y'                                                      
         BNE   *+8                                                              
         BRAS  RE,PRTTOMNT         PRINT RKEY BEING PUT TO MAINT                
*                                                                               
         GOTOR =V(ORDAMNT),DMCB,(R4),RKEY                                       
         CLI   ORDAERR,ORDATABZ    TABLE TOO SMALL?                             
         BNE   PROPNEXT                                                         
         DC    H'0'                                                             
         EJECT                                                                  
*                                                                               
*=====================                                                          
* PRINT SORT GET KEY                                                            
*=====================                                                          
PRTSORTG NTR1                                                                   
         MVC   P(19),=C'STEP3_SORTGET -SEQ#'                                    
         GOTOR =V(HEXOUT),DMCB,NXT#.RSORTSEQ,P+19,3,=C'TOG'                     
         MVC   P+26(7),=C'-BUYREC'                                              
         GOTOR =V(HEXOUT),DMCB,NXT#.RKEY,P+34,45,=C'TOG'                        
         B     PRNTR                                                            
*                                                                               
*=================================                                              
* PRINT TRACKABLE ACTIVITY FOUND                                                
*=================================                                              
PRTTAFD  NTR1                                                                   
         TIME                                                                   
         SRL   R0,4                SHIFT TIME FOR EDIT                          
         ST    R0,DUB                                                           
         MVC   P(10),=X'402021204B20204B2020'                                   
         ED    P(10),DUB                                                        
                                                                                
         MVC   P+11(24),=C'TRACKABLE ACTIVITY FOUND'                            
         GOTOR =V(PRINTER)                                                      
         B     EXIT                                                             
*                                                                               
*======================================                                         
* PRINT RKEY BEING PUT TO MAINTENANCE                                           
*======================================                                         
PRTTOMNT NTR1                                                                   
         MVC   P(24),=C'STEP3_TO MNT  ** COPY **'                               
         CLI   ORDAACTN,ORDACOPY      SET 'COPY' PROCESSING                     
         BE    PRNTR                                                            
         MVC   P+14(8),=C'**CHANGE**'                                           
         B     PRNTR                                                            
*                                                                               
*===================================                                            
* PRINT WSRTREC BEING PUT TO WSORT                                              
*===================================                                            
PRTWSORT NTR1                                                                   
         MVC   P(21),=C'STEP3_TO WSRT -ACTKEY'                                  
         MVC   P+22(2),WSRTAA                                                   
         GOTOR =V(HEXOUT),DMCB,WSRTAM,P+25,L'WSRTAM,=C'TOG'                     
         GOTOR =V(HEXOUT),DMCB,WSRTCLT,P+28,L'WSRTCLT,=C'TOG'                   
         GOTOR =V(HEXOUT),DMCB,WSRTPRD,P+33,L'WSRTPRD,=C'TOG'                   
         GOTOR =V(HEXOUT),DMCB,WSRTPRD2,P+36,L'WSRTPRD2,=C'TOG'                 
         GOTOR =V(HEXOUT),DMCB,WSRTEST,P+39,L'WSRTEST,=C'TOG'                   
         GOTOR =V(HEXOUT),DMCB,WSRTFLT,P+42,L'WSRTFLT,=C'TOG'                   
         MVC   P+45(1),WSRTTYPE                                                 
         GOTOR =V(HEXOUT),DMCB,WSRTDATE,P+47,L'WSRTDATE,=C'TOG'                 
         MVI   P+53,C'-'                                                        
         GOTOR =V(HEXOUT),DMCB,WSRTTIME,P+54,L'WSRTTIME,=C'TOG'                 
         B     PRNTR                                                            
*                                                                               
*======================                                                         
* PRINT ACTIVITY FOUND                                                          
*======================                                                         
PRTACTFD NTR1                                                                   
         MVC   P(22),=C'STEP3 - ACTIVITY FOUND'                                 
         B     PRNTR                                                            
*                                                                               
*========================================                                       
* PRINT INFO ON BAD RECOVERY RECORD KEY                                         
*========================================                                       
PRTBADRC NTR1                                                                   
         MVI   RCVERROR,C'Y'       SET ERROR                                    
*                                                                               
         MVC   P,0(R1)                                                          
         GOTOR =V(HEXOUT),DMCB,RKEY,P+32,1,=C'TOG'                              
         GOTOR =V(HEXOUT),DMCB,RKEY+1,P+40,2,=C'TOG'                            
         GOTOR =V(HEXOUT),DMCB,RKEY+3,P+50,1,=C'TOG'                            
         GOTOR =V(HEXOUT),DMCB,RKEY+4,P+60,5,=C'TOG'                            
         GOTOR =V(HEXOUT),DMCB,RKEY+9,P+76,1,=C'TOG'                            
PRNTR    GOTOR =V(PRINTER)                                                      
         B     EXITY                                                            
         DROP  NXT#                                                             
*                                                                               
*======================================================================         
*************************** ORDAUP_STEP4 ******************************         
*----------------------------------------------------------------------         
*   READ WORKFILE AND PUT TO SORTER TO SORT ACTIVITY                            
*======================================================================         
*                                                                               
*============================================                                   
* CALL SORTER TO SORT ACTIVITY WSRTREC KEYS                                     
*============================================                                   
SORTWKEY LAY   R4,WORKFILE         CLOSE ACTIVITY VIO FILE                      
         CLOSE ((4))                                                            
         OPEN  ((4),(INPUT))       OPEN VIO FILE FOR READING                    
         GOTOR =V(SORTER),DMCB,SRTCARDW,RECCARDW INITIALIZE SORT                
         MVI   EOFSW,C'N'                                                       
*                                                                               
PUTSORT  LA    R0,WSRTREC          WRITE RECORDS TO SORT FILE                   
         LAY   R1,WORKFILE                                                      
         GET   (1),(0)                                                          
         GOTOR =V(SORTER),DMCB,=C'PUT',WSRTREC                                  
         B     PUTSORT                                                          
*                                                                               
ENDIN2   LAY   R4,WORKFILE                                                      
         CLOSE ((4))                                                            
*                                                                               
*======================================================================         
*************************** ORDAUP_STEP5 ******************************         
*----------------------------------------------------------------------         
*   READ SORTED ACITIVITY (SORTER) AND UPDATE ORDER RESEND FLAG                 
*======================================================================         
*                                                                               
         GOTOR =V(SORTER),DMCB,=C'GET' GET FIRST SORTED KEY                     
         ICM   R6,15,4(R1)         TEST NO SORT RECORDS                         
         JZ    *+2                 SHOULD BE AT LEAST ONE RECORD                
         MVC   WSRTREC,0(R6)       SAVE FIRST KEY                               
         MVI   EOFSW,C'N'                                                       
*                                                                               
         TIME                                                                   
         SRL   R0,4                SHIFT TIME FOR EDIT                          
         ST    R0,DUB                                                           
         MVC   P(10),=X'402021204B20204B2020'                                   
         ED    P(10),DUB                                                        
*                                                                               
         MVC   P+11(15),=C'UPDATING ORDERS'                                     
         GOTOR =V(PRINTER)                                                      
*                                                                               
UPDATE   CLI   EOFSW,C'Y'          TEST ALL ORDER RECORDS UPDATED?              
         BE    EOJ                                                              
*                                                                               
         GOTOR =V(SORTER),DMCB,=C'GET' GET NEXT KEY                             
         ICM   R6,15,4(R1)         TEST END OF WORK FILE                        
         BNZ   UPOR010                                                          
         MVI   EOFSW,C'Y'          SET END OF WORK FILE FLAG ON                 
         GOTOR =V(SORTER),DMCB,=C'END'                                          
*                                                                               
UPOR010  CLC   WSRTKEY,0(R6)       TEST KEY CHANGE                              
         BE    UPDATE                                                           
*                                                                               
UPOR020  LAY   RE,UTL                                                           
         MVC   4(1,RE),SPTUTL                                                   
*====================                                                           
* GET CLIENT RECORD                                                             
*====================                                                           
         LAY   R5,CLTREC                                                        
         CLC   1(3,R5),WSRTAM      TEST SAME A-M/CLT                            
         BE    UPOR030                                                          
         USING CLTHDRD,R2                                                       
         LA    R2,KEY                                                           
         XC    KEY,KEY                                                          
         MVC   CKEYAM(3),WSRTAM                                                 
         DROP  R2                                                               
         MVI   DMINBTS,X'24'       FORCE READ FROM DISK                         
         BRAS  RE,HIGH                                                          
         MVI   DMINBTS,0                                                        
         CLC   KEY(13),KEYSAVE                                                  
         JNE   *+2                                                              
*                                                                               
         ST    R5,AIO                                                           
         BRAS  RE,GETREC                                                        
         JNE   *+2                                                              
*                                                                               
UPOR030  LAY   RE,UTL                                                           
         MVC   4(1,RE),SPTUTL                                                   
*                                                                               
*===================================                                            
* GET ORDER VIA CLIENT PASSIVE KEY                                              
*===================================                                            
         LA    R2,KEY                                                           
         USING DOKEY,R2                                                         
         MVI   DCKTYPE,DCKTYPQ     X'0D'                                        
         MVI   DCKSUBTY,DCKSTYPQ   X'B5'                                        
         MVC   DCKAGMD,WSRTAM                                                   
         MVC   DCKCLT,WSRTCLT                                                   
         MVC   DCKPRD,WSRTPRD                                                   
         MVC   DCKEST,WSRTEST                                                   
         MVC   DCKSTA,WSRTSTA                                                   
         MVC   DCKPRD2,WSRTPRD2                                                 
         CLI   WSRTFLT,WSRTFLT0    ZERO FLIGHT?                                 
         BE    *+10                                                             
         MVC   DCKFLTNM,WSRTFLT                                                 
                                                                                
         MVI   DCKFLAG,0           DEFAULT CASH                                 
         CLI   WSRTTYPE,WSRTTRAD   TRADE?                                       
         BNE   *+8                                                              
         OI    DCKFLAG,DCKFTRDE                                                 
         DROP  R2                                                               
*                                                                               
         BRAS  RE,HIGH                                                          
         JNE   *+2                                                              
*                                                                               
         LAY   RE,ORDREC                                                        
         LAY   RF,L'ORDREC                                                      
         XCEFL                                                                  
*                                                                               
         CLC   KEY(13),KEYSAVE     ORDER FOUND?                                 
         BNE   UPORNORD             NO, PRT MSG, AND GET NEXT ORDER             
*                                                                               
         LAY   RE,ORDREC                                                        
         ST    RE,AIO                                                           
         BRAS  RE,GETREC                                                        
         JNE   *+2                                                              
         LAY   RF,L'ORDREC                                                      
         CH    RF,13(R5)           RECORD TOO BIG?                              
         JNH   *+2                                                              
*                                                                               
         LAY   R5,ORDREC+DORFRST-DAREORDD                                       
UPOR040  CLI   0(R5),0             EOR?                                         
         BE    UPORNVST             YES, PRT MSG, AND GET NEXT ORDER            
*                                                                               
         CLI   0(R5),DOSTELQ       X'11'-STATUS ELEM?                           
         BE    UPOR050                                                          
         SR    RF,RF                                                            
         ICM   RF,1,1(R5)                                                       
         LA    R5,0(RF,R5)                                                      
         JNZ   UPOR040                                                          
         DC    H'0'                                                             
*                                                                               
         USING DOSTELD,R5                                                       
UPOR050  CLC   DOSTDATE,WSRTDATE   PAST DATE? ALREADY RESENT?                   
         BL    UPOR060              NO, UPDATE RESEND FLAG                      
         BH    UPORSENT             YES, PRT MSG AND GET NXT ORD                
         CLC   DOSTTIME,WSRTTIME   SAME DATE,PAST TIME? ALREADY RESENT?         
         BNL   UPORSENT             YES, PRT MSG AND GET NXT ORD                
*                                                                               
UPOR060  LAY   R5,ORDREC+DORFRST-DAREORDD                                       
UPOR070  CLI   0(R5),0             EOR?                                         
         JE    UPORMSUP             YES, PRT MSG, AND GET NEXT ORDER            
*                                                                               
         CLI   0(R5),DOSPELQ       X'03'-SUPPLEMENTARY ELEM?                    
         BE    UPOR080                                                          
         SR    RF,RF                                                            
         ICM   RF,1,1(R5)                                                       
         LA    R5,0(RF,R5)                                                      
         JNZ   UPOR070                                                          
         DC    H'0'                                                             
*                                                                               
         USING DOSPELD,R5                                                       
UPOR080  OI    DOSPFLG2,DOSPRSND                                                
         DROP  R5                                                               
*                                                                               
         BRAS  RE,PRTORD                                                        
*                                                                               
         CLI   WRITESW,C'Y'        TEST 'WRITE=Y'                               
         BNE   *+8                   NO, DON'T WRITE TO FILE                    
         BRAS  RE,PUTREC                                                        
*                                                                               
         CLI   TESTSW,C'Y'         WAS TEST OUTPUT FILE REQUESTED               
         BNE   UPOR090                                                          
*                                                                               
         LAY   RE,ORDREC           ORD RECORD                                   
         SR    R0,R0                                                            
         ICM   R0,3,13(RE)         GET RECORD LENGTH                            
         AHI   R0,5                ADD 5 TO LENGTH                              
         SLL   R0,16               LEFT ALIGN                                   
         AHI   RE,-5               BACK UP FROM START OF RECORD                 
         STCM  R0,15,0(RE)                                                      
         MVI   4(RE),C'P'          INDICATE PUTREC                              
*                                                                               
         LAY   R1,TESTOUT                                                       
         LR    R0,RE                                                            
         PUT   (1),(0)             WRITE OUTPUT RECORD TO TESTFILE              
*                                                                               
UPOR090  CLI   EOFSW,C'Y'          TEST EOF                                     
         BE    UPDATE                                                           
         MVC   WSRTREC,0(R6)       SAVE NEW SORT KEY                            
         B     UPDATE                                                           
*                                                                               
UPORNORD MVC   PERRTEXT(18),=C'**NO ORDER FOUND**'                              
         B     *+10                                                             
*                                                                               
UPORNVST MVC   PERRTEXT(20),=C'**ORDER NEVER SENT**'                            
         B     *+10                                                             
*                                                                               
UPORSENT MVC   PERRTEXT(22),=C'**ORDER SENT ALREADY**'                          
         B     *+10                                                             
*                                                                               
UPORMSUP MVC   PERRTEXT(24),=C'**ORDER MISS SUPP ELEM**'                        
         BRAS  RE,PRTORD            PRINT ORDER DETAILS                         
         B     UPOR090                                                          
*                                                                               
*================================================                               
* GET ORDER FROM ORDER RECORD AND OUTPUT EBCDIC                                 
*================================================                               
EDTORD   DS    0H                                                               
         LAY   RF,ORDREC+DOKORDER-DAREORDD                                      
         OC    0(L'DOKORDER,RF),0(RF)  HAVE ORDER?                              
         BZR   RE                                                               
         MVC   FULL,0(RF)                                                       
         XC    FULL,=X'FFFFFFFF'                                                
         NI    FULL,X'FF'-X'80'                                                 
         ICM   RF,15,FULL                                                       
         CVD   RF,DUB                                                           
         AP    DUB,=P'04000000'                                                 
         OI    DUB+7,X'0F'                                                      
         L     RF,0(R1)                                                         
         UNPK  0(8,RF),DUB                                                      
         BR    RE                                                               
*                                                                               
HIGH     NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         GOTOR =V(DATAMGR),DMCB,(DMINBTS,=C'DMRDHI'),=C'SPTDIR',       +        
               KEYSAVE,KEY                                                      
         B     PUTRECX                                                          
*                                                                               
SEQ      NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         GOTOR =V(DATAMGR),DMCB,(DMINBTS,=C'DMRSEQ'),=C'SPTDIR',       +        
               KEYSAVE,KEY                                                      
         B     PUTRECX                                                          
*                                                                               
GETREC   LA    RF,=C'GETREC'                                                    
         B     PUTREC2                                                          
*                                                                               
ADDREC   LA    RF,=C'ADDREC'                                                    
         AP    OUTCOUNT,=P'1'                                                   
         B     PUTREC2                                                          
*                                                                               
PUTREC   LA    RF,=C'PUTREC'                                                    
         AP    OUTCOUNT,=P'1'                                                   
*                                                                               
PUTREC2  NTR1                                                                   
*                                                                               
         GOTOR =V(DATAMGR),DMCB,(DMINBTS,(RF)),=C'SPTFILE',KEY+14,AIO,          
               DMWORK                                                           
*                                                                               
PUTRECX  TM    8(R1),X'FD'         SET CC ON EXIT                               
         J     EXIT                                                             
*                                                                               
EXITL    DS    0H                  CC=LOW EXIT                                  
EXITN    LHI   RE,0                CC=NOT EQUAL EXIT                            
         J     EXITCC                                                           
EXITY    LHI   RE,1                CC=EQUAL EXIT                                
         J     EXITCC                                                           
EXITH    LHI   RE,2                CC=HIGH EXIT                                 
EXITCC   CHI   RE,1                SET CONDITION CODE                           
                                                                                
EXIT     XIT1  ,                                                                
         EJECT                                                                  
*=============================================================                  
* PRINT ORDER DETAILS                                                           
*=============================================================                  
PRTORD   NTR1                                                                   
         CLC   OLDAAGY,SPACES      HAVE PREVIOUS AGENCY?                        
         BNH   PRT005              NO                                           
         CLC   WSRTAA,OLDAAGY      TEST SAME AGENCY                             
         BE    PRT030                                                           
         ZAP   LINE,=P'99'         FORCE NEW PAGE                               
PRT005   MVC   OLDAAGY,WSRTAA                                                   
*                                                                               
         LA    RE,AGYTAB                                                        
         LA    RF,16                                                            
PRT010   CLC   0(2,RE),OLDAAGY                                                  
         BE    PRT020                                                           
         LA    RE,4(RE)                                                         
         BCT   RF,PRT010                                                        
         DC    H'0'                                                             
PRT020   MVC   OLDCTRY,2(RE)                                                    
*                                                                               
*=========================                                                      
* PRINT THE AGENCY ALPHA                                                        
*=========================                                                      
PRT030   MVC   PAGY,WSRTAA                                                      
*                                                                               
*============================                                                   
* PRINT THE CASH/TRADE TYPE                                                     
*============================                                                   
         MVC   PTYPE,WSRTTYPE                                                   
*                                                                               
*===================                                                            
* PRINT THE CLIENT                                                              
*===================                                                            
         GOTO1 =V(CLUNPK),DMCB,WSRTCLT,PCLT                                     
*                                                                               
*==================                                                             
* PRINT THE MEDIA                                                               
*==================                                                             
         LLC   RE,WSRTAM                     GET EBCDIC MEDIA                   
         N     RE,=X'0000000F'                                                  
         IC    RE,MDTAB-1(RE)                                                   
         STC   RE,PMED                                                          
*                                                                               
*=====================                                                          
* PRINT THE ESTIMATE                                                            
*=====================                                                          
         LLC   R0,WSRTEST                                                       
         EDIT  (R0),PEST                                                        
*                                                                               
*===================                                                            
* PRINT THE FLIGHT                                                              
*===================                                                            
         MVI   PFLT+1,WSRTFLT0                                                  
         CLI   WSRTFLT,WSRTFLT0                                                 
         BE    PRT040                                                           
         MVI   PFLT+2,C' '                                                      
         LLC   R0,WSRTFLT                                                       
         EDIT  (R0),PFLT                                                        
*                                                                               
*=================================                                              
* PRINT THE PRODUCT AND PRODUCT2                                                
*=================================                                              
PRT040   LA    R1,WSRTPRD          POINT TO PRD 1                               
         BAS   RE,FINDPRD                                                       
         MVC   PPRD(3),WORK                                                     
                                                                                
         CLI   WSRTPRD2,0                                                       
         BE    PRT050                                                           
                                                                                
         LA    R1,WSRTPRD2         P/B                                          
         BAS   RE,FINDPRD                                                       
         MVI   PPRD+3,C'-'                                                      
         MVC   PPRD+4(3),WORK                                                   
*                                                                               
*==================                                                             
* PRINT THE ORDER                                                               
*==================                                                             
PRT050   GOTOR EDTORD,DMCB,PORDER                                               
*                                                                               
*===============================                                                
* PRINT THE MARKET AND STATION                                                  
*===============================                                                
         XC    HALF,HALF                                                        
         LAY   R6,ORDREC+DORFRST-DAREORDD                                       
PRT060   CLI   0(R6),0             END OF RECORD?                               
         JE    PRT080                                                           
         CLI   0(R6),DOSPELQ       FIND X'03'-SUPP ELEM?                        
         BE    PRT070                                                           
         SR    RF,RF                                                            
         ICM   RF,1,1(R6)                                                       
         LA    R6,0(RF,R6)                                                      
         JNZ   PRT060                                                           
         DC    H'0'                                                             
*                                                                               
PRT070   MVC   HALF,DOSPMKT-DOSPELD(R6)                                         
*                                                                               
PRT080   LAY   R1,STAWORK                                                       
         XC    0(32,R1),0(R1)                                                   
         USING STAPACKD,R1                                                      
*                                                                               
         MVI   STAPACT,C'U'                                                     
         MVC   STAPMED,PMED                                                     
         MVC   STAPAGY,OLDAAGY                                                  
         MVC   STAPCTRY,OLDCTRY                                                 
         MVC   STAPMKT,HALF                                                     
         MVC   STAPSTA,WSRTSTA                                                  
         MVC   STAPACOM,=A(COMFACS)                                             
         GOTO1 VSTAPACK,(R1)                                                    
*                                                                               
         OC    HALF,HALF           HAVE MARKET?                                 
         BZ    *+10                 NO, DON'T PRINT IT                          
         MVC   PMKT,STAPQMKT                                                    
*                                                                               
         MVC   PSTA(4),STAPQSTA                                                 
         CLI   STAPQSTA+4,C' '                                                  
         BE    *+14                                                             
         MVI   PSTA+4,C'-'                                                      
         MVC   PSTA+5(1),STAPQSTA+4                                             
*                                                                               
         CLC   STAPQNET,SPACES     CABLE HEAD                                   
         BE    *+14                                                             
         MVI   PSTA+4,C'/'                                                      
         MVC   PSTA+5(3),STAPQNET                                               
*                                                                               
         GOTOR =V(PRINTER)                                                      
*                                                                               
PRTORDX  J     EXIT                                                             
         DROP  R1                                                               
*                                                                               
MDTAB    DC    C'TRNX'             MEDIA LOOKUP TAB                             
*==================                                                             
* FIND ALPHA PROD                                                               
*==================                                                             
FINDPRD  LAY   RF,CLTREC+CLIST-CLTHDRD                                          
*                                                                               
FINDPRD2 CLC   0(1,R1),3(RF)                                                    
         BE    FINDPRD4                                                         
         LA    RF,4(RF)                                                         
         CLI   0(RF),C'A'                                                       
         BNL   FINDPRD2                                                         
         LA    RF,=C'***'                                                       
*                                                                               
FINDPRD4 MVC   WORK(3),0(RF)                                                    
         BR    RE                                                               
*                                                                               
*=============                                                                  
* END OF JOB                                                                    
*=============                                                                  
EOJ      DS    0H                                                               
         GOTOR =V(SORTER),DMCB,=C'END'   ENSURE THAT SORT ENDED                 
         MVI   SORTSW,C'N'               RESET SWITCH                           
         MVI   EOFSW,C'N'                                                       
         CLI   TESTSW,C'Y'                                                      
         BNE   EOJ20                                                            
         LAY   R4,TESTOUT                                                       
         CLOSE ((4))                                                            
*                                                                               
EOJ20    LAY   RF,SSB                                                           
         USING SSOOFF,RF                                                        
         CLI   MCINPUT,C'D'        TEST INPUT=DISK                              
         BNE   EOJX                                                             
         CLI   WRITESW,C'Y'        TEST 'WRITE=Y' OR NONE                       
         BNE   EOJ50                                                            
*                                                                               
         ICM   R7,15,SSOFWNDX      CLOSE FACWK FILE                             
         USING UKRECD,R7                                                        
         JZ    *+2                                                              
*                                                                               
         CP    OUTCOUNT,=P'0'                                                   
         BE    EOJ50                                                            
         LA    R0,=C'CLOSE'                                                     
         GOTOR =V(DATAMGR),DMCB,(X'00',(R0)),=C'FACWRK',(R7),A(BUYRECI)+        
               ,SSOFWBUF                                                        
         ZAP   OUTCOUNT,=P'0'                                                   
         DROP  R7,RF                                                            
                                                                                
* WRITE A RECORD TO THE CHECKPOINT FILE                                         
                                                                                
EOJ50    LAY   R4,CHKPOINT         OPEN CHECKPOINT FILE                         
         OPEN  ((4),OUTPUT)                                                     
         LTR   RF,RF                                                            
         JNZ   *+2                                                              
*                                                                               
         MVC   CARD,SPACES                                                      
         MVC   CARD(3),TODAY                                                    
         MVC   CARD+3(4),SAVERCV   PUT LAST REC FOUND TO CHKPT                  
         MVC   CARD+10(11),=C'OA OA OA OA'                                      
         LAY   R1,CHKPOINT                                                      
         PUT   (1),CARD                                                         
*                                                                               
         LAY   R4,CHKPOINT                                                      
         CLOSE ((4))               CLOSE THE FILE                               
         LTR   RF,RF                                                            
         JNZ   *+2                                                              
*                                                                               
         LAY   RF,SSB                                                           
         USING SSOOFF,RF                                                        
         L     RF,SSOFWNDX                                                      
         USING UKRECD,RF            SET FACWRK KEY VALUES                       
         MVC   UKFILNO,=X'FFFF'     SET TO GET NEW WRKR FILE                    
         ZAP   OUTCOUNT,=P'0'                                                   
         DROP  RF                                                               
*                                                                               
         ZAP   RECCOUNT,=P'0'                                                   
*                                                                               
         MVC   WAITMSG+12(4),WAITTIME+2                                         
         WTO   TEXT=WAITMSGH,MCSFLAG=HRDCPY                                     
*                                                                               
         L     R1,OPECB                                                         
         TM    0(R1),X'40'         TEST OPER EC POSTED                          
         BZ    EOJ60                NO                                          
         CLI   RCVEOFSW,C'Y'        YES, READ UNTIL RECOVERY-EOF?               
         BE    EOJ90                 YES, THEN WE'RE DONE                       
         WTO   'OPER EC POSTED - WILL CONTINUE PROCESSING UNTIL EOF'            
         B     EOJ70                                                            
*                                                                               
*                                                                               
EOJ60    STIMER WAIT,DINTVL=WAITTIME    WAIT SPECIFIED INTERVAL                 
*                                                                               
         L     R1,OPECB                                                         
         TM    0(R1),X'40'         TEST OPER EC POSTED                          
         BZ    EOJ70                                                            
         WTO   'OPER EC POSTED - WILL CONTINUE PROCESSING UNTIL EOF'            
                                                                                
*============================================================                   
* READ THE FIRST RECORD ON THE RECOVERY FILE TO FORCE                           
* REFRESH OF RECOVERY BUFFER                                                    
*============================================================                   
*                                                                               
* FLUSH BUFFERS                                                                 
*                                                                               
EOJ70    LAY   RE,UTL                                                           
         MVC   4(1,RE),SPTUTL                                                   
*                                                                               
         GOTOR =V(DATAMGR),DMCB,DMKEY,=C'SPTDIR',(4,0),0                        
         GOTOR =V(DATAMGR),DMCB,DMKEY,=C'SPTFIL',(4,0),0                        
*&&DO                                                                           
         WTO   'FLUSHING RECOVERY BUFFER'                                       
*                                                                               
         MVC   LASTRCV,=X'00000001'     READ FLUSH FIRST RECOVER RECORD         
         GOTOR =V(DATAMGR),DMCB,(X'24',=C'DMRDIR'),=C'RECOVER',LASTRCV,+        
               RS$RECVHDR,A(RCVBUFF)                                            
*                                                                               
         CLI   TRACSW,C'Y'                                                      
         BNE   EOJ80                                                            
         BRAS  RE,PROCMSG                                                       
*&&                                                                             
EOJ80    MVI   REREAD,C'N'                                                      
         MVC   LASTRCV,SAVERCV     RESTORE LAST D/A                             
*                                                                               
         OC    LASTRCV,LASTRCV                                                  
         BZ    INIT030                                                          
         MVI   REREAD,C'Y'         SET TO REREAD RECORD                         
         BRAS  RE,STRTMSG          PUT WTO MESSAGE WHEN PASS STARTS             
         B     INIT030             AND THEN CONTINUE                            
*                                                                               
EOJ90    WTO   TEXT=EOJMSGH,MCSFLAG=HRDCPY                                      
*                                                                               
EOJX     XBASE                                                                  
         DS    0D                                                               
DMKEY    DC    C'DMKEY   '                                                      
WAITTIME DC    C'00050000'         HHMMSSTH - DEFAULT 5 MINUTES                 
WAITMSGH DC    H'32'                                                            
WAITMSG  DC    CL32'WAITING FOR ....'                                           
*                                                                               
EOJMSGH  DC    H'24'                                                            
EOJMSG   DC    CL24'OPER EOJ RECEIVED'                                          
         EJECT                                                                  
*                                                                               
SESNAM   DC    C'SYS=SPT##'                                                     
*                                                                               
AUTOMSG  DC    C'AUTONOTE*HWON,WHOA:'                                           
SLOWMSGH DC    H'39'                                                            
SLOWMSG  DC    CL39'- SPOT ORDER BUY ACTIVITY HHHMMM BEHIND'                    
*                                                                               
COMMWORK DS    0D                                                               
                                                                                
*                                                                               
         DC    CL8'**OPECB*'                                                    
OPECB    DC    F'0'                                                             
         DC    F'0'                                                             
         DC    CL8'*LASTRCV'                                                    
LASTRCV  DC    F'0'                                                             
SAVERCV  DC    F'0'                                                             
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
BYTE2    DS    X                                                                
         DC    C'**DMCB**'                                                      
DMCB     DS    6F                                                               
         DC    C'***KEY**'                                                      
KEY      DS    XL32                                                             
KEYSAVE  DS    XL32                                                             
DMWORK   DS    XL96                                                             
*                                                                               
         DS    0D                                                               
WORK     DS    CL64                                                             
CARD     DS    CL80                                                             
PACKOF4B DS    PL4                                                              
MYSEQ    DS    F                                                                
DATLEN   DS    F                                                                
ELCODE   DS    X                                                                
MCINPUT  DS    X                                                                
MCRECOVR DS    X                                                                
MCFACPAK DS    X                                                                
MCORIGID DS    XL2                                                              
INITSUCC DC    X'00'                                                            
RCVEOFSW DC    X'00'                                                            
EOFSW    DC    X'00'                                                            
TESTSW   DC    X'00'               WRITE OUTPUT RECS TO TEST FILE               
TRACSW   DC    X'00'               TRACE SOME I/O                               
WRITESW  DC    X'00'                                                            
SORTSW   DC    X'00'                                                            
SVSYSCRD DS    0CL8                                                             
SVSYSPOT DS    CL4                 C'SPOT'                                      
SVSYSSEN DS    CL2                 SENUM                                        
SVSYSAA  DS    CL2                 AGENCY ALPHA                                 
OLDAAGY  DS    CL2                                                              
OLDCTRY  DS    C                                                                
DMINBTS  DS    X                                                                
TODAY    DS    XL3                                                              
SPJULTDY DS    XL3                                                              
TRFUTL   DS    XL1                                                              
SPTUTL   DS    XL1                                                              
SVUTL    DS    XL1                                                              
REREAD   DC    C'N'                                                             
*                                                                               
         DS    0D                                                               
PROFDAR  DS    XL16                                                             
STAWORK  DS    XL32                                                             
AGYTAB   DS    XL64                                                             
*                                                                               
RCVERROR DS    C                   Y OR N/NULL                                  
*                                                                               
AIO      DS    A                                                                
*                                                                               
VRCPACK  DS    V                                                                
VSTAPACK DS    V                                                                
VCABLETB DC    A(0)                                                             
*                                                                               
WKADDR   DS    A                                                                
WKCTR    DC    PL2'0'                                                           
LNCTR    DC    PL2'0'                                                           
RECCOUNT DC    PL4'0'                                                           
OUTCOUNT DC    PL4'0'                                                           
MAXRECS  DC    PL4'02500'          DEFAULT 2500 MAX RECORDS                     
*                                                                               
SLOWTIME DC    PL4'10000'          1 HOUR SLOW TIME                             
SLOWEML  DC    C'N'                                                             
*                                                                               
DELAY DC       PL4'00500'          5 MINUTE DELAY BUFFER                        
         EJECT                                                                  
EMSG     DS    CL80                ERROR MESSAGE                                
*                                                                               
SRTCARDR DC    CL80'SORT FIELDS=(5,16,A),FORMAT=BI,WORK=1'                      
RECCARDR DC    CL80'RECORD TYPE=V,LENGTH=6100'                                  
*                                                                               
SRTCARDW DC    CL80'SORT FIELDS=(1,11,A),FORMAT=BI,WORK=1'                      
RECCARDW DC    CL80'RECORD TYPE=F,LENGTH=18'                                    
*                                                                               
MISCLTMS DC    C'MISSING CLIENT A/M=XX  CLT=XXXX'                               
         LTORG                                                                  
         EJECT                                                                  
         DS    0D                                                               
         DC    C'*WSRTKEY'                                                      
WSRTREC  DS    0XL18                                                            
*                                                                               
WSRTKEY  DS    0XL11               ACTIVITY SORT KEY                            
WSRTAM   DS    XL1                                                              
WSRTCLT  DS    XL2                                                              
WSRTPRD  DS    XL1                                                              
WSRTEST  DS    XL1                                                              
WSRTSTA  DS    XL3                                                              
WSRTPRD2 DS    XL1                                                              
WSRTFLT  DS    XL1                                                              
WSRTFLT0 EQU   C'0'                FLIGHT 0 AS C'0'                             
WSRTTYPE DS    XL1                                                              
WSRTCASH EQU   C'C'                CASH                                         
WSRTTRAD EQU   C'T'                TRADE                                        
WSRTLNQ  EQU   *-WSRTKEY                                                        
*                                                                               
WSRTAA   DS    CL2                                                              
WSRTDATE DS    XL3                                                              
WSRTTIME DS    XL2                                                              
WSRTRLNQ EQU   *-WSRTREC                                                        
*                                                                               
         DS    0D                                                               
         DC    C'*ORDKEY*'                                                      
WORDKEY  DS    0XL11               ORDER KEY                                    
*                                                                               
WORDAM   DS    XL1                                                              
WORDCLT  DS    XL2                                                              
WORDPRD  DS    XL1                                                              
WORDEST  DS    XL1                                                              
WORDSTA  DS    XL3                                                              
WORDPRD2 DS    XL1                                                              
*                                                                               
WORDFLTN DS    XL1                                                              
WORDFLG  DS    XL1                                                              
*                                                                               
WORDAA   DS    CL2                                                              
*                                                                               
*                                                                               
         EJECT                                                                  
         DS    0D                                                               
         DC    C'*RECVREC'                                                      
RCVLEN   DS    H                   LOGICAL IOCS LEN                             
         DS    H                                                                
RSORTREC DS    0XL20                                                            
RSORTKEY DS    0XL13                                                            
*                                                                               
RSORTAM  DS    XL1                                                              
RSORTCLT DS    XL2                                                              
RSORTEST DS    XL1                                                              
RSORTMST DS    0XL5                                                             
RSORTMKT DS    XL2                                                              
RSORTSTA DS    XL3                                                              
RSORTPRD DS    XL1                                                              
RSORTBUY DS    XL3                                                              
*                                                                               
RSORTSEQ DS    XL3                                                              
RSPARE   DS    XL4                                                              
RSORTLNQ EQU   *-RSORTREC                                                       
*                                                                               
         PRINT ON                                                               
*PREFIX=RS$                        CURRENT RECOVER RECORD                       
       ++INCLUDE DMRCVRHDR                                                      
*PREFIX=                                                                        
         PRINT NOGEN                                                            
RKEY     DS    0CL13               IOAREA FOR RECOVERY RECORD                   
         DS    6100C                                                            
         EJECT                                                                  
*===============================================================                
* NOTE: AFTER THIS LOCATION, NO MORE ADDRESSABILITY                             
*===============================================================                
*                                                                               
*==============================================================                 
* READ ONLINE RECOVERY FILE                                                     
* IF NO PREVIOUS DISK ADDRESS, TRY TO READ IT FROM CHECKPOINT                   
*==============================================================                 
                                                                                
READRCV  NTR1  BASE=*,LABEL=*                                                   
         OC    LASTRCV,LASTRCV     HAVE A DISK ADDRESS?                         
         BNZ   RDRCV060            YES                                          
                                                                                
* NO - SEE IF THERE'S A CHECKPOINT RECORD                                       
                                                                                
         LAY   R4,CHKPOINT         OPEN CHECKPOINT FILE                         
         OPEN  ((4),INPUT)                                                      
         LTR   RF,RF                                                            
         JNZ   *+2                                                              
*                                                                               
RDRCV020 LAY   R1,CHKPOINT         AND READ THE RECORD                          
         GET   (1),CARD                                                         
*                                                                               
RDRCV040 LAY   R4,CHKPOINT                                                      
         CLOSE ((4))               CLOSE THE INPUT FILE                         
*                                                                               
         MVI   REREAD,C'N'         ASSUME CHECKPOINT INVALID                    
         CLC   TODAY,CARD          TEST DATA CURRENT                            
         BNE   RDRCV050            NO                                           
         MVC   LASTRCV,CARD+3      AND USE THIS DISK ADDRESS IF IT IS           
         OC    LASTRCV,LASTRCV                                                  
         BZ    *+8                                                              
         MVI   REREAD,C'Y'         AND MAKE SURE TO REREAD RECORD               
*                                                                               
RDRCV050 BRAS  RE,STRTMSG          PUT WTO MESSAGE WHEN PASS STARTS             
*                                                                               
RDRCV060 MVC   SAVERCV,LASTRCV     SAVE DA OF LAST REC THAT WAS FOUND           
*                                                                               
         LAY   RE,UTL                                                           
         MVC   4(1,RE),SPTUTL                                                   
*                                                                               
RDRCV070 LA    R0,=C'DMRDIR'                                                    
         MVI   BYTE,X'24'          READ FLUSH WHEN READING CHKPOINT             
         CLI   REREAD,C'Y'                                                      
         BE    RDRCV075                                                         
         MVI   BYTE,0              CLEAR FLUSH FLAG                             
         LA    R0,=C'DMRSEQ'                                                    
*                                                                               
RDRCV075 DS    0H                                                               
         GOTOR =V(DATAMGR),DMCB,(BYTE,(R0)),=C'RECOVER',LASTRCV,       +        
               RS$RECVHDR,A(RCVBUFF)                                            
*                                                                               
         CLI   TRACSW,C'Y'                                                      
         BNE   RDRCV080                                                         
         BRAS  RE,PROCMSG                                                       
*                                                                               
RDRCV080 TM    8(R1),X'80'         TEST EOF                                     
         BO    RDRCVEOF                                                         
*                                                                               
         CLI   8(R1),0             TEST ANY OTHER ERROR                         
         BNE   RDRCV085                                                         
*                                                                               
         CLI   REREAD,C'Y'         IF THIS WAS A REREAD                         
         BNE   RDRCV100            NO - PROCESS RECORD                          
         MVI   REREAD,C'N'         GO GET NEXT RECORD NOW                       
         B     RDRCV070                                                         
*                                                                               
RDRCV085 WTO   TEXT=RCVERRH,MCSFLAG=HRDCPY                                      
*                                                                               
RDRCV090 XC    LASTRCV,LASTRCV     START AT BEGINNING OF FILE                   
         MVI   REREAD,C'N'                                                      
         MVI   RDRCV090,0          BUT ONLY DO THIS ONCE!                       
         B     RDRCV050                                                         
*                                                                               
RCVERRH  DC    H'42'                                                            
RCVERR   DC    CL42'RECOVERY READ ERROR, REREAD FROM BEGINNING'                 
*                                                                               
RDRCV100 CLI   RS$RFILTY,QSPTFIL   TEST SPTFILE                                 
         BNE   RDRCV060                                                         
         CLI   RKEY,X'10'          TEST BUYREC (LOWER BOUND)                    
         BL    RDRCV060                                                         
*                                                                               
         L     R1,OPECB                                                         
         TM    0(R1),X'40'         TEST OPER EC POSTED                          
         BO    RDRCV110             YES, RELAX RULES & PROC TIL RCV-EOF         
*                                                                               
* DON'T PROCESS RECOVERY IF CHANGE MADE WITHIN LAST 5 MINUTES                   
*  AKA - 'DELAY BUFFERS'                                                        
*                                                                               
         CLC   RS$RDATE,TODAY      RCV-DATE BEFORE TODAY?                       
         BL    RDRCV110             YES, COULD BE OVERNIGHT, PROCESS IT         
*                                                                               
         TIME                                                                   
         SRL   R0,8                SHIFT HHMMSSUU DROPPING MILLISECS            
*                                                                               
         MVC   PACKOF4B,RS$RTIME   TIME 0HHMMSSC WHERE SIGN=F IF 1ST            
         NI    PACKOF4B,X'FF'-RS$RTIMEFID+RS$RTIMEEXT   CLEAR FLAGS             
         AP    PACKOF4B,DELAY      ADJUST, ADD DELAY BUFFER                     
*                                                                               
         L     RE,PACKOF4B                                                      
         SRL   RE,4                DROP THE SIGN                                
         CR    RE,R0               CHG MADE WITHIN LAST 5 MINUTES?              
         BNL   RDRCVNO                                                          
*                                                                               
RDRCV110 LA    RE,RS$RECVHDR+24    POINT TO KEY                                 
         LLH   RF,13(RE)           GET REC LENGTH                               
         LA    RF,4+24(RF)         ADJUST LENGTH FOR LEN+RS$RECVHDR             
         SLL   RF,16               MAKE IT LL00                                 
         LA    RE,RS$RECVHDR-4                                                  
         ST    RF,0(RE)                                                         
*                                                                               
         AP    RECCOUNT,=P'1'      BUMP RECORD COUNTER                          
*                                                                               
RDRCVYS  J     EXITY                                                            
RDRCVEOF MVI   RCVEOFSW,C'Y'                                                    
RDRCVNO  J     EXITN                                                            
         EJECT                                                                  
         LTORG                                                                  
*                                                                               
*======================================================================         
* BUILD THE ESTIMATE WEEK TABLE                                                 
*======================================================================         
BESTWKTB NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         LAY   RE,EWEEKTAB         INIT ESTIMATE WEEK TABLE                     
         LAY   RF,EWEEKTABX-EWEEKTAB                                            
         XCEFL                                                                  
*                                                                               
         USING ESTHDRD,R2                                                       
         LAY   R2,ESTREC                                                        
*                                                                               
         GOTO1 =V(GETDAY),DMCB,ESTART,WORK                                      
         CLI   0(R1),0             TEST DAY RETURNED                            
         JE    *+2                                                              
         ZIC   R0,0(R1)            GET DAY NUMBER                               
         BCTR  R0,0                GIVES DAYS TO MONDAY                         
         LCR   R0,R0               SET TO BACK UP TO PREVIOUS MONDAY            
         AHI   R0,7                AND BUMP TO NEXT MONDAY                      
         MVC   WORK(6),ESTART                                                   
*                                                                               
         LAY   R4,EWEEKTAB         POINT TO ESTIMATE WEEK TABLE                 
*                                                                               
         GOTO1 =V(DATCON),DMCB,ESTART,(2,(R4))                                  
         LA    R4,2(R4)            BUMP WEEK TABLE                              
*                                                                               
BEWT010  LAY   RE,EWEEKTABX        POINT TO END OF ESTIMATE WEEK TABLE          
         CR    R4,RE               OUT OF SPACE??                               
         JNL   *+2                  YES, DIE                                    
*                                                                               
         GOTO1 =V(ADDAY),DMCB,WORK,WORK+6,(R0)  WORK+6  = START OF WEEK         
         GOTO1 =V(DATCON),DMCB,WORK+6,(2,(R4))                                  
*                                                                               
         CLC   WORK+6(6),EEND      COMPARE TO START OF WEEK TO EST-END          
         BH    BEWT020             -PAST, OVERWRITE LAST DATE W/EST-END         
         LA    R4,2(R4)            BUMP WEEK TABLE                              
         BE    BEWT020             -ON, INCL LAST WEEK BY ADD EST-END           
         MVC   WORK(6),WORK+6      -BEFORE, NOT DONE YET                        
         LA    R0,7                ADD 7 DAYS TO GET NEXT WEEK                  
         B     BEWT010                                                          
*                                                                               
BEWT020  GOTO1 =V(DATCON),DMCB,EEND,(2,(R4))                                    
*                                                                               
BEWTX    J     EXIT                                                             
         DROP  R2                                                               
*                                                                               
*======================================================================         
* BUILD THE FLIGHT TABLE                                                        
*======================================================================         
BFLTTAB  NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         USING DFLRECD,R2                                                       
         LAY   R2,FLTREC+DFLEL-DFLRECD   POINT TO FIRST ELEMENT                 
         CLI   0(R2),DFINFELQ      HAVE OPTIONAL X'01' FLIGHT 0?                
         BNE   BFT010                                                           
         USING DFINFEL,R2                                                       
         LAY   R4,FLTFLT0                                                       
         GOTO1 =V(DATCON),DMCB,(3,DFINFSDT),(2,(R4))                            
         DROP  R2                                                               
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,1,1(R2)                                                       
         LA    R2,0(RF,R2)         BUMP TO FIRST FLIGHT ELEMENT                 
         JZ    *+2                 DIE ON ELEMENT LENGTH ZERO!                  
*                                                                               
BFT010   LAY   R4,FLTFLTS          POINT TO FIRST FLIGHT1 ENTRY                 
*                                                                               
BFT020   CLI   0(R2),0             NO MORE ELEMENTS?                            
         BE    BFTX                 NO, WERE DONE, EXIT                         
*                                                                               
         CLI   0(R2),DFFLTELQ      HAVE X'05' FLIGHT ELEMENT?                   
         JNE   BFTX                  NO, NO MORE                                
         USING DFFLTEL,R2                                                       
*                                                                               
         LAY   RE,FLTTABX                                                       
         CR    R4,RE               OUT OF SPACE IN TABLE?                       
         JNL   *+2                  YES, DIE                                    
*                                                                               
         GOTO1 =V(DATCON),DMCB,(3,DFFLTSTR),(2,(R4))                            
         GOTO1 =V(DATCON),DMCB,(3,DFFLTEND),(2,2(R4))                           
         DROP  R2                                                               
*                                                                               
         LA    R4,L'FLTFLTS(R4)    BUMP TO NEXT ENTRY IN FLIGHT TABLE           
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,1,1(R2)                                                       
         LA    R2,0(RF,R2)         BUMP TO NEXT FLIGHT ELEMENT                  
         JNZ   BFT020                                                           
         DC    H'0'                DIE ON ELEMENT LENGTH ZERO!                  
*                                                                               
BFTX     LAY   R4,FLTTAB           MAKE SURE TABLE IS VALID                     
         OC    0(FLTTABX-FLTTAB,R4),0(R4)                                       
         JNZ   EXIT                                                             
         DC    H'0'                                                             
                                                                                
*======================================================================         
* LOCATE SPOT SYSTEM AND GET SENUM. OPTIONAL AGENCY ALPHA ALSO                  
* CONVERT TO USE TWO CHR SYSTEM NAME SPOTXYAA WHERE XY=SYS AND AA=AGY           
*======================================================================         
                                                                                
GETSEAA  NTR1  BASE=*,LABEL=*                                                   
         CLI   CARD+7,C' '         SPOTXAA > SPOTX AA                           
         BNE   GETSE010                                                         
         CLI   CARD+6,C' '                                                      
         BE    GETSE010                                                         
         ICM   R0,3,CARD+5         MOVE AGY CODE ONE BYTE TO RIGHT              
         MVI   CARD+5,C' '                                                      
         STCM  R0,3,CARD+6                                                      
*                                                                               
GETSE010 MVC   SESNAM+7(2),CARD+4  EXTRACT ONE/TWO CHR SYSTEM ID                
*                                                                               
GETSE020 GOTOR =V(DMDDNAME),DMCB,(X'24',=C'DDNAME'),SESNAM,0                    
         CLI   8(R1),0                                                          
         BNE   GETSENO                                                          
         L     RF,8(R1)            GET A(FILE INFO LIST)                        
         MVC   SPTUTL,1(RF)        EXTRACT SENUM                                
         B     GETSEYS                                                          
*                                                                               
GETSENO  J     EXITN               ERROR                                        
GETSEYS  J     EXITY               VALID                                        
         LTORG                                                                  
                                                                                
*====================================================                           
* OPEN FILES                                                                    
*  IF WRITESW=Y, READ FOR UPDATE, OTHERWISE READONLY                            
*====================================================                           
OPENFILE NTR1  BASE=*,LABEL=*                                                   
         CLI   WRITESW,C'Y'        TEST 'WRITE=Y' OR NONE                       
         JE    OPFI020                                                          
         LAY   R1,FLIST                                                         
*                                                                               
OPFI010  MVI   0(R1),C'N'          SET FILES FOR NO UPDATE                      
         LA    R1,L'FLIST(R1)                                                   
         CLI   0(R1),C'X'                                                       
         JNE   OPFI010                                                          
*                                                                               
OPFI020  LAY   RE,UTL                                                           
         MVC   4(1,RE),SPTUTL                                                   
         GOTOR =V(DATAMGR),DMCB,=C'DMOPEN',=C'SPOT',FLIST,A(IO1)                
         JE    EXIT                                                             
         DC    H'0'                                                             
*                                                                               
         DS    0H                                                               
FLIST    DS    0CL8                                                             
         DC    CL8' SPTFILE'                                                    
         DC    CL8' SPTDIR '                                                    
         DC    CL8'NSTAFILE'                                                    
         DC    CL8'NCTFILE '                                                    
         DC    CL8'NRECV   '                                                    
         DC    CL8'X       '                                                    
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*=====================================================================*         
* READ CONTROL FILE TO GET USERID NUMBER FOR THIS SYSTEM                        
* AND SETUP JOB AS UPDATIVE SOON                                                
*=====================================================================*         
STUPSOON NTR1  BASE=*,LABEL=*                                                   
         XC    KEY,KEY                                                          
         MVI   KEY,C'I'                                                         
*                                                                               
******************                                                              
*** SPOT0 ORIGINALLY DIDN'T HAVE A DDSZ0 USERID ON CONTROL, SO I NEEDED         
*** TO READ THE TMNY USERID RECORD INSTEAD                                      
***  I'M LEAVING THIS CODE HERE IN CASE WE RUN INTO A PROBLEM WHERE A           
***  DDSZ** USERID DOES NOT EXIST IN THE FUTURE       -HWON 01/04/2017          
******************                                                              
***                                                                             
***      MVC   KEY+15(4),=C'TMNY'                                               
***      CLI   SESNAM+7,C'0'           SPOT0?                                   
***      BNE   *+14                                                             
***      CLC   SVSYSCRD+6(2),=C'TMNY'  TMNY?                                    
***      BE    STSO005                                                          
***                                                                             
         MVC   KEY+15(4),=C'DDSZ'                                               
         MVC   KEY+19(2),SESNAM+7                                               
STSO005  GOTOR =V(DATAMGR),DMCB,=C'DMRDHI',=C'CTFILE',KEY,A(BUYRECI)            
*                                                                               
******************                                                              
***  I'M LEAVING THIS CODE HERE IN CASE WE RUN INTO A PROBLEM WHERE A           
***  DDSZ** USERID DOES NOT EXIST IN THE FUTURE       -HWON 01/04/2017          
******************                                                              
***                                                                             
***      LA    RF,18                                                            
***      CLC   KEY+15(4),=C'TMNY'                                               
***      BE    *+8                                                              
***                                                                             
         LA    RF,20                                                            
*                                                                               
         LAY   RE,BUYRECI                                                       
         CLC   KEY(0),0(RE)                                                     
         EX    RF,*-6                                                           
         JNE   *+2                                                              
*                                                                               
         LA    RE,28(RE)           POINT TO FIRST ELEMENT                       
         SR    R0,R0                                                            
*                                                                               
STSO010  CLI   0(RE),2                                                          
         JE    STSO020                                                          
         CLI   0(RE),0                                                          
         JE    *+2                                                              
         IC    R0,1(RE)                                                         
         AR    RE,R0                                                            
         J     STSO010                                                          
*                                                                               
STSO020  MVC   MCORIGID,2(RE)       SAVE USERID NUMBER                          
                                                                                
         GOTOR =V(DATCON),DMCB,(5,0),(1,FULL)                                   
         LAY   RF,SSB                                                           
         USING SSOOFF,RF                                                        
         MVI   SSOXTND,X'FF'   +02  X'FF' TO SHOW OFFLINE EXTENSION             
         OI    SSOSTAT2,SSOSRWRK    SET FACWK RECOVERY BIT IN SSB               
*                                                                               
         L     RF,SSOFWNDX                                                      
         USING UKRECD,RF            SET FACWRK KEY VALUES                       
*                                                                               
         MVC   UKUSRID,MCORIGID     USERID                                      
         MVI   UKSYSPRG,C'S'        SYSTEM                                      
         MVC   UKSYSPRG+1(2),=C'OA' PROGRAM                                     
         MVC   UKSUBPRG,MCFACPAK    FACPAK SYSTEM ID                            
         MVC   UKDAY,FULL+2         DAY NUMBER                                  
         MVI   UKCLASS,C'R'         CLASS=RECOVERY                              
         MVI   UKFLAG,X'01'         FLAG DUPLICATES ALLOWED                     
         MVC   UKFILNO,=X'FFFF'     LET WRKR KNOW UKFLAG IS VALID               
         J     EXIT                                                             
         DROP  RF                                                               
         LTORG                                                                  
         EJECT                                                                  
                                                                                
*=====================================================================*         
* READ THE AGENCY HEADERS AND BUILD A TABLE                                     
* THAT IDENTIFIES AGENCY AS US OR CANADIAN                                      
*=====================================================================*         
                                                                                
BLDAG    NTR1  BASE=*,LABEL=*                                                   
         LAY   RE,UTL                                                           
         MVC   4(1,RE),SPTUTL                                                   
*                                                                               
         XC    AGYTAB,AGYTAB                                                    
         XC    KEY,KEY                                                          
         MVI   KEY,6                                                            
         BRAS  RE,HIGH                                                          
         J     BDLAG040                                                         
*                                                                               
BDLAG020 BRAS  RE,SEQ                                                           
*                                                                               
BDLAG040 CLI   KEY,6                                                            
         JNE   BLDAGX                                                           
         LAY   R6,IO1                                                           
         ST    R6,AIO                                                           
         BRAS  RE,GETREC                                                        
*                                                                               
         LA    R6,24(R6)                                                        
         SR    R0,R0                                                            
*                                                                               
BDLAG060 IC    R0,1(R6)                                                         
         AR    R6,R0                                                            
         CLI   0(R6),0                                                          
         JE    BDLAG020                                                         
         CLI   0(R6),2             FIND A MEDIA ELEMENT                         
         JNE   BDLAG060                                                         
         LLC   RE,3(R6)            GET AG/MD BYTE                               
         SRL   RE,4                DROP MEDIA                                   
         SLL   RE,2                X4                                           
         LA    RE,AGYTAB(RE)                                                    
         CLI   0(RE),0             TEST SOMETHING THERE ALREADY                 
         JNE   BDLAG020            YES - SKIP                                   
         LAY   RF,IO1                                                           
         MVC   0(2,RE),1(RF)       MOVE AGYALPHA TO TABLE                       
         LA    RF,AGYPROF+7-AGYHDR(RF)                                          
         MVC   2(1,RE),0(RF)       MOVE COUNTRY CODE TO TABLE                   
         J     BDLAG020                                                         
*                                                                               
BLDAGX   J     EXIT                                                             
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*=====================================================================*         
* CLEAR AND INITIALIZE ORDA TABLE WHERE R4 == A(ORDA TABLE)                     
*=====================================================================*         
ORDACLR  NTR1  BASE=*,LABEL=*                                                   
         USING ORDATABD,R4                                                      
         LR    RE,R4                                                            
         LH    RF,0(R4)                                                         
         LA    RE,2(RE)            SKIP PAST LENGTH                             
         LAY   RF,-2(RF)                                                        
         XCEFL                                                                  
*                                                                               
         LAY   RE,CLTREC+CLIST-CLTHDRD   GET A(CLIST)                           
         ST    RE,ORDAAPRD                                                      
*                                                                               
         LAY   RE,ESTREC+EDEMOS-ESTHDRD  GET A(EDEMOS)                          
         ST    RE,ORDAADEM                                                      
*                                                                               
         LAY   RE,EWEEKTAB         GET A(EST WEEK TAB)                          
         ST    RE,ORDAETAB                                                      
*                                                                               
         LAY   RE,FLTTAB           GET A(FLIGHT TABLE)                          
         ST    RE,ORDAFTAB                                                      
*                                                                               
         LAY   RE,COPYTRCK         GET A(COPY REC TRACKED FIELD)                
         ST    RE,ORDAATRK                                                      
*                                                                               
         LAY   RE,DARREPLO         GET A(REPLO AND REPHI)                       
         ST    RE,ORDAAREP                                                      
*                                                                               
         J     EXIT                                                             
         DROP  R4                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*=====================================================================*         
* PUT A MESSAGE TO THE CONSOLE HARDCOPY WHEN A PASS STARTS                      
*=====================================================================*         
STRTMSG  NTR1  BASE=*,LABEL=*                                                   
         GOTOR =V(HEXOUT),DMCB,LASTRCV,MESSAGE+23,4,=C'TOG'                     
         WTO   TEXT=MSGH,MCSFLAG=HRDCPY                                         
         J     EXIT                                                             
*                                                                               
MSGH     DC    AL2(32)                                                          
MESSAGE  DC    CL32'START PASS AT DISKADDR 00000000'                            
*                                                                               
*=====================================================================*         
* PUT A MESSAGE TO THE CONSOLE HARDCOPY WHEN A PROC RECOVERY                    
*=====================================================================*         
PROCMSG  NTR1  BASE=*,LABEL=*                                                   
         L     R1,DMCB           RELIES ON DMCB STILL HAVING DMGR PARMS         
         MVC   MESSAGE2+9(6),0(R1)                                              
         MVC   MESSAGE2+24(5),=C'     '                                         
         TM    DMCB,X'24'          FLUSHING RECOVERY BUFFER?                    
         BNO   *+10                                                             
         MVC   MESSAGE2+24(5),=C'FLUSH'                                         
*                                                                               
         GOTOR =V(HEXOUT),DMCB2,LASTRCV,MESSAGE2+42,4,=C'TOG'                   
         WTO   TEXT=MSGH2,MCSFLAG=HRDCPY                                        
         J     EXIT                                                             
*                                                                               
MSGH2    DC    AL2(50)                                                          
MESSAGE2 DC    CL50'TRACE=Y -XXXXXX RECOVER XXXXX AT DISKADDR 00000000'         
DMCB2    DS    6F                                                               
*                                                                               
DUMPLIST DS    0F                                                               
         DC    A(ORDAUPDT,65000)                                                
         ORG   *-4                                                              
         DC    X'80'                                                            
         ORG                                                                    
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*=====================================================================*         
* SET UP OPERATOR COMMUNICATIONS                                                
* OPERATOR NEEDS TO SEND A MODIFY EOJ COMMAND TO END RUN                        
*=====================================================================*         
SETOPS   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         EXTRACT ACOMM,FIELDS=COMM                                              
         L     RF,ACOMM                                                         
         USING COMLIST,RF                                                       
         LAY   RE,OPECB                                                         
         MVC   1(3,RE),COMECBPT+1                                               
         L     R2,COMCIBPT         GET A(CIB)                                   
         USING CIBNEXT,R2                                                       
         LA    R3,COMCIBPT         SET A(A(CIB))                                
         DROP  RF                                                               
*                                                                               
         CLI   CIBVERB,CIBSTART    TEST FOR 'S JOBNAME' (IE NOT BATCH)          
         BNE   SETOP2                                                           
         QEDIT ORIGIN=(R3),BLOCK=(R2)        RELEASE THE CIB                    
         DROP  R2                                                               
*                                                                               
SETOP2   QEDIT ORIGIN=(R3),CIBCTR=1          ACCEPT MODIFY COMMANDS             
         WTO   TEXT=CIBMSGH,MCSFLAG=HRDCPY                                      
*                                                                               
         J     EXIT                                                             
CIBMSGH  DC    H'24'                                                            
CIBMSG   DC    CL24'OPER INTRPTS ENABLED'                                       
ACOMM    DC    A(0)                                                             
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
         DS    0A                                                               
COMFACS  DS    CL(COMFACSL)                                                     
                                                                                
DARPROF  DS    XL16                                                             
DARREPLO DS    XL2                                                              
DARREPHI DS    XL2                                                              
                                                                                
         DS    0D                                                               
IO1      DS    4000C               IO AREA1                                     
*                                                                               
         DS    0D                                                               
         DC    C'*CLTREC*'                                                      
CLTREC   DS    CL(1024*2)          IOAREA FOR CLTHDR                            
*                                                                               
         DS    0D                                                               
         DC    C'*ESTREC*'                                                      
ESTREC   DS    CL(1024*2)          IOAREA FOR ESTHDR                            
*                                                                               
         DS    0D                                                               
         DC    C'*FLTREC*'                                                      
FLTREC   DS    CL(1024*2)          IOAREA FOR FLTHDR                            
*                                                                               
         DS    0D                                                               
         DC    C'*ORDREC*'                                                      
ORDREC   DS    CL(1024*6)          IOAREA FOR DARE ORDER RECORD                 
*                                                                               
         DS    0D                                                               
         DC    C'*BUYREC*'                                                      
BUYRECI  DS    CL(1024*6)          IOAREA FOR BUYREC                            
*                                                                               
         DS    0D                                                               
         DC    C'**COPY**'         COPY OF TRACKED FIELDS                       
COPYTRCK DS    XL(ORDTLNQ)                                                      
*                                                                               
P1       DC    CL132' '                                                         
P2       DC    CL132' '                                                         
P3       DC    CL132' '                                                         
P4       DC    CL132' '                                                         
P5       DC    CL132' '                                                         
P6       DC    CL132' '                                                         
P7       DC    CL132' '                                                         
P8       DC    CL132' '                                                         
P9       DC    CL132' '                                                         
P10      DC    CL132' '                                                         
P11      DC    CL132' '                                                         
P12      DC    CL132' '                                                         
*                                                                               
BUYMID1  DC    CL132'AG M CLT MKT  STATION  PRD-PR2 EST FL T ORDER'             
BUYMID2  DC    CL132'-- - --- ---- -------- ------- --- -- - --------'          
*                                                                               
MISSCLT  DC    CL132'MISS CLT,RCV REC BYPASSED.  A/M=XX  CLT=XXXX  PRD=+        
               XX  MKSTA=XXXXXXXXXX  EST=XX'                                    
BRDCLT   DC    CL132'BRD  CLT,RCV REC BYPASSED.  A/M=XX  CLT=XXXX  PRD=+        
               XX  MKSTA=XXXXXXXXXX  EST=XX'                                    
MISSEST  DC    CL132'MISS EST,RCV REC BYPASSED.  A/M=XX  CLT=XXXX  PRD=+        
               XX  MKSTA=XXXXXXXXXX  EST=XX'                                    
*                                                                               
RECVIN   DCB   DDNAME=RECVIN,DSORG=PS,RECFM=VB,LRECL=8200,             +        
               MACRF=GM,EODAD=PLSXTAP                                           
*                                                                               
TESTOUT  DCB   DDNAME=TESTOUT,DSORG=PS,RECFM=VB,LRECL=8200,            +        
               BLKSIZE=27648,MACRF=PM                                           
*                                                                               
WORKFILE DCB   DDNAME=WORKFILE,DSORG=PS,RECFM=FB,LRECL=13,             +        
               BLKSIZE=1300,MACRF=(GM,PM),EODAD=ENDIN2                          
*                                                                               
CHKPOINT DCB   DDNAME=CHKPOINT,DSORG=PS,RECFM=FB,LRECL=80,             +        
               BLKSIZE=160,MACRF=(GM,PM),EODAD=RDRCV040                         
*                                                                               
         DS    0D                                                               
         DC    C'*ORDTB1*'                                                      
ORDATAB1 DS    0F                                                               
         DC    AL2(ORDATABX1-ORDATAB1)                                          
         DC    6000X'00'                                                        
ORDATABX1 EQU  *                                                                
*                                                                               
         DS    0D                                                               
         DC    C'*EWKTAB*'                                                      
         DS    0F                                                               
EWEEKTAB DS    54XL(L'RDATE)       (53_WK)*(COMPRESSED_DATE)                    
EWEEKTABX EQU  *                                                                
         DC    X'00'                                                            
*                                                                               
         DS    0D                                                               
         DC    C'*FLTTAB*'                                                      
FLTTAB   DS    0F                                                               
FLTFLT0  DS    XL2                 FLIGHT 0 (COMPRESSED END DATE)               
FLTFLTS  DS    16XL(2*L'RDATE)     (16_FLT)*(START&END COMPRESSED DATE)         
FLTTABX  EQU   *                                                                
         DC    X'00'                                                            
*                                                                               
         DS    0D                                                               
         DC    CL12'** FACINDX **'                                              
FACINDX  DC    16X'00'                                                          
*                                                                               
         DC    CL12'** FACBUFF **'                                              
FACBUFF  DS    6144C               6K BUFFER                                    
         DS    XL256               FOR SAFETY                                   
         ORG                                                                    
*                                                                               
RCVBUFF  DS    64000C                                                           
         EJECT                                                                  
         DS    0D                                                               
         DC    CL8'**UTL **'                                                    
UTL      DC    8X'00'                                                           
*                                                                               
         DC    CL16'*****  SSB  ****'                                           
SSB      DC    XL2'00',X'FF',X'00' RECOVER !!!!!!!!!!!!!!!!!                    
         DC    5F'0'                                                            
         DC    A(FACINDX)                                                       
         DC    A(FACBUFF)                                                       
         DC    56F'0'                                                           
         EJECT                                                                  
                                                                                
*==============================                                                 
* START OF DSECTS                                                               
*==============================                                                 
                                                                                
       ++INCLUDE DDDPRINT                                                       
DPRINT   DSECT                                                                  
         ORG   P                                                                
PLINE    DS    0C                                                               
PAGY     DS    CL2                                                              
         DS    CL1                                                              
PMED     DS    CL1                                                              
         DS    CL1                                                              
PCLT     DS    CL3                                                              
         DS    CL1                                                              
PMKT     DS    CL4                                                              
         DS    CL1                                                              
PSTA     DS    CL7                                                              
         DS    CL2                                                              
PPRD     DS    CL7                                                              
         DS    CL1                                                              
PEST     DS    CL3                                                              
         DS    CL1                                                              
PFLT     DS    CL2                                                              
         DS    CL1                                                              
PTYPE    DS    CL1                                                              
         DS    CL1                                                              
PORDER   DS    CL8                                                              
         DS    CL1                                                              
PERRTEXT DS    0C                                                               
         ORG                                                                    
         EJECT                                                                  
       ++INCLUDE SPGENDRORD                                                     
         EJECT                                                                  
       ++INCLUDE SPGENDRFLT                                                     
         EJECT                                                                  
CLTHDRD  DSECT                                                                  
       ++INCLUDE SPGENCLT                                                       
         EJECT                                                                  
ESTHDRD  DSECT                                                                  
       ++INCLUDE SPGENEST                                                       
         EJECT                                                                  
BUYRECD  DSECT                                                                  
       ++INCLUDE SPGENBUY                                                       
         EJECT                                                                  
AGYHDRD  DSECT                                                                  
       ++INCLUDE SPGENAGY                                                       
         EJECT                                                                  
       ++INCLUDE DDCOMFACS                                                      
         EJECT                                                                  
       ++INCLUDE SPORDATABD                                                     
         EJECT                                                                  
*                                                                               
IEZCIBD  DSECT                                                                  
         IEZCIB                                                                 
                                                                                
IEZCOMD  DSECT                                                                  
         IEZCOM                                                                 
* DDCOREQUS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOREQUS                                                      
       ++INCLUDE FASSBOFF                                                       
       ++INCLUDE SPSTAPACKD                                                     
       ++INCLUDE DMWRKRK                                                        
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004SPORDAUP  10/23/19'                                      
         END                                                                    
