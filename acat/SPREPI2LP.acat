*          DATA SET SPREPI2LP  AT LEVEL 041 AS OF 07/12/07                      
*CATALP SPI2LETR                                                                
         TITLE 'SPI2LETR - I2 LETTER PRINTING'                                  
         PRINT NOGEN                                                            
***********************************************************************         
***********************************************************************         
***********************************************************************         
***********************************************************************         
***********************************************************************         
***********************************************************************         
*             THIS MODULE IS DEAD AS OF 7/12/07 R.I.P                 *         
***********************************************************************         
***********************************************************************         
***********************************************************************         
***********************************************************************         
***********************************************************************         
***********************************************************************         
LETPRNT  CSECT                                                                  
         NMOD1 0,LETPRNT                                                        
         USING SPWORKD,RA,R9                                                    
         LA    RC,SPACEND                                                       
         USING IMRWORK,RC                                                       
*                                                                               
         MVC   SAVSPROG,RCSUBPRG                                                
         MVI   RCSUBPRG,201        SUPPRESS ALL SPEC HEADLINES                  
         MVI   FORCEHED,C'Y'                                                    
         MVI   INSW,C'N'           CLEAR ACTIVE SWITCH                          
*                                                                               
         OPEN  (I2LETRS,(INPUT))                                                
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
LTP04    DS    0H                                                               
         GET   I2LETRS,INLINE                                                   
*                                                                               
         CLI   INLINE,C'*'         SKIP COMMENTS                                
         BE    LTP04                                                            
         CLC   INLINE(6),=C'&&&&KEY='  LOOK FOR                                 
         BNE   LTP06                                                            
         CLI   INSW,C'Y'           IF ALREADY IN A TEXT ANOTHER                 
         BE    LTP80               KEY= CARD MEANS WERE ARE DONE                
         CLC   INLINE+6(2),=C'ZZ'   Z'S ARE DEFAULTS                            
         BE    *+14                                                             
         CLC   INLINE+6(2),AGY     RIGHT AGENCY, ETC?                           
         BNE   LTP04                                                            
         CLI   INLINE+6,C'Z'                                                    
         BE    *+14                                                             
         CLC   INLINE+8(1),MED                                                  
         BNE   LTP04                                                            
         CLC   INLINE+9(3),=C'ZZZ'                                              
         BE    *+14                                                             
         CLC   INLINE+9(3),CLT                                                  
         BNE   LTP04                                                            
         MVI   INSW,C'Y'           SET IN TEXT                                  
         B     LTP04                                                            
*                                                                               
LTP06    DS    0H                  IF NOT A KEY=                                
         CLI   INSW,C'Y'           MUST BE IN A TEXT                            
         BNE   LTP04               ELSE SKIP                                    
*                                                                               
LTP08    DS    0H                                                               
         LA    R2,INLINE           EXAMINE LINE FOR SUBSTITUTIONS               
         LA    R0,L'INLINE-1                                                    
*                                                                               
LTP10    DS    0H                                                               
         CLC   0(2,R2),=C'&&&&'                                                 
         BNE   LTP14                                                            
         BAS   RE,INSUB                                                         
*                                                                               
LTP14    DS    0H                                                               
         LA    R2,1(R2)                                                         
         BCT   R0,LTP10                                                         
*                                                                               
         MVC   P+10(122),INLINE    LOOKS BETTER OFFSET                          
         GOTO1 AIRPRT                                                           
         B     LTP04                                                            
*                                                                               
LTP80    DS    0H                                                               
LTPEOF   DS    0H                                                               
         CLOSE I2LETRS                                                          
         MVC   RCSUBPRG,SAVSPROG   RESTORE SPROG                                
*                                                                               
LTPX     DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        INSUB - PERFORM IN-LINE SUBSTITUTIONS                                  
*                R2 POINTS TO &&                                                
*                                                                               
***********************************************************************         
*                                                                               
INSUB    NTR1                                                                   
         LA    R3,2(R2)                                                         
         CLI   0(R3),C' '                                                       
         BNH   *+12                                                             
         LA    R3,1(R3)                                                         
         B     *-12                                                             
         SR    R3,R2               R3 HAS LENGTH                                
         SH    R3,=H'2'                                                         
         BNP   INSERR              MUST BE POSITIVE                             
         LA    R0,10               AND 10 OR LESS                               
         CR    R3,R0                                                            
         BH    INSERR                                                           
*                                                                               
         LA    R5,SUBLIST          FIND ROUTINE                                 
         BCTR  R3,0                                                             
*                                                                               
INS04    DS    0H                                                               
         CLI   0(R5),X'FF'                                                      
         BE    INSERR                                                           
         EX    R3,*+8                                                           
         B     *+10                                                             
         CLC   2(0,R2),0(R5)                                                    
         BNE   INS12                                                            
*                                                                               
         CLC   AGY,10(R5)           RIGHT AGENCY                                
         BE    INS05                                                            
         CLC   10(2,R5),=C'ZZ'      OR DEFAULT                                  
         BNE   INS12                                                            
*                                                                               
INS05    DS    0H                                                               
         CLC   MED,12(R5)          RIGHT MEDIA                                  
         BE    INS06                                                            
         CLI   12(R5),C'Z'         OR DEFAULT                                   
         BNE   INS12                                                            
*                                                                               
INS06    DS    0H                                                               
         CLC   CLT,13(R5)          RIGHT CLIENT                                 
         BE    INS07                                                            
         CLC   13(3,R5),=C'ZZZ'    OR DEFAULT                                   
         BNE   INS12                                                            
*                                                                               
INS07    DS    0H                                                               
         ICM   RF,15,16(R5)        A(ROUTINE)                                   
         BASR  RE,RF                                                            
         B     INSX                                                             
*                                                                               
INS12    DS    0H                                                               
         LA    R5,SUBLISTL(R5)                                                  
         B     INS04                                                            
*                                                                               
INSERR   DS    0H                                                               
         MVC   0(2,R2),=C'??'                                                   
*                                                                               
INSX     DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        DATE ROUTINE                                                           
*                                                                               
***********************************************************************         
*                                                                               
DATP     DS    0H                                                               
         LR    R0,RE                                                            
         GOTO1 DATCON,DMCB,(0,TODAY),(5,0(R2))                                  
         LR    RE,R0                                                            
         BR    RE                                                               
         SPACE 2                                                                
***********************************************************************         
*                                                                               
*        STATION NAME ROUTINE                                                   
*                                                                               
***********************************************************************         
*                                                                               
SNAMP    DS    0H                                                               
         L     RF,ADSTATAD                                                      
         MVC   0(20,R2),ANAME-ADDRREC(RF)                                       
         BR    RE                                                               
         SPACE 2                                                                
***********************************************************************         
*                                                                               
*        STATION ADDRESS LINE 1 ROUTINE                                         
*                                                                               
***********************************************************************         
*                                                                               
SA1P     DS    0H                                                               
         L     RF,ADSTATAD                                                      
         MVC   0(24,R2),A1LINE-ADDRREC(RF)                                      
         BR    RE                                                               
         SPACE 2                                                                
***********************************************************************         
*                                                                               
*        STATION ADDRESS LINE 2 ROUTINE                                         
*                                                                               
***********************************************************************         
*                                                                               
SA2P     DS    0H                                                               
         L     RF,ADSTATAD                                                      
         MVC   0(24,R2),A2LINE-ADDRREC(RF)                                      
         BR    RE                                                               
         SPACE 2                                                                
***********************************************************************         
*                                                                               
*        STATION ADDRESS LINE 3 ROUTINE                                         
*                                                                               
***********************************************************************         
*                                                                               
SA3P     DS    0H                                                               
         L     RF,ADSTATAD                                                      
         MVC   0(3,R2),A3LINE-ADDRREC(RF)                                       
         MVC   6(10,R2),ABIGZIP-ADDRREC(RF)                                     
         BR    RE                                                               
         SPACE 2                                                                
***********************************************************************         
*                                                                               
*        STATION                                                                
*                                                                               
***********************************************************************         
*                                                                               
STNP     DS    0H                                                               
         MVC   0(9,R2),BIGSTA                                                   
         BR    RE                                                               
         SPACE 2                                                                
***********************************************************************         
*                                                                               
*        SIGNER                                                                 
*                                                                               
***********************************************************************         
*                                                                               
SIGNP    NTR1                                                                   
         BAS   RE,GETSTO           TRY FOR STATION OVERRIDE                     
         BNE   *+14                                                             
         MVC   0(24,R2),8(RF)      FOUND, RF POINTS TO ENTRY                    
         B     SIGNX                                                            
*                                                                               
         MVI   GMGRSCHM,C'W'       SCHEME FOR SAATCHI                           
         BAS   RE,GMGR             GET MGR RECORD                               
         BNE   SIGNP10                                                          
         MVC   0(12,R2),GMGRNAM1   1ST MARKET GROUP NAME IS SIGNER              
         B     SIGNX               (12 CHARACTERS ONLY)                         
*                                                                               
SIGNP10  DS    0H                                                               
         MVC   0(2,R2),=C'**'      NO NAME FOUND                                
*                                                                               
SIGNX    DS    0H                                                               
         B     INSX                                                             
*                                                                               
         SPACE 2                                                                
***********************************************************************         
*                                                                               
*        TELEPHONE PROC                                                         
*                                                                               
***********************************************************************         
*                                                                               
TELP     NTR1                                                                   
         BAS   RE,GETSTO           TRY FOR STATION OVERRIDE                     
         BNE   *+14                                                             
         MVC   0(14,R2),32(RF)     FOUND, RF POINTS TO ENTRY                    
         B     TELPX                                                            
*                                                                               
         MVI   GMGRSCHM,C'W'       SCHEME FOR SAATCHI                           
         BAS   RE,GMGR             GET MGR RECORD                               
         BNE   TELP04                                                           
*                                                                               
         MVC   0(12,R2),GMGRNAM1+12  1ST MGR NAME IS TELEPHONE                  
         B     TELPX               (LST 12 CHARACTERS)                          
*                                                                               
TELP04   DS    0H                                                               
         MVC   0(2,R2),=C'**'      NO TELEPHONE FOUND                           
*                                                                               
TELPX    DS    0H                                                               
         B     INSX                                                             
*                                                                               
         SPACE 2                                                                
***********************************************************************         
*                                                                               
*        MONTH PROCEDURE                                                        
*                                                                               
***********************************************************************         
*                                                                               
MONP     NTR1                                                                   
         MVC   HALF,BRDMON                                                      
         GOTO1 DATCON,DMCB,(2,BRDMON),WORK                                      
         CLC   WORK+4(2),=C'01'  IF ALREADY HAVE FIRST OF MONTH                 
         BE    MON04               OK                                           
         GOTO1 ADDAY,DMCB,WORK,WORK,7  ELSE BUMP INTO NEXT MONTH                
*                                                                               
MON04    DS    0H                                                               
         PACK  DUB,WORK+2(2)   MONTH                                            
         CVB   R1,DUB                                                           
         MH    R1,=H'9'                                                         
         LA    R1,MLIST-9(R1)                                                   
         MVC   0(9,R2),0(R1)                                                    
         LA    R3,8(R2)                                                         
         CLI   0(R3),C' '                                                       
         BH    *+8                                                              
         BCT   R3,*-8                                                           
         MVI   2(R3),C''''                                                      
         MVC   3(2,R3),WORK        YEAR                                         
*                                                                               
         B     INSX                                                             
*                                                                               
MLIST    DS    0C                                                               
         DC    CL9'JANUARY'                                                     
         DC    CL9'FEBRUARY'                                                    
         DC    CL9'MARCH'                                                       
         DC    CL9'APRIL'                                                       
         DC    CL9'MAY'                                                         
         DC    CL9'JUNE'                                                        
         DC    CL9'JULY'                                                        
         DC    CL9'AUGUST'                                                      
         DC    CL9'SEPTEMBER'                                                   
         DC    CL9'OCTOBER'                                                     
         DC    CL9'NOVEMBER'                                                    
         DC    CL9'DECEMBER'                                                    
*                                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        GETSTO - GET ANY STATION OVERRIDE                                      
*                                                                               
***********************************************************************         
*                                                                               
GETSTO   DS    0H                                                               
         LA    RF,STOVLST                                                       
*                                                                               
GSTO2    DS    0H                                                               
         CLI   0(RF),X'FF'         EOL                                          
         BE    GSTONO                                                           
         CLC   BIGSTA(8),0(RF)                                                  
         BE    GSTOYES                                                          
         LA    RF,STOVLSTL(RF)                                                  
         B     GSTO2                                                            
*                                                                               
GSTOYES  CR    R0,R0                                                            
         BR    RE                                                               
GSTONO   LTR   RD,RD                                                            
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        GMGR - GET MGR NAMES FOR THIS MARKET                                   
*                                                                               
***********************************************************************         
*                                                                               
GMGR     NTR1                                                                   
         CLC   GMGRSCHM,QMGR       IF LOOKUP SCHEME SAME AS REQUEST             
         BNE   GMGR03                                                           
         MVC   GMGRNAM1,MGR1NM     USE SPONSOR SET FIELDS                       
         MVC   GMGRNAM2,MGR2NM                                                  
         MVC   GMGRNAM3,MGR3NM                                                  
         B     GMGRYES                                                          
*                                                                               
GMGR03   DS    0H                                                               
         CLC   GMGRSVCL,BCLT       CHANGE OF CLIENT                             
         BNE   GMGR03F                                                          
         CLC   GMGRSVMK,BMKTSTA    OR MARKET FORCES LOOKUP                      
         BNE   GMGR03F                                                          
*                                  SAME CLIENT/MARKET                           
         OC    GMGRMGR,GMGRMGR     IF HAVE MGR                                  
         BNZ   GMGRYES             RETURN OK                                    
         B     GMGRNO                                                           
*                                                                               
GMGR03F  DS    0H                                                               
         MVC   GMGRSVCL,BCLT       SAVE CLIENT                                  
         MVC   GMGRSVMK,BMKTSTA    AND STATION                                  
*                                                                               
         XC    KEY,KEY                                                          
         LA    R2,KEY                                                           
         USING MKARECD,R2                                                       
         MVC   MKAKTYP,=X'0D03'                                                 
         MVC   MKAKAGMD,BAGYMD                                                  
         CLI   GMGRSCHM,C'G'       SCHEMES G+                                   
         BNL   *+10                DON'T NEED CLIENT                            
         MVC   MKAKCLT,BCLT                                                     
         MVC   MKAKMKT,BMKTSTA                                                  
*                                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE                                                  
         BNE   GMGRNO                                                           
*                                                                               
         MVC   AREC,ADBUY                                                       
         GOTO1 GET                                                              
*                                                                               
         L     R2,AREC                                                          
         LA    R3,MKAEL            FIRST ELEM                                   
*                                                                               
GMGR04   DS    0H                                                               
         CLI   0(R3),0             EOR                                          
         BE    GMGRNO                                                           
         CLI   0(R3),5             05 ELEMENT                                   
         BNE   GMGR20                                                           
         USING MKAEL05,R3                                                       
         CLC   MKAMGRP(1),GMGRSCHM                                              
         BNE   GMGR20                                                           
         MVC   GMGRMGR,MKAMGRP+1                                                
         B     GMGR30                                                           
*                                                                               
GMGR20   DS    0H                                                               
         ZIC   R0,1(R3)                                                         
         AR    R3,R0                                                            
         B     GMGR04                                                           
*                                                                               
GMGR30   DS    0H                                                               
         LA    R2,KEY                                                           
         XC    KEY,KEY                                                          
         USING MKGKEY,R2                                                        
         MVC   MKGKTYP,=X'0D02'                                                 
         MVC   MKGKAGMD,BAGYMD                                                  
         CLI   GMGRSCHM,C'G'       SCHEMES G+                                   
         BNL   *+10                DON'T NEED CLIENT                            
         MVC   MKGKCLT,BCLT                                                     
         MVC   MKGKMID,GMGRSCHM                                                 
         MVC   MKGKMGRP,GMGRMGR                                                 
*                                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                MISSING MARKET GROUP RECORD                  
*                                                                               
         MVC   AREC,ADBUY                                                       
         GOTO1 GET                                                              
*                                                                               
         L     R2,AREC                                                          
         LA    R3,MKGEL            FIRST ELEM                                   
*                                                                               
GMGR44   DS    0H                                                               
         CLI   0(R3),0             EOR                                          
         BE    GMGRNO                                                           
         CLI   0(R3),X'10'         10 ELEMENT                                   
         BNE   GMGR20                                                           
         USING MKGEL10,R3                                                       
         MVC   GMGRNAMS(72),MKGNAM1   NAMES (3*24)                              
         B     GMGRYES                                                          
*                                                                               
GMGR50   DS    0H                                                               
         ZIC   R0,1(R3)                                                         
         AR    R3,R0                                                            
         B     GMGR44                                                           
*                                                                               
         DROP  R2                                                               
         DROP  R3                                                               
*                                                                               
GMGRNO   LTR   RD,RD               CC NOT =, COULDN'T FIND MGR NAMES            
         B     *+6                                                              
GMGRYES  CR    RD,RD               OK EXIT                                      
         XIT1                                                                   
*                                                                               
GMGRMGR  DS    XL2                                                              
GMGRNAMS DS    0CL72                                                            
GMGRNAM1 DS    CL24                                                             
GMGRNAM2 DS    CL24                                                             
GMGRNAM3 DS    CL24                                                             
*                                                                               
GMGRSCHM DS    CL1                 SCHEME (SET BY CALLER)                       
GMGRSVCL DC    XL2'00'             SAVED CLIENT CODE                            
GMGRSVMK DC    XL2'00'             SAVED MARKET CODE                            
*                                                                               
*        LIST OF SUBSITUTION WORDS                                              
*                                                                               
*        CODE(8),AGENCY(2),MED(1),CLIENT(3),A(ROUTINE)                          
*        (Z MEANS DEFAULT AGY,MED, OR CLIENT)                                   
*                                                                               
SUBLIST  DS    0D                                                               
         DC    CL10'DATE      ',CL2'ZZ',CL1'Z',CL3'ZZZ',AL4(DATP)               
         DC    CL10'MONTH     ',CL2'ZZ',CL1'Z',CL3'ZZZ',AL4(MONP)               
         DC    CL10'SNAME     ',CL2'ZZ',CL1'Z',CL3'ZZZ',AL4(SNAMP)              
         DC    CL10'SADDR1    ',CL2'ZZ',CL1'Z',CL3'ZZZ',AL4(SA1P)               
         DC    CL10'SADDR2    ',CL2'ZZ',CL1'Z',CL3'ZZZ',AL4(SA2P)               
         DC    CL10'SADDR3    ',CL2'ZZ',CL1'Z',CL3'ZZZ',AL4(SA3P)               
         DC    CL10'STATION   ',CL2'ZZ',CL1'Z',CL3'ZZZ',AL4(STNP)               
         DC    CL10'SIGNER    ',CL2'ZZ',CL1'Z',CL3'ZZZ',AL4(SIGNP)              
         DC    CL10'TELEPHONE ',CL2'ZZ',CL1'Z',CL3'ZZZ',AL4(TELP)               
         DC    X'FF'                                                            
SUBLISTL EQU   20                                                               
*                                                                               
*        LIST OF STATION OVERRIDES                                              
*                                                                               
*        STATION(5),NAME(24),PHONE(14)                                          
*                                                                               
STOVLST  DS    0D                                                               
         DC    CL8'TNT -TV'                                                     
         DC    CL24'VINNY COPEY'                                                
         DC    CL14'212-463-2437'                                               
         DC    CL8'WTBS-TV'                                                     
         DC    CL24'ANDREA TRONDLE'                                             
         DC    CL14'212-463-2674'                                               
         DC    CL8'SCI -TV'                                                     
         DC    CL24'CHRISTINE PACIELLO'                                         
         DC    CL14'212-463-2674'                                               
         DC    CL8'TCN -TV'                                                     
         DC    CL24'NIRA FIRESTONE'                                             
         DC    CL14'212-463-2418'                                               
         DC    CL8'WGN -TV'                                                     
         DC    CL24'NIRA FIRESTONE'                                             
         DC    CL14'212-463-2418'                                               
         DC    CL8'CFOX-TV'                                                     
         DC    CL24'LISA SUPCICH'                                               
         DC    CL14'212-463-2354'                                               
         DC    CL8'CWOR-TV'                                                     
         DC    CL24'LISA SUPCICH'                                               
         DC    CL14'212-463-2354'                                               
         DC    CL8'USA -TV'                                                     
         DC    CL24'LISA SUPCICH'                                               
         DC    CL14'212-463-2354'                                               
         DC    X'FF'                                                            
STOVLSTL EQU   46                                                               
*                                                                               
I2LETRS  DCB   DDNAME=I2LETRS,DSORG=PS,RECFM=FB,MACRF=GM,EODAD=LTPEOF           
*                                                                               
         DS    0D                                                               
INLINE   DS    CL132                                                            
         DC    C' '                                                             
*                                                                               
INSW     DS    CL1                                                              
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
       ++INCLUDE SPGENMKA                                                       
*                                                                               
       ++INCLUDE SPGENMKG                                                       
*                                                                               
*++INCLUDE SPREPI2WKN/SPREPWORKD,SPGENADD                                       
         PRINT OFF                                                              
       ++INCLUDE SPREPI2WKN                                                     
       ++INCLUDE SPREPWORKD                                                     
       ++INCLUDE SPGENADD                                                       
*                                                                               
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'041SPREPI2LP 07/12/07'                                      
         END                                                                    
