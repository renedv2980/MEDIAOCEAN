*          DATA SET DMISDDS    AT LEVEL 008 AS OF 04/25/19                      
*CATALP DMISDDS                                                                 
***********************************************************************         
* NOTE - THIS IS THE 31 BIT ADDRESSING VERSION - HOWEVER...           *         
*                                                                     *         
* 1) MVS REQUIRES THAT IOB'S RESIDE IN 24 BIT STORAGE.                *         
*    THE IOB BUILT HERE IS IN WORKING STORAGE ACQUIRED VIA AN NMOD    *         
*    THAT AREA MUST BE BELOW THE LINE.                                *         
*                                                                     *         
* 2) IN THE DTF, THE FIELD ISFADCB IS ONLY 3 BYTES LONG. AS A RESULT, *         
*    THE DTF ITSELF MUST BE BELOW THE LINE                            *         
*                                                                     *         
* 3) BECAUSE HOB OF USER IO ADDRESS MAY NOT BE CLEAR IT IS ASSUMED    *         
*    USER IS RUNNING IN 24-BIT MODE. WE REQUIRE AN INPUT PARAMETER    *         
*    FROM A 31-BIT USER                                               *         
*                                                                     *         
* IN THE MEANWHILE, HIGH ORDER BYTES OF INPUT PARMS ARE CLEARED       *         
*                                                                     *         
* US DEFAULT IS TO SET FILES IXPDOV AND LEFT COMPRESSED               *         
***********************************************************************         
         EJECT                                                                  
ISDDS    TITLE 'INDEX SEQUENTIAL ACCESS METHOD - 31 BIT MODE'                   
         PRINT NOGEN                                                            
ISDDS    CSECT                                                                  
         ENTRY ISREAD              01                                           
         ENTRY ISRDSEQ             02                                           
         ENTRY ISADD               03                                           
         ENTRY ISERASE             04                                           
         ENTRY ISWRITE             05                                           
         ENTRY ISCPUID             06                                           
         ENTRY ISFNDEOF            07                                           
         ENTRY ISOPEN              08                                           
         ENTRY ISCLOSE             09                                           
         ENTRY ISOPOLD             0A                                           
         ENTRY ISRDFLS             0B                                           
         ENTRY ISRSFLS             0C                                           
         ENTRY ISCLEAR             0D                                           
                                                                                
***********************************************************************         
* P1     A(ROUTINE) OR X'0000000X' WHERE 0X IS HEX RTN ID             *         
*        IF  X'80' ON IN HOB THEN CHECK P7 FOR LOCKER                 *         
* P2     A(I/O AREA)                                                  *         
* P3     BYTES 0-1=RETURN ERROR FLAGS,BYTES 2-3=NOT USED              *         
* P4     A(DTFIS)                                                     *         
* P5     A(KEYARG)                                                    *         
* P6     DEPENDS ON P1                                                *         
* P7     A(0) OR A(PLIST FOR LOCKER - ONLY NEEDS D/A TO CHECK)        *         
*        ONLY TO BE TESTED IF HOB FOR P1 IS X'80'                     *         
*                                                                     *         
* ROUNTINE NUMBERS AND NAMES AS FOLLOWS:                              *         
*                                                                     *         
* 01 ISREAD    RANDOM READ HIGH/EQUAL FOR KEY AT P5 - LOGICAL NOT     *         
*              FOUND CAN NOT OCCUR.                                   *         
*                                                                     *         
* 0B ISRDFLS   READ HIGH/EQUAL WITH FLUSH OF BUFFER.                  *         
*                                                                     *         
* 02 ISRDSEQ   SEQUENTIAL READ. ONLY VALID AFTER ISREAD OR ISWRITE.   *         
*              P5 IS IGNORED. LAST I/O GOVERNS RECORD READ.           *         
*              IF P6(1)=FF THEN P6+1(3) IS A(FULL TRACK BUFFER)       *         
*                                                                     *         
* 0C ISRSFLS   READ SEQUENTIAL WITH FLUSH OF BUFFER.                  *         
*                                                                     *         
* 03 ISADD     ADD A RECORD. KEY IS IN RECORD AT P2. P5 IS IGNORED.   *         
*              DESTROYS SEQUENTIAL READ SEQUENCE.                     *         
*                                                                     *         
* 04 ISERASE   ERASE LAST RECORD READ. P5 IS IGNORED.                 *         
*              DESTROYS SEQUENTIAL READ SEQUENCE.                     *         
*                                                                     *         
* 05 ISWRITE   WRITE LAST REC READ. P5 IS IGNORED. KEY MUST NOT CHANGE*         
*                                                                     *         
* 06 ISCPUID   CHECK CPU ID IN RECORD ZERO OF TRACK ONE.              *         
*                                                                     *         
* 07 ISFNDEOF  RESET DISK ADDRESS OF LAST OVERFLOW RECORD.            *         
*                                                                     *         
* 08 ISOPEN    OPEN FILE AND BUILD EXTENT MATRIX.                     *         
*              IF P6(2)=CI/MI THEN P6+2(2)=L'NDX AND P2=A(NDX)        *         
*                                                                     *         
* 09 ISCLOSE   CLOSE FILE.                                            *         
*                                                                     *         
* 0A ISOPOLD   OPEN FILE (DISP=OLD) AND BUILD EXTENT MATRIX.          *         
*              IF P6(2)=CI/MI THEN P6+2(2)=L'NDX AND P2=A(NDX)        *         
*                                                                     *         
* 0D ISCLEAR   CLEAR ALL BUFFER CONTENTS TO FORCE NEW I/OS NEXT TIME  *         
*                                                                     *         
* 0E ISGETPD   GET PD D/A AND PASS IT BACK (DON'T ISSUE I/O)          *         
*              FOR DMGR LOCKING SO WE CAN LOCK THEN READ              *         
*                                                                     *         
* ERROR BYTE INDICATORS - RETURNED IN P3(2)                           *         
*                                                                     *         
* BYTE 0 X'80' UNRECOVERABLE ERROR                                    *         
*        X'40'                                                        *         
*        X'20' DUPLICATE KEY ON ADD                                   *         
*        X'10'                                                        *         
*        X'08'                                                        *         
*        X'04'                                                        *         
*        X'02'                                                        *         
*        X'01'                                                        *         
*                                                                     *         
* BYTE 1 X'80' INVALID CPU ID                                         *         
*        X'40'                                                        *         
*        X'20'                                                        *         
*        X'10'                                                        *         
*        X'08' NO RECORD FOUND                                        *         
*        X'04' END OF FILE                                            *         
*        X'02'                                                        *         
*        X'01'                                                        *         
***********************************************************************         
         NMOD1 WORKL,**ISDDS*,RA,CLEAR=YES                                      
         USING WORKD,RC                                                         
         ST    R1,SAVER1                                                        
         MVC   PARAMS,0(R1)                                                     
*                                                                               
         MVI   P2,0                CLEAR HIGH ORDER BYTES OF PARMS              
         MVI   P4,0                                                             
         MVI   P5,0                                                             
         L     R2,P4               R2=A(DTF)                                    
         USING ISDTF,R2                                                         
         SAM31                     SWITCH INTO XA                               
*                                                                               
ISDDS0   TM    ISFFLAG,ISFVSAM     TEST IF VSAM FILE                            
         BO    *+12                YES                                          
         TM    ISFTYPE,ISFTBIGF    TEST IF 20-BIT DISK ADDR                     
         BZ    ISDDS0X             NO                                           
         OC    P1+1(2),P1+1        TEST PASSED ROUTINE NUMBER                   
         BZ    ISDDS0C             YES - CAN GO DIRECT TO ISDDS20/VSAM          
*                                                                               
         XR    R0,R0               CONVERT ROUTINE ADDRESS TO A NUMBER          
         LHI   RE,32                                                            
         LARL  RF,ISROUTS                                                       
ISDDS0A  CLC   1(3,RF),P1+1        TEST IF ADDRESS MATCHES                      
         BE    ISDDS0B             YES                                          
         AHI   R0,1                                                             
         AHI   RF,4                                                             
         BCT   RE,ISDDS0A                                                       
         DC    H'0'                DIE=INV ROUTINE ADDR                         
ISDDS0B  STCM  R0,7,P1+1           PUT NUMBER OVER ADDRESS                      
*                                                                               
ISDDS0C  L     RF,=V(ISDDS20E)     PASS CONTROL TO ISDDS20 VIA ISDDS20E         
         TM    ISFFLAG,ISFVSAM     TEST IF VSAM FILE                            
         BZ    *+8                 NO                                           
         L     RF,=V(DMVSAME)      PASS CONTROL TO DMVSAM VIA DMVSAME           
         LR    R1,RC               R1=A(WORKING STORAGE)                        
         BASR  RE,RF                                                            
         B     EXIT                                                             
*                                                                               
ISDDS0X  LH    RE,ISKEYLN          SET KEY LEN + 4 FOR BLKIND CODE              
         AHI   RE,4                                                             
         STH   RE,ISKEYLN4                                                      
*                                                                               
         TM    P1,X'80'            CHECK WANT TO DO INTERNAL LOCKER             
         BZ    *+12                                                             
         OI    READTYPE,DOLOCKER                                                
         NI    P1,X'7F'                                                         
*                                                                               
         LARL  RE,DEV3390          3390 IS ONLY VALID CURRENT DEVICE            
*&&DO                                                                           
         SR    RE,RE               SET DISK DEVICE DATA                         
         ICM   RE,1,ISFDEVT                                                     
         BNZ   *+8                                                              
         LA    RE,1                                                             
         BCTR  RE,0                                                             
         SLL   RE,4                                                             
         L     RF,=A(DEVDISK)                                                   
         AR    RE,RF                                                            
*&&                                                                             
         MVC   DEVDATA,0(RE)                                                    
*                                                                               
ISDDS1   OC    P1+1(2),P1+1        TEST PASSED ROUTINE NUMBER                   
         BZ    ISDDS1A                                                          
         L     RF,P1               PASSED ADDRESS OF ROUTINE                    
         B     ISDDS1B                                                          
*                                                                               
ISDDS1A  LLC   RF,P1+3             CONVERT ROUTINE NUMBER TO ADDRESS            
         SLL   RF,2                                                             
         LARL  RE,ISROUTS                                                       
         AR    RE,RF                                                            
         L     RF,0(RE)                                                         
ISDDS1B  ST    RF,ISROUTA          SAVE ADDRESS OF ROUTINE                      
*                                                                               
         OC    ISCILAST,ISCILAST   INITIALISE ON FIRST CALL                     
         BZ    INIT                                                             
*                                                                               
ISDDS2   XC    P3(2),P3            CLEAR ERROR IND                              
         L     R3,P5               POINT TO KEYARG                              
         MVI   ISSWAPSW,0          RESET SWAP SWITCH                            
         L     RF,ISROUTA                                                       
         BR    RF                  GO TO REQUIRED IS ROUTINE                    
*                                                                               
EXIT     L     R1,SAVER1                                                        
         MVC   8(4,R1),P3                                                       
EXITX    XMOD1 1                   XMOD1 WILL RESTORE CALLERS MODE              
*                                                                               
RTNXIT   XIT1                      JUMP TO POINT FOR SUBROUTINE EXITS           
         EJECT                                                                  
***********************************************************************         
* ISREAD - READ RANDOM                                                *         
***********************************************************************         
ISRDFLS  OI    READTYPE,FLUSH      READ REQUIRES BLK TO BE READ                 
         B     ISRD6                                                            
*                                                                               
ISREAD   MVI   BUFFOK,X'80'        SET BUFFER OK FLAG TO SAY ISREAD             
         BRAS  R9,CHKHILO          SEE IF BLK IN CORE                           
         B     ISRD6               KEY ARG LO - GO READ INDEX                   
         B     ISRD4               KEY ARG HI - GO READ OVERFLOW BLK            
         B     ISRD10              FOUND, SET CC AND RETURN KEY                 
                                                                                
***********************************************************************         
* KEY ARG HIGHER THAN PDBUFF IN CORE.                                 *         
* TEST FOR OVERFLOW THIS BLK                                          *         
***********************************************************************         
ISRD4    BRAS  R9,CHKHITI          SEE IF REC IN OVFLO BLK THIS PD BLK          
         BNH   ISRD8               KEY ARG LE - READ OVFLO BLK                  
                                                                                
***********************************************************************         
* KEY ARG UNRELATED TO ANYTHING IN CORE. SEARCH INDEX                 *         
***********************************************************************         
ISRD6    BAS   R9,READIX                                                        
         B     EXIT                (FOR ISGETPD ONLY)                           
         B     ISRD8               KEYARG HI=READ OVERFLOW                      
         B     ISRD10                                                           
*                                                                               
ISRD8    L     R7,ISBUFF1          POINT TO PD BUFFER                           
         OC    2(4,R7),2(R7)       TEST FOR OVERFLOW PRESENT                    
         BZ    ISRD6               NO - SEARCH INDEX AGAIN                      
         BAS   R9,READOV                                                        
         DC    AL4(0)                                                           
         B     ISRD8                                                            
*                                                                               
ISRD10   BAS   R9,MOVEREC          NB - DOES NOT SET NOT FOUND IND              
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* ISRDSEQ - READ SEQUENTIAL                                           *         
***********************************************************************         
ISRSFLS  OI    READTYPE,FLUSH      SET FLUSH TYPE READING                       
*                                                                               
ISRDSEQ  OC    ISPDDA,ISPDDA       TEST BUFFER CONTENTS VALID                   
         JZ    *+2                 DIE=ZERO ISPDDA                              
*                                                                               
         L     R4,ISPDKEY                                                       
         CLI   0(R4),X'FF'         TEST FOR EOF REC                             
         BE    ISEOF               YES - SET EOF                                
*                                                                               
         ICM   R7,15,ISPDPTR       GET LAST REC ADDRESS                         
         JZ    *+2                 DIE=ZERO ISPDPTR                             
*                                                                               
         SR    RE,RE                                                            
         IC    RE,0(R7)            GET MISSING BYTES                            
         LH    R6,ISRECLN                                                       
         SR    R6,RE               GIVES RECORD BYTES PRESENT                   
         LA    R7,1(R7,R6)         LEN + CONTROL BYTE GIVES NEXT ADDR           
*                                                                               
ISRDSQ2  CLI   0(R7),X'FF'         TEST FOR EOB                                 
         BE    ISRDSQ4             YES                                          
*                                                                               
         ST    R7,ISPDPTR          SAVE NEW POINTER                             
         IC    RE,0(R7)            GET MISSING BYTES THIS REC                   
*                                                                               
         LA    RF,0(R4,RE)         GET TO ADDRESS (ISPDKEY+MISSING)             
         LH    R5,ISKEYLN1                                                      
         SR    R5,RE                                                            
         EX    R5,CHKMVKEY         MVC  0(0,RF),1(R7)                           
         BRAS  R9,MOVEREC          PASS REC TO USER                             
         B     EXIT                                                             
                                                                                
***********************************************************************         
* EOB ENCOUNTERED                                                     *         
***********************************************************************         
ISRDSQ4  L     R7,ISBUFF1                                                       
         OC    2(4,R7),2(R7)       IS LINK PRESENT                              
         BZ    ISRDSQ5             NO                                           
         CLC   2(4,R7),BLKINDEX    CHECK NOT PD BLOCK INDEX LINK                
         JE    *+2                 DIE=INV BLK INDEX                            
*                                                                               
         L     R7,ISBUFF1                                                       
         LA    R7,2(R7)            POINT TO DISK ADDRESS                        
         MVC   ISOVDA,0(R7)                                                     
         LA    R8,ISXTNTIX                                                      
         BAS   RE,ISBLDCCW                                                      
         BAS   RE,SETSCTR                                                       
         BAS   RE,EXCP                                                          
         B     ISRDSQ6                                                          
         EJECT                                                                  
***********************************************************************         
* NO LINK - GET NEXT PD REC ADDRESS                                   *         
***********************************************************************         
ISRDSQ5  XC    ISOVDA,ISOVDA       CLEAR OVERFLOW ADDRESS                       
         L     R4,ISTIHIKY         CLEAR TI HIGH KEY                            
         LH    R5,ISKEYLN1                                                      
         EX    R5,READXC                                                        
*                                                                               
         SR    RE,RE                                                            
         IC    RE,ISPDDA+2                                                      
         LA    RE,1(RE)                                                         
         STC   RE,ISPDDA+2                                                      
         CH    RE,ISHIPD           COMPARE TO HI REC ON PD TRACK                
         BNH   ISRDSQ5B                                                         
*                                                                               
ISRDSQ5A SR    RE,RE               BUMP TO NEXT TRACK                           
         ICM   RE,3,ISPDDA                                                      
         LA    RE,1(RE)                                                         
         STH   RE,ISPDDA                                                        
         MVI   ISPDDA+2,1                                                       
         SRDL  RE,32               TEST IF TI TRK (FIRST ON CYLINDER)           
         LH    R0,ISTRKS                                                        
         DR    RE,R0                                                            
         CHI   RE,1                                                             
         BE    ISRDSQ5A                                                         
*                                                                               
ISRDSQ5B LA    R7,ISPDDA                                                        
         LA    R8,ISXTNTPD                                                      
         BRAS  RE,LOCKBLK                                                       
         BRAS  RE,ISBLDCCW                                                      
         CLI   P6,X'FF'            TEST IF TRACK BUFFER PASSED                  
         BE    ISRDSQ7                                                          
*                                                                               
ISRDSQ5C BAS   RE,SETSCTR                                                       
         BAS   RE,EXCP                                                          
*                                                                               
ISRDSQ6  L     R7,ISBUFF1                                                       
         CLC   2(4,R7),BLKINDEX    TEST FOR PD BLOCK INDEX LINK                 
         BE    ISRDSQ6A                                                         
         AH    R7,0(R7)            ADD SLACK                                    
         LA    R7,6(R7)            POINT TO FIRST CONTROL BYTE                  
         L     R4,ISPDKEY                                                       
         SR    RE,RE               RESET MISSING BYTE COUNT                     
         B     ISRDSQ2                                                          
*                                                                               
ISRDSQ6A AH    R7,0(R7)            POINT TO FIRST DISK ADDRESS                  
         AH    R7,ISKEYLN                                                       
         MVC   ISOVDA,0(R7)        READ AND PROCESS NEXT BLOCK                  
         LA    R8,ISXTNTIX                                                      
         BAS   RE,ISBLDCCW                                                      
         BAS   RE,SETSCTR                                                       
         BAS   RE,EXCP                                                          
         B     ISRDSQ6                                                          
                                                                                
***********************************************************************         
* FULL TRACK READ LOGIC - RETRY IF ERROR OCCURS                       *         
***********************************************************************         
ISRDSQ7  SR    R6,R6                                                            
         ICM   R6,7,P6+1           GET FULL TRACK BUFFER ADDRESS                
         BZ    ISRDSQ5C                                                         
         MVC   DUB(5),SRCH                                                      
*                                                                               
         CLC   0(7,R6),ISDTF+22    TEST FIRST TIME                              
         BE    *+16                                                             
         XC    0(16,R6),0(R6)                                                   
         MVC   0(7,R6),ISDTF+22                                                 
         CLC   8(2,R6),ISPDDA      TEST SAME TRACK                              
         BE    ISRDSQ9                                                          
*                                                                               
ISRDSQ8  MVI   SRCH+4,0            SET TO DO FULL TRK READ                      
*                                                                               
         MVC   CCW4+6(2),MAXIOL                                                 
         SR    R0,R0                                                            
         ICM   R0,3,CCW4+6                                                      
         LA    R1,16(R6)           IO ADDRESS=BUFF+16                           
         BAS   RE,SETIDAWS                                                      
         LA    R0,IDAWS                                                         
         ST    R0,CCW4                                                          
         MVI   CCW4,X'5E'                                                       
         OI    CCW4+4,X'20'                                                     
*                                                                               
         BAS   RE,SETSCTRZ                                                      
         BAS   RE,EXCP                                                          
         BNE   ISRDSQ5B            IF ERROR, TRY WITHOUT TRACK BUFFER           
         MVC   8(2,R6),ISPDDA                                                   
*                                                                               
ISRDSQ9  LH    R1,ISPDLN           GET BLK LEN PLUS COUNT LEN                   
         LA    R1,8(R1)                                                         
         LLC   R0,ISPDDA+2         GET BLK NUM MINUS ONE                        
         BCTR  R0,0                                                             
         MR    R0,R0                                                            
         LA    RE,16(R6,R1)        POINT TO BLK COUNT FIELD                     
         CLC   0(5,RE),DUB                                                      
         BE    ISRDSQ10                                                         
         LA    R7,ISPDDA           REISSUE NORMAL IO IF NO MATCH                
         LA    R8,ISXTNTPD                                                      
         BAS   RE,ISBLDCCW                                                      
         XC    8(2,R6),8(R6)       RESET TRACK ADDRESS                          
         B     ISRDSQ5C                                                         
*                                                                               
ISRDSQ10 L     R0,ISBUFF1          SET TO ADDR                                  
         LH    R1,ISPDLN           SET TO LEN                                   
         LA    RE,8(RE)            SET FROM ADDR                                
         LR    RF,R1               FROM LEN=TO LEN                              
         MVCL  R0,RE                                                            
         MVI   BUFFOK,0            CLEAR BUFFER OK FLAGS                        
         B     ISRDSQ6                                                          
         EJECT                                                                  
***********************************************************************         
* ISWRITE - WRITE LAST RECORD READ                                    *         
***********************************************************************         
ISWRITE  L     R3,P2               USE RECORD AS KEYARG                         
         OC    ISPDDA,ISPDDA       TEST BUFFER CONTENTS VALID                   
         JZ    *+2                 DIE=ZERO ISPDDA                              
         L     R4,ISPDKEY                                                       
         LH    R5,ISKEYLN1                                                      
         EX    R5,SRCHCLC          CLC  0(0,R3),0(R4)                           
         JNE   *+2                 DIE=KEYS NOT EQL                             
*                                                                               
ISWRT2   TM    ISCMPRSW,VARIABLE   FIXED/VARIABLE LENGTH RECORDS?               
         BO    ISWRT4                                                           
*                                  FIXED LENGTH RECORDS                         
         LH    R6,ISRECLN                                                       
         L     RF,ISPDPTR          GET ADDRESS OF REC IN BUFFER                 
         SR    RE,RE                                                            
         IC    RE,0(RF)            GET MISSING KEY BYTES                        
         SR    R6,RE               GIVES ACTUAL REC BYTES PRESENT               
         LA    R7,0(RE,R3)         GET FROM ADDRESS                             
         LA    RF,1(RF)            GET TO ADDRESS                               
         BAS   R9,MOVEREC2         MVC  0(R6,RF),0(R7)                          
         BAS   R9,ISWRTPD          WRITE PD BLOCK                               
         B     EXIT                                                             
*                                  VARIABLE LENGTH RECORDS                      
ISWRT4   LH    R6,ISKEYLN                                                       
         AR    R6,R3               POINT TO REC LENGTH                          
         CLC   ISRECLN,0(R6)       DID LENGTH CHANGE                            
         BNE   ISWRT10             YES                                          
         L     RF,ISPDPTR                                                       
         SR    RE,RE                                                            
         IC    RE,0(RF)            GET MISSING BYTES                            
         LH    R6,ISRECLN                                                       
         SR    R6,RE               GIVES ACTUAL BYTES PRESENT                   
*                                                                               
         LA    R7,0(RE,R3)         GIVES FROM ADDRESS                           
         LA    RF,1(RF)            GIVES TO ADDRESS                             
         BAS   R9,MOVEREC2         MVC  0(R6,RF),0(R7)                          
         BAS   R9,ISWRTPD          WRITE BLK                                    
         B     EXIT                                                             
                                                                                
***********************************************************************         
* REC LENGTH HAS CHANGED                                              *         
***********************************************************************         
ISWRT10  L     R0,ISBUFF2          SET TO ADDRESS                               
         LH    R1,ISPDLN           SET TO LEN                                   
         L     RE,ISBUFF1          SET FROM ADDRESS                             
         LR    RF,R1               SET FROM LENGTH                              
         MVCL  R0,RE               MOVE BUFF1 TO BUFF2                          
                                                                                
***********************************************************************         
* MOVE BUFF2 TO BUFF1 OMITTING CHANGED REC                            *         
***********************************************************************         
         L     RF,ISPDPTR                                                       
         SR    RE,RE                                                            
         IC    RE,0(RF)            GET COMPRESSION FACTOR                       
         LCR   RE,RE               NEGATE IT                                    
         L     RF,ISBUFF1                                                       
         AH    RE,0(RF)            ADD OLD SPARE                                
         AH    RE,ISRECLN          PLUS OLD RECLEN                              
         LA    RE,1(RE)            AND ONE FOR LUCK                             
         STH   RE,0(RF)            SET NEW SPARE                                
*                                                                               
         LA    RF,6(RE,RF)         GIVES TO ADDRESS                             
         L     R7,ISBUFF2                                                       
         AH    R7,0(R7)                                                         
         LA    R7,6(R7)            GIVES FROM ADDRESS                           
         L     R0,ISPDPTR                                                       
         AH    R0,ISPDLN           SET PTR TO CHANGED REC IN BUFF2              
         ST    R0,DUB+4            AND SAVE IT                                  
         EJECT                                                                  
ISWRT12  CLI   0(R7),X'FF'         CHECK FOR EOB FLAG                           
         BE    ISWRT14                                                          
*                                                                               
         LH    R6,ISKEYLN                                                       
         SR    RE,RE                                                            
         IC    RE,0(R7)            GET MISSING BYTES                            
         SR    R6,RE                                                            
         LA    R6,1(R7,R6)         POINT TO RECLEN                              
         MVC   DUB(2),0(R6)        ALIGN                                        
         LH    R6,DUB                                                           
         SR    R6,RE               GIVES ACTUAL BYTES PRESENT                   
         LA    R6,1(R6)            ADJUST FOR CONTROL BYTE                      
         C     R7,DUB+4            IS THIS CHANGED REC                          
         BNE   ISWRT13             NO                                           
         AR    R7,R6               POINT TO NEXT REC                            
         ST    RF,DUB+4            THINK ABOUT IT                               
         B     ISWRT12                                                          
*                                                                               
ISWRT13  BAS   R9,MOVEREC2         MVC  0(R6,RF),0(R7)                          
         B     ISWRT12                                                          
*                                                                               
ISWRT14  MVI   0(RF),X'FF'         SET EOB FLAG                                 
         MVC   ISPDPTR,DUB+4       SET NEW INS. ADDRS                           
         B     ISADD18             ADD RECORD                                   
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO WRITE BLOCK                                              *         
***********************************************************************         
ISWRTPD  LA    R8,ISXTNTIX                                                      
         LA    R7,ISOVDA                                                        
         OC    0(4,R7),0(R7)       TEST OVERFLOW ADDRESS PRESENT                
         BNZ   *+12                YES                                          
         LA    R7,ISPDDA                                                        
         LA    R8,ISXTNTPD                                                      
*                                                                               
         BAS   RE,ISBLDCCW                                                      
         MVI   CCW4,5              SET OPCD=WRITE DATA                          
*                                                                               
         BAS   RE,SETSCTR                                                       
         BAS   RE,EXCP                                                          
         BR    R9                                                               
         EJECT                                                                  
***********************************************************************         
* ISADD - ADD A RECORD                                                *         
***********************************************************************         
B1       USING SVBLKD,BLKK1                                                     
B2       USING SVBLKD,BLKK2                                                     
*                                                                               
ISADD    L     R3,P2               POINT TO RECORD AS KEYARG                    
         MVI   BUFFOK,X'40'        SET BUFF OK FLAG TO SAY ISADD                
         BAS   R9,CHKHILO          SEE IF BLK IN CORE                           
         B     ISADD06             KEY ARG LO                                   
         B     ISADD04             KEY ARG HI                                   
*                                                                               
ISADD02  BNE   ISADD18             IF CC NOT =, GO ADD RECORD                   
         OI    P3,X'20'            SET DUP KEY ERROR                            
         B     ISADDX                                                           
                                                                                
***********************************************************************         
* KEYARG HIGHER THAN BUFFER IN CORE. TEST FOR OVERFLOW                *         
***********************************************************************         
ISADD04  BAS   R9,CHKHITI                                                       
         BNH   ISADD08             KEYARG LE - READ OVERFLOW BLK                
                                                                                
***********************************************************************         
* SEARCH INDEX TO FIND REC                                            *         
***********************************************************************         
ISADD06  BAS   R9,READIX                                                        
         DC    AL4(0)                                                           
         B     ISADD08                                                          
         BE    ISADD02+4           RECORD FOUND IS ERROR                        
         B     ISADD10                                                          
*                                                                               
ISADD08  BAS   R9,READOV                                                        
         DC    AL4(0)                                                           
         B     ISADD08                                                          
         BE    ISADD02+4                                                        
*                                                                               
ISADD10  TM    ISCMPRSW,X'80'      TEST FIXED/VARIABLE LENGTH RECORDS           
         BO    ISADD18                                                          
         B     ISADD12                                                          
         EJECT                                                                  
***********************************************************************         
* FOR FIXED LENGTH RECORDS                                            *         
* IF REC TO BE INSERTED IS LOWEST IN BUFFER -                         *         
* INTERCHANGE FIRST REC IN BLK AND INSERT REC, THEN PROCEED           *         
* WITH INSERTION. RESTORE USERS RECORD AT END.                        *         
***********************************************************************         
ISADD12  LH    R6,ISRECLN                                                       
         L     R7,ISBUFF1                                                       
         AH    R7,0(R7)                                                         
         LA    R7,7(R7)            POINT TO KEY OF FIRST REC                    
*                                                                               
         L     RF,ISPDPRKY                                                      
         LH    R5,ISKEYLN1                                                      
         EX    R5,ISADDOC          OC  0(0,RF),0(RF)                            
         BNZ   ISADD18             INSERT IS NOT LOWEST IN BLK                  
         EX    R5,SWAPMVC          MVC  0(0,RF),0(R3)                           
*                                                                               
         LA    RE,0(R6,R7)         SET NEW INSERTION ADDRESS                    
         ST    RE,ISPDPTR                                                       
*                                                                               
         LA    RE,256              SWAP IN 256 BYTE CHUNKS                      
ISADD14  CR    R6,RE                                                            
         BNH   ISADD16                                                          
         XC    0(256,R3),0(R7)                                                  
         XC    0(256,R7),0(R3)                                                  
         XC    0(256,R3),0(R7)                                                  
         SR    R6,RE                                                            
         AR    R3,RE                                                            
         AR    R7,RE                                                            
         B     ISADD14                                                          
*                                                                               
ISADD16  BCTR  R6,0                                                             
         EX    R6,SWAPXC37                                                      
         EX    R6,SWAPXC73                                                      
         EX    R6,SWAPXC37                                                      
*                                                                               
         L     R3,P2               RESET REC POINTER                            
         MVI   ISSWAPSW,C'S'       SET SWITCH                                   
         B     ISADD18                                                          
                                                                                
***********************************************************************         
* GET NUMBER OF MATCHING KEY BYTES                                    *         
***********************************************************************         
ISADD18  L     R4,ISPDPRKY         PRECEDING KEY SAVE AREA                      
         LH    R5,ISKEYLN1                                                      
*                                                                               
         TM    ISCMPRSW,X'80'      TEST FIXED/VARIABLE LENGTH RECORDS           
         BZ    *+14                                                             
         LR    RF,R4                                                            
         EX    R5,ISADDOC          CHECK FIRST IN BLK  OC 0(0,RF),0(RF)         
         BZ    *+12                SKIP COMPRESSION IF FIRST                    
         TM    ISCMPRSW,X'01'      ELSE TEST FILE USES COMPRESSION              
         BNZ   ISADD22             YES                                          
         SR    R5,R5               NO - SET MATCH LEN=-1                        
         BCTR  R5,0                                                             
         B     ISADD24                                                          
*                                                                               
ISADD20  EX    R5,SRCHCLC          CLC  0(0,R3),0(R4)                           
         BE    ISADD24                                                          
*                                                                               
ISADD22  AHI   R5,-1                                                            
         BNM   ISADD20                                                          
*                                                                               
ISADD24  LA    R5,1(R5)            SET NUMBER OF MATCHING BYTES                 
         ST    R5,DUB1             SAVE MATCHED BYTES INSERT REC                
                                                                                
***********************************************************************         
* RECORD WILL FIT IN BUFFER IF SPARE IS NOT LESS THAN COMPRESSED      *         
* LENGTH OF NEW RECORD                                                *         
***********************************************************************         
         TM    ISCMPRSW,X'80'      TEST FIXED/VARIABLE LENGTH RECORDS           
         BZ    *+16                                                             
         LH    RE,ISKEYLN                                                       
         AR    RE,R3               POINT TO RECLEN                              
         MVC   ISRECLN,0(RE)       SAVE RECLEN                                  
         LH    RE,ISRECLN                                                       
         BCTR  R5,0                ADJUST FOR CONTROL                           
         SR    RE,R5               GIVES TOTAL INSERT LENGTH                    
         ST    RE,DUB2             SAVE TOTAL INSERTION LENGTH                  
*                                                                               
         L     R7,ISBUFF1          GET BUFFER ADDRESS                           
         MVC   DUB(6),0(R7)        SAVE SPARE AND LINK                          
         LH    R0,0(R7)            GET SPARE                                    
         SR    R0,RE                                                            
         BM    ISADD28             NOT ENOUGH ROOM - SPLIT BUFFER               
*                                                                               
         STH   R0,0(R7)            SET REMAINING SPARE                          
         BAS   R9,ISADDMV          MOVE BUFFER TO LEFT AND INSERT DATA          
         B     ISADD26                                                          
*                                                                               
ISADD26  BAS   R9,ISWRTPD          WRITE PRIME DATA BLOCK                       
ISADD26A TM    ISCMPRSW,X'80'      TEST FIXED/VARIABLE LENGTH RECORDS           
         BO    ISADDX                                                           
         CLI   ISSWAPSW,0          TEST NEED TO UNSWAP                          
         BE    ISADDX              NO                                           
                                                                                
***********************************************************************         
* USERS RECORD IS AT TOP OF BUFFER - GIVE IT BACK                     *         
***********************************************************************         
         L     RF,P2               GET USER ADDRESS                             
         L     R7,ISBUFF1                                                       
         AH    R7,0(R7)                                                         
         LA    R7,7(R7)            POINT TO FIRST DATA BYTE                     
         LH    R6,ISRECLN                                                       
         BAS   R9,MOVEREC2         MVC  0(R6,RF),0(R7)                          
         B     ISADDX                                                           
                                                                                
***********************************************************************         
* REC WILL NOT FIT - SPLIT BUFFER                                     *         
***********************************************************************         
ISADD28  OC    ISOVLAST,ISOVLAST   TEST IF EOF SET FOR OVERFLOW                 
         JZ    *+2                 DIE=ZERO ISOVLAST                            
*                                                                               
         L     R0,ISBUFF1          MOVE BUFF1 TO BUFF2                          
         L     RE,ISBUFF2                                                       
         LH    R1,ISPDLN                                                        
         LH    RF,ISPDLN                                                        
         MVCL  RE,R0                                                            
*                                                                               
         L     R0,ISBUFF1                                                       
         LH    R1,ISPDLN                                                        
         AHI   R1,6                                                             
         XR    RF,RF                                                            
         MVCL  R0,RE               CLEAR BUFF1 + FIRST 6 BYTES OF BUFF2         
*                                                                               
         L     R0,ISPDPTR          GET OLD INSERTION ADDRESS                    
         AH    R0,ISPDLN           NOW IN BUFF2                                 
         ST    R0,ISPDPTR          SET IT                                       
*                                                                               
         L     R7,ISBUFF2                                                       
         BAS   R9,ISADDMV          MOVE BUFFER LEFT AND INSERT DATA             
                                                                                
***********************************************************************         
* NOW MOVE DATA TO TOP OF BUFF1 AND SET SLACK/LINK                    *         
***********************************************************************         
         L     R4,ISBUFF1                                                       
         L     R8,ISBUFF1X                                                      
         SH    R8,ISKEYLN          GIVES LAST AVAILABLE BYTE + 1                
*                                                                               
         LH    R0,ISPDLN                                                        
         SRL   R0,1                                                             
         TM    ISCMPRSW,X'80'      TEST FIXED/VARIABLE LENGTH RECORDS           
         BZ    *+8                                                              
         SRL   R0,1                FOR V/L MAX REC LE 3/4 BLK SIZE              
*                                                                               
         STH   R0,0(R4)            SET SLACK BYTES                              
         AR    R4,R0                                                            
         LA    R4,6(R4)            GET TO ADDRESS                               
         L     R3,DUB3             GET FROM ADDR    (SET BY ISADDMV)            
*                                                                               
ISADD30  CLI   0(R3),X'FF'         CHECK FOR EOB FLAG                           
         BE    ISADD32                                                          
*                                                                               
         LH    R6,ISKEYLN                                                       
         TM    ISCMPRSW,X'80'      TEST FIXED/VARIABLE LENGTH RECORDS           
         BO    *+8                                                              
         LH    R6,ISRECLN                                                       
*                                                                               
         SR    RE,RE                                                            
         IC    RE,0(R3)            GET MISSING BYTES                            
         SR    R6,RE               GIVES KEY BYTES PRESENT                      
         TM    ISCMPRSW,X'80'      TEST FIXED/VARIABLE LENGTH RECORDS           
         BZ    *+20                                                             
         LA    R6,1(R3,R6)         POINT TO RECLEN                              
         MVC   DUB(2),0(R6)        ALIGN                                        
         LH    R6,DUB                                                           
         SR    R6,RE               GIVES ACTUAL REC BYTES PRESENT               
         LA    R6,1(R6)            ADJUST FOR CONTROL BYTE                      
         LA    R0,0(R4,R6)                                                      
         CR    R0,R8                                                            
         BH    ISADD32             NO MORE ROOM                                 
*                                                                               
         LH    R5,ISKEYLN1         CONSTRUCT THIS KEY                           
         SR    R5,RE               GIVES KEY LEN PRESENT - 1                    
         L     RF,ISPDKEY                                                       
         AR    RF,RE               GET TO ADDRESS                               
         LR    R7,R3               GET FROM ADDRESS                             
         EX    R5,CHKMVKEY         MVC  0(0,RF),1(R7)                           
*                                                                               
         LR    RF,R4               GET TO ADDRESS                               
         LR    R4,R0               SAVE NEXT TO ADDRESS                         
         AR    R3,R6               SAVE NEXT FROM ADDRESS                       
         BAS   R9,MOVEREC2         MVC  0(R6,RF),0(R7)                          
         B     ISADD30                                                          
                                                                                
***********************************************************************         
* REC WILL NOT FIT IN BUFF1                                           *         
***********************************************************************         
ISADD32  MVI   0(R4),X'FF'         SET EOB FLAG                                 
         LA    R0,1(R4)            NOW CLEAR TO END OF BLK                      
         L     R1,ISBUFF1X                                                      
         SR    R1,R0               GIVES LEN TO CLEAR                           
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LH    R5,ISKEYLN1                                                      
         L     RF,ISBUFF1X                                                      
         SR    RF,R5               SET A(HI KEY AREA)                           
         L     R7,ISPDKEY                                                       
         EX    R5,MOVEMVC          MVC  0(0,RF),0(R7)                           
                                                                                
***********************************************************************         
* R3 POINTS TO REC IN BUFF2 THAT WON'T FIT                            *         
***********************************************************************         
         CLI   0(R3),X'FF'         TEST EOB FLAG                                
         BNE   ISADD34             NO                                           
         L     R7,ISBUFF1                                                       
         MVC   2(4,R7),DUB+2       RESTORE OLD LINK FIELD                       
         B     ISADD26                                                          
*                                                                               
ISADD34  L     R0,ISBUFF2          CLEAR TOP OF BUFF2                           
         LR    R1,R3                                                            
         SR    R1,R0                                                            
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
         EJECT                                                                  
***********************************************************************         
* NOW RESTORE MISSING BYTES OF FIRST KEY IN BUFF2                     *         
***********************************************************************         
         XR    R6,R6                                                            
         ICM   R6,1,0(R3)                                                       
         BZ    ISADD36                                                          
         SR    R3,R6                                                            
         LA    RF,1(R3)            SET TO ADDRESS                               
         L     R7,ISPDKEY          ISPDKEY HAS LAST KEY MOVED                   
         BCTR  R6,0                                                             
         EX    R6,MOVEMVC          MVC  0(0,RF),0(R7)                           
*                                                                               
ISADD36  L     R4,ISBUFF2                                                       
         LA    RF,6(R4)            CONTROL BYTES NOT INCLUDED                   
         SR    R3,RF                                                            
         STH   R3,0(R4)                                                         
         MVC   2(4,R4),DUB+2       RESTORE OLD LINK FIELD                       
*                                                                               
         BRAS  R9,ISADDOV          ADD ISBUFF2 TO OVERFLOW                      
*                                                                               
         TM    ISFFLAG,ISFBLKIX    BLOCK INDEX?                                 
         BZ    ISADD26             NO                                           
*                                                                               
         L     RE,ISBUFF1                                                       
         MVC   B2.SVLNK,2(RE)      SAVE OVERFLOW BLOCK D/A                      
         L     RE,ISBUFF2                                                       
         AH    RE,ISPDLN                                                        
         SH    RE,ISKEYLN          RE=(HIGH KEY IN PD BUFFER)                   
         LH    R1,ISKEYLN1                                                      
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   B2.SVHIKEY(0),0(RE) SAVE OVERFLOW HIGH KEY ADDED                 
         EJECT                                                                  
***********************************************************************         
* LOGIC TO PROCESS PD BLOCK INDEX AFTER SPLIT                         *         
***********************************************************************         
         TM    ISCMPRSW,X'80'      TEST FIXED/VARIABLE LENGTH RECORDS           
         BO    ISADD38                                                          
         CLI   ISSWAPSW,0          TEST NEED TO UNSWAP                          
         BE    ISADD38             NO                                           
                                                                                
***********************************************************************         
* USERS RECORD IS AT TOP OF BUFFER - GIVE IT BACK                     *         
***********************************************************************         
         L     RF,P2               GET USER ADDRESS                             
         L     R7,ISBUFF1                                                       
         AH    R7,0(R7)                                                         
         LA    R7,7(R7)            POINT TO FIRST DATA BYTE                     
         LH    R6,ISRECLN                                                       
         BAS   R9,MOVEREC2         MVC  0(R6,RF),0(R7)                          
         MVI   ISSWAPSW,0          CLEAR UNSWAP FLAG                            
*                                                                               
ISADD38  OC    ISOVDA,ISOVDA       BLKIND INDEX ALREADY PRESENT?                
         BNZ   ISADD40             YES                                          
*                                                                               
         L     R0,ISBUFF1          COPY ISBUFF1 TO ISBUFF2                      
         L     RE,ISBUFF2                                                       
         LH    R1,ISPDLN                                                        
         LH    RF,ISPDLN                                                        
         MVCL  RE,R0                                                            
*                                                                               
         L     RF,ISBUFF2          SAVE HIGH KEY                                
         AH    RF,ISPDLN                                                        
         SH    RF,ISKEYLN                                                       
         LH    R1,ISKEYLN1                                                      
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   B1.SVHIKEY(0),0(RF)                                              
*                                                                               
         BRAS  R9,ISADDOV          ADD PD BLOCK TO OVERFLOW                     
         L     RE,ISBUFF1                                                       
         MVC   B1.SVLNK,2(RE)      SAVE LINK D/A                                
         B     ISADD42                                                          
***********************************************************************         
* IF BLKIND PD BLOCK EXISTS, WE ARE ALWAYS SPLITTING AN OV BLOCK      *         
* WE ADDED THE SECOND HALF OF THE OV BLOCK. NOW WE JUST NEED TO       *         
* WRITE THIS PART BACK WHERE IT WAS IN THE OV CHAIN.                  *         
***********************************************************************         
ISADD40  MVC   B1.SVLNK,ISOVDA     SAVE LINK D/A                                
         L     RE,ISBUFF1                                                       
         AH    RE,ISPDLN                                                        
         SH    RE,ISKEYLN          RE=(HIGH KEY IN LOW OV BUFFER)               
         LH    R1,ISKEYLN1                                                      
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   B1.SVHIKEY(0),0(RE) SAVE KEY                                     
*                                                                               
         BRAS  R9,ISWRTPD          WRITE BACK OV BLOCK INTO ISOVDA              
*                                                                               
ISADD42  LA    R7,ISPDDA           GET PD BLOCK INTO ISBUFF1                    
         LA    R8,ISXTNTPD                                                      
         BRAS  RE,LOCKBLK                                                       
         BRAS  RE,ISBLDCCW                                                      
         BRAS  RE,SETSCTR                                                       
         BRAS  RE,EXCP                                                          
*                                                                               
         XC    ISOVDA,ISOVDA       MAKE SURE INDEX GOES TO PD BUFFER            
*                                                                               
         L     RE,ISBUFF1                                                       
         CLC   BLKINDEX,2(RE)      ALREADY A BLKIND BLOCK IN PD?                
         BNE   ISADD44             NO - BUILD A NEW ONE                         
*                                                                               
         BRAS  RE,CHKPD                                                         
         LA    R1,B1.SVBLKD        UPDATE BLOCK INDEX WITH NEW DETAILS          
         BRAS  RE,INSBI                                                         
         BRAS  RE,CHKPD                                                         
         LA    R1,B2.SVBLKD                                                     
         BRAS  RE,INSBI                                                         
         BRAS  RE,CHKPD                                                         
         B     ISADD46                                                          
*                                                                               
ISADD44  BRAS  RE,BLDNBI           BUILD NEW INDEX BLOCK                        
*                                                                               
ISADD46  BRAS  R9,ISWRTPD          WRITE IT BACK AS NECESSARY                   
         B     ISADDX                                                           
         EJECT                                                                  
***********************************************************************         
* ADD EXIT LOGIC                                                      *         
***********************************************************************         
ISADDX   XC    ISPDPTR,ISPDPTR     RESET SEQUENTIAL READ POINTER                
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* BUILD NEW BLKIND BLOCK                                              *         
***********************************************************************         
BLDNBI   NTR1                                                                   
         L     R0,ISBUFF1          CLEAR BUFFER                                 
         LH    R1,ISPDLN                                                        
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     R4,ISBUFF1                                                       
         MVC   2(4,R4),BLKINDEX    MARK LINK AS PD BLOCK INDEX                  
*                                                                               
         LH    RF,ISKEYLN4         ADDING 2 ENTRIES                             
         SLL   RF,1                                                             
         LH    R3,ISPDLN                                                        
         SR    R3,RF                                                            
         STH   R3,0(R4)            SET DISPLACEMENT TO FIRST DATA               
         AR    R3,R4               AND GO TO 2 ENTRIES FROM END                 
*                                                                               
         LA    R1,B1.SVBLKD        MAKE SURE FIRST TWO IN CORRECT ORDER         
         BRAS  RE,BLDNADD                                                       
         LA    R1,B2.SVBLKD                                                     
         BRAS  RE,BLDNADD                                                       
*                                                                               
         BRAS  RE,CHKPD                                                         
         B     RTNXIT                                                           
*                                                                               
         USING SVBLKD,R1                                                        
BLDNADD  LH    RF,ISKEYLN1         ADD ENTRY TO BUFFER AT (R3)                  
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),SVHIKEY                                                  
         AH    R3,ISKEYLN                                                       
         MVC   0(4,R3),SVLNK                                                    
         AHI   R3,4                                                             
         BR    RE                                                               
         DROP  R1,B1,B2                                                         
         EJECT                                                                  
***********************************************************************         
* INSERT NEW PD BLOCK INDEX ENTRY - R1=A(ENTRY TO ADD)                *         
***********************************************************************         
INSBI    NTR1                                                                   
         LR    R3,R1                                                            
         USING SVBLKD,R3                                                        
*                                                                               
INSB02   L     RF,ISBUFF1                                                       
         AH    RF,ISPDLN                                                        
         SH    RF,ISKEYLN4         POINT RF TO HIGHEST KEY IN BLOCK             
         LH    RE,ISKEYLN1                                                      
         EX    RE,*+8              ARE WE ADDING A HIGHER KEY?                  
         BNH   INSB04              NO - CARRY ON                                
         CLC   SVHIKEY(0),0(RF)                                                 
*                                                                               
         L     R7,ISBUFF1                                                       
         OC    6(4,R7),6(R7)       IS THERE AN OVERFLOW BLKIND BLOCK?           
         JZ    *+2                 DIE=NO OFLOW BLK                             
*                                                                               
         LA    R7,6(R7)                                                         
         MVC   ISOVDA,0(R7)        SAVE DISK ADDRESS                            
         LA    R8,ISXTNTIX                                                      
         BAS   RE,ISBLDCCW                                                      
         BAS   RE,SETSCTR                                                       
         BAS   RE,EXCP             AND READ THIS OVERFLOW BLOCK                 
         B     INSB02                                                           
*                                                                               
INSB04   OC    SVLNK,SVLNK         ENSURE WE HAVE A VALID D/A                   
         JZ    *+2                 DIE=ZERO LINK ADDR                           
*                                                                               
         L     R1,ISBUFF1                                                       
         AH    R1,0(R1)            POINT TO FIRST (LOWEST) KEY                  
         ST    R1,FULL             OVERWRITE POINT IF EQUAL                     
*                                                                               
         LH    RF,ISKEYLN1                                                      
         EX    RF,INSBCLC                                                       
         BE    INSB08              EQUAL - OVERWRITE                            
         BH    INSB06              HIGH - INSERT INTO ARRAY                     
*                                                                               
         L     R1,FULL             LOW - ADD BEFORE FIRST                       
         SH    R1,ISKEYLN4                                                      
         ST    R1,FULL             SET INSERT POINT BEFORE FIRST                
*                                                                               
         BRAS  RE,INSFIT           MAKE SURE ENTRY WILL FIT                     
         BRAS  RE,INSFXLN          UPDATE LENGTH TO REFLECT INSERTION           
         B     INSB08              INSERT KEY+DA (USE OVERWRITE LOGIC)          
*                                                                               
INSB06   LA    R1,SVHIKEY                                                       
         BRAS  RE,BSRCH            FIND INSERTION POINT (RET IN FULL)           
*                                                                               
         L     RE,FULL                                                          
         LH    RF,ISKEYLN1                                                      
         EX    RF,*+8                                                           
         BE    INSB08              EXACT MATCH - JUST OVERWRITE                 
         CLC   SVHIKEY(0),0(RE)                                                 
*                                                                               
         BRAS  RE,INSFIT           MAKE SURE ENTRY WILL FIT                     
*                                                                               
         L     RE,ISBUFF1          MOVE ALL ENTRIES BACK 1 SLOT                 
         AH    RE,0(RE)            RE=MOVE FROM POINT                           
         LR    R0,RE                                                            
         SH    R0,ISKEYLN4         R0=MOVE TO POINT                             
         L     RF,FULL                                                          
         SR    RF,RE               RF=MOVE FROM LENGTH                          
         L     R1,FULL                                                          
         SR    R1,R0               R1=MOVE TO LENGTH                            
         MVCL  R0,RE                                                            
*                                                                               
         L     RF,FULL                                                          
         SH    RF,ISKEYLN4         GO BACK 1 ENTRY FOR INSERT POINT             
         ST    RF,FULL                                                          
         BRAS  RE,INSFXLN          UPDATE LENGTH TO REFLECT INSERTION           
*                                                                               
INSB08   L     RF,FULL             OVERWRITE ENTRY - ADDRESS IN FULL            
         LH    RE,ISKEYLN1                                                      
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RF),SVHIKEY     KEY                                          
         AH    RF,ISKEYLN                                                       
         MVC   0(4,RF),SVLNK       D/A                                          
         B     RTNXIT                                                           
         DROP  R3                                                               
*                                                                               
INSFIT   L     R1,ISBUFF1                                                       
         LH    R0,0(R1)            MAKE SURE ANOTHER ENTRY WILL FIT             
         AHI   R0,-10                                                           
         CH    R0,ISKEYLN4                                                      
         BNLR  RE                                                               
*                                                                               
         BRAS  RE,INSBSPLT         SPLIT THIS INDEX BLOCK                       
         B     INSB02              AND TRY AGAIN                                
*                                                                               
INSFXLN  L     RF,ISBUFF1                                                       
         LH    R0,0(RF)            UPDATE LENGTH TO REFLECT INSERTION           
         SH    R0,ISKEYLN4                                                      
         STH   R0,0(RF)                                                         
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* CASE WHERE BLOCK IS FULL - NEED TO SPLIT THIS BLKIND BLOCK          *         
***********************************************************************         
INSBSPLT NTR1                                                                   
         BRAS  RE,SPBWTO           WRITE OUT CONSOLE MESSAGE                    
*                                                                               
         L     R0,ISBUFF2          CLEAR BUFFER 2                               
         LH    R1,ISPDLN                                                        
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     R7,ISBUFF2                                                       
         MVC   2(4,R7),BLKINDEX    RESET PD AS BLOCK INDEX                      
         LH    RF,ISPDLN                                                        
         STH   RF,0(R7)            SET DISPLACEMENT TO BE END                   
*                                                                               
         L     R7,ISBUFF1                                                       
         LH    RE,ISPDLN                                                        
         LH    RF,0(R7)                                                         
         SR    RE,RF                                                            
         SRDL  RE,32               FIND NUMBER OF WHOLE ENTRIES                 
         LH    R0,ISKEYLN4                                                      
         DR    RE,R0                                                            
         LTR   RE,RE                                                            
         JNZ   *+2                 DIE=INV BLK SPLIT                            
*                                                                               
         SRL   RF,1                WE WANT ABOUT 1/2 OF THE NUMBER              
         MH    RF,ISKEYLN4         WHICH THEN GIVES US A LENGTH                 
         ST    RF,SPADDR           THAT WE WANT TO SAVE FOR LATER USE           
         LR    R1,RF                                                            
*                                                                               
         LH    RE,ISPDLN                                                        
         SR    RE,RF                                                            
         A     RE,ISBUFF2          THIS GIVES A TO ADDRESS                      
         LH    R0,ISPDLN                                                        
         SR    R0,RF                                                            
         A     R0,ISBUFF1          THENCE WE DEDUCE A FROM ADDRESS              
*                                                                               
         MVCL  RE,R0               COPY BOTTOM 1/2 FROM ISBUFF1 TO 2            
*                                                                               
         L     R7,ISBUFF2                                                       
         LH    RF,ISPDLN                                                        
         S     RF,SPADDR                                                        
         STH   RF,0(R7)            THEN SET CORRECT LENGTH IN BUFFER            
         L     RE,ISBUFF1                                                       
         MVC   6(4,R7),6(RE)       AND COPY THE LINKAGE DISK ADDRESS            
*                                                                               
         BRAS  R9,ISADDOV          ADD ISBUFF2 TO OVERFLOW                      
*                                                                               
         L     R7,ISBUFF1                                                       
         MVC   6(4,R7),2(R7)       SAVE DISK ADDRESS CHAINED INDEX REC          
         MVC   2(4,R7),BLKINDEX    MARK THIS AS PD BLOCK INDEX                  
*                                                                               
         L     R0,ISBUFF1                                                       
         L     RE,ISBUFF2                                                       
         LH    R1,ISPDLN                                                        
         LH    RF,ISPDLN                                                        
         MVCL  RE,R0               COPY 1 TO 2                                  
*                                                                               
         L     R0,ISBUFF1                                                       
         AHI   R0,10                                                            
         LH    R1,ISPDLN                                                        
         AHI   R1,-10                                                           
         XR    RF,RF                                                            
         MVCL  R0,RE               CLEAR BUFFER 1 EXCEPT TOP INFO               
*                                                                               
         L     R7,ISBUFF2                                                       
         LH    RE,0(R7)            DISPLACEMENT TO FIRST DATA                   
         A     RE,ISBUFF2          RE=MOVE FROM POINT                           
*                                                                               
         LH    R0,0(R7)                                                         
         A     R0,SPADDR                                                        
         L     RF,ISBUFF1                                                       
         STH   R0,0(RF)            SET DISPLACEMENT IN ISBUFF1                  
         A     R0,ISBUFF1          R0=MOVE TO POINT                             
*                                                                               
         LH    RF,ISPDLN                                                        
         SH    RF,0(R7)                                                         
         S     RF,SPADDR           MOVE LENGTH=SPADDR - SLACK - OTHER           
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     R7,ISBUFF1                                                       
         BRAS  R9,ISWRTPD          WRITE FIRST  HALF BACK IN SITU               
*                                                                               
         XC    ISOVDA,ISOVDA                                                    
         LA    R7,ISPDDA           GET 'REAL' PD BLOCK BACK IN ISBUFF1          
         LA    R8,ISXTNTPD                                                      
         BRAS  RE,LOCKBLK                                                       
         BRAS  RE,ISBLDCCW                                                      
         BRAS  RE,SETSCTR                                                       
         BRAS  RE,EXCP                                                          
         J     RTNXIT              AND START OVER AGAIN                         
*                                                                               
INSBMV   MVC   0(0,RF),0(RE)                                                    
INSBXC   XC    0(0,RE),0(RE)                                                    
         EJECT                                                                  
***********************************************************************         
* ADD OVERFLOW RECORD IN ISBUFF2                                      *         
***********************************************************************         
ISADDOV  ST    R9,SAVER9                                                        
         BRAS  RE,LOCKDTF                                                       
*                                                                               
         LA    R7,ISOVLAST                                                      
         LA    R8,ISXTNTIX                                                      
         BAS   RE,ISBLDCCW         NOTE - BUILDS CORRECT SEEK ADDR              
         XC    CCW4,CCW4           MODIFY CCW4/CCW5                             
         LA    R0,COUNT                                                         
         ST    R0,CCW4                                                          
         MVI   CCW4,X'1D'          SET WTCKD                                    
         MVI   CCW4+4,X'80'        SET DATA CHAIN                               
         MVI   CCW4+7,8                                                         
*                                                                               
         LH    R0,ISPDLN           GET DATA LENGTH                              
         L     R1,ISBUFF2          GET DATA ADDRESS                             
         BAS   RE,SETIDAWS         BUILD IDAL                                   
*                                                                               
         LA    R0,IDAWS                                                         
         ST    R0,CCW5                                                          
         MVI   CCW5,X'05'          SET WRITE DATA                               
         MVI   CCW5+4,X'04'        SET IDAL FLAG                                
         MVC   CCW5+6(2),ISPDLN                                                 
                                                                                
***********************************************************************         
* NOW BUILD COUNT FIELD. REC ID IS 1 HIGHER THAN SRCH REC.            *         
***********************************************************************         
         XC    COUNT,COUNT                                                      
         MVC   COUNT(4),SRCH                                                    
         MVC   COUNT+6(2),ISPDLN                                                
         SR    RE,RE                                                            
         IC    RE,ISOVLAST+2                                                    
         LA    RE,1(RE)                                                         
         STC   RE,ISOVLAST+2                                                    
         STC   RE,COUNT+4                                                       
         L     R7,ISBUFF1                                                       
         MVC   2(4,R7),ISOVLAST    SET LINK FIELD IN BUFF1                      
         CH    RE,ISHIPD           SEE IF LAST REC ON PD TRK                    
         BL    *+22                NO                                           
         SR    RE,RE               YES - SET LAST TO REC 0 OF NEXT TRK          
         ICM   RE,3,ISOVLAST                                                    
         LA    RE,1(RE)                                                         
         SLL   RE,16                                                            
         ST    RE,ISOVLAST                                                      
*                                                                               
         BAS   RE,SETSCTR                                                       
         BAS   RE,EXCP                                                          
*                                                                               
         BRAS  RE,FREEDTF                                                       
         L     R9,SAVER9                                                        
         BR    R9                                                               
         EJECT                                                                  
***********************************************************************         
* MOVE DATA AT TOP OF BUFFER TO LEFT FOR INSERT LENGTH.               *         
* LENGTH OF MOVE IS GIVEN BY (INSERT ADDR - FIRST CONTROL BYTE ADDR)  *         
* DUB HAS ORIGINAL SPARE BYTES.                                       *         
* THEN INSERT DATA IN AREA JUST CREATED.                              *         
***********************************************************************         
ISADDMV  ST    R9,SAVER                                                         
*                                                                               
         AH    R7,DUB              ADD SPARE TO BUFFER ADDRESS                  
         LA    R7,6(R7)            FIRST CONTROL BYTE IS FROM ADDR              
         LR    RF,R7                                                            
         S     RF,DUB2             LESS INSERT LEN=TO ADDR                      
         ST    RF,DUB3             SAVE IT                                      
         L     R6,ISPDPTR          GET POINT OF INSERT                          
         SR    R6,R7               GIVES LENGTH TO MOVE                         
         BZ    *+8                 0 IF NEW REC IS FIRST IN BLK                 
         BAS   R9,MOVEREC2         MVC  0(R6,RF),0(R7)                          
*                                                                               
         L     RF,ISPDPTR          GET INSERTION ADDRESS                        
         L     R6,DUB2             GET INSERT LEN                               
         SR    RF,R6               GIVES TO ADDR                                
         L     RE,DUB1             GET NUMBER MATCHED BYTES INSERT REC          
         STC   RE,0(RF)            SET IN BUFFER                                
         LA    RF,1(RF)            SET TO ADDRESS                               
         BCTR  R6,0                                                             
         LA    R7,0(RE,R3)         GET FROM ADDRESS                             
         BAS   R9,MOVEREC2         MVC  0(R6,RF),0(R7)                          
*                                                                               
         L     R9,SAVER                                                         
         BR    R9                                                               
         EJECT                                                                  
***********************************************************************         
* CLEAR 0(R4) FOR LENGTH IN R6                                        *         
***********************************************************************         
ISADDXC  LR    R0,R4               SET TO ADDRESS                               
         LR    R1,R6               SET TO LENGTH                                
         XR    RF,RF               SET FROM LENGTH                              
         MVCL  R0,RE                                                            
         BR    R9                                                               
         EJECT                                                                  
***********************************************************************         
* SWAP ISBUFF1 WITH ISBUFF2                                           *         
***********************************************************************         
SWAPB12  ST    R9,SAVER                                                         
         LH    R6,ISPDLN                                                        
         L     R7,ISBUFF1                                                       
         L     R3,ISBUFF2                                                       
*                                                                               
         LA    RE,256              SWAP IN 256 BYTE CHUNKS                      
SWAP010  CR    R6,RE                                                            
         BNH   SWAP020                                                          
         XC    0(256,R3),0(R7)                                                  
         XC    0(256,R7),0(R3)                                                  
         XC    0(256,R3),0(R7)                                                  
         SR    R6,RE                                                            
         AR    R3,RE                                                            
         AR    R7,RE                                                            
         B     SWAP010                                                          
*                                                                               
SWAP020  BCTR  R6,0                                                             
         EX    R6,SWAPXC37                                                      
         EX    R6,SWAPXC73                                                      
         EX    R6,SWAPXC37                                                      
*                                                                               
         L     R3,P2               RESET REC POINTER                            
         L     R9,SAVER                                                         
         BR    R9                                                               
         EJECT                                                                  
***********************************************************************         
* ISERASE - ERASE RECORD IN ISPDKEY                                   *         
***********************************************************************         
ISERASE  OC    ISPDDA,ISPDDA       TEST BUFFER CONTENTS VALID                   
         JZ    *+2                 DIE=ZERO ISPDDA                              
         L     R3,P2               POINT TO RECORD AS KEYARG                    
         L     R4,ISPDKEY                                                       
         LH    R5,ISKEYLN1                                                      
         EX    R5,SRCHCLC          CLC  0(0,R3),0(R4)                           
         JNE   *+2                 DIE=KEYS NOT EQL                             
*                                                                               
         BRAS  R9,CHKBUFF          DO BUFFER SEARCH TO BUILD PRV KEY            
         DC    AL4(0)                                                           
         DC    AL4(0)                                                           
*                                                                               
         L     R4,ISPDPTR                                                       
         TM    ISCMPRSW,VARIABLE   TEST FIXED/VARIABLE LENGTH RECORDS           
         BZ    ISERS0                                                           
         LH    R6,ISKEYLN                                                       
         SR    RE,RE                                                            
         IC    RE,0(R4)            GET MISSING BYTES                            
         SR    R6,RE               GIVES KEY BYTES PRESENT                      
         LA    R6,1(R6,R4)         POINT TO REC LEN                             
         MVC   ISRECLN,0(R6)       SAVE REC LEN                                 
*                                                                               
ISERS0   LH    R6,ISRECLN                                                       
         SR    RE,RE                                                            
         IC    RE,0(R4)            GET MISSING BYTES                            
         BCTR  RE,0                ADJUST FOR CONTROL BYTE                      
         SR    R6,RE               GIVES BYTES PRESENT                          
         LA    R7,0(R6,R4)         POINT TO NEXT CONTROL BYTE                   
         STM   R6,R7,DUB           SAVE BYTES/NEXT ADDRESS                      
*                                                                               
         LH    R5,ISKEYLN1                                                      
         L     RF,ISPDPRKY                                                      
         EX    R5,ISADDOC          TEST NO PRV KEY  OC  0(0,RF),0(RF)           
         BNZ   ISERS4              REC IS NOT FIRST IN BLK                      
                                                                                
***********************************************************************         
* ERASING FIRST REC                                                   *         
***********************************************************************         
         CLI   0(R7),X'FF'         TEST NEXT IS EOB                             
         BE    ISERS2              YES - ONLY 1 REC IN BLK                      
*                                                                               
         LR    R3,R7               SAVE NEXT REC ADDRESS                        
         BAS   R9,ISADDXC          XC  0(R6,R4),0(R4)                           
                                                                                
***********************************************************************         
* NOW EXPAND NEW FIRST KEY IN BUFFER AND SET NEW SLACK                *         
***********************************************************************         
         SR    R6,R6                                                            
         IC    R6,0(R3)            GET MISSING KEY BYTES                        
         LTR   R6,R6                                                            
         BZ    ISERS1                                                           
         SR    R3,R6                                                            
         LA    RF,1(R3)            SET TO ADDRESS                               
         L     R7,ISPDKEY                                                       
         BCTR  R6,0                                                             
         EX    R6,MOVEMVC          MVC  0(0,RF),0(R7)                           
*                                                                               
ISERS1   L     R4,ISBUFF1                                                       
         LA    RF,6(R4)                                                         
         SR    R3,RF                                                            
         STH   R3,0(R4)                                                         
         B     ISADD26                                                          
                                                                                
***********************************************************************         
* ERASING ONLY RECORD IN BLOCK                                        *         
***********************************************************************         
ISERS2   L     R0,ISBUFF2          SAVE DEAD BLOCK IN ISBUFF2                   
         LH    R1,ISPDLN           (NEEDED TO SAVE HIGH KEY)                    
         L     RE,ISBUFF1                                                       
         LR    RF,R1                                                            
         MVCL  R0,RE               MOVE BUFF1 TO BUFF2                          
*                                                                               
         L     R4,ISBUFF1                                                       
         LA    R4,6(R4)                                                         
         LH    R6,ISPDLN           LET IT CLEAR 6 EXTRA                         
         BAS   R9,ISADDXC          XC  0(R6,R4),0(R4)                           
         L     R4,ISBUFF1                                                       
         XC    0(2,R4),0(R4)       CLEAR SPARE                                  
         MVI   6(R4),X'FF'                                                      
*                                                                               
         BAS   R9,ISWRTPD          WRITE PRIME DATA BLOCK                       
                                                                                
***********************************************************************         
* NOW CLEAN UP THE FILE BY FINDING THE BLOCK THAT POINTS TO THIS      *         
* ONE AND RECHAINING IT WITH THE LINK CURRENTLY IN THIS BLOCK         *         
***********************************************************************         
         OC    ISOVDA,ISOVDA       IF THIS IS NOT AN OVERFLOW BLOCK             
         BZ    ISERS2C             THE DAMAGE WILL BE MASSIVE                   
*                                                                               
         MVC   DEADBLKA,ISOVDA     SAVE DISK ADDRESS OF THIS BLOCK              
         MVC   DEADBLKL,2(R4)      SAVE LINK ADDRESS FROM THIS BLOCK            
*                                                                               
         XC    ISOVDA,ISOVDA       START WITH THE PRIME DATA BLOCK              
         LA    R7,ISPDDA                                                        
         LA    R8,ISXTNTPD                                                      
*                                                                               
ISERS2A  BAS   RE,ISBLDCCW                                                      
         BAS   RE,SETSCTR                                                       
         BAS   RE,EXCP                                                          
*                                                                               
         L     R7,ISBUFF1                                                       
         CLC   2(4,R7),BLKINDEX    TEST FOR PD BLOCK INDEX LINK                 
         BNE   ISERS2A1            NO                                           
*                                                                               
         LH    RF,ISKEYLN                                                       
         AH    RF,0(R7)                                                         
         AR    RF,R7                                                            
         MVC   DUB(4),0(RF)        SAVE FIRST PD OVERFLOW BLOCK DA              
*                                                                               
         BRAS  RE,ERSBI            ERASE PD BLOCK INDEX ENTRY                   
         BAS   R9,ISWRTPD          WRITE THE UPDATED PD BLOCK INDEX             
         MVC   ISOVDA,DUB                                                       
         CLC   DEADBLKA,DUB        IS THE DEAD BLOCK FIRST IN CHAIN             
         BE    ISERS2C             YES - NOTHING LINKS TO IT                    
         B     ISERS2A2            PROCESS FIRST PD OVERFLOW BLOCK              
*                                                                               
ISERS2A1 CLC   DEADBLKA,2(R7)      DOES THIS BLOCK POINT TO DEAD ONE            
         BE    ISERS2B                                                          
         OC    2(4,R7),2(R7)       MAKE SURE THERE IS A LINK                    
         JZ    *+2                 DIE=ZERO LINK ADDR                           
         MVC   ISOVDA,2(R7)        PROCESS NEXT PD OVERFLOW BLOCK               
*                                                                               
ISERS2A2 LA    R7,ISOVDA                                                        
         LA    R8,ISXTNTIX                                                      
         B     ISERS2A                                                          
*                                                                               
ISERS2B  MVC   2(4,R7),DEADBLKL    MOVE LINK FROM DEAD BLOCK                    
         BAS   R9,ISWRTPD          WRITE THIS UPDATED BLOCK                     
*                                                                               
ISERS2C  XC    ISPDPTR,ISPDPTR     RESET SEQUENTIAL READ POINTER                
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* NOT ERASING FIRST REC IN BLK                                        *         
* R6 HAS BYTES THIS REC/R7 HAS NEXT REC ADDRESS                       *         
* R4 POINTS TO REC BEING ERASED                                       *         
***********************************************************************         
ISERS4   CLI   0(R7),X'FF'         TEST NEXT IS EOB                             
         BE    ISERS5                                                           
**NOP**  B     ISERS6                                                           
                                                                                
***********************************************************************         
* TEST IF NUMBER OF MISSING BYTES IN REC BEING ERASED                 *         
* IS LESS THAN MISSING FROM NEXT                                      *         
***********************************************************************         
         CLC   0(1,R4),0(R7)                                                    
         BNL   ISERS6                                                           
         LLC   RE,0(R7)            GET MISSING BYTES FROM NEXT RECORD           
         L     RF,ISPDKEY                                                       
         AR    RF,RE               SET TO ADDR=ISPDKEY+MISSING                  
         LH    R5,ISKEYLN1                                                      
         SR    R5,RE               GIVES LEN-1 TO MOVE                          
         EX    R5,CHKMVKEY         MVC  0(0,RF),1(R7)                           
*                                                                               
         L     RE,ISPDPRKY         NOW FIND HOW MANY BYTES MATCH                
         L     RF,ISPDKEY                                                       
         SR    R0,R0               CLEAR COUNTER                                
*                                                                               
ISERS4A  CLC   0(1,RE),0(RF)                                                    
         BNE   ISERS4B                                                          
         LA    RE,1(RE)                                                         
         LA    RF,1(RF)                                                         
         BCT   R0,ISERS4A                                                       
*                                                                               
ISERS4B  LPR   R0,R0               GIVES CORRECTED MISSING BYTES                
         LLC   RE,0(R7)            GET CURRENT MISSING BYTES                    
         SR    RE,R0               GIVES NUMBER BYTES TO ADJUST                 
         SR    R7,RE               BACK UP NEXT RECORD POINTER                  
         SR    R6,RE               DECREMENT LENGTH TO MOVE                     
         STM   R6,R7,DUB           SAVE LENGTH AND POINTER                      
         STC   R0,0(R7)            SET MISSING BYTES                            
*                                                                               
         LH    RE,ISKEYLN1         MOVE CORRECTED KEY INTO NEXT RECORD          
         SR    RE,R0               GIVES BYTES PRESENT                          
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   1(0,R7),0(RF)                                                    
         B     ISERS6                                                           
                                                                                
***********************************************************************         
* ERASING LAST RECORD IN BLOCK                                        *         
***********************************************************************         
ISERS5   L     RF,ISBUFF1          MUST HAVE LINK                               
         OC    2(4,RF),2(RF)                                                    
         JZ    *+2                 DIE=ZERO LINK ADDR                           
         L     R7,ISPDPRKY         SET NEW HIGH KEY                             
         L     RF,ISBUFF1X                                                      
         SR    RF,R5               GET TO ADDRESS                               
         EX    R5,MOVEMVC          MVC  0(0,RF),0(R7)                           
                                                                                
***********************************************************************         
* NOW UPDATE BLKIND TO REFLECT THE NEW HIGH KEY IN THIS BLOCK         *         
* ISPDKEY  = A(OLD KEY)                                               *         
* ISPDPRKY = A(NEW KEY)                                               *         
* ISPDDA   = DA OF PD BLOCK (SHOULD BE A BLKIND)                      *         
***********************************************************************         
         L     R7,DUB+4            NEXT CONTROL BYTE=FROM ADDRESS               
         L     RF,ISPDPTR          GET TO ADDRESS                               
         L     R6,ISBUFF1X                                                      
         SH    R6,ISKEYLN1         CAREFUL NOT TO CLOBBER HI KEY                
         SR    R6,R7               GIVES LEN TO MOVE                            
         BAS   R9,MOVEREC2         MVC  0(R6,RF),0(R7)                          
*                                                                               
         L     R4,ISBUFF1X                                                      
         SH    R4,ISKEYLN1                                                      
         L     R6,DUB              LEN TO CLEAR                                 
         SR    R4,R6               GIVE START ADDRESS FOR CLEAR                 
         BAS   R9,ISADDXC          XC  0(R6,R4),0(R4)                           
         OC    ISOVDA,ISOVDA                                                    
         BZ    ISADD26                                                          
         BAS   R9,ISWRTPD          WRITE OV DATA BLOCK                          
*                                                                               
         LA    R7,ISPDDA           READ PD BLOCK                                
         BAS   RE,ISBLDCCW                                                      
         BAS   RE,SETSCTR                                                       
         BAS   RE,EXCP                                                          
*                                                                               
         L     R7,ISBUFF1                                                       
         CLC   2(4,R7),BLKINDEX    TEST FOR PD BLOCK INDEX LINK                 
         BNE   ISADD26A            NO                                           
*                                                                               
         BRAS  RE,UPDBI            UPDATE PD BLOCK INDEX ENTRY                  
         B     ISADD26             WRITE THE UPDATED PD BLOCK INDEX             
                                                                                
***********************************************************************         
* NOW MOVE DATA TO LEFT AND CLEAR                                     *         
***********************************************************************         
ISERS6   L     R7,DUB+4            NEXT CONTROL BYTE=FROM ADDRESS               
         L     RF,ISPDPTR          GET TO ADDRESS                               
         L     R6,ISBUFF1X                                                      
         SH    R6,ISKEYLN1         CAREFUL NOT TO CLOBBER HI KEY                
         SR    R6,R7               GIVES LEN TO MOVE                            
         BAS   R9,MOVEREC2         MVC  0(R6,RF),0(R7)                          
*                                                                               
ISERS8   L     R4,ISBUFF1X                                                      
         SH    R4,ISKEYLN1                                                      
         L     R6,DUB              LEN TO CLEAR                                 
         SR    R4,R6               GIVE START ADDRESS FOR CLEAR                 
         BAS   R9,ISADDXC          XC  0(R6,R4),0(R4)                           
         B     ISADD26                                                          
         EJECT                                                                  
***********************************************************************         
* GET CPU ID FROM RECORD ZERO OF TRACK ONE IN INDEX EXTENT            *         
* I/O IS DONE TO NMOD AREA IN 24 BIT ADDRESSABLE STORAGE, NO IDAL.    *         
* IF STORAGE PROTECT ON REC 0 READ INTO TCB FIELD (NO EXCP IN KEY 9)  *         
***********************************************************************         
ISCPUID  LA    R7,=F'-1'           POINT TO DUMMY D/A                           
         LA    R8,ISXTNTIX                                                      
         BRAS  RE,ISBLDCCW                                                      
*                                                                               
         XC    CCW4,CCW4           READ RECORD ZERO                             
         LA    RE,COUNT                                                         
*                                                                               
         ICM   RF,15,VSSB                                                       
         BZ    ISCP02                                                           
         USING SSBD,RF                                                          
         TM    SSBPROT,SSBPONQ                                                  
         BZ    ISCP02                                                           
         ICM   RF,15,SSBTKADR                                                   
         BZ    ISCP02                                                           
         USING TCBD,RF                                                          
*                                                                               
         LA    RE,TCBCIOA                                                       
         DROP  RF                                                               
*                                                                               
ISCP02   ST    RE,AREC0                                                         
         ST    RE,CCW4                                                          
         MVI   CCW4,6                                                           
         LA    RE,8                                                             
         STH   RE,CCW4+6                                                        
*                                                                               
         BRAS  RE,INIEXCP          READ RECORD ZERO                             
         BRAS  RE,EXCPCHK                                                       
         BZ    ISCP04                                                           
         MVC   P3(2),ERRF          SET ERROR AND EXIT                           
         B     EXIT                                                             
*                                                                               
ISCP04   L     RE,AREC0                                                         
         L     R5,X'10'(,R0)       POINT TO CPU ID                              
         L     R5,X'C4'(,R5)                                                    
         LA    R5,X'10'(,R5)       R5=A(FOUR CHR CPU ID)                        
         MVC   DUB(2),2(R5)                                                     
*                                                                               
         MVC   P3+2(2),6(RE)       RETURN REC ZERO CPU ID                       
         CLC   6(1,RE),DUB         SAME AS RECORD ZERO VALUE                    
         BE    ISCP06                                                           
         OI    P3+1,X'80'          NO - SET ERROR                               
         B     EXIT                                                             
*                                                                               
ISCP06   CLI   7(RE),C' '          TEST FOR DSPACE CHR                          
         BNH   EXIT                                                             
         TM    ISFFLAG,ISFGLOB     IS IT GLOBAL                                 
         BZ    EXIT                                                             
*                                                                               
         ICM   RF,15,VSSB                                                       
         BZ    EXIT                                                             
         USING SSOOFF,RF                                                        
         OC    SSOCNTL,SSOCNTL                                                  
         BNZ   ISCP07              TEST IF ONLINE                               
*                                                                               
         CLI   SSODSPAC,C' '                                                    
         BH    *+10                                                             
         MVC   SSODSPAC(1),7(RE)                                                
*                                                                               
         CLC   SSODSPAC(1),7(RE)                                                
         BE    EXIT                                                             
         OI    P3+1,X'10'                                                       
         B     EXIT                DSPACE REQUIRED DOES NOT MATCH               
*                                                                               
         USING SSBD,RF                                                          
ISCP07   TM    ISFOPEN,X'C0'       TEST R/O OR NOP                              
         BNZ   EXIT                                                             
*                                                                               
         TM    SSBSYSFL,X'80'      TEST SYSTEM                                  
         BZ    EXIT                                                             
         CLI   7(RE),C'A'          CHECK PROD FILE                              
         BNE   EXIT                                                             
         OI    ISFOPEN,X'04'       WRONG STAMP SET TO QUIESCED                  
*&&DO                                                                           
         LA    R4,MYWORK+8                                                      
         ST    R4,MYWORK                                                        
         OI    MYWORK+4,X'80'      HARDCOPY ONLY                                
         MVC   MYWORK+8(8),ISFDD                                                
         MVC   MYWORK+16(19),=C'QUIESCED DSPACE= //'                            
         MVC   MYWORK+32(1),7(RE)                                               
         GOTO1 =V(DDWTO),MYWORK                                                 
*&&                                                                             
         B     EXIT                                                             
         DROP  RF                                                               
         EJECT                                                                  
***********************************************************************         
* THIS ROUTINE IS USED OFFLINE TO MAKE SURE WE HAVE RIGHT EOF ADDRESS *         
* I/O IS DONE IN NMOD AREA (24-BIT) SO NO IDAL REQUIRED               *         
***********************************************************************         
ISFNDEOF MVC   DUB(4),ISOVLAST                                                  
         XR    RE,RE                                                            
         IC    RE,DUB+2                                                         
         CH    RE,ISHIPD           TEST LAST REC ON TRACK                       
         BL    ISEOF02             NO                                           
*                                                                               
         LH    RE,DUB                                                           
         LA    RE,1(RE)                                                         
         SLL   RE,16                                                            
         ST    RE,DUB                                                           
         SR    RE,RE                                                            
*                                                                               
ISEOF02  LA    RE,1(RE)                                                         
         STC   RE,DUB+2                                                         
         LA    R7,DUB                                                           
         LA    R8,ISXTNTIX                                                      
         BRAS  RE,ISBLDCCW                                                      
*                                                                               
         IC    R0,CCW4                                                          
         LA    RE,COUNT                                                         
         ST    RE,CCW4             MODIFY DATA ADDRESS                          
         STC   R0,CCW4                                                          
         MVI   CCW4+4,X'10'        RESET FLAGS/ SET SUPPRESS DATA XFR           
*                                                                               
         BAS   RE,INIEXCP                                                       
         BAS   RE,EXCPCHK                                                       
         TM    ERRF+1,X'08'        TEST NOT FOUND                               
         BO    EXIT                                                             
*                                                                               
         MVC   ISOVLAST,DUB        ELSE SET LAST KNOWN REC                      
         B     ISFNDEOF            AND TRY AGAIN                                
         EJECT                                                                  
***********************************************************************         
* READ INDEX RECORD - R3=A(KEYARG)                                    *         
***********************************************************************         
READIX   XC    ISPDDA(12),ISPDDA   INCLUDES ISOVDA,ISPDPTR                      
         LH    R5,ISKEYLN1                                                      
         L     R4,ISPDPRKY                                                      
         EX    R5,READXC                                                        
         L     R4,ISPDKEY                                                       
         EX    R5,READXC                                                        
         L     R4,ISTIHIKY                                                      
         EX    R5,READXC                                                        
                                                                                
***********************************************************************         
* SEARCH CI TABLE - ONE ENTRY PER CI INDEX PAGE                       *         
* TABLE FORMAT IS PAGE ADDRESS/HI KEY ON PAGE                         *         
***********************************************************************         
READIX2  L     R4,ISINDX           GET INDEX TABLE ADDRESS                      
         L     RE,0(R4)            GET ENTRY LENGTH                             
         L     RF,4(R4)            GET END ADDRESS                              
         LA    R4,8(R4)            POINT TO FIRST ENTRY                         
         LH    R5,ISKEYLN1                                                      
*                                                                               
READIX4  EX    R5,READICLC                                                      
         BNH   SRCHCI                                                           
         BXLE  R4,RE,READIX4                                                    
         DC    H'0'                DIE=INDEX IS BAD                             
                                                                                
***********************************************************************         
* NOW SEARCH CI INDEX - R4 POINTS TO FIRST SEARCH KEY                 *         
***********************************************************************         
SRCHCI   L     R4,0(R4)            POINT TO THE INDEX RECORD                    
*                                                                               
SRCHCI2  EX    R5,SRCHCLC          CLC  0(0,R3),0(R4)                           
         BNH   READTI                                                           
         AH    R4,ISIXLN                                                        
         B     SRCHCI2                                                          
         EJECT                                                                  
***********************************************************************         
* READ TRACK INDEX RECORD                                             *         
***********************************************************************         
READTI   LA    R8,ISXTNTPD                                                      
         LA    R7,1(R5,R4)                                                      
         XC    DUB,DUB                                                          
         MVC   DUB(2),0(R7)        SAVE RELTRK                                  
         ICM   R4,15,ISTIBUFF      USE TIBUFF IF AVAILABLE                      
         BZ    READTI2                                                          
         CLC   DUB(2),ISTITRK      IS IT THE TRK WE HAVE SAVED                  
         BE    READTI6             YES                                          
*                                                                               
READTI2  LA    R7,DUB                                                           
         BRAS  RE,ISBLDCCW                                                      
*                                                                               
         MVC   ISTITRK,DUB         SAVE TRACK NUMBER                            
         ICM   R1,15,ISTIBUFF      USE TIBUFF IF AVAILABLE                      
         BZ    READTI4                                                          
         SR    R0,R0               OFFLINE SET IDAW LIST FOR TI BUFFER          
         ICM   R0,3,ISTILN         GET DATA LEN                                 
         BAS   RE,SETIDAWS                                                      
         LA    R0,IDAWS                                                         
         STCM  R0,7,CCW4+1                                                      
*                                                                               
READTI4  MVI   CCW2,X'51'          SET TO SRCH ID HI  (REC=0)                   
         MVC   CCW4+6(2),ISTILN    SET CORRECT DATA LENGTH                      
*NOP*    BAS   RE,SETSCTR          DONT DO THIS - RECORD IS REPLICATED          
         BAS   RE,EXCP                                                          
         OC    ERRF,ERRF           TEST FOR ERROR READING TRACK INDEX           
         BZ    READTI5                                                          
         BAS   RE,EXCP             HAVE ANOTHER GO                              
         OC    ERRF,ERRF                                                        
         BZ    READTI5                                                          
         DC    H'0'                DIE=TRK NDX ERR                              
*                                                                               
READTI5  L     R4,IDAWS            PICK UP I/O AREA ADDRESS                     
*                                                                               
READTI6  SR    RE,RE               SET COUNTER                                  
         SR    R0,R0                                                            
         ICM   R0,3,ISTILN                                                      
         AR    R0,R4               SET UPPER BOUND FOR SEARCH                   
         B     SRCHTI2                                                          
                                                                                
SRCHTI   EX    R5,SRCHCLC          CLC  0(0,R3),0(R4)                           
         BNH   READPD                                                           
         AH    R4,ISKEYLN                                                       
         CR    R4,R0                                                            
         JNL   *+2                 DIE=TRK NDX ERR                              
SRCHTI2  BCT   RE,SRCHTI                                                        
         EJECT                                                                  
***********************************************************************         
* READ PRIME DATA RECORD                                              *         
***********************************************************************         
READPD   L     R1,ISTIHIKY         SAVE TI ENTRY                                
         EX    R5,CHKSVKEY         MVC  0(0,R1),0(R4)                           
*                                                                               
         LH    R0,ISHIPD                                                        
         LPR   RE,RE                                                            
         BCTR  RE,0                                                             
         SRDA  RE,32                                                            
         DR    RE,R0                                                            
         LA    RE,1(RE)            RE HAS REC-1                                 
         LA    RF,1(RF)            RF HAS TRK-1                                 
         ICM   R0,3,DUB            DUB HAS START OF CYLINDER                    
         AR    RF,R0                                                            
*                                                                               
         STH   RF,ISPDDA                                                        
         STC   RE,ISPDDA+2                                                      
*                                                                               
         LA    R7,ISPDDA                                                        
         LA    R8,ISXTNTPD                                                      
         BRAS  RE,LOCKBLK                                                       
         BAS   RE,ISBLDCCW                                                      
         BAS   RE,SETSCTR                                                       
         BAS   RE,EXCP                                                          
*                                                                               
         L     RF,ISBUFF1                                                       
         CLC   BLKINDEX,2(RF)      CHECK PD BLOCK INDEX STRUCTURE FOUND         
         BNE   *+8                 IF SO SET ISDTF FLAG ON FLY FOR              
         OI    ISFFLAG,ISFBLKIX    BACKWARD COMPATABILITY OF ISDDS              
*                                                                               
         TM    ISFFLAG,ISFBLKIX    TEST BLOCK INDEX ON SPLIT BLOCKS             
         BZ    CHKHI               NO - CHECK HIGH KEY IN BUFFER                
         CLC   BLKINDEX,2(RF)      PD IS BLKIND?                                
         BE    SRCHBI              YES                                          
         B     CHKHI               GO AND CHECK HIGH IN BUFFER                  
         EJECT                                                                  
***********************************************************************         
* READ OVERFLOW RECORD                                                *         
***********************************************************************         
READOV   L     R7,ISBUFF1                                                       
         LA    R7,2(R7)                                                         
         MVC   ISOVDA,0(R7)        SAVE DISK ADDRESS                            
         CLC   ISOVDA,BLKINDEX                                                  
         BNE   *+14                                                             
         LA    R7,4(R7)                                                         
         MVC   ISOVDA,0(R7)        SAVE BLKIND DISK ADDRESS                     
*                                                                               
         LA    R8,ISXTNTIX                                                      
         BAS   RE,ISBLDCCW                                                      
         BAS   RE,SETSCTR                                                       
         BAS   RE,EXCP                                                          
         B     CHKHI                                                            
         EJECT                                                                  
***********************************************************************         
* SEARCH PD BLOCK INDEX FOR A(FIRST PD) AND ITS HIGH KEY              *         
***********************************************************************         
SRCHBI   L     RF,ISBUFF1                                                       
         CLC   ISPDLN,0(RF)        IS THIS BLOCK EMPTY?                         
         BE    SRCHB02             YES - CAUSED BY SPLIT THEN BACKOUT           
*                                                                               
         AH    RF,ISPDLN                                                        
         SH    RF,ISKEYLN4         POINT RF TO HIGHEST KEY IN BLOCK             
         LH    RE,ISKEYLN1                                                      
         EX    RE,*+8              ARE WE ADDING A HIGHER KEY?                  
         BNH   SRCHB04             NO - CARRY ON                                
         CLC   0(0,R3),0(RF)                                                    
*                                                                               
SRCHB02  L     R7,ISBUFF1                                                       
         OC    6(4,R7),6(R7)       IS THERE AN OVERFLOW BLKIND BLOCK?           
         JZ    *+2                 DIE=BLK NDX ERR                              
*                                                                               
         LA    R7,6(R7)                                                         
         MVC   ISOVDA,0(R7)        SAVE DISK ADDRESS                            
         LA    R8,ISXTNTIX                                                      
         BAS   RE,ISBLDCCW                                                      
         BAS   RE,SETSCTR                                                       
         BAS   RE,EXCP             AND READ THIS OVERFLOW BLOCK                 
         B     SRCHBI                                                           
*                                                                               
SRCHB04  LR    R1,R3                                                            
         BRAS  RE,BSRCH            FIND NEXT HIGH KEY                           
         L     R7,FULL                                                          
         AH    R7,ISKEYLN                                                       
         MVC   ISOVDA,0(R7)        SAVE DISK ADDRESS                            
*                                                                               
         LA    R8,ISXTNTIX                                                      
         BAS   RE,ISBLDCCW                                                      
         BAS   RE,SETSCTR                                                       
         BAS   RE,EXCP                                                          
         B     CHKHI                                                            
         EJECT                                                                  
***********************************************************************         
* TEST IF RECORD IS IN PD BUFFER                                      *         
***********************************************************************         
CHKHILO  OC    ISPDDA,ISPDDA       TEST BUFFER CONTENTS VALID                   
         BZR   R9                  NO - TAKE RETURN 1                           
*                                                                               
         L     R4,ISBUFF1          GET A(LO KEY THIS PD BUFFER)                 
         CLI   6(R4),X'FF'         TEST NULL BLOCK                              
         BER   R9                  YES - EXIT                                   
*                                                                               
         CLC   BLKINDEX,2(R4)      PD BLOCK INDEX LINK?                         
         BER   R9                  EXIT AND READIX                              
                                                                                
CHKLO    NI    BUFFOK,255-X'01'    TEST IF KEY LOWER THAN LOW IN BLOCK          
         AH    R4,0(R4)                                                         
         LA    R4,7(R4)            POINT TO START OF FIRST KEY                  
         LH    R5,ISKEYLN1                                                      
         EX    R5,CHKCLC           CLC  0(0,R3),0(R4)                           
         BLR   R9                  START AGAIN WITH INDEX SEARCH                
         OI    BUFFOK,X'01'        SET BUFFER CHECKED FOR LOW KEY               
                                                                                
CHKHI    NI    BUFFOK,255-X'02'    TEST IF KEY HIGHER THAN MAX IN BLOCK         
         LH    R5,ISKEYLN1                                                      
         L     R4,ISBUFF1X         GET BUFFER END ADDRESS                       
         SR    R4,R5               BACK UP TO START OF HI KEY                   
         EX    R5,CHKCLC           CLC  0(0,R3),0(R4)                           
         BH    4(R9)               CHECK OVERFLOW BLOCK                         
         OI    BUFFOK,X'02'        SET BUFFER CHECKED FOR HIGH KEY              
         B     CHKBUFF                                                          
                                                                                
***********************************************************************         
* SEE IF KEYARG LESS THAN OR EQUAL TO TRACK INDEX ENTRY               *         
* FOR THIS PRIME DATA BLOCK.                                          *         
***********************************************************************         
CHKHITI  L     R4,ISTIHIKY                                                      
         LH    R5,ISKEYLN1                                                      
         EX    R5,CHKCLC           CLC  0(0,R3),0(R4)                           
         BR    R9                  RETURN WILL TEST CC                          
         EJECT                                                                  
***********************************************************************         
* SEARCH CORE BUFFER                                                  *         
***********************************************************************         
CHKBUFF  BRAS  RE,LOCKBLK          CHECK IF NEED TO LOCK                        
         TM    READTYPE,FLUSHME                                                 
         BNO   CHKBUFF2                                                         
         NI    READTYPE,255-FLUSHME                                             
         OI    READTYPE,FLUSH                                                   
         B     READIX                                                           
*                                                                               
CHKBUFF2 OC    ISPDPTR,ISPDPTR     IS A LOCATION IN THE BUFFER DEFINED          
         BZ    CHKBUFF3+6          NO - BEGIN AT START OF BLOCK                 
         TM    BUFFOK,X'83'        TEST ISREAD/LOWKEY/HIGHKEY                   
         BNO   CHKBUFF3            NO - BEGIN AT START OF BLOCK                 
         NI    BUFFOK,255-X'83'                                                 
*                                                                               
         L     R4,ISBUFF1          START OF BUFFER                              
         AH    R4,0(R4)            SKIP OVER SPARE                              
         LA    R4,6(R4)            POINT TO FIRST CONTROL BYTE                  
         C     R4,ISPDPTR          ARE WE AT START OF THE BLOCK?                
         BE    CHKBUFF3            YES - CLEAR VARIABLES                        
*                                                                               
         L     R4,ISPDKEY                                                       
         LH    R5,ISKEYLN1                                                      
         EX    R5,SRCHCLC          TEST PREVIOUS KEY TO THIS                    
         BE    8(R9)               EXIT IF THIS KEY SAME AS LAST KEY            
         BNH   CHKBUFF3            IF KEY LOW BEGIN AT START OF BLOCK           
         L     R1,ISPDPRKY                                                      
         L     R7,ISPDPTR                                                       
         B     CHKBUFF4            SET ISPDPRKY FROM ISPDKEY                    
*                                                                               
CHKBUFF3 XC    ISPDPTR,ISPDPTR     START FROM BEGINNING OF BUFFER               
         L     R4,ISPDPRKY                                                      
         LH    R5,ISKEYLN1                                                      
         EX    R5,READXC                                                        
*                                                                               
         L     R4,ISPDKEY                                                       
         L     R7,ISBUFF1                                                       
         AH    R7,0(R7)                                                         
         LA    R7,6(R7)            POINT TO FIRST CONTROL BYTE                  
*                                                                               
         CLI   0(R7),X'FF'         CHECK EOB                                    
         BE    4(R9)               NULL BLK - FORCE OVFLO READ                  
*                                                                               
         L     R1,ISPDPRKY                                                      
         B     *+8                                                              
CHKBUFF4 EX    R5,CHKSVKEY         SAVE THIS KEY MVC ISPDPRKY,ISPDKEY           
         SR    RE,RE                                                            
         IC    RE,0(R7)            GET MISSING BYTES                            
         LA    RF,0(RE,R4)         GET TO ADDRESS                               
         SR    RE,R5               GET KEY LEN TO MOVE -1.                      
         LPR   RE,RE                                                            
         J     CHKMV                                                            
*                                                                               
RETCHKMV TM    ISCMPRSW,VARIABLE   TEST FIXED/VARIABLE LENGTH RECORDS           
         BZ    *+14                                                             
         LA    RE,2(R7,RE)         POINT TO LENGTH                              
         MVC   ISRECLN,0(RE)       ALIGN AND SAVE                               
         J     CLCIT                                                            
*                                                                               
RETCLCIT BE    CHKBUFF8                                                         
         BL    CHKBUFF6            KEYARG LO                                    
         SR    RE,RE                                                            
         IC    RE,0(R7)            KEYARG HI=NEXT RECORD                        
         SH    RE,ISRECLN                                                       
         LPR   RE,RE               GET NUMBER OF BYTES PRESENT -1.              
         LA    R7,1(RE,R7)                                                      
         B     CHKBUFF4                                                         
*                                                                               
CHKBUFF6 LTR   R7,R7               SET CC NOT EQUAL                             
*                                                                               
CHKBUFF8 ST    R7,ISPDPTR          SAVE BUFFER POINTER                          
         B     8(R9)               TAKE NORMAL EXIT                             
         EJECT                                                                  
***********************************************************************         
* MOVE RECORD FROM BUFFER TO CALLER'S I/O AREA                        *         
***********************************************************************         
MOVEREC  L     R7,ISPDKEY          GET KEY ADDRESS                              
         LH    R5,ISKEYLN1                                                      
         L     RF,P2               GET USERS ADDRESS                            
         EX    R5,MOVEMVC          MVC  0(0,RF),0(R7)                           
*                                                                               
         L     R7,ISPDPTR          GET BUFFER POINTER                           
         SR    RE,RE                                                            
         IC    RE,0(R7)            GET MISSING BYTES                            
         LH    R5,ISKEYLN                                                       
         AR    RF,R5               GET TO ADDRESS                               
         SR    R5,RE               GET NUMBER OF KEY BYTES PRESENT              
         LA    R7,1(R5,R7)         GET NEXT FROM ADDRESS                        
         TM    ISCMPRSW,VARIABLE   TEST FIXED/VARIABLE LENGTH RECORDS           
         BZ    *+10                                                             
         MVC   ISRECLN,0(R7)       SAVE LENGTH OF THIS REC                      
*                                                                               
         LH    R6,ISRECLN                                                       
         SH    R6,ISKEYLN          MUST MOVE DATA LEN =RECLN-KEYLN              
*                                                                               
MOVEREC2 LA    R0,256                                                           
         CR    R6,R0                                                            
         BNH   *+20                                                             
         MVC   0(256,RF),0(R7)                                                  
         SR    R6,R0                                                            
         AR    R7,R0                                                            
         AR    RF,R0                                                            
         B     *-18                                                             
         BCTR  R6,0                                                             
         EX    R6,MOVEMVC                                                       
         TM    ISCMPRSW,VARIABLE   TEST FIXED/VARIABLE LENGTH RECORDS           
         BZ    *+12                                                             
         LA    R7,1(R6,R7)         BUMP POINTERS                                
         LA    RF,1(R6,RF)                                                      
         BR    R9                                                               
         EJECT                                                                  
***********************************************************************         
* COMPUTE SECTOR NUMBER FOR SEARCH                                    *         
***********************************************************************         
SETSCTR  STM   RE,RC,12(RD)        SAVE ALL CALLING REGS                        
         MVI   RPSSW,0                                                          
         OC    DEVSCT,DEVSCT       EXIT IF RPS NOT SUPPORTED                    
         BZR   RE                                                               
*                                                                               
         MVI   RPSSW,C'Y'                                                       
         SR    R0,R0                                                            
         ICM   R0,12,CCW4+6        GET BLOCK SIZE PLUS IRG                      
         TM    CCW4+4,X'80'                                                     
         BZ    *+8                                                              
         ICM   R0,12,CCW5+6                                                     
         ICM   R0,1,SEEK+6         GET REC NUM FROM SEEK ADDRESS                
*                                                                               
         LLC   RF,ISFUC            PICK UP INTERNAL DEVICE CODE                 
         BCTR  RF,0                                                             
         SLL   RF,1                                                             
         L     RE,=A(DEVCODE)      POINT TO THIS ENTRY                          
         AR    RE,RF                                                            
         LA    R2,SECTOR           POINT R2 TO OUTPUT FIELD                     
         ICM   R2,8,0(RE)          THEN PICK UP UCB DVC TYPE IN REG             
*                                                                               
         L     RF,X'10'(,R0)       CVT CORE LOCATION                            
         L     RF,X'E8'(,RF)       GET ROUTINE ADDRESS                          
         BASR  RE,RF               BRANCH TO SECTOR COMP ROUTINE                
*                                                                               
SETSCTRX LM    RE,RC,12(RD)        RESTORE REGS                                 
         BR    RE                                                               
*                                                                               
SETSCTRZ MVI   RPSSW,0             EXIT IF RPS NOT AVAILABLE                    
         OC    DEVSCT,DEVSCT                                                    
         BZR   RE                                                               
SETSCTR0 MVI   SECTOR,0            SET SECTOR TO ZERO                           
         MVI   RPSSW,C'Y'                                                       
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* BUILD CCW CHAIN                                                     *         
***********************************************************************         
ISBLDCCW ST    RE,SAVER                                                         
         MVI   RPSSW,0                                                          
         XC    CCWS,CCWS                                                        
         XC    IOB,IOB                                                          
         MVI   IOB,X'C3'           SET DC/CC/NOTRELATED/NOAPPENDG               
         LA    RE,ECB                                                           
         ST    RE,IOB+4                                                         
         LA    RE,CCWS                                                          
         ST    RE,IOB+16                                                        
         L     RE,ISFADCB          GET DCB ADDRESS                              
         STCM  RE,7,IOB+21                                                      
         LA    R0,SEEK                                                          
         ST    R0,CCW1                                                          
         MVI   CCW1,7              SET SEEK                                     
         MVI   CCW1+4,X'40'                                                     
         MVI   CCW1+7,6                                                         
*                                                                               
         LA    R0,SRCH                                                          
         ST    R0,CCW2                                                          
         MVI   CCW2,X'31'          SET SRCH ID EQL                              
         MVI   CCW2+4,X'40'                                                     
         MVI   CCW2+7,5                                                         
*                                                                               
         LA    R0,CCW2                                                          
         ST    R0,CCW3                                                          
         MVI   CCW3,8                                                           
         MVI   CCW3+4,X'40'                                                     
*                                                                               
         MVI   CCW4,X'06'          SET READ DATA                                
         OI    CCW4+4,X'04'        SET IDAW FLAG (BIT 37)                       
         MVC   CCW4+6(2),ISPDLN    FOR PD BUFFER LENGTH                         
*                                                                               
         LH    R0,ISPDLN           GET LENGTH                                   
         L     R1,ISBUFF1          AND DATA ADDRESS                             
         BAS   RE,SETIDAWS                                                      
         LA    R0,IDAWS                                                         
         STCM  R0,7,CCW4+1                                                      
         EJECT                                                                  
***********************************************************************         
* TRANSLATE DISK ADDRESS AT 0(R7) USING EXTENT AT 0(R8)               *         
***********************************************************************         
ISCCHH   MVC   SAVEDA,0(R7)        SAVE DISK ADDR FOR TRACE DATA                
         TM    ISFTYPE,ISFTPDOV    TEST OVERFLOW AT END OF PRIME DATA           
         BZ    *+8                                                              
         LA    R8,ISXTNT           ALWAYS POINT TO FIRST EXTENT                 
         USING EXTENTD,R8                                                       
*                                                                               
         XC    SEEK,SEEK                                                        
         CLC   0(4,R7),=F'-1'      DUMMY ADDRESS SET BY ISCPUID                 
         BNE   ISCCH1                                                           
         LH    RF,EXTCYLLO         POINT TO LOW CYL                             
         LH    RE,EXTTRKLO         POINT TO LOW TRACK                           
         B     ISCCH6                                                           
*                                                                               
ISCCH1   SR    RE,RE                                                            
         ICM   RE,3,0(R7)                                                       
         JZ    *+2                 DIE=INV DISK ADDR                            
*                                                                               
ISCCHH2  CLC   0(2,R7),EXT#TRKS    NUMBER OF TRACKS                             
         BNH   ISCCHH4                                                          
         SR    RE,RE                                                            
         ICM   RE,3,0(R7)                                                       
         SR    R0,R0                                                            
         ICM   R0,3,EXT#TRKS                                                    
         SR    RE,R0                                                            
         LA    R8,EXTLNQ(R8)                                                    
         CLI   0(R8),X'FF'                                                      
         BNE   ISCCHH2                                                          
ISEOF    MVI   P3+1,X'04'          SET EOF                                      
         B     EXCP80              RETURN TO USER                               
                                                                                
***********************************************************************         
* RE NOW CONTAINS REL TRK FOR THIS XTNT                               *         
***********************************************************************         
ISCCHH4  AH    RE,EXTTRKLO         ADD LOW TRACK                                
         BCTR  RE,0                                                             
         SRDA  RE,32                                                            
         LH    R0,ISTRKS                                                        
         DR    RE,R0               RF HAS CYL, RE HAS TRK                       
         AH    RF,EXTCYLLO         ADD LOW CYL                                  
         MVC   SEEK+6(1),2(R7)     SET REC                                      
ISCCH6   STH   RF,SEEK+2           SET CYL                                      
         STH   RE,SEEK+4           SET TRK                                      
         XC    ISFSEEK,ISFSEEK     SET SEEK ADDR IN DTF (MBBCCHHR)              
         MVC   ISFSEEK(1),EXTSEQ#                                               
         MVC   ISFSEEK+3(5),SEEK+2                                              
         MVC   ISFUC,EXTDEV#       SET SEQNUM/DEVTYP IN ISF                     
         MVC   IOB+32(8),ISFSEEK   SET SEEK ADDR IN IOB (MBBCCHHR)              
         L     RE,SAVER                                                         
         BR    RE                  RETURN                                       
         DROP  R8                                                               
         EJECT                                                                  
***********************************************************************         
* CONSTRUCT AN IDAW LIST                                              *         
* R1=START DATA ADDRESS                                               *         
* R0=START DATA LENGTH                                                *         
*                                                                     *         
* NUMBER OF IDAWS NEEDED IS MAX 2K BOUNDARIES CROSSED +1              *         
* =((DATA LEN - 1)/2048 + 1)+ 1                                       *         
*                                                                     *         
* NOTE: MOST CALLING ROUTINES WILL NOT NEED TO SET THE IDAL FLAG IN   *         
* BIT 37 IF THEY ARE ONLY MODIFYING CCW4 BECAUSE ISBLDCCW SETS IT ON. *         
* CALLER ONLY NEEDS TO SET BIT IF HE IS BUILDING CCW HIMSELF.         *         
***********************************************************************         
SETIDAWS AHI   R0,-1               SUBTRACT 1                                   
         SRL   R0,11               DIVIDE BY 2048                               
         AHI   R0,2                PLUS 2 GIVES MAX BOUNDARIES + 1              
*                                                                               
         LA    RF,IDAWS            POINT TO IDAW LIST                           
SETIDAW2 ST    R1,0(RF)            SET START ADDRESS                            
         AHI   R1,2048             AND THEN GO IN 2048 MULTS                    
         SRL   R1,11                                                            
         SLL   R1,11                                                            
         LA    RF,4(RF)                                                         
         BCT   R0,SETIDAW2                                                      
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* FILE INITIALIZATION ROUTINES                                        *         
*                                                                     *         
* OFFLINE: ACQUIRE PRIME DATA BUFFERS AND SAVE AREAS FOR KEYS         *         
*                                                                     *         
* ONLINE : DATA BUFFERS ARE ACQUIRED BY TASK, NOT BY FILE SO CANT BE  *         
*          DONE HERE                                                  *         
*                                                                     *         
* BOTH   : GETMAIN AN AREA AND BUILD INDEX. WHEN ALL CI RECORDS READ, *         
*          SAVE TTBR OF LAST IN ISCILAST.                             *         
*          THEN FIND LAST OVERFLOW RECORD AND SET ISOVLAST            *         
***********************************************************************         
INIT     LA    RF,ISOPEN           CHECK FOR SPECIAL COMMANDS                   
         C     RF,ISROUTA                                                       
         BE    ISDDS2                                                           
*                                                                               
         LA    RF,ISCLOSE                                                       
         C     RF,ISROUTA                                                       
         BE    ISDDS2                                                           
*                                                                               
         LA    RF,ISCPUID                                                       
         C     RF,ISROUTA                                                       
         BE    ISDDS2                                                           
*                                                                               
         LA    RF,ISOPOLD                                                       
         C     RF,ISROUTA                                                       
         BE    ISDDS2                                                           
*                                                                               
         LA    R1,ISXTNTIX                                                      
         OC    10(2,R1),10(R1)                                                  
         JZ    *+2                 DIE=FILE NOT OPEN                            
         OC    12(2,R1),12(R1)                                                  
         JNZ   *+2                 DIE=ALREADY INIT                             
*                                                                               
         MVC   DUB(4),=C'SSET'     SUSPEND TIMER DURING INITIALISATION          
         BAS   RE,GOTICTOC                                                      
         EJECT                                                                  
***********************************************************************         
* ALLOCATE DATA BUFFERS                                               *         
* NOTE THAT KEY AREAS ARE ALLOCATED BELOW THE LINE                    *         
* BECAUSE THEY ARE REFERENCED BY DATAMGR                              *         
***********************************************************************         
INIBUF   MVI   ONLINE,C'Y'                                                      
         ICM   R1,15,VSSB          SKIP ALLOCATION IF ONLINE                    
         BZ    INIBUF2                                                          
         OC    0(2,R1),0(R1)                                                    
         BNZ   INIBUFX                                                          
*                                                                               
INIBUF2  MVI   ONLINE,C'N'         SET NOT ONLINE FLAG                          
         OI    ISCMPRSW,X'20'      SET BEEN THROUGH INIT FLAG                   
*                                                                               
         LH    R0,ISPDLN                                                        
         TM    ISCMPRSW,X'40'      TEST ONE OR TWO BUFFERS                      
         BO    *+6                 FLAG IS ON IF ONLY 1                         
         AR    R0,R0               ELSE DOUBLE LENGTH                           
         GETMAIN RU,LV=(0),LOC=(ANY,ANY),STARTBDY=11                            
         LTR   RF,RF                                                            
         JNZ   *+2                 DIE=INVALID GETMAIN                          
         ST    R1,ISBUFF1                                                       
         AH    R1,ISPDLN                                                        
         TM    ISCMPRSW,X'40'      TEST ONE OR TWO BUFFERS                      
         BO    *+8                                                              
         ST    R1,ISBUFF2          SET ADDR OF SECOND BUFFER                    
         BCTR  R1,0                                                             
         ST    R1,ISBUFF1X                                                      
         EJECT                                                                  
INIBUF10 LH    R0,ISKEYLN                                                       
         MHI   R0,3                                                             
         GETMAIN RU,LV=(0)                                                      
         LTR   RF,RF                                                            
         JNZ   *+2                 DIE=INVALID GETMAIN                          
         ST    R1,ISPDKEY                                                       
         AH    R1,ISKEYLN                                                       
         ST    R1,ISPDPRKY                                                      
         AH    R1,ISKEYLN                                                       
         ST    R1,ISTIHIKY                                                      
INIBUFX  EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
* SET DEVICE DEPENDENT DATA IN DTF                                    *         
***********************************************************************         
SETDTF   MVC   ISTRKS,DEVTRKS      SET TRKS/CYL                                 
         SR    R0,R0                                                            
         ICM   R0,3,ISPDLN                                                      
         BAS   R9,SETDTFR          GET PD RECS/TRK                              
         STH   R0,ISHIPD                                                        
*                                                                               
         MH    R0,ISKEYLN          COMPUTE LEN OF TI REC                        
         LH    R1,ISTRKS                                                        
         BCTR  R1,0                                                             
         MR    R0,R0                                                            
         STH   R1,ISTILN           ISTILN=ISHIPD*ISKEYLN*(ISTRKS-1)             
         SRL   R1,1                                                             
         CH    R1,ISPDLN                                                        
         JNL   *+2                 DIE=INV BLOCK SIZE                           
*                                                                               
         LH    R0,ISIXLN           COMPUTE LEN OF CI REC                        
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         ICM   RF,3,ISTILN                                                      
         DR    RE,R0                                                            
         MR    RE,R0                                                            
         STH   RF,ISCILN           ISCILN=(ISTILN/ISIXLN)*ISIXLN                
*                                                                               
         SR    R0,R0                                                            
         ICM   R0,3,ISTILN                                                      
         BAS   R9,SETDTFR          GET TI RECS/TRK                              
*                                                                               
         STH   R0,ISHITI                                                        
         SR    R0,R0                                                            
         ICM   R0,3,ISCILN                                                      
         BAS   R9,SETDTFR          GET CI RECS/TRK                              
         STH   R0,ISHICI                                                        
         EJECT                                                                  
***********************************************************************         
* ALLOCATE EXTENDED IOAREAS IF NECESSARY NOW THAT BUFFER SIZES FIXED  *         
***********************************************************************         
         CLI   ONLINE,C'Y'                                                      
         BE    SETDTF10                                                         
         SR    R0,R0                                                            
         ICM   R0,1,ISFBUFFS       TEST EXTENDED IOAREAS REQUESTED              
         BZ    SETDTF10            NO                                           
         LA    R1,ISTILN           LENGTH IS BIGGER OF ISTILN/ISPDLN            
         CLC   ISTILN,ISPDLN                                                    
         BH    *+8                                                              
         LA    R1,ISPDLN                                                        
         MH    R0,0(R1)                                                         
         AH    R0,=AL2((ISXMAX+1)*ISXTBLN)  PLUS ROOM FOR TABLE                 
*                                                                               
         GETMAIN RU,LV=(0),LOC=(ANY,ANY)                                        
         LTR   RF,RF                                                            
         JNZ   *+2                 DIE=INVALID GETMAIN                          
*                                                                               
         ST    R1,ISFXTAB          SAVE TABLE ADDRESS                           
         USING ISXTABD,R1                                                       
         XC    0(ISXTBLN,R1),0(R1) CLEAR FIRST ENTRY                            
         LA    R1,ISXTBLN(R1)      AND POINT TO NEXT                            
*                                                                               
         LA    RE,(ISXMAX*ISXTBLN)(R1) POINT TO FIRST IOAREA                    
         LLC   RF,ISFBUFFS         GET NUMBER OF IOAREAS                        
         LA    R0,ISXMAX                                                        
         CR    RF,R0                                                            
         BNH   *+6                                                              
         LR    RF,R0                                                            
*                                                                               
         SR    R0,R0                                                            
         ICM   R0,3,ISPDLN                                                      
         CLC   ISPDLN,ISTILN                                                    
         BH    *+8                                                              
         ICM   R0,3,ISTILN                                                      
*                                                                               
SETDTF2  XC    ISXTABD(ISXTBLN),ISXTABD CLEAR THE ENTRY                         
         ST    RE,ISXTBADR         SET IOAREA ADDRESS                           
         AR    RE,R0               BUMP TO NEXT                                 
         LA    R1,ISXTBLN(R1)      POINT TO NEXT TABLE ENTRY                    
         BCT   RF,SETDTF2                                                       
                                                                                
***********************************************************************         
* ALLOCATE TRACK INDEX BUFFER IF OFFLINE                              *         
***********************************************************************         
SETDTF10 XC    ISTITRK,ISTITRK                                                  
         XC    ISTIBUFF,ISTIBUFF   MAKE SURE STARTS ZERO                        
         CLI   ONLINE,C'Y'                                                      
         BE    SETDTF12                                                         
         CLI   ISFBUFFS,0          TEST EXTENDED IOAREAS                        
         BNE   SETDTF12            YES - NO TIBUFF NEEDED                       
         LH    R0,ISTILN                                                        
         GETMAIN RU,LV=(0),LOC=(ANY,ANY)                                        
         LTR   RF,RF                                                            
         JNZ   *+2                 DIE=INVALID GETMAIN                          
         ST    R1,ISTIBUFF                                                      
*                                                                               
SETDTF12 B     SETDTFX                                                          
         EJECT                                                                  
***********************************************************************         
* SUBROUTINE COMPUTES RECS/TRK FOR RECLEN IN R0                       *         
***********************************************************************         
SETDTFR  LTR   R0,R0                                                            
         JZ    *+2                 DIE=REC LEN ZERO                             
         C     R0,=F'32767'                                                     
         JH    *+2                 DIE=REC LEN > 32K                            
*&&DO                                                                           
         CLI   DEVSUB,5            3390 IS ONLY VALID CURRENT DEVICE            
         BE    SET3390                                                          
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         ICM   RF,3,DEVCAPL                                                     
         OC    DEVMOD,DEVMOD                                                    
         BZ    *+16                                                             
         AH    R0,DEVMOD           ADJUST FOR MODULO                            
         SRL   R0,5                                                             
         SLL   R0,5                                                             
         AH    R0,DEVIRG           ADJUST FOR IRG                               
         DR    RE,R0                                                            
         LTR   R0,RF               RETURN RECS/TRK IN R0                        
         BNZR  R9                                                               
         DC    H'0'                DIE=REC LEN > TRK                            
*&&                                                                             
                                                                                
***********************************************************************         
* 3390 COMPUTATION ROUTINES                                           *         
* DN=(DL+6+231)/232                                                   *         
* D=9+(DL+6*DN)+6+33)/34                                              *         
* SPACE = 10+D                                                        *         
***********************************************************************         
SET3390  LR    RE,R0               GET RECLEN                                   
         LA    RE,237(RE)          RE=DL+6+231                                  
         SRDL  RE,32                                                            
         D     RE,=F'232'          RF=(DL+237)/232                              
         MHI   RF,6                RF=6*DN                                      
         AR    RF,R0               RF=DL+6*DN                                   
         LA    RF,39(RF)                                                        
         SR    RE,RE                                                            
         D     RE,=F'34'                                                        
         LA    RF,19(RF)           GIVES SPACE REQUIRED IN RF                   
         SR    R0,R0                                                            
         LA    R1,1729             1729=NUMBER OF CELLS/TRACK                   
         DR    R0,RF               DIVIDE BY SPACE/RECORD                       
         LR    R0,R1               GIVES MAX RECS/TRACK (TRUNCATED)             
         BR    R9                                                               
*                                                                               
SETDTFX  EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
* SPLIT THE EXTENT MATRIX INTO INDEX AND DATA PARTS                   *         
* UNLESS DTF SPECIFIES TYPE = NEW (CONTINUOUS EXTENTS)                *         
***********************************************************************         
         TM    ISFTYPE,ISFTPDOV    TEST OVERFLOW AT END OF PRIME DATA           
         BZ    INXT0                                                            
         LA    R1,ISXTNT                                                        
         BAS   R9,ISSETHI                                                       
         B     INITC                                                            
*                                                                               
INXT0    LA    R0,MAXXTNTS-1       SET MAX EXTENTS FOR PD                       
         LA    R1,ISXTNTIX+14      POINT TO PD XTNTS                            
         L     RE,ISBUFF1          MOVE AREA                                    
*                                                                               
INXT2    MVC   0(15,RE),0(R1)      MOVE 14 BYTE XTNT + 1                        
         LA    R1,14(R1)                                                        
         LA    RE,14(RE)                                                        
         CLI   0(R1),X'FF'         TEST FOR END                                 
         BE    INXT4                                                            
         BCT   R0,INXT2                                                         
         DC    H'0'                DIE=INV XTNT MATRIX                          
*                                                                               
INXT4    MVI   ISXTNTIX+14,X'FF'   SET END FLAG FOR INDEX XTNT                  
                                                                                
***********************************************************************         
* NOW MOVE PD EXTENT DATA BACK                                        *         
***********************************************************************         
         LA    R1,ISXTNTPD                                                      
         L     RE,ISBUFF1                                                       
*                                                                               
INXT6    MVC   0(15,R1),0(RE)      MOVE 14 BYTES XTNT + 1                       
         LA    RE,14(RE)                                                        
         LA    R1,14(R1)                                                        
         CLI   0(RE),X'FF'                                                      
         BNE   INXT6                                                            
                                                                                
***********************************************************************         
* SET HI RELTRK IN EACH MATRIX ENTRY                                  *         
***********************************************************************         
         LA    R1,ISXTNTIX                                                      
         BAS   R9,ISSETHI                                                       
         LA    R1,ISXTNTPD                                                      
         BAS   R9,ISSETHI                                                       
         B     INITC                                                            
*                                                                               
ISSETHI  SR    RE,RE                                                            
ISSETHI2 LH    R7,2(R1)            GET LOW CYL                                  
         MH    R7,ISTRKS           X TRKS/CYL                                   
         AH    R7,4(R1)            ADD LOW TRK                                  
         LH    R8,6(R1)            GET HI CYL                                   
         MH    R8,ISTRKS           X TRKS/CYL                                   
         AH    R8,8(R1)            ADD HI TRK                                   
         SR    R8,R7                                                            
         LA    RE,1(R8,RE)                                                      
         STH   RE,12(R1)                                                        
         LA    R1,14(R1)                                                        
         CLI   0(R1),X'FF'                                                      
         BNE   ISSETHI2                                                         
*                                                                               
         SRA   RE,16               TEST IF > 64K                                
         BZR   R9                                                               
         DC    H'0'                DIE=NUM TRKS > 64K                           
         EJECT                                                                  
***********************************************************************         
* BUILD CYLINDER INDEX                                                *         
* GETMAIN A LARGE AREA, STARTING ON A PAGE BOUNDARY                   *         
* CONSTRUCT INDEX, THEN FREEMAIN EXCESS STORAGE                       *         
***********************************************************************         
INITC    L     R0,MAXCISZ                                                       
         GETMAIN RC,LV=(0),LOC=(ANY,ANY),BNDRY=PAGE                             
         LTR   RF,RF                                                            
         BZ    INITC1                                                           
         WTO   'ISDDS - HIGH STORAGE GETMAIN FAILED'                            
         L     R0,MAXCISZ                                                       
         S     R0,=A(1024*32)      REDUCE REQUEST BY 32K                        
         ST    R0,MAXCISZ                                                       
         BP    INITC               AND TRY AGAIN                                
         DC    H'0'                DIE=XA GETMAIN ERR                           
*                                                                               
INITC1   ST    R1,ISINDX                                                        
         B     INITC2                                                           
MAXCISZ  DC    A(256*1024)         MAX CI SIZE IS 256K                          
*                                                                               
INITC2   LA    RE,1                READ CYLINDER INDEX RECORDS                  
         SLL   RE,16                                                            
         ST    RE,DUB                                                           
         MVI   DUB+2,1                                                          
         SR    R6,R6               CLEAR SPACE COUNTER                          
*                                                                               
INITC4   LA    R7,DUB                                                           
         LA    R8,ISXTNTIX                                                      
         BAS   RE,ISBLDCCW                                                      
*                                                                               
         MVC   CCW4+6(2),ISCILN    NEED TO BUILD NEW IDAWS                      
         LH    R0,CCW4+6           GET LENGTH                                   
         L     R1,ISBUFF1          AND DATA ADDRESS                             
         BAS   RE,SETIDAWS                                                      
         LA    R0,IDAWS                                                         
         STCM  R0,7,CCW4+1                                                      
*                                                                               
         BAS   RE,INIEXCP                                                       
         BAS   RE,EXCPCHK                                                       
         TM    ERRF+1,X'04'        TEST EOF RECORD                              
         BO    INITCX                                                           
         TM    ERRF+1,X'08'        TEST N/F                                     
         BZ    INITC12                                                          
         LTR   R6,R6               TEST FIRST RECORD                            
         BNZ   INITC12                                                          
         MVC   ISCILAST,DUB                                                     
         B     INITCX              THERE IS NO CYLINDER INDEX                   
*                                                                               
INITC12  LH    R1,ISIXLN           GET INDEX REC LEN                            
         BCTR  R1,0                                                             
         L     RE,ISBUFF1          GET ADDRESS OF REC                           
         LH    RF,ISCILN                                                        
         AR    RE,RF               POINT PAST END                               
         B     INITC16                                                          
*                                                                               
INITC14  SH    RF,ISIXLN           DECREMENT LENGTH                             
*                                                                               
INITC16  SH    RE,ISIXLN           DECREMENT ADDRESS                            
         EX    R1,INITOC           OC 0(0,RE),0(RE)                             
         BZ    INITC14                                                          
*                                                                               
         LR    R1,RE               RE=A(LAST ENTRY), RF=DATA LENGTH             
         AH    R1,ISKEYLN          POINT TO RELTRK IN LAST ENTRY                
         MVC   ISPDLAST(2),0(R1)   SAVE ADDRESS OF LAST TI TRK                  
*                                                                               
         L     R0,ISINDX           GET INDEX START                              
         AR    R0,R6               + LEN GIVES TO ADDRESS                       
         LR    R1,RF               SET TO LEN                                   
         L     RE,ISBUFF1          SET FROM ADDRESS (RF='FROM LEN')             
*                                                                               
         AR    R6,RF               INCREMENT LENGTH                             
         C     R6,MAXCISZ          TEST EXCEEDED MAX CI SIZE                    
         BH    INITERR1                                                         
         MVCL  (R0),(RE)                                                        
*                                                                               
INITC20  MVC   ISCILAST,DUB        SAVE ADDRESS OF LAST CI REC                  
         SR    RE,RE                                                            
         IC    RE,DUB+2                                                         
         LA    RE,1(RE)                                                         
         STC   RE,DUB+2                                                         
         CH    RE,ISHICI                                                        
         BNH   INITC4                                                           
*                                                                               
         LH    RE,DUB              BUMP TO FRST REC ON NEXT TRACK               
         LA    RE,1(RE)                                                         
         SLL   RE,16                                                            
         ST    RE,DUB                                                           
         MVI   DUB+2,1                                                          
         B     INITC4                                                           
*                                                                               
INITOC   OC    0(0,RE),0(RE)                                                    
*                                                                               
INITCX   STH   R6,ISINDXLN                                                      
         C     R6,=F'65536'        TEST LEN FITS IN HALFWORD                    
         BL    INITCX2                                                          
         AHI   R6,7                ROUND UP                                     
         SRL   R6,3                                                             
         STH   R6,ISINDXLN                                                      
         OI    ISINDXTY,X'02'      SET FLAG FOR SPACE IN DBLWDS                 
                                                                                
***********************************************************************         
* NOW CONSTRUCT A TABLE OF THE CYL INDEX ON 4K BOUNDARIES             *         
* MAP CONTAINS A(LOW KEY ON PAGE) AND HIGH KEY ON PAGE                *         
* SO TABLE ENTRIES ARE KEYLEN+4 LONG                                  *         
* WHEN TABLE IS COMPLETE, FREEMAIN UNUSED STORAGE                     *         
***********************************************************************         
INITCX2  L     R5,ISINDX           POINT TO START OF INDEX AREA                 
         SR    R6,R6                                                            
         ICM   R6,3,ISINDXLN       GET LENGTH OF INDEX                          
         TM    ISINDXTY,X'02'      TEST LEN IN DBLWDS                           
         BZ    *+8                                                              
         SLL   R6,3                                                             
         LA    R7,0(R5,R6)         POINT TO END                                 
         LA    R7,4095(R7)         NOW ROUND TO PAGE BNDRY START ADDR           
         SRL   R7,12                                                            
         SLL   R7,12                                                            
         ST    R7,ISINDX           AND SET INDEX TABLE ADDRESS                  
         LH    RE,ISKEYLN          GET KEY LENGTH                               
         LA    RE,4(RE)            +4 FOR ADDRESS                               
         ST    RE,0(R7)            GIVES ENTRY LENGTH                           
         LA    R7,8(R7)            POINT TO FIRST ENTRY                         
*                                                                               
         LR    R0,R5               GET START OF INDEX                           
                                                                                
***********************************************************************         
* NOW INCREMENT THROUGH INDEX TILL ADDRESS > PAGE BOUNDARY IN R0      *         
***********************************************************************         
INITCX4  ST    R5,0(R7)            SET A(FIRST KEY ON PAGE) IN TABLE            
         AHI   R0,4096             SET NEXT PAGE BOUNDARY                       
*                                                                               
INITCX6  CLI   0(R5),X'FF'         TEST END OF INDEX                            
         BE    INITCX10            YES - DONE                                   
         AH    R5,ISIXLN           ADD INDEX RECORD SIZE                        
         CR    R5,R0               TEST PAST PAGE BOUNDARY                      
         BNH   INITCX6             NO                                           
*                                                                               
         SH    R5,ISIXLN           BACK UP TO PREVIOUS                          
         LH    RE,ISKEYLN1         GET KEYLEN - 1                               
         EX    RE,*+8              MOVE HI KEY IN PAGE TO TABLE                 
         B     *+10                                                             
INITCXMV MVC   4(0,R7),0(R5)                                                    
*                                                                               
         AH    R5,ISIXLN           POINT TO NEXT ENTRY AGAIN                    
         LA    R7,5(RE,R7)         BUMP TABLE POINTER BY KEYLEN + 4             
         B     INITCX4                                                          
*                                                                               
INITCX10 LH    RE,ISKEYLN1         GET KEYLEN-1                                 
         EX    RE,INITCXMV         MOVE HI KEY TO TABLE                         
         LA    R7,4(RE,R7)         POINT TO NEW END OF TABLE-1                  
*                                                                               
         L     RE,ISINDX           GET START OF TABLE ADDRESS                   
         ST    R7,4(RE)            AND SET IN TABLE                             
                                                                                
***********************************************************************         
* RETURN UNUSED STORAGE VIA FREEMAIN                                  *         
***********************************************************************         
         LA    R7,1(R7)            POINT TO END OF TABLE                        
         LA    R7,4095(R7)         ROUND TO PAGE BOUNDARY                       
         SRL   R7,12                                                            
         SLL   R7,12                                                            
         LR    R1,R7               GIVES START OF FREEMAIN                      
*                                                                               
         L     RE,ISINDX           POINT TO START OF TABLE                      
         L     R0,8(RE)            GET A(FIRST PAGE)                            
         A     R0,MAXCISZ          + GETMAIN LENGTH                             
         SR    R0,R1               - FREEMAIN START GIVES LENGTH                
         FREEMAIN RC,A=(1),LV=(0)                                               
         LTR   RF,RF                                                            
         JNZ   *+2                 DIE=INVALID FREEMAIN                         
         EJECT                                                                  
***********************************************************************         
* OVERFLOW STARTS ON NEXT TRACK                                       *         
***********************************************************************         
INITOV   MVC   DUB+4(4),DUB        SAVE TRACK ADDRESS OF CI EOF REC             
         LH    RE,DUB              NEXT TRACK IS LOW BOUND OF BINSRCH           
         LA    RE,1(RE)                                                         
         STH   RE,LOARG                                                         
         ICM   RE,3,ISXTNTIX+12    NOW CAN FIND EOF WHEN FILE IS FULL           
         LA    RE,1(RE)                                                         
         STH   RE,HIARG            SET HI TRACK + 1 AS HIGH BOUND               
*                                                                               
         TM    ISFTYPE,ISFTPDOV    TEST OVERFLOW AT END OF PRIME DATA           
         BZ    INITOV10                                                         
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,3,ISPDLAST       GET LAST TRACK WITH DATA                     
         LA    RF,1(RF)            BUMP TO NEXT TRACK                           
         STH   RF,LOARG                                                         
*                                                                               
         LA    R1,ISXTNT                                                        
         XR    RE,RE                                                            
*                                                                               
INITOV02 ICM   RE,3,12(R1)         FIND LAST XTNT ENTRY                         
         STH   RE,HIARG                                                         
         LA    R1,14(R1)                                                        
         CLI   0(R1),X'FF'                                                      
         BNE   INITOV02                                                         
*                                                                               
INITOV04 ICM   RE,3,HIARG          NOW CAN FIND EOF WHEN FILE IS FULL           
         LA    RE,1(RE)                                                         
         STH   RE,HIARG            SET HI TRACK + 1 AS HIGH                     
*                                                                               
INITOV10 XR    R0,R0                                                            
         ICM   R0,3,LOARG                                                       
         XR    RE,RE                                                            
         ICM   RE,3,HIARG                                                       
         AR    R0,RE                                                            
         SRL   R0,1                R0=MIDPOINT TRACK                            
         STH   R0,CURARG                                                        
         CLC   LOARG,CURARG        WHEN LOARG=CURARG, WE'VE FOUND TRACK         
         BE    INITOV14                                                         
*                                                                               
         MVI   CURARG+2,1          LOOK AT FIRST RECORD ON TRACK                
         LA    R7,CURARG                                                        
         LA    R8,ISXTNTIX                                                      
         TM    ISFTYPE,ISFTPDOV    TEST OVERFLOW AT END OF PRIME DATA           
         BO    INITOV12                                                         
         CLC   CURARG(2),12(R8)    TEST BEYOND INDEX XTNT                       
         BH    INITOV14                                                         
*                                                                               
INITOV12 BAS   RE,ISBLDCCW                                                      
*                                                                               
         BAS   RE,INIEXCP                                                       
         BAS   RE,EXCPCHK                                                       
         LA    RE,LOARG            IF DATA, SET LOARG=CURARG                    
         TM    ERRF+1,X'08'        TEST NOT FOUND                               
         BZ    *+8                 RECORD WAS FOUND                             
         LA    RE,HIARG                                                         
         MVC   0(2,RE),CURARG                                                   
         B     INITOV10                                                         
*                                                                               
INITOV14 XC    DUB(4),DUB                                                       
         MVC   DUB(2),CURARG                                                    
*                                                                               
INITOV16 SR    RE,RE                                                            
         IC    RE,DUB+2            ADD 1 TO REC                                 
         LA    RE,1(RE)                                                         
         STC   RE,DUB+2                                                         
         CH    RE,ISHIPD           TEST GT HI REC                               
         BH    INITOV22            YES - SET REC 0 NEXT TRK                     
*                                                                               
INITOV18 LA    R7,DUB                                                           
         LA    R8,ISXTNTIX                                                      
         BAS   RE,ISBLDCCW                                                      
*                                                                               
         BAS   RE,INIEXCP                                                       
         BAS   RE,EXCPCHK                                                       
         TM    ERRF+1,X'08'        TEST NOT FOUND                               
         BNO   INITOV16                                                         
*                                                                               
INITOV20 IC    RE,DUB+2            SET PREVIOUS REC- THIS TRK                   
         BCTR  RE,0                                                             
         STC   RE,DUB+2                                                         
         B     INITOVX                                                          
*                                                                               
INITOV22 LH    RE,DUB              SET REC 0 OF NEXT TRK                        
         LA    RE,1(RE)                                                         
         SLL   RE,16                                                            
         ST    RE,DUB                                                           
*                                                                               
INITOVX  MVC   ISOVLAST,DUB                                                     
         XC    ISPDDA(12),ISPDDA   SET BUFFER CONTENTS NOT VALID                
         MVC   DUB(4),=C'RSET'     RESTART TIMER AT END OF INITIALISE           
         BAS   RE,GOTICTOC                                                      
INITOVXX B     ISDDS2                                                           
*                                                                               
INITERR1 OI    ISINDXTY,X'81'      SET SWITCHED FROM CYL TO MST INDEX           
         B     INITC               GO TRY AGAIN                                 
*                                                                               
INITERR2 XC    ISCILAST,ISCILAST   SET NOT INITIALISED                          
         MVC   DUB(4),=C'RSET'     RESTART TIMER AT END OF INITIALISE           
         BAS   RE,GOTICTOC                                                      
INITER2X DC    H'0'                DIE=MI BUFF SPACE                            
                                                                                
***********************************************************************         
* TIC TOC TIMMER POP                                                            
***********************************************************************         
GOTICTOC ICM   RF,15,VTICTOC                                                    
         BZR   RE                  NOT INCLUDED SO RETURN                       
         LR    R0,RE               SAVE RETURN ADDRESS                          
         LA    R1,DUB              TYPE OF CALL                                 
         BASSM RE,RF               CALL TICTOC IN APPROPRIATE MODE              
         LR    RE,R0               RESTORE                                      
         SAM31                     BACK TO 31 BIT MODE                          
         BR    RE                  RETURN TO CALLER                             
                                                                                
***********************************************************************         
* OPEN A FILE                                                         *         
***********************************************************************         
ISOPOLD  MVI   BYTE,1              FLAG OPEN DISP=OLD                           
         B     *+8                                                              
ISOPEN   MVI   BYTE,0                                                           
*                                  CALL ISOPEN AT INDIRECT ADDRESS              
         BRAS  RE,AISOPEN                                                       
         B     EXIT                                                             
                                                                                
***********************************************************************         
* CLOSE A FILE                                                        *         
***********************************************************************         
ISCLOSE  BRAS  RE,AISCLOSE         CALL ISCLOSE AT INDIRECT ADDRESS             
         B     EXIT                                                             
                                                                                
***********************************************************************         
* EXECUTE CHANNEL PROGRAM                                             *         
***********************************************************************         
EXCP     ST    RE,SAVER                                                         
         LA    R0,CCW1                                                          
         ST    R0,IOB+16                                                        
         CLI   RPSSW,0             INSERT RPS CCW IF ACTIVE                     
         BE    EXCP10                                                           
         LA    R0,CCW0                                                          
         ST    R0,IOB+16                                                        
         MVC   CCW0,CCW1                                                        
         XC    CCW1,CCW1                                                        
         LA    R0,SECTOR                                                        
         ST    R0,CCW1                                                          
         MVI   CCW1,X'23'          SET OPCD=SET SECTOR                          
         MVI   CCW1+4,X'40'        SET CC                                       
         MVI   CCW1+7,1            SET LEN=1                                    
         MVI   RPSSW,0                                                          
*                                                                               
EXCP10   L     RF,IOB+16           REMOVE LONG SEEK IF PRESENT                  
         CLI   0(RF),X'07'                                                      
         BNE   EXCP12                                                           
         LA    RF,8(RF)                                                         
         ST    RF,IOB+16                                                        
*                                                                               
EXCP12   CLI   CCW4,6              TEST READ DATA                               
         BNE   EXCP14                                                           
         TM    CCW4+4,X'10'        TEST SUPRESS DATA MOVE ON READ               
         BO    EXCP14              YES - THIS READ DOES NOTHING                 
         TM    ISFXTAB,X'80'       TEST EXTENDED BUFFERS DISABLED               
         BO    EXCP14              YES - HAD TOO MANY MISSES                    
         OC    ISFXTAB,ISFXTAB     TEST EXTENDED BUFFERS IN USE                 
         BZ    EXCP14                                                           
         BRAS  RE,ISX                                                           
         BNE   EXCP14              IF CC NEQ, DID NOT FIND RECORD               
         L     RE,SAVER                                                         
         BR    RE                                                               
*                                                                               
EXCP14   XC    ECB,ECB             EXECUTE CHANNEL PROGRAM                      
         EXCP  IOB                                                              
         MVI   BUFFOK,0            CLEAR BUFFER OK FLAGS                        
         LA    R1,ECB                                                           
         ICM   RF,15,VADWAIT       CALL V(ADWAIT) IF PRESENT                    
         BZ    EXCPWAIT                                                         
         BASSM RE,RF               BRANCH AND SET TO 24-BIT MODE                
         SAM31                                                                  
*                                                                               
EXCPWAIT WAIT  ECB=(1)             WAIT FOR I/O COMPLETION                      
         B     EXCP2                                                            
*                                                                               
EXCPCHK  ST    RE,SAVER                                                         
         SR    R0,R0                                                            
*                                                                               
EXCP2    XC    ERRF,ERRF                                                        
         CLI   IOB+4,X'7F'         TEST NORMAL COMPLETION                       
         BE    EXCP60                                                           
         CLI   IOB+4,X'41'         TEST PERMANENT ERROR                         
         BE    EXCP20                                                           
         CLI   IOB+4,X'42'         TEST OUTSIDE EXTENTS                         
         BE    EXCP50                                                           
         DC    H'0'                DIE=EXCP COMPLETION                          
*                                                                               
EXCP20   TM    IOB+12,X'02'        TEST CSW UNIT STAT FOR UNIT CHECK            
         BO    EXCP30                                                           
         TM    IOB+12,X'01'        TEST CSW UNIT STAT FOR UNIT EXCPN            
         BZ    EXCP22                                                           
         MVI   ERRF+1,X'04'        SET EOF                                      
         B     EXCP60                                                           
*                                                                               
EXCP22   TM    IOB+12,X'FF'        TEST CSW UNIT STAT                           
         JNZ   *+2                 DIE=CSW UNIT STATUS                          
         TM    IOB+13,X'FF'        TEST CSW CHNL STAT                           
         BZ    EXCP30                                                           
         MVI   ERRF,X'80'          SET UNRECOVERABLE ERROR                      
         B     EXCP60                                                           
*                                                                               
EXCP30   TM    IOB+2,X'04'         TEST SENSE BYTE 0 ON UNIT CHECK              
         BO    EXCP40                                                           
         CLI   IOB+2,0                                                          
         BNE   EXCP40                                                           
*                                                                               
EXCP32   TM    IOB+3,X'08'         TEST SENSE BYTE 1 ON UNIT CHECK              
         BO    EXCP34                                                           
         B     EXCP40              UNRECOVERABLE ERROR                          
EXCP34   MVI   ERRF+1,X'08'        SET NO RECORD FOUND                          
         B     EXCP60                                                           
*                                                                               
EXCP40   MVI   ERRF,X'80'          SET UNRECOVERABLE ERROR                      
         B     EXCP60                                                           
*                                                                               
EXCP50   MVI   ERRF,X'01'          SET REFERENCE OUTSIDE EXTENTS                
         B     EXCP60                                                           
*                                                                               
EXCP60   L     RE,SAVER            RETURN TO CALLER IF EXCP CHECK               
         LTR   R0,R0                                                            
         BNZ   *+12                                                             
         OC    ERRF,ERRF                                                        
         BR    RE                                                               
         OC    P3(2),ERRF          SET ERROR BITS AND EXIT IF ERROR             
         BZR   RE                                                               
*                                                                               
EXCP70   CLI   CCW4,X'5E'          TEST ERROR ON FULL TRK READ                  
         BNE   EXCP80              NO - PASS IT ON                              
         MVI   P6,0                SUPPRESS FOR RETRY                           
         XC    P3(2),P3            CLEAR ERROR FLAG                             
         LTR   RB,RB               SET CC NEQ                                   
         BR    RE                  AND RETURN ERROR TO CALLER                   
*                                                                               
EXCP80   XC    ISPDDA(8),ISPDDA    SET BUFF CONTENTS NOT VALID                  
         XC    ISPDPTR,ISPDPTR     CLEAR SEQ READ POINTER                       
         ICM   R1,15,ISFXTAB       TEST FOR EXTENDED BUFFERS                    
         BZ    EXIT                                                             
         XC    0(ISXTBLN,R1),0(R1)                                              
         B     EXIT                EXIT MODULE                                  
                                                                                
INIEXCP  ST    RE,SAVER                                                         
         L     RF,IOB+16           REMOVE LONG SEEK IF PRESENT                  
         CLI   0(RF),X'07'                                                      
         BNE   *+12                                                             
         LA    RF,8(RF)                                                         
         ST    RF,IOB+16                                                        
         XC    ECB,ECB                                                          
         EXCP  IOB                                                              
         WAIT  ECB=ECB                                                          
         L     RE,SAVER                                                         
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* LITERALS / EQUATES                                                  *         
***********************************************************************         
VARIABLE EQU   X'80'               ISCMPRSW FLAG FOR V/L RECORDS                
MAXXTNTS EQU   16                  MAX NUM OF EXTENTS                           
*                                                                               
         LTORG                                                                  
                                                                                
***********************************************************************         
* CONSTANTS                                                           *         
***********************************************************************         
VSSB     DC    V(SSB)                                                           
VTICTOC  DC    V(TICTOC)                                                        
VADWAIT  DC    V(ADWAIT)                                                        
ONLINE   DC    C' '                SET TO C'Y' IF ONLINE                        
         DC    X'00'               N/D                                          
MAXIOL   DC    AL2(56664)          MAX 3390 RECORD LENGTH                       
BLKINDEX DC    XL4'FFFFFFFF'       PD BLOCK INDEX INDICATOR IN LINK             
         EJECT                                                                  
***********************************************************************         
* IBM RECOMENDED CODE                                                 *         
***********************************************************************         
         USING SVBLKD,R3                                                        
READICLC CLC   0(0,R3),4(R4)                                                    
INSBCLC  CLC   SVHIKEY(0),0(R1)                                                 
READXC   XC    0(0,R4),0(R4)                                                    
SRCHCLC  CLC   0(0,R3),0(R4)                                                    
MOVEMVC  MVC   0(0,RF),0(R7)                                                    
CHKCLC   CLC   0(0,R3),0(R4)       CLC  KEYARG,WORK                             
CHKMVKEY MVC   0(0,RF),1(R7)                                                    
CHKSVKEY MVC   0(0,R1),0(R4)       MVC  ISPDPRKY,ISPDKEY                        
SWAPXC37 XC    0(0,R3),0(R7)                                                    
SWAPXC73 XC    0(0,R7),0(R3)                                                    
SWAPMVC  MVC   0(0,RF),0(R3)                                                    
ISADDOC  OC    0(0,RF),0(RF)                                                    
         DROP  R3                                                               
                                                                                
CHKMV    EQU   *                                                                
         BRCL  0,CHKMV             OPCODE C04                                   
         BRCL  0,CHKMV                                                          
         EX    RE,CHKMVKEY         MOVE TO WORK                                 
         J     RETCHKMV                                                         
CLCIT    EQU   *                                                                
         BRCL  0,CLCIT                                                          
         BRCL  0,CLCIT                                                          
         EX    R5,CHKCLC           TEST RIGHT KEY                               
         J     RETCLCIT                                                         
         EJECT                                                                  
***********************************************************************         
* INDEX SEQUENTIAL ROUTINES TABLE                                     *         
***********************************************************************         
ISROUTS  DC    A(0)                00                                           
         DC    A(ISREAD)           01                                           
         DC    A(ISRDSEQ)          02                                           
         DC    A(ISADD)            03                                           
         DC    A(ISERASE)          04                                           
         DC    A(ISWRITE)          05                                           
         DC    A(ISCPUID)          06                                           
         DC    A(ISFNDEOF)         07                                           
         DC    A(ISOPEN)           08                                           
         DC    A(ISCLOSE)          09                                           
         DC    A(ISOPOLD)          0A                                           
         DC    A(ISRDFLS)          0B                                           
         DC    A(ISRSFLS)          0C                                           
         DC    A(ISCLEAR)          0D                                           
         DC    9A(0)               0E-??                                        
                                                                                
***********************************************************************         
* DEVICE PHYSICAL CHARACTERISTICS                                     *         
*                                                                     *         
* X DEVICE TYPE - 1=CKD,2=RBA                                         *         
* X DEVICE SUB TYPE - 1=3340,2=3350,3=3375,4=3380,5=3390              *         
* H DEVICE TRACKS PER CYLINDER                                        *         
* H DEVICE INTER RECORD GAP                                           *         
* H DEVICE TRACK CAPACITY LOGICAL                                     *         
* H DEVICE TRACK CAPACITY PHYSICAL                                    *         
* H DEVICE RECORD ZERO OVERHEAD                                       *         
* H DEVICE SECTORS PER TRACK                                          *         
* H DEVICE MODULUS FACTOR                                             *         
***********************************************************************         
DEVCODE  DS    0H                  XL1=MVS,XL1=INTERNAL                         
         DC    X'0A01'             3340                                         
         DC    X'0B02'             3350                                         
         DC    X'0C03'             3375                                         
         DC    X'0E04'             3380                                         
         DC    X'0F05'             3390                                         
         DC    X'0000'                                                          
*                                                                               
DEVDISK  DS    0XL16                                                            
DEV3340  DC    X'0101',AL2(12,167,08535,08960,0293,064,000)                     
DEV3350  DC    X'0102',AL2(30,185,19254,19964,0389,128,000)                     
DEV3375  DC    X'0103',AL2(12,384,36000,36992,0992,196,031)                     
DEV3380  DC    X'0104',AL2(15,480,47968,49056,1088,221,043)                     
DEV3390  DC    X'0105',AL2(15,000,58786,58786,0000,224,000)                     
DEV3370  DC    X'0201',AL2(32,000,16384,16384,0000,000,000)                     
         EJECT                                                                  
***********************************************************************         
* ISCLEAR - CLEAR BUFFER CONTENTS TO FORCE I/OS NEXT TIME             *         
***********************************************************************         
ISCLEAR  XC    ISPDDA(12),ISPDDA   CLEAR INFO IN DTF                            
         ICM   R1,15,ISFXTAB       EXIT IF NO EXTRA BUFFERS                     
         JZ    ISCLRX                                                           
         USING ISXTABD,R1                                                       
         SR    R0,R0                                                            
         ICM   R0,1,ISFBUFFS       NUMBER OF IOAREAS                            
         AHI   R0,1                                                             
*                                                                               
ISCLR1   XC    ISXTBSK,ISXTBSK     CLEAR SEEK ADDRESS                           
         LA    R1,ISXTBLN(R1)                                                   
         JCT   R0,ISCLR1                                                        
*                                                                               
ISCLRX   J     EXIT                                                             
         DROP  R1                                                               
         EJECT                                                                  
***********************************************************************         
* CHECK PD INDEX TO MAKE SURE IT IS ALL IN ORDER                      *         
***********************************************************************         
CHKPD    NTR1  BASE=*,LABEL=*                                                   
         L     RF,ISBUFF1                                                       
         AH    RF,ISPDLN                                                        
         SH    RF,ISKEYLN4         LAST KEY - 1                                 
         BCTR  RF,0                (MINUS 1 BYTE FOR BXLE)                      
*                                                                               
         L     R4,ISBUFF1                                                       
         AH    R4,0(R4)            R4=FIRST KEY                                 
         CR    RF,R4                                                            
         JNH   RTNXIT              ONLY ONE KEY - MUST BE OK                    
         LH    R1,ISKEYLN1                                                      
         LH    RE,ISKEYLN4                                                      
*                                                                               
CHKPD02  LR    R5,R4                                                            
         AH    R5,ISKEYLN4         R5=KEY + 1                                   
         EX    R1,CHKPDCLC                                                      
         BNH   CHKPDDIE                                                         
         BXLE  R4,RE,CHKPD02                                                    
         J     RTNXIT                                                           
*                                                                               
CHKPDDIE DC    H'0'                DIE=INDEX IS BAD                             
*                                                                               
CHKPDCLC CLC   0(0,R5),0(R4)                                                    
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* BINARY SEARCH FOR REQUESTED TABLE ENTRY                             *         
* R1=A(KEY TO FIND)                                                   *         
* RF=A(RECORD) WITH CC EQL                                            *         
* RF=A(INSERTION POINT) WITH CC NEQ                                   *         
***********************************************************************         
BSRCH    NTR1  BASE=*,LABEL=*                                                   
         LR    R4,R1               R4=A(KEYARG)                                 
         L     R5,ISBUFF1                                                       
         AH    R5,0(R5)            R5=A(START OF DATA)                          
*                                                                               
         L     R0,ISBUFF1                                                       
         AH    R0,ISPDLN                                                        
         SR    R0,R5                                                            
         SRDL  R0,32               R0=LENGTH OF DATA IN BUFFER                  
*                                                                               
         LH    RF,ISKEYLN4         R0=LENGTH OF SINGLE ENTRY                    
         DR    R0,RF                                                            
         CHI   R1,6                                                             
         BL    SQSRCH              LESS THAN 6 - DO SEQUENTIAL SEARCH           
*                                                                               
         XR    R0,R0               R0=LOW                                       
         BCTR  R1,0                R1=HIGH                                      
         LH    RE,ISKEYLN1         RE=L'KEY-1                                   
*                                                                               
BSRCH02  CR    R0,R1               WHILE LOW <= HIGH                            
         BH    BSRCH04             R0 POINTS TO INSERTION POINT                 
*                                                                               
         LR    R3,R0               R3=MID                                       
         AR    R3,R1                                                            
         SRL   R3,1                MID=(HIGH+LOW)/2                             
*                                                                               
         LR    RF,R3                                                            
         MH    RF,ISKEYLN4                                                      
         AR    RF,R5               RF=A(TEST ENTRY)                             
*                                                                               
         EX    RE,BSMTCH           TRY TO MATCH KEY                             
         BE    BSXIT               KEY == K(MID)                                
         BL    *+12                                                             
         LA    R0,1(R3)            IF CC HIGH : LOW=MID+1                       
         B     BSRCH02                                                          
*                                                                               
         LR    R1,R3               IF CC LOW : HIGH=MID-1;                      
         BCTR  R1,0                                                             
         B     BSRCH02                                                          
*                                                                               
BSRCH04  LR    RF,R0                                                            
         MH    RF,ISKEYLN4                                                      
         AR    RF,R5                                                            
         B     BSXIT                                                            
*                                                                               
SQSRCH   L     RF,ISBUFF1                                                       
         AH    RF,0(RF)            RF=A(START OF DATA)                          
         L     R1,ISBUFF1                                                       
         AH    R1,ISPDLN                                                        
         SH    R1,ISKEYLN4         R1=A(LAST ENTRY)                             
         LH    R0,ISKEYLN4         R0=L'ENTRY                                   
         LH    RE,ISKEYLN1         RE=L'KEY-1                                   
*                                                                               
SSRCH02  EX    RE,BSMTCH           TRY TO MATCH KEY                             
         BNH   BSXIT                                                            
         BXLE  RF,R0,SSRCH02                                                    
         DC    H'0'                DIE=BINARY SEARCH                            
*                                                                               
BSMTCH   CLC   0(0,R4),0(RF)       COMPARE REQUESTED KEY WITH RECORD            
*                                                                               
BSXIT    ST    RF,FULL                                                          
         J     RTNXIT                                                           
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ERASE OLD PD BLOCK INDEX ENTRY                                      *         
***********************************************************************         
ERSBI    NTR1  BASE=*,LABEL=*                                                   
         BRAS  RE,CHKPD                                                         
*                                                                               
ERSB00   L     R5,ISBUFF1                                                       
         LH    R4,0(R5)                                                         
         AR    R4,R5               R4=A(FIRST KEY IN INDEX)                     
*                                                                               
         LH    RE,ISKEYLN4         RE=LENGTH OF SINGLE ENTRY                    
         LH    RF,ISPDLN                                                        
         AR    RF,R5                                                            
         BCTR  RF,0                RF=A(END-1 OF BLOCK)                         
*                                                                               
ERSB02   LH    R1,ISKEYLN                                                       
         AR    R1,R4               POINT R1 TO D/A PART OF NTRY                 
         CLC   DEADBLKA,0(R1)                                                   
         BE    ERSB04                                                           
         BXLE  R4,RE,ERSB02                                                     
*                                                                               
         L     R7,ISBUFF1                                                       
         OC    6(4,R7),6(R7)       IS THERE AN OVERFLOW BLKIND BLOCK?           
         JZ    *+2                 DIE=INV OFLOW INDEX                          
         LA    R7,6(R7)                                                         
         MVC   ISOVDA,0(R7)        SAVE DISK ADDRESS                            
         LA    R8,ISXTNTIX                                                      
         BAS   RE,ISBLDCCW                                                      
         BAS   RE,SETSCTR                                                       
         BAS   RE,EXCP             AND READ THIS OVERFLOW BLOCK                 
         B     ERSB00                                                           
*                                                                               
ERSB04   LH    RF,0(R5)                                                         
         AR    RF,R5               RF=A(FIRST ENTRY)                            
         LR    R1,R4               SHUFFLE ALL BEFORE OVER THIS ONE             
         SH    R1,ISKEYLN4         R1=A(MOVE FROM) - R4=A(MOVE TO)              
         LH    RE,ISKEYLN4                                                      
         BCTR  RE,0                RE=EXECUTE MOVE LENGTH                       
*                                                                               
ERSB06   CR    R4,RF               MOVED ENOUGH DOWN?                           
         BE    ERSB08              YES                                          
         EX    RE,ERSMVC                                                        
         SH    R4,ISKEYLN4                                                      
         SH    R1,ISKEYLN4                                                      
         B     ERSB06                                                           
*                                                                               
ERSMVC   MVC   0(0,R4),0(R1)       MOVE DOWN 1 SLOT                             
*                                                                               
ERSB08   LH    R4,0(R5)                                                         
         AR    R4,R5               R4=A(FIRST KEY IN INDEX)                     
         EX    RE,*+8                                                           
         B     *+10                                                             
         XC    0(0,R4),0(R4)       CLEAR OUT THIS ENTRY                         
*                                                                               
         LH    RF,0(R5)                                                         
         AH    RF,ISKEYLN4         ADJUST LENGTH                                
         STH   RF,0(R5)                                                         
*                                                                               
ERSBIX   BRAS  RE,CHKPD                                                         
         J     RTNXIT              EXIT TO WRITE UPDATED BLOCK INDEX            
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* UPDATE PD BLOCK INDEX WITH NEW HIGH KEY                             *         
***********************************************************************         
UPDBI    NTR1  BASE=*,LABEL=*                                                   
         BRAS  RE,CHKPD                                                         
         XC    ISOVDA,ISOVDA       ASSUME PD INDEX BLOCK INITIALLY              
*                                                                               
UPDB00   L     R5,ISBUFF1                                                       
         LH    R4,0(R5)                                                         
         AR    R4,R5               R4=A(FIRST KEY IN INDEX)                     
         LH    R3,ISKEYLN1         R3=LENGTH OF KEY-1                           
*                                                                               
         LH    RE,ISKEYLN4         RE=LENGTH OF SINGLE ENTRY                    
         LH    RF,ISPDLN                                                        
         AR    RF,R5                                                            
         BCTR  RF,0                RF=A(END-1 OF BLOCK)                         
*                                                                               
         L     R1,ISPDKEY                                                       
UPDB02   EX    R3,*+8              SUB 1 FOR COMPARE                            
         B     *+10                                                             
         CLC   0(0,R1),0(R4)       FIND ISPDKEY                                 
         BE    UPDB04                                                           
         BXLE  R4,RE,UPDB02                                                     
*                                                                               
         L     R7,ISBUFF1                                                       
         OC    6(4,R7),6(R7)       IS THERE AN OVERFLOW BLKIND BLOCK?           
         JZ    *+2                 DIE=INV OFLOW INDEX                          
         LA    R7,6(R7)                                                         
         MVC   ISOVDA,0(R7)        SAVE DISK ADDRESS                            
         LA    R8,ISXTNTIX                                                      
         BAS   RE,ISBLDCCW                                                      
         BAS   RE,SETSCTR                                                       
         BAS   RE,EXCP             AND READ THIS OVERFLOW BLOCK                 
         B     UPDB00                                                           
*                                                                               
UPDB04   L     R1,ISPDPRKY                                                      
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(R1)       COPY IN NEW KEY                              
*                                                                               
UPDBIX   BRAS  RE,CHKPD                                                         
         J     RTNXIT              EXIT TO WRITE UPDATED BLOCK INDEX            
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* SPLIT BLOCK WTO - R2=A(DTF)                                         *         
***********************************************************************         
SPBWTO   NTR1  BASE=*,LABEL=*                                                   
         LA    R4,MSGBF                                                         
         MVC   MSGBFDSN,ISFDD                                                   
         MVI   MSGBFKEY,C' '                                                    
         MVC   MSGBFKEY+1(L'MSGBFKEY-1),MSGBFKEY                                
         LH    R3,ISKEYLN          KEY LENGTH                                   
         LA    RF,L'MSGBFKEY/2     MAX LENGTH WE CAN HEXOUT                     
         CR    R3,RF                                                            
         JNH   *+6                                                              
         LR    R3,RF                                                            
         ICM   R5,15,ISPDKEY       SEE IF CAN WRITE OUT PART OF KEY             
         JZ    SPBWTO10                                                         
         ICM   RF,15,=V(HEXOUT)                                                 
         JZ    SPBWTO10                                                         
         OILH  GR5,X'8000'                                                      
         GOTOR (RF),MYWORK,(R5),MSGBFKEY,(R3),0                                 
*                                                                               
SPBWTO10 WTO   TEXT=(R4),MCSFLAG=HRDCPY                                         
         J     RTNXIT                                                           
*                                                                               
MSGBF    DC    AL2((MSGBFX-*)-L'MSGBF)                                          
         DC    C'ISDDS BLOCK INDEX FULL '                                       
MSGBFDSN DC    CL8' '                                                           
         DC    C' :KEY='                                                        
MSGBFKEY DC    CL30' '                                                          
MSGBFX   DS    0X                                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO OBTAIN STORAGE                                           *         
* R0=L'STAORAGE TO OBTAIN                                             *         
* GMFLAG=PARAMETERS TO DRIVE TYPE OF GETMAIN                          *         
***********************************************************************         
GETMAIN  NTR1  BASE=*,LABEL=*                                                   
         LA    R1,GMFTAB           MATCH SUPPORTED GETMAIN TYPES                
         LA    RF,4                                                             
         XR    RE,RE                                                            
*                                                                               
GETM02   CLI   0(R1),255                                                        
         JE    *+2                 DIE=INV GETMAIN TYPE                         
         CLC   GMFLAG,0(R1)                                                     
         BE    GETM04                                                           
         BXH   R1,RF,GETM02                                                     
*                                                                               
GETM04   ICM   RF,7,1(R1)                                                       
         BNZR  RF                                                               
         DC    H'0'                DIE=INV GETMAIN TYPE                         
*                                  UNCONDITIONAL/LOW/DBLWD                      
GMLO     GETMAIN RU,LV=(0),BNDRY=DBLWD                                          
         LTR   RF,RF                                                            
         BZ    GETMAINX                                                         
         DC    H'0'                DIE=INVALID GETMAIN                          
*                                  UNCONDITIONAL/HIGH/DBLWD                     
GMHI     GETMAIN RU,LV=(0),LOC=(ANY,ANY),BNDRY=DBLWD                            
         LTR   RF,RF                                                            
         BZ    GETMAINX                                                         
         DC    H'0'                DIE=INVALID GETMAIN                          
*                                                                               
GMHIPC   GETMAIN RC,LV=(0),LOC=(ANY,ANY),BNDRY=PAGE                             
         B     GETMAINX                                                         
*                                                                               
GETMAINX XIT1  REGS=(RF,R1)                                                     
*                                                                               
GMFTAB   DS    0XL4                                                             
         DC    AL1(GMFHI+GMFPG+GMFCD),AL3(GMHIPC)                               
         DC    AL1(GMFHI+GMFPG),AL3(0)                                          
         DC    AL1(GMFHI+GMFCD),AL3(0)                                          
         DC    AL1(GMFPG+GMFCD),AL3(0)                                          
         DC    AL1(GMFHI),AL3(GMHI)                                             
         DC    AL1(GMFCD),AL3(0)                                                
         DC    AL1(GMFPG),AL3(0)                                                
         DC    AL1(0),AL3(GMLO)                                                 
         DC    AL1(255)                                                         
         EJECT                                                                  
***********************************************************************         
* GETMAIN FAILURE INFORMATION MESSAGE                                 *         
***********************************************************************         
GMFWTO   NTR1                                                                   
         ST    R0,FULL                                                          
         BAS   RE,CVDA                                                          
         MVC   GMFR0,MYWORK                                                     
         ST    R1,FULL                                                          
         BAS   RE,CVDA                                                          
         MVC   GMFR1,MYWORK                                                     
         ST    RF,FULL                                                          
         BAS   RE,CVDA                                                          
         MVC   GMFRF,MYWORK                                                     
         L     RF,=A(MAXCISZ)                                                   
         MVC   FULL,0(RF)                                                       
         BAS   RE,CVDA                                                          
         MVC   GMFLEN,MYWORK                                                    
*                                                                               
         XR    R0,R0                                                            
         WTO   TEXT=((GMFHL,C),(GMFM1L,D),(GMFM2L,D),(0,E))                     
         J     RTNXIT                                                           
*                                                                               
GMFMALL  DS    0D                                                               
GMFHL    DC    AL2(25)                                                          
GMFH     DC    CL25'ISDDS GETMAIN FAILURE'                                      
GMFM1L   DC    AL2(70)                                                          
         DC    CL48'HIGH STORAGE GETMAIN FAILURE - LENGTH REQUESTED='           
GMFLEN   DC    CL08'        '                                                   
         DC    CL14' HEX'                                                       
GMFM2L   DC    AL2(61)                                                          
         DC    CL03'R0='                                                        
GMFR0    DC    CL08'        '                                                   
         DC    CL05', R1='                                                      
GMFR1    DC    CL08'        '                                                   
         DC    CL05', RF='                                                      
GMFRF    DC    CL08'        '                                                   
         DC    CL24' ATTEMPTING TO CONTINUE'                                    
         EJECT                                                                  
***********************************************************************         
* CONVERT HEX VALUE IN FULL TO EBCDIC IN MYWORK(8) SAFELY             *         
***********************************************************************         
CVDA     NTR1                                                                   
         ICM   R0,15,FULL                                                       
         SRDL  R0,4                                                             
         SRL   R1,32-4                                                          
         LA    R1,HEXTAB(R1)                                                    
         MVC   MYWORK+7(1),0(R1)                                                
         SRDL  R0,4                                                             
         SRL   R1,32-4                                                          
         LA    R1,HEXTAB(R1)                                                    
         MVC   MYWORK+6(1),0(R1)                                                
         SRDL  R0,4                                                             
         SRL   R1,32-4                                                          
         LA    R1,HEXTAB(R1)                                                    
         MVC   MYWORK+5(1),0(R1)                                                
         SRDL  R0,4                                                             
         SRL   R1,32-4                                                          
         LA    R1,HEXTAB(R1)                                                    
         MVC   MYWORK+4(1),0(R1)                                                
         SRDL  R0,4                                                             
         SRL   R1,32-4                                                          
         LA    R1,HEXTAB(R1)                                                    
         MVC   MYWORK+3(1),0(R1)                                                
         SRDL  R0,4                                                             
         SRL   R1,32-4                                                          
         LA    R1,HEXTAB(R1)                                                    
         MVC   MYWORK+2(1),0(R1)                                                
         SRDL  R0,4                                                             
         SRL   R1,32-4                                                          
         LA    R1,HEXTAB(R1)                                                    
         MVC   MYWORK+1(1),0(R1)                                                
         SRDL  R0,4                                                             
         SRL   R1,32-4                                                          
         LA    R1,HEXTAB(R1)                                                    
         MVC   MYWORK(1),0(R1)                                                  
         J     RTNXIT                                                           
*                                                                               
HEXTAB   DC    CL16'0123456789ABCDEF'                                           
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ISOPEN INDIRECT ADDRESS ROUTINE                                     *         
***********************************************************************         
AISOPEN  NTR1  BASE=*                                                           
         OC    ISCILAST,ISCILAST                                                
         BNZ   ISOPENX                                                          
         TM    ISFOPEN,X'20'                                                    
         BO    ISOPENX                                                          
*                                                                               
ISOPEN2  SR    R4,R4                                                            
         ICM   R4,7,ISFADCB+1      PICK UP 3 BYTE ADDRESS OF DCB                
         MVC   40(8,R4),ISFDD      SET FILE ID IN DCB DDNAME                    
*                                                                               
ISALLOC  BRAS  RE,ALLOC            ALLOCATE DD CARD                             
*                                                                               
ISOPEN3  ICM   RE,15,=V(SSB)       TEST OFFLINE WITH EXTENDED SSB               
         BZ    ISOPEN6                                                          
         OC    0(2,RE),0(RE)                                                    
         BNZ   ISOPEN6                                                          
         CLI   SSOXTND-SSOOFF(RE),X'FF'                                         
         BNE   ISOPEN6                                                          
ISOPEN4  TM    SSOMTIND-SSOOFF(RE),SSOWRTN TEST WRITE=N INPUT                   
         BZ    ISOPEN5                                                          
         C     R2,=V(SYS1FLEW)     IGNORE IF SY1 FILES                          
         BL    ISOPEN6                                                          
         OI    ISFOPEN,ISFORO      SET READ-ONLY FILE                           
         B     ISOPEN6                                                          
*                                                                               
ISOPEN5  TM    ISFOPEN,ISFORO      TEST READ-ONLY FILE                          
         BZ    ISOPEN6                                                          
         TM    ISCMPRSW,X'40'      DEFINED WITH ADD=NO IN DMIS MACRO            
         BO    ISOPEN6                                                          
         TM    SSOMTIND-SSOOFF(RE),SSOMRKY TEST IF MARKER=Y INPUT               
         BZ    ISOPEN6                                                          
         C     R2,=V(SYS1FLEW)     IGNORE IF SY1 FILES                          
         BL    ISOPEN6                                                          
         NI    ISFOPEN,255-ISFORO  TURN OFF READ-ONLY BIT                       
         OI    ISFFLAG,ISROWRN     SET FLAG TO ISSUE READ-ONLY WARNING          
*                                                                               
ISOPEN6  TM    ISFOPEN,ISFORO      TEST READ-ONLY FILE                          
         BZ    ISOPEN8                                                          
*                                                                               
ISOPEN7  STAM  ARE,AR1,ARSSAVER    OPEN FILE INPUT                              
         OPEN  ((4),INPUT)                                                      
         LAM   ARE,AR1,ARSSAVER                                                 
         B     ISOPEN9                                                          
*                                                                               
ISOPEN8  STAM  ARE,AR1,ARSSAVER    OPEN FILE INPUT/OUPUT                        
         OPEN  ((4),INOUT)                                                      
         LAM   ARE,AR1,ARSSAVER                                                 
*                                                                               
ISOPEN9  TM    48(R4),X'10'        TEST VALID OPEN                              
         BO    ISOPEN10                                                         
         ABEND 991,DUMP            ABEND FOR OPEN FAILURE                       
*                                                                               
ISOPEN10 LR    R1,R4               FIX DEBS TO MACRF=EXCP                       
         SVC   252                                                              
         MVI   42(R4),X'D0'        CHANGE MACRF BYTES TO EXCP                   
         MVI   43(R4),X'04'        IN OPENED FOUNDATION BLOCK                   
         OI    ISFOPEN,ISFOOPN+ISFOPROP  SET FILE OPEN/PREV OPEN                
*                                                                               
         LA    R4,ISXTNTIX         R4=A(EXTENT MATRIX ENTRY)                    
         SR    RE,RE                                                            
         ICM   RE,7,ISFADCB+1                                                   
         L     RE,44(RE)           GET DEB ADR FROM DCB                         
         SR    R1,R1                                                            
         ICM   R1,1,16(RE)         R1=NUMBER OF EXTENTS IN DEB                  
         JZ    *+2                 DIE=ZERO XTNTS                               
         SR    R5,R5                                                            
         ICM   R5,1,ISFXTNTS       GET NUM OF EXTENTS DEFINED IN DTF            
         BNZ   *+8                                                              
         LA    R5,MAXXTNTS                                                      
         CR    R1,R5                                                            
         JH    *+2                 DIE=TOO MANY XTNTS                           
         LA    R5,32(RE)           R5=A(DEB MATRIX ENTRY)                       
         SR    R6,R6               R6=EXTENT SEQUENCE NUMBER                    
*                                                                               
ISOPEN20 XC    0(14,R4),0(R4)                                                   
         SR    RE,RE                                                            
         ICM   RE,7,1(R5)                                                       
         MVC   0(1,R4),19(RE)      SET MVS DEVICE TYPE FROM UCB                 
         STC   R6,1(R4)            SET EXTENT SEQUENCE NUMBER                   
         MVC   2(8,R4),6(R5)       SET CCHH LOW AND CCHH HIGH                   
         MVC   10(2,R4),4(RE)      SET CHAN/UNIT FROM UCB                       
*                                                                               
         L     RE,=A(DEVCODE)                                                   
ISOPEN22 CLI   0(RE),0                                                          
         JE    *+2                 DIE=UNKNOWN DEVICE                           
         CLC   0(1,R4),0(RE)       TEST MVS CODE WITH TABLE ENTRY               
         BE    *+12                                                             
         LA    RE,2(RE)                                                         
         B     ISOPEN22                                                         
         MVC   0(1,R4),1(RE)       SET INT DEVICE TYPE CODE FROM TABLE          
         CLI   1(RE),5                                                          
         BE    ISOPEN24                                                         
         DC    H'0'                DIE=DEVICE NOT 3390                          
*                                                                               
ISOPEN24 LLC   RF,1(RE)            SET DEVICE DATA IN DTF                       
         BCTR  RF,0                                                             
         SLL   RF,4                                                             
         L     RE,=A(DEVDISK)                                                   
         AR    RF,RE                                                            
         LTR   R6,R6                                                            
         BNZ   ISOPEN26                                                         
         MVC   ISFDEVF(2),0(RF)    SET INTERNAL DEV FLAG AND TYPE               
         MVI   ISFAVAIL,X'00'      SET DTF AVAIL (X'80' MEANS BUSY)             
         OI    ISFDEVF,X'40'                                                    
ISOPEN26 MVC   DEVDATA,0(RF)                                                    
         LH    R3,DEVTRKS                                                       
         BCTR  R3,0                R3=TRKS/CYL-1                                
         OC    4(2,R4),4(R4)                                                    
         BNZ   *+12                                                             
         CH    R3,8(R4)                                                         
         BE    *+8                                                              
         OI    ISFDEVF,X'04'       SET EXTENTS NOT ON CYL BOUNDRYS              
*                                                                               
ISOPEN28 CLC   0(1,R4),ISFDEVT     TEST FOR MIXED EXTENTS                       
         BE    ISOPEN30                                                         
         OI    ISFDEVF,X'08'                                                    
*                                                                               
ISOPEN30 LA    R4,14(R4)           BUMP TO NEXT EXTENT                          
         LA    R5,16(R5)                                                        
         LA    R6,1(R6)                                                         
         BCT   R1,ISOPEN20                                                      
         MVI   0(R4),X'FF'         SET END OF EXTENTS                           
*                                                                               
ISOPENX  J     RTNXIT                                                           
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ISCLOSE INDIRECT ADDRESS ROUTINE                                    *         
***********************************************************************         
AISCLOSE NTR1  BASE=*                                                           
         SR    R4,R4                                                            
         ICM   R4,7,ISFADCB+1      CLOSE FILE                                   
         MVI   42(R4),X'28'        CHANGE MACRF BYTES BACK TO RI                
         MVI   43(R4),X'00'        IN OPENED FOUNDATION BLOCK                   
         CLOSE ((4))                                                            
         NI    ISFOPEN,255-ISFOOPN TURN OFF OPEN FLAG                           
         MVI   ISFAVAIL,X'00'      SET DTF AVAIL (X'80'=BUSY)                   
         OI    ISFDEVF,X'40'                                                    
         XC    ISCILAST,ISCILAST   SET NOT INITIALISED                          
         LA    R4,ISXTNTIX                                                      
         XC    0(15,R4),0(R4)      CLEAR FIRST EXTENT ENTRY                     
                                                                                
***********************************************************************         
* FREEMAIN ACQUIRED STORAGE                                           *         
* IF FILE OPENED BUT NOT ACCESSED, GETMAINS WILL NOT                  *         
* HAVE BEEN DONE - SO JUST EXIT                                       *         
***********************************************************************         
         TM    ISCMPRSW,X'20'      TEST BEEN THROUGH INIT                       
         BZ    ISCLOSEX            NO - NEVER ACCESSED FILE                     
         NI    ISCMPRSW,X'FF'-X'20'                                             
*                                                                               
         ICM   RE,15,ISINDX        POINT TO CI TABLE                            
         BZ    ISCLOSEX                                                         
         L     R1,8(RE)            FIRST ENTRY IS A(GETMAIN AREA)               
         L     R0,4(RE)            POINT TO EOT-1                               
         AH    R0,=H'1'                                                         
         SR    R0,R1               END-START GIVES LEN                          
         FREEMAIN RC,A=(1),LV=(0)                                               
         LTR   RF,RF                                                            
         JNZ   *+2                 DIE=INVALID FREEMAIN                         
         XC    ISINDX,ISINDX       CLEAR ADDRESS IN DTF                         
*                                                                               
         CLI   ONLINE,C'Y'                                                      
         BE    ISCLOSEX                                                         
         L     R1,ISBUFF1                                                       
         LH    R0,ISPDLN                                                        
         TM    ISCMPRSW,X'40'      TEST 1 OR 2 BUFFERS                          
         BO    *+6                 FLAG IS ON IF ONLY 1                         
         AR    R0,R0                                                            
         FREEMAIN RC,A=(1),LV=(0)                                               
         LTR   RF,RF                                                            
         JNZ   *+2                 DIE=INVALID FREEMAIN                         
         XC    ISBUFF1(12),ISBUFF1 CLEAR BUFF1/BUFF1X/BUFF2                     
*                                                                               
         L     R1,ISPDKEY                                                       
         LH    R0,ISKEYLN                                                       
         MHI   R0,3                                                             
         FREEMAIN RC,A=(1),LV=(0)                                               
         LTR   RF,RF                                                            
         JNZ   *+2                 DIE=INVALID FREEMAIN                         
*                                                                               
         XC    ISPDKEY,ISPDKEY                                                  
         XC    ISPDPRKY,ISPDPRKY                                                
         XC    ISTIHIKY,ISTIHIKY                                                
*                                                                               
         ICM   R1,15,ISTIBUFF                                                   
         BZ    ISCLOSE4                                                         
         LH    R0,ISTILN                                                        
         FREEMAIN RC,A=(1),LV=(0)                                               
         LTR   RF,RF                                                            
         JNZ   *+2                 DIE=INVALID FREEMAIN                         
         XC    ISTIBUFF,ISTIBUFF                                                
*                                                                               
ISCLOSE4 SR    R0,R0                                                            
         ICM   R0,1,ISFBUFFS       TEST EXTENDED IOAREAS REQUESTED              
         BZ    ISCLOSEX            NO                                           
         LA    R1,ISTILN           LENGTH IS BIGGER OF ISTILN/ISPDLN            
         CLC   ISTILN,ISPDLN                                                    
         BH    *+8                                                              
         LA    R1,ISPDLN                                                        
         MH    R0,0(R1)                                                         
         AH    R0,=AL2((ISXMAX+1)*ISXTBLN)  PLUS ROOM FOR TABLE                 
         L     R1,ISFXTAB                                                       
         FREEMAIN RC,A=(1),LV=(0)                                               
         LTR   RF,RF                                                            
         JNZ   *+2                 DIE=INVALID FREEMAIN                         
         XC    ISFXTAB,ISFXTAB     CLEAR ADDRESS                                
*                                                                               
ISCLOSEX J     RTNXIT                                                           
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO DYNAMICALLY ALLOCATE A DD CARD FOR FILE                  *         
***********************************************************************         
ALLOC    NTR1  BASE=*                                                           
*                                                                               
ALLOC1   ICM   RE,15,=V(SSB)       EXTRACT FACPAK ID IF ONLINE                  
         BZ    ALLOC3                                                           
         USING SSOOFF,RE                                                        
         OC    SSOCNTL,SSOCNTL     TEST ONLINE                                  
         BNZ   ALLOC3                                                           
*                                                                               
         CLI   SSOXTND,X'FF'       TEST IF OFFLINE EXTENDED SSB                 
         BNE   ALLOC3                                                           
         TM    SSOFLAG2,SSO2DYNO   EXIT IF DYNAMIC ALLOCATION INACTIVE          
         BO    ALLOCXIT                                                         
         OI    BYTE,X'80'          SET OFFLINE/EXTENDED SSB FLAG                
         USING ISDTF,R2                                                         
*                                                                               
ALLOC3   LA    R1,RBLK             SET UP REQ BLK POINTER                       
         ST    R1,ARBLK                                                         
         OI    ARBLK,X'80'                                                      
         MVI   RBLKLEN,20          SET UP REQ BLK LEN                           
         MVI   RBLKVERB,1          SET ALLOCATE BY DSN VERB CODE                
         LA    R1,ATXT                                                          
         ST    R1,RBLKATXT         SET POINTER TO TEXT POINTER LIST             
*                                                                               
         LA    R1,TXTDD            SET UP POINTER TO DDNAME TEXT                
         ST    R1,ATXTDD                                                        
         MVC   0(2,R1),=X'0001'    SET DDNAME TEXT CODE                         
         MVC   2(2,R1),=X'0001'                                                 
         MVC   6(8,R1),ISFDD       SET DDNAME VALUE TO DCB VALUE                
         LA    RF,6(R1)            FIND LEN OF DD NAME                          
         LA    R0,8                                                             
         CLI   0(RF),C' '                                                       
         BE    *+12                                                             
         LA    RF,1(RF)                                                         
         BCT   R0,*-12                                                          
         LA    RE,6(R1)                                                         
         SR    RF,RE                                                            
         STH   RF,4(R1)            SET LEN IN DDNAME TEXT HEADER                
*                                                                               
ALLOC5   LA    R1,TXTDSN           SET UP POINTER TO DSN TEXT                   
         ST    R1,ATXTDSN                                                       
         MVC   TXTDSN(2),=X'0002'  SET DSN TEXT CODE                            
         MVC   TXTDSN+2(2),=X'0001'                                             
         ICM   RF,15,=V(DMDYNDD)   CALL DMDYNDD TO GET DSN TEXT                 
         BZ    ALLOCXIT                                                         
         GOTO1 (RF),MYWORK,0,ISFDD,(R2)                                         
         BNE   ALLOCXIT                                                         
         TM    8(R1),X'02'         TEST IF DID GLOBAL ALLOCATE                  
         BZ    *+8                                                              
         OI    BYTE,X'20'                                                       
         TM    8(R1),X'01'         TEST IF ALLOCATED FLASH COPY                 
         BZ    ALLOC10                                                          
         OI    BYTE,X'10'                                                       
         OI    ISFOPEN,ISFORO      SET FLASH COPY FILE READ-ONLY                
         TM    BYTE,X'80'                                                       
         B     ALLOC10             *NOP* BZ DONT SET WRITE=N OFFLINE            
         ICM   RE,15,=V(SSB)       GET A(OFFLINE SSB)                           
         USING SSOOFF,RE                                                        
         OI    SSOMTIND,SSOWRTN    SET WRITE=N IF FLASH COPY FILE               
*                                                                               
ALLOC10  ICM   RE,15,0(R1)         P1=AL1(LENGTH OF DSN),AL3(NEW DSN)           
         JZ    *+2                 DIE=INV DSN LEN                              
         NILH  GRE,X'00FF'                                                      
         MVC   TXTDSN+6(20),0(RE)                                               
         LLC   RF,0(R1)                                                         
         STH   RF,TXTDSN+4         SET LEN IN DSN TEXT HEADER                   
*                                                                               
         LA    R1,TXTDISP          SET UP POINTER TO DISP TEXT                  
         ST    R1,ATXTDISP                                                      
         MVC   0(2,R1),=X'0004'    SET DISP TEXT CODE                           
         MVC   2(2,R1),=X'0001'                                                 
         MVC   4(2,R1),=X'0001'                                                 
         MVI   6(R1),X'08'         SET DISP=SHR CODE                            
*                                                                               
         TM    BYTE,X'01'          IS THIS AN ISOPOLD                           
         BZ    *+8                                                              
         MVI   6(R1),X'01'         SET DISP=OLD CODE                            
*                                                                               
         LA    R1,TXTUNAL          SET UP POINTER TO UNALLOCATE TEXT            
         ST    R1,ATXTUNAL                                                      
         MVC   0(2,R1),=X'001C'    SET UNALLOCATE TEXT CODE                     
         XC    2(4,R1),2(R1)                                                    
*                                                                               
         OI    ATXTLAST,X'80'      SET END OF TEXT POINTER LIST                 
         LA    R1,ARBLK                                                         
         LR    R5,R1               R5=A(REQ BLK IN DUMP)                        
         DYNALLOC                                                               
         LR    R6,RF               R6=RETURN CODE IN DUMP                       
         L     R7,RBLKERR          R7=RBLKERR/RBLKINFO IN DUMP                  
         LTR   RF,RF               TEST FOR ERRORS                              
         BZ    ALLOC30             NO                                           
*                                                                               
         CLC   RBLKERR,=X'1708'    FILE MAY NOT EXIST ERROR                     
         BNE   ALLOC16             YES                                          
         XC    MYWORK,MYWORK                                                    
         MVC   MYWORK,=X'0029'                                                  
         MVC   MYWORK+2(21),=C'FILE MAY NOT EXIST - '                           
         MVC   MYWORK+23(20),TXTDSN+6                                           
         LA    R4,MYWORK                                                        
         WTO   TEXT=(R4)                                                        
         ABEND 994                 FILE MAY NOT EXIST (SHR OR OLD)              
*                                                                               
ALLOC16  TM    BYTE,X'20'          YES  - TEST IF DID GLOBAL ALLOCATE           
         BO    ALLOC18                                                          
*                                                                               
         CLC   RBLKERR,=X'0410'    NORMAL - TEST IF DD CARD EXISTS              
         BE    ALLOCXIT            YES                                          
         XC    MYWORK,MYWORK                                                    
         MVC   MYWORK,=X'0033'                                                  
         MVC   MYWORK+2(32),=C'DISP=OLD ALLOCATION FAILURE FOR '                
         MVC   MYWORK+34(20),TXTDSN+6                                           
         TM    BYTE,X'01'                                                       
         BO    *+10                                                             
         MVC   MYWORK+7(3),=C'SHR'                                              
         LA    R4,MYWORK                                                        
         WTO   TEXT=(R4)                                                        
         TM    BYTE,X'01'          ASK OPERATOR IF DISP=OLD ALLOCATION          
         BZ    ALLOC20                                                          
         XC    ECBAD,ECBAD                                                      
         WTOR  'RETRY OR CANCEL',REPLY,1,ECBAD,ROUTCDE=(1)                      
         WAIT  ECB=ECBAD                                                        
         CLI   REPLY,C'R'                                                       
         BE    ALLOC1                                                           
         ABEND 990                 FILE IS IN USE - ABEND WITHOUT DUMP          
*                                                                               
ALLOC18  CLC   RBLKERR,=X'0410'    GLOBAL - TEST IF DD CARD EXISTS              
         BNE   ALLOC20                                                          
         XC    MYWORK,MYWORK       YES - OUTPUT DD OVERRIDE MESSAGE             
         MVC   MYWORK(2),=X'001E'                                               
         MVC   MYWORK+2(21),=C'DD OVERRIDE USED FOR '                           
         MVC   MYWORK+23(8),TXTDD+6                                             
         LA    R4,MYWORK                                                        
         WTO   TEXT=(R4),MCSFLAG=HRDCPY                                         
         B     ALLOCXIT            ALLOW DD OVERRIDE                            
*                                                                               
ALLOC20  LA    R1,TXTDSN                                                        
         ICM   RF,15,=V(ISGQU)     CALL ISGQU FOR ALLOCATION DETAIL             
         JZ    *+10                                                             
         BASR  RE,RF                                                            
         J     ALLOC1                                                           
*                                                                               
         ABEND 990,DUMP            ALLOCATION FAILURE ABEND                     
*                                                                               
ALLOC30  OI    ISFOPEN,X'10'       SET DD CARD DYNAMICALLY GENERATED            
*                                                                               
ALLOCXIT J     RTNXIT                                                           
         DROP  R2,RE                                                            
*                                                                               
ECBAD    DC    F'0'                WTOR FIELDS                                  
REPLY    DC    CL4' '                                                           
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO LOCK DTF                                                 *         
***********************************************************************         
LOCKDTF  NTR1  BASE=*                                                           
         ICM   R4,15,=V(SSB)                                                    
         BZ    LOCKDTFX                                                         
         USING SSBD,R4                                                          
         L     R3,P4               R3=A(DTF)                                    
         USING ISDTF,R3                                                         
*                                                                               
LKD020   CLI   ISFSNUM,0           IGNORE LOCK IF NO SYSTEM                     
         BE    LOCKDTFX                                                         
         TM    ISFFLAG,ISFGLOB     TEST GLOBAL RESOURCE                         
         BZ    LKD100              NOT GLOBAL - DO LOCAL LOCK                   
         XC    DUB,DUB                                                          
         MVC   DUB+3(1),ISFSNUM                                                 
*                                                                               
         GOTO1 =V(LOCKSPC),DUB     LOCK THE SYSTEM                              
         MVI   LOCKFLG,C'Y'                                                     
*                                                                               
         L     R2,4(R1)            SET FULL TO A(SYSTEM)                        
         USING DMSPACED,R2                                                      
         MVC   FULL,DSPECB                                                      
         SAC   512                                                              
         LAM   AR2,AR2,SSBALET                                                  
         L     R2,FULL                                                          
         USING DMSYSHDR,R2                                                      
         LH    R1,DSYFILES         R1=NUMBER OF FILES                           
         LA    R2,DSYDATA          R2=A(FILES)                                  
         USING DSFILHDR,R2                                                      
*                                                                               
LKD030   CLC   ISFXNUM,DSFILNUM    TEST FILE NUMBER                             
         BE    LKD050                                                           
         LA    R2,32(,R2)                                                       
         BCT   R1,LKD030                                                        
         DC    H'0'                DIE=INV FILE NUM                             
*                                                                               
LKD050   OC    DSEOF1,DSEOF1       TEST DSPACE NOT ZERO                         
         BZ    LKDERR                                                           
         CLC   ISOVLAST,DSEOF1     TEST OUR EOF NOT GREATER                     
         BH    LKDERR                                                           
         MVC   ISOVLAST,DSEOF1                                                  
         MVC   ISPDLAST,DSEOF2                                                  
         CR    RB,RB                                                            
         B     LKD999                                                           
*                                                                               
LKD100   ICM   RF,15,=V(ADWAIT)                                                 
         BZ    LOCKDTFX                                                         
         TS    ISFAVAIL            DTF+2 =X'80' MEANS BUSY                      
         BNZ   LKD102              BUSY - GO WAIT                               
         NI    ISDTF,X'BF'         TURN OFF POST BIT (ISFAVAIL=X'FF')           
         B     LOCKDTFX                                                         
*                                                                               
LKD102   LA    R1,ISDTF            POINT TO DTF                                 
         OILH  GR1,X'FF00'         SET 'DO NOT WAIT ON THIS ECB'                
         BASSM RE,RF               BRANCH AND SET 24-BIT MODE                   
         LA    RE,LKD100                                                        
         SAM31                     BACK TO 31 BIT MODE                          
         BR    RE                  TRY AGAIN                                    
*                                                                               
LKDERR   LTR   RB,RB               FORCE EOF SEARCH                             
*                                                                               
LKD999   SAC   0                                                                
*                                                                               
LOCKDTFX J     RTNXIT                                                           
*                                                                               
         LTORG                                                                  
         DROP  R2,R3,R4                                                         
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO UNLOCK SYSTEM                                            *         
***********************************************************************         
FREEDTF  NTR1  BASE=*                                                           
         ICM   R4,15,=V(SSB)                                                    
         BZ    FREEDTFX                                                         
         USING SSBD,R4                                                          
         L     R3,P4               R3=A(DTF)                                    
         USING ISDTF,R3                                                         
*                                                                               
FRE020   CLI   ISFSNUM,0           IGNORE LOCK IF NO SYSTEM                     
         BE    FREEDTFX                                                         
         TM    ISFFLAG,ISFGLOB     TEST GLOBAL RESOURCE                         
         BZ    FRE100              NO                                           
         XC    DUB,DUB                                                          
         OI    DUB,X'20'                                                        
         MVC   DUB+3(1),ISFSNUM                                                 
*                                                                               
         GOTO1 =V(LOCKSPC),DUB     ENQUIRE ON SYSTEM                            
*                                                                               
         L     R2,4(R1)            SET FULL TO A(SYSTEM)                        
         USING DMSPACED,R2                                                      
         MVC   FULL,DSPECB                                                      
         NI    FULL,X'3F'                                                       
         SAC   512                                                              
         LAM   AR2,AR2,SSBALET                                                  
         L     R2,FULL                                                          
         USING DMSYSHDR,R2                                                      
         LH    R1,DSYFILES         R1=NUMBER OF FILES                           
         LA    R2,DSYDATA          R2 POINTS TO FILES                           
         USING DSFILHDR,R2                                                      
*                                                                               
FRE030   CLC   ISFXNUM,DSFILNUM    TEST FILE NUMBER                             
         BE    FRE050                                                           
         LA    R2,32(,R2)                                                       
         BCT   R1,FRE030                                                        
         DC    H'0'                DIE=INV FILE NUM                             
*                                                                               
FRE050   MVC   DSEOF1,ISOVLAST     SET NEW VALUES                               
         MVC   DSEOF2,ISPDLAST                                                  
*                                                                               
         SAC   0                                                                
         MVI   DUB,X'10'                                                        
         MVC   DUB+3(1),ISFSNUM                                                 
         GOTO1 =V(LOCKSPC),DUB     FREE THE SYSTEM                              
         B     FREEDTFX                                                         
*                                                                               
FRE100   MVI   ISFAVAIL,0          FREE LOCK FLAG                               
         OI    ISDTF,X'40'         POST ECB COMPLETE                            
*                                                                               
FREEDTFX J     RTNXIT                                                           
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* SAVE CURRENT IO DATA IN OLDEST SLOT IN TABLE                        *         
* NOTE THAT FIRST TABLE ENTRY CONTAINS ADDRESS/LEN                    *         
* OF LAST ACTUAL I/O DONE                                             *         
***********************************************************************         
         DROP  R2,R3,R4                                                         
         USING ISDTF,R2            R2=A(DTF)                                    
ISX      NTR1  BASE=*                                                           
         L     R1,ISFXTAB                                                       
         USING ISXTABD,R1                                                       
*                                                                               
         OC    ISXTBADR,ISXTBADR   TEST ANY DATA TO BE SAVED                    
         BZ    ISX20               NO                                           
*                                                                               
         LA    R1,ISXTBLN(R1)      POINT TO SECOND ENTRY                        
         LLC   R0,ISFBUFFS         GET NUMBER OF BUFFERS                        
         B     ISX12                                                            
*                                                                               
ISX10    CLC   ISXTBSEQ,ISXTBSEQ-ISXTABD(R5)                                    
         BNL   *+6                                                              
ISX12    LR    R5,R1               LOW - SAVE ADDRESS                           
         LA    R1,ISXTBLN(R1)                                                   
         BCT   R0,ISX10                                                         
*                                                                               
         L     R1,ISFXTAB          R1 TO TOP OF TABLE                           
         MVC   ISXTBSK-ISXTABD(8,R5),ISXTBSK MOVE SEEK ADDRESS                  
         MVC   ISXTBDL-ISXTABD(4,R5),ISXTBDL MOVE DATA LENGTH                   
         SR    RE,RE                                                            
         ICM   RE,3,ISFXSEQ        BUMP SEQNUM AND SET IN DTF & TABLE           
         LA    RE,1(RE)                                                         
         STCM  RE,3,ISFXSEQ                                                     
         ST    RE,ISXTBSEQ-ISXTABD(R5)                                          
                                                                                
***********************************************************************         
* NOW MOVE DATA TO EXTENDED IOAREA                                    *         
***********************************************************************         
         LM    RE,RF,ISXTBADR      GET FROM ADDR/LEN FROM FIRST ENTRY           
         LR    R1,R5               POINT TO NEW ENTRY                           
         L     R0,ISXTBADR         SET TO ADDR                                  
         LR    R1,RF               SET TO LEN                                   
         MVCL  R0,RE               MOVE DATA                                    
                                                                                
***********************************************************************         
* SAVE DATA FOR THIS IO IN FIRST TABLE ENTRY                          *         
***********************************************************************         
ISX20    L     R1,ISFXTAB          R1 TO TOP OF TABLE                           
         MVC   ISXTBSK,ISFSEEK     SAVE SEEK ADDRESS                            
         SR    R0,R0                                                            
         ICM   R0,3,CCW4+6         DATA LENGTH                                  
         ST    R0,ISXTBDL                                                       
         MVC   ISXTBADR,IDAWS      SET DATA ADDRESS FROM FIRST IDAW             
                                                                                
***********************************************************************         
* NOW HAVE A LOOK TO SEE IF REQ DATA IS IN CORE                       *         
***********************************************************************         
         LA    R1,ISXTBLN(R1)      POINT TO SECOND ENTRY                        
         LLC   R0,ISFBUFFS         NUMBER OF IOAREAS                            
*                                                                               
ISX22    OC    ISXTBSEQ,ISXTBSEQ   IS THIS ENTRY VALID                          
         BZ    *+14                NO                                           
         CLC   ISFSEEK,ISXTBSK     COMPARE SEEK ADDRESSES                       
         BE    ISX24               GOT IT                                       
ISX23    LA    R1,ISXTBLN(R1)                                                   
         BCT   R0,ISX22                                                         
         B     ISX26                                                            
*                                                                               
ISX24    TM    READTYPE,FLUSH      TEST IF WANT FORCED READ OF BLOCK            
         BZ    ISX30               NO THEN USE THIS BUFFER                      
         CLC   ISXTBDL+2(2),ISTILN                                              
         BE    ISX30               ONLY CONCERED ABOUT PD BUFFS                 
         XC    ISXTBSEQ,ISXTBSEQ   THROW THIS BUFFER AWAY                       
         B     ISX23                                                            
*                                                                               
ISX26    L     R1,ISFXTAB          POINT TO FIRST ENTRY                         
         SR    RE,RE                                                            
         ICM   RE,3,ISXTBSEQ       THIS HLFWD USED TO COUNT MISSES              
         LA    RE,1(RE)                                                         
         STCM  RE,3,ISXTBSEQ                                                    
*                                                                               
ISX28    MVI   ECB,X'FF'           SET CC FOR LATER                             
         B     ISX40               AND EXIT                                     
                                                                                
***********************************************************************         
* FOUND THE DATA IN EXT IOAREA - MOVE IT TO CURRENT IOA               *         
* R1 POINTS TO THE TABLE ENTRY                                        *         
***********************************************************************         
ISX30    XC    ISXTBSEQ,ISXTBSEQ   SET SLOT FREE NOW                            
         LM    RE,RF,ISXTBADR      FROM ADDR/LEN                                
         L     R1,ISFXTAB          R1 TO FIRST ENTRY                            
         L     R0,ISXTBADR         TO ADDR                                      
         LR    R1,RF               TO LEN=FROM LEN                              
         MVCL  R0,RE               MOVE DATA TO REQUESTED ADDRESS               
         MVI   ECB,0               SET CC FOR LATER                             
         MVI   BUFFOK,0            CLEAR BUFFER OK FLAGS                        
*                                                                               
ISX40    L     R1,ISFXTAB          POINT TO FIRST SLOT                          
         SR    RE,RE                                                            
         ICM   RE,3,ISXTBSEQ+2     BUMP TOTAL I/O COUNT                         
         LA    RE,1(RE)                                                         
         STCM  RE,3,ISXTBSEQ+2                                                  
         B     ISXX                *NOP* COUNTER LOGIC                          
*                                                                               
         N     RE,=X'000003FF'     TEST DIVISIBLE BY 1024                       
         BNZ   ISXX                NO - GET OUT                                 
         N     RE,=X'0000FFFF'     TEST OVERFLOWED COUNTER                      
         BNZ   *+14                IF BITS ARE ON, NO OVERFLOW YET              
         XC    ISXTBSEQ,ISXTBSEQ   IF BITS ARE ALL OFF, CLEAR COUNTERS          
         B     ISXX                                                             
*                                                                               
         SR    RF,RF               TEST RATIO OF MISSES TO TRIES                
         ICM   RF,3,ISXTBSEQ       GET NUMBER OF MISSES                         
         M     RE,=F'100'                                                       
         SR    R0,R0                                                            
         ICM   R0,3,ISXTBSEQ+2     GET NUMBER OF EXCP'S                         
         DR    RE,R0                                                            
         C     RF,=F'90'           TEST 90% MISSED                              
         BL    ISXX                NO                                           
         OI    ISFXTAB,X'80'       SET TO STOP USING EXT BUFFS                  
*                                                                               
ISXX     CLI   ECB,0               SET CC ON EXIT                               
         J     RTNXIT                                                           
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO CALL LOCKER TO LOCK PD DATA RECORD                       *         
***********************************************************************         
LOCKBLK  NTR1  BASE=*,LABEL=*                                                   
         TM    READTYPE,DOLOCKER                                                
         BZ    LOCKBLKX                                                         
*                                                                               
LOCKBLK0 ICM   RF,15,VSSB          TEST TO CALL DMLOCKER OFFLINE                
         BZ    LOCKBLKX                                                         
         OC    0(2,RF),0(RF)                                                    
         BNZ   LOCKBLK1                                                         
         TM    SSOSTAT2-SSOOFF(RF),SSOSLOCK                                     
         BZ    LOCKBLKX                                                         
*                                                                               
LOCKBLK1 ICM   R1,15,P7            TEST IF LOCKER PARAM LIST PASSED             
         BZ    LOCKBLKX                                                         
         MVC   1(3,R1),ISPDDA      SET PD D/A                                   
         LR    R0,R2               SAVE A(DTF)                                  
         L     R2,P8               DMGR CONTROL BLOCK                           
         MVC   LOCKBYTE,0(R2)      LOCKER IS FUSSY ABOUT BITS...                
         MVI   0(R2),X'80'                                                      
LOCKBLK2 GOTO1 ALOCKER,(R1)                                                     
         MVC   0(1,R2),LOCKBYTE                                                 
         LR    R2,R0               RESET A(DTF)                                 
*                                                                               
         TM    8(R1),X'01'         TEST ALREADY HAVE THE LOCK                   
         BO    LOCKBLKX                                                         
         OI    READTYPE,FLUSHME    IF DONT HAVE LOCK THEN FORCE READ            
*NOP*    TM    8(R1),X'08'         TEST WAITED                                  
*NOP*    BO    LOCKBLK2            YES - TRY FOR LOCK AGAIN                     
*                                                                               
LOCKBLKX XIT1                                                                   
*                                                                               
ALOCKER  DC    V(LOCKER)                                                        
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE DSECT                                               *         
***********************************************************************         
       ++INCLUDE DMISDDSWRK                                                     
         EJECT                                                                  
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
*FASSB                                                                          
         PRINT OFF                                                              
       ++INCLUDE FASSB                                                          
         PRINT ON                                                               
*FASSBOFF                                                                       
SSBOFFD  DSECT                                                                  
         PRINT OFF                                                              
       ++INCLUDE FASSBOFF                                                       
         PRINT ON                                                               
*FATCB                                                                          
         PRINT OFF                                                              
       ++INCLUDE FATCB                                                          
         PRINT ON                                                               
*DMDYNDDD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMDYNDDD                                                       
         PRINT ON                                                               
*DMXTNTD                                                                        
         PRINT OFF                                                              
       ++INCLUDE DMXTNTD                                                        
         PRINT ON                                                               
*DMSPACED                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMSPACED                                                       
         PRINT ON                                                               
*DMDSYSHDR                                                                      
         PRINT OFF                                                              
       ++INCLUDE DMDSYSHDR                                                      
       ++INCLUDE DMDSHDR                                                        
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'008DMISDDS   04/25/19'                                      
         END                                                                    
