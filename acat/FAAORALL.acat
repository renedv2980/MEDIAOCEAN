*          DATA SET FAAORALL   AT LEVEL 035 AS OF 05/05/14                      
*CATALP AORALL                                                                  
         TITLE 'FAAORALL - KEEP STOKENS AND ALETS FOR ALL FACPAK'               
         SPACE 1                                                                
***********************************************************************         
* FAAORALL                                                            *         
* NTRY: R1 = A(RETURN SLOT FOR AOR NUMBER - MUST BE A 4 BYTES)        *         
* EXIT: RETURN SLOT HOLDS ADDRESS OF FACPAKS TABLE                    *         
*       ARS ARE PRESERVED, BUT MODE SET SAC 0 ON RETURN               *         
***********************************************************************         
         SPACE 1                                                                
AORALL   CSECT                                                                  
         NMOD1 WORKX-WORKD,*AORALL*,CLEAR=Y                                     
         STAR  CLEAR=ARZERO,ARS=OFF                                             
         USING WORKD,RC                                                         
         L     R9,VSYSFAC                                                       
         USING SYSFACD,R9                                                       
         L     RA,VSSB                                                          
         USING SSBD,RA                                                          
         ST    R1,APARM                                                         
*                                                                               
*NOP     CLI   INIT,C'Y'           MUST CHECK IN CASE OF RESTARTS               
*NOP     BE    AORAHKX                                                          
*                                                                               
         LAM   AR2,AR2,SSBALET                                                  
         LA    R6,FACALET                                                       
         SRL   R6,4                QUADWORD ALIGN                               
         SLL   R6,4                                                             
         USING FACALETD,R6                                                      
*                                                                               
         XR    R2,R2                                                            
         SAC   512                                                              
         USING DMDSHDR,R2                                                       
         L     R3,DHAPGMS                                                       
         L     R2,DHATOR           FIND START OF TOR BLOCK                      
         USING TORFACD,R2                                                       
AORA000  ST    R2,CURRTOR                                                       
         CR    R2,R3                                                            
         BNL   AORAHKX                                                          
*                                                                               
         CLC   TOREYE(3),=C'TOR'   MAKE SURE WE HAVE A TOR                      
         BNE   AORA040                                                          
         MVC   CURRFACN(4),TOREYE+4                                             
         CLI   TORSTRTD,C'Y'       STARTED                                      
         BNE   AORA040                                                          
         CLI   TORAVLBL,C'Y'       AVAILABLE                                    
         BNE   AORA040                                                          
         LA    R1,TORFACLQ+6       LINE UP ON EXCHANGE BLOCK                    
         AR    R2,R1                                                            
         USING SBEXCHD,R2                                                       
         LA    R5,SBFACMAX                                                      
AORA010  ST    R2,CURRFAC                                                       
         MVI   CURRFACN+4,C' '                                                  
         MVC   TSTOKEN,SBSTOKEN                                                 
         OC    TSTOKEN,TSTOKEN                                                  
         BZ    AORA031                                                          
*                                                                               
         MVC   FACALSTD,SBSTRTD    STARTED                                      
         MVC   FACALAVL,SBAVLBL    AVAILABLE                                    
*                                                                               
         CLC   FACALTOK,TSTOKEN    SAME AS BEFORE                               
         BE    AORA030                                                          
*                                                                               
         MVC   FACALTOK,TSTOKEN                                                 
         MVC   FACALSSB,SBSSB                                                   
         MVC   FACALTCB,SBTCB                                                   
         LH    R1,SBFACID                                                       
         SHI   R1,2                                                             
         BM    AORA015                                                          
         AHI   R1,C'A'                                                          
         CLI   CURRFACN+3,C' '                                                  
         BNE   *+12                                                             
         STC   R1,CURRFACN+3                                                    
         B     *+8                                                              
         STC   R1,CURRFACN+4                                                    
*                                                                               
AORA015  MVC   FACALFAC,CURRFACN                                                
         XC    ADFCARD,ADFCARD     SVC 247 NEEDS CHANGING                       
         MVC   ADFCARD+00(04),=C'PALE'                                          
         MVC   ADFCARD+04(12),=C'TESTDATAMGRT'                                  
         MVC   ADFCARD+28(08),TSTOKEN                                           
         LA    R0,21                                                            
         LNR   R0,R0                                                            
         LA    R1,ADFCARD                                                       
         SAC   0                                                                
         SVC   247                                                              
         SAC   512                                                              
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     RF,ADFCARD+24       SAVE ALET FOR TOR IN SSB & LOCALLY           
         ST    RF,FACALALE                                                      
*                                                                               
AORA030  AHI   R6,FACALLEN                                                      
*                                                                               
AORA031  L     R2,CURRFAC          NEXT FACPAK                                  
         A     R2,=A(SBEXCHLQ)                                                  
         BCT   R5,AORA010                                                       
*                                                                               
AORA040  L     R2,CURRTOR          NEXT TOR                                     
         LA    R1,SBFACMAX                                                      
         MHI   R1,SBEXCHLQ                                                      
         AHI   R1,TORFACLQ+6                                                    
         AR    R2,R1                                                            
         B     AORA000                                                          
*                                                                               
AORAHKX  DS    0H                                                               
         MVI   FACALSTD,SBNO       INDICATE NOT STARTED                         
         MVI   FACALAVL,SBNO       INTICATE NOT AVAILABLE                       
*                                                                               
         OC    FACALFAC,FACALFAC   ANY MORE                                     
         BZ    AORAHKXX                                                         
         OC    FACALALE,FACALALE                                                
         BNZ   AORAHKX1                                                         
*                                                                               
         XC    ADFCARD,ADFCARD     SVC 247 NEEDS CHANGING                       
         MVC   ADFCARD+00(04),=C'PALE'                                          
         MVC   ADFCARD+04(12),=C'TESTDATAMGRT'                                  
         MVC   ADFCARD+28(08),FACALTOK                                          
         LA    R0,21                                                            
         LNR   R0,R0                                                            
         LA    R1,ADFCARD                                                       
         SAC   0                                                                
         SVC   247                                                              
         SAC   512                                                              
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     RF,ADFCARD+24       SAVE ALET FOR TOR IN SSB & LOCALLY           
         ST    RF,FACALALE                                                      
*                                                                               
AORAHKX1 AHI   R6,FACALLEN                                                      
         B     AORAHKX                                                          
*                                                                               
AORAHKXX SAC   0                                                                
         REAR  ARS=OFF                                                          
         MVI   INIT,C'Y'                                                        
         L     R1,APARM                                                         
         LA    R6,FACALET                                                       
         SRL   R6,4                QUADWORD ALIGN                               
         SLL   R6,4                                                             
         ST    R6,0(R1)            RETURN A(FACALET)                            
         XMOD1 ,                                                                
         DROP  RA,RC                                                            
*                                                                               
         LTORG                                                                  
VSYSFAC  DC    V(SYSFAC)                                                        
ARZERO   DC    16F'0'                                                           
INIT     DC    C'N'                                                             
         DS    0QD                                                              
         DC    C'*** FACALETS ***' EYECATCHER                                   
         DC    C'****************' PADDING TO QUADWORD                          
FACALET  DC    64XL(FACALLEN)'00'                                               
         EJECT                                                                  
WORKD    DSECT                                                                  
APARM    DS    A                                                                
CURRTOR  DS    A                   A CURRENT TOR                                
CURRFAC  DS    A                   A CURRENT FAC                                
CURRFACN DS    CL8                 CURRENT FAC NAME                             
TSTOKEN  DS    D                                                                
ADFCARD  DS    XL48                                                             
WORKX    EQU   *                                                                
*                                                                               
FACALETD DSECT                                                                  
FACALFAC DS    CL8                                                              
FACALTOK DS    CL8                                                              
FACALALE DS    CL4                                                              
FACALSSB DS    CL4                                                              
FACALTCB DS    CL4                                                              
FACALSTD DS    C                                                                
FACALAVL DS    C                                                                
         DS    CL2                                                              
FACALLEN EQU   *-FACALETD                                                       
*                                                                               
       ++INCLUDE FASSB                                                          
         EJECT                                                                  
       ++INCLUDE FAUTL                                                          
         EJECT                                                                  
       ++INCLUDE FASYSFAC                                                       
         EJECT                                                                  
       ++INCLUDE FAPIGFACD                                                      
         EJECT                                                                  
       ++INCLUDE DMDSHDR                                                        
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'035FAAORALL  05/05/14'                                      
         END                                                                    
