*          DATA SET DDHEXOUT   AT LEVEL 002 AS OF 07/29/11                      
*CATALP HEXOUT                                                                  
*        THIS CSECT TRANSLATES A SOURCE STRING OF BYTES TO DISPLAYABLE          
*        HEXADECIMAL CHARACTERS AS A DESTINATION STRING.THE DESTINATION         
*        STRING HAS ONE OF TWO POSSIBLE FORMATS                                 
*        MIXED    - EACH ZONE PRECEDES EACH DIGIT ZDZD..ZDZDZDZD..ZDZD          
*        SEPERATE - ALL ZONES PRECEDE ALL DIGITS  ZZZZ..ZZZZDDDD..DDDD          
*        PARAMS VIA R1 ARE WORDS DEFINED AS FOLLOWS                             
*        1ST   A(SOURCE STRING) X'80' IF 31-BIT ADDRESS            (R2)         
*        2ND   A(DESTNE STRING) X'80' IF 31-BIT ADDRESS            (R3)         
*        3RD   L'SOURCE STRING                                     (R4)         
*        4TH   AL1(LITE), AL3(OPTION) IF C'SEP' SEPERATE OR MIXED  (R5)         
*        5TH   VALUE RETURN L'DESTNE STRING OR ZERO IF ERROR       (R6)         
*                                                                               
*        THIS VERSION HAS LITE CODE THAT CAN BE CALLED DIRECT USING             
*        ENTRY POINTS, OR BY SETTING HOB OF P4 TO 24 OR 31                      
*        LITE VERSION ONLY DOES MIXED AND DOES NOT RETURN LENGTH IN P5          
                                                                                
HEXOUT   CSECT                                                                  
         PRINT NOGEN                                                            
         ENTRY HEXO24              LITE VERSION 24-BIT ADDRESSES                
         ENTRY HEXO31              LITE VERSION 31-BIT ADDRESSES                
*                                                                               
         CLI   12(R1),24           TEST HOB OF P4 FOR LITE VERSION              
         JE    HEXO24                                                           
         JH    HEXO31V                                                          
*                                                                               
HEXO     NMOD1 0,HEXOUT            NORMAL VERSION ENTRY POINT                   
         SAM24                                                                  
         TM    0(R1),X'80'         TEST 31-BIT ADDRESS(S)                       
         BO    *+12                                                             
         TM    4(R1),X'80'                                                      
         BZ    HEXO0                                                            
         SAM31                                                                  
*                                                                               
HEXO0    LM    R2,R5,0(R1)                                                      
         N     R2,=XL4'7FFFFFFF'                                                
         N     R3,=XL4'7FFFFFFF'                                                
         LTR   R4,R4                                                            
         BP    HEXO1                                                            
         SR    R6,R6               RETURN ZERO LENGTH LE ZERO                   
         B     HEXOX                                                            
*                                                                               
HEXO1    LTR   R5,R5               ZERO P4 IS DEFAULT                           
         BZ    HEXO1A                                                           
         CLC   0(3,R5),=C'SEP'     CONVERT EACH SOURCE CHR X'ZD'                
         BE    HEXO2                                                            
HEXO1A   LA    R5,2                DEFAULT TO MIXED OPTION                      
         LA    R6,1(R3)                                                         
         B     HEXO3                                                            
HEXO2    LA    R5,1                SET TO SEPERATE OPTION                       
         LA    R6,0(R3,R4)                                                      
HEXO3    UNPK  0(1,R3),0(1,R2)                                                  
         NI    0(R3),X'0F'                                                      
         MVN   0(1,R6),0(R2)                                                    
         NI    0(R6),X'0F'                                                      
         LA    R2,1(R2)            UP SOURCE PTR                                
         AR    R3,R5               UP DESTN ZONE PTR                            
         AR    R6,R5               UP DESTN DIGT PTR                            
         BCT   R4,HEXO3                                                         
*                                                                               
         LM    R3,R4,4(R1)         R3=A(DESTN)                                  
         SLA   R4,1                R4=L'DESTN                                   
         LR    R6,R4                                                            
HEXO4    CHI   R4,256              TRANSLATE DESTN                              
         BL    HEXO6                                                            
         TR    0(256,R3),CHRHTAB                                                
         AHI   R4,-256                                                          
         LA    R3,256(R3)                                                       
         B     HEXO4                                                            
HEXO5    TR    0(0,R3),CHRHTAB                                                  
HEXO6    LTR   R4,R4                                                            
         BZ    HEXOX                                                            
         BCTR  R4,R0                                                            
         EX    R4,HEXO5                                                         
*                                                                               
HEXOX    ST    R6,16(R1)                                                        
         XMOD1 1                                                                
                                                                                
* HEXOUT LITE 31 AND 24 BIT ADDRESS MODES - NO P4 OR P5 RETURN                  
*                                                                               
HEXO31V  CLI   12(R1),31           CHECK IF HOB IS SET TO 31                    
         JNE   HEXO                                                             
                                                                                
HEXO31   STM   RE,R3,12(RD)        ONLY SAVE WHAT WE USE                        
         SAM31                                                                  
         J     HOLITE                                                           
                                                                                
HEXO24   STM   RE,R3,12(RD)        ONLY SAVE WHAT WE USE                        
         SAM24                                                                  
                                                                                
HOLITE   LM    RE,RF,0(R1)         RE=A(SOURCE),RF=A(DESTINATION)               
         SR    R2,R2                                                            
         ICM   R2,3,10(R1)         R2=L'SOURCE                                  
         JZ    HOLITEX                                                          
         BASR  R3,0                R3=A(TABLE-4)                                
         J     HOLITE1                                                          
         DC    C'0123456789ABCDEF'                                              
*                                                                               
HOLITE1  IC    R0,0(RE)            R0=NEXT INPUT HEX CHR                        
         SRDL  R0,4                                                             
         SRL   R1,28               R1=LOB OF INPUT CHR                          
         IC    R1,4(R3,R1)                                                      
         STC   R1,1(RF)                                                         
         SRDL  R0,4                                                             
         SRL   R1,28               R1=HOB OF INPUT CHR                          
         IC    R1,4(R3,R1)                                                      
         STC   R1,0(RF)                                                         
         LA    RE,1(RE)            BUMP TO NEXT INPUT HEX CHR                   
         LA    RF,2(RF)                                                         
         JCT   R2,HOLITE1                                                       
*                                                                               
HOLITEX  LM    RE,R3,12(RD)                                                     
         BSM   0,RE                                                             
                                                                                
CHRHTAB  DC    C'0123456789ABCDEF'                                              
         LTORG                                                                  
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002DDHEXOUT  07/29/11'                                      
         END                                                                    
