*          DATA SET MXPOST     AT LEVEL 090 AS OF 06/21/19                      
*CATALP MXPOST                                                                  
         TITLE 'MXPOST - FOR CURRENT ACCOUNT SETS UP WORKER FILE REC'           
         SPACE 2                                                                
**********************************************************                      
*        PARM 1 BYTE 0       NOT USED                    *                      
*               BYTE 1-3     A(CONTROL DSECT) SEE MXPOSTD*                      
**********************************************************                      
*---------------------------------------------------------------------*         
* USER    JIRA       DATE                  CHANGE LOG                 *         
* ---- ----------  -------- ------------------------------------------*         
* SMUR  SPEC-33293 03/05/19 ESTIMATE PO NUMBER (19.2)                 *         
* SMUR  SPEC-9487  04/24/17 CLIENT CURRENCY - SPEC CHANGED            *         
* SMUR  SPEC-9487  03/29/17 CLIENT CURRENCY                           *         
*---------------------------------------------------------------------*         
         SPACE 2                                                                
MXPOST   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKDL,MXPST,R9,R8                                               
         USING WORKD,RC                                                         
*                                                                               
         L     R7,0(R1)            A(CONTROL DSECT)                             
         USING MXPOSTD,R7                                                       
*                                                                               
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
*                                                                               
         L     R6,MXBILL          R6=A(BILL RECORD)                             
         USING BILLRECD,R6                                                      
         L     R4,MXROW           R4=A(CURRENT ACCOUNT INFO)                    
         USING ACPRTND,R4                                                       
         EJECT                                                                  
         L     R3,=V(REC)          R3=A(POSTING RECORD TO ADD)                  
         LR    R1,R3                                                            
         MVC   2(2,R1),=H'2'       SET INITIAL LENGTH                           
         LA    R1,4(R1)                                                         
         STCM  R1,15,NXTRECE       A(NEXT POSITION TO ADD ELEMENT)              
*                                                                               
         BAS   RE,EXTRACT          EXTRACT SOME BILL INFORMATION                
         BAS   RE,DOSWITCH         SET ACCOUNT SWITCHES                         
         BAS   RE,BLDHEAD          BUILD POSTING HEADER ELEMENT (X'50')         
         BAS   RE,BLDTRANS         BUILD POSTING TRANS ELEM (X'44')             
         BAS   RE,BLDDUE           BUILD DUE DATE ELEMENT (X'61')               
         BAS   RE,BLDGDT           BUILD GENERAL DATE ELEMENT (X'E5')           
         BAS   RE,BLDMBI           BUILD MEDIA TRANSFER LEMENT (X'F3')          
         BAS   RE,BLDAPT           BUILD ANALYSIS POINTER ELEM (X'C0')          
         BAS   RE,BLDINVL          BUILD LONG INV # ELEMENT (X'DB')             
         BAS   RE,BLDCURR          BUILD CURRENCY ELEMENT (X'DB')               
*                                                                               
         OC    MXCNET,MXCNET       CALCULATE NET ON BILL OR TRADE CR?           
         BZ    PST30                                                            
         CLC   =C'REGM',MXDBTYPE   FOR REGM BILLS STORE THE MIDAS               
         BNE   PST10               TRADE CREDIT ON THE OTHER INCOME ACC         
         CLI   ACPBTYP,MBTTOINC    OTHER INCOME ACCOUNT                         
         BE    PST20                                                            
         B     PST30                                                            
PST10    CLI   ACPBTYP,MBTTRCV     IF RECEIVABLE ACCOUNT                        
         BNE   PST30                                                            
PST20    BAS   RE,BLDCNET                                                       
*                                                                               
PST30    DS    0H                                                               
*        CLC   =C'REGM',MXDBTYPE   FOR REGM BILLS STORE THE MIDAS               
*        BNE   POSTAC02            EXCHANGE (NB) AMT ON THE RCV POSTNG          
*        CLI   ACPBTYP,MBTTRCV     AS A GROSS MEMO                              
*        BNE   POSTAC02                                                         
*        BAS   RE,SUBGRS2                                                       
*                                                                               
         CLI   AORSW,C'Y'          ADDING THE AOR INFO ELEMENT?                 
         BNE   *+8                 NO                                           
         BAS   RE,AORDB            YES-ADD AOR DB ELEMENT                       
*                                                                               
         CLC   =C'RET',MXBTYPE     IF RETAIL BILL                               
         BNE   *+8                                                              
         BAS   RE,BLDRET           BUILD RETAIL ELEMENT (X'2C')                 
*                                                                               
         BAS   RE,BLDDB            EVERYONE GETS THIS ELEMENT                   
         BAS   RE,DBADID           THIS ONE TOO IF AD-ID EXISTS                 
                                                                                
         CLI   MTSW,C'N'           IF CREATING MEDIA ELEMS (1A,A2,D7)           
         BE    PST100                                                           
         BAS   RE,BLDPGR           BUILD PROD GROUP ELEM (X'D7')                
         BAS   RE,SPLITEST         SPLIT ESTIMATE DESC (RETURNS SPLIT)          
         BAS   RE,BLDMT            BUILD MEDIA TRANSFER ELEMENT (X'1A')         
         CLI   MXRSYS,0            SYSTEM AND/OR MEDIA CODE REDEFINED?          
         BNE   *+12                YES                                          
         CLI   MXRMED,0                                                         
         BE    *+8                 NO                                           
         BAS   RE,BLDMT2           BUILD 2ND MEDIA TRANSFER ELEM (X'DB)         
         BAS   RE,BLDFLD           BLD USER FIELD SELECT ELEMS (X'2A')          
         BAS   RE,BLDPO#           BUILD PO# ELEMENTS (X'DB')                   
*                                                                               
PST100   CLC   =C'NNNNN',CDSW      IF CREATING SUBS. CASH ELES(X'50')           
         BE    *+8                                                              
         BAS   RE,BLDSUB           BUILD SUBSIDARY CASH ELEM (X'50')            
*                                                                               
         BAS   RE,BLDPAY           BUILD PAY ELEMMENT (X'46')                   
         BAS   RE,BLDOTH           BUILD OTHERS ELEMENT (FOR UNIT S)            
         BAS   RE,BLDMKT           BUILD MARKET IN FREEFORM TEXT(X'DB')         
         BAS   RE,BLDREG           BUILD REGION/DISTRICT IN (X'DB')             
*                                                                               
* REMOVED THIS B/C NOW WE ALWAYS CREATE THE C0 ELEMENT FOR ALL POSTINGS         
*        CLI   APSW,C'Y'           IF BUILDING ANALYSIS POINTER ELEMENT         
*        BNE   *+8                                                              
*        BAS   RE,BLDAPE           BUILD IT (X'C0' FOR SI POSTINGS)             
*                                                                               
         SR    R1,R1               MOVE LENGTH FROM REC+2 TO REC+0              
         ICM   R1,3,2(R3)          AND ADJUST IT                                
         LA    R1,2(R1)                                                         
         STCM  R1,3,0(R3)                                                       
         XC    2(2,R3),2(R3)                                                    
         B     XIT                                                              
         SPACE 2                                                                
YES      SR    RC,RC                                                            
NO       LTR   RC,RC                                                            
XIT      XIT1                                                                   
         EJECT                                                                  
*              ROUTINE TO EXTRACT SOME BILL INFORMATION                         
*                                                                               
EXTRACT  NTR1                                                                   
         CLI   MXSYS,C'P'          IF PRINT SYSTEM                              
         BNE   XTRCT50                                                          
         SR    RE,RE                                                            
         ICM   RE,3,PBILKEST                                                    
         CVD   RE,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  BESTNUM(3),DUB      PRINT ESTIMATE NUMBER                        
         MVC   BDUEDT,PBILDUED           DUE DATE                               
         MVC   BBILLDT,PBILLDAT          BILL DATE                              
         MVC   FULL,PBILKMOS             MONTH OF SERVICE                       
         MVI   FULL+3,1                                                         
         GOTO1 MXDTCN,DMCB,(3,FULL),(1,BMOSDT)                                  
*                                        INVOICE DATE                           
         GOTO1 MXDTCN,DMCB,(3,PBILINVD),(1,BINVDT)                              
         MVC   SYSCH,=C'P '              SYSTEM CHARACTER 'P'                   
         B     XTRCTX                                                           
*                                                                               
XTRCT50  ZIC   RE,BKEYEST                                                       
         CVD   RE,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  BESTNUM(3),DUB      SPOT/NET ESTIMATE NUMBER                     
         MVC   BDUEDT,BDUEDATE              DUE DATE                            
         MVC   BBILLDT,BDATE                BILL DATE                           
         MVC   DUB(4),BMONSERV              MONTH OF SERVICE                    
         MVC   DUB+4(2),=C'01'                                                  
         GOTO1 MXDTCN,DMCB,(0,DUB),(1,BMOSDT)                                   
         GOTO1 MXDTCN,DMCB,(0,BQDATE),(1,BINVDT) INVOICE DATE                   
         MVC   SYSCH,=C'S '                 SYSTEM CHARACTER 'S'                
*                                                                               
XTRCTX   B     XIT                                                              
         EJECT                                                                  
*              DOSWITCH - ROUTINE TO SET SWITCHES (CONTROL ELEMENTS             
*                         ADDED TO POSTING RECORD)                              
*                                                                               
DOSWITCH NTR1                                                                   
         MVI   MTSW,C'N'           MEDIA TRANSFER ELEMENT (X'1A')               
         MVI   MTAMTS,C'Y'         MEDIA TRANSFER ELEM AMTS (X'1A')             
         MVC   CDSW,=5C'N'         CASH DESC ELEMENT (X'50')                    
         MVI   PAYSW,C'N'          EXTRA PAY ELEMENT (X'46')                    
         MVI   APSW,C'N'           ANALYSIS PTR ELEM (X'C0')                    
         MVI   AORSW,C'N'          AOR INFO ELEMENT (X'DB')                     
*                                                                               
         CLC   =C'AOR',MXBTYPE     IS THIS AN AOR BILL?                         
         BNE   DOSW05              NO SO SKIP                                   
         CLI   ACPBIL2,2           YES-BUT IS THIS THE MAIN AOR BILL?           
         BE    DOSW05              NO SO SKIP                                   
         CLI   ACPBTYP,MBTTRCV     IF RECEIVABLE ACCOUNT                        
         BE    *+12                OR INCOME ACCOUNT OF MAIN AOR BILL           
         CLI   ACPBTYP,MBTTINC     ADD THE AOR INFO ELEMENT TO THE              
         BNE   *+8                 POSTING.                                     
         MVI   AORSW,C'Y'                                                       
*                                                                               
DOSW05   CLI   ACPBTYP,MBTTRCV     IF RECEIVABLE ACCOUNT                        
         BE    *+12                                                             
         CLI   ACPBTYP,MBTTIRCV    INTERCOMPANY RECIEVABLE                      
         BNE   *+12                                                             
         BAS   RE,SWRCV            SET SWITCHES FOR ACCOUNT                     
         B     DOSWX                                                            
*                                                                               
         CLI   ACPBTYP,MBTTCD      IF CASH DISCOUNT ACCOUNT                     
         BNE   DOSW20                                                           
         MVI   MTSW,C'Y'                                                        
         MVI   CDSW,C'O'           50 T W/OUT GST & 50Q W/OUT PST               
         MVI   MTAMTS,C'Z'                                                      
         MVI   APSW,C'Y'                                                        
         B     DOSWX                                                            
*                                                                               
DOSW20   CLI   ACPBTYP,MBTTINC     IF INCOME ACCOUNT                            
         BE    DOSW25                                                           
         CLI   ACPBTYP,MBTTARI     OR AOR INCOME ACCOUNT                        
         BE    DOSW25                                                           
         CLI   ACPBTYP,MBTTSEL     OR SELLOFF INCOME ACCOUNT                    
         BE    DOSW25                                                           
         CLI   ACPBTYP,MBTTRINC    OR TRADE INCOME                              
         BE    DOSW25                                                           
         CLI   ACPBTYP,MBTTOINC    OR OTHER INCOME (GROUPM MIDAS TRADE)         
         BE    DOSW25                                                           
         CLI   ACPBTYP,MBTTOPIN    OR OPCO INCOME (GROUPM MIDAS TRADE)          
         BE    DOSW25                                                           
         CLI   ACPBTYP,MBTTINTI    OR INTERNAL INCOME ACCOUNT                   
         BNE   DOSW30                                                           
DOSW25   MVI   MTSW,C'Y'           ADD MEDIA TRANSFER ELEMENT                   
         MVI   CDSW,C'G'           ADD GROSS MEMO ELEMENT                       
         MVI   APSW,C'Y'                                                        
         CLI   ACPBIL2,2           AND MAIN BILL                                
         BE    *+8                                                              
         MVI   CDSW+1,C'O'         50 T W/OUT GST & 50 Q W/OUT PST'S            
         B     DOSWX                                                            
*                                                                               
DOSW30   CLI   ACPBTYP,MBTTGST     IF GST OUTPUT ACCOUNT                        
         BE    DOSW45                                                           
         CLI   ACPBTYP,MBTTPQO     OR PST PQ OUTPUT ACCOUNT                     
         BE    DOSW45                                                           
         CLI   ACPBTYP,MBTTNBO     OR PST NB OUTPUT ACCOUNT                     
         BE    DOSW45                                                           
         CLI   ACPBTYP,MBTTNSO     OR PST NS OUTPUT ACCOUNT                     
         BE    DOSW45                                                           
         CLI   ACPBTYP,MBTTNFO     OR PST NF OUTPUT ACCOUNT                     
         BE    DOSW45                                                           
         CLI   ACPBTYP,MBTTBCO     OR PST BC OUTPUT ACCOUNT                     
         BE    DOSW45                                                           
         CLI   ACPBTYP,MBTTONO     OR PST ON OUTPUT ACCOUNT                     
         BE    DOSW45                                                           
         CLI   ACPBTYP,MBTTPEO     OR PST PE OUTPUT ACCOUNT                     
         BE    DOSW45                                                           
         CLI   ACPBTYP,MBTTGSTI    OR GST INPUT ACCOUNT                         
         BE    DOSW45                                                           
         CLI   ACPBTYP,MBTTPQI     OR PST PQ INPUT ACCOUNT                      
         BNE   *+12                                                             
DOSW45   MVI   CDSW,C'P'           ADD '50' TYPE N (NET PAYABLE)                
         B     DOSWX                                                            
*                                                                               
         CLI   ACPBTYP,MBTTAPR     IF AOR PAY/RCVBL ACCOUNT                     
         BE    *+12                                                             
         CLI   ACPBTYP,MBTTIPBL    INTERCOMPANY PAYABLE                         
         BNE   *+12                                                             
         BAS   RE,SWPOR            SET SWITCHES                                 
         B     DOSWX                                                            
*                                                                               
         CLI   ACPBTYP,MBTTRCS     IF SUSPENSE REBATE ACCOUNT                   
         BE    *+12                                                             
         CLI   ACPBTYP,MBTTRCR     OR REBATE ACCOUNT                            
         BNE   DOSWX                                                            
         MVI   MTSW,C'Y'           ADD MEDIA XFER ELEMENT                       
         MVI   MTAMTS,C'Z'         WITH ZERO AMOUNTS                            
         MVI   CDSW,C'Z'           ADD '50' TYPE I W/ZERO AMTS                  
DOSWX    B     XIT                                                              
         EJECT                                                                  
*              SWRCV - SETS SWITCHES FOR RECEIVABLE ACCOUNT                     
*                                                                               
SWRCV    NTR1                                                                   
         MVI   CDSW,C'I'           ADD '50' ELE TYPE I W/INCOME                 
         MVI   MTSW,C'Y'           ADD MEDIA XFER EEL (SJ & SR)                 
         MVI   CDSW+1,C'O'         50 T W/OUT GST & 50 Q W/OUT PST'S            
         CLC   =C'REGM',MXDBTYPE   FOR REGM BILLS STORE THE MIDAS               
         BNE   *+8                 EXCHANGE (NB) AMT ON THE RCV POSTING         
         MVI   CDSW+2,C'G'         AS GROSS                                     
*                                                                               
         CLI   MXSYS,C'P'          IF PRINT SYSTEM                              
         BNE   SWRCVX                                                           
         CLC   =C'SJ',ACPACC       AND SJ ACCOUNT W/PROF=NO                     
         BNE   SWRCV10                                                          
         GOTO1 MXMXPROF,DMCB,MTPFNMEM,PWORK                                     
         CLI   PWORK,C'N'                                                       
         BNE   *+12                                                             
         MVI   MTAMTS,C'C'         DON'T ADD '50' CD & 1A W/NO CD=              
         B     DOSWX                                                            
*                                                                               
SWRCV10  MVI   CDSW+2,C'D'         OTHERWISE ADD '50' TYPE D W/CD               
         CLC   =C'REGM',MXDBTYPE   FOR REGM BILLS STORE THE MIDAS               
         BNE   *+8                 EXCHANGE (NB) AMT ON THE RCV POSTING         
         MVI   CDSW+3,C'G'         AS GROSS                                     
SWRCVX   B     XIT                                                              
         SPACE 2                                                                
*              SWPOR - SETS SWITCHES OR PAYABLE/RECEIVABLE ACCOUNT              
*                                                                               
SWPOR    NTR1                                                                   
         CLC   =C'SR',ACPACC       IF PAYABLE ACCOUNT                           
         BE    SWPOR10                                                          
         MVI   PAYSW,C'Y'          ADD EXTRA PAY ELEMENT                        
         MVI   CDSW,C'T'           50 T W/INP GST & 50 Q W/INP PST'S            
         MVI   CDSW+1,C'G'         ADD '50' TYPE G W/ ITS MEMO                  
         B     SWPORX                                                           
*                                                                               
SWPOR10  MVI   MTSW,C'Y'           ADD MEDIA XFER ELEMENT                       
         MVI   CDSW,C'Z'           ADD '50' TYPE I W/ZERO AMTS                  
         MVI   CDSW+1,C'T'         50 T W/INP GST & 50 Q W/INP PST'S            
*                                                                               
SWPORX   B     XIT                                                              
         EJECT                                                                  
*              BLDHEAD - ROUTINE BUILDS HEADER ELEMENT FOR ACCOUNT              
*                                                                               
BLDHEAD  NTR1                                                                   
         MVC   ELEM,SPACES           PRE CLEAR ELEMENT                          
         LA    R2,ELEM               R2=A(ELEMENT TO BUILD)                     
         USING PSHEADD,R2                                                       
         MVI   PSHDEL,PSHDELQ        ELEMENT CODE X'50'                         
         MVI   PSHDLEN,PSHEADL       ELEMENT LENGTH                             
         MVC   PSHDANAL,SPACES       PRE CLEAR WORK CODE                        
         CLI   MXSYS,C'P'            IF PRINT SYSTEM                            
         BNE   BLDHEAD5                                                         
         CLC   =C'SJ',ACPACC         AND CURRENT ACCOUNT IS SJ                  
         BNE   BLDHEAD5                                                         
         GOTO1 MXMXPROF,DMCB,MTPFWKSJ,PWORK                                     
         MVC   PSHDANAL,PWORK        SET WORK CODE                              
*                                                                               
BLDHEAD5 MVC   PSHDACC(1),MXCMP      COMPANY CODE                               
         MVC   PSHDACC+1(14),ACPACC  CURRENT ACCOUNT NUMBER                     
         OC    PSHDACC+1(14),SPACES                                             
         MVC   PSHDSBAC,MXCONTRA     CONTRA ACCOUNT NUMBER                      
         MVC   PSHDSBNM,MXCONTRN     CONTRA ACCOUNT NAME                        
         BAS   RE,ADDELEM            ADD ELEMENT TO RECORD                      
         B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*              BLDTRANS - ROUTINE BUILDS TRANSACTION ELE FOR ACCOUNT            
*                                                                               
BLDTRANS NTR1                                                                   
         XC    ELEM,ELEM           PRE CLEAR ELEMENT                            
         LA    R2,ELEM             R2=A(ELEMENT TO BUILD)                       
         USING TRNELD,R2                                                        
         MVI   TRNEL,TRNELQ        ELEMENT CODE X'44'                           
         MVI   TRNLN,TRNLN1Q       ELEMENT LENGTH                               
         MVC   TRNDATE,BINVDT      INVOICE DATE                                 
         MVC   TRNREF,MXINV        INVOICE NUMBER FOR MAIN BILL                 
         CLI   ACPBIL2,2           IF AOR SCENARIO                              
         BNE   *+10                                                             
         MVC   TRNREF,MXTINV       TRUE AOR INVOICE NUMBER                      
         MVI   TRNTYPE,9           INPUT TYPE                                   
         CLI   MXQOPT1,C'R'        IF REVERSAL                                  
         BNE   BLDT15                                                           
         BAS   RE,CHKMBT           AND NO CHANGE IN KEY OF TRANSACTION          
         BNE   BLDT15                                                           
         MVI   TRNSUB,2            ENSURE TRANSFER ALWAYS AFTER REV.            
BLDT15   ZAP   TRNAMNT,=P'0'       POSTING AMOUNT                               
         OC    ACPAMT2,ACPAMT2                                                  
         BZ    BLDT25                                                           
         TM    ACPSTAT,ACPDEB                                                   
         BNO   BLDT20                                                           
         MVC   TRNSTAT,ACPSTAT     DEBIT OR CREDIT                              
         CLI   MXSYS,C'P'          IF PRINT SYSTEM                              
         BNE   BLDT20                                                           
         CLC   =C'SJ',ACPACC       AND IF CURRENT ACCOUNT IS SJ                 
         BNE   BLDT20                                                           
         OI    TRNSTAT,TRNSNOCM    ALSO NON COMISS.                             
BLDT20   MVC   TRNAMNT,ACPAMT2     POSTING AMOUNT                               
*                                                                               
BLDT25   MVC   TRNANAL,SPACES      ACCOUNTING OFFICE CODE                       
         MVC   TRNANAL(L'MXACCOFF),MXACCOFF                                     
*                                                                               
         MVC   TRNMOS(1),MXTRANDT+1 SECOND CHAR OF YEAR                         
         ZIC   RF,MXTRANDT+3                                                    
         CLI   MXTRANDT+2,C'1'     IF MONTH IS ONE DIGIT (0-9)                  
         BNE   *+8                 YES - THEN MOVE IT                           
         SH    RF,=H'47'           NO- MAKE 10=C1,11=C2,12=C3,ETC..             
         STC   RF,TRNMOS+1                                                      
         MVC   TRNBREF,SPACES      BATCH REFERENCE NUMBER                       
*                                                                               
         CLI   MXSYS,C'P'          IF PRINT SYSTEM                              
         BNE   BLDT50                                                           
         CLC   =C'SJ',ACPACC       AND IF CURRENT ACCOUNT IS SJ                 
         BNE   *+10                                                             
         MVC   TRNANAL,MXWKCD      SET WORK CODE                                
         CLI   ACPACC,C'S'         IF CURRENT ACCOUNT IS UNIT S                 
         BNE   BLDT50                                                           
         CLC   MXJOBCD,SPACES      AND JOB CODE                                 
         BE    BLDT50                                                           
         ZIC   RF,MXJBLEN          SET JOB CODE AND DESCRIPTION                 
         LA    RF,TRNLN1Q(RF)                                                   
         STC   RF,TRNLN                                                         
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   TRNNARR(0),MXJBNARR IN NARRATION                                 
*                                                                               
BLDT50   BAS   RE,ADDELEM         ADD NEW ELEMENT TO RECORD                     
         B     XIT                                                              
         DROP  R2                                                               
         SPACE 2                                                                
*              CHKMBT - ROUTINE CHECKS FOR KEY CHANGE IN TRANSACTION            
*                                  XIT - CC SET                                 
*                                                                               
CHKMBT   NTR1                                                                   
         L     R2,=V(MBTELES)      R2=A(TABLE OF REVERSED ELEMENTS)             
         USING MBTELD,R2                                                        
*                                                                               
CHKMBT5  OC    0(MBTPDLNQ,R2),0(R2) TEST END OF ELEMENT TABLE                   
         BZ    NO                                                               
         CLC   ACPACC,MBTULA       MATCH ON CURRENT ACCOUNT                     
         BNE   CHKMBT10                                                         
         CLC   MXCONTRA+1(14),MBTCNTRA  MATCH ON CONTRA ACCOUNT                 
         BNE   CHKMBT10                                                         
         CLI   MXSYS,C'P'          IF PRINT SYSTEM                              
         BNE   CHKMBT7                                                          
         CLC   =C'SJ',ACPACC       AND IF CURRENT ACCOUNT IS SJ                 
         BNE   CHKMBT7                                                          
         CLC   MXWKCD,MBTOFFC      MATCH ON WORK CODE                           
         BE    YES                                                              
         B     CHKMBT10                                                         
*                                                                               
CHKMBT7  CLI   MXNEWOFF,C'Y'       ELSE IF NEWOFF                               
         BNE   YES                                                              
         CLI   MXEMU,C'Y'          AND EMULATING                                
         BNE   YES                                                              
         CLC   MXACCOFF,MBTOFFC    MATCH ON OFFICE                              
         BE    YES                                                              
*                                                                               
CHKMBT10 LA    R2,MBTPDLNQ(R2)     BUMP TO NEXT ELEMENT IN TABLE                
         B     CHKMBT5                                                          
         DROP  R2                                                               
         EJECT                                                                  
*              BLDDUE - ROUTINE BUILDS DUE DATE ELEMENT                         
* AS OF JAN/2004 - DO NOT ADD THE DUE DATE ELEMENT FOR MEDIA PAYABLE            
* ACCOUNTS SINCE THE DUE DATE IS NOT A TRUE RECEIVABLE DUE DATE.                
*                                                                               
BLDDUE   NTR1                                                                   
         CLC   =C'SP',ACPACC       PRINT VENDORS                                
         BE     XIT                                                             
         CLC   =C'SQ',ACPACC       CANADIAN PRINT VENDORS                       
         BE     XIT                                                             
         CLC   =C'SS',ACPACC       SPOT VENDORS                                 
         BE     XIT                                                             
         CLC   =C'ST',ACPACC       CANDADIAN SPOT VENDORS                       
         BE     XIT                                                             
         CLC   =C'SU',ACPACC       NETWORK VENDORS                              
         BE     XIT                                                             
*                                                                               
         XC    ELEM,ELEM           PRE CLEAR ELEMENT                            
         LA    R2,ELEM             R2=A(ELEMENT TO BUILD)                       
         USING DUEELD,R2                                                        
         MVI   DUEEL,DUEELQ        ELEMET CODE X'61'                            
         MVI   DUELN,DUELNQ        ELEMENT LENGTH                               
         GOTO1 MXDTCN,DMCB,(3,BDUEDT),(2,DUEDATE)                               
         BAS   RE,ADDELEM          ADD NEW ELEMENT TO RECORD                    
         B     XIT                                                              
         DROP  R2                                                               
         SPACE 2                                                                
*              BLDGDT - ROUTINE BUILDS GENERAL DATE ELEMENT                     
*                                                                               
BLDGDT   NTR1                                                                   
         XC    ELEM,ELEM           PRE CLEAR ELEMENT                            
         LA    R2,ELEM             R2=A(ELEMENT TO BUILD)                       
         USING GDAELD,R2                                                        
         MVI   GDAEL,GDAELQ        ELEMENT CODE X'E5'                           
         MVI   GDALN,GDALNQ        ELEMENT LENGTH                               
         MVI   GDATYPE,GDATMBD     MEDIA BILLING DATE TYPE                      
         GOTO1 MXDTCN,DMCB,(0,BBILLDT),(1,GDADATE)                              
         BAS   RE,ADDELEM          ADD NEW ELEMENT TO RECORD                    
         B     XIT                                                              
         DROP  R2                                                               
         SPACE 2                                                                
*              BLDMBI - ROUTINE TO BUILD MY OWN ELEMENT (FOR REVERSALS)         
*                                                                               
BLDMBI   NTR1                                                                   
         XC    ELEM,ELEM           PRE CLEAR ELEMENT                            
         LA    R2,ELEM             R2=A(ELEMENT TO BUILD)                       
         USING MBIELD,R2                                                        
         MVI   MBIEL,MBIELQ        ELEMENT CODE X'F3'                           
         MVI   MBILN,MBILNQ        ELEMENT LENGTH                               
         MVC   MBISYS,SYSCH        MEDIA SYSTEM                                 
         MVC   MBIMED,MXMED        MEDIA                                        
         MVC   MBICLI,MXCLT        MEDIA CLIENT                                 
         MVC   MBIPRD,MXPRD        MEDIA PRODUCT                                
         MVC   MBIEST,BESTNUM      ESTIMATE NUMBER                              
         MVC   MBIMOS,BMOSDT       MONTH OF SERVICE                             
         MVC   MBITDAT,MXTODAY     DATE OF TRANSFER                             
         OC    MXRERNDT,MXRERNDT   IF DIE MODE - USE ORIG. TRANS DT             
         BZ    BLDMBI20                                                         
         GOTO1 MXDTCN,DMCB,(0,MXRERNDT),(2,MBITDAT)                             
BLDMBI20 BAS   RE,ADDELEM                                                       
         B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*              BLDAPT - ROUTINE TO BUILD ANALYSIS POINTER ELEMENT               
*                                                                               
         USING APEELD,R2                                                        
         USING APENTRY,R5                                                       
T        USING ACPRTND,RF                                                       
BLDAPT   NTR1                                                                   
         MVI   BYTE,0              KEEP COUNTER OF HOW MANY ACCTS               
         MVI   BYTE2,0             KEEP ACCUMULATED LENGTH                      
         XC    FULL,FULL           HOLDS THE DISPLACEMENT TO NEXT ENTRY         
         LA    R1,APELN1Q          OVERHEAD LENGTH                              
         ST    R1,FULL             SAVE THE DISPLACEMENT                        
         XC    ELEM,ELEM           PRE CLEAR ELEMENT                            
         LA    R2,ELEM             R2=A(ELEMENT TO BUILD)                       
         LR    R5,R2               R5 POINTS TO SUB-ELEMENTS                    
         MVI   APEEL,APEELQ        ELEMENT CODE X'C0'                           
         ZIC   RE,MXPSTN2          RE=(M'POSTINGS)                              
         L     RF,=V(POSTINGS)                                                  
BLDAPT05 LR    R5,R2                                                            
         A     R5,FULL             PT R5 TO RIGHT SUB ELEMENT                   
BLDAPT10 OC    0(ACPRTNL2,RF),0(RF)   TEST END OF POSTINGS                      
         BZ    BLDAPT60                                                         
         CLC   T.ACPACC,ACPACC     DON'T STORE THE ACCT CURRENTLY               
         BE    BLDAPT55            BEING PROCESSED                              
         MVC   SVACC(L'ACPACC),T.ACPACC                                         
         ST    RF,SVADDR           STORE ADDRESS OF CURRENT TABLE ENTRY         
         BRAS  RE,CHKDUP           DON'T STORE DUPLICATES                       
         BNE   BLDAPT55                                                         
         LA    R1,T.ACPACC                                                      
         LA    R1,L'ACPACC-1(R1)   POINT TO LAST BYTE OF ACCOUNT                
         LA    R3,L'ACPACC         MAX LENGTH OF ACCOUNT                        
BLDAPT30 CLI   0(R1),C' '                                                       
         BH    BLDAPT40                                                         
         BCTR  R1,0                                                             
         BCT   R3,BLDAPT30                                                      
*                                                                               
BLDAPT40 LTR   R3,R3               ANY LENGTH FOR THIS ACCOUNT?                 
         BZ    BLDAPT50            NO GET NEXT ACCOUNT                          
         LR    R1,R3               LENGTH OF ACCOUNT +                          
         AHI   R1,APELN2Q          OVERHEAD =                                   
         STC   R1,APENLEN          LENGTH OF SUB-ELEMENT                        
         TM    T.ACPSTAT,ACPDEB    DEBIT?                                       
         BZ    *+10                                                             
         MVC   APENSTAT,T.ACPSTAT  STORE DEBIT ONLY (ZERO IF CREDIT)            
         BCTR  R3,0                                                             
         EXMVC R3,APENACT,T.ACPACC MOVE IN ACCOUNT                              
         ZIC   R1,BYTE                                                          
         AHI   R1,1                                                             
         STC   R1,BYTE                                                          
         AHI   R3,1                BUMP BACK FOR REAL LENGTH                    
         AHI   R3,APELN2Q          ADD OVERHEAD                                 
         AR    R5,R3               BUMP TO NEXT OPEN ENTRY                      
         SR    R5,R2               AND STORE DISPL INTO FULL                    
         ST    R5,FULL                                                          
         ZIC   R1,BYTE2            ACCUMULATED LENGTH OF ALL ENTRIES            
         AR    R3,R1               ADD THIS ENTRY                               
         STC   R3,BYTE2                                                         
*                                                                               
BLDAPT50 LA    RF,ACPRTNL2(RF)     BUMP TO NEXT ACCOUTING ROW                   
         BCT   RE,BLDAPT05         LOOP                                         
         B     BLDAPT60                                                         
*                                                                               
BLDAPT55 LA    RF,ACPRTNL2(RF)     BUMP TO NEXT ACCOUTING ROW                   
         B     BLDAPT10                                                         
*                                                                               
BLDAPT60 CLI   BYTE,0              ADDED ANYTHING TO THIS ELEMENT?              
         BE    XIT                 NO                                           
         MVC   APENUM,BYTE         STORE # OF ACCOUNTS                          
         ZIC   R1,BYTE2            ACCUMULATED LENGTH                           
         AHI   R1,APELN1Q          ADD OVERHEAD                                 
         STC   R1,APELN                                                         
         BAS   RE,ADDELEM                                                       
         B     XIT                                                              
         DROP  R2,T                                                             
         EJECT                                                                  
*                                                                               
*        BLDINVL - ROUTINE BUILDS DB ELEM FOR LONG INVOICE NUMBER               
*                                                                               
BLDINVL  NTR1                                                                   
         CLC   MXLINV,SPACES                                                    
         BNH   XIT                                                              
         XC    ELEM,ELEM                                                        
         LA    R2,ELEM                                                          
         USING FFTELD,R2                                                        
         MVI   FFTEL,FFTELQ        X'DB'                                        
         MVI   FFTTYPE,FFTTINVN    INVOICE NUMBER (44)                          
         MVI   FFTSEQ,0                                                         
         LA    RF,MXLINV                                                        
         AHI   RF,L'MXLINV-1                                                    
         LHI   R1,L'MXLINV                                                      
BINVL10  CLI   0(RF),C' '                                                       
         BH    BINVL20                                                          
         SHI   RF,1                                                             
         BCT   R1,BINVL10                                                       
*                                                                               
BINVL20  STC   R1,FFTDLEN          ACTUAL LENGTH OF TEXT                        
         BCTR  R1,0                                                             
         LA    RF,MXLINV           POINT TO LONGER INVOICE NUMBER               
         CLI   ACPBIL2,2           IF AOR SCENARIO                              
         BNE   *+8                                                              
         LA    RF,MXTLINV          USE TRUE AOR LONGER INVOICE NUM              
         EXMVC R1,FFTDATA,0(RF)                                                 
         AHI   R1,1                RESTORE LENGTH                               
         LA    RE,FFTLN1Q          ELEMENT OVERHEAD                             
         AR    RE,R1                                                            
         AHI   RE,L'FFTDLEN                                                     
         STC   RE,FFTLN            LENGTH OF ELEMENT                            
         BAS   RE,ADDELEM                                                       
         B     XIT                                                              
         DROP  R2                                                               
*                                                                               
*        BLDCURR - ROUTINE BUILDS DB ELEM FOR CURRENCY                          
*                                                                               
BLDCURR  NTR1                                                                   
         CLI   MXCURR,X'40'                                                     
         BNH   XIT                                                              
*                                                                               
         XC    ELEM,ELEM                                                        
         LA    R2,ELEM                                                          
         USING FFTELD,R2                                                        
         MVI   FFTEL,FFTELQ        X'DB'                                        
         MVI   FFTTYPE,FFTTACUR    ASSOCIATED CURRENCY (243)                    
         MVI   FFTSEQ,0                                                         
         MVI   FFTDLEN,3           ACTUAL LENGTH OF TEXT                        
*                                                                               
         LA    RF,CURRTBL          CURRENCY TABLE                               
         USING CURRD,RF                                                         
         LA    R0,CURTBLN                                                       
CUR04    CLC   CURRA,MXCURR        MATCH ON CURRENCY ALPHA?                     
         BNE   CUR06                                                            
         MVC   FFTDATA(3),CURR     ACTUAL CURRENCY(USD/EUR/GBP/YEN/MXN)         
         B     CUR10                                                            
CUR06    LA    RF,CURRLEN(RF)                                                   
         BCT   R0,CUR04                                                         
         XC    ELEM,ELEM                                                        
         B     XIT                 IGNORE ANY OTHER VALUES                      
*                                                                               
CUR10    LLC   R1,FFTDLEN          ACTUAL LENGTH OF TEXT                        
         LA    RE,FFTLN1Q          ELEMENT OVERHEAD                             
         AR    RE,R1                                                            
         AHI   RE,L'FFTDLEN                                                     
         STC   RE,FFTLN            LENGTH OF ELEMENT                            
         BAS   RE,ADDELEM                                                       
         B     XIT                                                              
         DROP  R2                                                               
*                                                                               
* CURRENCY TABLE EQUATES                                                        
*                                                                               
CURRTBL  DC    C'U',C'USD'                                                      
         DC    C'E',C'EUR'                                                      
         DC    C'G',C'GBP'                                                      
         DC    C'Y',C'YEN'                                                      
         DC    C'P',C'MXN'                                                      
CURTBLN  EQU   (*-CURRTBL)/CURRLEN                                              
*                                                                               
*        BLDPO# - ROUTINE BUILDS DB ELEM FOR PO# AND PO LABLE OVERRIDE          
*                                                                               
BLDPO#   NTR1                                                                   
         CLC   MXPONUM,SPACES      IS THERE PO#?                                
         BNH   XIT                                                              
         XC    ELEM,ELEM                                                        
         LA    R2,ELEM                                                          
         USING FFTELD,R2                                                        
         MVI   FFTEL,FFTELQ        X'DB'                                        
         MVI   FFTTYPE,FFTTPO#     PO# ELEMENT                                  
         MVI   FFTSEQ,0                                                         
         MVI   FFTDLEN,L'MXPONUM   ACTUAL LENGTH OF TEXT                        
         LA    RE,FFTLN1Q          ELEMENT OVERHEAD                             
         LA    R1,L'FFTDLEN                                                     
         AR    RE,R1                                                            
         LA    R1,L'MXPONUM        LENGTH OF DATA                               
         AR    RE,R1                                                            
         STC   RE,FFTLN            LENGTH OF ELEMENT                            
         MVC   FFTDATA(L'MXPONUM),MXPONUM     PO#                               
         BAS   RE,ADDELEM                                                       
         CLC   MXPOLBL,SPACES      IS THERE PO LABLE OVERRIDE ?                 
         BNH   XIT                                                              
         XC    ELEM,ELEM                                                        
         LA    R2,ELEM                                                          
         USING FFTELD,R2                                                        
         MVI   FFTEL,FFTELQ        X'DB'                                        
         MVI   FFTTYPE,FFTTPOL     PO LABLE OVERRIDE ELEMENT                    
         MVI   FFTSEQ,0                                                         
         MVI   FFTDLEN,L'MXPOLBL   ACTUAL LENGTH OF TEXT                        
         LA    RE,FFTLN1Q          ELEMENT OVERHEAD                             
         LA    R1,L'FFTDLEN                                                     
         AR    RE,R1                                                            
         LA    R1,L'MXPOLBL        LENGTH OF DATA                               
         AR    RE,R1                                                            
         STC   RE,FFTLN            LENGTH OF ELEMENT                            
         MVC   FFTDATA(L'MXPOLBL),MXPOLBL     PO LABLE                          
         BAS   RE,ADDELEM                                                       
         B     XIT                                                              
         DROP  R2                                                               
*                                                                               
*              BLDRET - ROUTINE BUILDS RETAIL PARTICIPANT ACCOUNT               
*                                                                               
BLDRET   NTR1                                                                   
         CLI   ACPBTYP,MBTTRCV     IF CURRENT ACCOUNT IS RECEIVABLES            
         BE    BLDRET10                                                         
         CLI   ACPBTYP,MBTTINC     OR INCOME                                    
         BE    BLDRET10                                                         
         CLI   ACPBTYP,MBTTCD      OR CASH DISCOUNT                             
         BNE   BLDRETX                                                          
*                                                                               
BLDRET10 XC    ELEM,ELEM           PRE CLEAR ELEMENT                            
         LA    R2,ELEM             R2=A(ELEMENT TO BUILD)                       
         USING SPAELD,R2                                                        
         MVI   SPAEL,SPAELQ        SET ELEMENT TYPE                             
         MVI   SPALN,SPALNQ        SET ELEMENT LENGTH                           
         MVI   SPATYPE,SPATPART    PARTICPANT ACCOUNT                           
         MVI   SPAAUNT,C'3'        UNIT THREE                                   
         GOTO1 MXMXPROF,DMCB,MTPF3LDG,PWORK                                     
         MVC   SPAALDG,PWORK       SET LEDGER FROM RPROF                        
*                                                                               
         CLI   MXSYS,C'P'          IF PRINT SYSTEM                              
         BNE   BLDRET15                                                         
         MVC   SPAAACT,PBRACCT     SET RETAIL ACCOUNT                           
         OC    SPAAACT,SPACES      ENSURE PADDED WITH SPACES                    
         CLI   PBRETAIL,X'41'      CHECK CORP SUMMARY BILL                      
         BNE   BLDRET20                                                         
         B     BLDRET18                                                         
*                                                                               
BLDRET15 MVC   SPAAACT,BRETACCT    RETAIL PART. FROM SPOT/NET BILL              
         OC    SPAAACT,SPACES      ENSURE PADDED WITH SPACES                    
         CLI   BRETAIL,X'41'       IF CORP SUMMARY BILL                         
         BNE   BLDRET20                                                         
*                                                                               
BLDRET18 GOTO1 MXMXPROF,DMCB,MTPFMGOV,PWORK                                     
         OC    PWORK,PWORK         USE OVERRIDE MARGET GROUP IF ANY             
         BZ    *+10                                                             
         MVC   SPAAACT(4),PWORK                                                 
*                                                                               
BLDRET20 BAS   RE,ADDELEM          ADD ELEMENT TO RECORD                        
BLDRETX  B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*              SPLITEST - ROUTINE ADJUSTS EST DESC BASED ON SPEC. DELIM         
*                         1A ALL AFTER DELIM  &   A2 ALL BEFORE DELIM           
*                         NO DELIMITER 1A GETS EVERYTHING                       
*                                                                               
SPLITEST NTR1                                                                   
         MVI   SPLIT,C'N'          DEFAULT NOT TO SPLIT ESTIMATE                
         MVC   DESC1,SPACES        SPECIAL NUMBER DELIMITER                     
         MVC   DESC2,MXSVDESC      REAL ESTIMATE DESCRIPTION                    
         MVI   DESC1LN,0                                                        
         GOTO1 MXMXPROF,DMCB,MTPFDELM,PWORK                                     
         OC    PWORK,PWORK         IF SPECIAL NUMBER DELIMITER                  
         BZ    SPLEX                                                            
         LA    R2,MXSVDESC                                                      
         LA    R1,L'MXSVDESC                                                    
SPLE4    CLC   0(1,R2),PWORK       LOOK FOR IT                                  
         BE    SPLE6                                                            
         LA    R2,1(R2)                                                         
         BCT   R1,SPLE4                                                         
         B     SPLEX               NOT FOUND IN DESCRIPTION                     
*                                                                               
SPLE6    LR    R3,R2                                                            
         LA    R0,MXSVDESC+1                                                    
         SR    R3,R0               LENGTH-1 OF SPEC NO.                         
         BM    SPLEX               1ST CHAR IS DELIMITER - EXIT                 
         MVC   DESC2,DESC1         2ND HALF TO SPACES                           
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   DESC1(0),MXSVDESC                                                
         LA    R3,1(R3)            RESTORE LENGTH                               
         STC   R3,DESC1LN          SAVE LENGTH                                  
*                                                                               
         SH    R1,=H'2'            R1 HAS LENGTH OF 2ND HALF                    
         BM    SPLEX                                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   DESC2(0),1(R2)                                                   
         MVI   SPLIT,C'Y'                                                       
SPLEX    B     XIT                                                              
         EJECT                                                                  
*                                                                               
*              BLDDB - ROUTINE BUILDS BILL AND ELEMENT TYPE DATA (DB)           
*                                                                               
BLDDB    NTR1                                                                   
*                                                                               
         XC    ELEM,ELEM                                                        
         LA    R2,ELEM                                                          
         USING FFTELD,R2                                                        
         MVI   FFTEL,FFTELQ        X'DB'                                        
         MVI   FFTTYPE,FFTTMXTY    MEDIA TRANSFER INFO (67)                     
         MVI   FFTSEQ,0                                                         
         MVI   FFTDLEN,FFTMLN2Q    ACTUAL LENGTH OF TEXT                        
         LA    RE,FFTLN1Q          ELEMENT OVERHEAD                             
         LA    R1,L'FFTDLEN                                                     
         AR    RE,R1                                                            
         LA    R1,FFTMLN2Q         LENGTH OF DATA                               
         AR    RE,R1                                                            
         STC   RE,FFTLN            LENGTH OF ELEMENT                            
         MVC   FFTMXTE,ACPBTYP     ELEMENT CODE                                 
         MVC   FFTMXTB,MXDBTYPE    BILL TYPE                                    
         MVC   FFTMXTAG,MXALPHA    MEDIA AGY ALPHA                              
         MVC   FFTMXCO2,MXFLAG2    COST2 FLAG                                   
         MVC   FFTMXBTN,MXBTNUM    BILL TYPE NUMBER                             
         BAS   RE,ADDELEM                                                       
         B     XIT                                                              
         DROP  R2                                                               
*                                                                               
*              BLDCNET-ROUTINE BUILDS SAVED CALCULATED NET  DATA (DB)           
*                                                                               
BLDCNET  NTR1                                                                   
*                                                                               
         XC    ELEM,ELEM                                                        
         LA    R2,ELEM                                                          
         USING FFTELD,R2                                                        
         MVI   FFTEL,FFTELQ        X'DB'                                        
         MVI   FFTTYPE,FFTTCNET    MEDIA TRANSFER INFO (107)                    
         MVI   FFTSEQ,0                                                         
         MVI   FFTDLEN,FFTCALNQ    ACTUAL LENGTH OF TEXT                        
         LA    RE,FFTLN1Q          ELEMENT OVERHEAD                             
         LA    R1,L'FFTDLEN                                                     
         AR    RE,R1                                                            
         LA    R1,FFTCALNQ         LENGTH OF DATA                               
         AR    RE,R1                                                            
         STC   RE,FFTLN            LENGTH OF ELEMENT                            
         MVC   FFTCALNT,MXCNET                                                  
         BAS   RE,ADDELEM                                                       
         B     XIT                                                              
         DROP  R2                                                               
*                                                                               
*              DBADID - ROUTINE BUILDS AD-ID DB ELEMENT IF AD-ID EXISTS         
*                                                                               
DBADID   NTR1                                                                   
*                                                                               
         CLC   MXADID,SPACES                                                    
         BNH   XIT                 DOESN'T EXIST - DON'T BOTHER                 
         XC    ELEM,ELEM                                                        
         LA    R2,ELEM                                                          
         USING FFTELD,R2                                                        
         MVI   FFTEL,FFTELQ        X'DB'                                        
         MVI   FFTTYPE,FFTTADID    AD-ID (99)                                   
         MVI   FFTSEQ,0                                                         
         MVI   FFTDLEN,FFTADLQ     ACTUAL LENGTH OF TEXT                        
         LA    RE,FFTLN1Q          ELEMENT OVERHEAD                             
         LA    R1,L'FFTDLEN                                                     
         AR    RE,R1                                                            
         LA    R1,FFTADLQ          LENGTH OF DATA                               
         AR    RE,R1                                                            
         STC   RE,FFTLN            LENGTH OF ELEMENT                            
         MVC   FFTADID,MXADID                                                   
         BAS   RE,ADDELEM                                                       
         B     XIT                                                              
         DROP  R2                                                               
*                                                                               
*              AORDB - ROUTINE BUILDS DB ELEMENT FOR AOR INFO ON SR/SI          
*                      POSTINGS OF MAIN AOR BILL                                
         SPACE 1                                                                
T        USING ACPRTND,RF                                                       
AORDB    NTR1                                                                   
         XC    ELEM,ELEM                                                        
         LA    R2,ELEM                                                          
         USING FFTELD,R2                                                        
         MVI   FFTEL,FFTELQ        X'DB'                                        
         MVI   FFTTYPE,FFTTAOR     (96)                                         
         MVI   FFTSEQ,0                                                         
         MVI   FFTDLEN,FFTALNQ     ACTUAL LENGTH OF TEXT                        
         LA    RE,FFTLN1Q          ELEMENT OVERHEAD                             
         LA    R1,L'FFTDLEN                                                     
         AR    RE,R1                                                            
         LA    R1,FFTALNQ          LENGTH OF DATA                               
         AR    RE,R1                                                            
         STC   RE,FFTLN            LENGTH OF ELEMENT                            
*                                                                               
         ZIC   RE,MXPSTN2          RE=(M'POSTINGS)                              
         L     RF,=V(POSTINGS)                                                  
AORDB10  OC    0(ACPRTNL2,RF),0(RF)   TEST END OF POSTINGS                      
         BZ    AORDBX                                                           
         CLI   T.ACPBTYP,MBTTAPR   AOR PAY/RECV ENTRY?                          
         BE    AORDB20                                                          
         LA    RF,ACPRTNL2(RF)     BUMP TO NEXT ACCOUTING ROW                   
         BCT   RE,AORDB10          LOOP                                         
*                                                                               
AORDB20  MVC   FFTPRAOR,T.ACPACC     PAY/RECV ACCOUNT                           
         MVC   FFTRTAOR,MXTINV     TRUE AOR REFERENCE #                         
         ZAP   FFTAMAOR,T.ACPAMT2    AMOUNT                                     
         MVC   FFTSTAT2,T.ACPSTAT    DEBIT OR CREDIT                            
         BAS   RE,ADDELEM                                                       
*                                                                               
AORDBX   B     XIT                                                              
         DROP  R2,T                                                             
*                                                                               
*              BLDMT - ROUTINE BUILDS MEDIA TRANSFER ELEMENT                    
*                                                                               
BLDMT    NTR1                                                                   
         XC    ELEM,ELEM           PRE CLEAR ELEMENT                            
         XC    ELEM2,ELEM2                                                      
         LA    R2,ELEM             BUILD '1A' ELEMENT IN ELEM                   
         LA    R3,ELEM2            BUILD '6A' ELEMENT IN ELEM2                  
         USING MDTELD,R2                                                        
         USING MDPELD,R3                                                        
         OI    MXFLAG,OK1A          DEFAULT TO ADDING THE 1A ELEM               
         MVI   MDTEL,MDTELQ        ELEMENT CODE X'1A'                           
         MVI   MDPEL,MDPELQ        ELEMENT CODE X'6A'                           
*                                                                               
         MVI   MDTLN,MDTLNQ        ELEMENT LENGTH                               
         MVI   MDPLN,MDPLNQ                                                     
*                                                                               
         MVC   MDTSYS,SYSCH        MEDIA SYSTEM                                 
         MVC   MDPSYS,SYSCH                                                     
*                                                                               
         MVC   MDTMED,MXMED        MEDIA                                        
         MVC   MDPMED,MXMED                                                     
*                                                                               
         MVC   MDTCLI,MXCLT        CLIENT                                       
         MVC   MDPCLI,MXCLT                                                     
*                                                                               
         CLC   MXOCLT,SPACES       IF OVERRIDE CLIENT                           
         BE    *+16                                                             
         MVC   MDTCLI,MXOCLT       SET OVERRIDE                                 
         MVC   MDPCLI,MXOCLT                                                    
*                                                                               
         MVC   MDTPRD,MXPRD        PRODUCT                                      
         MVC   MDPPRD,MXPRD                                                     
*                                                                               
         CLC   MXOPRD,SPACES       IF OVERRIDE PRODUCT                          
         BE    *+16                                                             
         MVC   MDTPRD,MXOPRD       SET OVERRIDE                                 
         MVC   MDPPRD,MXOPRD                                                    
*                                                                               
         MVC   MDTEST,SPACES                                                    
         MVC   MDTEST(L'BESTNUM),BESTNUM ESTIMATE NUMBER                        
         MVC   MDPEST,SPACES                                                    
         MVC   MDPEST(L'BESTNUM),BESTNUM                                        
*                                                                               
         MVC   MDTMOS,BMOSDT       MONTH OF SERVICE                             
         MVC   MDPMOS,BMOSDT                                                    
*                                                                               
         MVC   MDTDTE,MXTODAY      DATE OF MEDIA TRANSFER                       
         MVC   MDPDTE,MXTODAY                                                   
*                                                                               
         CLI   MXQOPT1,C'D'        SPECIAL RE-RUN MODE?                         
         BNE   *+16                                                             
         MVC   MDTDTE,MXREQTDT     USE DATE OF FIRST MEDIA TRANSFER             
         MVC   MDPDTE,MXREQTDT                                                  
*                                                                               
         MVC   MDTDSCP,SPACES                                                   
         MVC   MDTDSCP(L'DESC2),DESC2                                           
         MVC   MDPDSCP,SPACES                                                   
         MVC   MDPDSCP(L'DESC2),DESC2                                           
         ZAP   MDPINTL,=P'0'       SET INTERNAL INCOME TO ZERO                  
*                                                                               
         CLI   MTAMTS,C'Z'         IF AMOUNTS TO SET                            
         BE    BLDMT70                                                          
*                                                                               
         CLI   ACPBTYP,MBTTAPR     IF AOR RECEIVABLE/PAYABLE POSTING            
         BNE   BLDMT8                                                           
         CLC   =C'SR',ACPACC       AND RECEIVABLE POSTING                       
         BNE   BLDMT8                                                           
         BAS   RE,MTAPR            SET AMOUNTS APPROPRIATELY                    
         B     BLDMT70                                                          
*                                                                               
BLDMT8   CLI   ACPBTYP,MBTTRCV     IF RECEIVABLE POSTING                        
         BE    *+12                                                             
         CLI   ACPBTYP,MBTTIRCV    OR INTERCOMPANY RECIEVABLE                   
         BNE   *+12                                                             
         BAS   RE,MTRCV            SET AMOUNTS APPROPRIATELY                    
         B     BLDMT60             DO REST OF THE AMOUNTS                       
*                                                                               
         BAS   RE,MTGRS            SET GROSS AMOUNT (GRS=)                      
         BAS   RE,MTCOM            SET COMMISSION AMOUNT (COM=)                 
*                                                                               
         CLI   ACPBTYP,MBTTSEL    IF SELLOFF INCOME                             
         BE    BLDMT70                                                          
         CLI   ACPBTYP,MBTTINTI   OR INTERNAL INCOME                            
         BE    BLDMT70            THAT IS IT - NO MORE AMOUNTS                  
         CLI   ACPBTYP,MBTTRINC   OR TRADE INCOME                               
         BE    BLDMT70                                                          
         CLI   ACPBTYP,MBTTOINC   OR OTHER INCOME (GROUPM MIDAS TRADE)          
         BE    BLDMT65                                                          
         CLI   ACPBTYP,MBTTOPIN   OR OPCO INCOME (GROUPM MIDAS TRADE)           
         BE    BLDMT70                                                          
         CLI   ACPBTYP,MBTTINC    IF MEDIA INCOME                               
         BNE   BLDMT40                                                          
         CLI   MXPREVI,C'Y'       ALREADY POSTED TO INCOME ACCOUNT?             
         BE    BLDMT70            YES -THAT'S IT, NO MORE AMOUNTS               
         MVI   MXPREVI,C'Y'       NO - BUT NOW WE HAVE - DO OTHER AMTS          
         B     BLDMT60                                                          
*                                                                               
BLDMT40  CLI   ACPBTYP,MBTTARI    IF AOR INCOME                                 
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   RE,CHKMINC         CHK MEDIA INCOME EXISTS                       
         BE    BLDMT70            YES -THAT'S IT, NO MORE AMOUNTS               
         CLI   MXPREVAI,C'Y'      ALREADY POSTED TO AOR INCOME ACCOUNT?         
         BE    BLDMT70            YES -THAT'S IT, NO MORE AMOUNTS               
         MVI   MXPREVAI,C'Y'      NO - BUT NOW WE HAVE - DO OTHER AMTS          
*                                                                               
BLDMT60  BAS   RE,MTNET           SET NET AMONT (NET=)                          
         BAS   RE,MTVAT           SET OUTPUT GST AMOUNT (GST=)                  
         BAS   RE,MTCD            SET CASH DISCOUNT AMOUNT                      
BLDMT65  BAS   RE,MTRECV          SET RCVBL AMOUNT                              
*                                                                               
BLDMT70  TM    MXFLAG,OK1A       DID ALL THE AMOUNTS FIT INTO 1A FIELDS         
         BO    BLDMT80           YES GO ADD 1A                                  
         XC    ELEM,ELEM         NO SO OVERWRITE ELEM WITH 6A INFO              
         MVC   ELEM,ELEM2        AND GO ADD 6A                                  
BLDMT80  BAS   RE,ADDELEM        ADD ELEMENT                                    
         B     XIT                                                              
         EJECT                                                                  
*                                                                               
*              BLDMT2 - ROUTINE BUILDS 2ND MEDIA TRANSFER ELEMENT (DB)          
*                                                                               
BLDMT2   NTR1                                                                   
*                                                                               
         XC    ELEM,ELEM                                                        
         LA    R2,ELEM                                                          
         USING FFTELD,R2                                                        
         MVI   FFTEL,FFTELQ        X'DB'                                        
         MVI   FFTTYPE,FFTTMTRA    MEDIA TRANSFER INFO (67)                     
         MVI   FFTSEQ,0                                                         
         MVI   FFTDLEN,FFTRLNQ     ACTUAL LENGTH OF TEXT                        
         LA    RE,FFTLN1Q          ELEMENT OVERHEAD                             
         LA    R1,L'FFTDLEN                                                     
         AR    RE,R1                                                            
         LA    R1,FFTRLNQ          LENGTH OF DATA                               
         AR    RE,R1                                                            
         STC   RE,FFTLN            LENGTH OF ELEMENT                            
         MVC   FFTRSYS,MXRSYS     REDEFINED SYSTEM CODE                         
         MVC   FFTRMED,MXRMED      REDEFINED MEDIA CODE                         
         BAS   RE,ADDELEM                                                       
         B     XIT                                                              
         DROP  R2                                                               
*                                                                               
         USING MDTELD,R2                                                        
MTAPR    NTR1                                                                   
         ZAP   DUB,=P'0'           PRE CLEAR AMOUNT                             
*        CLI   MXSYS,C'P'          PRINT SYSTEM?                                
*        BNE   MTAPR15             NO SO #'S CAN BE TOO BIG                     
         OC    ACPAMT2,ACPAMT2                                                  
         BZ    MTAPR15                                                          
         CP    ACPAMT2,HIAMT       CHECK IF TOO BIG FOR 1A ELEM                 
         BH    MTAPR10                                                          
         CP    ACPAMT2,LOAMT                                                    
         BNL   MTAPR15                                                          
MTAPR10  NI    MXFLAG,X'FF'-OK1A   TOO BIG SO SHUT OFF MXFLAG FOR 1A            
         OC    ACPAMT2,ACPAMT2     IF POSTING AMOUNT                            
         BZ    *+10                                                             
         ZAP   DUB,ACPAMT2         USE IT                                       
         ZAP   MDPRECV,DUB+2(6)    STORE IN 6A ELEM THAN                        
         B     MTAPR17                                                          
MTAPR15  OC    ACPAMT2,ACPAMT2     IF POSTING AMOUNT                            
         BZ    *+10                                                             
         ZAP   DUB,ACPAMT2         USE IT                                       
         CVB   RE,DUB                                                           
         STCM  RE,15,MDTRECV       RCVBL = ITS OWN POSTING (AOR+IT)             
         OC    ACPAMT2,ACPAMT2     IF POSTING AMOUNT                            
         BZ    *+10                                                             
         ZAP   DUB,ACPAMT2         USE IT                                       
         ZAP   MDPRECV,DUB+2(6)    STORE IN 6A ELEM TOO                         
*                                                                               
MTAPR17  GOTO1 GETAMNT,DMCB,MBTTGSTI                                            
*        CLI   MXSYS,C'P'          PRINT SYSTEM?                                
*        BNE   MTAPR25             NO SO #'S CAN BE TOO BIG                     
         OC    AMOUNT,AMOUNT                                                    
         BZ    MTAPR25                                                          
         CP    AMOUNT,HIAMT        CHECK IF TOO BIG FOR 1A ELEM                 
         BH    MTAPR20                                                          
         CP    AMOUNT,LOAMT                                                     
         BNL   MTAPR25                                                          
MTAPR20  NI    MXFLAG,X'FF'-OK1A    TOO BIG SO SHUT OFF MXFLAG FOR 1A           
         ZAP   DUB,AMOUNT                                                       
         MP    DUB,=P'-1'                                                       
         ZAP   MDPVAT,DUB+2(6)     STORE IN 6A ELEM THAN                        
         B     MTAPR27                                                          
MTAPR25  ZAP   DUB,AMOUNT                                                       
         MP    DUB,=P'-1'                                                       
         CVB   RE,DUB                                                           
         STCM  RE,15,MDTVAT       GST= INPUT GST AMOUNT X -1                    
         ZAP   MDPVAT,DUB+2(6)     STORE IN 6A ELEM TOO                         
                                                                                
MTAPR27  CP    MXTAMT,HIAMT        CHECK IF TOO BIG FOR 1A ELEM                 
         BH    MTAPR30                                                          
         CP    MXTAMT,LOAMT                                                     
         BNL   MTAPR35                                                          
                                                                                
MTAPR30  NI    MXFLAG,X'FF'-OK1A  TOO BIG SO SHUT OFF MXFLAG FOR 1A             
         ZAP   MDPCOM,MXTAMT      STORE IN 6A ELEM ALSO                         
         B     XIT                                                              
                                                                                
MTAPR35  ZAP   DUB,MXTAMT         IF PRINT STORED IN PACKED FORMAT              
         CVB   RE,DUB                                                           
         STCM  RE,15,MDTCOM                                                     
         B     XIT                                                              
         DROP  R2                                                               
         SPACE 2                                                                
*              MTRCV - ROUTINE SETS AMOUNTS FOR REGULAR SR POSTING              
*                                                                               
*** AS OF 4/25/01 FOR BBDO CONTINUE TO USE THE NEW WAY(CARRY GROSS              
*** MEMO FROM SR POSTING IF EXISTS.  FOR OTHER COMPANIES GO BACK                
*** TO THE OLD WAY(CARRY GROSS MEMO FROM INCOME POSTING)                        
*** AS OF 3/29/02 USE NEW WAY FOR BILLS ORIGINATING FROM THE BBDO               
*** FILE BUT POSTINGS BEING MADE TO THE OMD FILE.                               
*                                                                               
         USING MDTELD,R2                                                        
MTRCV    NTR1                                                                   
* COMMENTED OUT THIS CODE BECAUSE WE REALIZED THAT BBDO HAS ALREADY             
* BEEN USING THIS FEATURE FOR PRINT.  IT SEEMS THAT THE CASH DISCOUNT           
* WILL ALWAYS BE CARRIED ON SR (WITH FEW EXCEPTIONS).  YOU DO NOT NEED          
* TO SET A 'C' KEYWORD IN THE MEMO FIELD OF SR                                  
*        CLI   MXSYS,C'P'          IF PRINT THAN MUST DO THE OLD WAY            
*        BE    MTRCV05             UNTIL WE FIGURE OUT A WAY TO DO IT           
         CLI   MXCMP,X'F5'         BBDO?                                        
         BE    MTRCV02                                                          
         CLI   MXCMP,X'53'         OMD?                                         
         BNE   MTRCV05                                                          
         CLI   MXCMP2,X'F5'        AND IS BBDO THE ORIGINATING FILE?            
         BNE   MTRCV05             NO                                           
MTRCV02  OC    ACPMEMO2,ACPMEMO2   IF RCV POSTING CARRIES A MEMO                
         BZ    MTRCV05             AMOUNT USE IT AS OF FEB 2001                 
         GOTO1 =A(RCVMEMO),DMCB,(RC)                                            
         B     MTRCV18             THAN GO FILL IN THE INC AMOUNT               
*                                                                               
MTRCV05  GOTO1 GETMEMO,DMCB,MBTTRINC      TRADE INCOME                          
         BE    MTRCV10                                                          
         GOTO1 GETMEMO,DMCB,MBTTOINC      OTHER INC(GROUPM MIDAS TRADE)         
         BE    MTRCV10                                                          
         GOTO1 GETMEMO,DMCB,MBTTOPIN      OPCO INC(GROUPM MIDAS TRADE)          
         BE    MTRCV10                                                          
         GOTO1 GETMEMO,DMCB,MBTTINC                                             
         BE    MTRCV10                                                          
         GOTO1 GETMEMO,DMCB,MBTTARI                                             
MTRCV10  DS    0H                                                               
*        CLI   MXSYS,C'P'          PRINT SYSTEM?                                
*        BNE   MTRCV17             NO SO # CAN'T BE TOO BIG                     
         OC    MEMO,MEMO                                                        
         BZ    MTRCV17                                                          
         CP    MEMO,HIAMT          CHECK IF TOO BIG FOR 1A ELEM                 
         BH    MTRCV15                                                          
         CP    MEMO,LOAMT                                                       
         BNL   MTRCV17                                                          
MTRCV15  NI    MXFLAG,X'FF'-OK1A   TOO BIG SO SHUT OFF MXFLAG FOR 1A            
         ZAP   MDPGRS,MEMO         STORE IN 6A ELEM TOO                         
         B     MTRCV18                                                          
MTRCV17  ZAP   DUB,MEMO                                                         
         CVB   RE,DUB                                                           
         STCM  RE,15,MDTGRS       GRS= MEMO FROM MED/AOR INCOME                 
         ZAP   MDPGRS,MEMO         STORE IN 6A ELEM TOO                         
*                                                                               
MTRCV18  CLI   ACPBTYP,MBTTIRCV    INTERCOMP RECIEVABLE?                        
         BNE   MTRCV19                                                          
         GOTO1 GETAMNT,DMCB,MBTTIRCV      INTERCOMP RECIVABLE?                  
         BE    MTRCV20                                                          
MTRCV19  GOTO1 GETAMNT,DMCB,MBTTINC                                             
         BE    MTRCV20                                                          
         GOTO1 GETAMNT,DMCB,MBTTARI                                             
MTRCV20  DS    0H                                                               
*        CLI   MXSYS,C'P'          PRINT SYSTEM?                                
*        BNE   MTRCV30             NO SO # CAN'T BE TOO BIG                     
         OC    AMOUNT,AMOUNT                                                    
         BZ    MTRCV30                                                          
         CP    AMOUNT,HIAMT        CHECK IF TOO BIG FOR 1A ELEM                 
         BH    MTRCV25                                                          
         CP    AMOUNT,LOAMT                                                     
         BNL   MTRCV30                                                          
MTRCV25  NI    MXFLAG,X'FF'-OK1A  TOO BIG SO SHUT OFF MXFLAG FOR 1A             
         ZAP   MDPCOM,AMOUNT      STORE IN 6A ELEM THAN                         
         B     XIT                                                              
MTRCV30  ZAP   DUB,AMOUNT                                                       
         CVB   RE,DUB                                                           
         STCM  RE,15,MDTCOM       INC= AMOUNT FROM MED/AOR INCOME               
         ZAP   MDPCOM,AMOUNT      STORE IN 6A ELEM TOO                          
         B     XIT                                                              
         DROP  R2                                                               
         SPACE 2                                                                
*                                                                               
*            RCVMEMO - ROUTINE SETS GROSS AMOUNT FROM MEMO KEYWORD              
*                      FOR RECEIVABLE POSTING                                   
         USING MDTELD,R2                                                        
RCVMEMO  NTR1                                                                   
*        CLI   MXSYS,C'P'          PRINT SYSTEM?                                
*        BNE   RCVM20              NO SO # CAN'T BE TOO BIG                     
         OC    ACPMEMO2,ACPMEMO2                                                
         BZ    RCVM20                                                           
         CP    ACPMEMO2,HIAMT          CHECK IF TOO BIG FOR 1A ELEM             
         BH    RCVM10                                                           
         CP    ACPMEMO2,LOAMT                                                   
         BNL   RCVM20                                                           
RCVM10   NI    MXFLAG,X'FF'-OK1A   TOO BIG SO SHUT OFF MXFLAG FOR 1A            
         ZAP   MDPGRS,ACPMEMO2     STORE IN 6A ELEM TOO                         
         B     XIT                                                              
RCVM20   ZAP   DUB,ACPMEMO2                                                     
         CVB   RE,DUB                                                           
         STCM  RE,15,MDTGRS       GRS= MEMO FROM MED/AOR INCOME                 
         ZAP   MDPGRS,ACPMEMO2     STORE IN 6A ELEM TOO                         
         B     XIT                                                              
         DROP  R2                                                               
         SPACE 2                                                                
*                                                                               
*                                                                               
*              MTGRS - ROUTINE SETS GROSS AMOUNT                                
*                                                                               
         USING MDTELD,R2                                                        
MTGRS    NTR1                                                                   
         ZAP   DUB,=P'0'                                                        
*        CLI   MXSYS,C'P'          PRINT SYSTEM?                                
*        BNE   MTGRS15             NO SO # CAN'T BE TOO BIG                     
         OC    ACPMEMO2,ACPMEMO2                                                
         BZ    MTGRS15                                                          
         CP    ACPMEMO2,HIAMT      CHECK IF # TOO BIG FOR 1A                    
         BH    MTGRS10                                                          
         CP    ACPMEMO2,LOAMT                                                   
         BNL   MTGRS15                                                          
MTGRS10  NI    MXFLAG,X'FF'-OK1A    TOO BIG SO SHUT OFF MXFLAG FOR 1A           
         OC    ACPMEMO2,ACPMEMO2                                                
         BZ    *+10                                                             
         ZAP   DUB,ACPMEMO2                                                     
         ZAP   MDPGRS,DUB+2(6)    STORE IN 6A ELEM THAN                         
         B     XIT                                                              
MTGRS15  OC    ACPMEMO2,ACPMEMO2  GROSS AMT = GET ITS OWN MEMO                  
         BZ    *+10                                                             
         ZAP   DUB,ACPMEMO2                                                     
         CVB   RE,DUB                                                           
         STCM  RE,15,MDTGRS                                                     
         OC    ACPMEMO2,ACPMEMO2                                                
         BZ    *+10                                                             
         ZAP   DUB,ACPMEMO2                                                     
         ZAP   MDPGRS,DUB+2(6)    STORE IN 6A ELEM TOO                          
         B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*              MTCOM - ROUTINE SETS COMMISSION AMOUNT                           
*                                                                               
         USING MDTELD,R2                                                        
MTCOM    NTR1                                                                   
         ZAP   DUB,=P'0'                                                        
*        CLI   MXSYS,C'P'          PRINT SYSTEM?                                
*        BNE   MTCOM15             NO SO # CAN'T BE TOO BIG                     
         OC    ACPAMT2,ACPAMT2                                                  
         BZ    MTCOM15                                                          
         CP    ACPAMT2,HIAMT       CHECK IF # TOO BIG FOR 1A                    
         BH    MTCOM10                                                          
         CP    ACPAMT2,LOAMT                                                    
         BNL   MTCOM15                                                          
MTCOM10  NI    MXFLAG,X'FF'-OK1A  TOO BIG SO SHUT OFF MXFLAG FOR 1A             
         OC    ACPAMT2,ACPAMT2                                                  
         BZ    *+10                                                             
         ZAP   DUB,ACPAMT2                                                      
         ZAP   MDPCOM,DUB+2(6)    STORE IN 6A ELEM THAN                         
         B     XIT                                                              
MTCOM15  OC    ACPAMT2,ACPAMT2                                                  
         BZ    *+10                                                             
         ZAP   DUB,ACPAMT2                                                      
         CVB   RE,DUB                                                           
         STCM  RE,15,MDTCOM       COM AMOUNT=GET ITS OWN POSTING AMOUNT         
         OC    ACPAMT2,ACPAMT2                                                  
         BZ    *+10                                                             
         ZAP   DUB,ACPAMT2                                                      
         ZAP   MDPCOM,DUB+2(6)    STORE IN 6A ELEM TOO                          
         B     XIT                                                              
         DROP  R2                                                               
         SPACE 2                                                                
*              MTCD - ROUTINE SETS CASH DISCOUNT AMOUNT                         
*                                                                               
         USING MDTELD,R2                                                        
MTCD     NTR1                                                                   
         XC    MDTCD,MDTCD         PRE CLEAR CASH DISC AMOUNT                   
         CLI   MXSYS,C'P'          IF PRINT SYSTEM                              
         BNE   MTCDX                                                            
         CLI   MTAMTS,C'C'         AND IF SETTING CD AMOUNT                     
         BE    MTCDX                                                            
*                                                                               
         CLI   PBILCDSW,C'S'       IF NOT CD TYPE BILLS                         
         BE    MTCD5                                                            
         ZAP   DUB,PBILLGRS        GET CD FROM BILL RECORD DIRECTLY             
         SP    DUB,PBILLBIL                                                     
         CP    DUB+2(6),HIAMT      CHECK IF # IS TOO BIG FOR 1A ELEM            
         BH    MTCD2                                                            
         CP    DUB+2(6),LOAMT                                                   
         BNL   MTCD4                                                            
MTCD2    NI    MXFLAG,X'FF'-OK1A   TOO BIG SO TURN OFF MXFLAG FOR 1A            
         ZAP   MDPCD,DUB+2(6)      STORE IN 6A ELEM THAN                        
         B     MTCDX                                                            
MTCD4    CVB   RE,DUB                                                           
         STCM  RE,15,MDTCD                                                      
         ZAP   MDPCD,DUB+2(6)      STORE IN 6A ELEM TOO                         
         B     MTCDX                                                            
*                                                                               
MTCD5    CLI   PBILSEP,C'C'        IF CD BILL OF CD TYPES                       
         BE    MTCD10                                                           
         XC    MDTCD,MDTCD         MAIN CD BILL GETS NO CD                      
         ZAP   MDPCD,=P'0'                                                      
         B     MTCDX                                                            
*                                                                               
MTCD10   GOTO1 GETAMNT,DMCB,MBTTNET                                             
         OC    AMOUNT,AMOUNT                                                    
         BZ    MTCD25                                                           
         CP    AMOUNT,HIAMT        CHECK IF # IS TOO BIG FOR 1A                 
         BH    MTCD20                                                           
         CP    AMOUNT,LOAMT                                                     
         BNL   MTCD25                                                           
MTCD20   NI    MXFLAG,X'FF'-OK1A   TOO BIG SO TURN OF MXFLAG FOR 1A             
         ZAP   DUB,AMOUNT                                                       
         MP    DUB,=P'-1'                                                       
         ZAP   MDPCD,DUB+2(6)      STORE IN 6A ELEM THAN                        
         B     XIT                                                              
MTCD25   ZAP   DUB,AMOUNT                                                       
         MP    DUB,=P'-1'                                                       
         CVB   RE,DUB                                                           
         STCM  RE,15,MDTCD         CD AMOUNT=-(AMOUNT FROM SZ POST)             
         ZAP   MDPCD,DUB+2(6)      STORE IN 6A ELEM TOO                         
MTCDX    B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*              MTNET - ROUTINE SETS NET AMOUNT                                  
*                                                                               
         USING MDTELD,R2                                                        
MTNET    NTR1                                                                   
         GOTO1 GETAMNT,DMCB,MBTTNET                                             
*        CLI   MXSYS,C'P'          PRINT SYSTEM ?                               
*        BNE   MTNET15             NO THEN # CAN'T BE TOO BIG                   
         CLI   ACPBTYP,MBTTIRCV    INTERCOMP RECIEVABLE?                        
         BNE   *+10                                                             
         ZAP   AMOUNT,=P'0'        CLEAR OUT COM (PER JCLE)                     
         OC    AMOUNT,AMOUNT                                                    
         BZ    MTNET15                                                          
         CP    AMOUNT,HIAMT        CHECK IF # IS TOO BIG FOR 1A ELEM            
         BH    MTNET10                                                          
         CP    AMOUNT,LOAMT                                                     
         BNL   MTNET15                                                          
MTNET10  NI    MXFLAG,X'FF'-OK1A   TOO BIG SO TURN OFF MXFLAG FOR 1A            
         ZAP   MDPNET,AMOUNT       STORE IN 6A ELEM THAN                        
         B     XIT                                                              
MTNET15  ZAP   DUB,AMOUNT                                                       
         CVB   RE,DUB                                                           
         STCM  RE,15,MDTNET        SET NET AMOUNT                               
         ZAP   MDPNET,AMOUNT       STORE IN 6A ELEM TOO                         
         B     XIT                                                              
         DROP  R2                                                               
         SPACE 2                                                                
*              MTVAT - ROUTINE SETS GST AMOUNT                                  
*                                                                               
         USING MDTELD,R2                                                        
MTVAT    NTR1                                                                   
         GOTO1 GETAMNT,DMCB,MBTTGST                                             
*        CLI   MXSYS,C'P'          PRINT SYSTEM ?                               
*        BNE   MTVAT15             NO THEN # CAN'T BE TOO BIG                   
         OC    AMOUNT,AMOUNT                                                    
         BZ    MTVAT15                                                          
         CP    AMOUNT,HIAMT        CHECK IF # IS TOO BIG FOR 1A ELEM            
         BH    MTVAT10                                                          
         CP    AMOUNT,LOAMT                                                     
         BNL   MTVAT15                                                          
MTVAT10  NI    MXFLAG,X'FF'-OK1A   TOO BIG SO TURN OFF MXFLAG FOR 1A            
         ZAP   MDPVAT,AMOUNT       STORE IN 6A ELEM THAN                        
         B     XIT                                                              
MTVAT15  ZAP   DUB,AMOUNT                                                       
         CVB   RE,DUB                                                           
         STCM  RE,15,MDTVAT       SET OUTPUT GST AMOUNT (GST=)                  
         ZAP   MDPVAT,AMOUNT       STORE IN 6A ELEM TOO                         
         B     XIT                                                              
         DROP  R2                                                               
         SPACE 2                                                                
*              ROUTINE SETS RCVBL AMOUNT                                        
*                                                                               
         USING MDTELD,R2                                                        
MTRECV   NTR1                                                                   
         CLI   ACPBTYP,MBTTIRCV    INTERCOMP RECIEVABLE?                        
         BE    *+12                                                             
         CLI   ACPBTYP,MBTTOINC    OTHER INCOME MIDAS?                          
         BNE   MTRECV0                                                          
*                                                                               
         GOTO1 GETAMNT,DMCB,MBTTIRCV                                            
         B     MTRECV1                                                          
MTRECV0  GOTO1 GETAMNT,DMCB,MBTTRCV                                             
*        CLI   MXSYS,C'P'          PRINT SYSTEM ?                               
*        BNE   MTRECV4             NO THEN # CAN'T BE TOO BIG                   
MTRECV1  OC    AMOUNT,AMOUNT                                                    
         BZ    MTRECV4                                                          
         CP    AMOUNT,HIAMT        CHECK IF # IS TOO BIG FOR 1A ELEM            
         BH    MTRECV2                                                          
         CP    AMOUNT,LOAMT                                                     
         BNL   MTRECV4                                                          
MTRECV2  NI    MXFLAG,X'FF'-OK1A   TOO BIG SO TURN OFF MXFLAG FOR 1A            
         ZAP   MDPRECV,AMOUNT      STORE IN 6A ELEM THAN                        
         B     MTREC4A                                                          
MTRECV4  ZAP   DUB,AMOUNT                                                       
         CVB   RE,DUB                                                           
         STCM  RE,15,MDTRECV      RCVBL AMT= AMOUNT FROM SR POSTING             
         ZAP   MDPRECV,AMOUNT      STORE IN 6A ELEM TOO                         
*                                                                               
MTREC4A  CLI   MXSYS,C'P'          IF PRINT SYSTEM                              
         BNE   MTRECVX                                                          
         GOTO1 MXMXPROF,DMCB,MTPFRCSJ,PWORK                                     
         CLI   PWORK,C'Y'          IF POSTING TO SJ AND                         
         BNE   MTRECVX                                                          
         CLI   ACPBTYP,MBTTINC     CURRENT ACCOUNT IS AN SI                     
         BE    MTRECV5                                                          
         CLI   ACPBTYP,MBTTCD                                                   
         BE    MTRECV5                                                          
         CLI   ACPBTYP,MBTTSEL                                                  
         BE    MTRECV5                                                          
         CLI   ACPBTYP,MBTTINTI                                                 
         BE    MTRECV5                                                          
         CLI   ACPBTYP,MBTTARI                                                  
         BNE   MTRECVX                                                          
MTRECV5  XC    MDTRECV,MDTRECV     SET RCVBL = 0                                
         ZAP   MDPRECV,=P'0'                                                    
MTRECVX  B     XIT                                                              
         DROP  R2                                                               
*                                                                               
HIAMT    DC    PL6'2100000000'                                                  
LOAMT    DC    PL6'-2100000000'                                                 
         EJECT                                                                  
*              CHKMINC - ROUTINE TO CHECK FOR MEDIA INCOME ACCOUNT              
*                                                                               
CHKMINC  NTR1                                                                   
         ZIC   RE,MXPSTN2          RE=(M'POSTING ACCOUNTS)                      
         L     R4,=V(POSTINGS)     R4=A(POSTING ACCOUNTS)                       
CHKM10   CLI   ACPBTYP,MBTTINC     MATCH ON MEDIA INCOME ROW                    
         BE    YES                                                              
         LA    R4,ACPRTNL2(R4)     BUMP TO NEXT ACCOUTING ROW                   
         BCT   RE,CHKM10           LOOP                                         
         B     NO                  ROW NOT FOUND                                
         EJECT                                                                  
*              BLDFLD - ROUTINE BUILDS USER FIELD SELECT ELEMENT                
*                                                                               
BLDFLD   NTR1                                                                   
         NI    MXFLAG,X'FF'-SETUCOM                                             
         LA    R2,WORK                                                          
         CLI   SPLIT,C'Y'          IF SPLITTING ESTIMATE DESCRIPTION            
         BNE   BLDFLD02                                                         
         MVC   0(12,R2),=CL12'SPECIAL NO.'                                      
         GOTO1 SETA2,DMCB,=C'SN',(DESC1LN,(R2)),(DESC1LN,DESC1)                 
*                                                                               
BLDFLD02 TM    MXSVUSR,MXSVUP1     IF PRODUCT USER FIELD 1                      
         BZ    BLDFLD04                                                         
         LA    R2,MXPDESC1         R2=A(DESCRIPTION)                            
         GOTO1 SETA2,DMCB,=C'P1',(L'MXPUSER1,(R2)),                    +        
               (L'MXPUSER1,MXPUSER1)                                            
*                                                                               
BLDFLD04 TM    MXSVUSR,MXSVUP2     IF PRODUCT USER FIELD 2                      
         BZ    BLDFLD06                                                         
         LA    R2,MXPDESC2         R2=A(DESCRIPTION)                            
         GOTO1 SETA2,DMCB,=C'P2',(L'MXPUSER2,(R2)),                    +        
               (L'MXPUSER2,MXPUSER2)                                            
*                                                                               
BLDFLD06 TM    MXSVUSR,MXSVUE1     IF ESTIMATE USER FIELD 1                     
         BZ    BLDFLD08                                                         
         LA    R2,MXEDESC1         R2=A(DESCRIPTION)                            
         GOTO1 SETA2,DMCB,=C'E1',(L'MXEUSER1,(R2)),                    +        
               (L'MXEUSER1,MXEUSER1)                                            
*                                                                               
BLDFLD08 TM    MXSVUSR,MXSVUE2     IF ESTIMATE USER FIELD 2                     
         BZ    BLDFLD09                                                         
         LA    R2,MXEDESC2         R2=A(DESCRIPTION)                            
         GOTO1 SETA2,DMCB,=C'E2',(L'MXEUSER2,(R2)),                    +        
               (L'MXEUSER2,MXEUSER2)                                            
*                                                                               
BLDFLD09 CLC   MXEPONUM,SPACES     IF ESTIMATE PO NUMBER                        
         BE    BLDFLD10                                                         
         LA    R2,MXEPONUM         R2=A(ESTIMATE PO)                            
         GOTO1 SETA2,DMCB,=C'PO',(L'MXEPONUM,(R2)),                    +        
               (L'MXEPONUM,MXEPONUM)                                            
*                                                                               
BLDFLD10 CLC   MXUCMP1,SPACES      IF UCOM PRODUCT FIELD 1                      
         BE    BLDFLD12                                                         
         OI    MXFLAG,SETUCOM      SET BIT THAT UCOM NOT UDEF                   
         LA    R2,MXUCMP1D         R2=A(DESCRIPTION)                            
         GOTO1 SETA2,DMCB,=C'P1',(MXUP1LN,(R2)),(L'MXUCMP1,MXUCMP1)             
         NI    MXFLAG,X'FF'-SETUCOM                                             
*                                                                               
BLDFLD12 CLC   MXUCMP2,SPACES      IF UCOM PRODUCT FIELD 2                      
         BE    BLDFLD14                                                         
         OI    MXFLAG,SETUCOM      SET BIT THAT UCOM NOT UDEF                   
         LA    R2,MXUCMP2D         R2=A(DESCRIPTION)                            
         GOTO1 SETA2,DMCB,=C'P2',(MXUP2LN,(R2)),(L'MXUCMP2,MXUCMP2)             
         NI    MXFLAG,X'FF'-SETUCOM                                             
*                                                                               
BLDFLD14 CLC   MXUCMP3,SPACES      IF UCOM PRODUCT FIELD 3                      
         BE    BLDFLD16                                                         
         OI    MXFLAG,SETUCOM      SET BIT THAT UCOM NOT UDEF                   
         LA    R2,MXUCMP3D         R2=A(DESCRIPTION)                            
         GOTO1 SETA2,DMCB,=C'P3',(MXUP3LN,(R2)),(L'MXUCMP3,MXUCMP3)             
         NI    MXFLAG,X'FF'-SETUCOM                                             
*                                                                               
BLDFLD16 CLC   MXUCMP4,SPACES      IF UCOM PRODUCT FIELD 4                      
         BE    BLDFLD18                                                         
         OI    MXFLAG,SETUCOM      SET BIT THAT UCOM NOT UDEF                   
         LA    R2,MXUCMP4D         R2=A(DESCRIPTION)                            
         GOTO1 SETA2,DMCB,=C'P4',(MXUP4LN,(R2)),(L'MXUCMP4,MXUCMP4)             
         NI    MXFLAG,X'FF'-SETUCOM                                             
*                                                                               
BLDFLD18 CLC   MXFLDET,SPACES      ANY FLIGHT ID?                               
         BNH   BLDFLD19                                                         
         LA    RF,MXFLDET          FIGURE OUT HOW LONG THE FLIGHT ID IS         
         AHI   RF,L'MXFLDET-1                                                   
         LHI   R1,L'MXFLDET                                                     
BLDF18A  CLI   0(RF),C' '                                                       
         BH    BLDF18B                                                          
         SHI   RF,1                                                             
         BCT   R1,BLDF18A                                                       
*                                                                               
BLDF18B  STC   R1,DESC1LN                                                       
         CLI   MXFLPRF,1           WB FLIGHT SET TO USE E1?                     
         BNE   BLDFLD19                                                         
         OI    MXFLAG,SETUCOM      SET BIT THAT UCOM NOT UDEF                   
         MVC   0(9,R2),=CL9'WB FLIGHT'                                          
         GOTO1 SETA2,DMCB,=C'E1',(DESC1LN,(R2)),(L'MXFLDET,MXFLDET)             
         NI    MXFLAG,X'FF'-SETUCOM                                             
         B     BLDFLD20            IGNORE REAL EST UCOMM FIELD 1                
*                                                                               
BLDFLD19 CLC   MXUCME1,SPACES      IF UCOM ESTIMATE FIELD 1                     
         BE    BLDFLD20                                                         
         OI    MXFLAG,SETUCOM      SET BIT THAT UCOM NOT UDEF                   
         LA    R2,MXUCME1D         R2=A(DESCRIPTION)                            
         GOTO1 SETA2,DMCB,=C'E1',(MXUE1LN,(R2)),(L'MXUCME1,MXUCME1)             
         NI    MXFLAG,X'FF'-SETUCOM                                             
*                                                                               
BLDFLD20 CLC   MXFLDET,SPACES      ANY FLIGHT ID?                               
         BNH   BLDFLD21                                                         
         CLI   MXFLPRF,2           WB FLIGHT SET TO USE E2?                     
         BNE   BLDFLD21                                                         
         OI    MXFLAG,SETUCOM      SET BIT THAT UCOM NOT UDEF                   
         MVC   0(9,R2),=CL9'WB FLIGHT'                                          
         GOTO1 SETA2,DMCB,=C'E2',(DESC1LN,(R2)),(L'MXFLDET,MXFLDET)             
         NI    MXFLAG,X'FF'-SETUCOM                                             
         B     BLDFLD22            IGNORE REAL EST UCOMM FIELD 2                
*                                                                               
BLDFLD21 CLC   MXUCME2,SPACES      IF UCOM ESTIMATE FIELD 2                     
         BE    BLDFLD22                                                         
         OI    MXFLAG,SETUCOM      SET BIT THAT UCOM NOT UDEF                   
         LA    R2,MXUCME2D         R2=A(DESCRIPTION)                            
         GOTO1 SETA2,DMCB,=C'E2',(MXUE2LN,(R2)),(L'MXUCME2,MXUCME2)             
         NI    MXFLAG,X'FF'-SETUCOM                                             
*                                                                               
BLDFLD22 CLC   MXUCME3,SPACES      IF UCOM ESTIMATE FIELD 3                     
         BE    BLDFLD24                                                         
         OI    MXFLAG,SETUCOM      SET BIT THAT UCOM NOT UDEF                   
         LA    R2,MXUCME3D         R2=A(DESCRIPTION)                            
         GOTO1 SETA2,DMCB,=C'E3',(MXUE3LN,(R2)),(L'MXUCME3,MXUCME3)             
         NI    MXFLAG,X'FF'-SETUCOM                                             
*                                                                               
BLDFLD24 CLC   MXUCME4,SPACES      IF UCOM ESTIMATE FIELD 4                     
         BE    BLDFLD26                                                         
         OI    MXFLAG,SETUCOM      SET BIT THAT UCOM NOT UDEF                   
         LA    R2,MXUCME4D         R2=A(DESCRIPTION)                            
         GOTO1 SETA2,DMCB,=C'E4',(MXUE4LN,(R2)),(L'MXUCME4,MXUCME4)             
         NI    MXFLAG,X'FF'-SETUCOM                                             
*                                                                               
BLDFLD26 CLC   MXUCMM1,SPACES      IF UCOM MARKET FIELD 1                       
         BNH   BLDFLD28                                                         
         OI    MXFLAG,SETUCOM      SET BIT THAT UCOM NOT UDEF                   
         LA    R2,MXUCMM1D         R2=A(DESCRIPTION)                            
         GOTO1 SETA2,DMCB,=C'M1',(MXUM1LN,(R2)),(L'MXUCMM1,MXUCMM1)             
         NI    MXFLAG,X'FF'-SETUCOM                                             
*                                                                               
BLDFLD28 CLC   MXUCMM2,SPACES      IF UCOM MARKET FIELD 2                       
         BNH   BLDFLD30                                                         
         OI    MXFLAG,SETUCOM      SET BIT THAT UCOM NOT UDEF                   
         LA    R2,MXUCMM2D         R2=A(DESCRIPTION)                            
         GOTO1 SETA2,DMCB,=C'M2',(MXUM2LN,(R2)),(L'MXUCMM2,MXUCMM2)             
         NI    MXFLAG,X'FF'-SETUCOM                                             
*                                                                               
BLDFLD30 CLC   MXUCMM3,SPACES      IF UCOM MARKET FIELD 3                       
         BNH   BLDFLD32                                                         
         OI    MXFLAG,SETUCOM      SET BIT THAT UCOM NOT UDEF                   
         LA    R2,MXUCMM3D         R2=A(DESCRIPTION)                            
         GOTO1 SETA2,DMCB,=C'M3',(MXUM3LN,(R2)),(L'MXUCMM3,MXUCMM3)             
         NI    MXFLAG,X'FF'-SETUCOM                                             
*                                                                               
BLDFLD32 CLC   MXUCMM4,SPACES      IF UCOM MARKET FIELD 4                       
         BNH   BLDFLD34                                                         
         OI    MXFLAG,SETUCOM      SET BIT THAT UCOM NOT UDEF                   
         LA    R2,MXUCMM4D         R2=A(DESCRIPTION)                            
         GOTO1 SETA2,DMCB,=C'M4',(MXUM4LN,(R2)),(L'MXUCMM4,MXUCMM4)             
         NI    MXFLAG,X'FF'-SETUCOM                                             
*                                                                               
BLDFLD34 CLC   MXUCMR1,SPACES      IF UCOM REGION FIELD 1                       
         BNH   BLDFLD36                                                         
         OI    MXFLAG,SETUCOM      SET BIT THAT UCOM NOT UDEF                   
         LA    R2,MXUCMR1D         R2=A(DESCRIPTION)                            
         GOTO1 SETA2,DMCB,=C'R1',(MXUR1LN,(R2)),(L'MXUCMR1,MXUCMR1)             
         NI    MXFLAG,X'FF'-SETUCOM                                             
*                                                                               
BLDFLD36 CLC   MXUCMR2,SPACES      IF UCOM REGION FIELD 2                       
         BNH   BLDFLD38                                                         
         OI    MXFLAG,SETUCOM      SET BIT THAT UCOM NOT UDEF                   
         LA    R2,MXUCMR2D         R2=A(DESCRIPTION)                            
         GOTO1 SETA2,DMCB,=C'R2',(MXUR2LN,(R2)),(L'MXUCMR2,MXUCMR2)             
         NI    MXFLAG,X'FF'-SETUCOM                                             
*                                                                               
BLDFLD38 CLC   MXUCMR3,SPACES      IF UCOM REGION FIELD 3                       
         BNH   BLDFLD40                                                         
         OI    MXFLAG,SETUCOM      SET BIT THAT UCOM NOT UDEF                   
         LA    R2,MXUCMR3D         R2=A(DESCRIPTION)                            
         GOTO1 SETA2,DMCB,=C'R3',(MXUR3LN,(R2)),(L'MXUCMR3,MXUCMR3)             
         NI    MXFLAG,X'FF'-SETUCOM                                             
*                                                                               
BLDFLD40 CLC   MXUCMR4,SPACES      IF UCOM REGION FIELD 4                       
         BNH   BLDFLD42                                                         
         OI    MXFLAG,SETUCOM      SET BIT THAT UCOM NOT UDEF                   
         LA    R2,MXUCMR4D         R2=A(DESCRIPTION)                            
         GOTO1 SETA2,DMCB,=C'R4',(MXUR4LN,(R2)),(L'MXUCMR4,MXUCMR4)             
         NI    MXFLAG,X'FF'-SETUCOM                                             
*                                                                               
BLDFLD42 CLC   MXUCMD1,SPACES      IF UCOM DISTRICT FIELD 1                     
         BNH   BLDFLD44                                                         
         OI    MXFLAG,SETUCOM      SET BIT THAT UCOM NOT UDEF                   
         LA    R2,MXUCMD1D         R2=A(DESCRIPTION)                            
         GOTO1 SETA2,DMCB,=C'D1',(MXUD1LN,(R2)),(L'MXUCMD1,MXUCMD1)             
         NI    MXFLAG,X'FF'-SETUCOM                                             
*                                                                               
BLDFLD44 CLC   MXUCMD2,SPACES      IF UCOM DISTRICT FIELD 2                     
         BNH   BLDFLD46                                                         
         OI    MXFLAG,SETUCOM      SET BIT THAT UCOM NOT UDEF                   
         LA    R2,MXUCMD2D         R2=A(DESCRIPTION)                            
         GOTO1 SETA2,DMCB,=C'D2',(MXUD2LN,(R2)),(L'MXUCMD2,MXUCMD2)             
         NI    MXFLAG,X'FF'-SETUCOM                                             
*                                                                               
BLDFLD46 CLC   MXUCMD3,SPACES      IF UCOM DISTRICT FIELD 3                     
         BNH   BLDFLD48                                                         
         OI    MXFLAG,SETUCOM      SET BIT THAT UCOM NOT UDEF                   
         LA    R2,MXUCMD3D         R2=A(DESCRIPTION)                            
         GOTO1 SETA2,DMCB,=C'D3',(MXUD3LN,(R2)),(L'MXUCMD3,MXUCMD3)             
         NI    MXFLAG,X'FF'-SETUCOM                                             
*                                                                               
BLDFLD48 CLC   MXUCMD4,SPACES      IF UCOM DISTRICT FIELD 4                     
         BNH   BLDFLDX                                                          
         OI    MXFLAG,SETUCOM      SET BIT THAT UCOM NOT UDEF                   
         LA    R2,MXUCMD4D         R2=A(DESCRIPTION)                            
         GOTO1 SETA2,DMCB,=C'D4',(MXUD4LN,(R2)),(L'MXUCMD4,MXUCMD4)             
         NI    MXFLAG,X'FF'-SETUCOM                                             
*                                                                               
BLDFLDX  B     XIT                                                              
         EJECT                                                                  
*              SETA2 - ROUTINE TO SET UP A2 ELEMENT                             
*                                                                               
SETA2    NTR1                                                                   
         LM    R2,R4,0(R1)         R2=A(CODE),R3=A(DESC),R4=A(DATA)             
         ZIC   RF,8(R1)            RF=MAX LENGTH OF DATA                        
         ZIC   R5,4(R1)            R5=TRUE LENGTH OF DATA                       
*                                                                               
         XC    ELEM,ELEM                                                        
         LA    RE,ELEM                                                          
         USING UFSELD,RE                                                        
         MVI   UFSEL,UFSELQ       ELEMENT CODE X'A2'                            
**NO-OP* MVI   UFSSEQ,1           FIELD SEQUENCE NO                             
         MVC   UFSCODE,0(R2)      USER FIELD CODE                               
         MVC   UFSDESC,0(R3)      USER FIELD DESCRIPTION                        
         OC    UFSDESC,SPACES                                                   
         STC   RF,UFSMXLN         MAX LENGTH OF DATA                            
         BCTR  R5,0                                                             
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   UFSDATA(0),0(R4)   USER FIELD DATA                               
         EX    R5,*+8                                                           
         B     *+10                                                             
         OC    UFSDATA(0),SPACES   PAD WITH SPACES                              
         EX    R5,*+8                                                           
         B     *+10                                                             
         CLC   UFSDATA(0),SPACES   IF NO REAL DATA                              
         BE    SETA2X              DON'T ADD ELEMENT                            
         LA    R5,1+UFSDATA-UFSEL(R5)                                           
         STC   R5,UFSLN           TOTAL LENGTH OF ELEMENT                       
         NI    UFSSTAT,X'FF'-UFSUCOM                                            
         TM    MXFLAG,SETUCOM      IS THIS A UCOM OR UDEF?                      
         BZ    *+8                                                              
         OI    UFSSTAT,UFSUCOM     INDICATE THAT IT'S UCOM                      
         BAS   RE,ADDELEM                                                       
*                                                                               
SETA2X   B     XIT                                                              
         DROP  RE                                                               
         SPACE 2                                                                
*              BLDPRG - ROUTINE BUILDS MEDIA PRODUCT GROUP ELEMENT              
*                                                                               
BLDPGR   NTR1                                                                   
         CLC   MXPGRNM,SPACES      IF PRODUCT GROUP FOR THIS PRODUCT            
         BNH   BLDPGRX                                                          
         XC    ELEM,ELEM           PRE CLEAR ELEMENT                            
         LA    R2,ELEM             R2=A(ELEMENT TO BUILD)                       
         USING MPGELD,R2                                                        
         MVI   MPGEL,MPGELQ        ELEMENT CODE  X'D7'                          
         MVI   MPGLN,MPGLNQ        ELEMENT LENGTH                               
         MVC   MPGCODE,MXPGRCD     PRODUCT GROUP CODE                           
         MVC   MPGNME,MXPGRNM      PRODUCT GROUP NAME                           
         BAS   RE,ADDELEM          ADD ELEMENT TO RECORD                        
BLDPGRX  B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*              BLDSUB - BUILDS SUBSIDIARY CASH (MEMO) ELEMENT                   
*                                                                               
BLDSUB   NTR1                                                                   
         LA    R2,CDSW            CD SWITCHES                                   
         LA    RE,L'CDSW          NUMBER OF CD SWITCHES                         
         STC   RE,BYTE2                                                         
*                                                                               
BLDSUB10 CLI   0(R2),C'N'          IF ADDING ELEMENT                            
         BE    BLDSUB50                                                         
         CLI   0(R2),C'Z'          CHECK TO ADD ZERO INCOME ELEMENT             
         BNE   *+12                                                             
         BAS   RE,SUBZERO          ADD ZERO INCOME ELEMENT                      
         B     BLDSUB50                                                         
*                                                                               
         CLI   0(R2),C'I'          IF ADDING INCOME ELEMENT                     
         BNE   *+12                                                             
         BAS   RE,SUBINC           ADD INCOME ELEMENT                           
         B     BLDSUB50                                                         
*                                                                               
         CLI   0(R2),C'G'          IF ADDING GROSS ELEMENT                      
         BNE   *+12                                                             
         BAS   RE,SUBGRS           ADD GROSS ELEMENT                            
         B     BLDSUB50                                                         
*                                                                               
         CLI   0(R2),C'D'          IF ADDING CASH DISCOUNT                      
         BNE   *+12                                                             
         BAS   RE,SUBCD            ADD CD ELEMENT                               
         B     BLDSUB50                                                         
*                                                                               
         CLI   MXCNTRY,C'C'         IF CANADIAN                                 
         BNE   BLDSUB50                                                         
*                                                                               
         CLI   0(R2),C'P'          IF ADDING PAYABLE ELEMENT                    
         BNE   *+12                                                             
         BAS   RE,SUBPAY           ADD NET PAYABLE ELEMENT                      
         B     BLDSUB50                                                         
*                                                                               
         CLI   0(R2),C'O'          IF ADDING OUPUT GST/PST'S ELEMENT            
         BNE   *+12                                                             
         BAS   RE,SUBOVAT          ADD THEM                                     
         B     BLDSUB50                                                         
*                                                                               
         CLI   0(R2),C'T'          IF ADDING INPUT GST/PST'S ELEMENT            
         BNE   BLDSUB50                                                         
         BAS   RE,SUBIVAT          ADD THEM                                     
*                                                                               
BLDSUB50 LA    R2,1(R2)            BUMP TO NEXT CD ELEMENT SWITCH               
         ZIC   RE,BYTE2                                                         
         BCTR  RE,0                                                             
         LTR   RE,RE                                                            
         BZ    XIT                                                              
         STC   RE,BYTE2                                                         
         B     BLDSUB10            LOOP                                         
         EJECT                                                                  
*              SUBZERO - ROUTINE TO ADD ZERO INCOME ELEMENT                     
*                                                                               
SUBZERO  NTR1                                                                   
         MVI   BYTE,SCITINCA                                                    
         ZAP   AMOUNT,=P'0'                                                     
         BAS   RE,ADDSUB                                                        
         B     XIT                                                              
         SPACE 2                                                                
*              SUBINC - ROUTINE TO ADD INCOME ELEMENT                           
*                                                                               
SUBINC   NTR1                                                                   
         CLI   ACPBTYP,MBTTIRCV    INTERCOMP RECIEVABLE?                        
         BNE   SUBINC3                                                          
         GOTO1 GETAMNT,DMCB,MBTTIRCV                                            
         B     SUBINC5                                                          
SUBINC3  GOTO1 GETAMNT,DMCB,MBTTINC                                             
         BE    SUBINC5                                                          
         GOTO1 GETAMNT,DMCB,MBTTARI                                             
SUBINC5  MVI   BYTE,SCITINCA                                                    
         BAS   RE,ADDSUB                                                        
         B     XIT                                                              
         SPACE 2                                                                
*              SUBGRS - ROUTINE TO ADD GROSS MEMO ELEMENT                       
*                                                                               
SUBGRS   NTR1                                                                   
         MVI   BYTE,SCITGRSS                                                    
         ZAP   AMOUNT,=P'0'                                                     
         CLC   =C'REGM',MXDBTYPE   FOR REGM BILLS GET THE GROSS MEMO            
         BNE   SUBGRS10            AMOUNT FROM MIDAS EXCHANGE AND STORE         
         CLI   ACPBTYP,MBTTRCV     AS GROSS FOR RECEIVABLE AND OTHER            
         BE    SUBGRS05            INCOME ACCOUNT                               
         CLI   ACPBTYP,MBTTOINC                                                 
*        BE    SUBGRS05                                                         
*        CLI   ACPBTYP,MBTTOPIN    OPCO INCOME (MIDAS)                          
         BNE   SUBGRS10                                                         
SUBGRS05 OC    MXBNET,MXBNET                                                    
         BZ    *+10                                                             
         ZAP   AMOUNT,MXBNET                                                    
         B     SUBGRS20                                                         
*                                                                               
SUBGRS10 OC    ACPMEMO2,ACPMEMO2                                                
         BZ    *+10                                                             
         ZAP   AMOUNT,ACPMEMO2                                                  
SUBGRS20 BAS   RE,ADDSUB                                                        
         B     XIT                                                              
         EJECT                                                                  
*                                                                               
*&&DO                                                                           
SUBGRS2  NTR1                                                                   
         MVI   BYTE,SCITGRSS                                                    
         ZAP   AMOUNT,=P'0'                                                     
         OC    MXBNET,MXBNET                                                    
         BZ    *+10                                                             
         ZAP   AMOUNT,MXBNET                                                    
         BAS   RE,ADDSUB                                                        
         B     XIT                                                              
*&&                                                                             
*                                                                               
*              SUBCD - ROUTINE TO ADD CASH DISCOUNT MEMO ELEMENT                
*                                                                               
SUBCD    NTR1                                                                   
         MVI   BYTE,SCITCDSC                                                    
         CLI   PBILCMSW,C'S'       CD TYPE BILLS?                               
         BNE   SUBCD20                                                          
         CLI   PBILSEP,C'C'        CD BILL OF CD TYPE                           
         BE    SUBCD10                                                          
         ZAP   AMOUNT,=P'0'        CD MAIN GETS ZERO                            
         BAS   RE,ADDSUB                                                        
         B     SUBCDX                                                           
*                                                                               
SUBCD10  GOTO1 GETAMNT,DMCB,MBTTNET                                             
         MVI   BYTE,SCITCDSC       RESET BYTE                                   
         BAS   RE,ADDSUB           ADD CD MEMO                                  
         B     SUBCDX              CD BILL GETS SR AMOUNT POSTING               
*                                                                               
SUBCD20  ZAP   DUB,PBILLGRS                                                     
         SP    DUB,PBILLBIL                                                     
         MP    DUB,=P'-1'                                                       
         TM    PBILBASA,X'04'      BILL FORM SAYS LESS CD?                      
         BNO   SUBCD40             NO - THEN POSTING AMOUNT IS -C               
         BAS   RE,CHKRCV           YES -CHK IF RCVBL AMOUNT POSTING=B           
         BNE   SUBCD40             NO - THEN POSTING AMOUNT IS -C               
         MP    DUB,=P'-1'          YES -THEN CD IS C                            
SUBCD40  ZAP   AMOUNT,DUB                                                       
         BAS   RE,ADDSUB           ADD CD MEMO                                  
SUBCDX   B     XIT                                                              
         SPACE                                                                  
*              CHKRCV - CHECK RCVBL AMOUNT WAS B OR B+T                         
*                                 (I DID NOT MAKE UP THESE RULES)               
*                                                                               
CHKRCV   NTR1                                                                   
         ZIC   R0,MXPSTNA          R0=(M'POSTING ACCOUNTS)                      
         L     R4,=V(IOAREA)       R4=A(POSTING ACCOUNTS W/EXPRESSIONS)         
CHKRCV5  CLI   ACPBTYP,MBTTRCV     TEST RECEIVABLE ROW                          
         BE    CHKRCV10                                                         
         LA    R4,ACPRTNL(R4)      BUMP TO NEXT ACCOUNTING ROW                  
         BCT   R0,CHKRCV5          LOOP                                         
         DC    H'0'                RECEIVABLE ROW NOT FOUND                     
CHKRCV10 OC    ACPAMT,SPACES                                                    
         CLC   =CL8'B',ACPAMT      EXP = B, B+T                                 
         BE    *+10                                                             
         CLC   =CL8'B+T',ACPAMT                                                 
         B     XIT                                                              
         EJECT                                                                  
*              SUBPAY - ROUTINE TO ADD NET PAYABLE MEMO ELEMENT                 
SUBPAY   NTR1                                                                   
         MVI   BYTE,SCITGLEV                                                    
         ZAP   AMOUNT,=P'0'                                                     
         OC    ACPMEMO2,ACPMEMO2                                                
         BZ    *+10                                                             
         ZAP   AMOUNT,ACPMEMO2                                                  
         BAS   RE,ADDSUB                                                        
         B     XIT                                                              
         SPACE                                                                  
*              SUBOVAT - ROUTINE TO ADD OUTPUT 50 ELE. FOR GST/PST'S            
SUBOVAT  NTR1                                                                   
         GOTO1 GETAMNT,DMCB,MBTTGST                                             
         GOTO1 GETMEMO,DMCB,MBTTGST                                             
**NO-OP* CP    AMOUNT,=P'0'                                                     
**NO-OP* BNE   SUBOVAT1                                                         
**NO-OP* CP    MEMO,=P'0'                                                       
**NO-OP* BE    SUBOVAT2                                                         
SUBOVAT1 MVI   BYTE,SCITTAXP                                                    
         XC    PRVCODE,PRVCODE                                                  
         MVC   TAXCD,MXGSTO                                                     
         BAS   RE,ADDSUB3         ADD OUTPUT GST TAX AMOUNT                     
*                                                                               
SUBOVAT2 OC    MXPSTO(ACPMCODE*L'MXPSTO),MXPSTO                                 
         BZ    SUBOVATX                                                         
*                                                                               
         LA    R0,10                                                            
         LA    R2,MXPSTO                                                        
SUBOVAT5 OC    0(L'MXPSTO,R2),0(R2)                                             
         BZ    SUBOVAT6                                                         
         MVC   PRVCODE,0(R2)      SET PROVINCE CODE                             
         ZIC   RF,2(R2)                                                         
         GOTO1 GETAMNT,DMCB,(RF)                                                
         ZIC   RF,2(R2)                                                         
         GOTO1 GETMEMO,DMCB,(RF)                                                
         MVC   TAXCD,3(R2)        SET TAX CODE                                  
         MVI   BYTE,SCITTQST      SET ELEMENT TYPE                              
         BAS   RE,ADDSUB3         ADD OUTPUT PST TAX AMOUNT                     
*                                                                               
SUBOVAT6 LA    R2,L'MXPSTO(R2)                                                  
         BCT   R0,SUBOVAT5                                                      
*                                                                               
SUBOVATX B     XIT                                                              
         EJECT                                                                  
*              SUBIVAT - ROUTINE TO ADD INPUT 50 ELEMENTS FOR GST/PST           
SUBIVAT  NTR1                                                                   
         GOTO1 GETAMNT,DMCB,MBTTGSTI                                            
         GOTO1 GETMEMO,DMCB,MBTTGSTI                                            
**NO-OP* CP    AMOUNT,=P'0'                                                     
**NO-OP* BNE   SUBIVAT1                                                         
**NO-OP* CP    MEMO,=P'0'                                                       
**NO-OP* BE    SUBIVAT2                                                         
SUBIVAT1 MVI   BYTE,SCITTAXP                                                    
         XC    PRVCODE,PRVCODE                                                  
         MVC   TAXCD,MXGSTI                                                     
         BAS   RE,ADDSUB3         ADD INPUT GST TAX AMOUNT                      
*                                                                               
SUBIVAT2 OC    MXPSTI(ACPMCODE*L'MXPSTI),MXPSTI                                 
         BZ    SUBIVATX                                                         
*                                                                               
         LA    R0,10                                                            
         LA    R2,MXPSTI                                                        
SUBIVAT5 OC    0(L'MXPSTI,R2),0(R2)                                             
         BZ    SUBIVAT7                                                         
         MVC   PRVCODE,0(R2)      SET PROVINCE CODE                             
         ZIC   RF,2(R2)                                                         
         GOTO1 GETAMNT,DMCB,(RF)                                                
         ZIC   RF,2(R2)                                                         
         GOTO1 GETMEMO,DMCB,(RF)                                                
         MVC   TAXCD,3(R2)        SET TAX CODE                                  
         MVI   BYTE,SCITTQST      ELEMENT TYPE TO ADD                           
         BAS   RE,ADDSUB3         ADD INPUT PST TAX AMOUNT FOR PQ               
*                                                                               
SUBIVAT7 LA    R2,L'MXPSTI(R2)                                                  
         BCT   R0,SUBIVAT5                                                      
*                                                                               
SUBIVATX B     XIT                                                              
         EJECT                                                                  
*              ADDSUB - ROUTINE ADDS SUBISIDIARY CASH ELEMENT                   
*                                  NTRY - AMOUNT AND TYPE                       
*                                                                               
ADDSUB   NTR1                                                                   
         XC    ELEM,ELEM                                                        
         LA    R2,ELEM            R2=A(ELEMENT TO BUILD)                        
         USING SCIELD,R2                                                        
         MVI   SCIEL,SCIELQ       X'50'                                         
         MVI   SCILN,SCILN1Q      LENGTH                                        
         MVC   SCITYPE,BYTE       TYPE OF AMOUNT                                
         ZAP   SCIAMNT,AMOUNT     AMOUNT                                        
         BAS   RE,ADDELEM         ADD ELEMENT TO RECORD                         
         B     XIT                                                              
         SPACE 2                                                                
*              ADDSUB3- ROUTINE ADDS SUBISIDIARY CASH ELEMENT                   
*                                  NTRY - AMOUNT AND TYPE                       
*                                                                               
ADDSUB3  NTR1                                                                   
         XC    ELEM,ELEM                                                        
         LA    R2,ELEM            R2=A(ELEMENT TO BUILD)                        
         USING SCIELD,R2                                                        
         MVI   SCIEL,SCIELQ       X'50'                                         
         MVI   SCILN,SCILN3Q      EXPANDED LENGTH                               
         MVC   SCITYPE,BYTE       TYPE OF AMOUNT                                
         ZAP   SCIAMNT,AMOUNT     AMOUNT                                        
         MVC   SCISUBPR,SPACES    PRECLEAR TO SPACES (FOR GST)                  
         OC    PRVCODE,PRVCODE    IF PROVINCE CODE                              
         BZ    *+10                                                             
         MVC   SCISUBPR,PRVCODE   SET PROVINCE                                  
         MVC   SCISUBPT,TAXCD     TAX CODE                                      
         ZAP   SCIBASE,MEMO       BASIS AMOUNT                                  
*                                                                               
ADDSUB3X BAS   RE,ADDELEM         ADD ELEMENT TO RECORD                         
         B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*              BLDPAY - ROUTINE BUILDS EXTRA PAY ELEMENT                        
*                                                                               
BLDPAY   NTR1                                                                   
         CLI   PAYSW,C'Y'          IF PAY SWITCH ON                             
         BNE   BLDPAYX                                                          
         XC    ELEM,ELEM           PRE CLEAR ELEMENT                            
         LA    R2,ELEM             R2=A(ELEMENT TO BUILD)                       
         USING XPYELD,R2                                                        
         MVI   XPYEL,XPYELQ        ELEMENT CODE X'46'                           
         MVI   XPYLN,XPYLNQ        ELEMENT LENGTH                               
         ZAP   XPYCD,=P'0'         CASH DISCOUNT                                
         MVC   XPYCLI,MXSJCLTN     CLIENT NAME                                  
         MVC   XPYPRO,MXSJPRDN     PRODUCT NAME                                 
         MVC   XPYINV(L'MXTINV),MXTINV TRUE AOR INVOICE NUMBER                  
         GOTO1 MXDTCN,DMCB,(1,BINVDT),(X'20',XPYPER)                            
         MVI   XPYPER+6,C'-'                                                    
         MVC   XPYPER+7(6),XPYPER  INVOICE DATE - INVOICE DATE                  
         BAS   RE,ADDELEM         ADD ELEMENT TO RECORD                         
BLDPAYX  B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*              BLDOTH - ROUTINE BUILDS OTHER ELEMENT                            
*                                                                               
BLDOTH   NTR1                                                                   
         CLI   ACPACC,C'S'         IF CURRENT ACCOUNT IS A UNIT S               
         BNE   BLDOTHX                                                          
*                                                                               
         XC    ELEM,ELEM                                                        
         LA    R2,ELEM             R2=A(ELEMENT TO BUILD)                       
         USING OTHELD,R2                                                        
         MVI   OTHEL,OTHELQ        X'23'                                        
         MVI   OTHLN,OTHLN1Q       LENGTH                                       
         MVC   OTHNUM,SPACES       PRODUCT/ESTIMATE                             
         MVC   OTHPROF(2),SYSCH    MEDIA SYSTEM                                 
*                                                                               
         CLI   MXSYS,C'P'                                                       
         BNE   BLDOTH50                                                         
         CLC   MXJOBCD,SPACES                                                   
         BNE   BLDOTH20                                                         
         MVC   OTHPROF+2(3),BMOSDT MONTH OF SERVICE                             
         MVC   OTHNUM(3),MXPRD     PRODUCT                                      
         MVC   OTHNUM+3(3),BESTNUM ESTIMATE                                     
         B     BLDOTH80                                                         
*                                                                               
BLDOTH20 MVC   OTHNUM(6),MXJOBCD   JOB CODE FROM BILL RECORD                    
         CLI   PBILCMSW,C'S'                                                    
         BNE   BLDOTH40                                                         
         CLI   PBILSEP,C'C'        IF CD BILL & IF SPROF MAINT OVERRIDE         
         BNE   BLDOTH40                                                         
         CLC   MXSPJBCD,SPACES                                                  
         BNH   BLDOTH40                                                         
         MVC   OTHNUM(6),MXSPJBCD  USE IT                                       
BLDOTH40 MVC   OTHPROF(2),=C'J '   PRODUCTION                                   
         B     BLDOTH80                                                         
*                                                                               
BLDOTH50 MVC   OTHPROF+2(3),BMOSDT MONTH OF SERVICE                             
         MVC   OTHNUM(3),MXPRD     PRODUCT                                      
         MVC   OTHNUM+3(3),BESTNUM ESTIMATE                                     
         CLI   BRETAIL,0           STATION/NETWORK ONLY IF NON-RETAIL           
         BNE   BLDOTH80                                                         
         CLI   BLSTATN+1,C'0'      NUMERIC MEANS ITS A MGR                      
         BNL   BLDOTH80            (TEST MGR/NOT SCHEME)                        
         CLI   BLSTATN+1,C' '      BUT TEST IF PRESENT                          
         BNH   BLDOTH80                                                         
         MVC   OTHNET,BLSTATN      SET STATION/NETWORK                          
         MVI   OTHLN,OTHLN2Q       NEW LENGTH                                   
*                                                                               
BLDOTH80 BAS   RE,ADDELEM          ADD ELEMENT TO RECORD                        
*                                                                               
BLDOTHX  B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*              BLDMKT - ROUTINE SAVES MARKET IN FREE FORM X'DB'                 
*                                                                               
BLDMKT   NTR1                                                                   
         CLI   ACPBTYP,MBTTRCV     IF CURRENT ACCOUNT IS RECEIVABLES            
         BNE   BLDMKTX                                                          
         CLI   MXSYS,C'P'          BUT NOT PRINT SYSTEM                         
         BE    BLDMKTX                                                          
*                                                                               
* REMOVED THIS CHECK FOR RETAIL BILLS BECAUASE SPOT/NET BILL HEADERS            
* ARE NOW BIG ENOUGH TO HOLD BOTH MARKET NAME AND RETAIL CODE SO THEY           
* NO LONGER SHARE THE SAME SPOT ON THE BILL HEADER RECORD                       
*        CLC   =C'RET',MXBTYPE     NOT RETAIL BILL                              
*        BE    BLDMKTX                                                          
*                                                                               
         XC    ELEM,ELEM           PRE CLEAR ELEMENT                            
         LA    R2,ELEM             R2=A(ELEMENT TO BUILD)                       
         USING FFTELD,R2                                                        
         MVI   FFTEL,FFTELQ        SET ELEMENT TYPE                             
         LA    RE,FFTLN1Q          ELEMENT OVERHEAD                             
         LA    R1,L'FFTDLEN        ACTUAL LENGTH OF TEXT                        
         AR    RE,R1                                                            
         LA    R1,L'FFTMRKTC       LENGTH OF MARKET CODE                        
         AR    RE,R1                                                            
         STC   RE,FFTLN                  SAVE LENGTH                            
         MVI   FFTTYPE,FFTTMRKT          MARKET CODE EQUATE                     
         MVC   FFTDLEN,=AL1(L'FFTMRKTC)                                         
         MVC   FFTMRKTC,BLMKT            SAVE MARKET CODE                       
         OC    FFTMRKTC,SPACES           MAKE SURE PADDED WITH SPACES           
         CLC   FFTMRKTC,SPACES                                                  
         BNH   BLDMKTX                                                          
         MVC   FFTMRKTN(L'MXMRKTN),SPACES                                       
         OC    MXMRKTN,SPACES                                                   
         CLC   MXMRKTN,SPACES                                                   
         BE    BLDMKT08                  IF NO MRKT NAME YOUR DONE              
         LA    R1,MXMRKTN+L'MXMRKTN-1                                           
         CLI   0(R1),C' '                                                       
         BH    *+8                                                              
         BCT   R1,*-8                                                           
         LA    R0,MXMRKTN                                                       
         SR    R1,R0                                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   FFTMRKTN(0),MXMRKTN                                              
         LA    R1,1(R1)                                                         
         SR    RE,RE                                                            
         IC    RE,FFTLN                                                         
         AR    RE,R1                                                            
         STC   RE,FFTLN            SAVE ELEMENT LENGTH                          
         SR    RE,RE                                                            
         IC    RE,FFTDLEN                                                       
         AR    RE,R1                                                            
         STC   RE,FFTDLEN          SAVE DATA ENTRY LENGTH                       
*                                                                               
BLDMKT08 BAS   RE,ADDELEM          ADD ELEMENT TO RECORD                        
*                                                                               
BLDMKTX  B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*              BLDREG - ROUTINE SAVES REGION/DIST IN FREE FORM X'DB'            
*                                                                               
BLDREG   NTR1                                                                   
         CLI   ACPBTYP,MBTTRCV     IF CURRENT ACCOUNT IS RECEIVABLES            
         BNE   BLDREGX                                                          
         CLI   MXSYS,C'P'          ONLY PRINT SYSTEM                            
         BNE   BLDREGX                                                          
         CLC   =C'RET',MXBTYPE     NOT RETAIL BILL                              
         BE    BLDREGX                                                          
*                                  *** REGION ***                               
         XC    ELEM,ELEM           PRE CLEAR ELEMENT                            
         LA    R2,ELEM             R2=A(ELEMENT TO BUILD)                       
         USING FFTELD,R2                                                        
         MVI   FFTEL,FFTELQ        SET ELEMENT TYPE                             
         LA    RE,FFTLN1Q          ELEMENT OVERHEAD                             
         LA    R1,L'FFTDLEN        ACTUAL LENGTH OF TEXT                        
         AR    RE,R1                                                            
         LA    R1,L'FFTREGNC       LENGTH OF REGION CODE                        
         AR    RE,R1                                                            
         STC   RE,FFTLN                  SAVE LENGTH                            
         MVI   FFTTYPE,FFTTREGN          REGION CODE EQUATE                     
         MVC   FFTDLEN,=AL1(L'FFTREGNC)                                         
         MVC   FFTREGNC,MXREGCD          SAVE REGION CODE                       
         OC    FFTREGNC,SPACES           MAKE SURE PADDED WITH SPACES           
         CLC   FFTREGNC,SPACES                                                  
         BNH   BLDREG10                                                         
         MVC   FFTREGNN(L'MXREGNM),SPACES                                       
         OC    MXREGNM,SPACES                                                   
         CLC   MXREGNM,SPACES                                                   
         BE    BLDREG08                  IF NO REGION NAME YOUR DONE            
         LA    R1,MXREGNM+L'MXREGNM-1                                           
         CLI   0(R1),C' '                                                       
         BH    *+8                                                              
         BCT   R1,*-8                                                           
         LA    R0,MXREGNM                                                       
         SR    R1,R0                                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   FFTREGNN(0),MXREGNM                                              
         LA    R1,1(R1)                                                         
         SR    RE,RE                                                            
         IC    RE,FFTLN                                                         
         AR    RE,R1                                                            
         STC   RE,FFTLN            SAVE ELEMENT LENGTH                          
         SR    RE,RE                                                            
         IC    RE,FFTDLEN                                                       
         AR    RE,R1                                                            
         STC   RE,FFTDLEN          SAVE DATA ENTRY LENGTH                       
*                                                                               
BLDREG08 BAS   RE,ADDELEM          ADD ELEMENT TO RECORD                        
*                                                                               
*                                  *** DISTRICT ***                             
BLDREG10 XC    ELEM,ELEM           PRE CLEAR ELEMENT                            
         LA    R2,ELEM             R2=A(ELEMENT TO BUILD)                       
         USING FFTELD,R2                                                        
         MVI   FFTEL,FFTELQ        SET ELEMENT TYPE                             
         LA    RE,FFTLN1Q          ELEMENT OVERHEAD                             
         LA    R1,L'FFTDLEN        ACTUAL LENGTH OF TEXT                        
         AR    RE,R1                                                            
         LA    R1,L'FFTDISTC       LENGTH OF DISTRICT CODE                      
         AR    RE,R1                                                            
         STC   RE,FFTLN                  SAVE LENGTH                            
         MVI   FFTTYPE,FFTTDIST          DISTRICT CODE EQUATE                   
         MVC   FFTDLEN,=AL1(L'FFTDISTC)                                         
         MVC   FFTDISTC,MXDISTCD         SAVE DISTRICT CODE                     
         OC    FFTDISTC,SPACES           MAKE SURE PADDED WITH SPACES           
         CLC   FFTDISTC,SPACES                                                  
         BNH   BLDREGX                                                          
         MVC   FFTDISTN(L'MXDISTNM),SPACES                                      
         OC    MXDISTNM,SPACES                                                  
         CLC   MXDISTNM,SPACES                                                  
         BE    BLDREG18                  IF NO DIST NAME YOUR DONE              
         LA    R1,MXDISTNM+L'MXDISTNM-1                                         
         CLI   0(R1),C' '                                                       
         BH    *+8                                                              
         BCT   R1,*-8                                                           
         LA    R0,MXDISTNM                                                      
         SR    R1,R0                                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   FFTDISTN(0),MXDISTNM                                             
         LA    R1,1(R1)                                                         
         SR    RE,RE                                                            
         IC    RE,FFTLN                                                         
         AR    RE,R1                                                            
         STC   RE,FFTLN            SAVE ELEMENT LENGTH                          
         SR    RE,RE                                                            
         IC    RE,FFTDLEN                                                       
         AR    RE,R1                                                            
         STC   RE,FFTDLEN          SAVE DATA ENTRY LENGTH                       
*                                                                               
BLDREG18 BAS   RE,ADDELEM          ADD ELEMENT TO RECORD                        
*                                                                               
BLDREGX  B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*              ADDELEM - ROUTINE TO ADD ELEM IN NEXT AVAIL POSITION             
*                                                                               
ADDELEM  NTR1                                                                   
         ICM   R2,15,NXTRECE       POSTION TO INSERT NEXT ELEMENT               
         L     R3,=V(REC)                                                       
         GOTO1 MXRECUP,DMCB,(X'FF',2(R3)),ELEM,(R2)                             
         ZIC   R0,1(R2)            ELEMENT LENGTH                               
         AR    R2,R0               ADD TO TOTAL RECORD LENGTH                   
         STCM  R2,15,NXTRECE       NEW POSITION TO INSERT NEXT ELE              
         B     XIT                                                              
         EJECT                                                                  
*&&DO                                                                           
*              BLDAPE - ROUTINE ADDS 1C ACCOUNT TO SI POSTINGS                  
*                                                                               
BLDAPE   NTR1                                                                   
         XC    ELEM,ELEM           PRE CLEAR ELEMENT                            
         LA    R2,ELEM                                                          
         USING APEELD,R2                                                        
         MVI   APEEL,APEELQ                      ELEMENT CODE X'C0'             
         MVI   APELN,APELN1Q+APELN2Q+L'APENACT   ELEMENT LENGTH                 
         MVI   APENUM,1                          ONLY ONE SUB-ELEMENT           
         MVI   APENLEN,APELN2Q+L'APENACT         SUB-ELEMENT LENGTH             
         OI    APENSTAT,APENSDR                  POSTINGS WAS A DEBIT           
*                                                                               
         CLI   ACPBTYP,MBTTINTI     IF INTERNAL INCOME                          
         BNE   BLDAPE5                                                          
         GOTO1 GETACC,DMCB,MBTTINTC GET INTERNAL 1C ACCOUNT                     
         B     BLDAPE8                                                          
BLDAPE5  GOTO1 GETACC,DMCB,MBTTCOS  ELSE, GET REGULAR 1C ACCOUNT                
*                                                                               
BLDAPE8  OC    ACC,ACC                                                          
         BZ    BLDAPEX                                                          
         MVC   APENACT,ACC                                                      
         BAS   RE,ADDELEM                                                       
BLDAPEX  B     XIT                                                              
         DROP  R2                                                               
         SPACE 2                                                                
*&&                                                                             
*              GETMEMO - ROUTINE WILL SET MEMO AMOUNT                           
*                                  NTRY - ROW NUMBER                            
*                                  XIT  - MEMO & CC CODE SET                    
*                                                                               
GETMEMO  NTR1                                                                   
         MVC   BYTE,3(R1)          ROW NUMBER                                   
         ZAP   MEMO,=P'0'          PRE CLEAR MEMO AMOUNT                        
         ZIC   RE,MXPSTN2          RE=(M'POSTINGS)                              
         L     R4,=V(POSTINGS)     R4=A(POSTINGS)                               
*                                                                               
GETM5    CLC   ACPBTYP,BYTE        MATCH ON ROW NUMBER                          
         BE    GETM10                                                           
         LA    R4,ACPRTNL2(R4)     BUMP TO NEXT POSTING                         
         BCT   RE,GETM5            LOOP                                         
         B     NO                  REQUESTED POSTING NOT FOUND                  
*                                                                               
GETM10   OC    ACPMEMO2,ACPMEMO2                                                
         BZ    *+10                                                             
         MVC   MEMO,ACPMEMO2      SET MEMO AMOUNT                               
         B     YES                                                              
         EJECT                                                                  
*              GETAMNT - ROUTINE SETS AMOUNT                                    
*                                  NTRY - ROW NUMBER                            
*                                  XIT  - MEMO & CC CODE SET                    
*                                                                               
GETAMNT  NTR1                                                                   
         MVC   BYTE,3(R1)         ROW NUMBER                                    
         ZAP   AMOUNT,=P'0'       PRE CLEAR AMOUNT                              
         ZIC   RE,MXPSTN2         RE=(M'POSTINGS)                               
         L     R4,=V(POSTINGS)    R4=A(POSTINGS)                                
*                                                                               
GETA5    CLC   ACPBTYP,BYTE        MATCH ON ROW                                 
         BE    GETA10                                                           
         LA    R4,ACPRTNL2(R4)     BUMP TO NEXT POSTING                         
         BCT   RE,GETA5            LOOP                                         
         B     NO                  REQUESTED POSTING NOT FOUND                  
*                                                                               
GETA10   OC    ACPAMT2,ACPAMT2                                                  
         BZ    *+10                                                             
         MVC   AMOUNT,ACPAMT2     SET AMOUNT                                    
         B     YES                                                              
         SPACE 2                                                                
*              GETACC  - ROUTINE SETS ACC                                       
*                                  NTRY - ROW NUMBER                            
*                                  XIT  - ACC & CC CODE SET                     
*                                                                               
GETACC   NTR1                                                                   
         MVC   BYTE,3(R1)         ROW NUMBER                                    
         XC    ACC,ACC            PRE CLEAR ACCOUNT                             
         ZIC   RE,MXPSTN2         RE=(M'POSTINGS)                               
         L     R4,=V(POSTINGS)    R4=A(POSTINGS)                                
*                                                                               
GETAC5   CLC   ACPBTYP,BYTE        MATCH ON ROW                                 
         BE    GETAC10                                                          
         LA    R4,ACPRTNL2(R4)     BUMP TO NEXT POSTING                         
         BCT   RE,GETAC5           LOOP                                         
         B     NO                  REQUESTED POSTING NOT FOUND                  
*                                                                               
GETAC10  OC    ACPACC,ACPACC                                                    
         BZ    *+10                                                             
         MVC   ACC,ACPACC         SET POSTING ACCOUNT                           
         B     YES                                                              
         EJECT                                                                  
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*         
* CHECK FOR DUPLICATES WHEN WRITING CO ELEMENT                                  
* XIT - EQUAL IF NO DUPLICATE FOUND                                             
*     - NOT EQUAL IF DUPLICATE FOUND                                            
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*         
         SPACE 1                                                                
T        USING ACPRTND,RF                                                       
CHKDUP   NTR1  BASE=*,LABEL=*                                                   
         ZIC   RE,MXPSTN2          RE=(M'POSTINGS)                              
         L     RF,=V(POSTINGS)                                                  
CHKDUP10 OC    0(ACPRTNL2,RF),0(RF)   TEST END OF POSTINGS                      
         BZ    CHKDUPE                                                          
         CLC   SVACC,T.ACPACC                                                   
         BE    CHKDUP20                                                         
         LA    RF,ACPRTNL2(RF)     BUMP TO NEXT ACCOUTING ROW                   
         BCT   RE,CHKDUP10                                                      
*                                                                               
CHKDUP20 L     R1,SVADDR           SVADDR POINTS TO CURRENT ACCT BEING          
         CR    R1,RF               TESTED.  IF THIS ADDRESS = THEN OK           
         BE    CHKDUPE             ELSE YOU HAVE A DUPLICATE                    
*                                                                               
CHKDUPL  LTR   RC,RC                                                            
         B     CHKDUPX                                                          
CHKDUPE  SR    RC,RC                                                            
CHKDUPX  XIT1                                                                   
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*=============*                                                                 
* LITERAL POOL*                                                                 
*=============*                                                                 
*                                                                               
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                 DROP PREVIOUS BASE REGS                       
         SPACE                                                                  
         EJECT                                                                  
WORKD    DSECT                                                                  
*                                                                               
DMWORK   DS    12D                                                              
DUB      DS    D                                                                
         DS    2D                                                               
*                                                                               
DMCB     DS    6F                                                               
FULL     DS    F                                                                
NXTRECE  DS    F                   A(NEXT POSITION TO ADD ELEMENT)              
SVADDR   DS    A                                                                
         DS    3F                                                               
*                                                                               
BYTE     DS    XL1                                                              
BYTE2    DS    XL1                                                              
MXFLAG   DS    XL1                                                              
SETUCOM  EQU   X'80'                                                            
OK1A     EQU   X'40'                                                            
*                                                                               
SYSCH    DS    CL2                 SPOT/PRINT                                   
BESTNUM  DS    CL3                 BILL ESTIMATE NUMBER                         
BMOSDT   DS    PL3                 BILL MONTH OF SERVICE                        
BINVDT   DS    PL3                 BILL INVOICE DATE                            
BDUEDT   DS    XL3                 BILL DUE DATE                                
BBILLDT  DS    CL6                 BILL DATE                                    
*                                                                               
MEMO     DS    PL6                                                              
AMOUNT   DS    PL6                                                              
ACC      DS    CL14                UNIT/LEDGER/ACCOUNT NAME                     
*                                                                               
DESC1    DS    CL20                ESTIMATE DESC PART 1                         
DESC2    DS    CL20                ESTIMATE DESC PART 2                         
DESC1LN  DS    XL1                 LENGTH OF FIRST ESTIMATE DESC                
SPLIT    DS    CL1                 Y= SPLITTING ESTIMATE                        
MTSW     DS    XL1                 MEDIA TRANSFER ELEMENT SWITCH                
MTAMTS   DS    XL1                 MEDIA XFER AMOUNTS SWITCH                    
PAYSW    DS    XL1                 EXTRA PAY ELEMENT SWITCH                     
AORSW    DS    XL1                 AOR ELEMENT SWITCH                           
CDSW     DS    CL5                 CASH DISC ELEMENT SWITCH                     
APSW     DS    CL5                 ANALYSIS POINTER ELEMENT SWITCH              
PRVCODE  DS    CL2                 PROVINCE CODE                                
TAXCD    DS    CL1                 TAX CODE                                     
SVACC    DS    CL14                SAVED ACCOUNT CODE (ACPACC)                  
*                                                                               
PWORK    DS    CL20                WORK AREA FOR MXPROF                         
WORK     DS    CL100               WORK AREA                                    
SPACES   DS    CL132               SPACES                                       
ELEM     DS    CL256               ELEMENT                                      
ELEM2    DS    CL256               2ND ELEMENT                                  
WORKDL   EQU   *-WORKD                                                          
         EJECT                                                                  
       ++INCLUDE MXPOSTD                                                        
         EJECT                                                                  
*                                                                               
CURRD    DSECT                                                                  
CURRA    DS    CL1                 CURRENCY ALPHA                               
CURR     DS    CL3                 ASSOCIATED CURRENCY                          
CURRLEN  EQU   *-CURRD                                                          
*                                                                               
*=======================*                                                       
* OTHER VARIOUS DSECTS  *                                                       
*=======================*                                                       
*                                                                               
*  ACGENFILE                                                                    
*  ACGENPOST                                                                    
*  ACPOSTD                                                                      
*  SPGENBILL                                                                    
*  PBILLREC                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
       ++INCLUDE ACGENPOST                                                      
       ++INCLUDE ACPOSTD                                                        
       ++INCLUDE MXPRNTD                                                        
       ++INCLUDE MXTBDSECT                                                      
       ++INCLUDE ACTRAPRVD                                                      
BILLRECD DSECT                                                                  
       ++INCLUDE PBILLREC                                                       
         ORG   BILLRECD                                                         
       ++INCLUDE SPGENBILL                                                      
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'090MXPOST    06/21/19'                                      
         END                                                                    
