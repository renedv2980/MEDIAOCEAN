*          DATA SET FASECURE   AT LEVEL 004 AS OF 07/19/07                      
*CATALP FASECURE                                                                
***********************************************************************         
* Validate user PID and/or PIN from security field                    *         
*  Return person number or pin value.                                 *         
* Input Parameters:                                                   *         
*  Parm 1 -   A(Security Field Header)                                *         
*       2 -   A(Security Block - Optional)                            *         
*       3 -   A(Comfacs)                                              *         
*       4 -   A(5 byte return field)                                  *         
*             Return data -  1 byte type: x'02' PIN                   *         
*                                         x'03' PID                   *         
*                            4 byte PID: 2 byte Agency Alpha          *         
*                                        2 byte binary person number  *         
*                            4 byte PIN: Users Pin number             *         
***********************************************************************         
SECURE   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKL,**SECU**,R8,CLEAR=YES                                      
         USING WORKD,RC            RC=A(W/S)                                    
*                                                                               
         ST    R1,APARMS                                                        
         MVC   PARM(PARML),0(R1)   SAVE INPUT PARMS                             
*                                                                               
         L     RF,ACOMFACS                                                      
         MVC   VDATAMGR,CDATAMGR-COMFACSD(RF)                                   
         MVC   VSECRET,CSECRET-COMFACSD(RF)                                     
         MVC   VSCANNER,CSCANNER-COMFACSD(RF)                                   
         MVC   VDATCON,CDATCON-COMFACSD(RF)                                     
         MVC   VDICTATE,CDICTATE-COMFACSD(RF)                                   
*                                                                               
         XR    R8,R8                                                            
         ICM   R8,15,ASECBLK                                                    
         BNZ   SECU03                                                           
         LA    R8,SECBLK                                                        
         ST    R8,ASECBLK                                                       
         USING SECD,R8                                                          
         GOTOR VSECRET,DMCB,('SECPINIT',ASECBLK),0                              
         BNL   *+6                                                              
         DC    H'0'                CAN'T INITIALISE SECRET                      
*                                                                               
SECU03   LA    R4,WORK             TEST FOR PID= OR PIN=                        
         USING SCANBLKD,R4                                                      
         GOTO1 VSCANNER,DMCB,ASECFLDH,(2,(R4))                                  
         XR    R0,R0                                                            
         ICM   R0,1,4(R1)          GET NUMBER OF SCAN LINES                     
         BZ    ERRXIT              INVALID INPUT                                
         CHI   R0,1                                                             
         BH    ERRXIT                                                           
         XR    R1,R1                                                            
         ICM   R1,1,SC1STLEN                                                    
         BZ    ERRXIT                                                           
         CLC   SC1STFLD(L'DC@PID),DC@PID                                        
         BE    SECU05                                                           
         CLC   SC1STFLD(L'DC@PIN),DC@PIN                                        
         BE    SECU20                                                           
         B     ERRXIT                                                           
*                                                                               
SECU05   XR    R1,R1               TEST PID=XXXXXXXX OR PID=*                   
         ICM   R1,1,SC2NDLEN                                                    
         BZ    ERRXIT                                                           
         CHI   R1,1                                                             
         BH    SECUR07                                                          
         CLI   SC2NDFLD,C'*'       TEST PID OF USER                             
         BNE   ERRXIT              YES,                                         
         MVC   PERID,SECPID        PERSONAL ID                                  
         B     SECU09                                                           
*                                                                               
SECUR07  BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   PERID(0),SC2NDFLD   SAVE OPTIONAL PID                            
*                                                                               
SECU09   LA    R2,IOKEY                                                         
         USING SAPEREC,R2          READ PERSON RECORD                           
         XC    SAPEKEY,SAPEKEY                                                  
         MVI   SAPETYP,SAPETYPQ                                                 
         MVI   SAPESUB,SAPESUBQ                                                 
         MVC   SAPEAGY,SECAGY                                                   
         OC    SECOAGPE,SECOAGPE   SWITCH TO SECURITY AGY FOR PERSON            
         BZ    *+10                                                             
         MVC   SAPEAGY,SECOAGPE                                                 
         MVC   SAPEPID,PERID                                                    
         OC    SAPEPID,SPACES                                                   
         GOTOR VDATCON,DMCB,(5,0),(2,SAPEDEF)                                   
         XC    SAPEDEF,EFFS        COMPLEMENT DATE                              
         GOTOR RDHISA                                                           
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   SAPEKEY(SAPEDEF-SAPEKEY),IOAREA                                  
         BNE   ERRXIT              NO RECORD FOUND FOR THIS PERSON              
         DROP  R2                                                               
*                                                                               
         LA    R3,IOAREA+(SAPEDATA-SAPEREC)                                     
SECU11   CLI   0(R3),0             TEST E-O-R                                   
         BE    ERRXIT                                                           
         CLI   0(R3),SAPWDELQ      TEST PERSON PASSWORD ELEMENT                 
         BE    SECU13                                                           
         XR    RF,RF                                                            
         IC    RF,1(R3)                                                         
         AR    R3,RF                                                            
         B     SECU11                                                           
*                                                                               
         USING SAPWDD,R3                                                        
SECU13   MVC   RTNPID(2),SECOAGY     RETURN AGENCY                              
         MVC   RTNPID+2(2),SAPWDNUM  AND PASSWORD NUMBER                        
         MVI   RTNTYP,RTNTPIDQ                                                  
         B     RETURN                                                           
         DROP  R3                                                               
*                                                                               
SECU20   XR    R1,R1               PIN=XXXX                                     
         ICM   R1,1,SC2NDLEN       MUST HAVE RIGHT SIEDE                        
         BZ    ERRXIT                                                           
         CHI   R1,4                                                             
         BH    ERRXIT              NOT MORE THAN 4                              
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   RTNPIN(0),SC2NDFLD  RETURN PIN                                   
         MVI   RTNTYP,RTNTPINQ                                                  
         B     RETURN                                                           
*                                                                               
ERRXIT   XC    RTN(RTNL),RTN                                                    
RETURN   L     RF,ARETURN                                                       
         MVC   0(RTNL,RF),RTN                                                   
         B     XIT                                                              
         DROP  R4,R8                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO READ SECURITY ACCESS RECORD                              *         
***********************************************************************         
                                                                                
READSA   NTR1  ,                                                                
         MVC   IOAREA(L'IOKEY),IOKEY                                            
         GOTOR VDATAMGR,DMCB,DMREAD,CTFILE,IOAREA,IOAREA                        
         B     XIT                                                              
                                                                                
***********************************************************************         
* ROUTINE TO READ HIGH FOR A SECURITY ACCESS RECORD                   *         
***********************************************************************         
                                                                                
RDHISA   NTR1  ,                                                                
         MVC   IOAREA(L'IOKEY),IOKEY                                            
         GOTOR VDATAMGR,DMCB,DMRDHI,CTFILE,IOAREA,IOAREA                        
         B     XIT                                                              
                                                                                
***********************************************************************         
* ROUTINE TO READ SEQUENTIAL FOR A SECURITY ACCESS RECORD             *         
***********************************************************************         
                                                                                
RDSQSA   NTR1  ,                                                                
         MVC   IOAREA(L'IOKEY),IOKEY                                            
         GOTOR VDATAMGR,DMCB,DMRDSQ,CTFILE,IOAREA,IOAREA                        
         B     XIT                                                              
                                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* DATA CONSTANTS AND LITERAL POOL                                     *         
***********************************************************************         
FF       EQU   X'FF'                                                            
EFFS     DC    X'FFFFFFFFFFFFFFFF'                                              
SPACES   DC    CL132' '                                                         
*                                                                               
DMREAD   DC    C'DMREAD '          FOR DATA MANAGER CALLS                       
DMRDHI   DC    C'DMRDHI '                                                       
DMRDSQ   DC    C'DMRSEQ '                                                       
CTFILE   DC    C'CTFILE '                                                       
*                                                                               
DC@PID   DC    CL8'PID'                                                         
DC@PIN   DC    CL8'PIN'                                                         
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* LOCAL WORKING STORAGE                                               *         
***********************************************************************         
WORKD    DSECT                                                                  
APARMS   DS    A                   A(INPUT PARMS)                               
*                                                                               
PARM     DS    0F                  INPUT PARAMTERS                              
ASECFLDH DS    A                   A(FIELD HEADER)                              
ASECBLK  DS    A                   A(SECRECT BLOCK)                             
ACOMFACS DS    A                   A(COMFACS)                                   
ARETURN  DS    A                   A(RETURN DATA)                               
PARML    EQU   *-PARM                                                           
*                                                                               
VDATAMGR DS    V(DATAMGR)          A(DATA MANAGER)                              
VSECRET  DS    V(SECRET)           A(SECRET)                                    
VSCANNER DS    V(SCANNER)          A(SCANNER)                                   
VDATCON  DS    V(DATCON)           A(DATCON)                                    
VDICTATE DS    V(DICTATE)          A(DICTATE)                                   
*                                                                               
DMCB     DS    8A                                                               
WORK     DS    CL240                                                            
*                                                                               
RTN      DS    0X                  RETURN BLOCK                                 
RTNTYP   DS    XL1                 DATA TYPE                                    
RTNTPINQ EQU   2                    PIN                                         
RTNTPIDQ EQU   3                    PID                                         
RTNPID   DS    XL4                                                              
         ORG   RTNPID                                                           
RTNPIN   DS    CL4                                                              
RTNL     EQU   *-RTN                                                            
*                                                                               
PERID    DS    CL8                 PERSONAL ID                                  
*                                                                               
SECBLK   DS    XL(SECLENQ)         SECRET BLOCK                                 
*                                                                               
IOKEY    DS    XL(L'SAASKEY)       IO KEY                                       
IOAREA   DS    XL2048              IO AREA                                      
*                                                                               
WORKL    EQU   *-WORKD                                                          
                                                                                
         EJECT                                                                  
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
                                                                                
* DDDSCANBLKD                                                                   
         PRINT OFF                                                              
       ++INCLUDE DDSCANBLKD                                                     
         PRINT ON                                                               
                                                                                
* FASECRETD                                                                     
         PRINT OFF                                                              
       ++INCLUDE FASECRETD                                                      
         PRINT ON                                                               
                                                                                
* SEACSFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE SEACSFILE                                                      
         PRINT ON                                                               
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004FASECURE  07/19/07'                                      
         END                                                                    
