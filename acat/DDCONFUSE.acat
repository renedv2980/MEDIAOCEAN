*          DATA SET DDCONFUSE  AT LEVEL 019 AS OF 05/01/02                      
*CATALP CONFUSE                                                                 
*                                                                               
* PARAMETER 1   BYTE  0       C'F' = FIRST CALL                                 
*                             C'H' = READ HIGH (SEQUENCE BROKEN)                
*                             C'S' = READ SEQUENTIAL FOR NEXT                   
*                             AFTER CONFUSE SET TO C'S' IF OK                   
*               BYTES 1-3     A(KEY SPEC.)                                      
*                             KEY SPEC. CONSISTS OF -                           
*                             RECORD TYPE    CL1       F,O OR U                 
*                             SYSTEM         CL1                                
*                             AGENCY         CL1/2                              
*                             PROGRAM        CL2/3                              
* PARAMETER 2   BYTES 0-3     A(I/O AREA)                                       
* PARAMETER 3   BYTE  0       AFTER CONFUSE INDICATES ERROR CONDITIONS          
*                             X'00' OK                                          
*                             X'01' UNRECOGNISED RECORD TYPE OR SYSTEM          
*                             X'02' USER-ID LEVEL PROFILE RECORD                
*                             X'80' END OF FILE OR NO MORE RECORDS THAT         
*                                   FIT THE KEY SPEC.                           
*                             OTHERWISE AS FOR DATAMGR                          
*               BYTES 1-3     A(DATAMGR)                                        
* PARAMETER 4   BYTES 0-3     A(CL10 AREA FOR RETURNED USER-ID)                 
*                               IF = 0 IGNORE USER-ID PROFILE RECORDS           
         TITLE 'CONFUSE - MODULE TO GET CTFILE RECS. FOR OTHER SYSTEMS'         
CONFUSE  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 LOCALEN,*CONFUSE                                                 
         LR    RA,R1                                                            
         USING PARAD,RA            RA = PARAMETERS                              
         USING LOCALD,RC           RC = LOCAL W/S                               
         XC    LOCALD(LOCALEN),LOCALD                                           
         LM    R8,R9,PARAD                                                      
         MVC   RTYPE(7),0(R8)                                                   
         CLC   PROGRAM(2),=C'  '                                                
         BNE   *+10                                                             
         XC    PROGRAM,PROGRAM                                                  
         CLI   PROGRAM,C' '                                                     
         BNE   *+8                                                              
         MVI   PROGRAM,0                                                        
         CLI   SYSTEM,C'M'         SPECIAL CODE FOR MEDIA SYSTEM                
         BNE   GC05                                                             
         CLI   RTYPE,PROFILE                                                    
         BNE   GC05                                                             
         CLI   PROGRAM+2,C' '      SHUFFLE 2 CHAR PROGRAM FOR PROFILE           
         BNE   GC05                                                             
         MVC   PROGRAM+2(1),PROGRAM+1                                           
         MVC   PROGRAM+1(1),PROGRAM                                             
         MVI   PROGRAM,0                                                        
*                                                                               
         USING IO,R9               R9 = IO AREA                                 
GC05     LA    R8,KEY              R8 = KEY                                     
         MVI   PERROR,UNKNOWN                                                   
         SPACE 1                                                                
GC10     LA    R1,SYSTAB           GET LENGTH OF AGENCY FOR THIS SYSTEM         
GC12     CLI   0(R1),X'FF'         (MINUS 1 FOR EXECUTED COMPARES ETC)          
         BE    EXIT                                                             
         CLC   SYSTEM,0(R1)                                                     
         BE    *+12                                                             
         LA    R1,2(R1)                                                         
         B     GC12                                                             
         MVC   EXLAGY+1(1),1(R1)                                                
         SPACE 1                                                                
GC20     LA    R1,RECTAB           GET KEY DETAILS FOR THIS RECORD TYPE         
         USING RECTABD,R1                                                       
GC22     CLI   RECTYPE,X'FF'                                                    
         BE    EXIT                                                             
         CLC   RTYPE,RECTYPE                                                    
         BE    *+12                                                             
         LA    R1,RECLEN(R1)                                                    
         B     GC22                                                             
         MVC   DISPSYS+1(1),RECDISPS                                            
         MVC   DISPAGY+1(1),RECDISPA                                            
         MVC   DISPPRG+1(1),RECDISPP                                            
         SPACE 1                                                                
GC30     CLI   PTYPE,FIRST         BUILD KEY FOR FIRST CALL                     
         BNE   GC40                                                             
         MVC   KEY,RECKEY          MOVE IN BASIC KEY                            
         MVI   LASTPROG,X'FF'      FORCE CHANGE OF PROGRAM                      
         LH    R7,DISPSYS                                                       
         AR    R7,R8                                                            
         MVC   0(1,R7),SYSTEM      INSERT SYSTEM                                
         LH    R7,DISPPRG                                                       
         LTR   R7,R7                                                            
         BZ    *+12                                                             
         AR    R7,R8                                                            
         MVC   0(2,R7),PROGRAM     INSERT PROGRAM                               
         CLI   RTYPE,PROFILE                                                    
         BNE   *+14                PROFILES HAVE 3CHR PROGRAM                   
         MVC   0(3,R7),PROGRAM                                                  
         B     GC50                                                             
         LH    R7,DISPAGY                                                       
         AR    R7,R8                                                            
         LH    R1,EXLAGY                                                        
         EX    R1,GC30MVC          INSERT AGENCY                                
         B     GC50                                                             
GC30MVC  MVC   0(0,R7),AGENCY                                                   
         DROP  R1                                                               
         SPACE 1                                                                
GC40     MVC   KEY,IO              FOR 2ND OR SUBSEQ CALL READ HIGH             
         CLI   RTYPE,PROFILE       OR SEQUENTIAL FOR NEXT                       
         BNE   GC45                                                             
         USING CTUREC,R9                                                        
         MVC   LASTPROG,CTUKPROG   SAVE LAST PROGRAM IF PROFILE                 
         OC    CTUKAGY,CTUKAGY     TEST AGENCY PRESENT                          
         BNZ   GC52                YES - CONTINUE                               
         SPACE 1                                                                
         USING IO,R9                                                            
         SPACE 1                                                                
* IF AGENCY NOT PRESENT, LAST RECORD CAME FROM CTFILE                           
* INSERTING AGENCY FORCES NEXT READ FROM CTUSER                                 
         SPACE 1                                                                
         LA    R7,KEY                                                           
         AH    R7,DISPAGY                                                       
         LH    R1,EXLAGY                                                        
*                                  CHECK IF USERID LEVEL PROFILE                
         OC    PAUID,PAUID           RECORDS REQUESTED                          
         BZ    GC40A                                                            
         MVC   0(2,R7),=XL2'0001'  MOVE INITIAL ID TO KEY                       
         B     GC50                  AND READ HIGH                              
*                                                                               
GC40A    EX    R1,GC30MVC          ELSE MOVE AGENCY TO KEY                      
         B     GC50                AND READ HIGH                                
*                                                                               
GC45     CLI   PTYPE,SEQ                                                        
         BE    GC52                                                             
         SPACE 1                                                                
GC48     ZIC   R1,KEY+L'KEY-1      BUMP KEY FOR READ HIGH                       
         LA    R1,1(R1)                                                         
         STC   R1,KEY+L'KEY-1                                                   
         SPACE 1                                                                
GC50     LA    R2,=C'DMRDHI'       READ A RECORD                                
         B     GC54                                                             
         SPACE 1                                                                
GC52     LA    R2,=C'DMRSEQ'                                                    
         SPACE 1                                                                
GC54     LA    R3,=C'CTFILE'                                                    
         STM   R2,R3,DMCB                                                       
         STM   R8,R9,DMCB+8                                                     
         GOTO1 PADMGR,DMCB                                                      
         MVC   PERROR,DMCB+8       RETURN ERROR BYTE                            
         CLI   PERROR,0                                                         
         BNE   EXIT                                                             
         MVI   PTYPE,SEQ                                                        
         SPACE 1                                                                
GC60     LH    R1,DISPSYS          CHECK FOR SAME RECTYPE/SYSTEM                
         EX    R1,GC60CLC                                                       
         BNE   GCEOF                                                            
         LH    R7,DISPPRG                                                       
         LTR   R7,R7                                                            
         BZ    GC70                                                             
         OC    PROGRAM,PROGRAM                                                  
         BZ    GC70                                                             
         AR    R7,R9                                                            
         CLI   RTYPE,PROFILE                                                    
         BNE   GC62                                                             
         CLI   SYSTEM,C'M'         SPECIAL CODE FOR MEDIA SYSTEM                
         BNE   GC60A                                                            
         CLI   PROGRAM,0                                                        
         BE    GC61                                                             
GC60A    CLC   0(3,R7),PROGRAM                                                  
         BE    GC70                                                             
         B     GCEOF                                                            
GC61     CLI   0(R7),0             2-CHARACTER PROGRAM FILTER                   
         BNE   GC61A                                                            
         CLC   0(3,R7),PROGRAM     THIS IS A 2-CHARACTER PROGRAM                
         BE    GC70                                                             
         BL    GC45                                                             
         B     GC90                                                             
GC61A    CLC   0(2,R7),PROGRAM+1   THIS IS A 3 CHARACTER PROGRAM                
         BE    GC70                                                             
         BH    GCEOF                                                            
         B     GC90                                                             
GC62     CLC   0(2,R7),PROGRAM     TEST IF SAME PROGRAM                         
         BE    GC70                                                             
GCEOF    MVI   PERROR,EOF                                                       
         B     EXIT                                                             
GC60CLC  CLC   IO(0),KEY                                                        
         SPACE 1                                                                
GC70     CLI   RTYPE,PROFILE                                                    
         BNE   GC80                                                             
         USING CTUREC,R9                                                        
         OC    CTUKAGY,CTUKAGY                                                  
         BZ    EXIT                                                             
         CLC   CTUKPROG,LASTPROG                                                
         BNE   GC90                NO - GET NEXT FIELD RECORD                   
         USING IO,R9                                                            
         SPACE 1                                                                
GC80     LH    R7,DISPAGY          CHECK FOR REQUIRED AGENCY                    
         AR    R7,R9                                                            
         LH    R1,EXLAGY                                                        
         EX    R1,GC80CLC                                                       
         BE    EXIT                YES                                          
         BL    GC90                WEVE PASSED IT                               
*                                                                               
*                                  SPECIAL CODE FOR USERID LEVEL                
         CLI   RTYPE,PROFILE         PROFILE RECORDS                            
         BNE   GC85                                                             
         CLC   0(2,R7),=XL2'4040'  MINIMUM AGENCY VALUE                         
         BNL   GC85                                                             
         OC    PAUID,PAUID         CHECK USERIDS REQUESTED                      
         BZ    GC85                                                             
         MVC   UIDNUM,0(R7)        GET USERID NAME                              
         BAS   RE,GETUID                                                        
         BE    EXIT                EXIT TO CALLING ROUTINE IF FOUND OK          
*                                                                               
         LA    R2,L'KEY              ELSE READ NEXT USERID PROFILE              
         SH    R2,DISPAGY                                                       
         BCTR  R2,0                                                             
         EX    R2,GC80XC                                                        
         SR    RF,RF                                                            
         ICM   RF,3,UIDNUM                                                      
         LA    RF,1(RF)                                                         
         STCM  RF,3,0(R7)                                                       
         MVC   KEY,IO                                                           
         B     GC50                                                             
*                                                                               
GC85     LA    R2,L'KEY            WE HAVEN'T REACHED IT SO READ HIGH           
         SH    R2,DISPAGY          FOR IT                                       
         BCTR  R2,0                                                             
         EX    R2,GC80XC                                                        
         EX    R1,GC80MVC                                                       
         MVC   KEY,IO                                                           
         B     GC50                                                             
GC80CLC  CLC   AGENCY(0),0(R7)                                                  
GC80XC   XC    0(0,R7),0(R7)                                                    
GC80MVC  MVC   0(0,R7),AGENCY                                                   
         SPACE 1                                                                
* ON CHANGE OF PROGRAM, MUST GO BACK AND READ NEXT RECORD FROM CTFILE           
* THIS IS DONE BY LEAVING AGENCY AT X'00'                                       
         SPACE 1                                                                
GC90     LH    R7,DISPPRG                                                       
         AR    R7,R9                                                            
         ICM   RE,7,0(R7)          GET LAST VALUE                               
         STCM  RE,7,LASTPROG       UPDATE LAST PROG                             
         LA    RE,1(RE)            AND BUMP IT                                  
*                                                                               
GC95     STCM  RE,7,0(R7)                                                       
*                                                                               
GC97     LH    R7,DISPAGY          THEN CLEAR AGENCY                            
         AR    R7,R9                                                            
         LH    R1,EXLAGY                                                        
         EX    R1,GC80XC                                                        
         MVC   KEY,IO              AND MOVE IO TO KEY                           
         B     GC50                                                             
         EJECT                                                                  
*                                                                               
* RETURN USERID NAME USING USERID NUMBER IN PROFILE RECORD KEY                  
*                                                                               
GETUID   NTR1                                                                   
         MVC   KEYSAVE,IO          SAVE PROFILE RECORD KEY                      
         USING CTIREC,R9                                                        
         XC    CTIKEY,CTIKEY       BUILD KEY USERID RECORD                      
         MVI   CTIKEY,C'I'                                                      
         MVC   CTIKNUM,UIDNUM                                                   
*                                                                               
         MVC   KEY,CTIKEY          READ USERID RECORD                           
         BAS   RE,READCTF                                                       
         BNE   GUIDNO              RETURN NOT FOUND                             
*                                                                               
         LA    R3,CTIDATA                                                       
         SR    RF,RF                                                            
*                                  EXTRACT ELEMENT DATA                         
GUID010  CLI   0(R3),0                                                          
         BE    GUID100                                                          
         CLI   0(R3),X'02'         USERID NAME POINTER                          
         BE    GUID030                                                          
         CLI   0(R3),X'06'         AGENCY ID                                    
         BE    GUID040                                                          
GUID020  IC    RF,1(R3)                                                         
         AR    R3,RF                                                            
         B     GUID010                                                          
*                                                                               
GUID030  L     R2,PAUID            SAVE USERID NAME AT RETURN ADDRESS           
         MVC   0(10,R2),2(R3)                                                   
         B     GUID020                                                          
*                                                                               
GUID040  CLC   AGENCY(2),2(R3)     CHECK SAME AGENCY                            
         BNE   GUIDNO              ELSE RETURN NOT FOUND                        
         B     GUID020                                                          
*                                  CHECK VALID USERID ACCESS                    
GUID100  B     GUIDOK                                                           
*                                                                               
GUIDOK   MVC   KEY,KEYSAVE         RESTORE LAST PROFILE RECORD                  
         BAS   RE,READCTF                                                       
         BE    *+6                                                              
         DC    H'00'                                                            
         SR    RC,RC               RETURN CC EQUAL                              
         LTR   RC,RC                                                            
         XIT1                                                                   
*                                                                               
GUIDNO   MVC   KEY,KEYSAVE         RESTORE LAST PROFILE RECORD                  
         BAS   RE,READCTF                                                       
         BE    *+6                                                              
         DC    H'00'                                                            
         LTR   RC,RC               RETURN CC NOT EQUAL                          
         XIT1                                                                   
         SPACE 1                                                                
* READ CONTROL FILE                                                             
         SPACE 1                                                                
READCTF  NTR1                                                                   
         LA    R2,=C'DMREAD'                                                    
         LA    R3,=C'CTFILE'                                                    
         STM   R2,R3,DMCB                                                       
         STM   R8,R9,DMCB+8                                                     
         GOTO1 PADMGR,DMCB                                                      
         MVC   PERROR,DMCB+8                                                    
         CLI   PERROR,0                                                         
         XIT1                                                                   
         SPACE 1                                                                
*                                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
*              TABLES                                                           
         SPACE 1                                                                
RECTAB   DS    0C                  RECORD TYPE TABLE COVERED BY RECTABD         
         DC    C'F',AL1(CTVKSYS-CTVREC,CTVKKEY-CTVREC,0)                        
         DC    C'V',XL16'00',C'F',XL7'00'                                       
         DC    C'O',AL1(CTVKSYS-CTVREC,CTVKKEY-CTVREC,0)                        
         DC    C'V',XL16'00',C'O',XL7'00'                                       
         DC    C'U',AL1(CTUKSYS-CTUREC,CTUKAGY-CTUREC)                          
         DC    AL1(CTUKPROG-CTUREC)                                             
         DC    C'U',XL24'00'                                                    
         DC    X'FF'                                                            
         SPACE 1                                                                
SYSTAB   DS    0CL2                SYSTEM/LENGTH OF AGENCY KEY MINUS 1          
         DC    C'A',AL1(1)                                                      
         DC    C'M',AL1(1)                                                      
         DC    C'S',AL1(1)                                                      
         DC    C'P',AL1(1)                                                      
         DC    C'R',AL1(1)                                                      
         DC    C'C',AL1(1)                                                      
         DC    X'FF'                                                            
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
*              DSECTS AND EQUATES                                               
         SPACE 1                                                                
*              DSECT TO COVER LOCAL WORKING STORAGE                             
         SPACE 1                                                                
LOCALD   DSECT                                                                  
DMCB     DS    6F        V                                                      
DISPAGY  DS    H         B         DISPLACEMENT OF AGENCY FROM KEY+0            
DISPSYS  DS    H         B         DISPLACEMENT OF SYSTEM FROM KEY+0            
DISPPRG  DS    H         B         DISPLACEMENT OF PROG   FROM KEY+0            
EXLAGY   DS    H         B         LENGTH OF AGENCY MINUS 1                     
KEY      DS    CL25      V         KEY                                          
KEYSAVE  DS    CL25      V         SAVE KEY DURRING USERID RECORD READ          
UIDNUM   DS    H         B         PROFILE USERID NUMBER                        
LASTPROG DS    CL3       V         LAST PROGRAM (PROFILE RECORDS)               
RTYPE    DS    CL1       C         RECORD TYPE                                  
SYSTEM   DS    CL1       C         SYSTEM                                       
AGENCY   DS    CL2       V         AGENCY - 1 OR 2 BYTES                        
PROGRAM  DS    CL3       C         PROGRAM FILTER                               
LOCALEN  EQU   *-LOCALD                                                         
         SPACE 3                                                                
*              DSECT TO COVER INPUT PARAMETERS                                  
         SPACE 1                                                                
PARAD    DSECT                                                                  
PTYPE    DS    0CL1      C         CALL TYPE - FIRST,HIGH OR SEQ                
PASPEC   DS    A         A         A(KEY SPEC.)                                 
PAIO     DS    A         A         A(I/O AREA)                                  
PERROR   DS    0CL1      X         ERROR SETTING                                
PADMGR   DS    A         A         A(DATAMGR)                                   
PAUID    DS    A         A         A(USERID NAME)                               
         SPACE 3                                                                
*              DSECT TO COVER A RECTAB TABLE ENTRY                              
         SPACE 1                                                                
RECTABD  DSECT                                                                  
RECTYPE  DS    CL1       C         RECORD TYPE OF CALL                          
RECDISPS DS    CL1       B         DISPLACEMENT OF SYSTEM FROM KEY+0            
RECDISPA DS    CL1       B         DISPLACEMENT OF AGENCY FROM KEY+0            
RECDISPP DS    CL1       B         DISPLACEMENT OF PROG   FROM KEY+0            
RECKEY   DS    CL25      V         INITIAL KEY MINUS SYSTEM & AGENCY            
RECLEN   EQU   *-RECTABD                                                        
         SPACE 3                                                                
IO       DSECT                                                                  
         SPACE 1                                                                
*              CTGENFILE                                                        
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
         SPACE 3                                                                
*              EQUATED VALUES                                                   
         SPACE 1                                                                
FIRST    EQU   C'F'                                                             
HIGH     EQU   C'H'                                                             
SEQ      EQU   C'S'                                                             
UNKNOWN  EQU   X'01'                                                            
EOF      EQU   X'80'                                                            
PROFILE  EQU   C'U'                                                             
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'019DDCONFUSE 05/01/02'                                      
         END                                                                    
