*          DATA SET DDFILMAN   AT LEVEL 042 AS OF 09/21/20                      
*PROCESS USING(WARN(15))                                                        
*&&NOP   SET   N                                                                
*CATALP  FILMAN                                                                 
         PRINT NOGEN                                                            
FILMAN   CSECT                                                                  
         NMODL WORKL,**FILMAN,RA,R9,CLEAR=YES                                   
         USING WORKD,RC                                                         
         ST    RD,SAVERD                                                        
         MVC   MODE,0(R1)          MODE INIT/SPACE/COMM                         
         MVC   SAVEPARM,0(R1)                                                   
         MVI   REBUILD,NO                                                       
         XR    RF,RF                                                            
         ICM   RF,7,1(R1)          GET PARM FROM REBUILD= STARTUP               
         BZ    *+10                                                             
         MVC   REBUILD,0(RF)       Will be Y if ,REBUILD=Y is used              
*                                                                               
         L     R1,=A(IOAREA-WORKD)                                              
         AR    R1,RC                                                            
         ST    R1,AIO                                                           
         L     R1,=A(RECVREC-WORKD)                                             
         AR    R1,RC                                                            
         ST    R1,ARECVREC                                                      
*                                                                               
         BRAS  RE,ARSOFF                                                        
*                                                                               
         LA    R2,FULL             GET JOB NAME INTO MVSNAME                    
         EXTRACT (2),FIELDS=TIOT                                                
         L     R2,FULL                                                          
         MVC   MVSNAME,0(R2)                                                    
*                                                                               
         USING SSBD,R1                                                          
         L     R1,ASSB             GET & SET ALET                               
         MVC   DMALET,SSOALET                                                   
         DROP  R1                                                               
*                                                                               
         CLI   MODE,C'I'           OPEN ALL SYSTEMS PASSED                      
         BNE   FILMAN02                                                         
         BRAS  RE,CHECK                                                         
         BRAS  RE,INIT                                                          
         WTO   'Finished initialization'                                        
         B     EXMOD                                                            
*                                                                               
FILMAN02 CLI   MODE,C'S'           CHECK SPACE ON ALL FILES                     
         BNE   FILMAN04                                                         
         WTO   'Starting checking file space'                                   
         BRAS  RE,CHKSPACE                                                      
         WTO   'Finished checking file space'                                   
         B     EXMOD                                                            
*                                                                               
FILMAN04 CLI   MODE,C'R'           REPORT on file information                   
         JNE   *+2                 UNKNOWN MODE                                 
         WTO   'Starting running stats'                                         
         BRAS  RE,RUNSTATS                                                      
         WTO   'Finished running stats'                                         
         B     EXMOD                                                            
*                                                                               
EXMOD    BRAS  RE,ARSOFF           ONE FOR LUCK                                 
         B     XMOD                                                             
         EJECT                                                                  
************************************************************                    
*        CHECK FOR CLEAN DATASPACE                         *                    
************************************************************                    
CHECK    NTR1                                                                   
*                                                                               
         LA    R1,1                LOOP THROUGH SE TAB                          
CHECK010 CHI   R1,255                                                           
         JE    CHECK090            SE 1 TO 255                                  
*                                                                               
         LAM   AR2,AR2,DMALET      FIND IN DATASPACE                            
         LR    R2,R1                                                            
         SLL   R2,6                                                             
         SAC   512                                                              
*                                                                               
         ICM   R2,15,DSPTFRST-DMSPACED(R2)                                      
         JZ    CHECK500                                                         
         USING DMSYSHDR,R2         R2 = SYSTEM ENTRY                            
         MVC   HALF,DSYFILES                                                    
         MVC   FULL,DSYAJOBS                                                    
*                                                                               
         LA    R2,DSYDATA          LOOK AT FILE TABLES                          
         USING DSFILHDR,R2                                                      
         LH    R0,HALF                                                          
CHECK020 OC    DSEOF1,DSEOF1       CHECK NO EOF VALUES                          
         JNZ   CHECK999                                                         
         OC    DSEOF2,DSEOF2                                                    
         JNZ   CHECK999                                                         
         LA    R2,DSFILLNQ(,R2)                                                 
         JCT   R0,CHECK020         NEXT FILE                                    
*                                                                               
         L     R2,FULL                                                          
         USING DSJOBHDR,R2                                                      
         LA    R0,DSJBHMXQ                                                      
CHECK030 OC    0(DSJBHLNQ,R2),0(R2)   CHECK NO JOBS                             
         JNZ   CHECK999                                                         
         LA    R2,DSJBHLNQ(,R2)                                                 
         JCT   R0,CHECK030         NEXT FILE                                    
*                                                                               
CHECK500 LA    R1,1(R1)            NEXT SE                                      
         J     CHECK010                                                         
*                                                                               
CHECK090 CLI   REBUILD,YES         WAS REBUILD REQUESTED                        
         JE    CHECKXIT                                                         
         MVI   REBUILD,C'A'        NO SET REBUILD AVAILABLE                     
         J     CHECKXIT                                                         
*                                                                               
CHECK999 CLI   REBUILD,NO          NO REBUILD REQUESTED SO GOOD                 
         JE    CHECKXIT                                                         
*                                                                               
         SAC   0                                                                
         XC    ECBAD,ECBAD                                                      
         WTOR  TEXT=(OPLEN1,REPLY,1,ECBAD,ROUTCDE=(1))                          
         WAIT  ECB=ECBAD                                                        
         MVI   REBUILD,C'A'                                                     
         CLI   REPLY,YES                                                        
         JE    CHECKXIT                                                         
         MVI   REBUILD,NO                                                       
         J     CHECKXIT                                                         
*                                                                               
OPLEN1   DC    H'42'                                                            
         DC    C'ACTIVE SYSTEMS FOUND. REBUILD ANYWAY Y/N '                     
*                                                                               
CHECKXIT SAC   0                                                                
         J     EXITOK                                                           
         EJECT                                                                  
************************************************************                    
*        INITIALISATION                                    *                    
************************************************************                    
INIT     NTR1                                                                   
         MVI   INITFLG,NO          SET OLD EOFS NOT UP TO DATE                  
         GOTO1 =V(DMSYSFIL),DMCB                                                
*                                                                               
         USING DMATABS,R1                                                       
         MVC   ASYSTABS,DMSYTBLL   DMSYSTABUS or DMSYSTABUK                     
         DROP  R1                                                               
         CLI   INITOPEN,YES        Have we already started INIT                 
         JNE   INIT005                                                          
         L     R1,INITNOW          Reset R1 to INITNOW and bump to next         
         J     INIT030                                                          
                                                                                
INIT005  LARL  R1,SELISTN          LOOP THROUGH SE TAB                          
         MVI   0(R1),0             Mark as empty                                
*                                                                               
         BAS   RE,READDEF                                                       
*                                                                               
         MVI   INITOPEN,YES        Flag we have started INIT                    
*                                                                               
         USING SEINFD,R1                                                        
INIT010  CLI   0(R1),0                                                          
         BE    EXMOD                                                            
         ST    R1,INITNOW          Current entry                                
*                                                                               
         LAM   AR2,AR2,DMALET      FIND IN DATASPACE                            
         LLC   R2,SEINFNUM         SE number                                    
         LR    R3,R2                                                            
         MHI   R3,DMSYLEN                                                       
         A     R3,ASYSTABS         Point to system in SYSTABS                   
         USING SYSTABD,R3                                                       
         MVC   SEOV,DMSYOVNO       Need for OPENSYS                             
         MVC   SENUM,SEINFNUM      Need for OPENSYS                             
         DROP  R3                                                               
*                                                                               
         SLL   R2,6                x 64, Point to system in dataspace           
         SAC   512                                                              
*                                                                               
         USING DMSPACED,R2                                                      
         MVC   SENAM,DSPNAME       Need for OPENSYS                             
         ICM   R2,15,DSPTFRST                                                   
         NILF  GR2,X'3FFFFFFF'     Mask out bits 31 and 30                      
*                                                                               
         USING DMSYSHDR,R2         R2 = SYSTEM ENTRY                            
         TM    DSYSSTAT,DSYQOPEN   OPEN                                         
         JNO   INIT020                                                          
         L     R2,DSYAJOBS                                                      
         USING DSJOBHDR,R2                                                      
*                                                                               
         LA    R0,DSJBHMXQ         MAX JOBS IN TABLE                            
INIT011  OC    DSJOBNAM,DSJOBNAM                                                
         BZ    INIT012                                                          
         TM    DSJOBFLG,DSJOBUPQ+DSJOBMAQ  UPDATE OR MAINT                      
         JZ    INIT012                                                          
         MVC   DUB,DSJOBNAM        SAVE JOBNAME                                 
         J     INIT015                                                          
*                                                                               
INIT012  LA    R2,DSJBHLNQ(,R2)    TEST ALL JOBS                                
         JCT   R0,INIT011                                                       
         J     INIT020                                                          
*                                                                               
INIT015  SAC   0                                                                
         ST    R1,FULL             Save A(SE entry)                             
         LA    R4,WORK2                                                         
         MVC   2(42,R4),=C'........ INIT IGNORED DUE TO OPEN JOB --- '          
         MVC   2(8,R4),SENAM                                                    
         MVC   44(8,R4),DUB                                                     
         MVC   0(2,R4),=H'50'                                                   
         WTO   TEXT=WORK2,MCSFLAG=HRDCPY                                        
         L     R1,FULL             Restore A(SE etnry)                          
         J     INIT030                                                          
*                                                                               
INIT020  SAC   0                                                                
         BRAS  RE,OPENSYS          OPENING SYSTEMS                              
*                                                                               
INIT030  AHI   R1,SEINFLNQ         Next entry                                   
         B     INIT010                                                          
         DROP  R1                                                               
         EJECT                                                                  
************************************************************                    
*        READ FILE DEFINITION PARMS                        *                    
************************************************************                    
READDEF  NTR1                                                                   
*                                                                               
READ010  GOTO1 =V(CARDS),DMCB,CARD,=C'RE00'                                     
         CLC   =C'/*',CARD                                                      
         BE    READEOF                                                          
*                                                                               
READ020  LA    R1,CARD                                                          
         CLI   0(R1),C'*'          IGNORE *                                     
         BE    READ010                                                          
         CLI   0(R1),C' '          IGNORE BLANK                                 
         BNH   READ010                                                          
*                                                                               
         CLC   CARD(7),=C'SYSTEMS'                                              
         BNE   *+12                                                             
         MVI   FMODE,C'S'          READ SYSTEMS                                 
         B     READ010                                                          
*                                                                               
         CLC   CARD(8),=C'WARNINGS'                                             
         BNE   *+12                                                             
         MVI   FMODE,C'W'          READ WARNINGS                                
         B     READ010                                                          
*                                                                               
         CLC   CARD(5),=C'FILES'                                                
         BNE   *+12                                                             
         MVI   FMODE,C'F'          READ FILENAMES                               
         B     READ010                                                          
*                                                                               
         CLC   CARD(6),=C'NOTIFY'                                               
         BNE   *+12                                                             
         MVI   FMODE,C'N'          READ WHO TO NOTIFY                           
         B     READ010                                                          
*                                                                               
*&&US                                                                           
         CLC   =C'ACCESSMETHOD',CARD                                            
         BNE   *+12                                                             
         MVI   FMODE,C'A'          READ ACCESS METHODS                          
         B     READ010                                                          
*                                                                               
         CLC   =C'VSAMCONTROL',CARD                                             
         BNE   *+16                                                             
         MVI   FMODE,C'V'          READ VSAM CONTROL                            
         BRAS  RE,CLRVSAM                                                       
         B     READ010                                                          
*&&                                                                             
*                                                                               
         CLI   FMODE,C'S'          VALIDATE SYSTEMS                             
         BNE   *+8                                                              
         BAS   RE,VALSYS                                                        
*                                                                               
         CLI   FMODE,C'W'          VALIDATE WARNINGS                            
         BNE   *+8                                                              
         BAS   RE,VALWARN                                                       
*                                                                               
         CLI   FMODE,C'F'          VALIDATE FILENAMES                           
         BNE   *+8                                                              
         BAS   RE,VALFILE                                                       
*                                                                               
         CLI   FMODE,C'N'          VALIDATE NOTIFICATION                        
         BNE   *+8                                                              
         BAS   RE,VALNOTY                                                       
*                                                                               
*&&US                                                                           
         CLI   FMODE,C'A'          VALIDATE ACCESS METHODS                      
         BNE   *+8                                                              
         BAS   RE,VALAMETH                                                      
*                                                                               
         CLI   FMODE,C'V'          VALIDATE VSAM VERSION CONTROL                
         BNE   *+8                                                              
         BRAS  RE,VALVSAM                                                       
*&&                                                                             
*                                                                               
         B     READ010                                                          
*                                                                               
READEOF  EQU   *                                                                
         B     EXITOK                                                           
         EJECT                                                                  
************************************************************                    
*        VALIDATE SE SYS                                   *                    
************************************************************                    
VALSYS   NTR1                                                                   
         GOTO1 =V(DMSYSFIL),DMCB                                                
*                                                                               
         USING DMATABS,R1                                                       
         MVC   ASYSTABS,DMSYTBLL   DMSYSTABUS or DMSYSTABUK                     
         DROP  R1                                                               
*                                                                               
         LA    R3,CARD                                                          
         LA    RF,CARD                                                          
*                                                                               
VALSYS1  CLI   0(RF),C' '                                                       
         BE    EXITOK                                                           
                                                                                
VALSYS2  CLI   0(RF),C','          C'$' is less then a comma                    
         BE    VALSYS5                                                          
         CLI   0(RF),C' '                                                       
         BNH   VALSYS5                                                          
         LA    RF,1(RF)                                                         
         B     VALSYS2                                                          
*                                                                               
VALSYS5  SR    RF,R3                                                            
         BCTR  RF,0                subtract 1 for EX intruction                 
*                                                                               
         LAM   AR2,AR2,DMALET      FIND IN DATASPACE                            
         SAC   512                                                              
         LA    R2,L'DMDHDR         Point past first header info                 
         LA    R1,1                R1 will be SE#, starting with 1              
                                                                                
VALSYS10 CHI   R1,255              Anymore to check                             
         JE    *+2                 SE 1 TO 255                                  
*                                                                               
         USING DMSPACED,R2         Need exact match                             
         L     RE,DSPNAME+1(RF)    CHECK DSPNAME+RF+1=SPACE                     
         CLM   RE,8,=C' '                                                       
         JNE   VALSYS11            NO MUST BE LONGER NAME THEN                  
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R3),DSPNAME     TEST NAME                                    
         BE    VALSYS20                                                         
*                                                                               
VALSYS11 AHI   R1,1                NEXT SE                                      
         AHI   R2,L'DSPHDR         Next system in data space                    
         B     VALSYS10                                                         
*                                                                               
VALSYS20 STC   R1,SENUM            Save SE#                                     
         MVC   SENAM,DSPNAME       Save system name                             
         MHI   R1,DMSYLEN          index into SYSTABUS                          
         A     R1,ASYSTABS                                                      
         USING SYSTABD,R1                                                       
         MVC   SEOV,DMSYOVNO                                                    
         SAC   0                                                                
         DROP  R1,R2               SYSTABD, DMSPACED                            
*                                                                               
         LARL  R1,SELISTN          FIND A ZERO ENTRY                            
         USING SEINFD,R1                                                        
VALSYS30 CLI   0(R1),0             Find end of table                            
         BE    VALSYS40                                                         
         AHI   R1,SEINFLNQ                                                      
         B     VALSYS30                                                         
*                                                                               
VALSYS40 MVC   SEINFNUM,SENUM      SET NUMBER IN LIST                           
         MVC   SEINFNAM,SENAM                                                   
         MVC   SEINFOV,SEOV                                                     
         MVI   SEINFLNQ(R1),0      Mark end                                     
         LA    R3,2(R3,RF)         Add 2 for comma and the EX -1                
         LR    RF,R3               Point to next in CARD                        
         B     VALSYS1                                                          
         DROP  R1                  SEINFD                                       
         EJECT                                                                  
************************************************************                    
*        VALIDATE WARNINGS I.E MEDFIL=90 OR MEDFIL1=80     *                    
************************************************************                    
VALWARN  NTR1                                                                   
         GOTO1 =V(SCANNER),DMCB,(C'C',CARD),IOAREA                              
         LA    R1,IOAREA                                                        
         SR    RF,RF                                                            
         ICM   RF,1,0(R1)          RF = LEN -1                                  
         BZ    VALWARNX                                                         
         BCTR  RF,0                                                             
         LA    R3,12(R1)           R3 = FILEDD                                  
         MVC   FULL,8(R1)          SAVE NUMERIC IN FULL                         
*                                                                               
         LARL  R4,SELISTN                                                       
         USING SEINFD,R4                                                        
VALWAR3  CLI   0(R4),0                                                          
         BE    VALWARNX                                                         
*                                                                               
         ST    RF,FULL1            SAVE LENGTH OF KEY                           
         XC    DMCB(6*4),DMCB      ENQUIRE ON SE                                
         MVC   DMCB+3(1),SEINFNUM                                               
         OI    DMCB,X'20'                                                       
         GOTO1 =V(LOCKSPC),DMCB                                                 
         L     R2,4(R1)                                                         
         USING DMSPACED,R2                                                      
         ICM   R2,15,DSPECB                                                     
         NILF  GR2,X'3FFFFFFF'                                                  
         L     RF,FULL1            RESTORE LENGTH                               
*                                                                               
         SAC   512                                                              
         LAM   AR2,AR2,DMALET                                                   
         USING DMSYSHDR,R2                                                      
         SR    R0,R0               SET R0 TO NUMBER OF FILES                    
         ICM   R0,3,DSYFILES                                                    
         LA    R2,DSYDATA                                                       
         USING DSFILHDR,R2                                                      
VALWAR5  EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   DSFILDD(0),0(R3)    DOES THIS MATCH                              
         BNE   VALWAR6                                                          
         MVC   DSFILWRN,FULL+3     SAVE WARNING VALUE                           
VALWAR6  MVC   DSFILWRS,DSFILWRN   COPY TO SECONDARY WARNING VALUE              
         LA    R2,32(R2)                                                        
         BCT   R0,VALWAR5          NEXT FILE                                    
*                                                                               
         AHI   R4,SEINFLNQ         NEXT SE                                      
         SAC   0                                                                
         B     VALWAR3                                                          
*                                                                               
VALWARNX B     EXITOK                                                           
         DROP  R2,R4               DSFILHDR, SEINFD                             
         EJECT                                                                  
*&&US                                                                           
***********************************************************************         
*        VALIDATE ACCESS METHOD (E.G.: DEMDIRR=VSAM)                            
*                                                                               
* Valid access methods are VSAM and DANDX.                                      
* XVSAM is a VSAM kill switch. If set, it will prevent VSAM reads               
*  regardless of other overrides (except for the offline SSB field              
*  SSODMSTA, which overrides even the the XVSAM kill switch).                   
* Currently applicable to U.S. demographics directory/files only.               
* The first letter of the access method is saved as a status flag.              
***********************************************************************         
VALAMETH NTR1                                                                   
*                                                                               
         GOTO1 =V(SCANNER),DMCB,(C'C',CARD),IOAREA                              
*                                                                               
         CLC   =C'DANDX ',IOAREA+22                                             
         BE    VALAM10                                                          
         CLC   =C'XVSAM ',IOAREA+22                                             
         BE    VALAM10                                                          
         CLC   =C'VSAM ',IOAREA+22                                              
         BNE   ERIVPARM              Invalid access method                      
*                                                                               
VALAM10  DS    0H                                                               
         SR    R2,R2                                                            
         LAM   AR2,AR2,DMALET                                                   
         SAC   512                                                              
         L     R2,DHASSBG-DMDHDR(,R2)                                           
         USING FASSBG,R2                                                        
         SELECT CLC,IOAREA+12(8),EQ  Set corresponding flag                     
           WHEN (=CL8'DEMDIRA')                                                 
             MVC SSGDMDRA,IOAREA+22                                             
           WHEN (=CL8'DEMDIRN')                                                 
             MVC SSGDMDRN,IOAREA+22                                             
           WHEN (=CL8'DEMDIRR')                                                 
             MVC SSGDMDRR,IOAREA+22                                             
           WHEN (=CL8'NTIDIR')                                                  
             MVC SSGDMNTI,IOAREA+22                                             
           WHEN (=CL8'PAVDIR')                                                  
             MVC SSGDMPAV,IOAREA+22                                             
           OTHRWISE                                                             
             SAC 0                                                              
             B   ERIVPARM            Invalid directory name                     
         ENDSEL                                                                 
         DROP  R2                                                               
                                                                                
         SAC   0                                                                
                                                                                
         B     EXITOK                                                           
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
*        VALIDATE NOTIFICATION LIST                                             
***********************************************************************         
VALNOTY  NTR1                                                                   
         LA    R2,CARD                                                          
         LA    RF,CARD+L'CARD-1                                                 
*                                                                               
VALNF01  CR    R2,RF                                                            
         BE    EXITOK                                                           
         CLI   0(RF),C' '                                                       
         BH    VALNF10                                                          
         BCTR  RF,0                                                             
         B     VALNF01                                                          
*                                                                               
VALNF10  SR    RF,R2                                                            
         LHI   R1,L'NOTYLIST                                                    
         CR    RF,R1               Make sure it fits in the list                
         BNL   ERTOBIG                                                          
*                                                                               
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   NOTYLIST(0),CARD                                                 
*                                                                               
         B     EXITOK                                                           
         EJECT                                                                  
************************************************************                    
*        VALIDATE FILENAME DEFINITIONS                     *                    
*        FIELD1 = DD  name                                 *                    
*        FILED2 = DSN name                                 *                    
*        FILED3 = System specific (optional)               *                    
*        FILED4 = RO or READONLY  (optional)               *                    
* -------------------------------------------------------- *                    
* GETFLDS returns                                          *                    
*        FLDFLAG = X'08' System specific                   *                    
*                  X'04' Read-only                         *                    
*                  X'02' NOP                               *                    
*        FLDSE#  will be set if FLDFLAG=X'08'              *                    
*        example                                           *                    
*        MEDFIL1  MED.MEDFIL1                              *                    
*        REVIEW1  REP.REVIEW1  REP1    RO                  *                    
*        SPTFIL1  SPT.SPTFIL1  SPT1                        *                    
************************************************************                    
VALFILE  NTR1                                                                   
         BAS   RE,GETFLDS                                                       
*                                                                               
         LARL  R4,SELISTN          List of systems to process                   
         USING SEINFD,R4                                                        
VALFIL3  CLI   0(R4),0                                                          
         BE    VALFILEX                                                         
*                                                                               
         MVC   SE#,SEINFNUM        Save off SE#                                 
         TM    FLDFLAG,FLD_SE#     Specific system                              
         BZ    VALFIL4             No                                           
         CLC   SE#,FLDSE#          Yes, so does it match                        
         BNE   VALFIL80            No so skip check for system                  
                                                                                
VALFIL4  XC    DMCB(6*4),DMCB      ENQUIRE ON SE                                
         MVC   DMCB+3(1),SE#                                                    
         OI    DMCB,X'20'                                                       
         GOTO1 =V(LOCKSPC),DMCB                                                 
         L     R2,4(,R1)                                                        
         USING DMSPACED,R2                                                      
         ICM   R2,15,DSPECB                                                     
         NILF  GR2,X'3FFFFFFF'                                                  
*                                                                               
         SAC   512                                                              
         LAM   AR2,AR2,DMALET                                                   
         USING DMSYSHDR,R2                                                      
         SR    R0,R0               SET R0 TO NUMBER OF FILES                    
         ICM   R0,3,DSYFILES                                                    
         LA    R2,DSYDATA                                                       
*                                                                               
         USING DSFILHDR,R2                                                      
VALFIL5  CLC   DSFILDD(8),FIELD1   DOES THIS MATCH                              
         BNE   VALFIL30                                                         
         TM    FLDFLAG,FLD_RO+FLD_NOP   Read-only or NOP                        
         BZ    VALFIL8                  No                                      
                                                                                
VALFIL7  MVC   FILE#,DSFILNUM                                                   
         SAC   0                                                                
         BRAS  RE,SETFILE                                                       
         LAM   AR2,AR2,DMALET                                                   
         SAC   512                                                              
                                                                                
VALFIL8  ST    R2,SAVER2                                                        
         LA    RE,1                START FROM 1                                 
         SR    R2,R2                                                            
         USING DMDSHDR,R2          LOCATE DHADSN                                
         L     R2,DHADSN                                                        
VALFIL10 OC    0(32,R2),0(R2)      FIND AN EMPTY ENTRY                          
         BZ    VALFIL20                                                         
         CLC   0(32,R2),FIELD2     OR A MATCHING ONE                            
         BE    VALFIL20                                                         
         LA    RE,1(RE)            BUMP TO NEXT                                 
         LA    R2,32(,R2)                                                       
         CH    RE,=Y(12*4096/32)   12 = NUM PAGES ALLOCATED                     
         BL    VALFIL10                                                         
         DC    H'0'                NOT ENOUGH ROOM FOR FILES                    
*                                  NEED TO INCREASE PAGES ALLOCATED             
*                                  TO DHADSN IN DMDMGRDSP AND CHANGE            
*                                  THE ABOVE =Y(12*4096/32)                     
VALFIL20 MVC   0(32,R2),FIELD2                                                  
         L     R2,SAVER2                                                        
         USING DSFILHDR,R2                                                      
         STH   RE,DSDSNX           SAVE INDEX VALUE                             
*                                                                               
VALFIL30 LA    R2,32(,R2)                                                       
         BCT   R0,VALFIL5          NEXT FILE                                    
*                                                                               
VALFIL80 AHI   R4,SEINFLNQ         NEXT SE                                      
         SAC   0                                                                
         B     VALFIL3                                                          
*                                                                               
VALFILEX B     EXITOK                                                           
         DROP  R2,R4               DSFILHDR, SEINFD                             
         EJECT                                                                  
************************************************************                    
*        Split card into fields 1 to 4                     *                    
************************************************************                    
GETFLDS  NTR1                                                                   
         MVI   FLDFLAG,0                                                        
         MVI   FLDSE#,0                                                         
         MVC   FIELDS,SPACES       SET TO BLANK INITIALLY                       
         XC    FIELDLN,FIELDLN                                                  
         LA    R1,CARD                                                          
         LA    R2,CARD+80                                                       
         LA    R4,FIELD1                                                        
         LA    RF,FIELD1                                                        
         LA    R5,FIELDLN                                                       
         LA    R6,L'FIELDS/L'FIELD1     Number of fields (4)                    
*                                                                               
GETF010  CLI   0(R1),C' '          FIND NON SPACE                               
         BH    GETF020                                                          
         LA    R1,1(R1)                                                         
         CR    R1,R2               See if at end                                
         BL    GETF010             No                                           
         B     GETF050             UNTIL END OF CARD                            
*                                                                               
GETF020  LA    R0,L'FIELD1                                                      
GETF022  MVC   0(1,RF),0(R1)       COPY CARD                                    
         LA    R1,1(R1)                                                         
         LA    RF,1(RF)                                                         
         CLI   0(R1),C' '          UNTIL SPACE                                  
         BNH   GETF030             Found space                                  
         BCT   R0,GETF022                                                       
         B     ERTOBIG             TOO BIG > 32                                 
*                                                                               
GETF030  LA    RF,L'FIELD1                                                      
         SR    RF,R0               RF=Length of data                            
         STC   RF,0(,R5)           Save length                                  
         LA    R4,L'FIELD1(,R4)    NEXT FILED                                   
         LA    R5,1(,R5)           Next length field                            
         LR    RF,R4                                                            
         BCT   R6,GETF010          Do only 4                                    
         LA    RF,CARD+L'CARD-1                                                 
         SR    RF,R1               Length left                                  
         CLC   0(0,R1),SPACES                                                   
         EX    RF,*-6                                                           
         BH    ERTOMANY                                                         
*                                                                               
GETF050  XR    RF,RF                                                            
         ICM   RF,1,FIELD3LN       Length of field 3                            
         BZ    GETFLDX             Done if zero                                 
         GOTOR IS_RO,3                                                          
         BE    GETF060                                                          
         GOTOR IS_SYS,3                                                         
         BNE   ERIVPARM            Invalid parameter                            
                                                                                
GETF060  ICM   RF,1,FIELD4LN       length of filed 4                            
         BZ    GETFLDX             Done if zero                                 
         GOTOR IS_RO,4                                                          
         BE    GETFLDX             Done                                         
         GOTOR IS_SYS,4                                                         
         BNE   ERIVPARM            Invalid parameter                            
*                                                                               
GETFLDX  B     EXITOK                                                           
                                                                                
SYSCARD  DC    C'SYS='                                                          
SYSCDATA DS    CL8                                                              
         EJECT                                                                  
***********************************************************************         
* Validate for RO / READONLY parameter                                          
***********************************************************************         
IS_RO    NTR1                                                                   
         LA    R2,FIELD3                                                        
         LA    R3,FIELD3LN                                                      
         CHI   R1,3                                                             
         BE    IS_RO10                                                          
         LA    R2,FIELD4                                                        
         LA    R3,FIELD4LN                                                      
*                                                                               
IS_RO10  CLC   =C'RO',0(R2)                                                     
         BE    IS_RO30                                                          
         CLC   =C'NOP',0(R2)                                                    
         BE    IS_RO40                                                          
         LLC   RF,0(,R3)                                                        
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   =C'READONLY',0(R2)                                               
         BNE   IS_ROXIT                                                         
*                                                                               
IS_RO30  OI    FLDFLAG,FLD_RO      Set to read-only file                        
         B     *+8                                                              
IS_RO40  OI    FLDFLAG,FLD_NOP     Set to NOP                                   
         CR    R1,R1                                                            
*                                                                               
IS_ROXIT B     EXIT                                                             
***********************************************************************         
* Validate for system parameter                                                 
***********************************************************************         
IS_SYS   NTR1                                                                   
         LA    R2,FIELD3                                                        
         CHI   R1,3                                                             
         BE    *+8                                                              
         LA    R2,FIELD4                                                        
*                                                                               
         MVC   SYSCDATA,0(R2)      See if it is system                          
         GOTOR =V(DMDDNAME),DMCB,(0,=C'DDNAME'),SYSCARD,0                       
         CLI   8(R1),0                                                          
         BNE   IS_SYXIT                                                         
         L     RF,8(,R1)           Get A(FILE INFO LIST)                        
         MVC   DDNADATA,0(RF)      Move block in                                
         MVC   FLDSE#,DDNASENO                                                  
         OI    FLDFLAG,FLD_SE#     Specific SE to apply to                      
         CR    R1,R1                                                            
*                                                                               
IS_SYXIT B     EXIT                                                             
***********************************************************************         
* Set file DTF to read-only                                                     
***********************************************************************         
SETFILE  NTR1                                                                   
         L     RE,=V(UTL)                                                       
         MVC   4(1,RE),SE#         SET UTL                                      
*                                                                               
         XC    DMCB,DMCB           DO SYSFLES CALL FOR FILES                    
         MVC   DMCB+11(1),SE#                                                   
         GOTO1 =V(DATAMGR),DMCB,DMREAD,=C'SYSFLES'                              
         ICM   R1,15,12(R1)                                                     
         BNZ   *+6                                                              
         DC    H'0'                                                             
                                                                                
         NILH  GR1,X'00FF'         R1=SE FILES (24 BIT ADDRESS)                 
*                                                                               
         USING SYSFLSTD,R1                                                      
         CLC   SE#,SYSFSYS#        CONFIRM FILE LIST                            
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         SR    RF,RF               RE=NUMBER OF FILES                           
         ICM   RF,3,SYSF#FLS       # OF FILES                                   
         LA    R1,SYSFLIST         POINT R1 TO FILES                            
SETFL10  CLC   SYSFILE#,FILE#      Match on file number                         
         BE    SETFL20                                                          
         LA    R1,SYSFLNQ(,R1)                                                  
         BCT   RF,SETFL10                                                       
         DC    H'00'                                                            
                                                                                
         USING DTFPHD,R4                                                        
SETFL20  ICM   R4,15,SYSFADTF-1                                                 
         TM    FLDFLAG,FLD_RO      Read-only?                                   
         BZ    *+8                   No                                         
         OI    DTFOPEN,DTF_RO        Yes                                        
         TM    FLDFLAG,FLD_NOP     NOP file?                                    
         BZ    *+8                   No                                         
         OI    DTFOPEN,DTF_NOP       Yes                                        
         B     EXITOK                                                           
         DROP  R1,R4                                                            
         EJECT ,                                                                
************************************************************                    
*        Run stats and write out to disk upon request      *                    
************************************************************                    
***********************************************************************         
*  R2 = Always be mindful of R2, it is used to access the dataspace   *         
*  R6 = list of systems to process. Do not use R6                     *         
***********************************************************************         
         USING PSLINED,PSLINE                                                   
RUNSTATS NTR1                                                                   
         LARL  R6,SELISTN          List of systems to check                     
         USING SEINFD,R6                                                        
         L     R1,SAVEP2                                                        
         MVC   STATSYS,0(R1)                                                    
         CLI   STATOPEN,YES        ARE WE ALREADY IN PROCESS                    
         JE    RSTAT006                                                         
*                                                                               
         MVC   DSSESYS,=C'XTNT'    SET DEFAULTS FOR ALL SYS STATUS              
         MVI   DSEXTRA,C' '                                                     
         MVC   DSDOTD,=C'.D'                                                    
         MVC   DSDOTT,=C'.T'                                                    
         MVI   TXTDSNL,DSNLNQ                                                   
*                                                                               
         OC    STATSYS,STATSYS     SPECIFIC SYS                                 
         JZ    RSTAT005                                                         
         MVC   DSSESYS(5),STATSYS  SYSNAME                                      
*                                                                               
RSTAT001 CLC   SEINFNAM(7),STATSYS THIS SYSTEM                                  
         JE    RSTAT005                                                         
         AHI   R6,SEINFLNQ         Next system                                  
         CLI   0(R6),0                                                          
         JE    RSTATXIT            DONE                                         
         J     RSTAT001                                                         
*                                                                               
RSTAT005 CLI   STATOPEN,YES        ARE WE ALREADY IN PROCESS                    
         JNE   RSTAT010                                                         
*                                                                               
RSTAT006 L     R6,STATNOW          YES MUST HAVE FAILED ON THIS ONE             
         MVC   MSGSYACT,TXTESYS    Error system                                 
         MVC   MSGSYSNM,SEINFNAM                                                
         WTO   TEXT=MSGSYLEN,MCSFLAG=HRDCPY                                     
         MVC   SENAM,SEINFNAM                                                   
         J     RSTAT200                                                         
*                                                                               
RSTAT010 CLI   0(R6),0                                                          
         BE    RSTATXIT            Done                                         
         CLI   OPENED,YES                                                       
         JNE   RSTAT015                                                         
         L     R6,STATNOW          YES MUST HAVE FAILED ON THIS ONE             
         MVC   SENAM,SEINFNAM                                                   
         J     RSTAT200                                                         
*                                                                               
RSTAT015 ST    R6,STATNOW                                                       
         MVC   SENUM,SEINFNUM      Run through SE list                          
         MVC   SENAM,SEINFNAM                                                   
         MVC   SEOV,SEINFOV                                                     
*                                                                               
         MVC   MSGSYACT,TXTOSYS    Opening system                               
         MVC   MSGSYSNM,SENAM                                                   
*AH3     WTO   TEXT=MSGSYLEN,MCSFLAG=HRDCPY                                     
*                                                                               
***********************************************************************         
* Need to see if system exclusivly locked for maintenance             *         
***********************************************************************         
         LAM   AR2,AR2,DMALET                                                   
         LLC   R1,SENUM            R1 = SE NUMBER                               
         SLL   R1,6                * 64 = SE ENTRY                              
         LR    R2,R1                                                            
         SAC   512                                                              
*                                                                               
         USING DMSPACED,R2                                                      
         ICM   R2,15,DSPECB                                                     
         NILF  GR2,X'3FFFFFFF'     Mask out bits 31 and 30                      
*                                                                               
         USING DMSYSHDR,R2         R2 = SYSTEM ENTRY                            
         SR    R3,R3                                                            
         ICM   R3,3,DSYFILES       R3 = NUMBER OF FILES                         
         BZ    RSTAT300                                                         
         TM    DSYSSTAT,DSYQEXCL   Exclusive ownership                          
         BO    RSTAT300            YES, SO DON'T CHECK                          
         SAC   0                                                                
         DROP  R2                                                               
*                                                                               
         BRAS  RE,OPENRO                                                        
         BL    RSTAT300            Skip system                                  
         CLI   STATOPEN,YES                                                     
         BE    RSTAT030                                                         
         MVI   STATOPEN,YES                                                     
         BRAS  RE,DYNIT            Create date/time dataset                     
         OPEN  (XTNTSTAT,OUTPUT)                                                
         MVC   PSLINE,SPACES                                                    
         MVC   PSDATE,SAVDATE                                                   
         MVC   PSHOURS,SAVHOUR                                                  
         MVC   PSMINS,SAVMINS                                                   
         MVC   PSSECS,SAVSECS                                                   
         MVI   PSCOLIN1,C':'                                                    
         MVI   PSCOLIN2,C':'                                                    
         PUT   XTNTSTAT,PSLINE                                                  
         MVC   PSLINE,SPACES                                                    
*                                                                               
*                                                                               
RSTAT030 LT    R8,TOTALSYS         ANY FILES?                                   
         JZ    *+2                                                              
         LAM   AR2,AR2,DMALET                                                   
         LLC   R2,SENUM            R1 = SE NUMBER                               
         SLL   R2,6                * 64 = SE ENTRY                              
*                                                                               
         MVC   PSLINE,SPACES                                                    
         MVC   PSSYSNME,SENAM      System name                                  
         SAC   512                                                              
*                                                                               
         USING FINFO,R4                                                         
         USING DMSPACED,R2                                                      
         LA    R4,FILEINFO         List of files to process                     
         ICM   R2,15,DSPECB        Point to the file list                       
         NILF  GR2,X'3FFFFFFF'                                                  
                                                                                
***********************************************************************         
*  R8 = number of actual files to report                                        
*  R3 = number of files for system (from dataspace)                             
*  FILEINFO = list of files to process                                          
***********************************************************************         
         USING DMSYSHDR,R2         R2 = SYSTEM ENTRY                            
         SR    R3,R3                                                            
         ICM   R3,3,DSYFILES       R3 = number of files in DSpace list          
         JZ    *+2                                                              
*                                                                               
         LA    R2,DSYDATA          POINT R2 TO FILE TABLE                       
         USING DSFILHDR,R2                                                      
RSTAT040 CLC   DSFILNUM,FINFOSE#                                                
         BE    RSTAT042                                                         
         AHI   R2,32               Next file                                    
         JCT   R3,RSTAT040                                                      
         DC    H'00'               Check out the code?                          
*                                                                               
RSTAT042 ST    R2,SAVER2           Save where we left off                       
         SR    R1,R1               INDEX TO FILENAME                            
         ICM   R1,3,DSDSNX                                                      
         BZ    RSTAT043            No name                                      
         BCTR  R1,0                                                             
         SLL   R1,5                x32                                          
         SR    R2,R2                                                            
         L     R2,DHADSN-DMDHDR(,R2) point to DSN in dataspace                  
         AR    R2,R1                                                            
         MVC   PSDSN,0(R2)         Save off file name                           
*                                                                               
RSTAT043 L     R2,SAVER2           Restore location of file entry               
                                                                                
         USING DTFPHD,R5                                                        
         L     R5,FINFODTF         Load DTF of file                             
*                                                                               
         MVC   PSDDNAME,DSFILDD    DDNAME                                       
         MVI   LOCAL,NO                                                         
         MVC   EOFADR,DSEOF1                                                    
         SAC   0                                                                
*                                                                               
         ICM   RF,15,EOFADR        EOF FILE (If set)                            
         BNZ   *+8                                                              
         MVI   LOCAL,YES           Not set so assume local                      
*                                                                               
         ST    R5,ADTF             Save off DTF                                 
         TM    DTFTYPE,DTFTIS      File is IS or DA?                            
         BZ    RSTAT044                                                         
*                                                                               
         USING ISDTF,R5                                                         
         LA    R7,ISXTNT           IS                                           
         CLI   0(R7),0                                                          
         JE    *+2                                                              
         GOTO1 =V(ISDDS),DMCB,ISCPUID,AIO,0,(R5),0                              
         BRAS  RE,ISCALC                                                        
         L     RE,ISATPCT                                                       
*                                                                               
         CLI   LOCAL,YES                                                        
         BNE   *+10                                                             
         MVC   EOFADR,ISOVLAST     Start with this one                          
         TM    ISFTYPE,ISFTBIGF    20-BIT FILE ?                                
         BO    RSTAT046            Yes                                          
         TM    ISFTYPE,ISFTPDOV                                                 
         JZ    *+2                 All files should be converted                
         B     RSTAT046                                                         
*                                                                               
         USING DTFPHD,R5                                                        
RSTAT044 LA    R7,DMTX             R4=A(DA EXTENT MATRIX)                       
         TM    DIND,DINDXAM        High core maxtrix                            
         BZ    RSTAT045                                                         
         SAM31                                                                  
         ICM   R7,15,DMTX                                                       
         JZ    *+2                                                              
RSTAT045 GOTO1 =V(DADDS),DMCB,DACPUID,AIO,0,(R5),0                              
*                                                                               
         BRAS  RE,DACALC                                                        
         L     RE,DAATPCT                                                       
         CLI   LOCAL,YES                                                        
         BNE   *+10                                                             
         MVC   EOFADR,DADNEXT                                                   
*                                                                               
RSTAT046 CVD   RE,DUB                                                           
         OI    DUB+L'DUB-1,X'0F'                                                
         UNPK  PSPAVAIL(3),DUB                                                  
         CLI   PSPAVAIL,C'0'                                                    
         BNE   RSTAT047                                                         
         MVI   PSPAVAIL,C' '                                                    
         CLI   PSPAVAIL+1,C'0'                                                  
         BNE   *+8                                                              
         MVI   PSPAVAIL+1,C' '                                                  
RSTAT047 MVI   PSPAVAIL+3,C'%'                                                  
*                                                                               
         L     RE,EOFADR           EOF address,isolate tracks                   
         SRL   RE,10                                                            
         MVC   PSSIZE,=C'22'                                                    
         TM    DTFTYPE,DTFTB22     22 bit file?                                 
         BO    RSTAT048            Yes                                          
         SRL   RE,2                                                             
         MVC   PSSIZE,=C'20'                                                    
         TM    DTFTYPE,DTFTBIGF    20 bit file?                                 
         BO    RSTAT048            Yes                                          
         SRL   RE,2                                                             
         MVC   PSSIZE,=C'18'                                                    
         TM    DTFTYPE,DTFTBIG     18 bit file?                                 
         BO    RSTAT048            Yes                                          
         SRL   RE,2                                                             
         MVC   PSSIZE,=C'16'       16 bit file                                  
*                                                                               
RSTAT048 ST    RE,TRKEOF           Save EOF track location                      
         XC    TRKTOTAL,TRKTOTAL                                                
         MVI   PSGLOBAL,C'L'       Set to local                                 
         CLI   LOCAL,YES           No EOF so say local                          
         BE    RSTAT049                                                         
         TM    DTFFLAG,DTFGLOB     File is global?                              
         BZ    *+8                                                              
         MVI   PSGLOBAL,C'G'                                                    
*                                                                               
RSTAT049 MVC   PSTYPE,=C'IS'                                                    
         TM    DTFTYPE,DTFTIS      File is IS or DA?                            
         BO    *+10                                                             
         MVC   PSTYPE,=C'DA'                                                    
*                                                                               
*        LLH   R1,DBLKSZ           Blocksize                                    
*        CVD   R1,DUB                                                           
*        OI    DUB+L'DUB-1,X'0F'                                                
*        UNPK  PSBLKSZ,DUB                                                      
*                                                                               
         USING EXTENTD,R7                                                       
RSTAT060 CLI   EXTDEV#,X'FF'       Last extent?                                 
         BE    RSTAT100                                                         
         LLC   RF,EXTSEQ#          Extent number                                
         CVD   RF,DUB                                                           
         OI    DUB+L'DUB-1,X'0F'                                                
         UNPK  PSEXT#,DUB                                                       
*                                                                               
         GOTOR =V(HEXOUT),DMCB,EXTCHU,(X'80',PSCHU#),L'EXTCHU                   
         GOTOR =V(HEXOUT),DMCB,EOFADR,PSEOF,L'EOFADR                            
*                                                                               
         LLH   RF,EXTCYLLO                                                      
         CVD   RF,DUB                                                           
         OI    DUB+L'DUB-1,X'0F'                                                
         UNPK  PSCYLLO,DUB                                                      
*                                                                               
         MVI   PS1DASH,C'-'                                                     
         LLH   RF,EXTCYLHI                                                      
         CVD   RF,DUB                                                           
         OI    DUB+L'DUB-1,X'0F'                                                
         UNPK  PSCYLHI,DUB                                                      
*                                                                               
         LLH   RF,EXT#TRKS         Number of tracks for extent                  
         CVD   RF,DUB                                                           
         OI    DUB+L'DUB-1,X'0F'                                                
         UNPK  PSTRKTOT,DUB                                                     
*                                                                               
         L     R0,TRKEOF           R0 = EOF track                               
         L     R1,TRKTOTAL         R1 = prior track total                       
         LR    RE,R1                                                            
         AR    RE,RF               RE = Over-all track total                    
         ST    RE,TRKTOTAL                                                      
         CR    R0,RE               Compare EOF, TOTAL                           
         BL    RSTAT078                                                         
         MVC   PSTRKUSE,PSTRKTOT   This are in use                              
         MVC   PSTRKAVL,=C'000000' Non available                                
         B     RSTAT080                                                         
*                                                                               
RSTAT078 CR    R0,R1               EOF, prior total                             
         BH    RSTAT079                                                         
         MVC   PSTRKAVL,PSTRKTOT   All  are available                           
         MVC   PSTRKUSE,=C'000000' None are in use                              
         B     RSTAT080                                                         
*                                                                               
RSTAT079 LR    RE,R0               Calculate what part is used                  
         SR    R0,R1                                                            
         CVD   R0,DUB                                                           
         OI    DUB+L'DUB-1,X'0F'                                                
         UNPK  PSTRKUSE,DUB                                                     
*                                                                               
         SR    RF,R0               Calulcate what is available                  
         CVD   RF,DUB                                                           
         OI    DUB+L'DUB-1,X'0F'                                                
         UNPK  PSTRKAVL,DUB                                                     
*                                                                               
RSTAT080 PUT   XTNTSTAT,PSLINE                                                  
         AHI   R7,EXTLNQ           Next extent                                  
         B     RSTAT060                                                         
         DROP  R7                                                               
*                                                                               
RSTAT100 SAM24                                                                  
         AHI   R4,FINFOQ           Next file entry                              
         SAC   512                                                              
         BCT   R8,RSTAT040         Loop for each file                           
         DROP  R4                                                               
*                                                                               
RSTAT200 SAC   0                                                                
         MVC   MSGSYACT,TXTCSYS    Closing system                               
*AH3     WTO   TEXT=MSGSYLEN,MCSFLAG=HRDCPY                                     
         BRAS  RE,CLOSERO                                                       
*                                                                               
RSTAT300 SAC   0                                                                
         OC    STATSYS,STATSYS     ONE SYSTEM                                   
         JNZ   RSTATXIT                                                         
         AHI   R6,SEINFLNQ         Next system                                  
         B     RSTAT010                                                         
         DROP  R6                  SEINFD                                       
*                                                                               
RSTATXIT SAC   0                                                                
         CLI   STATOPEN,YES                                                     
         BNE   EXITOK                                                           
         CLOSE XTNTSTAT            Close file                                   
         MVC   MSGDSN,DSNAME       Send message to console                      
         WTO   TEXT=MSGFLEN                                                     
         MVI   STATOPEN,NO                                                      
         B     EXITOK                                                           
*                                                                               
MSGSYLEN DC    AL2(MSGOPLN)                                                     
MSGSYACT DC    CL16' '                                                          
MSGSYSNM DS    CL7                                                              
MSGOPLN  EQU   *-MSGSYACT                                                       
*                                                                               
TXTESYS  DC    C'ERROR   SYSTEM: '                                              
TXTOSYS  DC    C'OPENING SYSTEM: '                                              
TXTCSYS  DC    C'CLOSING SYSTEM: '                                              
         EJECT ,                                                                
***********************************************************************         
* Open systems as read-only                                                     
***********************************************************************         
OPENRO   CLI   OPENED,YES          Already opened?                              
         BER   RE                  Yes                                          
OPENRO01 NTR1                                                                   
         MVI   OPENED,YES                                                       
         XC    DMCB,DMCB           Get files for system                         
         MVC   DMCB+11(1),SENUM                                                 
         GOTO1 =V(DATAMGR),DMCB,DMREAD,=C'SYSFLES'                              
         ICM   R1,15,12(R1)                                                     
         JZ    *+2                                                              
         NILH  GR1,X'00FF'         R1=SE FILES (24 BIT ADDRESS)                 
         ST    R1,ASYSFLES                                                      
                                                                                
         L     RE,=V(UTL)                                                       
         MVC   4(1,RE),SENUM       SET UTL                                      
         MVI   FILE1ST,YES                                                      
*&&US*&& MVI   SPOTSYS,NO                                                       
*&&US*&& MVI   TRFFSYS,NO                                                       
                                                                                
         USING SYSFLSTD,R4                                                      
OPENRO10 L     R4,ASYSFLES                                                      
         NI    SYSFSTAT,X'FF'-SYSFSINT   NASTY LITTLE TRICK                     
         CLC   SENUM,SYSFSYS#      CONFIRM FILE LIST                            
         JNE   *+2                                                              
*                                                                               
         LLC   R1,SEOV             SAVE SHIFTED OV NUM IN BYTE                  
         SLL   R1,4                                                             
         STC   R1,BYTE                                                          
*                                                                               
         LLH   R1,SYSF#FLS         # OF FILES                                   
         CHI   R1,SEFILMAX-1                                                    
         JH    *+2                 MAKE SEFILN BIGGER                           
*                                                                               
         LA    R4,SYSFLIST         from list of files                           
         XR    R8,R8               Count number of files                        
OPENRO20 TM    SYSFIND1,SFNOP+SFNOEOF    FILE IS NO-OPED or No EOF              
         BNZ   OPENRO28                                                         
         TM    SYSFIND2,SFALIAS    FILE IS ALIAS                                
         BO    OPENRO28                                                         
*&&UK                                                                           
         MVC   BYTE1,SYSFILE#      CHECK FILE BELONGS TO SYSTEM                 
         NI    BYTE1,X'F0'                                                      
         CLC   BYTE,BYTE1                                                       
         BNE   OPENRO28                                                         
*&&                                                                             
*&&US                                                                           
         CLI   SENUM,X'0C'         DEMO, system?                                
         BE    OPENRO28            Skip                                         
         CLI   SENUM,X'0A'         CONTROL system?                              
         BE    OPENRO25            Okay, process                                
         CLI   SYSFILE#,X'A1'      CONTROL file                                 
         BL    OPENRO22            No, keep going                               
         CLI   SYSFILE#,X'AF'      CONTROL file                                 
         BNH   OPENRO28            Skip control file                            
*                                                                               
OPENRO22 CLI   FILE1ST,YES         First File for system?                       
         BNE   OPENRO23            No                                           
         MVI   FILE1ST,NO                                                       
         CLI   SYSFILE#,X'23'      SPTDIR?                                      
         BNE   *+8                                                              
         MVI   SPOTSYS,YES                                                      
         CLI   SYSFILE#,X'33'      TRFDIR?                                      
         BNE   *+8                                                              
         MVI   TRFFSYS,YES                                                      
*                                                                               
OPENRO23 CLI   SPOTSYS,YES                                                      
         BNE   OPENRO24                                                         
         CLI   SYSFILE#,X'32'      TRFFIL                                       
         BE    OPENRO28            Skip                                         
         CLI   SYSFILE#,X'33'      TRFDIR                                       
         BE    OPENRO28            Skip                                         
         B     OPENRO25                                                         
*                                                                               
OPENRO24 CLI   TRFFSYS,YES                                                      
         BNE   OPENRO25                                                         
         CLI   SYSFILE#,X'21'      SPTFIL                                       
         BE    OPENRO28            Skip                                         
         CLI   SYSFILE#,X'23'      SPTDIR                                       
         BE    OPENRO28            Skip                                         
         CLI   SYSFILE#,X'22'      STATION                                      
         BE    OPENRO28            Skip                                         
*&&                                                                             
         USING DTFPHD,R5                                                        
OPENRO25 XR    R5,R5                                                            
         ICM   R5,7,SYSFADTF       R5=A(DTF)                                    
         USING FINFO,RE                                                         
         LR    RE,R8                                                            
         MHI   RE,FINFOQ                                                        
         LA    RE,FILEINFO(RE)     Point to new entry                           
         MVC   FINFOSE#,SYSFILE#                                                
         ST    R4,FINFOSYF         Save location in SYSFLES                     
         ST    R5,FINFODTF                                                      
         LR    RF,R8                                                            
         MHI   RF,8                                                             
         LA    RF,SEFILN(RF)                                                    
         MVI   0(RF),C'N'                                                       
         MVC   1(7,RF),DTFDD                                                    
         MVI   8(RF),C'X'          END OF LIST                                  
         AHI   R8,1                Count how many files                         
         DROP  RE,R5                                                            
*                                                                               
OPENRO28 LA    R4,SYSFLNQ(,R4)                                                  
         BCT   R1,OPENRO20                                                      
         ST    R8,TOTALSYS                                                      
*                                                                               
         USING SSBD,R2                                                          
         L     R2,ASSB             SSB                                          
         OI    SSOFLAG3,SSO3NOFM   Do not output file info message              
         GOTO1 =V(DATAMGR),DMCB,OPEN,SENAM,SEFILN,AIO,0                         
         NI    SSOFLAG3,X'FF'-SSO3NOFM                                          
         DROP  R2                                                               
*                                                                               
         J     EXITOK                                                           
         DROP  R4                                                               
         EJECT ,                                                                
*********************************************************************           
* Close system                                                                  
***********************************************************************         
CLOSERO  CLI   OPENED,NO                                                        
         BER   RE                  Not open                                     
*                                                                               
CLOSERO1 NTR1                                                                   
         MVI   OPENED,NO                                                        
         GOTOR =V(DATAMGR),DMCB,CLOSE,SENAM,0,0                                 
         B     EXITOK                                                           
         EJECT ,                                                                
*********************************************************************           
* Dynamically Allocate Report Dump Tape                                         
***********************************************************************         
DYNIT    NTR1  ,                                                                
         USING SSBD,RE                                                          
         L     RE,ASSB             SSB                                          
         MVC   DSSYS,=C'TST'                                                    
         CLI   SSODSPAC,C'T'                                                    
         BE    DYNIT02                                                          
         MVC   DSSYS,=C'CSC'                                                    
         CLI   SSODSPAC,C'C'                                                    
         BE    DYNIT02                                                          
         MVC   DSSYS,=C'FQA'                                                    
         CLI   SSODSPAC,C'Q'                                                    
         BE    DYNIT02                                                          
         MVC   DSSYS,=C'BAR'                                                    
         CLI   SSODSPAC,C'B'                                                    
         JE    DYNIT02                                                          
         MVC   DSSYS,=C'ADV'                                                    
         CLI   SSODSPAC,C'A'                                                    
         JE    DYNIT02                                                          
         MVC   DSSYS,=C'REP'                                                    
         CLI   SSODSPAC,C'R'                                                    
         JNE   *+2                 No clue what it is                           
         DROP  RE                                                               
*                                                                               
DYNIT02  GOTO1 =V(DATCON),DMCB,(5,0),(X'20',DSNDATE)   DATE                     
         EDIT  (TIME,NOW),DUB1                         TIME                     
         MVC   DSNHOUR,DUB1                                                     
         MVC   DSNMINS,DUB1+3                                                   
         MVC   DSNSECS,DUB1+6                                                   
*                                                                               
         MVC   SAVDATE,DSNDATE     SAVE THESE BEFORE DSN SQUASH                 
         MVC   SAVHOUR,DSNHOUR                                                  
         MVC   SAVMINS,DSNMINS                                                  
         MVC   SAVSECS,DSNSECS                                                  
*                                                                               
         CLI   DSEXTRA,C' '        5 CHR SYS                                    
         JNE   DYNIT03                                                          
         LLC   R1,TXTDSNL                                                       
         BCTR  R1,0                DEC LEN                                      
         STC   R1,TXTDSNL                                                       
         MVC   DSEXTRA(16),DSDOTD                                               
         MVI   DSEXTRA+16,C' '                                                  
*                                                                               
DYNIT03  LA    R1,ARBLK            DYNAMIC ALLOCATION PARAMETER BLOCK           
         DYNALLOC                                                               
         LTR   RF,RF                                                            
         JNZ   DIT010              ERROR CREATING DATA SET                      
*                                                                               
         LA    R3,JFCB                                                          
         RDJFCB XTNTSTAT                                                        
         LTR   RF,RF                                                            
         JNZ   DIT010                                                           
*                                                                               
         USING INFMJFCB,R3                                                      
         MVC   JFCBXPDT,JFCBCRDT   COPY CREATION DATE TO EXPIRATION             
*                                                                               
         USING SSBD,RE                                                          
         L     RE,ASSB             SSB                                          
         CLI   SSODSPAC,C'A'                                                    
         JNE   DIT005                                                           
         DROP  RE                                                               
*                                                                               
         LLC   R1,JFCBXPDT                                                      
         AHI   R1,1                                                             
         STC   R1,JFCBXPDT         EXPIRES 1 YEAR AFTER CREATION                
         J     EXITOK                                                           
*                                                                               
DIT005   XR    R1,R1               FOR TEST IT'S 5 DAYS                         
         ICM   R1,B'0011',JFCBXPDT+1                                            
         AHI   R1,5                                                             
         CHI   R1,365                                                           
         JH    EXITOK              END OF THE YEAR EVERYTHINGS GOES             
         STCM  R1,B'0011',JFCBXPDT+1                                            
         J     EXITOK                                                           
         DROP  R3                                                               
*                                                                               
DIT010   MVC   WTOBLK,SPACES                                                    
         MVC   WTOBLK+2(13),=C'<<RTC=00000>>'                                   
         CVD   RF,DUB                                                           
         OI    DUB+L'DUB-1,X'0F'                                                
         UNPK  WTOBLK+8(5),DUB                                                  
         MVC   WTOBLK(2),=AL2(78)                                               
         MVC   WTOBLK+20(22),=C'DYNALLOC ERROR CODE = '                         
         GOTO1 =V(HEXOUT),DMCB,RBLK+4,WTOBLK+42,2,=C'TOG'                       
         MVC   WTOBLK+46(15),=C',  INFO CODE = '                                
         GOTO1 =V(HEXOUT),DMCB,RBLK+6,WTOBLK+61,2,=C'TOG'                       
*                                                                               
         LA    R3,WTOBLK                                                        
         WTO   TEXT=(R3)                                                        
         DC    H'0'                COULD NOT ALLOCATE TAPE                      
                                                                                
*----------------------------------------------------------------------         
* DYNALLOC REQUEST BLOCK                                                        
*----------------------------------------------------------------------         
         DS    0F                                                               
ARBLK    DC    X'80',AL3(RBLK)     R1 POINTS TO THIS BEFORE DYNALLOC            
*                                                                               
RBLK     DC    X'1401000000000000',A(ATXT),X'0000000000000000'                  
*                                                                               
ATXT     DC    X'00',AL3(TXTDD)                                                 
         DC    X'00',AL3(TXTDSN)                                                
         DC    X'00',AL3(TXTUNIT)                                               
         DC    X'00',AL3(TXTSPACE)                                              
         DC    X'00',AL3(TXTPRIME)                                              
         DC    X'00',AL3(TXTSECND)                                              
         DC    X'00',AL3(TXTSDISP)                                              
         DC    X'00',AL3(TXTNDISP)                                              
         DC    X'00',AL3(TXTCDISP)                                              
         DC    X'00',AL3(TXTRELSE)                                              
         DC    X'80',AL3(TXTCLOSE)                                              
*                                                                               
TXTDD    DC    AL2(DALDDNAM),X'00010008',CL8'XTNTSTAT' DDNAME=XTNTSTAT          
TXTDSN   DC    AL2(DALDSNAM),X'000100'                                          
TXTDSNL  DC    AL1(DSNLNQ)                                                      
DSNAME   DC    0C                  ???????.                                     
         DC    C'DDSPROD.'                                                      
DSSYS    DC    C'TST'              TST,CSC,FQA,ADV,REP                          
         DC    C'.'                                                             
DSSESYS  DC    C'XTNT'             XTNT OR SYS                                  
DSEXTRA  DC    C' '                                                             
DSDOTD   DC    C'.D'                                                            
DSNDATE  DC    C'YYMMDD'           YEAR/MONTH/DAY                               
DSDOTT   DC    C'.T'               .T = TIME                                    
DSNHOUR  DC    C'HH'               HOURS                                        
DSNMINS  DC    C'MM'               MINUTES                                      
DSNSECS  DC    C'SS'               SECONDS                                      
DSNLNQ   EQU   *-DSNAME                                                         
*                                                                               
TXTUNIT  DC    AL2(DALUNIT),X'00010005',CL5'SYSDA'     UNIT=SYSDA               
TXTSPACE DC    AL2(DALTRK),X'0000'                                              
TXTPRIME DC    AL2(DALPRIME),X'00010003',XL3'000005'                            
TXTSECND DC    AL2(DALSECND),X'00010003',XL3'000001'                            
TXTSDISP DC    AL2(DALSTATS),X'00010001',X'04'         DISP=(NEW,     )         
TXTNDISP DC    AL2(DALNDISP),X'00010001',X'02'              ( ,CATLG, )         
TXTCDISP DC    AL2(DALCDISP),X'00010001',X'02'              (   ,CATLG)         
TXTRELSE DC    AL2(DALRLSE),X'0000'                                             
TXTCLOSE DC    AL2(DALCLOSE),X'0000'                   FREE=CLOSE               
*                                                                               
         ORG                                                                    
MSGFLEN  DC    AL2(DSNLNQ+L'MSGDSCP)                                            
MSGDSCP  DC    C'<0333> File Checkpoint report- DSN created:'                   
MSGDSN   DS    CL(DSNLNQ)                                                       
         EJECT ,                                                                
************************************************************                    
*        Check SPACE ON EACH FILE REPORT ON LOWSPACE       *                    
************************************************************                    
CHKSPACE NTR1                                                                   
         LARL  R6,SELISTN          LOOP THROUGH SE TAB                          
         USING SEINFD,R6                                                        
CHK010   CLI   0(R6),0                                                          
         BE    CHKSPX                                                           
*                                                                               
         MVC   SENUM,SEINFNUM                                                   
         MVC   SENAM,SEINFNAM                                                   
         MVC   SEOV,SEINFOV        Overlay number                               
         LAM   AR2,AR2,DMALET                                                   
         LLC   R1,SENUM            R1 = SE NUMBER                               
         SLL   R1,6                * 64 = SE ENTRY                              
         LR    R2,R1                                                            
         SAC   512                                                              
*                                                                               
         USING DMSPACED,R2                                                      
         CLC   SENAM,DSPNAME                                                    
         JNE   *+2                 The names should match                       
                                                                                
         ICM   R2,15,DSPECB                                                     
         NILF  GR2,X'3FFFFFFF'     Mask out bits 31 and 30                      
*                                                                               
         USING DMSYSHDR,R2         R2 = SYSTEM ENTRY                            
         SR    R3,R3                                                            
         ICM   R3,3,DSYFILES       R3 = NUMBER OF FILES                         
         BZ    CHKNXT                                                           
*&&UK*&& TM    DSYSSTAT,DSYQOPEN   IS SYSTEM OPEN                               
*&&UK*&& JZ    CHKNXT              NO, SO DON'T CHECK in UK                     
         TM    DSYSSTAT,DSYQEXCL   Exclusive ownership                          
         JO    CHKNXT              YES, SO DON'T CHECK                          
*                                                                               
CHK020   LA    R2,DSYDATA          POINT R2 TO FILE TABLE                       
         USING DSFILHDR,R2                                                      
CHK040   ST    R2,SAVER2                                                        
*                                                                               
         SR    R4,R4                                                            
         SR    R1,R1               INDEX TO FILENAME                            
         ICM   R1,3,DSDSNX                                                      
         BZ    CHK070                                                           
         BCTR  R1,0                                                             
         SLL   R1,5                                                             
         SR    R2,R2                                                            
         L     R2,DHADSN-DMDHDR(,R2)                                            
         AR    R2,R1                                                            
         LR    R4,R2               SAVE FILENAME POINTER IN R4                  
         L     R2,SAVER2                                                        
*                                                                               
         MVC   BYTE,DSFILTOT                                                    
         NI    BYTE,X'C0'          SAVE BIG FLAGS IN BYTE                       
         MVC   FULL+1(3),DSFILTOT                                               
         NC    FULL,=X'003FFFFF'   SAVE MAX TRACK IN FULL                       
         OC    FULL,FULL                                                        
         BZ    CHK070                                                           
*                                                                               
         ICM   R1,15,DSEOF1                                                     
         BNZ   CHK045                                                           
*&&UK*&& J     CHK070              IF UK JUST IGNORE THIS FILE                  
*                                                                               
         MVC   SE#,SENUM                                                        
         MVC   FILE#,DSFILNUM                                                   
         MVC   WORK(L'DSFILDD),DSFILDD                                          
         SAC   0                                                                
*                                                                               
         BRAS  RE,OPENRO                                                        
         BNL   CHKNXT                                                           
         BRAS  RE,LOSIZE           Local file size check                        
         LAM   AR2,AR2,DMALET                                                   
         SAC   512                                                              
         CLC   HALF,=X'FFFF'                                                    
         JNE   CHK056                                                           
*                                                                               
CHK045   TM    BYTE,X'C0'                                                       
         BO    CHK046                                                           
         BM    CHK048                                                           
         SRL   R1,16               16 BIT TRACKS                                
         B     CHK050                                                           
*                                                                               
CHK046   SRL   R1,10               22 BIT TRACKS                                
         B     CHK050                                                           
*                                                                               
CHK048   TM    BYTE,X'40'          18 BIT TRACKS                                
         BZ    *+12                                                             
         SRL   R1,14                                                            
         B     CHK050                                                           
*                                                                               
         TM    BYTE,X'80'          20 BIT TRACKS                                
         BZ    *+12                                                             
         SRL   R1,12                                                            
         B     CHK050                                                           
*                                                                               
CHK050   MVI   INITSPC,YES                                                      
         SR    RF,RF               R1 = CURRENT / FULL = MAX                    
         ICM   RF,7,DSEOFOLD                                                    
         BNZ   *+8                                                              
         MVI   INITSPC,NO                                                       
         ST    R1,FULL1            SAVE CURRENT                                 
         SR    R1,RF               HOW MANY TRACK USED THIS TIME                
         LR    RF,R1               SAVE IN RF                                   
         L     R1,FULL1            RESTORE CURRENT IN R1                        
         STCM  R1,7,DSEOFOLD       SAVE THIS FOR NEXT TIME                      
         SLL   RF,2                CHK POP IS 15 MINS SO * 4 = HOUR             
         ST    RF,FULL1                                                         
*                                                                               
         CLI   INITFLG,NO          IGNORE FIRST TIME IN                         
         BE    CHK055                                                           
         CLI   INITSPC,NO          OR IF ZERO IN EOFOLD                         
         BE    CHK055                                                           
*                                                                               
         OC    FULL1,FULL1         ZERO TRACKS USED THEN FORGET IT              
         BZ    CHK055                                                           
         L     RF,FULL             RF=TOTAL                                     
         SR    RF,R1               LESS CURRENT = REMAINING                     
         SR    RE,RE                                                            
         D     RE,FULL1            REMAINING / TRACK PER HOUR                   
         ST    RF,FULL1            SAVE HOURS TO GO TO EOF                      
*                                                                               
CHK055   MHI   R1,100                                                           
         SR    RE,RE                                                            
         LR    RF,R1                                                            
         D     RE,FULL                                                          
         STH   RF,HALF             HALF = PERCENT USED                          
*                                                                               
CHK056   CLC   DSFILWRN,HALF+1     WANT A WARNING IF THIS % IS USED             
         BNH   CHK057              ISSUE WARNING MESSAGE                        
         CLC   DSFILWRN,DSFILWRS   DO WE NEED TO RESET SECOND WARNING?          
         BE    CHK060              NO                                           
         MVC   DSFILWRS,DSFILWRN   YES, RESET SECONDARY WARNING VALUE           
         B     CHK060                                                           
*                                                                               
CHK057   CLI   HALF+1,97           ALWAYS WARN AT 97% USED                      
         BNL   CHK058              YES, ISSUE WARNING                           
*                                                                               
*&&UK*&& B     CHK058              UK WARN EVERY 15 MINS                        
*                                                                               
         CLC   DSFILWRS,HALF+1     SECONDARY WARNING REACHED?                   
         BH    CHK060              NO, HOLD OFF ON WARNING                      
         LLC   R1,DSFILWRS         PICK UP SECONDARY WARNING VALUE              
         LHI   R0,100                                                           
         SR    R0,R1               SUBTRACT FROM 100                            
         SRL   R0,1                CUT THAT VALUE IN HALF                       
         AR    R1,R0               AND ADD IT BACK TO R1                        
         STC   R1,DSFILWRS         NEXT WARNING ISSUED AT THIS %                
*&&US                                                                           
CHK058   MVC   WORK1(30),=C' WARNING FILE   0% USED. FILE='                     
         LR    R2,R4                                                            
         MVC   WORK1+30(32),0(R2)  FILENAME                                     
         MVC   WORK1+62(2),=C'//'                                               
         EDIT  (B2,HALF),(3,WORK1+14)                                           
         SAC   0                                                                
         GOTO1 =V(DDWTO),DMCB,WORK1,(X'80',0)                                   
         BAS   RE,NOTYMSG                                                       
         SAC   512                                                              
         L     R2,SAVER2                                                        
*&&                                                                             
*&&UK                                                                           
CHK058   MVC   WORK1(35),=C' WARNING FILE   0% AVAILABLE. FILE='                
         LR    R2,R4                                                            
         MVC   WORK1+35(32),0(R2)  FILENAME                                     
         MVC   WORK1+67(2),=C'//'                                               
         LA    R1,100                          REVERSE WARNING FOR UK           
         SH    R1,HALF                                                          
         STH   R1,HALF1                                                         
         EDIT  (B2,HALF1),(3,WORK1+14),ZERO=NOBLANK                             
         SAC   0                                                                
         GOTO1 =V(DDWTO),DMCB,WORK1,(X'80',0)                                   
         BAS   RE,NOTYMSG                                                       
         SAC   512                                                              
         L     R2,SAVER2                                                        
*&&                                                                             
CHK060   OC    FULL1,FULL1                     ZERO USED                        
         BZ    CHK070                                                           
         CLC   FULL1,=F'2'                     MORE THAN 2 HOURS                
         BH    CHK070                                                           
         MVC   WORK1(30),=C' WARNING EOF IN    HOURS FILE='                     
         LR    R2,R4                                                            
         MVC   WORK1+30(32),0(R2)  FILENAME                                     
         MVC   WORK1+62(2),=C'//'                                               
         EDIT  (B4,FULL1),(2,WORK1+16)                                          
         SAC   0                                                                
         GOTO1 =V(DDWTO),DMCB,WORK1,(X'80',0)                                   
         SAC   512                                                              
*                                                                               
CHK070   XC    FULL1,FULL1                                                      
         L     R2,SAVER2                                                        
         LA    R2,DSFILLNQ(,R2)                                                 
         BCT   R3,CHK040                                                        
*                                                                               
CHKNXT   SAC   0                                                                
         BRAS  RE,CLOSERO                                                       
         AHI   R6,SEINFLNQ         Next SE                                      
         B     CHK010                                                           
         DROP  R6                                                               
*                                                                               
CHKSPX   MVI   INITFLG,YES         SET OLD EOFS INITIALISED                     
         SAC   0                                                                
         B     EXITOK                                                           
         EJECT                                                                  
*&&DO                                                                           
************************************************************                    
*        FIND SE ENTRY FOR SYSTEM                          *                    
************************************************************                    
SEEKSYS  NTR1                                                                   
         L     R5,=V(SELIST)       MUST HAVE SELIST                             
         LH    R2,0(,R5)           SET BXLE                                     
         L     R3,2(,R5)                                                        
         LA    R5,6(,R5)                                                        
*                                                                               
         USING SELISTD,R5                                                       
SEEK010  CLC   SESYS,0(R1)         TEST SE SYSTEM                               
         BNE   SEEK020                                                          
*                                                                               
         LR    R1,R5                                                            
         BRAS  RE,OPENSYS          FIND AND OPEN SYSTEM                         
         B     EXITOK              Done with SYSTEM                             
*                                                                               
SEEK020  BXLE  R5,R2,SEEK010       NEXT                                         
         B     EXITOK                                                           
         DROP  R5                                                               
*&&                                                                             
         EJECT                                                                  
************************************************************                    
*        Open system and calculate space available         *                    
************************************************************                    
OPENSYS  NTR1                                                                   
*&&DO                                                                           
         USING SELISTD,R5                                                       
         LR    R5,R1               R1=SELIST FOR SYSTEM                         
         MVC   SENAM,SENAME        SAVE NAME                                    
         MVC   SENUM,SESYS         SAVE NUM                                     
         MVC   SEOV,SEOVSYS        SAVE OV SYS                                  
         DROP  R5                                                               
*&&                                                                             
         LA    RE,SEFILN                                                        
         LA    RF,L'SEFILN         CLEAR DOWN SEFILN                            
         SR    R0,R0                                                            
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         L     RE,=V(UTL)                                                       
         MVC   4(1,RE),SENUM       SET UTL                                      
*                                                                               
         XC    DMCB,DMCB           DO SYSFLES CALL FOR FILES                    
         MVC   DMCB+11(1),SENUM                                                 
         GOTO1 =V(DATAMGR),DMCB,DMREAD,=C'SYSFLES'                              
         ICM   R1,15,12(R1)                                                     
         BNZ   *+6                                                              
         DC    H'0'                                                             
                                                                                
         NILH  GR1,X'00FF'         R1=SE FILES (24 BIT ADDRESS)                 
         ST    R1,ASYSFLES                                                      
*                                                                               
         USING SYSFLSTD,R1                                                      
         CLC   SENUM,SYSFSYS#      CONFIRM FILE LIST                            
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R4,SEFILN                                                        
         SR    RE,RE               RE=NUMBER OF FILES                           
         ICM   RE,3,SYSF#FLS       # OF FILES                                   
         CHI   RE,SEFILMAX-1                                                    
         JH    *+2                 MAKE SEFILN BIGGER                           
*                                                                               
         LA    R1,SYSFLIST         POINT R1 TO FILES                            
                                                                                
         USING DTFPHD,RF                                                        
         XR    RF,RF                                                            
         ICM   RF,7,SYSFADTF       RF=A(DTF)                                    
         TM    DTFFLAG,DTFGLOB     First file global?                           
         BO    OPEN130                                                          
         STM   RE,R1,SVRER1                                                     
         MVC   MSG1SYSN,SENAM                                                   
         LA    R6,MSG1                                                          
         WTO   TEXT=(R6)                                                        
         LM    RE,R1,SVRER1                                                     
                                                                                
OPEN130  TM    SYSFIND1,SFNOP      FILE IS NO-OPED                              
         BO    OPEN132             NEXT                                         
*                                                                               
         TM    SYSFIND1,SFRCV      FILE IS RECOVERY                             
         BZ    OPEN130A                                                         
         SR    RF,RF                                                            
         ICM   RF,7,SYSFADTF       RF=DTF ADDR                                  
         ST    RF,SVRECDTF         Save for recovery rebuild                    
                                                                                
OPEN130A SR    RF,RF                                                            
         ICM   RF,7,SYSFADTF       RF=DTF ADDR                                  
         MVI   0(R4),C'N'                                                       
         TM    DTFOPEN,DTF_RO                                                   
         BO    OPEN131                                                          
         TM    SYSFIND2,SFALIAS                                                 
         BO    OPEN132                                                          
         MVI   0(R4),C'U'                                                       
*                                                                               
OPEN131  MVC   1(7,R4),DTFDD       GET NAME                                     
         LA    R4,8(R4)            NEXT FILE                                    
         DROP  RF                                                               
*                                                                               
OPEN132  LA    R1,SYSFLNQ(,R1)                                                  
         BCT   RE,OPEN130                                                       
         MVI   0(R4),C'X'          END OF                                       
         DROP  R1                                                               
*                                                                               
         GOTO1 =V(DATAMGR),DMCB,OPEN,SENAM,SEFILN,AIO,0                         
         EJECT                                                                  
************************************************************                    
*        READ TRACK PARAMETERS INTO SEFILN                 *                    
************************************************************                    
         LA    R5,SEFILN           RETURN (X'EXT',AL3(TOTAL TRACK))             
         LR    RE,R5                                                            
         LA    RF,L'SEFILN         CLEAR DOWN SEFILN                            
         SR    R0,R0                                                            
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         USING SYSFLSTD,R6                                                      
OPEN150  L     R6,ASYSFLES                                                      
         SR    R1,R1               SAVE SHIFTED OV NUM IN BYTE                  
         IC    R1,SEOV                                                          
         SLL   R1,4                                                             
         STC   R1,BYTE                                                          
*                                                                               
         SR    RE,RE               RE=NUMBER OF FILES                           
         ICM   RE,3,SYSF#FLS                                                    
         LA    R6,SYSFLIST         POINT R6 TO FILES                            
*                                                                               
OPEN160  ST    RE,SAVERE                                                        
         TM    SYSFIND1,SFNOP      File is NO-OPED                              
         BO    OPEN180             Next                                         
         TM    SYSFIND1,SFNOEOF    EOF is off, fixed size file                  
         BO    OPEN180             Next                                         
         TM    SYSFIND2,SF_RO      Read-only                                    
         BO    OPEN180             Next                                         
         TM    SYSFIND2,SFALIAS    Alias files are read-only type               
         BO    OPEN180             Next                                         
                                                                                
         USING DTFPHD,RF                                                        
         SR    RF,RF                                                            
         ICM   RF,7,SYSFADTF       RF=A(DTF)                                    
         TM    DTFOPEN,DTF_NOP     Yes                                          
         BO    OPEN180             Next                                         
         ST    RF,ADTF             Save off current DTF                         
         XC    WORK1(10),WORK1                                                  
         MVC   WORK1(6),DTFDD                                                   
         DROP  RF                                                               
*&&UK                                                                           
         MVC   BYTE1,SYSFILE#      CHECK FILE BELONGS TO SYSTEM                 
         NI    BYTE1,X'F0'                                                      
         CLC   BYTE,BYTE1                                                       
         BNE   OPEN180                                                          
*&&                                                                             
*&&US                                                                           
         CLI   SENUM,X'0A'         Control system?                              
         BE    OPEN160A                                                         
         CLI   SYSFILE#,X'A1'      Control file                                 
         BL    OPEN160A                                                         
         CLI   SYSFILE#,X'AF'      Control file                                 
         BNH   OPEN180             Skip control file                            
*&&                                                                             
OPEN160A TM    SYSFIND1,SFISF      X'01' TEST FOR IS FILE                       
         BZ    OPEN160B                                                         
         BRAS  RE,ISCALC                                                        
         L     R0,ISTTRKS          Total Tracks                                 
         B     OPEN161                                                          
*                                                                               
OPEN160B BRAS  RE,DACALC                                                        
         L     R0,DATTRKS          Total Tracks                                 
*                                                                               
         TM    SYSFIND1,SFRCV      Save DNEXT on recovery file                  
         BZ    *+10                                                             
         MVC   SVDNEXT,DADNEXT                                                  
*************************************************************                   
*  X'00'=16 BIT, X'40'=18 BIT, X'80'=20 BIT, X'C0'=22 BIT   *                   
*************************************************************                   
         USING DTFPHD,RF                                                        
OPEN161  L     RF,ADTF                                                          
         MVC   0(1,R5),SYSFILE#    Save file and track info                     
         STCM  R0,7,1(R5)          Save off track total                         
         TM    DTFTYPE,DTFTBIG     SAVE BIGFILE STATUS TOO                      
         BZ    *+8                                                              
         OI    1(R5),X'40'         40 = BIG FILE 18 BIT TRACK                   
         TM    DTFTYPE,DTFTBIGF                                                 
         BZ    *+8                                                              
         OI    1(R5),X'80'         80 = BIG BIG 20 BIT TRACK                    
         LA    R5,4(,R5)           Next                                         
         DROP  RF                                                               
*                                                                               
OPEN180  LA    R6,SYSFLNQ(,R6)      NEXT FILE                                   
         L     RE,SAVERE                                                        
         BCT   RE,OPEN160                                                       
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
*        READ RECOVERY FILE LAST BLOCK BACK TO DSPACE BUFFER          *         
***********************************************************************         
OPEN200  LA    R2,DMCB             POINT TO DUMMY DMCB                          
         CLI   REBUILD,NO                                                       
         JE    OPEN200X            IGNORE THIS IF REBUILD=N                     
*                                                                               
         XC    DMCB,DMCB                                                        
         XC    RECDA,RECDA                                                      
         XC    Q1(24),Q1           SET PARM LIST TO READ LAST RECORD            
         MVC   Q1,=X'00000008'     =V(READ)                                     
         L     RE,ARECVREC                                                      
         ST    RE,Q2                                                            
                                                                                
         L     R7,SVRECDTF         R7=A(DTF)                                    
         USING DTFPHD,R7                                                        
         TM    DTFOPEN,DTF_RO      EXIT IF READ ONLY FILE                       
         BO    OPEN300                                                          
         ST    R7,Q4                                                            
         TM    DTFTYPE,DTFTBIGF      20 bit?                                    
         BO    OPEN200A              Yes                                        
         CLC   SVDNEXT,=X'00010000'  Test no records on file                    
         BE    OPEN300               None                                       
         B     OPEN200C                                                         
                                                                                
OPEN200A CLC   SVDNEXT,=X'00001000'  Test no records on file                    
         BE    OPEN300               None                                       
                                                                                
OPEN200C CLC   SVDNEXT,=X'00000000'  Test no records on file                    
         BE    OPEN300               None                                       
         MVC   RECDA,SVDNEXT       SET LAST REC ADDRESS                         
         TM    DTFTYPE,DTFTBIGF    20 bit?                                      
         JO    *+8                                                              
         MVI   RECDA+3,0           No clear record byte                         
         LA    RE,RECDA            SET A(DA)                                    
         ST    RE,Q5                                                            
         LA    R1,Q1                                                            
         L     RF,=V(DMOD000)                                                   
*                                                                               
         BASR  RE,RF                                                            
         OC    Q3(2),Q3                                                         
         BNZ   OPEN200X            DISK READ ERROR RCVR FILE                    
         OC    DBLKSZ,DBLKSZ                                                    
         BZ    OPEN200X                                                         
         MVC   RECSZ,DBLKSZ                                                     
*                                                                               
         L     R3,ARECVREC         Recovery buffer                              
         LA    RF,VRCVLNQ          Entry length old style                       
         MVI   VERMONT,VMONTXTN                                                 
         TM    DIND,DINDVRX        Extended vermont?                            
         BZ    OPEN200D            No                                           
         LA    RF,VRCXLNQ          Entry length new style                       
         MVI   VERMONT,VMONTXTN                                                 
*                                                                               
OPEN200D STH   RF,VMNTRYLN                                                      
         MHI   RF,VRCVMAXQ                                                      
         BCTR  RF,0                                                             
         EX    RF,OCVMONT          See if any entries                           
         JZ    OPEN200X            No it's empty                                
*                                                                               
         CLI   REBUILD,YES         IF REBUILD THEN DO IT                        
         JE    OPEN300                                                          
*                                                                               
         MVC   OPMSG(8),DTFDD                                                   
         XC    ECBAD,ECBAD                                                      
         WTOR  TEXT=(OPLEN,REPLY,1,ECBAD,ROUTCDE=(1))                           
         WAIT  ECB=ECBAD                                                        
         CLI   REPLY,YES                                                        
         JE    OPEN300                                                          
         J     OPEN200X                                                         
*                                                                               
OPLEN    DC    H'20'                                                            
OPMSG    DC    C'         REBUILD Y/N'                                          
*                                                                               
OPEN200X XC    RECDA,RECDA                                                      
         J     OPEN300                                                          
         DROP  R7                                                               
         EJECT                                                                  
***********************************************************************         
*        ENTER TRACK VALUES INTO DATASPACE                            *         
***********************************************************************         
OPEN300  XC    DMCB(6*4),DMCB      ALLOCATE SE IN DSPACE                        
         MVC   DMCB+3(1),SENUM                                                  
         GOTO1 =V(LOCKSPC),DMCB                                                 
*                                                                               
         LAM   AR2,AR2,DMALET                                                   
         L     R2,DMCB+4           Local copy                                   
*                                                                               
         USING DMSPACED,R2                                                      
         ICM   R2,15,DSPECB                                                     
         NILF  GR2,X'3FFFFFFF'     Mask out bits 31 and 30                      
*                                                                               
* AH3    put shared memory calls in around here to get A(BUFFER)                
*                                                                               
         SAC   512                 Set up to SYSHDHR and save R2                
         USING DMSYSHDR,R2                                                      
         ST    R2,@SYSHDR                                                       
         EJECT                                                                  
***********************************************************************         
*        If REBUILD=Y then restore recovery buffers and build         *         
*        job and or ADV entries to force recovery by RCVRADV          *         
***********************************************************************         
         CLI   REBUILD,NO                                                       
         BE    RESTR00X            IGNORE THIS IF REBUILD=N                     
         OC    RECDA,RECDA                                                      
         JZ    RESTR00X            IGNORE IF NO BUFFER                          
*                                                                               
RESTR001 L     R2,DSYABUFF         RESTORE BUFFER IF EMPTY                      
         USING VRCVHDR,R2                                                       
         OC    VRCVDA,VRCVDA       In not null then exit                        
         BNZ   RESTR00X                                                         
         DROP  R2                                                               
*                                                                               
RESTR002 XR    R3,R3                                                            
         ICM   R3,3,RECSZ          R3=RECOVERY BLOCK SIZE                       
         BZ    RESTR00X                                                         
         LR    RF,R3                                                            
         L     RE,ARECVREC                                                      
         MVCL  R2,RE               COPY OLD LAST BUFFER BACK IN                 
*                                                                               
RESTR010 LA    R1,VRCVMAXQ         10 VERMONT HEADERS                           
         L     R3,ARECVREC                                                      
         USING VRCVHDR,R3                                                       
         LA    R3,VRCVNTRY                                                      
         USING VRCVNTRY,R3                                                      
*                                                                               
RESTR020 LA    R0,DSJBHMXQ         MAX JOB ENTRIES                              
         L     R2,@SYSHDR                                                       
         USING DMSYSHDR,R2                                                      
         L     R2,DSYAJOBS                                                      
         USING DSJOBHDR,R2                                                      
         XC    FULL,FULL                                                        
*                                                                               
RESTR030 LH    RF,VMNTRYLN         Length of vermont entry                      
         BCTR  RF,0                                                             
         EX    RF,*+8              ANY VERMONT ENTRY                            
         B     *+10                                                             
         OC    0(0,R3),0(R3)                                                    
         BZ    RESTR050                                                         
*                                                                               
         OC    DSJOBNAM,DSJOBNAM   FREE ENTRY?                                  
         BNZ   RESTR035                                                         
         OC    FULL,FULL                                                        
         BNZ   RESTR035                                                         
         ST    R2,FULL             SAVE ADDR IN FULL                            
*                                                                               
RESTR035 CLI   VRCVOFFL,X'FF'      FOR A JOB                                    
         BNE   RESTR036                                                         
         MVC   JOBNUM,VRCVJOB#                                                  
         CLI   VERMONT,VMONTXTN                                                 
         BNE   *+10                                                             
         MVC   JOBNUM,VRCXJOB#                                                  
         CLC   DSJOBNUM,JOBNUM     TEST JOBNUM MATCH                            
         BE    RESTR050                                                         
         B     RESTR040                                                         
*                                                                               
RESTR036 CLC   DSJOBADV,VRCVFID#   TEST ADV NUM MATCH                           
         BE    RESTR050                                                         
*                                                                               
RESTR040 LA    R2,DSJBHLNQ(,R2)    NEXT JOB ENTRY                               
         BCT   R0,RESTR030                                                      
*                                                                               
         ICM   R2,15,FULL          GET A(FREE ENTRRY)                           
         BZ    RESTR050                                                         
         USING DSJOBHDR,R2         FAKE UP NEW JOB ENTRY FOR RECOVERY           
*                                                                               
         SAC   0                                                                
         STM   R0,R1,DUB1                                                       
         MVC   WORK1(26),=C'FOUND   ................//'                         
         GOTOR =V(HEXOUT),DMCB,(R3),WORK1+8,8                                   
         GOTOR =V(DDWTO),DMCB,WORK1,(X'80',0)                                   
         LM    R0,R1,DUB1                                                       
         SAC   512                                                              
*                                  Off-line jobs                                
         CLI   VRCVOFFL,X'FF'      DUMMY ENTRIES WILL BE RECOVERED              
         BNE   RESTR041                                                         
         MVC   DSJOBNAM,=C'DUMMYJ..'                                            
         MVC   DSJOBNAM+6(2),JOBNUM Add jobnum to DUMMY to make unique          
         MVC   DSJOBNUM,JOBNUM                                                  
         MVI   DSJOBFLG,DSJOBRCV   Set as recoverable                           
         B     RESTR050                                                         
*                                                                               
RESTR041 MVC   DSJOBNAM,=C'FADUMMY '                                            
         MVC   DSJOBNAM+7(1),VRCVFID# FADUMMY+SYS (+SYS to make unique)         
         MVC   DSJOBNUM+1(1),VRCVFID# JOB NUM 00+SYS                            
         MVI   DSJOBFLG,DSJOBRCV                                                
         MVC   DUB,DSJOBNAM        SAVE IN DUB AND HALF FOR ADV TABLE           
         MVC   HALF,DSJOBNUM                                                    
*                                                                               
         MVC   DSJOBADV,VRCVFID#   SET ADV IN SYSTEM TABLE AND ADV TAB          
         USING DMDSHDR,R2                                                       
         XR    R2,R2                                                            
         L     R2,DHAJOBS                                                       
         LA    R0,DSJOBMXQ+1       128 possible job entries                     
         USING DSJOBD,R2                                                        
RESTR045 OC    DSJNAM,DSJNAM                                                    
         BNZ   RESTR049                                                         
*                                                                               
         MVC   DSJNAM,DUB          USE JOBNAME AND JOBNUM FROM EARLIER          
         MVC   DSJNUM,HALF                                                      
         MVC   DSJADV,VRCVFID#                                                  
         B     RESTR050                                                         
*                                                                               
RESTR049 LA    R2,DSJOBLNQ(,R2)    NEXT ADV TABLE ENTRY                         
         BCT   R0,RESTR045                                                      
         DC    H'0'                TOO MANY ADV SYSTEMS                         
         DROP  R2                                                               
*                                                                               
RESTR050 AH    R3,VMNTRYLN         NEXT VERMONT ENTRY                           
         BCT   R1,RESTR020                                                      
         DROP  R3                                                               
*                                                                               
RESTR00X L     R2,@SYSHDR                                                       
                                                                                
***********************************************************************         
*        CLEAN UP JOBTAB AND EXIT                                     *         
***********************************************************************         
         USING DMSYSHDR,R2                                                      
         LA    R0,DSJBHMXQ         MAX JOB ENTRIES                              
         L     R2,DSYAJOBS                                                      
         USING DSJOBHDR,R2                                                      
OPEN301  CLC   MVSNAME,DSJOBNAM    FIND THIS RECOVERY JOB                       
         BE    OPEN302                                                          
         LA    R2,DSJBHLNQ(,R2)                                                 
         BCT   R0,OPEN301                                                       
         B     OPEN303                                                          
*                                                                               
OPEN302  XC    0(DSJBHLNQ,R2),0(R2)      REMOVE IT FROM JOBLIST                 
*                                                                               
OPEN303  L     R2,@SYSHDR                                                       
         USING DMSYSHDR,R2                                                      
         L     R2,DSYAJOBS                                                      
         USING DSJOBHDR,R2                                                      
         OC    0(256,R2),0(R2)     IS JOBTABLE NOW EMPTY                        
         BNZ   OPEN304                                                          
         OC    256(256,R2),256(R2)                                              
         BNZ   OPEN304                                                          
         L     R2,@SYSHDR                                                       
         USING DMSYSHDR,R2                                                      
         MVI   DSYSSTAT,0          IF SO SET THIS SYSTEM TO CLOSED              
*                                                                               
OPEN304  L     R2,@SYSHDR                                                       
         SR    R1,R1                                                            
         ICM   R1,3,DSYFILES       SET R1 TO NUMBER OF FILES                    
         LA    R2,DSYDATA                                                       
*                                                                               
OPEN310  LA    RF,SEFILN           TABLE FROM PREVIOUS ROUTINE                  
         DROP  R2                                                               
*                                                                               
         USING DSFILHDR,R2                                                      
OPEN320  CLC   DSFILNUM,0(RF)      FIND FILE NUMBER                             
         BE    OPEN330             MATCHED                                      
         LA    RF,4(RF)                                                         
         CLI   0(RF),0             END OF LIST                                  
         BNE   OPEN320                                                          
         B     OPEN331                                                          
*                                                                               
OPEN330  MVC   DSFILTOT,1(RF)      SAVE TOTAL TRACKS                            
OPEN331  LA    R2,DSFILLNQ(,R2)    NEXT ENTRY IN DATASPACE                      
         BCT   R1,OPEN310                                                       
         SAC   0                                                                
*                                                                               
         XC    DMCB(6*4),DMCB      FREE SE IN DSPACE                            
         OI    DMCB,X'10'                                                       
         MVC   DMCB+3(1),SENUM                                                  
         GOTO1 =V(LOCKSPC),DMCB                                                 
         GOTO1 =V(DATAMGR),DMCB,CLOSE,SENAM,SEFILN,AIO,0                        
*                                                                               
         B     EXITOK                                                           
         EJECT                                                                  
ISDATA   DS    0F                                                               
ISATRKS  DS    F                   Available tracks                             
ISUTRKS  DS    F                   Used      tracks                             
ISTTRKS  DS    F                   Total     tracks                             
ISATPCT  DS    F                   Available percent                            
ISUTPCT  DS    F                   Used      percent                            
*                                                                               
DADATA   DS    0F                                                               
DADNEXT  DS    F                   DNEXT                                        
DAUTRKS  DS    F                   Used      tracks                             
DATTRKS  DS    F                   Total     tracks                             
DAATRKS  DS    F                   Available tracks                             
DAATPCT  DS    F                   Available percent                            
DAUTPCT  DS    F                   Used      percent                            
                                                                                
MSG1     DC    AL2(MSG1LNQ)                                                     
MSG1TXT  DC    C'Failed DSN check - system not global>'                         
MSG1SYSN DC    C'xxxxxxx'                                                       
MSG1LNQ  EQU   *-MSG1TXT                                                        
         ORG                                                                    
VERMONT  DS    C                                                                
VMONTREG EQU   C'R'                VERMONT REGULAR                              
VMONTXTN EQU   C'X'                VERMONT EXTENDED                             
         DS    0H                                                               
JOBNUM   DS    XL2                                                              
VMNTRYLN DS    XL2                                                              
         USING VRCVHDR,R3                                                       
OCVMONT  OC    VRCVNTRY(0),VRCVNTRY   IGNORE ZERO ENTRIES                       
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* LOCAL FILE SIZE CHECK                                                         
***********************************************************************         
LOSIZE   NTR1                                                                   
         MVC   HALF,=X'FFFF'                                                    
         MVI   FLDFLAG,0                                                        
*                                                                               
         L     RE,=V(UTL)                                                       
         MVC   4(1,RE),SENUM       SET UTL                                      
         L     R4,ASSB                                                          
         USING SSBD,R4                                                          
*                                                                               
         XC    DMCB,DMCB           DO SYSFLES CALL FOR FILES                    
         MVC   DMCB+11(1),SENUM                                                 
         GOTO1 =V(DATAMGR),DMCB,DMREAD,=C'SYSFLES'                              
         ICM   R1,15,12(R1)                                                     
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         NILH  GR1,X'00FF'         R1=SE FILES (24 BIT ADDRESS)                 
         ST    R1,ASYSFLES                                                      
*                                                                               
         L     R6,ASYSFLES                                                      
         USING SYSFLSTD,R6                                                      
         CLC   SENUM,SYSFSYS#      CONFIRM FILE LIST                            
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LLC   R1,SEOV             SAVE SHIFTED OV NUM IN BYTE                  
         SLL   R1,4                                                             
         STC   R1,BYTE                                                          
*                                                                               
         SR    R1,R1               R1=NUMBER OF FILES                           
         ICM   R1,3,SYSF#FLS       # OF FILES                                   
         CHI   R1,SEFILMAX-1                                                    
         JH    *+2                 MAKE SEFILN BIGGER                           
*                                                                               
         LA    R6,SYSFLIST         POINT R6 TO FILES                            
LOS130   CLC   SYSFILE#,FILE#      THE FILE WE WANT?                            
         BE    LOS132              NEXT                                         
         LA    R6,SYSFLNQ(,R6)                                                  
         BCT   R1,LOS130                                                        
         DC    H'0'                Where did the file go?                       
*                                                                               
*        USING DTFPHD,R5                                                        
LOS132   XR    R5,R5                                                            
         ICM   R5,7,SYSFADTF       R5=A(DTF)                                    
         ST    R5,ADTF                                                          
*        DROP  R5                                                               
*                                                                               
         TM    SYSFIND1,SFNOP      FILE IS NO-OPED                              
         BO    EXIT                                                             
         TM    SYSFIND2,SFALIAS    FILE IS ALIAS                                
         BO    EXIT                                                             
*        MVI   SEFILN,C'N'                                                      
*        MVC   SEFILN+1(7),WORK                                                 
*        MVI   SEFILN+8,C'X'       END OF LIST                                  
*                                                                               
*&&UK                                                                           
         MVC   BYTE1,SYSFILE#      CHECK FILE BELONGS TO SYSTEM                 
         NI    BYTE1,X'F0'                                                      
         CLC   BYTE,BYTE1                                                       
         BNE   EXIT                                                             
*&&                                                                             
*&&US                                                                           
         CLI   SENUM,X'0A'         Control system?                              
         BE    LOS140                                                           
         CLI   SYSFILE#,X'A1'      Control file                                 
         BL    LOS140                                                           
         CLI   SYSFILE#,X'AF'      Control file                                 
         BNH   EXIT                Skip control file                            
*&&                                                                             
LOS140   DS    0H                                                               
*        OI    SSOFLAG3,SSO3NOFM   Do not output file info message              
*        GOTO1 =V(DATAMGR),DMCB,OPEN,SENAM,SEFILN,AIO,0                         
*        NI    SSOFLAG3,X'FF'-SSO3NOFM                                          
*        DROP  R5                                                               
*                                                                               
LOS144   TM    SYSFIND1,SFISF      X'01' TEST FOR IS FILE                       
         BZ    LOS150                                                           
         BRAS  RE,ISCALC                                                        
         MVC   HALF,ISUTPCT+2                                                   
         B     LOS160                                                           
*                                                                               
LOS150   BRAS  RE,DACALC                                                        
         MVC   HALF,DAUTPCT+2                                                   
*                                                                               
LOS160   DS    0C                                                               
*        OI    SSOFLAG3,SSO3NOFM   Do not output file info message              
*        GOTO1 =V(DATAMGR),DMCB,CLOSE,SENAM,SEFILN,AIO,0                        
*        NI    SSOFLAG3,X'FF'-SSO3NOFM                                          
         B     EXIT                                                             
         DROP  R4,R6                                                            
***********************************************************************         
* IS file calculate available, used and total tracks                            
***********************************************************************         
         USING ISDTF,R5                                                         
ISCALC   NTR1                                                                   
         L     R5,ADTF                                                          
         MVI   WORK,X'FF'                                                       
         MVC   WORK+1(L'WORK-1),WORK                                            
         GOTO1 =V(DATAMGR),DMCB,DMRDHI,ISFDD,WORK,AIO,0,0                       
         GOTO1 =V(DATAMGR),DMCB,=C'FINFO',0,ISDATA,(R5),0                       
         L     RF,ISATRKS                                                       
         MHI   RF,100                                                           
         XR    RE,RE                                                            
         D     RE,ISTTRKS                                                       
         ST    RF,ISATPCT                                                       
                                                                                
         L     RF,ISUTRKS                                                       
         MHI   RF,100                                                           
         XR    RE,RE                                                            
         D     RE,ISTTRKS                                                       
         ST    RF,ISUTPCT                                                       
                                                                                
         CLI   MODE,C'I'                                                        
         BNE   EXIT                                                             
                                                                                
         MVC   WORK1,SPACES                                                     
         LA    RF,L'WORK1-2                                                     
         STCM  RF,3,WORK1                                                       
         MVC   WORK1+2(7),ISFDD                                                 
         MVI   WORK+10,C'>'                                                     
         MVC   WORK1+14(6),=C'Avail='                                           
         MVC   WORK1+25(6),=C' Used='                                           
         MVC   WORK1+37(6),=C'Total='                                           
                                                                                
         LA    R4,WORK1                                                         
         EDIT  ISATPCT,(4,20(R4)),ZERO=NOBLANK,TRAIL=C'%'                       
         EDIT  ISUTPCT,(4,31(R4)),ZERO=NOBLANK,TRAIL=C'%'                       
         EDIT  ISTTRKS,(7,43(R4)),ALIGN=LEFT,ZERO=NOBLANK                       
         WTO   TEXT=WORK1,MCSFLAG=HRDCPY                                        
         B     EXIT                                                             
         DROP  R5                                                               
         EJECT                                                                  
                                                                                
***********************************************************************         
* DA file calculate available, used and total tracks                            
***********************************************************************         
         USING DTFPHD,R5                                                        
DACALC   NTR1                                                                   
         L     R5,ADTF                                                          
         GOTO1 =V(DATAMGR),DMCB,=C'FINFO',0,DADATA,(R5),0                       
         L     RF,DATTRKS          Total tracks                                 
         L     RE,DAUTRKS          Total used                                   
         LR    R1,RE                                                            
         SR    RF,RE                                                            
         ST    RF,DAATRKS          Available                                    
                                                                                
         L     RF,DAATRKS                                                       
         MHI   RF,100                                                           
         XR    RE,RE                                                            
         D     RE,DATTRKS                                                       
         ST    RF,DAATPCT                                                       
                                                                                
         L     RF,DAUTRKS                                                       
         MHI   RF,100                                                           
         XR    RE,RE                                                            
         D     RE,DATTRKS                                                       
         ST    RF,DAUTPCT                                                       
                                                                                
         CLI   MODE,C'I'                                                        
         BNE   EXIT                                                             
                                                                                
         MVC   WORK1,SPACES                                                     
         LA    RF,L'WORK1-2                                                     
         STCM  RF,3,WORK1                                                       
         MVC   WORK1+2(7),DTFDD                                                 
         MVI   WORK+10,C'>'                                                     
         MVC   WORK1+14(6),=C'Avail='                                           
         MVC   WORK1+25(6),=C' Used='                                           
         MVC   WORK1+37(6),=C'Total='                                           
                                                                                
         LA    R4,WORK1                                                         
         EDIT  DAATPCT,(4,20(R4)),ZERO=NOBLANK,TRAIL=C'%'                       
         EDIT  DAUTPCT,(4,31(R4)),ZERO=NOBLANK,TRAIL=C'%'                       
         EDIT  DATTRKS,(7,43(R4)),ALIGN=LEFT,ZERO=NOBLANK                       
         WTO   TEXT=WORK1,MCSFLAG=HRDCPY                                        
         B     EXIT                                                             
         DROP  R5                                                               
         EJECT                                                                  
                                                                                
***********************************************************************         
* Notification Message                                                          
*        on entry - HALF= % used, WORK1= ISCALC/DACALC message                  
***********************************************************************         
NOTYMSG  NTR1                                                                   
         CLC   NOTYLIST,SPACES                                                  
         BNH   EXIT                                                             
         XC    WORK2,WORK2                                                      
         LA    R2,WORK2                                                         
         MVC   WORK2+2(9),=C'AUTONOTE*'                                         
         MVC   WORK2+2+9(L'NOTYLIST),NOTYLIST                                   
*                                                                               
         LA    R4,WORK2+L'WORK2-1                                               
NOTYMSG1 CR    R2,R4                                                            
         BE    NOTYMSG2                                                         
         CLI   0(R4),C' '                                                       
         BH    NOTYMSG2                                                         
         BCTR  R4,0                                                             
         B     NOTYMSG1                                                         
*                                                                               
NOTYMSG2 MVI   1(R4),C':'                                                       
         MVC   2(35,R4),=C' WARNING ONLY   0% AVAILABLE. FILE='                 
         LHI   R1,100                                                           
         SH    R1,HALF                                                          
         LA    R4,16(,R4)                                                       
         EDIT  (R1),(3,(R4))                                                    
         MVC   21(32,R4),WORK1+30                                               
         LA    R4,56(,R4)                                                       
         SR    R4,R2                                                            
         STCM  R4,3,WORK2                                                       
         WTO   TEXT=WORK2,MCSFLAG=HRDCPY                                        
         B     EXIT                                                             
         EJECT                                                                  
                                                                                
***********************************************************************         
* ERROR EXITS                                                         *         
***********************************************************************         
ERTOBIG  MVC   WORK,=C'PARAMETER TOO BIG //'                                    
         B     ERREXIT                                                          
                                                                                
ERTOMANY MVC   WORK,=C'TOO MANY FIELDS //'                                      
         B     ERREXIT                                                          
                                                                                
ERIVPARM MVC   WORK,=C'INVALID PARM //'                                         
         MVC   WORK+16(30),SYSCARD                                              
         B     ERREXIT                                                          
*                                                                               
ERREXIT  L     RD,SAVERD                                                        
         GOTO1 =V(DDWTO),DMCB,WORK,(X'80',0)                                    
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* COMMON ROUTINES                                                     *         
***********************************************************************         
ARSOFF   SAC   0                                                                
         LAM   AR0,ARF,ARZERO                                                   
         BR    RE                                                               
*                                                                               
EXITOK   CR    RB,RB                                                            
         B     EXIT                                                             
*                                                                               
EXITL    CLI   *,255                                                            
         B     EXIT                                                             
*                                                                               
EXIT     XIT1  ,                                                                
*                                                                               
XMOD     L     RD,SAVERD           EXIT FROM TOP                                
         XMOD1 ,                                                                
         EJECT                                                                  
XTNTSTAT DCB   DSORG=PS,MACRF=PM,DDNAME=XTNTSTAT,RECFM=FB,LRECL=(166), +        
               EXLST=JFCBPTR                                                    
*                                                                               
JFCB     DS    44F                                                              
JFCBPTR  DC    X'87'                                                            
         DC    AL3(JFCB)                                                        
         EJECT                                                                  
***********************************************************************         
* CONSTANTS & LTORG                                                   *         
***********************************************************************         
         LTORG                                                                  
YES      EQU   C'Y'                                                             
NO       EQU   C'N'                                                             
K        EQU   1024                                                             
*                                                                               
MVSNAME  DS    CL8                 JOB NAME                                     
*                                                                               
NOTYLIST DS    CL50                NOTIFICATION LIST                            
*                                                                               
ASSB     DC    V(SSB)                                                           
DMALET   DS    A                                                                
*                                                                               
OPENED   DC    AL1(NO)             SYSTEM IS NOT OPEN                           
*                                                                               
STATOPEN DC    C'N'                Stat file open? (Y/N)                        
STATNOW  DC    F'0'                Current system                               
*                                                                               
INITOPEN DC    C'N'                INIT in progress                             
INITNOW  DC    A(0)                Current system                               
*                                                                               
PSLINE   DS    CL130                                                            
SPACES   DC    CL166' '                                                         
EFFS     DC    16X'FF'                                                          
*                                                                               
ARZERO   DC    16F'0'                                                           
*                                                                               
DMREAD   DC    CL8'DMREAD'                                                      
DMRDHI   DC    CL8'DMRDHI'                                                      
OPEN     DC    CL8'OPEN  '                                                      
CLOSE    DC    CL8'DMCLSE'                                                      
*                                                                               
ECBAD    DC    A(0)                                                             
REPLY    DC    CL8' '                                                           
*                                                                               
SELISTN  DS    256CL(SEINFLNQ)                                                  
*                                                                               
         EJECT                                                                  
*&&US                                                                           
***********************************************************************         
*        VALIDATE VSAM DEMO CONTROL AND ADD TO TABLE                            
*                                                                               
* One card per rule. Each card has the format: aa,files[,function]              
* aa       - the alpha id the rule applies to. It can be ALL.                   
* files    - the files the rule applies to in the format f/f/...                
*            where f can be A, N, R, NTI or PAV. Can also specify               
*            NONE.                                                              
* function - an optional string that identifies the systems and                 
*            programs the rule applies to. If omitted, assume all.              
*          - For FACPAK, specify FACPAK[,facid[,system[prog]]]                  
*            facid, system and prog are hex, e.g. FACPAK,10,0211 to             
*            apply to ADV6, SPOT/BUY. Facid can be omitted.                     
*          - For RUNNER, specify RUNNER[,facid[,server]]                        
*            facid is hex, e.g. RUNNER,10,T to apply to requests                
*            from ADV6 server type T (Desktops). Facid can be omitted.          
*          - For batch, specify BATCH[facid[,system[,progid]]]                  
*            facid and system are hex, e.g. BATCH,,02,RW to apply to            
*            SPOT WRITER requests. Facid can be omitted and can be used         
*            to limit to SOON requests from a particular FACPAK.                
* Examples:                                                                     
*  ALL,N/R           - all alpha ids use the N and R files                      
*  D1,N,FACPAK       - alpha Dl to use the N file in any FACPAK                 
*  ALL,N/R,RUNNER,10 - all alpha ids to use the N and R files in any            
*                      runner request from ADV6                                 
*  D1,R,BATCH,,02,RW - alpha D1 to use the R file in any batch SPOT             
*                      WRITER request from any FACPAK                           
***********************************************************************         
VALVSAM  NTR1  BASE=*                                                           
*                                                                               
         XC    WORK(DMVSVTLQ),WORK CLEAR A NEW ENTRY                            
WK       USING DMVSVSNT,WORK                                                    
*                                                                               
         GOTO1 =V(SCANNER),DMCB,(C'C',CARD),IOAREA                              
         LLC   R3,DMCB+4           NUMBER OF ENTRIES                            
         CHI   R3,2                                                             
         JL    ERIVPARM            MUST BE AT LEAST TWO FIELDS                  
*                                                                               
VALVS10  LA    R4,IOAREA           VALIDATE ALPHA ID                            
         CLI   1(R4),0                                                          
         JNE   ERIVPARM            CAN'T HAVE SECOND PART                       
         CLI   0(R4),3                                                          
         JNE   VALVS12             CAN'T BE 'ALL' IF NOT 3 BYTES                
         CLC   =C'ALL',12(R4)                                                   
         JE    VALVS20             LEAVE NULL IF ALL                            
         J     ERIVPARM            CAN'T BE ANYTHING ELSE                       
VALVS12  CLI   0(R4),2                                                          
         JNE   ERIVPARM            ALPHA ID MUST BE 2 BYTES                     
         MVC   WK.DMVSVTAL,12(R4)                                               
*                                                                               
VALVS20  JCT   R3,*+8              SKIP IF MORE THAN ONE FIELD                  
         J     *+2                 WE ALREADY CHECKED THERE WERE TWO            
         LA    R4,32(,R4)          VALIDATE FILES                               
         CLI   1(R4),0                                                          
         JNE   ERIVPARM            CAN'T HAVE SECOND PART                       
         CLI   0(R4),4                                                          
         JNE   VALVS22             CAN'T BE 'NONE' IF NOT 4 BYTES               
         CLC   =C'NONE',12(R4)                                                  
         JE    VALVS30             LEAVE NULL IF 'NONE'                         
VALVS22  LA    R1,12(,R4)          START OF FIELD                               
         LLC   R0,0(,R4)                                                        
VALVS24  LA    RF,3                                                             
         CLC   =C'NTI',0(R1)                                                    
         JNE   *+12                                                             
         MVI   BYTE,DMVSVNTQ                                                    
         J     VALVS26                                                          
         CLC   =C'PAV',0(R1)                                                    
         JNE   *+12                                                             
         MVI   BYTE,DMVSVPVQ                                                    
         J     VALVS26                                                          
         LA    RF,1                                                             
         CLI   0(R1),C'A'                                                       
         JNE   *+12                                                             
         MVI   BYTE,DMVSVDAQ                                                    
         J     VALVS26                                                          
         CLI   0(R1),C'N'                                                       
         JNE   *+12                                                             
         MVI   BYTE,DMVSVDNQ                                                    
         J     VALVS26                                                          
         CLI   0(R1),C'R'                                                       
         JNE   ERIVPARM                                                         
         MVI   BYTE,DMVSVDRQ                                                    
VALVS26  MVC   BYTE1,BYTE                                                       
         NC    BYTE1,WK.DMVSVTFL   ALREADY SET?                                 
         JNZ   ERIVPARM                                                         
         OC    WK.DMVSVTFL,BYTE                                                 
         SR    R0,RF                                                            
         JZ    VALVS30             ALL DONE                                     
         JM    ERIVPARM                                                         
         AR    R1,RF                                                            
         CLI   0(R1),C'/'          SEPARATOR                                    
         JNE   ERIVPARM            NOPE                                         
         LA    R1,1(,R1)           NEXT FIELD                                   
         JCT   R0,VALVS24                                                       
         J     ERIVPARM            NOTHING AFTER /                              
*                                                                               
VALVS30  JCT   R3,*+8              TEST A FUNCTION FIELD PRESENT                
         J     VALVS90             NO, ALL DONE                                 
         LA    R4,32(,R4)          VALIDATE FUNCTION                            
         CLI   1(R4),0                                                          
         JNE   ERIVPARM            CAN'T HAVE SECOND PART                       
         CLC   =C'FACPAK ',12(R4)                                               
         JNE   *+12                                                             
         MVI   WK.DMVSVTTY,DMVSVTFQ FACPAK TYPE                                 
         J     VALVS32             GO DO FACID                                  
         CLC   =C'BATCH ',12(R4)                                                
         JNE   *+12                                                             
         MVI   WK.DMVSVTTY,DMVSVTBQ BATCH TYPE                                  
         J     VALVS32             GO DO FACID                                  
         CLC   =C'RUNNER ',12(R4)                                               
         JNE   ERIVPARM                                                         
         MVI   WK.DMVSVTTY,DMVSVTRQ RUNNER TYPE                                 
*                                                                               
VALVS32  JCT   R3,*+8              TEST A FACID FIELD                           
         J     VALVS90             NO, ALL DONE                                 
         LA    R4,32(,R4)          VALIDATE FACID (FACPAK/RUNNER)               
         CLI   1(R4),0                                                          
         JNE   ERIVPARM            CAN'T HAVE SECOND PART                       
         CLI   0(R4),0                                                          
         JE    VALVS34             MAY BE OMITTED                               
         CLI   0(R4),3                                                          
         JNE   *+14                CAN'T BE 'ALL' IF NOT 3 BYTES                
         CLC   =C'ALL',12(R4)                                                   
         JE    VALVS34             'ALL' SAME AS IF OMITTED                     
         CLI   0(R4),2                                                          
         JNE   ERIVPARM            MUST BE 2 BYTES                              
         TM    2(R4),X'20'                                                      
         JZ    ERIVPARM            MUST BE HEX                                  
         GOTOR =V(HEXIN),DMCB,12(R4),WK.DMVSVTFI,2                              
VALVS34  CLI   WK.DMVSVTTY,DMVSVTRQ TEST RUNNER                                 
         JNE   VALVS36             NO, GO LOOK AT SYSTEM                        
*                                                                               
         JCT   R3,*+8              TEST A SERVER FIELD                          
         J     VALVS90             NO, ALL DONE                                 
         LA    R4,32(,R4)          VALIDATE SERVER (RUNNER ONLY)                
         CLI   1(R4),0                                                          
         JNE   ERIVPARM            CAN'T HAVE SECOND PART                       
         CLI   0(R4),1                                                          
         JNE   ERIVPARM            SERVER MUST BE ONE CHARACTER                 
         MVC   WK.DMVSVTSV,12(R4)                                               
         J     VALVS40             ALL DONE (HOPEFULLY)                         
*                                                                               
VALVS36  JCT   R3,*+8              TEST A SYS[PROG] FIELD                       
         J     VALVS90             NO, ALL DONE                                 
         LA    R4,32(,R4)          VALIDATE SYS[PROG]                           
         CLI   1(R4),0                                                          
         JNE   ERIVPARM            CAN'T HAVE SECOND PART                       
         TM    2(R4),X'20'                                                      
         JZ    ERIVPARM            MUST BE HEX                                  
         CLI   0(R4),2                                                          
         JE    VALVS38             TWO BYTE SYSTEM ONLY                         
         CLI   WK.DMVSVTTY,DMVSVTFQ TEST FACPAK                                 
         JNE   ERIVPARM            NO, ERROR                                    
         CLI   0(R4),4                                                          
         JNE   ERIVPARM            FOUR BYTE SYSTEM/PROGRAM                     
         GOTOR =V(HEXIN),DMCB,12(R4),WK.DMVSVTSY,4                              
         J     VALVS40             ALL DONE (HOPEFULLY)                         
VALVS38  GOTOR =V(HEXIN),DMCB,12(R4),WK.DMVSVTSY,2                              
         CLI   WK.DMVSVTTY,DMVSVTBQ TEST BATCH                                  
         JNE   VALVS40             NO, ALL DONE (HOPEFULLY)                     
*                                                                               
         JCT   R3,*+8              TEST A PROGRAM FIELD                         
         J     VALVS90             NO, ALL DONE                                 
         LA    R4,32(,R4)          VALIDATE PROGRAM (BATCH ONLY)                
         CLI   1(R4),0                                                          
         JNE   ERIVPARM            CAN'T HAVE SECOND PART                       
         CLI   0(R4),2                                                          
         JNE   ERIVPARM            PROGRAM MUST BE TWO CHARACTERS               
         MVC   WK.DMVSVTRP,12(R4)                                               
*                                                                               
VALVS40  JCT   R3,ERIVPARM         ERROR IF ANOTHER FIELD                       
                                                                                
         DROP  WK                                                               
*                                                                               
VALVS90  SR    R2,R2               COPY NEW ENTRY TO TABLE                      
         LAM   AR2,AR2,DMALET                                                   
         SAC   512                                                              
         L     R2,DHASSBG-DMDHDR(,R2)                                           
         USING FASSBG,R2                                                        
         LH    R1,SSGDMVST         GET COUNT OF ENTRIES                         
         CHI   R1,DMVSVTMQ         COMPARE WITH MAXIMUM                         
         JNH   VALVS92             OK IF ROOM FOR ANOTHER                       
         SAC   0                                                                
         B     ERIVPARM            ELSE INVALID, TOO MANY CARDS                 
*                                                                               
VALVS92  LA    RF,1(,R1)                                                        
         STH   RF,SSGDMVST         INCREMENT COUNT OF ENTRIES                   
         MHI   R1,DMVSVTLQ         TIMES LENGTH                                 
         LA    R2,SSGDMVST+2(R1)   NEW ENTRY LOCATION                           
         DROP  R2                                                               
         MVC   0(DMVSVTLQ,R2),WORK COPY NEW ENTRY                               
         MVI   DMVSVTLQ(R2),X'FF'  ADD A TERMINATOR JUST IN CASE                
                                                                                
         SAC   0                                                                
                                                                                
         B     EXITOK                                                           
         LTORG                                                                  
***********************************************************************         
*        EMPTY VSAM DEMO CONTROL TABLE                                          
***********************************************************************         
CLRVSAM  NTR1                                                                   
*                                                                               
         SR    R2,R2                                                            
         LAM   AR2,AR2,DMALET                                                   
         SAC   512                                                              
         L     R2,DHASSBG-DMDHDR(,R2)                                           
         USING FASSBG,R2                                                        
         XC    SSGDMVST(2),SSGDMVST CLEAR COUNT OF ENTRIES                      
         DROP  R2                                                               
                                                                                
         SAC   0                                                                
                                                                                
         B     EXITOK                                                           
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE                                                     *         
***********************************************************************         
WORKD    DSECT                                                                  
DUB      DS    D                                                                
DUB1     DS    D                                                                
FULL     DS    F                                                                
FULL1    DS    F                                                                
HALF     DS    H                                                                
HALF1    DS    H                                                                
BYTE     DS    X                                                                
BYTE1    DS    X                                                                
MODE     DS    C                                                                
FMODE    DS    C                                                                
COMM     DS    C                                                                
INITFLG  DS    C                                                                
REBUILD  DS    C                                                                
INITSPC  DS    C                                                                
*                                                                               
SVRER1   DS    4A                                                               
SAVERD   DS    A                                                                
SAVERE   DS    A                                                                
SAVER2   DS    A                                                                
@SYSHDR  DS    A                                                                
ASYSFLES DS    A                                                                
AIO      DS    A                                                                
ADTF     DS    A                                                                
ASYSTABS DS    A                                                                
*                                                                               
SAVEPARM DS    0XL8                                                             
SAVEP1   DS    F                                                                
SAVEP2   DS    F                                                                
*                                                                               
SAVDATE  DS    CL6                 YEAR/MONTH/DAY                               
SAVHOUR  DS    CL2                 HOURS                                        
SAVMINS  DS    CL2                 MINUTES                                      
SAVSECS  DS    CL2                 SECONDS                                      
*                                                                               
TOTALSYS DS    F                                                                
*                                                                               
ARECVREC DS    A                                                                
SVRECDTF DS    F                                                                
SVDNEXT  DS    F                                                                
RECDA    DS    F                                                                
RECSZ    DS    H                                                                
*                                                                               
CARD     DS    CL80                                                             
WTOBLK   DS    CL80                                                             
*                                                                               
FIELDS   DS    0CL128                                                           
FIELD1   DS    CL32                                                             
FIELD2   DS    CL32                                                             
FIELD3   DS    CL32                                                             
FIELD4   DS    CL32                                                             
*                                                                               
FIELDLN  DS    0XL4                                                             
FIELD1LN DS    X                                                                
FIELD2LN DS    X                                                                
FIELD3LN DS    X                                                                
FIELD4LN DS    X                                                                
*                                                                               
DMCB     DS    6F                                                               
*                                                                               
Q1       DS    F                                                                
Q2       DS    F                                                                
Q3       DS    F                                                                
Q4       DS    F                                                                
Q5       DS    F                                                                
Q6       DS    F                                                                
*                                                                               
SE#      DS    X                                                                
FILE#    DS    X                                                                
FLDSE#   DS    X                                                                
FLDFLAG  DS    X                   Flags set based on FIELD values              
FLD_SE#  EQU   X'08'               .  System specific                           
FLD_RO   EQU   X'04'               .  Read-only file                            
FLD_NOP  EQU   X'02'               .  NOP file in system                        
*                                                                               
SENUM    DS    CL1                 SE NUMBER                                    
SEOV     DS    CL1                 OV NUMBER                                    
SENAM    DS    CL7                 SE NAME                                      
SEFILN   DS    CL960               FILENAMES FOR OPEN                           
SEFILMAX EQU   L'SEFILN/8                                                       
*                                                                               
STATSYS  DS    CL8                                                              
*                                                                               
FILE1ST  DS    C                                                                
SPOTSYS  DS    C                                                                
TRFFSYS  DS    C                                                                
LOCAL    DS    C                   Force to LOCAL not global                    
EOFADR   DS    F                   EOF address                                  
TRKEOF   DS    F                                                                
TRKTOTAL DS    F                                                                
*                                                                               
WORK     DS    CL64                                                             
WORK1    DS    CL64                                                             
WORK2    DS    CL128                                                            
*                                                                               
       ++INCLUDE DMDDNAMED                                                      
*                                                                               
         DS    0F                                                               
FILEINFO DC    50XL(FINFOQ)'00'                                                 
*                                                                               
IOAREA   DS    (4*K)C                                                           
*                                                                               
RECVREC  DS    (18*K)C             RECOVERY BUFFER 18K                          
*                                                                               
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
*************************************************************                   
*        Basic system info. List of system processing       *                   
*************************************************************                   
                                                                                
SEINFD   DSECT                                                                  
SEINFNUM DS    X                                                                
SEINFOV  DS    X                                                                
SEINFNAM DS    XL8                                                              
SEINFLNQ EQU   *-SEINFD                                                         
         EJECT                                                                  
*************************************************************                   
*        STATS DSECTS                                       *                   
*************************************************************                   
                                                                                
FINFO    DSECT                                                                  
FINFOSE# DS    XL1                                                              
         DS    XL3                                                              
FINFODD  DS    XL8                                                              
FINFODTF DS    A                                                                
FINFOSYF DS    A                                                                
FINFOQ   EQU   *-FINFO                                                          
*                                                                               
PSLINED  DSECT                                                                  
PSDATE   DS    CL6                 YYMMDD                                       
         DS    C                   Space                                        
         DS    0C                                                               
PSHOURS  DS    CL2                 Hours                                        
PSCOLIN1 DS    C                                                                
PSMINS   DS    CL2                 Minutes                                      
PSCOLIN2 DS    C                                                                
PSSECS   DS    CL2                 Seconds                                      
         ORG   PSLINED                                                          
PSSYSNME DS    CL7                 System                                       
         DS    CL1                                                              
PSDDNAME DS    CL8                 DD Name                                      
         DS    CL1                                                              
PSDSN    DS    CL32                Data Set name                                
         DS    CL1                                                              
PSPAVAIL DS    CL4                 Percent avaiable                             
         DS    CL1                                                              
PSEXT#   DS    CL2                 Extent number                                
         DS    CL1                                                              
PSCHU#   DS    CL4                 Channel/head unit                            
         DS    CL1                                                              
PSCYLLO  DS    CL5                 Cylinder low                                 
PS1DASH  DS    CL1                 -                                            
PSCYLHI  DS    CL5                 Cylinder high                                
         DS    CL1                                                              
PSTRKTOT DS    CL5                 Track total / extent                         
         DS    CL1                                                              
PSTRKUSE DS    CL5                 Track used                                   
         DS    CL1                                                              
PSTRKAVL DS    CL5                 Track available                              
         DS    CL1                                                              
PSGLOBAL DS    CL1                 G=GLOBAL, L=LOCAL                            
         DS    CL1                                                              
PSSIZE   DS    CL2                 Size = 16,18,20,22 bit                       
         DS    CL1                                                              
PSEOF    DS    CL8                 A(EOF)                                       
         DS    CL1                                                              
PSTYPE   DS    CL2                                                              
         DS    CL1                                                              
PSBLKSZ  DS    XL6                                                              
PSLINEQ  EQU   *-PSLINED                                                        
         EJECT                                                                  
         PUSH  ACONTROL                                                         
         ACONTROL COMPAT(NOCASE)                                                
         IEFZB4D0                                                               
         IEFZB4D2                                                               
         IEFJFCBN                                                               
         POP   ACONTROL                                                         
         EJECT                                                                  
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
* FASSBOFF                                                                      
SSBD     DSECT                                                                  
         PRINT OFF                                                              
       ++INCLUDE FASSBOFF                                                       
         PRINT ON                                                               
* DMSPACED                                                                      
         PRINT OFF                                                              
       ++INCLUDE DMSPACED                                                       
         PRINT ON                                                               
* DMDSHDR                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMDSHDR                                                        
         PRINT ON                                                               
* DMDSYSHDR                                                                     
         PRINT OFF                                                              
       ++INCLUDE DMDSYSHDR                                                      
         PRINT ON                                                               
* DMSYSTABD                                                                     
         PRINT OFF                                                              
       ++INCLUDE DMSYSTABD                                                      
         PRINT ON                                                               
* DMATABS                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMATABS                                                        
         PRINT ON                                                               
* FASELIST                                                                      
         PRINT OFF                                                              
**     ++INCLUDE FASELIST                                                       
         PRINT ON                                                               
* DMRCVRD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMRCVRD                                                        
         PRINT ON                                                               
* DMSYSFD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMSYSFD                                                        
         PRINT ON                                                               
* DMDTFIS                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMDTFIS                                                        
         PRINT ON                                                               
* DMDTFPH                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMDTFPH                                                        
         PRINT ON                                                               
* DMXTNTD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMXTNTD                                                        
         PRINT ON                                                               
* DMGREQUS                                                                      
         PRINT OFF                                                              
       ++INCLUDE DMGREQUS                                                       
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'042DDFILMAN  09/21/20'                                      
         END                                                                    
