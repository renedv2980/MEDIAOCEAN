*          DATA SET DDDBLKTAPE AT LEVEL 003 AS OF 05/01/02                      
*CATALP DBLKTAPE                                                                
         SPACE 2                                                                
* PARM1  AL4   A(OUTPUT REC) - SET TO ZERO AT EOF - LEN AT RECORD-4             
* PARM2  XL1   DISP OF DADDS LINK FIELD                                         
*        AL3   A(HALFWORD CONTAINING DADDS BLOCK LEN)                           
* PARM3  AL4   N/D                                                              
* PARM4  CL1   TAPE TYPE - S=SAVRES,W=WDU                                       
*        AL3   A(FILE NAME)                                                     
         SPACE 2                                                                
* SAVRES TAPE IS LABELLED WITH RECFORM=UNDEF. FILE FORMAT AS FOLLOWS            
*                                                                               
* SAVRES HDR REC FOR FILE-X EXTENT FILE NAME AT REC+1                           
* SAVRES DELIMITER REC             C'TAPEMARK' AT REC+0                         
* FILE-X DATA RECORDS FRST EXTENT  FULL TRACK FORMAT COUNT/KEY/DATA             
* SAVRES DELIMITER REC                                                          
* SAVRES DELIMITER REC                                                          
* SAVRES HDR REC FOR FILE-X EXTENT                                              
* SAVRES DELIMITER REC                                                          
* FILE-X DATA RECORDS SCND EXTENT                                               
* SAVRES DELIMITER REC                                                          
* SAVRES END-OF-FILE REC                                                        
* SAVRES DELIMITER REC                                                          
* SAVRES HDR REC FOR FILE-Y                                                     
* SAVRES DELIMITER REC                                                          
* FILE-Y DATA RECORDS                                                           
* SAVRES DELIMITER REC                                                          
* SAVRES END-OF-FILE REC                                                        
* SAVRES DELIMITER REC                                                          
         SPACE 2                                                                
* WDU TAPE IS LABELLED WITH RECFORM=VARBLK. FILE FORMAT AS FOLLOWS              
*                                                                               
* WDU HDR REC FOR FILE-X SD EXTENT 4X'FF' AT REC+0 FILENAME AT REC+8            
* FILE-X DATA RECORDS FRST EXTENT  FULL TRACK FORMAT COUNT/KEY/DATA             
* WDU HDR REC FOR FILE-X SD EXTENT 4X'FF'X'00' AT REC+0                         
* FILE-X DATA RECORDS SCND EXTENT                                               
* WDU HDR REC FOR FILE-Y DA        5X'FF' AT REC+0                              
* FILE-Y DATA RECORDS ALL EXTENTS                                               
         TITLE 'DBLKTAPE - DEBLOCK PHYSICAL DUMP TAPE - SAVRES OR WDU'          
* SAVED REGISTERS BETWEEN CALLS AS FOLLOWS                                      
*                                                                               
* R3 TAPE BLK  A(DATA REC COUNT FIELD)                                          
* R4 TAPE BLK  RESIDUAL BLK COUNT                                               
* R5 DADDS BLK A(DATA REC LENGTH FIELD)                                         
* R6 DADDS BLK RESIDUAL BLK COUNT                                               
         SPACE 2                                                                
         PRINT NOGEN                                                            
DBLKTAPE CSECT                                                                  
         ENTRY DBLKSVRS                                                         
         ENTRY DBLKWDU                                                          
         SPACE 2                                                                
         NMOD1 0,DBLKTAPE          ENTRY FOR ANY TAPE                           
         MVC   TTYPE,12(R1)                                                     
         B     DBLK                                                             
         SPACE 2                                                                
DBLKSVRS NMOD1 0,DBLKSVRS          ENTRY FOR SAVRES TAPE                        
         L     RB,BASESVRS                                                      
         USING DBLKTAPE,RB                                                      
         MVI   TTYPE,C'S'                                                       
         B     DBLK                                                             
BASESVRS DC    A(DBLKTAPE)                                                      
         SPACE 2                                                                
DBLKWDU  NMOD1 0,DBLKWDU           ENTRY FOR WDU TAPE                           
         L     RB,BASEWDU                                                       
         USING DBLKTAPE,RB                                                      
         MVI   TTYPE,C'W'                                                       
         B     DBLK                                                             
BASEWDU  DC    A(DBLKTAPE)                                                      
         SPACE 2                                                                
DBLKEXIT XMOD1 1                                                                
         EJECT                                                                  
DBLK     LR    R2,R1               R2=A(PARAM LIST)                             
         SR    R7,R7                                                            
         ICM   R7,7,1(R2)          R7=A(OUTPUT REC)                             
         BNZ   *+6                                                              
         DC    H'0'                                                             
         CLI   FRSTTIME,C'Y'       TEST IF FIRST CALL                           
         BE    DBLK1                                                            
         LM    R3,R6,SAVEREGS      LOAD CONTROL REGISTERS                       
         B     NXTREC                                                           
*                                                                               
DBLK1    MVI   FRSTTIME,C'N'       SAVE AND CHECK TAPE TYPE                     
         MVC   TYPE,TTYPE                                                       
         CLI   TYPE,C'S'                                                        
         BE    DBLK2                                                            
         CLI   TYPE,C'W'                                                        
         BE    DBLK2                                                            
         DC    H'0'                                                             
*                                                                               
DBLK2    XC    BLKLEN,BLKLEN       SAVE DADDS BLOCK LEN                         
         ICM   RE,15,4(R1)                                                      
         BZ    *+10                TEST IF CALLER PASSED BLOCK LEN              
         MVC   BLKLEN,0(RE)                                                     
         MVI   OVERFLOW,C'Y'       SET UNBLOCKED                                
         OC    BLKLEN,BLKLEN                                                    
         BZ    *+8                                                              
         MVI   OVERFLOW,C'N'       SET BLOCKED IF NONZERO BLOCK LEN             
         MVC   LNKDSP+1(1),4(R1)   SAVE DADDS DISP TO LINK FIELD                
         L     RE,12(R1)                                                        
         MVC   FILEID,0(RE)        SAVE FILE NAME                               
*                                                                               
DBLK3    XC    CCW,CCW             BUILD CCW FOR TAPE READ                      
         MVI   CCW,X'02'                                                        
         LA    RE,BUFF                                                          
         STCM  RE,7,CCW+1                                                       
         MVI   CCW+4,X'20'                                                      
         MVC   CCW+6(2),LBUFF+2                                                 
         XC    TINFO,TINFO                                                      
         BAS   R9,OPNTAPE          OPEN INPUT TAPE FILE                         
*                                                                               
DBLK6    CLI   TYPE,C'S'           FIND SAVRES FILE                             
         BNE   DBLK7                                                            
         BAS   R9,GETTAPE                                                       
         CLC   FILEID,1(R3)        TEST IF FIRST FILE MATCHES                   
         BNE   DBLK6A                                                           
         BAS   R9,GETTAPE          YES TEST IF FOLLOWED BY SAVRES DELIM         
         CLC   0(8,R3),=C'TAPEMARK'                                             
         BE    DBLK6B                                                           
         DC    H'0'                                                             
DBLK6A   BAS   R6,GETTAPE          SEARCH FOR FILE                              
         CLC   0(8,R3),=C'TAPEMARK'                                             
         BNE   DBLK6A                                                           
         BAS   R9,GETTAPE                                                       
         CLC   FILEID,1(R3)        TEST IF THIS FILE MATCHES                    
         BNE   DBLK6A                                                           
         BAS   R9,GETTAPE          YES TEST IF FOLLOWED BY SAVRES DELIM         
         CLC   0(8,R3),=C'TAPEMARK'                                             
         BE    DBLK6B                                                           
         DC    H'0'                                                             
DBLK6B   BAS   R9,GETTAPE          READ FIRST DATA RECORD FOR FILE              
         CLI   4(R3),0                                                          
         BNE   NXTREC2                                                          
         OC    BLKLEN,BLKLEN                                                    
         BZ    *+10                                                             
         MVC   BLKLEN,22(R3)       SET DADDS BLKLEN FROM 1ST TAPE BLOCK         
         B     NXTREC2                                                          
*                                                                               
DBLK7    CLI   TYPE,C'W'           FIND WDU FILE                                
         BNE   DBLK8                                                            
DBLK7A   BAS   R9,GETTAPE                                                       
         CLC   0(4,R3),=5X'FF'     TEST FOR WDU HEADER RECORD                   
         BNE   DBLK7A                                                           
         CLC   FILEID,8(R3)        TEST FOR FILE ID                             
         BNE   DBLK7A                                                           
         BAS   R9,GETTAPE          READ FIRST DATA RECORD FOR FILE              
         CLI   4(R3),0                                                          
         BNE   NXTREC2                                                          
         OC    BLKLEN,BLKLEN                                                    
         BZ    *+10                                                             
         MVC   BLKLEN,22(R3)       SET DADDS BLKLEN FROM 1ST TAPE BLOCK         
         B     NXTREC2                                                          
*                                                                               
DBLK8    DC    H'0'                                                             
         EJECT                                                                  
NXTREC   CLI   OVERFLOW,C'Y'       TEST IF IN OVERFLOW (UNBLKD RECS)            
         BE    NXTREC1                                                          
         LH    R1,0(R5)            R5=A(RECORD-2)                               
         SR    R6,R1               R6=RESIDUAL DADDS BLOCK LEN                  
         BNP   NXTREC1                                                          
         AR    R5,R1               POINT TO NEXT REC IN DADDS BLK               
         B     NXTRECA                                                          
*                                                                               
NXTREC1  SR    R1,R1               BUMP TO NEXT DISK REC IN TAPE BLOCK          
         ICM   R1,3,6(R3)                                                       
         LA    R1,8(R1)            R1=L'COUNT+L'DATA                            
         SR    R4,R1                                                            
         BNP   NXTTAPE             END OF TAPE BLOCK                            
         AR    R3,R1                                                            
*                                                                               
NXTREC2  CLI   4(R3),0             TEST FOR RECORD ZERO                         
         BNE   NXTREC3                                                          
         LA    R3,16(R3)           BUMP PAST REC ZERO COUNT & DATA              
         SH    R4,=H'16'                                                        
         BZ    NXTTAPE                                                          
*                                                                               
NXTREC3  OC    BLKLEN,BLKLEN       DID CALLER PASS DADDS BLOCK LEN              
         BZ    *+14                NO                                           
         CLC   BLKLEN,6(R3)        IS THIS A DADDS BLOCK                        
         BE    NXTREC4             YES                                          
         MVI   OVERFLOW,C'Y'       SET NOT A DADDS BLOCK                        
         LA    R5,6(R3)            POINT TO RECORD-2                            
         B     NXTRECA                                                          
*                                                                               
NXTREC4  LH    R6,12(R3)           SET DADDS BLOCK LEN                          
         SH    R6,=H'6'                                                         
         LA    R5,14(R3)           POINT TO RECORD-2 IN DADDS BLOCK             
         MVI   OVERFLOW,C'N'                                                    
*                                                                               
NXTRECA  SR    RF,RF               USER RECORD HAS BEEN LOCATED                 
         ICM   RF,3,0(R5)                                                       
         CLI   OVERFLOW,C'Y'                                                    
         BE    *+8                                                              
         SH    RF,=H'2'            RF=L'RECORD                                  
         LA    RE,2(R5)            RE=A(RECORD)                                 
         STM   RE,RF,DUB                                                        
*                                                                               
         SR    R1,R1                                                            
         ICM   R1,3,LNKDSP         DID CALLER PASS DADDS LINK DISP              
         BZ    *+16                NO                                           
         AR    R1,RE                                                            
         OC    0(4,R1),0(R1)       NONZERO LINK MEANS DONT WANT REC             
         BNZ   NXTREC                                                           
*                                                                               
         LR    R1,R7               BUILD TAPE HDR AT USERREC-4                  
         SH    R1,=H'4'                                                         
         LR    R0,RF                                                            
         AH    R0,=H'4'                                                         
         SLL   R0,16                                                            
         ST    R0,0(R1)                                                         
*                                                                               
         LR    R0,R7               MOVE REC TO CALLERS BUFF                     
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
         OC    BLKLEN,BLKLEN                                                    
         BZ    NXTRECX                                                          
         L     RF,DUB+4                                                         
         AR    RF,R7                                                            
         XC    0(2,RF),0(RF)       SET ZERO AT END OF DADDS RECORD              
*                                                                               
NXTRECX  STM   R3,R6,SAVEREGS      SAVE CONTROL REGISTERS                       
         B     DBLKEXIT                                                         
         EJECT                                                                  
NXTTAPE  CLI   TYPE,C'S'           GET NEXT TAPE RECORD                         
         BE    NXTTAPS                                                          
         CLI   TYPE,C'W'                                                        
         BE    NXTTAPW                                                          
         DC    H'0'                                                             
*                                                                               
NXTTAPS  BAS   R9,GETTAPE          SAVRES TAPE                                  
         CLC   0(8,R3),=C'TAPEMARK'                                             
         BNE   NXTREC2             RETURN IF DATA RECORD                        
         BAS   R9,GETTAPE                                                       
         CLC   FILEID,1(R3)        CHECK FOR NEXT EXTENT - ELSE EOF             
         BNE   GETTS1                                                           
         BAS   R9,GETTAPE                                                       
         CLC   0(8,R3),=C'TAPEMARK'                                             
         BE    NXTTAPS                                                          
         DC    H'0'                FORMAT ERROR                                 
*                                                                               
NXTTAPW  BAS   R9,GETTAPE          WDU TAPE                                     
         CLC   0(4,R3),=5X'FF'                                                  
         BNE   NXTREC2             RETURN IF DATA RECORD                        
         CLC   FILEID,8(R3)                                                     
         BNE   GETTW1              CHECK FOR NEXT EXTENT - ELSE EOF             
         CLI   4(R3),0                                                          
         BE    NXTTAPW                                                          
         DC    H'0'                FORMAT ERROR                                 
         EJECT                                                                  
GETTAPE  ST    R9,SAVER9           GET TAPE REC - R3=A(DATA) R4=L'DATA          
         CLI   TYPE,C'S'                                                        
         BE    GETTS                                                            
         CLI   TYPE,C'W'                                                        
         BE    GETTW                                                            
         DC    H'0'                                                             
*                                                                               
GETTS    BAS   RE,EXCPTAPE         SAVRES TAPE                                  
         BE    GETTS2                                                           
         TM    TERR,X'80'                                                       
         BZ    *+6                                                              
         DC    H'0'                                                             
GETTS1   XC    0(4,R2),0(R2)       END OF FILE                                  
         MVI   FRSTTIME,C'Y'                                                    
         BAS   R9,CLSTAPE                                                       
         B     DBLKEXIT                                                         
GETTS2   LA    R3,BUFF             R3=A(DATA RECORD)                            
         L     R4,TPLEN            R4=L'DATA RECORD                             
         STM   R3,R4,TDADR                                                      
         B     GETTAPEX                                                         
*                                                                               
GETTW    OC    TBLEN,TBLEN         WDU TAPE                                     
         BNZ   GETTW4                                                           
GETTW0   BAS   RE,EXCPTAPE         READ NEW BLOCK                               
         BE    GETTW2                                                           
         TM    TERR,X'80'                                                       
         BZ    *+6                                                              
         DC    H'0'                                                             
GETTW1   XC    0(4,R2),0(R2)       END OF FILE                                  
         MVI   FRSTTIME,C'Y'                                                    
         BAS   R9,CLSTAPE                                                       
         B     DBLKEXIT                                                         
GETTW2   MVC   TBLEN+2(2),BUFF     SET BLOCK RESIDUAL LEN                       
         L     RE,TBLEN                                                         
         SH    RE,=H'4'            ADJUST FOR BLOCK HDR                         
         ST    RE,TBLEN                                                         
         LA    RE,BUFF+4                                                        
GETTW3   ST    RE,TLADR            SET LOGICAL REC ADR                          
         MVC   TLLEN+2(2),0(RE)    SET LOGICAL REC LEN                          
         LA    RE,4(RE)                                                         
         ST    RE,TDADR            SET DATA REC ADR                             
         L     RE,TLLEN                                                         
         SH    RE,=H'4'                                                         
         ST    RE,TDLEN            SET DATA RECORD LEN                          
         LM    R3,R4,TDADR                                                      
         B     GETTAPEX                                                         
GETTW4   L     RE,TBLEN            BUMP TO NEXT REC IN BLOCK                    
         S     RE,TLLEN                                                         
         BP    *+14                                                             
         XC    TBLEN,TBLEN         END OF BLOCK - BACK FOR NEXT                 
         B     GETTW0                                                           
         ST    RE,TBLEN            SET NEW RESIDUAL BLOCK LEN                   
         L     RE,TLADR                                                         
         A     RE,TLLEN            POINT TO NEXT REC IN BLOCK                   
         B     GETTW3                                                           
*                                                                               
GETTAPEX L     R9,SAVER9                                                        
         BR    R9                                                               
         EJECT                                                                  
OPNTAPE  LA    R1,TAPEIN           OPEN TAPE FILE                               
         OPEN  (1)                                                              
         BR    R9                                                               
         SPACE 2                                                                
CLSTAPE  XC    CCW1,CCW1           BUILD REWIND & UNLOAD CCW                    
         MVI   CCW1,X'0F'                                                       
         LA    RE,BUFF                                                          
         STCM  RE,7,CCW1+1                                                      
         MVI   CCW1+7,1                                                         
         XC    CCB,CCB                                                          
         MVC   CCB+6(2),=X'0108'                                                
         LA    RE,CCW1                                                          
         ST    RE,CCB+8                                                         
         LA    R1,CCB                                                           
         EXCP  (1)                                                              
         WAIT  (1)                                                              
         LA    R1,TAPEIN           CLOSE TAPE FILE                              
         CLOSE (1)                                                              
         BR    R9                                                               
         SPACE 2                                                                
EXCPTAPE ST    RE,SAVERE           EXECUTE CHAN PROG TO READ TAPE FILE          
         XC    CCB,CCB                                                          
         MVC   CCB+6(2),=X'0108'                                                
         LA    RE,CCW                                                           
         ST    RE,CCB+8                                                         
         MVI   TERR,0                                                           
         LA    R1,CCB                                                           
         EXCP  (1)                                                              
         WAIT  (1)                                                              
         L     RF,LBUFF            SET LENGTH OF PHYSICAL READ                  
         SR    RE,RE                                                            
         ICM   RE,3,CCB                                                         
         SR    RF,RE                                                            
         ST    RF,TPLEN                                                         
*                                                                               
EXCPT1   TM    CCB+2,X'20'         TEST FOR ERRORS AND EOF                      
         BZ    *+12                                                             
         OI    TERR,X'80'                                                       
         B     EXCPTX                                                           
         TM    CCB+4,X'01'                                                      
         BZ    *+12                                                             
         OI    TERR,X'40'                                                       
         B     EXCPT2                                                           
         CLI   CCB+5,0                                                          
         BE    *+12                                                             
         OI    TERR,X'80'                                                       
         B     EXCPTX                                                           
         B     EXCPTX                                                           
*                                                                               
EXCPT2   MVC   DUB(4),=4C' '       ASK IF ANY MORE TAPES AT EOF                 
         MVC   MSG+9(1),TYPE                                                    
         GOTO1 =V(LOGIO),DMCB,1,(16,MSG)                                        
         GOTO1 (RF),(R1),0,(3,DUB)                                              
         CLI   DUB,C'N'                                                         
         BE    EXCPTX                                                           
         CLI   DUB,C'Y'                                                         
         BNE   EXCPT2                                                           
         BAS   R9,CLSTAPE                                                       
         BAS   R9,OPNTAPE                                                       
         B     EXCPTAPE+4                                                       
*                                                                               
EXCPTX   L     RE,SAVERE           EXIT WITH CC EQL IF OK                       
         CLI   TERR,0                                                           
         BR    RE                                                               
         SPACE 2                                                                
TAPEIN   DTFPH DEVICE=TAPE,DEVADDR=SYS008,TYPEFLE=INPUT                         
         EJECT                                                                  
DUB      DS    D                                                                
DUB1     DS    D                                                                
DMCB     DS    6F                                                               
*                                                                               
CCB      DS    CL16                                                             
CCW      DS    CL8                                                              
CCW1     DS    CL8                                                              
*                                                                               
SAVEREGS DS    4F                                                               
SAVER9   DS    F                                                                
SAVERE   DS    F                                                                
APARM    DS    F                                                                
*                                                                               
BLKLEN   DC    H'0'                LENGTH OF DADDS BLOCK                        
LNKDSP   DC    H'0'                DISP TO DADDS LINK FIELD                     
FILEID   DC    CL7' '              FILE ID                                      
TYPE     DC    C' '                TAPE TYPE                                    
TTYPE    DC    C' '                                                             
FRSTTIME DC    C'Y'                                                             
OVERFLOW DC    C'N'                                                             
         DC    C' '                                                             
*                                                                               
TINFO    DC    0XL28'00'                                                        
TBLEN    DC    F'0'                TAPE RESIDUAL BLOCK LENGTH                   
TPLEN    DC    F'0'                TAPE PHYSICAL I/O READ LENGTH                
TLADR    DC    A(0)                TAPE LOGICAL REC ADR                         
TLLEN    DC    F'0'                TAPE LOGICAL REC LEN                         
TDADR    DC    A(0)                TAPE DATA REC ADR                            
TDLEN    DC    F'0'                TAPE DATA REC LEN                            
TERR     DC    F'0'                TAPE READ ERROR FLAG 80=ERR,40=EOF           
*                                                                               
MSG      DC    C'ANY MORE X TAPES'                                              
*                                                                               
         LTORG                                                                  
*                                                                               
LBUFF    DC    F'49152'            BUFFER LENGTH                                
         DC    C'**DBLKBUFF**'                                                  
BUFF     DC    49152X'00'                                                       
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003DDDBLKTAPE05/01/02'                                      
         END                                                                    
