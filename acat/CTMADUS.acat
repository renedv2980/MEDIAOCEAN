*          DATA SET CTMADUS    AT LEVEL 190 AS OF 08/30/06                      
*CATALP MADUSUK                                                                 
         TITLE 'CTMADUS - $MAD US APPLICATION VALIDATION ROUTINES'              
USUKCOMM CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,**USUK**,RA,R9                                                 
*                                                                               
         USING CONTROLD,RC         CONTROLLER COMMON STORAGE                    
         USING APPLD,R7            FIRST APPLICATION COMMON STORAGE             
         USING USUKD,R8            US/UK APPLICATION COMMON STORAGE             
*                                                                               
         L     RC,ACONTD           RC = A(CONTROLLER STORAGE)                   
         USING CONTROLD,RC                                                      
***********************************************************************         
* UPON ENTRY, RF HOLDS A(USUKCOMM) IN ITS LOW ORDER THREE BYTES AND             
* THE ROUTINE NUMBER IN ITS HIGH ORDER BYTE.  THE ROUTINE NUMBER                
* IS USED TO BRANCH TO THE DESIRED ROUTINE.                                     
***********************************************************************         
         SRL   RF,24               BRANCH TO DESIRED ROUTINE                    
         SLL   RF,2                                                             
         B     VBRANCH(RF)                                                      
***********************************************************************         
* TABLE OF BRANCH ADDRESSES TO APPLICATION ROUTINES                             
***********************************************************************         
VBRANCH  B     VVALIMED                                                         
         B     VVALICLT                                                         
         B     VVALIPRD                                                         
         B     VVALIEST                                                         
         B     VVALISTA                                                         
         B     VVALIDPT                                                         
         B     VVALIRGE                                                         
         B     VVALISLN                                                         
         B     VVALIES1                                                         
         B     VSETHEIR                                                         
         EJECT                                                                  
***********************************************************************         
* VALIDATES THE MEDIA FIELD.                                                    
*                                                                               
* ON ENTRY:    PARAMETER 1         A(MEDIA)                                     
*                                                                               
* RETURNS:     QMED        CL1     EBCDIC STORAGE OF MEDIA                      
*              APROFILE    XL20    AGENCY PROFILE                               
*              BAGYMED     XL1     BINARY AGENCY/MEDIA CODE                     
***********************************************************************         
VVALIMED DS    0H                                                               
         L     R2,0(R1)            R2 = A(MEDIA CODE)                           
*                                                                               
         MVC   QMED,0(R2)          COPY MEDIA CODE TO GLOBAL                    
         CLI   QMED,C'R'           TEST RADIO                                   
         BE    VM2                                                              
         MVI   QMED,C'T'           THEN USE 'T' FOR CABLE                       
*                                                                               
VM2      XC    KEY,KEY             BUILD AGENCY KEY                             
         LA    R4,KEY                                                           
         USING AGYHDRD,R4                                                       
         MVI   AGYKTYPE,X'06'                                                   
         MVC   AGYKAGY,SIGNON2C                                                 
         DROP  R4                                                               
*                                                                               
         GOTO1 HIGH                IF AGENCY KEY NOT FOUND                      
         CLC   KEY(L'AGYKEY),KEYSAVE                                            
         BNE   VMAGY               THEN ERROR                                   
*                                                                               
         GOTO1 GETREC              ELSE GET RECORD                              
*                                                                               
         L     R6,AIO              R6 = A(DESCRIPTION ELEMENT)                  
         GOTO1 GETELEM,CMDMCB,1        X'01' ELEMENT                            
         BNE   VMAGY               ERROR IF NONE                                
*                                                                               
         USING AGYEL,R6            SAVE AGENCY PROFILE IN GLOBAL                
         MVC   APROFILE,AGYPROF                                                 
*                                                                               
         L     R6,AIO              R6 = A(FIRST AGENCY/MEDIA ELEMENT)           
         GOTO1 GETELEM,CMDMCB,2        X'02' ELEMENT                            
*                                                                               
VM10     BNE   VMMED               ERROR IF NO AGY/MED ELEM WITH MATCH          
*                                                                               
         CLC   QMED,2(R6)          IF MEDIA MATCHES ELEMENT                     
         BE    VM20                THEN DONE                                    
*                                                                               
         GOTO1 NEXTELEM            ELSE BUMP R6 TO NEXT ELEMENT                 
         B     VM10                LOOP BACK                                    
*                                                                               
VM20     MVC   BAGYMED,3(R6)       COPY AGENCY/MEDIA CODE TO GLOBAL             
*                                                                               
         B     VMYES               RETURN 'YES' TO CALLER                       
         DROP  R6                                                               
***********************************************************************         
* SET OF ERRORS USED BY THIS ROUTINE.                                           
***********************************************************************         
*                                  INVALID AGENCY                               
VMAGY    MVC   APPLERR,=Y(ERA1AGY)                                              
         B     VMNO                                                             
*                                  INVALID MEDIA                                
VMMED    MVC   APPLERR,=Y(ERA1MED)                                              
         B     VMNO                                                             
*                                                                               
VMYES    B     YES                                                              
*                                                                               
VMNO     B     NO                                                               
         EJECT                                                                  
***********************************************************************         
* VALIDATES THE CLIENT FIELD.                                                   
*                                                                               
* ON ENTRY:    PARAMETER 1         A(CLIENT)                                    
*                                                                               
* RETURNS:     QCLT        CL3     EBCDIC STORAGE OF CLIENT                     
*              BCLT        XL2     BINARY CLIENT CODE                           
*              CPROFILE    XL15    CLIENT PROFILE                               
*              CPROFILX    XL15    CLIENT EXTRA PROFILE                         
*              PRDLIST     880C    PRODUCT LIST                                 
*              CLTCOST2    XL4     CLIENT'S COST2 FIELD                         
***********************************************************************         
VVALICLT DS    0H                                                               
         L     R2,0(R1)            R2 = A(CLIENT CODE)                          
*                                                                               
         MVC   QCLT,0(R2)          SAVE CLIENT CODE IN GLOBAL                   
*                                                                               
*                                  CONVERT CLIENT TO BINARY CLIENT CODE         
         GOTO1 CLPACK,CMDMCB,QCLT,BCLT                                          
         CLI   0(R1),0             IF INVALID CLIENT CODE                       
         BNE   VCCLT               THEN ERROR                                   
*                                                                               
         XC    KEY,KEY             BUILD CLIENT KEY                             
         LA    R4,KEY                                                           
         USING CLTHDRD,R4                                                       
         MVC   CKEYAM,BAGYMED                                                   
         MVC   CKEYCLT,BCLT                                                     
         DROP  R4                                                               
*                                                                               
         GOTO1 HIGH                IF RECORD NOT FOUND                          
         CLC   KEY(L'CKEY),KEYSAVE                                              
         BNE   VCCLT               THEN ERROR                                   
*                                                                               
         GOTO1 GETREC              GET RECORD                                   
*                                                                               
         L     R6,AIO              SAVE CLIENT PROFILE IN GLOBAL                
         USING CLTHDRD,R6                                                       
         MVC   CPROFILE,CPROF                                                   
         MVC   CPROFILX,CEXTRA                                                  
*                                                                               
         LA    RE,PRDLIST          SAVE PRODUCT CODE LIST IN GLOBAL             
         LA    RF,880                                                           
         LA    R0,CLIST                                                         
         LR    R1,RF                                                            
         MVCL  RE,R0                                                            
*                                                                               
         MVC   MCLUNQ,CMCLTUNQ     SAVE MASTER TRAFFIC SEQ NUMBER               
         MVC   MCLCOD,CMCLTCOD                         CLIENT CODE              
         MVC   MCLPRD,CMCLTPRD                         PRODUCT CODE             
*                                                                               
         MVC   CLTCOST2,CCOST2     SAVE CLIENT'S COST2                          
*                                                                               
         B     VCYES               RETURN 'YES' TO CALLER                       
         DROP  R6                                                               
***********************************************************************         
* ERROR USED BY THIS ROUTINE.                                                   
***********************************************************************         
*                                  INVALID CLIENT                               
VCCLT    MVC   APPLERR,=Y(ERA1CLT)                                              
         B     VCNO                                                             
*                                                                               
VCYES    B     YES                                                              
*                                                                               
VCNO     B     NO                                                               
         EJECT                                                                  
***********************************************************************         
* VALIDATES THE PRODUCT FIELD.                                                  
*                                                                               
* ON ENTRY:    PARAMETER 1         A(PRODUCT)                                   
*                                                                               
* RETURNS:     QPRD        CL3     EBCDIC STORAGE OF PRODUCT                    
*              BPRD        XL2     BINARY PRODUCT CODE                          
*              PRDCODE     XL2     PRODUCT CODE                                 
***********************************************************************         
VVALIPRD DS    0H                                                               
         L     R2,0(R1)            R2 = A(PRODUCT CODE)                         
*                                                                               
         MVC   QPRD,0(R2)          SAVE PRODUCT CODE IN GLOBAL                  
*                                                                               
         LA    R6,PRDLIST          R6 = A(1ST PRD IN PRD CODE LIST)             
*                                                                               
VP10     CLI   0(R6),0             IF END OF TABLE                              
         BE    VPPRD               THEN ERROR                                   
*                                                                               
         CLC   QPRD,0(R6)          IF PRODUCT MATCHES REQUEST                   
         BE    VP20                THEN DONE                                    
*                                                                               
         LA    R6,4(R6)            BUMP R6 TO NEXT PRODUCT                      
         B     VP10                LOOP BACK                                    
*                                                                               
VP20     MVC   BPRD,3(R6)          SAVE PRODUCT NUMBER IN GLOBAL                
*                                                                               
         XC    KEY,KEY             BUILD PRODUCT KEY                            
         LA    R4,KEY                                                           
         USING PRDHDRD,R4                                                       
         MVC   PKEYAM,BAGYMED                                                   
         MVC   PKEYCLT,BCLT                                                     
         MVC   PKEYPRD,QPRD                                                     
         DROP  R4                                                               
*                                                                               
         GOTO1 HIGH                IF RECORD NOT FOUND                          
         CLC   KEY(L'CKEY),KEYSAVE                                              
         BNE   VPPRD               THEN ERROR                                   
*                                                                               
         GOTO1 GETREC              GET PRODUCT RECORD                           
*                                                                               
         L     R6,AIO              SAVE 2-BYTE PRODUCT CODE IN GLOBAL           
         USING PRDHDRD,R6                                                       
         MVC   PRDCODE,PCODE                                                    
*                                                                               
         B     VPYES               RETURN 'YES' TO CALLER                       
         DROP  R6                                                               
***********************************************************************         
* ERROR USED BY THIS ROUTINE.                                                   
***********************************************************************         
*                                  INVALID PRODUCT                              
VPPRD    MVC   APPLERR,=Y(ERA1PRD)                                              
         B     VPNO                                                             
*                                                                               
VPYES    B     YES                                                              
*                                                                               
VPNO     B     NO                                                               
         EJECT                                                                  
***********************************************************************         
* VALIDATES THE ESTIMATE.                                                       
*                                                                               
* ON ENTRY:    PARAMETER 1         ESTIMATE CODE                                
*                                                                               
* RETURNS:     BEST                BINARY EQUIVALENT OF ESTIMATE CODE           
*              ESTDYMNU            DAYPART MENU NUMBER                          
*              ESTSTRT             PERIOD START DATE (YYMMDD)                   
*              ESTEND              PERIOD END DATE (YYMMDD)                     
*              ESTBOOK             RATING BOOK FOR YEAR/MONTH                   
*              ESTOWKSD            OUT OF WEEK START DAY IN HOB                 
*              ESTREPCD            SPECIAL REP CODE                             
*              ESTDEMOS            ESTIMATE'S DEMO CATEGORIES                   
*              ESTCOST2            ESTIMATE'S COST2 FIELD                       
***********************************************************************         
VVALIEST DS    0H                                                               
         L     R2,0(R1)            R2 = A(ESTIMATE CODE HEX INPUT)              
*                                  IF ESTIMATE CODE NOT VALID HEX               
         GOTO1 HEXIN,CMDMCB,(R2),BEST,L'BEST*2                                  
         OC    12(4,R1),12(R1)                                                  
         BZ    VEHEST              THEN ERROR                                   
         B     VEKEY                                                            
*                                                                               
*--SECOND ENTRY TO DEAL WITH ESTIMATE NUMBERS THAT ARE IN                       
*--STRAIGHT CHARACTER FORMAT.  NOT CHARACTER HEX FORMAT.                        
VVALIES1 DS    0H                                                               
         L     R2,0(R1)            R2 = A(ESTIMATE CODE CHAR INPUT)             
*                                                                               
         PACK  KEY(8),0(3,R2)                                                   
         CVB   RE,KEY                                                           
         STC   RE,BEST                                                          
*                                                                               
VEKEY    XC    KEY,KEY             BUILD ESTIMATE KEY                           
         LA    R4,KEY                                                           
         USING ESTHDRD,R4                                                       
         MVC   EKEYAM,BAGYMED                                                   
         MVC   EKEYCLT,BCLT                                                     
         MVC   EKEYPRD,QPRD                                                     
         MVC   EKEYEST,BEST                                                     
         DROP  R4                                                               
*                                                                               
         GOTO1 HIGH                IF RECORD NOT FOUND                          
         CLC   KEY(L'EKEY),KEYSAVE                                              
         BNE   VEEST               THEN ERROR                                   
*                                                                               
VPBX     GOTO1 GETREC              GET THE RECORD                               
*                                                                               
         L     R6,AIO              EXTRACT INFO FROM THE ESTIMATE REC           
         USING ESTHDRD,R6                                                       
         MVC   ESTDYMNU,EDAYMENU                                                
         MVC   ESTSTRT,ESTART                                                   
         MVC   ESTEND,EEND                                                      
         MVC   ESTBOOK,EBOOK                                                    
         MVC   ESTOWKSD,EOWSDAY                                                 
         MVC   ESTREPCD,EREP                                                    
         MVC   ESTDEMOS,EDEMLST                                                 
         MVC   ESTDEM25(60),EDEMLST                                             
         MVC   ESTDEM25+60(3),EDEM21  FOR NETPAK                                
         MVC   ESTCOST2,ECOST2                                                  
*                                                                               
         B     VEYES               RETURN 'YES' TO CALLER                       
*                                                                               
*                                  ESTIMATE CODE NOT VALID HEX                  
VEHEST   MVC   APPLERR,=Y(ERA1HEST)                                             
         B     VENO                                                             
*                                  ESTIMATE RECORD NOT FOUND                    
VEEST    MVC   APPLERR,=Y(ERA1EST)                                              
         B     VENO                                                             
*                                                                               
VEYES    B     YES                                                              
*                                                                               
VENO     B     NO                                                               
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
* VALIDATES THE STATION FIELD.                                                  
*                                                                               
* ON ENTRY:    P1         A(STATION CALL LETTERS)                               
*              P2         A(CABLE NETWORK) IF P1 = X'80'                        
* RETURNS:     BMKTSTA     XL5     MARKET STATION PACKED                        
*              BTAXRATE    XL2     STATION TAX RATE (TO 3 DECIMALS)             
***********************************************************************         
VVALISTA DS    0H                                                               
         XC    DUB,DUB                                                          
         L     RE,0(R1)            GET P1                                       
         MVC   DUB(5),0(RE)        SAVE STATION                                 
         TM    0(R1),X'80'         TEST CABLE NET PASSED                        
         BZ    VVSTA2              NO                                           
         L     RE,4(R1)            GET P2                                       
         MVC   DUB+5(3),0(RE)                                                   
*                                                                               
VVSTA2   MVI   KEY,C'0'            BUILD STATION ADDRESS KEY                    
         MVC   KEY+1(L'STAKEY-1),KEY                                            
         LA    R4,KEY                                                           
         USING STAHDRD,R4                                                       
         MVI   STAKTYPE,C'S'                                                    
         MVC   STAKMED,QMED                                                     
         MVC   STAKCALL,DUB                                                     
         MVC   STAKAGY,SIGNON2C                                                 
         MVC   STAKCLT,QCLT                                                     
         DROP  R4                                                               
*                                                                               
         MVC   AIO,AIO1            USE I/O AREA 1                               
*                                                                               
         BAS   RE,STAHI            READ THE STATION RECORD                      
         L     R6,AIO                                                           
         CLC   KEY(15),0(R6)       IF NOT THE SAME                              
         BE    VVSTA10                                                          
         MVC   KEY+9(3),=3C'0'     THEN C'0' OUT THE CLIENT                     
         BAS   RE,STAHI            READ THE STATION RECORD                      
         L     R6,AIO                                                           
         CLC   KEY(15),0(R6)       IF NOT THE SAME                              
         BNE   VSTSTA                                                           
*                                                                               
VVSTA10  L     R6,AIO              SAVE TAX RATE IN GLOBAL                      
         USING STAHDRD,R6                                                       
         MVC   BTAXRATE,SNEWTAX                                                 
*                                                                               
         LA    R1,WORK                                                          
         XC    0(32,R1),0(R1)                                                   
         USING STAPACKD,R1                                                      
*                                                                               
         MVI   STAPACT,C'P'                                                     
         MVC   STAPAGY,SIGNON2C                                                 
         MVC   STAPMED,QMED                                                     
         MVC   STAPACOM,ACOMFACS                                                
         MVC   STAPQMKT,SMKT                                                    
         MVC   STAPQSTA,DUB                                                     
         MVC   STAPQNET,DUB+5                                                   
         GOTO1 STAPACK,(R1)                                                     
         CLI   STAPERR,0                                                        
         BNE   VSTSTB                                                           
         MVC   BMKTSTA,STAPMKST                                                 
         DROP  R1                                                               
*                                                                               
         MVC   STAPST,SPST         SAVE CANADIAN PST                            
*                                                                               
         B     VSTYES              RETURN 'YES' TO CALLER                       
         DROP  R6                                                               
***********************************************************************         
* ERROR USED BY THIS ROUTINE.                                                   
***********************************************************************         
*                                  STATION RECORD NOT FOUND                     
VSTSTA   MVC   APPLERR,=Y(ERA1STA)                                              
         B     VSTNO                                                            
*                                                                               
VSTSTB   MVC   APPLERR,=Y(ERA1STB) INVALID CABLE NETWORK                        
         B     VSTNO                                                            
*                                                                               
VSTYES   B     YES                                                              
*                                                                               
VSTNO    B     NO                                                               
*                                                                               
* READ FROM STATION FILE                                                        
*                                                                               
STAHI    NTR1                                                                   
         GOTO1 DATAMGR,CMDMCB,(0,=C'DMRDHI'),=C'STATION',KEY,AIO                
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* THIS ROUTINE VALIDATES THE DAYPART USING THE DAYPART MENU FOUND IN            
* THE ESTIMATE.  SEE SPBUY01, LABEL EST20                                       
***********************************************************************         
VVALIDPT DS    0H                                                               
         L     R2,0(R1)            R2 = A(DAYPART CODE)                         
*                                                                               
         LA    R1,CMDMCB           READ THE DAYPART MENU                        
         MVC   0(2,R1),SIGNON2C                                                 
         MVC   2(1,R1),QMED                                                     
         MVC   3(1,R1),ESTDYMNU                                                 
         GOTO1 DPTRD,(R1),,AIO,DATAMGR                                          
*                                                                               
         CLI   8(R1),X'FF'         DIE IF ERROR READING MENU                    
         BE    VDMENU                                                           
         TM    8(R1),X'08'                                                      
         BO    VDMENU                                                           
*                                                                               
         LA    R0,36               R0 = MAXIMUM NUMBER OF DAYPART CODES         
         L     R1,AIO              R1 = A(FIRST DAYPART IN MENU)                
*                                                                               
VD10     CLI   0(R1),0             ERROR IF NO MORE DAYPARTS                    
         BE    VDDPT                                                            
*                                                                               
         CLI   0(R1),C'Z'          IF DAYPART IS A 'Z'                          
         BE    VD20                THEN SKIP IT (SEE SPBUY01, EST21)            
*                                                                               
         CLC   0(1,R2),0(R1)       IF DAYPART MATCHES                           
         BNE   VD20                                                             
         MVC   BDPTNUM,1(R1)       THEN SAVE DAYPART NUMBER                     
         B     VDYES                    RETURN 'YES' TO CALLER                  
*                                                                               
VD20     LA    R1,5(R1)            R1 = A(NEXT DAYPART IN MENU)                 
*                                                                               
         BCT   R0,VD10             LOOP BACK UNTIL MATCH OR NO MORE             
*                                                                               
         B     VDDPT               ERROR IF NO MATCH                            
***********************************************************************         
* ERROR USED BY THIS ROUTINE.                                                   
***********************************************************************         
*                                  DAYPART MENU NOT FOUND                       
VDMENU   MVC   APPLERR,=Y(ERA1MENU)                                             
         B     VDNO                                                             
*                                  INVALID DAYPART CODE                         
VDDPT    MVC   APPLERR,=Y(ERA1DPT)                                              
         B     VDNO                                                             
*                                                                               
VDYES    B     YES                                                              
*                                                                               
VDNO     B     NO                                                               
         EJECT                                                                  
***********************************************************************         
* THIS ROUTINE VALIDATES THE GEOGRAPHIC CATEGORY PASSED IN PARM 1               
* AND SAVES ITS BINARY CODE IN APPLICATION STORAGE.  THE TABLE USED             
* FOR THE VALIDATION IS IN DERADCATS WHICH IS INCLUDED BELOW.                   
*                                                                               
* ON ENTRY:    PARAMETER 1         A(GEOGRAPHIC CATEGORY LETTER)                
*                                                                               
* RETURNS:     BRADGOEG    XL1     BINARY GOEGRAPHIC CODE                       
***********************************************************************         
VVALIRGE DS    0H                                                               
         L     R2,0(R1)            R2 = A(GEOGRAPHIC CATEGORY LETTER)           
         LA    R3,PFXRAD           R3 = A(GEOGRAPHIC CATEGORY TABLE)            
         LA    R4,PRERADES         R4 = NUMBER OF ENTRIES IN TABLE              
*                                                                               
VRG10    CLC   0(1,R2),1(R3)       IF LETTER MATCHES TABLE THEN DONE            
         BE    VRG20                                                            
         LA    R3,8(R3)            ELSE BUMP TO NEXT TABLE ENTRY                
         BCT   R4,VRG10            REPEAT UNTIL END OF TABLE                    
*                                                                               
         B     VRGRGEO             GEOGRAPHIC CODE NOT FOUND ERROR              
*                                                                               
VRG20    MVC   BRADGEOG,0(R3)      SAVE BINARY CODE AND RETURN 'YES'            
         B     VRGYES                                                           
*                                  RADIO GEOGRAPHIC CODE NOT FOUND              
VRGRGEO  MVC   APPLERR,=Y(ERA1RGEO)                                             
         B     VRGNO                                                            
*                                                                               
VRGYES   B     YES                                                              
*                                                                               
VRGNO    B     NO                                                               
***********************************************************************         
* THE RADIO GEOGRAPHIC CATEGORY TABLE IS INCLUDED HERE                          
***********************************************************************         
       ++INCLUDE DERADCATS                                                      
         EJECT                                                                  
***********************************************************************         
* VALIDATES THE SPOT LENGTH (EBCDIC)                                            
*                                                                               
* ON ENTRY:    PARAMETER 1         A(SPOTLEN)                                   
*                                                                               
* RETURNS:     BSPOTLEN            BINARY SPOT LENGTH                           
***********************************************************************         
VVALISLN DS    0H                                                               
         L     R2,0(R1)            R2 = A(SPOTLEN)                              
*                                                                               
*                                  IF SPOT LENGTH NOT VALID HEX                 
         GOTO1 HEXIN,CMDMCB,0(R2),BSPOTLEN,2                                    
         OC    12(4,R1),12(R1)                                                  
         BZ    VSPOTNO             THEN ERROR                                   
*                                                                               
         L     R1,VSLNTAB                                                       
         LH    RE,0(R1)            RE = L(EACH AGY/MEDIA ENTRY)                 
         L     RF,2(R1)            RF = A(EOT)                                  
         AR    RF,R1                                                            
         AHI   R1,6                R2 = A(1ST ENTRY)                            
*                                                                               
VSPLN010 CLC   =C'00',0(R1)        FOUND DEFAULT ENTRY?                         
         JE    VSPLN020                                                         
         CLC   SIGNON2C,0(R1)        NO, MATCH ON AGENCY                        
         JNE   *+14                                                             
VSPLN020 CLC   QMED,2(R1)         MATCH ON MEDIA                                
         JE    VSPLN030                                                         
         JXLE  R1,RE,VSPLN010                                                   
         DC    H'0'                                                             
                                                                                
VSPLN030 AHI   R1,4                BUMP PAST THE ALPHA AND MEDIA                
                                                                                
         XR    RE,RE               LET'S SEE IF IT IS VALID                     
         IC    RE,BSPOTLEN                                                      
         MHI   RE,2                SEE SPSLENTAB                                
         AR    RE,R1                                                            
         CLI   1(RE),0             EQUIVALENCE OF 0 IS NOT VALID                
         BNE   VSPOTYES                                                         
VSPOTNO  B     NO                  RETURN 'NO' TO CALLER                        
*                                                                               
VSPOTYES B     YES                 RETURN 'YES' TO CALLER                       
         EJECT                                                                  
***********************************************************************         
* EXTRACTS ACCOUNT LEDGER HEIRARCHY INFO FROM LEDGER RECORD                     
*                                                                               
* ON ENTRY:    CUL                 COMPANY/UNIT/LEDGER (SET BY CALLER)          
*                                                                               
* RETURNS:     LLEVA - LPROJOB     (SEE CTMADWORKD FOR DETAILS)                 
***********************************************************************         
VSETHEIR DS    0H                                                               
         MVC   BIGKEY(42),BLANKS   GET LEDGER RECORD                            
         MVC   BIGKEY(3),CUL                                                    
         GOTO1 HIGH                                                             
         CLC   BIGKEY(42),KEYSAVE                                               
         BNE   SHCUL                                                            
         GOTO1 GETREC                                                           
*                                                                               
         L     R6,AIO              GET HEIRARCHY ELEMENT                        
         GOTO1 GETELEM,CMDMCB,ACHRELQ                                           
         BNE   SHCUL                                                            
         USING ACHEIRD,R6                                                       
*                                                                               
         CLC   CUL+1(2),=C'1R'     CHECK IF GETTING 1R LEDGER LENGTHS           
         BE    SHEIR10                                                          
         MVC   LLEVA,ACHRLEVA       EXTRACT AND CALCULATE LENGTHS               
         MVC   LLEVAB,ACHRLEVB                                                  
         MVC   LLEVABC,ACHRLEVC                                                 
         MVC   LLEVABCD,ACHRLEVD                                                
         ZIC   R1,LCLIPRO                                                       
         ZIC   R0,LCLI                                                          
         SR    R1,R0                                                            
         STC   R1,LPRO                                                          
         IC    R1,LCLIJOB                                                       
         IC    R0,LCLIPRO                                                       
         SR    R1,R0                                                            
         STC   R1,LJOB                                                          
         IC    R1,LCLIJOB                                                       
         IC    R0,LCLI                                                          
         SR    R1,R0                                                            
         STC   R1,LPROJOB                                                       
         B     SHYES                                                            
*                                                                               
SHEIR10  MVC   LOFF,ACHRLEVA                                                    
         ZIC   RF,ACHRLEVB               LEVEL B LENGTH                         
         ZIC   RE,LOFF                                                          
         SR    RF,RE                                                            
         STC   RF,LDEP                                                          
         ZIC   RF,ACHRLEVC               LEVEL C LENGTH                         
         ZIC   RE,ACHRLEVB               LEVEL B LENGTH                         
         SR    RF,RE                                                            
         STC   RF,LSUB                                                          
*&&UK                                                                           
         ZIC   RF,ACHRLEVD               LEVEL D LENGTH                         
         ZIC   RE,ACHRLEVC               LEVEL C LENGTH                         
         SR    RF,RE                                                            
         STC   RF,LPER                                                          
*&&                                                                             
         B     SHYES                                                            
         DROP  R6                                                               
***********************************************************************         
* SET OF ERRORS USED BY THIS ROUTINE.                                           
***********************************************************************         
*                                  INVALID ACCOUNT LEDGER                       
SHCUL    MVC   APPLERR,=Y(ERA1CUL)                                              
         B     SHNO                                                             
*                                                                               
SHYES    B     YES                                                              
*                                                                               
SHNO     B     NO                                                               
         EJECT                                                                  
***********************************************************************         
*        VARIOUS EXIT POINTS USED BY THE ROUTINES IN THIS FILE.                 
***********************************************************************         
YES      SR    RC,RC               RETURN CC EQUAL                              
NO       LTR   RC,RC               RETURN CC NOT EQUAL                          
XIT      XIT1                                                                   
*                                                                               
BLANKS   DC    CL256' '                                                         
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
       ++INCLUDE RADSDPL                                                        
         EJECT                                                                  
* CTMADWORKD                                                                    
       ++INCLUDE CTMADWORKD                                                     
         EJECT                                                                  
* CTMADDSECT                                                                    
       ++INCLUDE CTMADDSECT                                                     
         EJECT                                                                  
* CTMADEQUS                                                                     
       ++INCLUDE CTMADEQUS                                                      
         EJECT                                                                  
AGYHDRD  DSECT                                                                  
       ++INCLUDE SPGENAGY                                                       
         EJECT                                                                  
CLTHDRD  DSECT                                                                  
       ++INCLUDE SPGENCLT                                                       
         EJECT                                                                  
PRDHDRD  DSECT                                                                  
       ++INCLUDE SPGENPRD                                                       
         EJECT                                                                  
ESTHDRD  DSECT                                                                  
       ++INCLUDE SPGENEST                                                       
         EJECT                                                                  
STAHDRD  DSECT                                                                  
       ++INCLUDE SPGENSTA                                                       
         EJECT                                                                  
       ++INCLUDE SPSTAPACKD                                                     
* ACGENFILE                                                                     
* ACGENBOTH                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
       ++INCLUDE ACGENBOTH                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'190CTMADUS   08/30/06'                                      
         END                                                                    
