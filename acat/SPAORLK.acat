*          DATA SET SPAORLK    AT LEVEL 025 AS OF 02/13/03                      
*CATALP SPAORLK                                                                 
         TITLE 'SPAORLK - SPOTPAK AOR LOOKUP MODULE'                            
***********************************************************************         
*        PARM 1  BYTE 0     NOT USED                                            
*                BYTE 1-3   A(CONTROL DSECT)  SEE MAAORLKD                      
***********************************************************************         
         SPACE 2                                                                
SPAORLK  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKDL,AORLK                                                     
         USING WORKD,RC                                                         
*                                                                               
         L     RA,0(R1)            A(CONTROL DSECT)                             
         USING MAAORLKD,RA                                                      
         L     RF,MAAORACF         A(COMFACS)                                   
         MVC   VDATAMGR,CDATAMGR-COMFACSD(RF)                                   
*                                                                               
         EJECT                                                                  
***********************************************************************         
*        SPOT BILL PROCESSING                                                   
***********************************************************************         
         SPACE 2                                                                
         L     R8,MAAORABL         A(BILL RECORD)                               
         USING BILLREC,R8                                                       
*                                                                               
         XC    MAAORRTN(MAAORRTL),MAAORRTN    CLEAR RETURNED DATA AREA          
         MVI   MAAORAGY,C' '                                                    
         MVC   MAAORAGY+1(L'MAAORAGY-1),MAAORAGY                                
         MVC   MAAORRCV,MAAORAGY                                                
         MVC   MAAORCOM,MAAORAGY                                                
*                                                                               
         TM    BILSTAT,X'30'       TRUE AOR OR CLIENT AOR BILL                  
         BZ    EXIT                NO, SKIP                                     
         XC    KEY,KEY                                                          
         LA    R7,KEY                                                           
         USING AORKEY,R7                                                        
         MVC   AORKTYP,=X'0D45'   RECORD TYPE                                   
         MVC   AORKAGMD,BKEYAM                                                  
         MVC   AORKCLT,BKEYCLT                                                  
         MVC   AORKPRD,BKEYPRD                                                  
         MVC   AORKEST+1(1),BKEYEST     ESTIMATE NUMBER                         
*                                                                               
         MVI   AORKDPT,X'FF'       DEFAULT DAYPART                              
         CLI   BLDPT,C' '          TEST BILL FOR ONE DAYPART                    
         BNH   *+10                                                             
         MVC   AORKDPT(1),BLDPT                                                 
         MVI   AORKSTYP,X'FF'      DEFAULT STATION TYPE                         
         CLI   BLMED,C' '          TEST BILL FOR ONE STATION TYPE               
         BNH   *+10                                                             
         MVC   AORKSTYP(1),BLMED                                                
         MVI   AORKEXT+2,X'FF'     3RD POSITION NOT USED                        
*                                                                               
         CLC   KEY(13),SVAORKEY    TEST SEARCHING FOR SAME DATA                 
         BNE   ML028                                                            
         MVC   KEY+14(4),SVAORKEY+14   YES, RESTORE DISK ADDR                   
         B     ML110                   AND GET RECORD                           
*                                                                               
ML028    DS    0H                                                               
         MVC   SVAORKEY,KEY                                                     
         L     RF,MAAORAIO         CLEAR AOR REC                                
         XC    0(60,RF),0(RF)                                                   
*                                                                               
         BAS   RE,HIGH                                                          
         CLC   KEY(8),KEYSAVE      TEST THRU PRD                                
         BNE   ML290               NOTHING FOR PRODUCT                          
         CLC   KEY(10),KEYSAVE     TEST THRU EST                                
         BE    ML040                                                            
         CLC   KEY+8(2),=X'FFFF'   DID WE FIND DEFAULT                          
         BE    ML040               YES, USE IT                                  
*                                                                               
ML030    DS    0H                                                               
         MVC   KEYSAVE+8(2),=X'FFFF'  TRY DEFAULT EST                           
         MVC   KEY,KEYSAVE                                                      
         BAS   RE,HIGH                                                          
         CLC   KEY(10),KEYSAVE                                                  
         BNE   ML290               NOTHING THERE EITHER                         
*                                                                               
ML040    DS    0H                                                               
         CLC   KEY+10(1),KEYSAVE+10   DAYPART                                   
         BE    ML060                                                            
         CLI   KEY+10,X'FF'           DID WE FIND DEFAULT                       
         BE    ML060                                                            
         CLI   KEYSAVE+10,X'FF'       DID WE TRY FOR IT                         
         BE    ML080                                                            
*                                                                               
ML050    DS    0H                                                               
         MVI   KEYSAVE+10,X'FF'       TRY DEFAULT                               
         MVC   KEY,KEYSAVE                                                      
         BAS   RE,HIGH                                                          
         CLC   KEY(11),KEYSAVE                                                  
         BNE   ML080                 NOTHING                                    
*                                                                               
ML060    DS    0H                                                               
         CLC   KEY+11(1),KEYSAVE+11    STATION TYPE                             
         BE    ML090                                                            
         CLI   KEY+11,X'FF'            DID WE FIND DEFAULLT                     
         BE    ML090                                                            
         CLI   KEYSAVE+11,X'FF'        DID WE TRY FOR IT                        
         BE    ML070                                                            
         MVI   KEYSAVE+11,X'FF'        TRY DEFAULT                              
         MVC   KEY,KEYSAVE                                                      
         BAS   RE,HIGH                                                          
         CLC   KEY(12),KEYSAVE                                                  
         BE    ML090                                                            
*                                                                               
ML070    DS    0H                  NOTHING FOUND                                
         CLI   KEYSAVE+10,X'FF'    WERE WE LOOKING UNDER ALL DPTS               
         BE    ML080               YES                                          
*                                  NO, DO IT NOW                                
         MVI   KEYSAVE+11,X'FF'    RESET STATION TYPE                           
         CLI   BLMED,C' '                                                       
         BNH   *+10                                                             
         MVC   KEYSAVE+11(1),BLMED                                              
         B     ML050                                                            
*                                                                               
ML080    DS    0H                                                               
         CLC   KEYSAVE+8(2),=X'FFFF'   WERE WE LOOKING UNDER ALL ESTS           
         BE    ML290               YES, NOTHING MORE TO DO                      
*                                  NO, DO IT NOW                                
         MVI   KEYSAVE+10,X'FF'     RESET DAYPART                               
         CLI   BLDPT,C' '                                                       
         BNH   *+10                                                             
         MVC   KEYSAVE+10(1),BLDPT                                              
         MVI   KEYSAVE+11,X'FF'    RESET STATION TYPE                           
         CLI   BLMED,C' '                                                       
         BNH   *+10                                                             
         MVC   KEYSAVE+11(1),BLMED                                              
         B     ML030                                                            
*                                                                               
ML090    DS    0H                                                               
         B     ML110                                                            
*                                                                               
ML110    DS    0H                                                               
         L     R7,MAAORAIO                                                      
         CLC   KEY(8),0(R7)      TEST ALREADY HAVE FILE RECORD                  
         BE    ML200             (TEST KEY THROUGH PRODUCT)                     
         BAS   RE,GET                                                           
*                                                                               
ML200    DS    0H                                                               
         LA    R2,AORELS                                                        
*                                                                               
ML210    DS    0H                                                               
         CLI   0(R2),0             EOR                                          
         BE    ML300                                                            
*                                                                               
         CLI   0(R2),X'02'         AOR ADDRESS                                  
         BE    ML220                                                            
         CLI   0(R2),X'03'         AOR INFO                                     
         BE    ML230                                                            
*                                                                               
ML212    DS    0H                                                               
         ZIC   R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     ML210                                                            
*                                                                               
ML220    DS    0H                                                               
         USING AORADREL,R2                                                      
         MVC   MAAORAGY,AORLIN1                                                 
         B     ML212                                                            
*                                                                               
ML230    DS    0H                                                               
         USING AORELEM,R2                                                       
         MVC   MAAORRCV,AORRCVBL                                                
         MVC   MAAORCOM,AORCOMM                                                 
         MVI   MAAORBAS,C'G'       GROSS BASIS                                  
         CLI   AORBAS,0                                                         
         BE    ML240                                                            
         MVI   MAAORBAS,C'C'       COMMISSION                                   
         TM    AORBAS,X'80'                                                     
         BNZ   ML240                                                            
         MVI   MAAORBAS,C'?'                                                    
*                                                                               
ML240    DS    0H                                                               
         MVC   MAAORPCT,AORPCT                                                  
         MVC   MAAOREFD,AOREFF                                                  
*                                                                               
         TM    BILSTAT,X'20'       FOR TRUE AOR BILL                            
         BZ    ML250                                                            
         MVC   MAAORAMT,BACTUAL    ACTUAL IS AOR AMOUNT                         
         ZAP   MAAORAMP,BACTP      ACTUAL IS AOR AMOUNT                         
         B     ML260                                                            
*                                                                               
ML250    DS    0H                  FOR CLIENT AOR BILL, CALCULATE               
*                                                                               
         ZAP   PACK1,BGRSP                                                      
         TM    AORBAS,X'F0'        IF NO BASIS BITS ON                          
         BZ    ML250A                                                           
         SP    PACK1,BNETP         ELSE A/C (GROSS LESS NET)                    
         TM    AORBAS,X'80'                                                     
         BNZ   ML250A                                                           
         ZAP   PACK1,BNETP         OR NET                                       
         TM    AORBAS,X'40'                                                     
         BNZ   ML250A                                                           
         DC    H'0'                                                             
*                                                                               
ML250A   ICM   RE,15,MAAORPCT    PERCENTAGE                                     
         CVD   RE,DUB            CONVERT TO PACKED                              
         MP    PACK1,DUB         AOR * PERCENTAGE                               
         SRP   PACK1,64-6,0      DIVIDE BY 1,000,000                            
         ZAP   MAAORAMP,PACK1                                                   
*                                                                               
         ICM   R0,15,MAAORPCT                                                   
         PACK  DUB,BAMT            GROSS                                        
         CVB   RF,DUB                                                           
         TM    AORBAS,X'F0'        IF NO BASIS BITS ON                          
         BZ    ML252                                                            
         ICM   RE,15,BNET                                                       
         SR    RF,RE               ELSE A/C (GROSS LESS NET)                    
         TM    AORBAS,X'80'                                                     
         BNZ   ML252                                                            
         LR    RF,RE               OR NET                                       
         TM    AORBAS,X'40'                                                     
         BNZ   ML252                                                            
         DC    H'0'                                                             
*                                                                               
ML252    DS    0H                                                               
         MR    RE,R0                                                            
         SLDA  RE,1                                                             
         D     RE,=F'1000000'                                                   
         LTR   RF,RF                                                            
         BNP   *+8                                                              
         A     RF,=F'1'                                                         
         SRA   RF,1                                                             
         STCM  RF,15,MAAORAMT                                                   
*                                                                               
ML260    DS   0H                                                                
         B     ML212                                                            
*                                                                               
         DROP  R7                                                               
         DROP  R2                                                               
*                                                                               
ML290    DS    0H                  MISSING DATA OR ERROR                        
         B     ML300               SO WHAT, JUST EXIT?                          
*                                                                               
ML300    DS    0H                                                               
*                                                                               
EXIT     DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
*        DATAMGR CALLS                                                          
***********************************************************************         
         SPACE 2                                                                
HIGH     DS    0H                                                               
         LR    R0,RE                                                            
         MVC   KEYSAVE,KEY                                                      
         GOTO1 VDATAMGR,DMCB,=C'DMRDHI',=C'SPTDIR',KEY,KEY                      
         TM    DMCB+8,X'FF'                                                     
         BZ    *+6                                                              
         DC    H'0'                                                             
         LR    RE,R0                                                            
         BR    RE                                                               
         SPACE 2                                                                
GET      DS    0H                                                               
         LR    R0,RE                                                            
         GOTO1 VDATAMGR,DMCB,=C'GETREC',=C'SPTFILE',KEY+14,MAAORAIO,   X        
               DMWORK                                                           
         TM    DMCB+8,X'FF'                                                     
         BZ    *+6                                                              
         DC    H'0'                                                             
         LR    RE,R0                                                            
         BR    RE                                                               
         SPACE 3                                                                
         LTORG                                                                  
         SPACE 3                                                                
WORKD    DSECT                                                                  
VDATAMGR DS    F                                                                
DMCB     DS    6F                                                               
WORK     DS    XL100                                                            
ELEM     DS    XL100                                                            
REC      DS    XL1024                                                           
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
KEY      DS    XL32                                                             
KEYSAVE  DS    XL32                                                             
DMWORK   DS    12D                                                              
PACK1    DS    PL16                                                             
*                                                                               
WORKDL   EQU   *-WORKD                                                          
         SPACE 2                                                                
       ++INCLUDE MAAORLKD                                                       
*                                                                               
         ORG   MAAORWRK                                                         
SVAORKEY DS    XL32                                                             
         SPACE 2                                                                
       ++INCLUDE SPGENAOR                                                       
         SPACE 2                                                                
       ++INCLUDE SPGENBILL                                                      
         SPACE 2                                                                
*++INCLUDE DDCOMFACSD                                                           
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACSD                                                     
         SPACE 2                                                                
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'025SPAORLK   02/13/03'                                      
         END                                                                    
