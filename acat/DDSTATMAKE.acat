*          DATA SET DDSTATMAKE AT LEVEL 008 AS OF 06/03/14                      
*CATALP STATMAKE                                                                
         TITLE 'STATMAKE - BUILD STATISTICS FILE'                               
         PRINT NOGEN                                                            
STATMAKE CSECT                                                                  
         ENTRY SSB                                                              
         ENTRY UTL                                                              
         NBASE WRKX-WRKD,STATMAKE,WORK=A(STATWORK)                              
         USING WRKD,RC                                                          
*                                                                               
INIT     XC    P1(24),P1           INITIALIZE DMOD0000 PARAMS                   
         LH    R3,RECSIZE                                                       
         STH   R3,P3+2                                                          
         L     R3,=V(STATS)                                                     
         ST    R3,P4                                                            
         LA    R3,ADR                                                           
         ST    R3,P5                                                            
         LA    R8,REC                                                           
         BAS   R9,OPENIT           OPEN STATISTICS FILE                         
         L     R3,P4                                                            
         LA    RE,1                                                             
         SLL   RE,16                                                            
         ST    RE,40(R3)           SET NEXT = TRK1/BLK0                         
*                                                                               
SETRECS  XC    Q1(24),Q1           GET RECORDS PER TRACK                        
         L     R3,=V(DARPT)                                                     
         ST    R3,Q1                                                            
         LH    R3,RECSIZE                                                       
         STH   R3,Q3+2                                                          
         L     R3,=V(STATS)                                                     
         ST    R3,Q4                                                            
         GOTO1 =V(DADDS),Q1                                                     
         LH    R0,Q3+2                                                          
         LTR   R0,R0                                                            
         BNZ   SETRECSX                                                         
         DC    H'0'                                                             
SETRECSX STH   R0,RECTRK                                                        
*                                                                               
         LH    R1,RECSIZE          SET INDEX REC TO HIGH VALUES                 
         LA    RF,NDX                                                           
         LA    RE,FFS                                                           
         MOVE  ((RF),(R1)),(RE)                                                 
         XC    NDX(16),NDX                                                      
*                                                                               
         LA    RE,REC              CLEAR DATA RECORD                            
         LH    RF,RECSIZE                                                       
         XCEF                                                                   
*                                                                               
         GOTO1 =V(CARDS),DMCB,C,=C'RE00'                                        
         CLC   C(2),=C'/*'                                                      
         BE    ERROR0                                                           
         MVC   ACTION(1),C                                                      
         EJECT                                                                  
CREATE   CLI   ACTION,C'C'         CREATE FILE                                  
         BNE   ERROR0                                                           
*                                                                               
         MVC   DUB(4),=4C'0'                                                    
         MVZ   DUB(4),C+1                                                       
         CLC   DUB(4),=4C'0'                                                    
         BNE   ERROR2                                                           
         PACK  DUB,C+1(4)          GET NUMBER OF TRM RECS                       
         CVB   R0,DUB                                                           
         CH    R0,TRMMAX                                                        
         BH    ERROR2                                                           
         CH    R0,TRMMIN                                                        
         BL    ERROR2                                                           
         STH   R0,TRMNUM                                                        
*                                                                               
         MVC   DUB(4),=4C'0'                                                    
         MVZ   DUB(4),C+5                                                       
         CLC   DUB(4),=4C'0'                                                    
         BNE   ERROR3                                                           
         PACK  DUB,C+5(4)          GET NUMBER OF PRG RECS                       
         CVB   R0,DUB                                                           
         CH    R0,PRGMAX                                                        
         BH    ERROR3                                                           
         CH    R0,PRGMIN                                                        
         BL    ERROR3                                                           
         STH   R0,PRGNUM                                                        
         MVC   FACNAME,C+9         GET FACPAK NAME (OPTIONAL)                   
*                                                                               
         SR    RE,RE               CALC NUMBER OF HEADERS                       
         LH    RF,TRMNUM                                                        
         LA    R0,200              MAX TRMS PER RECORD                          
         DR    RE,R0                                                            
         LTR   RE,RE                                                            
         BZ    *+8                                                              
         LA    RF,1(RF)            RF=NUM OF TERMINAL INDEX RECORDS             
         LA    RF,1(RF)            ONE FOR PROGRAM INDEX                        
         STH   RF,HDRNUM                                                        
*                                                                               
         MVC   NDX+02(2),HDRNUM    SET FILE DATA IN INDEX RECORD                
         MVC   NDX+04(2),TRMNUM                                                 
         MVC   NDX+06(2),PRGNUM                                                 
         MVC   NDX+08(2),RECSIZE                                                
         MVC   NDX+10(2),RECTRK                                                 
         MVC   NDX+12(4),FACNAME                                                
         EJECT                                                                  
INDEX    LA    R2,NDX+16           R2=A(NEXT INDEX ENTRY)                       
         SR    R3,R3               R3=NUM OF INDEX ENTRYS                       
         LH    R4,HDRNUM                                                        
         AH    R4,TRMNUM                                                        
         STH   R4,RECNUM           SET REC NUM OF 1ST PROGRAM RECORD            
*                                                                               
INDEX1   GOTO1 =V(CARDS),DMCB,C,=C'RE00'                                        
         CLI   C,C'*'                                                           
         BE    INDEX1              IGNORE COMMENT CARDS                         
         CLC   C(2),=C'/*'                                                      
         BE    INDEX2                                                           
         LA    R3,1(R3)            BUMP NUM OF INDEX ENTRYS                     
         CH    R3,PRGMAX                                                        
         BH    ERROR3                                                           
         GOTO1 =V(HEXIN),DMCB,C,(R2),4                                          
         OC    12(4,R1),12(R1)                                                  
         BZ    ERROR4                                                           
         MVC   2(7,R2),C+4                                                      
         MVC   9(7,R2),C+11                                                     
         MVI   16(R2),0                                                         
         BAS   R9,GETADR                                                        
         MVC   17(4,R2),RECADR                                                  
         LA    R2,21(R2)                                                        
         B     INDEX1                                                           
*                                                                               
INDEX2   EQU   *                                                                
         EJECT                                                                  
LOAD     LA    R8,NDX              LOAD PROGRAM INDEX RECORD                    
         BAS   R9,LOADIT                                                        
         XC    NDX+12(4),NDX+12    CLEAR FACPAK NAME FIELD                      
*                                                                               
         MVI   NDX+1,1                                                          
         LH    R7,HDRNUM                                                        
         BCTR  R7,0                R7=NUM OF TERMINAL INDEX RECORDS             
         MVC   RECNUM,HDRNUM                                                    
         SR    R3,R3                                                            
         LA    R8,NDX                                                           
LOAD1    LA    R5,NDX+16                                                        
         LA    R6,200                                                           
LOAD2    MVC   0(12,R5),FFS                                                     
         LTR   R3,R3                                                            
         BNZ   *+10                                                             
         XC    0(8,R5),0(R5)                                                    
         LA    R3,1(R3)                                                         
         CH    R3,TRMNUM                                                        
         BH    LOAD3                                                            
         BAS   R9,GETADR                                                        
         MVC   8(4,R5),RECADR                                                   
LOAD3    LA    R5,12(R5)                                                        
         BCT   R6,LOAD2                                                         
         BAS   R9,LOADIT                                                        
         BCT   R7,LOAD1                                                         
*                                                                               
         LA    R8,REC              LOAD TERMINAL RECS FROM REC                  
         LH    R7,TRMNUM                                                        
         MVI   REC,1                                                            
         MVC   REC+2(14),FFS                                                    
         BAS   R9,LOADIT                                                        
         BCT   R7,*-14                                                          
*                                                                               
         LA    R8,REC              LOAD PROGRAM RECORDS FROM REC                
         LH    R7,PRGNUM                                                        
         MVI   REC,2                                                            
         MVC   REC+2(14),FFS                                                    
         BAS   R9,LOADIT                                                        
         BCT   R7,*-14                                                          
*                                                                               
         LA    R8,REC              LOAD EOF RECORD FROM REC                     
         MVC   REC(16),FFS                                                      
         BAS   R9,LOADIT                                                        
*                                                                               
         B     PRINTIT                                                          
         EJECT                                                                  
ERROR0   MVC   MSG(14),=C'INVALID ACTION'                                       
         B     ERRORX                                                           
ERROR1   MVC   MSG(32),=C'NUMBER OF HEADER RECORDS INVALID'                     
         B     ERRORX                                                           
ERROR2   MVC   MSG(34),=C'NUMBER OF TERMINAL RECORDS INVALID'                   
         B     ERRORX                                                           
ERROR3   MVC   MSG(33),=C'NUMBER OF PROGRAM RECORDS INVALID'                    
         B     ERRORX                                                           
ERROR4   MVC   MSG(26),=C'PROGRAM XXXX NOT VALID HEX'                           
         MVC   MSG+8(4),C                                                       
         B     ERRORX                                                           
ERROR5   MVC   MSG(10),=C'DISK ERROR'                                           
         B     ERRORX                                                           
ERRORX   MVI   ACTION,X'FF'                                                     
*                                                                               
         B     PRINTIT                                                          
         EJECT                                                                  
PRINTIT  L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
         MVC   TITLE(25),=C'.... STATISTICS FILE MAKE'                          
         MVC   TITLE(4),FACNAME                                                 
*                                                                               
         CLI   ACTION,X'FF'                                                     
         BNE   PRINT1                                                           
         MVC   P(13),=C'*** ERROR ***'                                          
         MVC   P+14(40),MSG                                                     
         GOTO1 =V(PRINTER)                                                      
         B     EOJ                                                              
*                                                                               
PRINT1   CLI   ACTION,C'C'                                                      
         BNE   *+10                                                             
         MVC   MID1(23),=C'STATISTICS FILE CREATED'                             
         CLI   ACTION,C'I'                                                      
         BNE   *+10                                                             
         MVC   MID1(23),=C'STATISTICS INDX CREATED'                             
         MVC   ADR,=X'00010100'                                                 
         LA    R8,NDX                                                           
         BAS   R9,READIT           READ INDEX RECORD                            
         LA    R2,MID1+27                                                       
*                                                                               
PRINT2   MVC   0(8,R2),=C'HEADERS='                                             
         LH    R0,NDX+2                                                         
         CVD   R0,DUB                                                           
         UNPK  8(4,R2),DUB                                                      
         OI    11(R2),X'F0'                                                     
         MVC   14(10,R2),=C'TERMINALS='                                         
         LH    R0,NDX+4                                                         
         CVD   R0,DUB                                                           
         UNPK  24(4,R2),DUB                                                     
         OI    27(R2),X'F0'                                                     
         MVC   30(9,R2),=C'PROGRAMS='                                           
         LH    R0,NDX+6                                                         
         CVD   R0,DUB                                                           
         UNPK  39(4,R2),DUB                                                     
         OI    42(R2),X'F0'                                                     
         MVC   45(12,R2),=C'RECORD-SIZE='                                       
         LH    R0,NDX+8                                                         
         CVD   R0,DUB                                                           
         UNPK  57(4,R2),DUB                                                     
         OI    60(R2),X'F0'                                                     
         MVC   63(12,R2),=C'RECORDS/TRK='                                       
         LH    R0,NDX+10                                                        
         CVD   R0,DUB                                                           
         UNPK  75(4,R2),DUB                                                     
         OI    78(R2),X'F0'                                                     
         MVC   SUB1(38),=C'SYST  PRGM  TTTTBBRR  SYSNAME  PRGNAME'              
         MVC   SUB2(38),=C'----  ----  --------  -------  -------'              
*                                                                               
         LA    R7,NDX+16                                                        
PRINT3   CLI   0(R7),X'FF'                                                      
         BE    EOJ                                                              
         GOTO1 =V(HEXOUT),DMCB,(R7),P+1,1,=C'TOG'                               
         GOTO1 (RF),(R1),1(R7),P+7                                              
         GOTO1 (RF),(R1),17(R7),P+12,4                                          
         MVC   P+22(7),2(R7)                                                    
         MVC   P+31(7),9(R7)                                                    
         GOTO1 =V(PRINTER)                                                      
         LA    R7,21(R7)                                                        
         B     PRINT3                                                           
*                                                                               
EOJ      XBASE                                                                  
         EJECT                                                                  
OPENIT   L     RF,=V(OPEN)                                                      
         B     IO                                                               
LOADIT   L     RF,=V(WRTAFT)                                                    
         B     IO                                                               
READIT   L     RF,=V(READ)                                                      
         B     IO                                                               
WRITIT   L     RF,=V(WRITE)                                                     
*                                                                               
IO       ST    RF,P1               SET COMMAND                                  
         ST    R8,P2               SET BUFFER                                   
         LA    R2,DMCB                                                          
         LA    R1,P1                                                            
         L     RF,=V(DMOD000)                                                   
         BASR  RE,RF               GOTO DMOD000                                 
*                                                                               
         OC    P3(2),P3                                                         
         BNZ   ERROR5                                                           
         BR    R9                                                               
*                                                                               
GETADR   SR    RE,RE               RECNUM=RECNUM+1 AND CONV TO TTTTBB00         
         LH    RF,RECNUM                                                        
         LA    RF,1(RF)                                                         
         STH   RF,RECNUM                                                        
         LH    R0,RECTRK                                                        
         AR    RF,R0                                                            
         BCTR  RF,0                                                             
         DR    RE,R0                                                            
         LA    RE,1(RE)                                                         
         STH   RF,RECADR                                                        
         STC   RE,RECADR+2                                                      
         MVI   RECADR+3,0                                                       
         BR    R9                                                               
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
RECSIZE  DC    H'2416'                                                          
RECTRK   DC    H'16'                                                            
HDRNUM   DC    H'0'                                                             
TRMMAX   DC    H'12000'            MAXIMUM NUMBER OF TERMINALS                  
TRMMIN   DC    H'1'                                                             
TRMNUM   DC    H'0'                                                             
PRGMAX   DC    H'110'              MAXIMUM NUMBER OF PROGRAMS                   
PRGMIN   DC    H'0'                                                             
PRGNUM   DC    H'0'                                                             
FACNAME  DC    CL4' '                                                           
MSG      DC    CL40' '                                                          
FFS      DC    2416X'FF'                                                        
         EJECT                                                                  
STATWORK DS    0D                                                               
         DS    1280D                                                            
*                                                                               
SSB      DS    0D                                                               
         DC    X'00000002',XL28'00'                                             
*                                                                               
UTL      DS    0D                                                               
         DC    F'0',X'01',XL27'00'                                              
*                                                                               
WRKD     DSECT                                                                  
DUB      DS    D                                                                
*                                                                               
P1       DS    F                                                                
P2       DS    F                                                                
P3       DS    F                                                                
P4       DS    F                                                                
P5       DS    F                                                                
P6       DS    F                                                                
*                                                                               
Q1       DS    F                                                                
Q2       DS    F                                                                
Q3       DS    F                                                                
Q4       DS    F                                                                
Q5       DS    F                                                                
Q6       DS    F                                                                
*                                                                               
DMCB     DS    6F                                                               
C        DS    20F                                                              
ADR      DS    F                                                                
ACTION   DS    F                                                                
RECNUM   DS    H                                                                
RECADR   DS    F                                                                
*                                                                               
REC      DS    2416C                                                            
NDX      DS    2416C                                                            
*                                                                               
WRKX     DS    0C                                                               
         EJECT                                                                  
       ++INCLUDE DDDPRINT                                                       
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'008DDSTATMAKE06/03/14'                                      
         END                                                                    
