*          DATA SET TABIPOST   AT LEVEL 109 AS OF 08/13/15                      
*CATALP TABIPOST                                                                
*                                                                               
         TITLE 'TABIPOST - TALENT BILLING - POSTING ROUTINES'                   
*                                                                               
*        PARMS                                                                  
*              P1        - RC                                                   
*              P2 BYTE 3 - ROUTINE EQUATE                                       
*              P3        - A(ENTRY IN CHECK TABLE)                              
*                                                                               
TABIPOST CSECT                                                                  
         ENTRY SSB                                                              
         PRINT NOGEN                                                            
         NMOD1 0,TABIPOST,R7                                                    
         L     RC,0(R1)                                                         
         USING GEND,RC                                                          
         L     R8,ASPOOLD                                                       
         USING SPOOLD,R8                                                        
         L     R9,ASUBSYSD         SYSTEM SPECIFIC WORK                         
         USING SUBSYSD,R9                                                       
         LA    RA,BUFF             BILL DSECT                                   
         LA    RA,8(RA)                                                         
         USING BILLD,RA                                                         
         EJECT                                                                  
*                                                                               
*        DECIDE WHAT ROUTINE TO GO TO                                           
*                                                                               
         CLI   7(R1),EOPENWK       OPEN WORKER FILE                             
         BE    OPWK                                                             
         CLI   7(R1),EPOSTSR       DO SR POSTINGS                               
         BE    POSTSR                                                           
         CLI   7(R1),EPOSTSX       DO SX POSTINGS                               
         BE    POSTSX                                                           
         CLI   7(R1),EPOSTSE       DO SE POSTINGS                               
         BE    POSTSE                                                           
         CLI   7(R1),EEOFPOST      DO EOF POSTINGS                              
         BE    EOFPOST                                                          
         CLI   7(R1),EPSTSJSV      DO SJ & SV POSTINGS                          
         BE    POSTSJSV                                                         
         CLI   7(R1),EPUTTAB       PUT INFO IN TABLE FOR SE POSTINGS            
         BE    PUTTAB                                                           
         CLI   7(R1),ECKPOST       CREATE TABLE OF POSTINGS - WORK CODE         
         BE    CKPOST                                                           
         CLI   7(R1),EINVLVL       SET TNH ON INVOICE LEVEL                     
         BE    INVLVL                                                           
         DC    H'0'                                                             
         SPACE 1                                                                
         EJECT                                                                  
*                                                                               
*        OPEN A WORKER FILE FOR A NEW AGENCY                                    
*                                                                               
         SPACE 1                                                                
OPWK     DS    0H                  OPEN WORKER FILE                             
         L     R2,=V(AREA)         CLEAR OUT AREA BEFORE NEW INV                
         MVI   0(R2),C' '                                                       
         MVC   1(249,R2),0(R2)                                                  
         XC    ID,ID               BUILD WORKER KEY                             
         MVC   ID(2),TGUSER                                                     
         MVI   ID+2,C'T'                                                        
         MVC   ID+3(2),RCPROG+2    BI OR PB                                     
         PACK  DUB(2),RCDATE+3(3)                                               
         MVC   ID+6(1),DUB                                                      
         MVI   ID+7,C'P'                                                        
         OI    ID+13,X'01'         ALLOW DUPLICATE KEYS                         
         MVI   OPEN,C'Y'           SET FLAG - WORKER FILE OPEN                  
*                                                                               
OP05     CLI   SWPOST,POSTPROD                                                  
         BE    OP10                                                             
         MVC   ID2,ID                                                           
         MVC   ID2(2),TPORIG+8                                                  
*                                                                               
OP10     MVC   COMMAND(6),=CL6'OPEN'                                            
         BAS   RE,WKFILE                                                        
*                                                                               
OPX      B     XIT                                                              
         EJECT                                                                  
*                                                                               
*        ROUTINES TO POST SR POSTINGS TO ACC                                    
*                                                                               
         SPACE 1                                                                
POSTSR   DS    0H                                                               
         OC    TBTOT,TBTOT         IF TBTOT = 0 - DON'T POST                    
         BZ    PORX                                                             
         MVI   SWPOST,POSTRCV                                                   
         MVI   DRORCR,C'D'                                                      
         MVC   ACC(1),TPHEX        HEX COMP                                     
         MVC   ACC+1(2),=C'SR'                                                  
         MVC   ACC+3(12),AYRECV    RECEIVABLE ACCOUNT                           
*                                                                               
         MVC   SUBAC,SPACES                                                     
         MVC   SUBAC+3(12),TBCLNAME                                             
         OC    SUBAC,SPACES                                                     
*                                                                               
         MVC   SUBWORK,SPACES                                                   
         MVC   AMOUNT,TBTOT                                                     
         BAS   RE,POSTIT                                                        
*                                                                               
PORX     B     XIT                                                              
         EJECT                                                                  
*                                                                               
*        ROUTINES TO POST BNP SX POSTINGS TO ACC                                
*                                                                               
         SPACE 1                                                                
POSTSX   DS    0H                                                               
         MVC   ACC,SPACES          CLEAR ACC KEY                                
         MVI   SWPOST,POSTSUMM                                                  
         L     R2,=V(BNPTAB)       POST PNH FOR BNP INVOICES                    
         USING BNPD,R2                                                          
*                                                                               
POX10    C     R2,=V(BNPTABX)      IF WE REACHED THE END OF TABLE               
         BNL   POXX                                                             
         OC    0(BNPLEN,R2),0(R2)  OR END OF ENTRIES                            
         BZ    POXX                                                             
         MVI   DRORCR,C'C'                                                      
         MVC   ACC(1),TPHEX        HEX COMP                                     
         MVC   ACC+1(4),=C'SXPW'                                                
         MVC   ACC+5(3),BNPUNI     UNION                                        
*                                                                               
         MVC   SUBAC,SPACES                                                     
         MVC   SUBAC+3(6),BNPUNI                                                
         MVC   SUBAC+10(3),=C'P&&H'                                             
         OC    SUBAC,SPACES                                                     
*                                                                               
         MVC   SUBWORK,SPACES                                                   
         MVC   AMOUNT,BNPAMT                                                    
         OC    AMOUNT,AMOUNT                                                    
         BZ    POX20                                                            
         MVI   MEMO,0                                                           
         BAS   RE,POSTIT                                                        
*                                                                               
POX20    LA    R2,BNPLEN(R2)       BUMP TO NEXT ENTRY                           
         B     POX10                                                            
*                                                                               
POXX     B     XIT                                                              
         EJECT                                                                  
*        ROUTINES TO POST SE POSTINGS TO ACC                                    
*                                                                               
POSTSE   DS    0H                                                               
         MVI   SWPOST,POSTSUMM                                                  
         LA    R3,PCODES                                                        
         CLI   TBMED,TACOMEDE      IF MEDIA = EVENT                             
         BNE   *+8                                                              
         L     R3,=A(PCODES2)      USE DIFFERENT POSTINGS TABLE                 
         USING PCODED,R3                                                        
*                                                                               
         MVC   ACC(1),TPHEX        ACCOUNT                                      
         MVC   SUBAC(1),TPHEX      CONTRA ACCOUNT                               
         MVC   SUBWORK,SPACES                                                   
         MVI   MEMO,0                                                           
*                                                                               
PO10     CLI   0(R3),X'FF'                                                      
         BE    POX                                                              
         MVC   DRORCR,PCDRORCR           DEBIT OR CREDIT                        
         MVC   ACC+1(L'PCACCT),PCACCT    ACCOUNT CODE                           
         MVC   SUBAC+1(L'PCCONT),PCCONT  CONTRA/ACCOUNT                         
         CLC   SUBAC+1(2),=C'SR'                                                
         BNE   *+10                                                             
         MVC   SUBAC+3(1),TBTPO                                                 
*                                                                               
         LH    R2,PCDISP           DISPACEMENT TO POSTTAB                       
*        LA    R2,POSTTAB(R2)                                                   
         A     R2,APOSTTAB                                                      
         OC    0(L'POSTTAB,R2),0(R2)                                            
         BZ    PO20                IF AMOUNT = 0 DON'T POST                     
         MVC   AMOUNT,0(R2)                                                     
         BAS   RE,POSTIT                                                        
         XC    0(L'POSTTAB,R2),0(R2)                                            
*                                                                               
PO20     LA    R3,PCODELEN(R3)                                                  
         B     PO10                                                             
*                                                                               
POX      B     XIT                                                              
         EJECT                                                                  
*                                                                               
*              ROUTINES TO POST TRAILER AND TOTAL TAXES                         
*                                                                               
EOFPOST  DS    0H                                                               
         ZIC   R3,STATUS           SAVE STATUS BYTE                             
         XC    STATUS,STATUS       CLEAR STATUS 'COD PROBLEM'                   
         L     R2,=V(AREA)         TRAILER RECORD                               
         USING PSSUBFD,R2                                                       
         MVC   PSSBEL(2),=X'521D'                                               
         MVC   PSSBDESC,SPACES                                                  
         MVC   PSSBDESC(7),=C'TALENT-'                                          
         CLI   SWPOST,POSTPROD     WHICH POSTING ARE WE DOING                   
         BE    EOF10                                                            
         MVC   PSSBDESC+7(6),TPUSER                                             
         ZAP   PSSBRECS,TALRECS                                                 
         ZAP   PSSBCASH,TALCASH                                                 
         MVI   PSSBCASH+6,0                                                     
         B     EOF30                                                            
*                                                                               
         USING TAIFD,R4                                                         
EOF10    LA    R4,IFAYR            AGENCY INTERFACE                             
         CLI   INTFACE,C'A'                                                     
         BE    EOF20                                                            
         LA    R4,IFCLR            CLIENT OVERRIDE                              
*                                                                               
EOF20    MVC   PSSBDESC+7(6),TAIFAGY                                            
         ZAP   PSSBRECS,PRODRECS                                                
         ZAP   PSSBCASH,PRODCASH                                                
         MVI   PSSBCASH+6,0                                                     
         ZAP   PRODRECS,=P'0'                                                   
         ZAP   PRODCASH,=P'0'                                                   
*                                                                               
EOF30    MVC   COMMAND,=CL6'ADD'                                                
         BAS   RE,WKFILE                                                        
*                                                                               
         CLI   SWPOST,POSTRCV      CHECK FOR ERROR RECIEVABLES POSTING          
         BNE   EOF50                                                            
         CP    TALCASH,TALCRDTS    DOES DEBITS = CREDITS?                       
         BE    EOF50                                                            
*                                                                               
         GOTO1 =A(SENDMAIL),DMCB                                                
*                                                                               
EOF50    MVC   COMMAND,=CL6'CLOSE'                                              
         BAS   RE,WKFILE                                                        
*                                                                               
         TM    PRGSTAT,FQASYS      PUT ALL WRKR FILES ON FQA ON KEEP            
         BNZ   EOF55                                                            
*                                                                               
         CLI   SWPOST,POSTRCV      CHECK FOR ERROR RECIEVABLES POSTING          
         BNE   EOF60                                                            
         CP    TALCASH,TALCRDTS    DOES DEBITS = CREDITS?                       
         BE    EOF60                                                            
EOF55    MVC   COMMAND,=CL6'KEEP'                                               
         BAS   RE,WKFILE                                                        
*                                                                               
EOF60    STC   R3,STATUS           RESTORE STATUS BYTE                          
         CLI   SWPOST,POSTPROD                                                  
         BNE   EOFX                                                             
         MVI   OPEN,C'N'                                                        
*                                                                               
EOFX     B     XIT                                                              
         DROP  R2,R4                                                            
         EJECT                                                                  
*                                                                               
*              ROUTINE TO PUT INVOICE INFO TO TABLE                             
*                                                                               
PUTTAB   DS    0H                                                               
         USING TXPOSTTB,R6                                                      
         L     R6,APOSTTAB                                                      
                                                                                
         XC    INVCR,INVCR         CLEAR FOR EACH INVOICE                       
         XC    INVDR,INVDR                                                      
         CLI   TBNPOST,C'Y'        IF THIS IS NOT A POSTING INVOICE             
         BE    PTX                    DON'T POST RECEIVIBLES                    
         TM    STATUS,STCOD        IF THIS IS A COD - THEN DON'T                
         BO    PTX                    POST RECEIVIBLES                          
*                                                                               
         AF    INVDR,TBTOT         ADD INVOICE TOTAL TO DEBITS                  
*                                                                               
         TM    TBISTAT,TAPDSCAN    IF THIS IS A CANADIAN PAYMENT                
         BO    PUT70               POST TO CANADIAN ACCOUNT                     
         LA    R2,TXPGRS1          (SE41001)                                    
**NO-OP  CLC   LEOR,TGTPEMP        IF THIS IS NOT TALENT PARTNERS               
**JAN13  BE    PUT10                                                            
**       CLC   LEOR,=C'PP '        AND NOT EMPLOYER PP                          
**       BE    PUT10                                                            
**NO-OP  LA    R2,TXPGRS2          ADD AMOUNT TO SE41002                        
PUT10    AF    0(R2),TOTGRS                                                     
         AF    INVCR,TOTGRS        INCREMENT INVOICE CREDITS                    
         AF    0(R2),TBACTWKU                                                   
         AF    INVCR,TBACTWKU      ADD ACTRAWORKS IN US$ ALSO                   
*                                                                               
         AF    TXPMISC,TOTMISC                                                  
         AF    INVCR,TOTMISC                                                    
         AF    TXPMISC,TBGSTU      ADD GST PAID FOR US$ INVOICES                
         AF    TXPMISC,TBPSTU      ADD GST PAID FOR US$ INVOICES                
         AF    INVCR,TBGSTU                                                     
         AF    INVCR,TBPSTU                                                     
         CLI   TBMED,TACOMEDE      IF EVENT = MEDIA,                            
         BNE   PUT15                                                            
**NO-OP  AF    TXPMISC,TOTTXNW     ADD TAXABLE NON WAGES                        
**12/14  AF    INVCR,TOTTXNW                                                    
         AF    TXPMISC,TOTNTXNW    ADD NON TAXABLE NON WAGES                    
         AF    INVCR,TOTNTXNW                                                   
PUT15    AF    TXPMISC,AOSPST      AOS PST                                      
         AF    INVCR,AOSPST        (SE41011)                                    
                                                                                
         CLI   TBMED,TACOMEDE      IF EVENT = MEDIA,                            
         BNE   PUT20                                                            
         AF    TXPTXTR,TOTTXNW     ADD TAXABLE NON WAGES                        
         AF    INVCR,TOTTXNW       (SE41013)                                    
                                                                                
PUT20    GOTO1 =A(TXPUNIT),DMCB,TXPTXWA,=C'WA '                                 
         GOTO1 =A(TXPUNIT),DMCB,TXPTXHI,=C'HI '                                 
*        GOTO1 =A(TXPUNIT),DMCB,(X'80',TXPTXCA),=C'CA '                         
*        GOTO1 =A(TXPUNIT),DMCB,(X'80',TXPTXNY),=C'NY '                         
         GOTO1 =A(TXPUNIT),DMCB,TXPTXCA,=C'CA '                                 
         GOTO1 =A(TXPUNIT),DMCB,TXPTXNY,=C'NY '                                 
                                                                                
PUT40    LA    R2,TXPTAX1          EOR TAX (SE41004)                            
**NO-OP  CLC   LEOR,TGTPEMP        IF THIS IS NOT TALENT PARTNERS               
**JAN13  BE    PUT50                                                            
**       CLC   LEOR,=C'PP '        AND NOT EMPLOYER PP                          
**       BE    PUT50                                                            
**NO-OP  LA    R2,TXPTXFCR         ADD AMOUNT TO 41005                          
*                                                                               
PUT50    AF    0(R2),TBTAX                                                      
         AF    INVCR,TBTAX                                                      
*                                                                               
         AF    TXPTXFCR,FICR       FICA CREDITS  (SE41005)                      
         AF    INVCR,FICR                                                       
*                                                                               
         AF    TXPHAND,TBHANDC     HANDLING FEES (SE41006)                      
         AF    INVCR,TBHANDC                                                    
         AF    TXPHAND,TBHANDI                                                  
         AF    INVCR,TBHANDI                                                    
*                                                                               
         AF    TXPPNH,TPNH         P & H (SE41007)                              
         AF    INVCR,TPNH                                                       
*                                                                               
         TM    STATUS,STBNP                                                     
         BNO   PUT60                                                            
         AF    TXPBNP,TPNH         B-N-P P & H (SE42005)                        
         AF    INVDR,TPNH          DEBIT                                        
*                                                                               
PUT60    AF    TXPACOMM,TACOMM     AGENCY COMMISSION                            
         AF    INVCR,TACOMM                                                     
*                                                                               
         AF    TXPACOMM,TASIGN     SIGNATORY FEE                                
         AF    INVCR,TASIGN                                                     
*                                                                               
         AF    TXPCSF,TCSF         CONTRACT SERVICE FEE                         
         AF    INVCR,TCSF                                                       
*                                                                               
         B     PTX                                                              
*                                                                               
PUT70    AF    TXPGMIR,TBGMINR         CANADIAN WAGES + I & R                   
         AF    INVCR,TBGMINR           (SE41009)                                
         AF    TXPGMIR,TBACTWKU                                                 
         AF    INVCR,TBACTWKU          ADD ACTRAWORKS AMOUNT ALSO               
*                                                                               
         AF    TXPCHND,TBCHANDU        CANADIAN HANDLING                        
         AF    INVCR,TBCHANDU          (SE41010)                                
*                                                                               
         AF    TXPGST,TBGSTU           CANADIAN GST                             
         AF    TXPGST,TBPSTU                    PST                             
         AF    INVCR,TBGSTU            (SE41011)                                
         AF    INVCR,TBPSTU                                                     
         LHI   RE,TBPSTAOS-BILLD                                                
         AR    RE,RA                                                            
         AF    TXPGST,0(RE)            AOS PST                                  
         AF    INVCR,0(RE)             (SE41011)                                
*                                                                               
PUT80    TM    TBOPTS,TBRERUN          IF THIS IS A RERUN                       
         BNO   PUT90                                                            
         TM    TBBDSTAT,TABDSCNW       & BILLED THE OLD WAY                     
         BNO   PUT100                  DON'T DO FOLLOWING POSTINGS              
*                                                                               
PUT90    AF    TXPGSTC,TBCGST2         GST CAN$ (SYTXGST)                       
         AF    INVCR,TBCGST2           ON HANDLING & I&R                        
*                                                                               
         AF    TXPGSTU,TBGSTU2         GST US$ (SE42011)                        
         AF    INVDR,TBGSTU2           HANDLING & I&R                           
*                                                                               
         AF    TXPGSTD,TBDIFGST        GST DIFFERENCE                           
         AF    INVDR,TBDIFGST                                                   
*                                                                               
PUT100   AF    TXPPNH,TBCPNHU          P & H (SE41007)                          
         AF    INVCR,TBCPNHU           TREAT THE SAME AS ALL INVOICES           
*                                                                               
         GOTO1 CANCVTUS,DMCB,TACOMM                                             
         AF    TXPACOMM,TACOMM       AGENCY COMMISSION                          
         AF    INVCR,TACOMM                                                     
*                                                                               
         GOTO1 CANCVTUS,DMCB,TASIGN                                             
         AF    TXPACOMM,TASIGN       SIGNATORY FEE                              
         AF    INVCR,TASIGN                                                     
*                                                                               
         TM    STATUS,STBNP                                                     
         BNO   PTX                                                              
         AF    TXPBNP,TBCPNHU          B-N-P P & H (SE42005)                    
         AF    INVDR,TBCPNHU           TREAT THE SAME AS ALL INVOICES           
*                                                                               
PTX      BAS   RE,FIXTAL               FIX CANADIAN ROUNDING PROBLEM            
         B     XIT                                                              
         EJECT                                                                  
*                                                                               
*                                                                               
*        IF TOTAL CREDITS DIFFER FROM DEBITS BY LESS THAN A FEW CENTS           
*        ADJUST SE41009 ENTRY TO ACCOUNT FOR ROUNDING PROBLEM                   
*                                                                               
FIXTAL   NTR1                                                                   
         TM    TBISTAT,TAPDSCAN    IF THIS IS A CANADIAN PAYMENT                
         BNO   FTX                                                              
         L     R1,INVCR            CREDITS                                      
         S     R1,INVDR            DEBITS                                       
         BZ    FTX                                                              
         LPR   R3,R1                                                            
         CH    R3,=H'10'           IF DIFFERENCE IS > A FEW CENTS               
         BH    FTX                 LET IT GO                                    
         L     R2,TXPGMIR            ELSE SUBTRACT THE DIFFERENCE               
         SR    R2,R1                                                            
         ST    R2,TXPGMIR            FROM GROSS & MISC & I&R                    
*                                                                               
FTX      B     XIT                                                              
                                                                                
         DROP  R6                                                               
         EJECT                                                                  
*                                                                               
*        ROUTINE TO CONVERT AMOUNT TO US$                                       
*                                                                               
CANCVTUS NTR1                                                                   
         L     R5,0(R1)            ADDRESS OF AMOUNT TO CONVERT                 
         OC    0(4,R5),0(R5)       SEE IF AMOUNT IS ZERO                        
         BZ    XIT                 YES LEAVE                                    
         ZAP   DUB,TBGCVT                                                       
         CVB   R1,DUB                                                           
         ST    R1,FULL             CONVERT IT TO US DOLLARS                     
                                                                                
         L     R3,0(R5)            FOR POSTING                                  
         GOTO1 MULT,DMCB,(R3),FULL                                              
         L     R1,0(R1)                                                         
         ST    R1,0(R5)                                                         
         B     XIT                                                              
         EJECT                                                                  
*                                                                               
*                                                                               
*        ROUTINE TO SET TNH ON INVOICE LEVEL                                    
*                                                                               
         USING TABCHKD,R3                                                       
INVLVL   DS    0H                                                               
         CLI   INTFACE,C'N'        IF NOT ON INTERFACE - EXIT                   
         BE    ILVLX                                                            
*                                                                               
         L     R3,ACHKTAB          PUT TO ENTRY IN CHECK TABLE                  
***      MVC   SVTAB,0(R3)         SAVE ENTRY CONTENTS                          
         LA    RE,0(R3)            RE=A(DATA TO MOVE FROM)                      
         LA    R0,SVTAB            R0=A(DATA TO MOVE TO)                        
         LHI   R1,TABLEN           R1 AND RF = LENGTH TO MOVE                   
         LR    RF,R1                                                            
         MVCL  R0,RE               SAVE ENTRY CONTENTS IN SVTAB                 
***      XC    0(TABLEN,R3),0(R3)  PRE-CLEAR AREA                               
         LA    RE,0(R3)                                                         
         LHI   RF,TABLEN                                                        
         SR    R1,R1                                                            
         SR    R0,R0                                                            
         MVCL  RE,R0               PRE-CLEAR AREA                               
         MVI   TBPRERUN,C'Y'                                                    
*                                                                               
         USING TABDD,R4                                                         
         GOTO1 EXTRCT,DMCB,(RC)    GENERAL EXTRACT TEST                         
         BNE   ILVL05                                                           
         L     R4,AIO1             USE EXISTING INFO                            
         MVI   ELCODE,TABDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   ILVL05                                                           
         MVC   TABTAX,TABDTAX      RESET TAX AMOUNT                             
*                                                                               
         L     RE,TABDHND                                                       
         A     RE,TASIGN                                                        
         ST    RE,TABHND           HANDLING                                     
*                                                                               
         MVC   TABHNDC,TABDHNDC    CORP HANDLING                                
*                                                                               
         L     RF,TABDGST          SET GST                                      
         ST    RF,TABGST                                                        
         OC    TABGST,TABGST       IF GST ON US DOLLAR INVOICE                  
         BZ    ILVL02                                                           
         TM    TBISTAT,TAPDSCAN                                                 
         BO    ILVL02                                                           
         OI    TABSTAT2,TABSTGST   THEN POST IT WITH WAGES                      
*                                                                               
ILVL02   MVC   TABFICR,TABDFICR    RESET FICA AMOUNT                            
*                                  NO-OP FOR CORRECT POSTS WHEN EXTRACT         
******   L     R1,TPAYI            RESET ALL INFO FOR POSTING                   
******   A     R1,TPAYC                                                         
******   ST    R1,TABPAYC                                                       
******   MVC   TABREXP,TREXP                                                    
******   MVC   TABPNH,TPNH                                                      
******   MVC   TABHNW,THNW                                                      
******   MVC   TABINR,TINR                                                      
         B     ILVL70              ALL INFO SET FROM TABD ELEMENT               
*                                                                               
ILVL05   TM    BOVRSTAT,BOVRTAX    BOVER TAX AMOUNT?                            
         BZ    ILVL08                                                           
         GOTO1 =A(SETTAX)          SET TAX OVERRIDE AMOUNT                      
         L     R1,TABTAX                                                        
         A     R1,FULL                                                          
         ST    R1,TABTAX                                                        
         B     ILVL10                                                           
*                                                                               
ILVL08   TM    SVOPT1,TAPDOTAX     IF OVERRIDE TAX AMOUNT                       
         BZ    ILVL10                                                           
         MVI   WORK,TAPATTAX                                                    
         BAS   RE,GETPAAMT         GET AMOUNT FROM TAPA ELEMENT                 
         MVC   TABTAX,FULL                                                      
*                                                                               
ILVL10   TM    BOVRSTAT,BOVRHNDB+BOVRHNDP+BOVRHNDC BOVER HAND AMOUNT?           
         BNZ   ILVL15                                                           
         TM    SVOPT1,TAPDOHND     IF OVERRIDE HANDLING FEE                     
         BZ    ILVL40                                                           
ILVL15   CLI   GTYPE,TABRTY1       IF BILLING TYPE 1 OR 2 OR 20                 
         BE    ILVL20                                                           
         CLI   GTYPE,TABRTY2       AND THERE'S HANDLING OVERRIDE                
         BE    ILVL20                                                           
         CLI   GTYPE,TABRTY16                                                   
         BE    ILVL20                                                           
         CLI   GTYPE,TABRTY20                                                   
         BNE   ILVL30                                                           
*                                                                               
ILVL20   CLC   SVCKNUM,=H'1'       IF MORE THAN 1 PERFORMER PAID                
         BH    ILVL24                                                           
         TM    STAT2,STCORP        OR IF THERE IS A CORP ON INVOICE             
         BO    ILVL24                                                           
         CLI   GTYPE,TABRTY2       IF BTYPE 2 OR 16, DON'T NEED                 
         BE    ILVL24              OVERRIDE TAX AMOUNT                          
         CLI   GTYPE,TABRTY16                                                   
         BE    ILVL24                                                           
         TM    BOVRSTAT,BOVRTAX    OR BOVER TAX AMOUNT?                         
         BZ    ILVL25                                                           
ILVL24   TM    BOVRSTAT,BOVRHNDB+BOVRHNDP+BOVRHNDC BOVER HAND AMOUNT?           
         BZ    ILVL30                                                           
         BAS   RE,SETHANDI         SET OVERRIDE HANDLING AMT                    
         L     R1,TABHNDC                                                       
         A     R1,FULL                                                          
         ST    R1,TABHNDC                                                       
         B     ILVL40                                                           
*                                                                               
ILVL25   TM    SVOPT1,TAPDOTAX     OR THERE WAS OVERRIDE TAX AMOUNT             
         BNO   ILVL40                                                           
*                                                                               
ILVL30   MVI   WORK,TAPATHND                                                    
         BAS   RE,GETPAAMT         GET AMOUNT FROM TAPA ELEMENT                 
         MVC   TABHNDC,FULL                                                     
*                                                                               
ILVL40   BAS   RE,CKFEE                                                         
         BAS   RE,CKSIGN                                                        
*                                                                               
         TM    SVOPT2,TAPDOFCR     IF OVERRIDE FICA CREDITS                     
         BNO   ILVL50                                                           
         MVI   WORK,TAPATFIC                                                    
         BAS   RE,GETPAAMT         GET AMOUNT FROM TAPA ELEMENT                 
         MVC   TABFICR,FULL                                                     
*                                                                               
ILVL50   TM    SVOPT1,TAPDOGST     IF OVERRIDE GST                              
         BZ    ILVL70                                                           
         MVI   WORK,TAPATGST                                                    
         BAS   RE,GETPAAMT         GET AMOUNT FROM TAPA ELEMENT                 
         MVC   TABGST,FULL                                                      
*                                                                               
ILVL70   TM    TBISTAT,TAPDSCAN    IF THIS IS A CANADIAN PAYMENT                
         BNO   ILVL75                                                           
         L     RF,TABGST                                                        
         A     RF,TBCPST                                                        
         ST    RF,TABGST                                                        
         GOTO1 ACVTTAB,DMCB,TABCHKD CONVERT CAN$ TO US$ IN TABLE                
         B     ILVL80                                                           
*                                                                               
ILVL75   L     RF,TABGST           US$ PAYMENT                                  
         A     RF,TBPSTU           COMBINE PST WITH GST                         
         ST    RF,TABGST                                                        
*                                                                               
ILVL80   OC    TABTAX(TABBDL),TABTAX  IF NO OVERRIDE AMOUNTS                    
         BNZ   ILVL83                 DON'T BOTHER GOING TO TAINTER             
         LHI   RE,TBPSTAOS-BILLD                                                
         AR    RE,RA                                                            
         OC    0(4,RE),0(RE)          IF NO AOSPST,                             
         BZ    ILVL95                 DON'T BOTHER GOING TO TAINTER             
*                                                                               
ILVL83   MVI   BYTE,C'C'                                                        
         BAS   RE,CMPCHKS          CHECK SAME CATEGORY ON ALL CHECKS            
         BNE   *+10                                                             
         MVC   TABONOFF,SVTAB+TABONOFF-TABCHKD YES - SET IN TABLE               
         MVI   BYTE,C'U'                                                        
         BAS   RE,CMPCHKS          CHECK SAME UNION ON ALL CHECKS               
         BNE   *+10                                                             
         MVC   TABUNI,SVTAB+TABUNI-TABCHKD     YES - SET IN TABLE               
*                                                                               
         CLC   SVCKNUM,=H'1'       IF MORE THAN ONE CHECK                       
         BNH   ILVL85                                                           
         XC    TGCASTAT,TGCASTAT   CLEAR CAST RELATED INFORMATION               
         XC    TGCATYPE,TGCATYPE                                                
*                                                                               
ILVL85   OC    TABUNI,TABUNI       IF NO UNION SET                              
         BNZ   ILVL90                                                           
*        TM    TGUSXUNI,AFM        TEST IF MUSIC PAYMENT (EXCLUDE AFM)          
         GOTO1 UNITEST,DMCB,TGUSXUNS,AFM,0,0,0                                  
         BO    ILVL90                                                           
         MVC   TABUNI,=C'AFM'                                                   
*                                                                               
ILVL90   BAS   RE,CKPOSTL          CREATE TABLE OF POSTINGS                     
*                                                                               
***L95   MVC   0(TABLEN,R3),SVTAB  RESET TABLE ENTRY                            
ILVL95   LA    RE,SVTAB            RE=A(DATA TO MOVE FROM)                      
         LA    R0,0(R3)            R0=A(DATA TO MOVE TO)                        
         LHI   R1,TABLEN           R1 AND RF = LENGTH TO MOVE                   
         LR    RF,R1                                                            
         MVCL  R0,RE               RESET TABLE ENTRY IN 0(R3)                   
*                                                                               
ILVLX    B     XIT                                                              
         EJECT                                                                  
*        CHECK IF PAYMENT OPTION FEE                                            
*        IF SO ADD TO HANDLING                                                  
*                                                                               
CKFEE    NTR1                                                                   
         USING TAPAD,R4                                                         
         L     R4,AIO1                                                          
         MVI   ELCODE,TAPAELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TAPATFEE))                                     
         BNE   CFEEXIT                                                          
         L     R4,TGELEM                                                        
         ICM   RE,15,TAPADATA                                                   
         DROP  R4                                                               
*                                                                               
         TM    TBISTAT,TAPDSCAN    IF THIS IS A CANADIAN PAYMENT                
         BNO   CFEE10                                                           
         A     RE,TABHNDC          ADD THE FEE TO CANADIAN HANDLING             
         ST    RE,TABHNDC                                                       
         B     CFEEXIT                                                          
*                                                                               
CFEE10   CLI   GTYPE,TABRTY10      FOR TYPE 10'S                                
         BE    CFEE20                                                           
         CLI   GTYPE,TABRTY15      AND FOR TYPE 15'S                            
         BE    CFEE20                                                           
         CLI   GTYPE,TABRTY14      AND FOR TYPE 14'S                            
         BE    CFEE20                                                           
         TM    STAT2,STCORP        OR IF THERE IS A CORP ON INVOICE             
         BNO   CFEE30                                                           
CFEE20   A     RE,TABHNDC                                                       
         ST    RE,TABHNDC                                                       
         B     CFEEXIT             DONE                                         
*                                                                               
CFEE30   CLI   GTYPE,TABRTY1       IF BILLING TYPE 1                            
         BE    CFEE40                                                           
         CLI   GTYPE,TABRTY2       OR 2                                         
         BE    CFEE40                                                           
         CLI   GTYPE,TABRTY16      OR 16                                        
         BE    CFEE40                                                           
         CLI   GTYPE,TABRTY20      OR 20                                        
         BNE   CFEE50                                                           
CFEE40   CLC   SVCKNUM,=H'1'       AND MORE THAN 1 PERFORMER PAID               
         BNH   CFEE50                                                           
         A     RE,TABTAX           ADD IT TO THE TAX                            
         ST    RE,TABTAX                                                        
         B     CFEEXIT                                                          
*                                                                               
CFEE50   A     RE,TABHND                                                        
         ST    RE,TABHND           ELSE SET INDIVIDUAL HANDLING                 
CFEEXIT  B     XIT                                                              
         SPACE 2                                                                
         EJECT                                                                  
*        CHECK IF SIGNATORY FEE                                                 
*        IF SO ADD TO HANDLING                                                  
*                                                                               
CKSIGN   NTR1                                                                   
         OC    TASIGN,TASIGN                                                    
         BZ    XIT                                                              
*                                                                               
         TM    TBISTAT,TAPDSCAN    IF THIS IS A US$ PAYMENT                     
         BO    CSIGN10             ADD SIGNATORY FEE TO HANDLING                
         L     RE,TABHND                                                        
         A     RE,TASIGN                                                        
         ST    RE,TABHND                                                        
         B     XIT                                                              
*                                                                               
CSIGN10  L     RE,TABHNDC          IF THIS IS CAN$ PAYMENT                      
         A     RE,TASIGN           ADD SIGNATORY FEE TO CANADIAN                
         ST    RE,TABHNDC          HANDLING                                     
         B     XIT                                                              
*                                                                               
         EJECT                                                                  
*              ROUTINE SEES IF SAME CATEGORY OR UNION ON ALL CHECKS             
*                                  NTRY - BYTE SET C=CATEGORY,U=UNION           
*                                  XIT  - CC SET EQ=SAME,NEQ=NOT SAME           
         USING TABCHKD,R3                                                       
CMPCHKS  NTR1                                                                   
         LH    RE,SVCKNUM          RE=NUMBER OF CHECKS                          
         CH    RE,=H'1'            IF ONLY ONE CHECK                            
         BNH   YES                 IMPLIES SAME                                 
         BCTR  RE,0                                                             
         LA    R3,TABLEN(R3)                                                    
*                                                                               
CMPCHK5  CLI   BYTE,C'C'           IF CHECKING CATEGORY                         
         BNE   CMPCHK8                                                          
         CLC   TABONOFF,SVTAB+TABONOFF-TABCHKD  MATCH ON IT                     
         BNE   NO                                                               
         B     CMPCHK10                                                         
CMPCHK8  CLC   TABUNI,SVTAB+TABUNI-TABCHKD      ELSE MATCH ON UNION             
         BNE   NO                                                               
*                                                                               
CMPCHK10 LA    R3,TABLEN(R3)       BUMP TO NEXT CHECK ENTRY                     
         BCT   RE,CMPCHK5          LOOP                                         
         B     YES                                                              
         EJECT                                                                  
*                                                                               
*        GET OVERRIDE HANDLING FROM TAPA ELEMENT                                
*        IF THERE IS NO TAPA - TRY TABD (OLD WAY)                               
*        INPUT  - WORK = DATA TYPE                                              
*        RETURN - AMOUNT IN FULL                                                
*                                                                               
GETPAAMT NTR1                                                                   
         XC    FULL,FULL           CLEAR RETURN VALUE                           
         MVI   ELCODE,TAPAELQ      GET TAPA ELEMENT                             
         GOTO1 GETL,DMCB,(1,WORK)                                               
         BNE   GPA10                                                            
         USING TAPAD,R4                                                         
         L     R4,TGELEM                                                        
         MVC   FULL,TAPADATA       AND SET THE OVERIDE AMOUNT                   
         B     GPAX                                                             
*                                                                               
GPA10    L     R4,AIO                                                           
         USING TABDD,R4                                                         
         MVI   ELCODE,TABDELQ      GET TABD ELEMENT                             
         BAS   RE,GETEL                                                         
         BNE   GPAX                                                             
         CLI   WORK,TAPATHND       REQUESTING HANDLING                          
         BNE   GPA20                                                            
         MVC   FULL,TABDHND                                                     
         CLI   GTYPE,TABRTY15      IF BILLING TYPE 15 OR                        
         BE    GPA15                                                            
         CLI   GTYPE,TABRTY14      IF BILLING TYPE 14 OR                        
         BE    GPA15                                                            
         CLI   GTYPE,TABRTY10      IF BILLING TYPE 10                           
         BNE   GPAX                                                             
GPA15    MVC   FULL,TABDHNDC       OVERRIDE IS IN CORP HANDLING                 
*                                                                               
GPA20    CLI   WORK,TAPATTAX       REQUESTING TAX                               
         BNE   *+10                                                             
         MVC   FULL,TABDTAX                                                     
*                                                                               
         CLI   WORK,TAPATFIC       REQUESTING FICA                              
         BNE   *+10                                                             
         MVC   FULL,TABDFICR                                                    
*                                                                               
         CLI   WORK,TAPATGST       REQUESTING GST                               
         BNE   GPAX                                                             
         L     RF,TABDGST          GST+PST                                      
         A     RF,TABDPST                                                       
         ST    RF,FULL                                                          
*                                                                               
GPAX     B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
*                                                                               
*        ROUTINE TO CREATE TABLE OF POSTINGS BY WORK CODE                       
*                                                                               
         USING TABCHKD,R3                                                       
CKPOSTL  NTR1                      LOCAL ENTRY POINT                            
         B     CP10                                                             
         SPACE                                                                  
CKPOST   DS    0H                                                               
         L     R3,8(R1)            =A(CURRENT PERFORMER ENTRY IN TAB)           
CP10     TM    TBSTAT,TAINSCIN     IF INVOICE IS CANCELLED                      
         BO    CP30                                                             
         TM    TBOPTS,TBRERUN      OR IF RERUN                                  
         BO    CP30                                                             
         TM    TBSTA2,TAINSHLR     OR IF RELEASED                               
         BNO   CP40                                                             
         TM    TBSTA2,TAINSHLP        AND PRINTED                               
         BNO   CP40                                                             
*                                                                               
CP30     DS    0H                  NO-OP FOR CORRECT POSTS WHEN EXTRACT         
*******  CLI   TBPRERUN,C'Y'       AND TABLE HAS INVOICE AMOUNTS                
*******  BNE   CPX                                                              
*                                                                               
CP40     BAS   RE,SETTAIN          SET INTERFACE BLOCK FOR TAINTER              
         GOTO1 =V(TAINTER),DMCB,BLOCK                                           
*                                                                               
CPX      B     XIT                                                              
         EJECT                                                                  
*        ROUTINE TO SET UP INTERFACE BLOCK TO TAINTER                           
         SPACE 1                                                                
SETTAIN  NTR1                                                                   
         LA    R2,BLOCK                                                         
         USING TND,R2                                                           
         XC    0(TNLNQ,R2),0(R2)                                                
         ST    RC,TNRC             A(GEND)                                      
         LA    R1,SYSCOMM          A(SYSCOMM)                                   
         ST    R1,TNASYCOM                                                      
         L     R1,=V(POSTPOOL)                                                  
         ST    R1,TNAWCTAB                                                      
         LA    R1,IFAYR                                                         
         CLI   INTFACE,C'A'        USE AGENCY INTERFACE                         
         BE    TAIN10                                                           
         LA    R1,IFCLR            OR CLIENT OVERRIDE                           
*                                                                               
TAIN10   ST    R1,TNAIFEL          A(INTERFACE ELEMENT)                         
         CLI   RECNUM,PB           IF NOT PRINT BILLING RUN                     
         BE    TAIN15                                                           
         TM    TABSTAT2,TABSTGST   AND GST# ON W4 & UNION ACTRA                 
         BZ    TAIN12                                                           
         GOTO1 EXTRCT,DMCB,(RC)    AND EXTRACTING                               
         BE    *+14                                                             
         CLC   =C'ACT',TABUNI                                                   
         BNE   TAIN12                                                           
         OI    TNSTAT,TNSGSTU      TNGST IS US$ FOR ACTRA PYMTS                 
*                                                                               
TAIN12   TM    TABSTAT2,TABSTACT   AND IF CHECK FOR ACTRAWORKS                  
         BZ    TAIN15                                                           
         L     R1,TABPAYI          POST AMOUNT TO WORKCODE FOR                  
         A     R1,TABPAYC                                                       
         ST    R1,TNPNH            PENSION & HEALTH                             
         B     TAIN20                                                           
TAIN15   L     R1,TABPAYI                                                       
         A     R1,TABPAYC                                                       
         ST    R1,TNPAY            PAYMENT AMOUNT                               
         MVC   TNPNH,TABPNH        PENSION & HEALTH                             
*                                                                               
TAIN20   MVC   TNREXP,TABREXP      REIMBURSED EXPENSES                          
         MVC   TNTAX,TABTAX        TAXES                                        
         L     R1,TABHND                                                        
         A     R1,TABHNDC                                                       
         ST    R1,TNHAND           HANDLING                                     
         MVC   TNCOMM,TACOMM       AGENCY COMMISSION                            
         MVC   TNFICR,TABFICR      FICA CREDITS                                 
*                                                                               
         L     R1,TABGST           GST                                          
         TM    STAT3,STSPTAOS      IF AOS ON SPLITS, PST ALREADY ADDED          
         BO    TAIN23                                                           
         LHI   RE,TBPSTAOS-BILLD                                                
         AR    RE,RA                                                            
         ICM   RF,15,0(RE)         ADD PST AOS US$                              
         AR    R1,RF                                                            
TAIN23   ST    R1,TNGST                                                         
*                                                                               
         OC    TABSSN,TABSSN       SECOND TIME AROUND                           
         BZ    TAIN35              SKIP THIS                                    
         TM    STAT2,STCSF         PAYING CSF?                                  
         BZ    TAIN25                                                           
TAIN24   LA    R1,TABLEN(R3)                                                    
***      OC    0(TABLEN,R1),0(R1)  END OF TABLE?                                
         OC    0(TABDSK,R1),0(R1)  END OF TABLE?                                
         BNZ   TAIN35                                                           
         LHI   R1,CSFRATE          POST CSF RATE ONE TIME                       
         GOTO1 =A(CSFMULT)                                                      
         ST    R1,TNCSF                                                         
         B     TAIN35                                                           
*                                                                               
TAIN25   TM    STAT2,STCSFC        CANCELLING CSF?                              
         BNO   TAIN30                                                           
         LA    R1,TABLEN(R3)                                                    
***      OC    0(TABLEN,R1),0(R1)  END OF TABLE?                                
         OC    0(TABDSK,R1),0(R1)  END OF TABLE?                                
         BNZ   TAIN35                                                           
         SR    R1,R1                                                            
         SHI   R1,CSFRATE          POST NEGATIVE CSF RATE ONE TIME              
         GOTO1 =A(CSFMULT)                                                      
         ST    R1,TNCSF                                                         
         NI    STAT2,X'FF'-STCSFC  TURN OFF SO IT ONLY POSTS ONCE               
         B     TAIN35                                                           
*                                                                               
TAIN30   TM    STAT3,STCSFCR       CREDITING CSF?                               
         BNO   TAIN35                                                           
         LA    R1,TABLEN(R3)                                                    
***      OC    0(TABLEN,R1),0(R1)  END OF TABLE?                                
         OC    0(TABDSK,R1),0(R1)  END OF TABLE?                                
         BNZ   TAIN35                                                           
         SR    R1,R1                                                            
         SHI   R1,CSFRATE          POST NEGATIVE CSF RATE ONE TIME              
         GOTO1 =A(CSFMULT)                                                      
         ST    R1,TNCSF                                                         
*                                                                               
TAIN35   MVC   TNINV,TGINV         SET INVOICE NUMBER                           
         XC    TGUNEQU,TGUNEQU     CLEAR GLOBAL FIELD                           
         XC    TGUNEQUS,TGUNEQUS                                                
         GOTO1 UNIVAL,DMCB,TABUNI                                               
         MVC   TNCASTA2,TABCAST2   CAST STATUS BYTE 2 (CELEBRITY)               
         MVC   TGONOF,TABONOFF                                                  
         CLC   TGONOF,SPACES       IF ON/OFF CAMERA NOT SET                     
         BH    CPXIT                                                            
         MVC   TGONOF,=C'OFF'      SET IT TO OFF                                
CPXIT    B     XIT                                                              
         DROP  R3                                                               
         EJECT                                                                  
*                                                                               
*              ROUTINES TO MAKE SJ AND SV POSTINGS                              
*                                                                               
POSTSJSV DS    0H                                                               
         XR    R5,R5                                                            
         TM    TBSTA2,TAINSPRM                                                  
         BNO   POSJ8                                                            
         IC    R5,TBSICNT                                                       
         L     R6,=V(SITAB)                                                     
         USING SITABD,R6                                                        
*                                                                               
POSJ5    MVC   TBEST,SITBEST                                                    
         MVC   TBTOT,SITBTOT                                                    
         MVC   IFCLR,SITBIFC                                                    
         GOTO1 TINVCON,DMCB,SITBINV,TBINV,DATCON                                
POSJ8    BAS   RE,FIXPROD          ADJUST CANADIAN ROUNDING                     
         BAS   RE,SETNARR          SET NARRATIVE                                
         MVI   SWPOST,POSTPROD                                                  
         MVI   DRORCR,C'D'                                                      
         MVC   ACC(1),TGACCHX      HEX COMP                                     
         MVC   ACC+1(2),=C'SJ'     DEBIT TO SJ                                  
         MVC   ACC+3(12),TBEST     JOB NAME                                     
         BAS   RE,GETVEND          GET PROD. INTERFACE DATA FROM FILE           
         MVC   SUBAC(1),TGACCHX    COMPANY HEXCOMP                              
         MVC   SUBWORK,SPACES                                                   
         MVC   SUBNAME,SPACES                                                   
         MVC   SUBNAME(6),TGAGY                                                 
         MVC   SUBNAME+6(1),TBMED  PASS MEDIA TO PRODUCTION REPORT              
         XC    MEMO,MEMO           CLEAR MEMO AREA                              
         BAS   RE,SETFFT           SEE IF FFTEL ELEMENT NEEDED                  
*                                                                               
         L     R2,=V(POSTPOOL)                                                  
         USING WCD,R2                                                           
POSJ10   OC    0(WCLNQ2,R2),0(R2)                                               
         BZ    POSJ50                                                           
         CLI   TBSICNT,0           IF INVOICE NUMBER PRESENT                    
         BE    *+14                                                             
         CLC   WCINV,SITBINV       MATCH ON IT                                  
         BNE   POSJ40                                                           
*                                                                               
         MVC   SUBWORK,WCCODE                                                   
         MVC   AMOUNT,WCAMT                                                     
*                                                                               
         USING TAIFD,R4                                                         
         LA    R4,IFAYR                                                         
         CLI   INTFACE,C'A'                                                     
         BE    POSJ20                                                           
         LA    R4,IFCLR                                                         
POSJ20   TM    TAIFSTAT,TAIFSPZR   POST EVEN IF AMOUNT = ZERO                   
         BO    *+14                                                             
         OC    AMOUNT,AMOUNT                                                    
         BZ    POSJ40                                                           
         CLI   INTXCLI,C'Y'        IF CLI EXCLUDED FROM INTERFACE               
         BE    POSJ40                 SKIP POSTINGS                             
         BAS   RE,SETPAK           SET PAYABLE DATA ELEMENT                     
         BAS   RE,POSTIT                                                        
         XC    MEMO,MEMO           CLEAR AREA IN CASE FFT CREATED               
*                                                                               
POSJ40   MVC   SUBWORK,SPACES                                                   
         LA    R2,WCNEXT2          TABLE IS BIGGER                              
         B     POSJ10                                                           
*                                                                               
POSJ50   MVI   NARRLEN,0           CLEAR-NARRATIVE ONLY FOR SJ POSTINGS         
*                                                                               
         CLI   INTFACE,C'N'        IF ON INTERFACE                              
         BE    *+8                                                              
         BAS   RE,POSTSV           POST TO SV                                   
*                                                                               
         CLI   TBSICNT,0                                                        
         BE    POSJ60                                                           
         LA    R6,SITBLEN(R6)      NEXT ENTRY                                   
         BCT   R5,POSJ5            LOOP                                         
*                                                                               
POSJ60   L     R2,=V(POSTPOOL)                                                  
         L     R3,=V(POSTPX)                                                    
         BAS   RE,CLRAREA          CLEAR POSTPOOL                               
         B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*                                                                               
*        IF TOTAL CREDITS DIFFER FROM DEBITS BY LESS THAN A FEW CENTS           
*        ADJUST 1ST TABLE ENTRY TO ACCOUNT FOR ROUNDING PROBLEM                 
*                                                                               
FIXPROD  NTR1                                                                   
         TM    TBISTAT,TAPDSCAN    ONLY FOR CANADIAN PAYMENT                    
         BNO   FPX                                                              
         L     R2,=V(POSTPOOL)                                                  
         USING WCD,R2                                                           
         LR    R4,R2               FIRST TABLE ENTRY(SINGLE INVOICE)            
         MVI   BYTE,C'F'           SET FIRST ENTRY(MULT INV TABLE)              
         SR    R1,R1               TOTAL POSTING AMOUNT                         
*                                                                               
FP10     OC    0(WCLNQ2,R2),0(R2)                                               
         BZ    FP20                                                             
         CLI   TBSICNT,0                                                        
         BE    FP12                                                             
         CLC   WCINV,SITBINV       MATCH ON CURRENT INVOICE                     
         BNE   FP15                                                             
         CLI   BYTE,C'F'                                                        
         BNE   FP12                                                             
         LR    R4,R2               SAVE 1ST TABLE ENTRY                         
         MVI   BYTE,C'X'                                                        
*                                                                               
FP12     A     R1,WCAMT            ADD IN THIS WC POSTING AMOUNT                
*                                                                               
FP15     LA    R2,WCNEXT2          TABLE IS BIGGER                              
         B     FP10                                                             
*                                                                               
FP20     S     R1,TBTOT            SUB FROM TABLE TOTAL, CREDIT AMOUNT          
         LPR   R3,R1                                                            
         CH    R3,=H'10'           IF DIFFERENCE  =< A FEW CENTS                
         BH    FPX                                                              
         LR    R2,R4               ADJUST 1ST TABLE ENTRY                       
         L     R3,WCAMT            TO ACCOUNT                                   
         SR    R3,R1                                                            
         ST    R3,WCAMT            FOR CONVERSION ROUNDING                      
*                                                                               
FPX      B     XIT                                                              
         DROP  R2,R6                                                            
         EJECT                                                                  
*              ROUTINE SETS NARRATIVE FOR SJ POSTINGS                           
*                                                                               
         USING TAIFD,R4                                                         
SETNARR  NTR1                                                                   
         LA    R4,IFAYR                                                         
         CLI   INTFACE,C'A'                                                     
         BE    *+8                                                              
         LA    R4,IFCLR                                                         
         TM    TAIFSTAT,TAIFSNAR   TEST WANT NARRATIVE                          
         BZ    XIT                                                              
*                                                                               
         USING NARRD,R2                                                         
         LH    R2,=Y(NARR-BILLD)                                                
         AR    R2,RA                                                            
         MVI   NARRD,C' '           BUILD NARRATIVE IN NARR                     
         MVC   NARRD+1(NARLNQ-1),NARRD                                          
         MVC   NARCID,TGCID                                                     
         MVI   NARQUOT1,C'"'       PUT QUOTES AROUND COMM'L NAME                
         MVC   NARCONME,TBCONAME                                                
         LA    R1,NARCONME+L'NARCONME-1  START FROM END OF NAME                 
         CLI   0(R1),C' '                FIND FIRST NON-BLANK                   
         BNE   *+8                                                              
         BCT   R1,*-8                                                           
         LA    R1,1(R1)                                                         
         MVI   0(R1),C'"'                                                       
         MVC   NARUSE,TGUSNAME                                                  
         LA    R1,NARDET1                                                       
         TM    TGUSTYST,UPGRADE    IF UPGRADE                                   
         BZ    *+14                                                             
         MVC   0(4,R1),=C'FROM'    SAY "FROM" BEFORE ORIG USE DETAILS           
         LA    R1,5(R1)                                                         
         LH    RE,=Y(USEDET1-BILLD)   POINT RE TO USEDET1                       
         AR    RE,RA                                                            
         MVC   0(L'USEDET1,R1),0(RE)                                            
         LA    RE,L'USEDET1(RE)       POINT RE TO USEDET2                       
         OC    0(L'USEDET2,RE),0(RE)  TEST HAVE 2ND LINE OF USE DETAILS         
         BZ    SETN10                                                           
         LA    R1,NARDET2                                                       
         TM    TGUSTYST,USES       IF HAVE USES                                 
         BZ    SETN5                                                            
         MVC   0(4,R1),=C'LIFT'    SAY "LIFT" BEFORE LIFT USES                  
         LA    R1,5(R1)                                                         
         B     SETN7                                                            
SETN5    TM    TGUSTYST,UPGRADE    IF UPGRADE                                   
         BZ    SETN7                                                            
         MVC   0(2,R1),=C'TO'      SAY "TO" BEFORE UPGRADE DETAILS              
         LA    R1,3(R1)                                                         
SETN7    MVC   0(L'USEDET2,R1),0(RE)                                            
*                                                                               
SETN10   OC    TBCDTS,TBCDTS       TEST HAVE CYCLE START/END DATES              
         BZ    SETN15                                                           
         GOTO1 DATCON,DMCB,(X'11',TBCDTS),(5,NARCYC)                            
         SPACE                                                                  
SETN15   TM    STAT2,STAPPLS+STAPPLH  TEST APPLIED SESS OR HF CREDITS           
         BZ    SETNX                                                            
         MVC   NARLESS,=C'LESS'                                                 
         TM    STAT2,STAPPLS                                                    
         BZ    *+10                                                             
         MVC   NARSESS,=C'SESSION'                                              
         TM    STAT2,STAPPLH                                                    
         BZ    *+10                                                             
         MVC   NARHOLD,=C'HOLDING FEE'                                          
         TM    STAT2,STAPPLH+STAPPLS                                            
         BNO   *+10                                                             
         MVC   NARAND,=C'AND'                                                   
         SPACE                                                                  
SETNX    GOTO1 SQUASHER,DMCB,NARRD,NARLNQ                                       
         MVC   NARRLEN,7(R1)       GET L'INPUT AFTER SQUASHING                  
         OC    NARRD(132),SPACES   MAKE SURE ALL UPPER CASE                     
         OC    NARRD+132(L'NARR-132),SPACES                                     
         B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*                                                                               
*        ADD FFTEL IF CLOSING THE PO                                            
*                                                                               
SETFFT   NTR1                                                                   
         XC    MEMO,MEMO           CLEAR AREA                                   
         TM    TBISTA2,TAPDSCPO    OK TO CLOSE PO?                              
         BNO   SETFX               NO, EXIT                                     
         LA    R2,MEMO                                                          
         USING FFTELD,R2                                                        
         MVI   FFTEL,FFTELQ                                                     
         MVI   FFTTYPE,FFTTORDC    INDICTATE TO CLOSE ORDER                     
*                                                                               
         LA    RE,L'ORDKORD                                                     
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   FFTDATA(0),TBPO     MOVE IN PO NUMBER                            
*                                                                               
         LA    RE,1(RE)                                                         
         STC   RE,FFTDLEN          SET DATA LENGTH                              
         AH    RE,=Y(FFTDATA-FFTELD)                                            
         STC   RE,FFTLN            SET ELEMENT LENGTH                           
*                                                                               
SETFX    B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*                                                                               
*        ADD PAYABLE ELEMENT IF SV, SX, SW OR SY                                
*                                                                               
SETPAK   NTR1                                                                   
         USING FFTELD,R2                                                        
         LA    R2,MEMO                                                          
         CLI   0(R2),0             ANY FFTEL?                                   
         BE    SETPAK2             NO                                           
*                                                                               
         SR    RE,RE               YES, BUMP PAST IT                            
         IC    RE,FFTLN                                                         
         AR    R2,RE                                                            
         DROP  R2                                                               
*                                                                               
         USING PAKELD,R2                                                        
SETPAK2  MVI   PAKEL,PAKELQ                                                     
         MVI   PAKLN,PAKLNQ                                                     
         MVC   PAKACC,SUBAC                                                     
         MVC   PAKOFF,SPACES                                                    
         MVC   PAKCON,SPACES                                                    
         MVC   PAKCON(6),ACC                                                    
         MVC   PAKDATE,TBILLDTE                                                 
         MVC   PAKREF,TBINV                                                     
*                                                                               
SETPX    B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*              ROUTINE TO MAKE SV POSTINGS                                      
*                                                                               
POSTSV   NTR1                      LOCAL ROUTINE                                
         MVC   SVACC,ACC                                                        
         MVC   ACC,SUBAC           POST TO PRODUCTION VENDOR                    
         MVC   SUBAC,SPACES                                                     
         MVC   SUBAC(6),SVACC                                                   
         MVC   SUBNAME,SPACES                                                   
         MVC   SUBNAME(12),TBCLNAME                                             
*                                                                               
         XC    MEMO,MEMO                                                        
         LA    R2,MEMO             TACK ON PRODUCT AND ESTIMATE                 
         USING ACOTHERD,R2                                                      
         MVC   ACOTEL(15),SPACES                                                
         MVC   ACOTEL(2),=X'230F'                                               
         MVC   ACOTNUM(3),TBEST+3     PRODUCT                                   
         MVC   ACOTNUM+6(6),TBEST+6   ESTIMATE                                  
         MVC   ACOTNUM+12(1),TBMED    & MEDIA                                   
         MVI   DRORCR,C'C'                                                      
         MVC   AMOUNT,TBTOT           INVOICE TOTAL                             
         TM    TAIFSTAT,TAIFSPZR      POST EVEN IF AMOUNT = ZERO                
         BO    POSV60                                                           
         OC    AMOUNT,AMOUNT                                                    
         BZ    POSV70                                                           
*                                                                               
POSV60   CLI   INTXCLI,C'Y'        IF CLI EXCLUDED FROM INTERFACE               
         BE    POSV70                 SKIP POSTINGS                             
         BAS   RE,POSTIT                                                        
*                                                                               
POSV70   MVI   MEMO,0                                                           
         B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
CLRAREA  NTR1                                                                   
         SR    R3,R2               SIZE OF AREA                                 
         XR    RE,RE                                                            
         LR    RF,RE                                                            
         MVCL  R2,RE                                                            
         B     XIT                                                              
         SPACE 3                                                                
*                                                                               
*              ROUTINE TO GET INTERFACE DATA FROM FILE                          
*                                                                               
         USING TAIFD,R4                                                         
GETVEND  NTR1                                                                   
         MVC   SUBAC,SPACES                                                     
         LA    R4,IFAYR            DEFAULT TO AGENCY INTERFACE                  
         CLI   INTFACE,C'A'                                                     
         BE    GETV10                                                           
         LA    R4,IFCLR                                                         
*                                                                               
GETV10   MVC   SUBAC+1(L'TAIFVEND),TAIFVEND   INITIALIZE C/A (VENDOR)           
         B     XIT                                                              
         EJECT                                                                  
*                                                                               
*              ROUTINE TO COMPLETE POSTINGS                                     
*                                                                               
POSTIT   NTR1                                                                   
         L     R2,=V(AREA)                                                      
         USING PSHEADD,R2                                                       
         MVI   PSHDEL,X'50'        HEAD ELEMENT                                 
         MVI   PSHDLEN,70                                                       
         MVC   PSHDACC,ACC                                                      
         OC    PSHDACC+1(L'PSHDACC-1),SPACES                                    
         MVC   PSHDANAL,SUBWORK                                                 
         MVC   PSHDSBAC,SUBAC                                                   
         OC    PSHDSBAC+1(L'PSHDSBAC-1),SPACES                                  
         MVC   PSHDSBNM,SUBNAME                                                 
*                                                                               
         LA    R2,70(R2)                                                        
         USING TRANSD,R2                                                        
         MVI   TRNSEL,TRNSELQ                                                   
         ZIC   R1,NARRLEN                                                       
         LA    R1,TRNSLNQ(R1)                                                   
         STC   R1,TRNSLEN                                                       
         MVC   TRNSDATE,TBILLDTE                                                
         MVI   TRNSSBRF,0                                                       
         MVI   TRNSTYPE,50                                                      
         MVC   TRNSREF,TBINV       INV # = REFERENCE NUMBER                     
         CLI   SWPOST,POSTPROD     PRODUCTION POSTINGS                          
         BE    POST20                                                           
         MVI   TRNSTYPE,9                                                       
         CLI   SWPOST,POSTRCV                                                   
         BE    POST20                                                           
         MVC   TRNSREF(2),=C'BI'   EXCEPT FOR SE & SX POSTINGS                  
         MVC   TRNSREF+2(2),TGTODAY0+2      REF = BI(MMDD)                      
         MVC   TRNSREF+4(2),TGTODAY0+4                                          
*                                                                               
POST20   DS    0H                                                               
         MVI   TRNSSTAT,0                                                       
         CLI   DRORCR,C'C'                                                      
         BE    *+8                                                              
         MVI   TRNSSTAT,X'80'                                                   
         MVC   TRNSBTCH,SPACES                                                  
         MVC   TRNSBTCH(2),MNTH                                                 
*                                                                               
         CLI   SWPOST,POSTPROD            PROD POSTINGS                         
         BNE   POST25                                                           
*                                                                               
         BAS   RE,GETMONTH     GET POSTING MONTH FROM STATUS ON INT REC         
         BE    POST25                                                           
*                                                                               
         CLC   TGAGY,=CL6'DTNYT'          DEUTSCH AGENCIES ONLY                 
         BE    POST23                                                           
         CLC   TGAGY,=CL6'DTLAT'                                                
         BE    POST23                                                           
         CLC   TGAGY,=CL6'DTCA'                                                 
         BE    POST23                                                           
         CLC   TGAGY,=CL6'0072'           AND LMNYA                             
         BNE   POST24                                                           
*                                                                               
POST23   CLC   TGTODAY1+1(1),TGNXT2DY+1   IF MONTHS ARE DIFFERENT               
         BE    POST25                                                           
         GOTO1 DATCON,DMCB,(1,TGNXT2DY),(0,WORK)                                
         MVC   TRNSBTCH(1),WORK+1                                               
         MVC   TRNSBTCH+1(1),WORK+3       MAKE MONTHS 10-12 TO A-C              
         CLI   WORK+2,C'1'                                                      
         BNE   POST25                                                           
         SR    R1,R1                                                            
         IC    R1,WORK+3                                                        
         AHI   R1,1                                                             
         STC   R1,TRNSBTCH+1                                                    
         NI    TRNSBTCH+1,X'FF'-X'30'                                           
         B     POST25                                                           
*                                                                               
POST24   BAS   RE,GETJWT                  GET JWT AGENCIES                      
*                                                                               
POST25   L     R0,AMOUNT                                                        
         CVD   R0,DUB                                                           
         ZAP   TRNSAMNT,DUB                                                     
         MVC   TRNSANAL,SPACES                                                  
*                                                                               
         CLI   SWPOST,POSTPROD     IF SR/SE/SX POSTINGS                         
         BE    POST30                                                           
         CLC   ACC+1(4),=C'SXPW'   BUT NOT SXPW                                 
         BE    POST28                                                           
         MVC   TRNSANAL(L'TBTPO),TBTPO  TRNSANAL GETS TP OFFICE CODE            
         B     POST30                                                           
POST28   MVI   TRNSANAL,C'H'       FOR SXPW, TRNSANAL GETS OFFICE H             
         CLI   RECNUM,PB           IF PRINT BILLING                             
         BE    POST30                                                           
         MVI   TRNSANAL,C'8'       ELSE TRNSANAL GETS OFFICE 8                  
*                                                                               
POST30   CLC   SUBWORK,SPACES                                                   
         BE    *+10                                                             
         MVC   TRNSANAL,SUBWORK                                                 
         OC    TRNSANAL,SPACES                                                  
         USING NARRD,R1                                                         
         LH    R1,=Y(NARR-BILLD)                                                
         AR    R1,RA                                                            
         ZIC   RE,NARRLEN                                                       
         LTR   RE,RE                                                            
         BZ    POST40                                                           
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   TRNSNARR(0),NARRD   MOVE IN VARIABLE NARRATIVE                   
*                                                                               
POST40   CLI   SWPOST,POSTSUMM     IF SUMMARY POSTING                           
         BE    POST42              INCREMENT COUNT                              
         CLI   TBNPOST,C'Y'        IF THIS IS NOT A POSTING INVOICE             
         BE    POST50                                                           
         TM    STATUS,STCOD        OR IF THIS IS A COD - THEN                   
         BO    POST50                 DON'T ADD IT TO COUNTER                   
         CLI   SWPOST,POSTRCV      INCREMENT RECEIVABLE POSTING AMOUNTS         
         BNE   POST45                                                           
*                                                                               
POST42   AP    TALRECS,=P'1'                                                    
         CLI   DRORCR,C'D'                                                      
         BNE   POST43                                                           
         AP    TALCASH,TRNSAMNT                                                 
         AF    CTRCVBL,AMOUNT      RECEIVABLE TOTALS                            
         B     POST50                                                           
*                                                                               
POST43   AP    TALCRDTS,TRNSAMNT                                                
         B     POST50                                                           
*                                                                               
POST45   CLI   SWPOST,POSTPROD                                                  
         BNE   POST50                                                           
         AP    PRODRECS,=P'1'                                                   
         CLI   DRORCR,C'D'                                                      
         BNE   POST50                                                           
         AP    PRODCASH,TRNSAMNT                                                
         DROP  R2                                                               
*                                                                               
POST50   ZIC   R1,1(R2)            BUMP TO NEXT SLOT                            
         AR    R2,R1                                                            
         CLI   SWPOST,POSTSUMM                                                  
         BE    POST70                                                           
         USING ACTAD,R2                                                         
         XC    ACTAEL(ACTALNQ),ACTAEL                                           
         MVI   ACTAEL,ACTAELQ      ELCODE                                       
         MVI   ACTALEN,ACTALNQ     LENGTH                                       
         MVC   ACTAAGY,TGAGY       AGENCY                                       
         MVC   ACTACLI,TGCLI       CLIENT                                       
         MVC   ACTAPRD,TBPRD       PRODUCT                                      
         MVC   ACTACID,TGCID       COMMERCIAL ID                                
         MVC   ACTAEST,TBEST       ESTIMATE NUMBER                              
         LA    R2,ACTALNQ(R2)      BUMP TO NEXT SLOT                            
*                                                                               
         CLI   SWPOST,POSTPROD                                                  
         BNE   POST70                                                           
         CLC   ACC+1(2),=C'SJ'     IF SV POSTING                                
         BE    POST70                                                           
         CLI   INTFACE,C'N'        & THIS AGY'S ON THE INTERFACE                
         BE    POST70                                                           
         USING TAIFD,R4            THEN GET INTERFACE INFO                      
         LA    R4,IFAYR            AGENCY INTERFACE                             
         CLI   INTFACE,C'A'                                                     
         BE    POST60                                                           
         LA    R4,IFCLR            CLIENT OVERRIDE                              
*                                                                               
POST60   TM    TAIFSTAT,TAIFSUSV   UPDATE SV POSTING                            
         BNO   POST70                                                           
         USING TRCPJD,R2                                                        
         XC    TRCPEL(23),TRCPEL                                                
         MVC   TRCPEL(2),=X'4F17'  ELCODE+LENGTH                                
         LA    R2,23(R2)           BUMP TO NEXT SLOT                            
*                                                                               
POST70   MVI   0(R2),0             MARK E-O-R                                   
         CLI   MEMO,0                                                           
         BE    POST80                                                           
         MVC   0(L'MEMO,R2),MEMO                                                
*                                                                               
POST80   CLI   SWPOST,POSTPROD     IF THIS IS A PRODUCTION POSTING              
         BNE   POST90                                                           
         TM    TBOPTS,TBRCVBL      AND THIS RUN IS TO POST ONLY RCVBL           
         BO    POSTX                                                            
         B     POST100                                                          
*                                                                               
POST90   TM    TBOPTS,TBPRWK       THIS MUST BE RECEIVABLE POSTING              
         BO    POSTX               IF POSTING ONLY PRODUCTION - DON'T           
*                                  POST RECEIVABLE                              
POST100  CLI   SWPOST,POSTSUMM     ALWAYS DO SE POSTINGS                        
         BE    POST110                                                          
         CLI   TBNPOST,C'Y'        IF THIS IS NOT A POSTING INVOICE             
         BE    POSTX                                                            
         TM    STATUS,STCOD        OR IF THIS IS A COD INVOICE                  
         BO    POSTX               THEN DON'T DO ANY POSTINGS                   
*                                                                               
POST110  MVC   COMMAND,=CL6'ADD'                                                
         BAS   RE,WKFILE                                                        
*                                                                               
POSTX    B     XIT                                                              
         EJECT                                                                  
*=====================================================================          
*        IF CALCULATING HANDLING THE NEW WAY,                                   
*        STORE INDIVIDUAL HANDLING OVERRIDE IN FULL                             
*=====================================================================          
SETHANDI NTR1                                                                   
         XC    FULL,FULL                                                        
         USING TMD,R3                                                           
         L     R3,ATALIM                                                        
         L     RE,TMACURPG         GET TAPG ELEMENT                             
         LTR   RE,RE               IF NEW POSTING ELEMENT EXISTS,               
         BZ    SHNDX                                                            
         TM    BOVRSTAT,BOVRHNDB+BOVRHNDP  BASIC OR PREM HAND OVERRIDE?         
         BZ    SHNDX                                                            
                                                                                
         USING TARAD,R4                                                         
         L     R4,AIO1                                                          
         MVI   ELCODE,TARAELQ      GET BOVER ON INVOICE                         
         BRAS  RE,GETEL                                                         
         BNE   SHNDX                                                            
                                                                                
         TM    TBISTAT,TAPDSCAN    CANADIAN PAYMENT?                            
         BZ    SHDI05                                                           
         TM    BOVRSTAT,BOVRHNDC   CANADIAN HAND OVERRIDE?                      
         BZ    SHDI10                                                           
         MVC   FULL,TARAHFBC       CANADIAN HANDLING IN DOLLARS                 
         B     SHDI10                                                           
                                                                                
SHDI05   TM    BOVRSTAT,BOVRHNDB   BASIC HAND OVERRIDE?                         
         BZ    SHDI10                                                           
         MVC   FULL,TARAHFB        BASIC HANDLING IN DOLLARS                    
                                                                                
SHDI10   TM    BOVRSTAT,BOVRHNDP   PREM HAND OVERRIDE?                          
         BZ    SHNDX                                                            
         L     R1,FULL                                                          
         ICM   R2,15,TARAHFP       PREM HANDLING IN DOLLARS                     
         AR    R1,R2                                                            
         ST    R1,FULL             IND HAND + PREM HANDLING OVERRIDE            
                                                                                
SHNDX    XIT1                                                                   
         DROP  R3,R4                                                            
         EJECT                                                                  
*                                                                               
*        JWT AGENCIES - CHANGE MOA TO BE THE SAME AS ACTIVITY DATE              
*        USE MONTH FROM TOMORROW'S DATE IN TRNSBTCH                             
*        ALSO Y&R AGENCIES AS OF 03/05                                          
*        R2 --> TRANSACTION ELEMENT                                             
*                                                                               
         USING TRANSD,R2                                                        
GETJWT   NTR1                                                                   
         LA    R4,JWTTAB         JWT AGENCY TABLE                               
GJWT10   CLI   0(R4),X'FF'       IF AGENCY NOT IN TABLE, EXIT                   
         BE    GJWTX                                                            
         CLC   TGAGY,0(R4)                                                      
         BE    GJWT20                                                           
         LA    R4,L'JWTTAB(R4)                                                  
         B     GJWT10                                                           
*                                                                               
GJWT20   GOTO1 ADDAY,DMCB,TGTODAY0,WORK,1  GET NEXT DAY                         
         CLC   TGTODAY0+2(2),WORK+2        IF MONTHS ARE DIFFERENT,             
         BE    GJWTX                                                            
         MVC   TRNSBTCH(1),WORK+1          TOMORROW'S YEAR                      
         MVC   TRNSBTCH+1(1),WORK+3        MAKE MONTHS 10-12 TO A-C             
         CLI   WORK+2,C'1'                                                      
         BNE   GJWTX                                                            
         SR    R1,R1                                                            
         IC    R1,WORK+3                                                        
         AHI   R1,1                                                             
         STC   R1,TRNSBTCH+1                                                    
         NI    TRNSBTCH+1,X'FF'-X'30'                                           
GJWTX    B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*                                                                               
*        CALCULATE POSTING MONTH FROM NUMBER OF NEXT BUSINESS DAYS              
*        FOUND IN INTERFACE RECORD.  STORE MONTH IN TRNSBTCH.                   
*        RETURNS CC EQUAL IF INTERFACE HAS BDAYS STATUS                         
*        R2 --> TRANSACTION ELEMENT                                             
*                                                                               
         USING TRANSD,R2                                                        
GETMONTH NTR1                                                                   
         USING TAIFD,R4                                                         
         LA    R4,IFAYR            AGENCY INTERFACE                             
         CLI   TAIFBDAY,0          ANY END OF MONTH OVERRIDE?                   
         BNH   NO                  NO, EXIT                                     
         ZIC   R0,TAIFBDAY         # OF DAYS PRIOR TO END OF MONTH              
         DROP  R4                                                               
*                                                                               
         SHI   R0,1                                                             
         CHI   R0,0                USE NEXT BUSINESS DAY                        
         BH    GMONTH10                                                         
         MVC   TGDUB,TGNXTBUS      PACKED DATE                                  
         B     GMONTH50                                                         
*                                                                               
GMONTH10 SHI   R0,1                                                             
         MVC   TGDUB,TGNXT2DY      USE NEXT BUSINESS DAY+1 (PACKED)             
         CHI   R0,0                                                             
         BNH   GMONTH50                                                         
*                                                                               
*                                  GET NEXT BUSINESS DAY                        
GMONTH20 LA    R1,DMCB                                                          
         LA    RE,TGDUB            START WITH NEXT BUSINESS DAY+1               
         ST    RE,0(R1)                                                         
         MVI   0(R1),1                                                          
         BAS   RE,GTNXTBUS         GET NEXT BUSINESS DAY                        
         LA    R3,WORK             R3=A(GETRET BLOCK)                           
         USING GETRETD,R3                                                       
         GOTO1 DATCON,DMCB,(3,GRDODY),(1,TGDUB)                                 
         DROP  R3                                                               
         BCT   R0,GMONTH20         GET NEXT BUSINESS DAY AGAIN                  
*                                                                               
GMONTH50 CLC   TGTODAY1+1(1),TGDUB+1     IF MONTHS ARE DIFFERENT                
         BE    YES                                                              
         XC    WORK,WORK                                                        
         GOTO1 DATCON,DMCB,(1,TGDUB),(0,WORK)                                   
         MVC   TRNSBTCH(1),WORK+1                                               
         MVC   TRNSBTCH+1(1),WORK+3       MAKE MONTHS 10-12 TO A-C              
         CLI   WORK+2,C'1'                                                      
         BNE   YES                                                              
         SR    R1,R1                                                            
         IC    R1,WORK+3                                                        
         AHI   R1,1                                                             
         STC   R1,TRNSBTCH+1                                                    
         NI    TRNSBTCH+1,X'FF'-X'30'                                           
         B     YES                                                              
         DROP  R2                                                               
*=====================================================================          
*              ROUTINE CALCULATES NEXT BUSINESS DAY                             
*                 DMCB(0) HOLD DATE TO FIND NEXT BUSINESS DAY FROM              
*=====================================================================          
GTNXTBUS NTR1                                                                   
         XC    WORK,WORK           SET TO ADD 1 BUSINESS DAY TO TODAY           
         LA    R3,WORK             R3=A(GETRET BLOCK)                           
         USING GETRETD,R3                                                       
         GOTO1 DATCON,DMCB,,(3,GRDIDY)                                          
         MVC   GRDHRS,=H'24'                        + 24 HOURS                  
         SPACE 1                                                                
         L     RF,ACOMFACS         USE GETRET TO ADD BUSINESS DAYS              
         USING COMFACSD,RF                                                      
         OI    GRDFLAG,GRDFTAL                                                  
         GOTO1 CGETRET,(R3)        CALCULATE NEXT BUSINESS DAY                  
         B     XIT                                                              
         DROP  R3                                                               
         EJECT                                                                  
*                                                                               
*              WORKER INTERFACE                                                 
*                                                                               
WKFILE   NTR1                                                                   
         CLI   SWPOST,POSTPROD     IF THIS IS A PRODUCTION POSTING              
         BNE   WK10                                                             
         TM    TBOPTS,TBRCVBL      AND THIS RUN IS TO POST ONLY RCVBL           
         BO    WKX                     THEN EXIT                                
         B     WK20                                                             
*                                                                               
WK10     TM    TBOPTS,TBPRWK       THIS MUST BE RECEIVABLE POSTING              
         BO    WKX                 IF POSTING ONLY PRODUCTION - DON'T           
*                                  POST RECEIVABLE                              
WK20     CLI   COMMAND,C'A'        SKIP IF NOT ADD                              
         BNE   WK50                                                             
         L     R2,=V(AREA)                                                      
         SR    R3,R3                                                            
*                                                                               
WK30     CLI   0(R2),0             FIND EOR                                     
         BE    WK40                                                             
         IC    R3,1(R2)                                                         
         AR    R2,R3                                                            
         B     WK30                                                             
*                                                                               
WK40     L     R6,=V(AREA)         COMPUTE L'RECORD + 4                         
         SH    R6,=H'4'                                                         
         XC    0(4,R6),0(R6)                                                    
         LA    R2,1(R2)                                                         
         SR    R2,R6                                                            
         STH   R2,0(R6)                                                         
*                                                                               
WK50     TM    TBOPTS,TBTRACE                                                   
         BNO   WK80                                                             
         CLI   COMMAND,C'A'        ONLY PRINT ID FOR OPEN/CLOSE                 
         BE    WK70                                                             
         LA    R4,ID                                                            
         CLI   SWPOST,POSTPROD                                                  
         BE    WK60                                                             
         LA    R4,ID2                                                           
*                                                                               
WK60     GOTO1 TRACE,DMCB,(R4),16,=C'POST ID RECORD',14                         
         B     WK80                                                             
*                                                                               
WK70     LH    R4,DATADISP         SAVE DATADISP                                
         MVC   DATADISP,=H'4'                                                   
         GOTO1 AMYTRACE,DMCB,(RC),=C'POST RECORD',(R6),0                        
         STH   R4,DATADISP         RESTORE DATADISP                             
*                                                                               
WK80     L     R4,=A(POSTBUFF)                                                  
         L     R3,=V(AREA)                                                      
         LA    R5,ID                                                            
         CLI   SWPOST,POSTPROD                                                  
         BE    WK90                                                             
         LA    R5,ID2                                                           
         L     R4,=A(POSTBUF2)                                                  
*                                                                               
WK90     SH    R3,=H'4'                                                         
         TM    TBOPTS,TBNOPOST     CHECK FOR POSTING = NO                       
         BO    WKX                                                              
*                                                                               
         GOTO1 WORKER,DMCB,COMMAND,(R4),(R5),(R3)                               
         TM    DMCB+8,X'C0'                                                     
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
WKX      B     XIT                                                              
         SPACE 3                                                                
YES      SR    RC,RC               SET CONDITION CODE                           
NO       LTR   RC,RC                                                            
XIT      XIT1                                                                   
         SPACE 2                                                                
         GETEL R4,DATADISP,ELCODE                                               
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
         SPACE                                                                  
*                                                                               
*        JWT AGENCY TABLE                                                       
*                                                                               
JWTTAB   DS    0CL6                                                             
*NO-OP   DC    CL6'0144'     JWT AGENCIES USE BDAYS STATUS ON INTFACE           
*APR07   DC    CL6'0258'                                                        
***      DC    CL6'0268'                                                        
***      DC    CL6'0368'                                                        
***      DC    CL6'1950'                                                        
***      DC    CL6'7099'                                                        
*                             Y&R AGENCIES                                      
         DC    CL6'BRAV'                                                        
         DC    CL6'YNBRMO'                                                      
         DC    CL6'YNMEND'                                                      
         DC    CL6'YRNY'                                                        
         DC    CL6'YRSF'                                                        
         DC    CL6'0084'                                                        
         DC    CL6'0357'                                                        
         DC    CL6'1390'                                                        
         DC    CL6'1806'                                                        
         DC    CL6'9052'                                                        
         DC    X'FF'                                                            
         SPACE                                                                  
*                                                                               
         DS    0D                                                               
         DC    CL8'*POSTING'                                                    
PCODES   DS    0CL(PCODELEN)                                                    
         DC    CL14'SE41001',CL14'SR',AL2(TXPGRS1-TXPOSTTB),C'C'                
         DC    CL14'SE41002',CL14'SR',AL2(TXPGRS2-TXPOSTTB),C'C'                
         DC    CL14'SE41003',CL14'SR',AL2(TXPMISC-TXPOSTTB),C'C'                
         DC    CL14'SE41004',CL14'SR',AL2(TXPTAX1-TXPOSTTB),C'C'                
         DC    CL14'SE41005',CL14'SR',AL2(TXPTXFCR-TXPOSTTB),C'C'               
         DC    CL14'SE41006',CL14'SR',AL2(TXPHAND-TXPOSTTB),C'C'                
         DC    CL14'SE41007',CL14'SR',AL2(TXPPNH-TXPOSTTB),C'C'                 
         DC    CL14'SE41008',CL14'SR',AL2(TXPTXHI-TXPOSTTB),C'C'                
         DC    CL14'SE41009',CL14'SR',AL2(TXPGMIR-TXPOSTTB),C'C'                
         DC    CL14'SE41010',CL14'SR',AL2(TXPCHND-TXPOSTTB),C'C'                
         DC    CL14'SE41011',CL14'SR',AL2(TXPGST-TXPOSTTB),C'C'                 
         DC    CL14'SE41012',CL14'SR',AL2(TXPTXWA-TXPOSTTB),C'C'                
         DC    CL14'SE41013',CL14'SR',AL2(TXPTXTR-TXPOSTTB),C'C'                
         DC    CL14'SE41014',CL14'SR',AL2(TXPTXNY-TXPOSTTB),C'C'                
         DC    CL14'SE41015',CL14'SR',AL2(TXPTXCA-TXPOSTTB),C'C'                
         DC    CL14'SYTXCN G',CL14'SE42011',AL2(TXPGSTC-TXPOSTTB),C'C'          
         DC    CL14'SE42005',CL14'SXPW',AL2(TXPBNP-TXPOSTTB),C'D'               
         DC    CL14'SE42011',CL14'SYTXCN G',AL2(TXPGSTU-TXPOSTTB),C'D'          
         DC    CL14'SK2CDNGST',CL14'SE42011',AL2(TXPGSTD-TXPOSTTB),C'D'         
         DC    CL14'SE41096',CL14'SR',AL2(TXPACOMM-TXPOSTTB),C'C'               
         DC    CL14'SE41050',CL14'SR',AL2(TXPCSF-TXPOSTTB),C'C'                 
         DC    X'FF'                                                            
         SPACE                                                                  
*                                                                               
         DC    CL8'*POSTIN2'                                                    
PCODES2  DS    0CL(PCODELEN)                                                    
         DC    CL14'SE41001',CL14'SR',AL2(TXPGRS1-TXPOSTTB),C'C'                
         DC    CL14'SE41002',CL14'SR',AL2(TXPGRS2-TXPOSTTB),C'C'                
         DC    CL14'SE41003',CL14'SR',AL2(TXPMISC-TXPOSTTB),C'C'                
         DC    CL14'SE41004',CL14'SR',AL2(TXPTAX1-TXPOSTTB),C'C'                
         DC    CL14'SE41005',CL14'SR',AL2(TXPTXFCR-TXPOSTTB),C'C'               
         DC    CL14'SE41006',CL14'SR',AL2(TXPHAND-TXPOSTTB),C'C'                
         DC    CL14'SE41007',CL14'SR',AL2(TXPPNH-TXPOSTTB),C'C'                 
         DC    CL14'SE41008',CL14'SR',AL2(TXPTXHI-TXPOSTTB),C'C'                
         DC    CL14'SE41009',CL14'SR',AL2(TXPGMIR-TXPOSTTB),C'C'                
         DC    CL14'SE41010',CL14'SR',AL2(TXPCHND-TXPOSTTB),C'C'                
         DC    CL14'SE41011',CL14'SR',AL2(TXPGST-TXPOSTTB),C'C'                 
         DC    CL14'SE41012',CL14'SR',AL2(TXPTXWA-TXPOSTTB),C'C'                
         DC    CL14'SE41013',CL14'SR',AL2(TXPTXTR-TXPOSTTB),C'C'                
         DC    CL14'SE41014',CL14'SR',AL2(TXPTXNY-TXPOSTTB),C'C'                
         DC    CL14'SE41015',CL14'SR',AL2(TXPTXCA-TXPOSTTB),C'C'                
         DC    CL14'SYTXCN G',CL14'SE42011',AL2(TXPGSTC-TXPOSTTB),C'C'          
         DC    CL14'SE42005',CL14'SXPW',AL2(TXPBNP-TXPOSTTB),C'D'               
         DC    CL14'SE42011',CL14'SYTXCN G',AL2(TXPGSTU-TXPOSTTB),C'D'          
         DC    CL14'SK2CDNGST',CL14'SE42011',AL2(TXPGSTD-TXPOSTTB),C'D'         
         DC    CL14'SE21210',CL14'SR',AL2(TXPACOMM-TXPOSTTB),C'D'               
         DC    CL14'SE41050',CL14'SR',AL2(TXPCSF-TXPOSTTB),C'C'                 
         DC    X'FF'                                                            
         SPACE                                                                  
*                                                                               
         DS    0D                                                               
         DC    CL8'*PBUFF2*'                                                    
POSTBUF2 DC    4500X'00'           POSTING BUFFER                               
         SPACE 1                                                                
*                                                                               
         DS    0D                                                               
         DC    CL8'*PBUFF**'                                                    
POSTBUFF DC    4500X'00'           POSTING BUFFER                               
         EJECT                                                                  
*---------------------------------------------------------------                
         DS    0D                                                               
         DC    CL8'**SSB **'                                                    
SSB      CSECT                                                                  
         DC    X'0000FF',X'00',XL252'00'                                        
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------          
* E-MAIL ERROR REPORT                                                           
*---------------------------------------------------------------------          
SENDMAIL NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         USING UKRECD,RF                                                        
         LA    RF,ID2              NOTIFY TALENT PROGRAMMERS                    
*                                                                               
         EDIT  (B2,UKUSRID),(5,OOBUSID),0,LEFT,FILL=0                           
*                                                                               
         MVC   FILENAME,=CL8'CTFILE' SET TO READ CONTROL FILE                   
         MVI   USEIO,C'Y'                                                       
         XC    KEY,KEY                                                          
         LA    R5,KEY                                                           
         USING CTIREC,R5                                                        
         MVI   CTIKTYP,C'I'        BUILD KEY FOR ID RECORD                      
         MVC   CTIKNUM,UKUSRID                                                  
         GOTO1 HIGH                                                             
*                                                                               
         L     R5,AIO                                                           
         LA    R4,CTIDATA                                                       
SM05     CLI   0(R4),0                                                          
         JE    SM10                                                             
         CLI   0(R4),CTDSCELQ                                                   
         JE    SM08                                                             
         ZIC   R0,1(R4)                                                         
         AR    R4,R0                                                            
         J     SM05                                                             
         DROP  R5                                                               
*                                                                               
SM08     MVC   OOBUSID(6),2(R4)                                                 
*                                                                               
         USING UKRECD,RF                                                        
SM10     LA    RF,ID2              NOTIFY TALENT PROGRAMMERS                    
         MVC   OOBSYSP(4),UKSYSPRG                                              
         CLI   OOBSYSP+3,0                                                      
         JNE   *+8                                                              
         MVI   OOBSYSP+3,C'*'                                                   
*                                                                               
         MVC   FULL(1),UKDAY                                                    
         MVI   FULL+1,X'0C'                                                     
         UNPK  OOBDAYN(3),FULL(2)                                               
         MVC   OOBCLASS(1),UKCLASS                                              
         EDIT  (B2,UKFILNO),(4,OOBSEQN),0,LEFT,FILL=0                           
         EDIT  TALCASH,(10,BODYL6A1),2                                          
         EDIT  TALCRDTS,(10,BODYL6A2),2                                         
*                                                                               
         GOTOR =V(SMTP),DMCB,('SMTPAINI',JESMAIL)                               
*                                                                               
         L     RF,AMASTD                                                        
         USING MASTD,RF                                                         
         MVC   SUBJJOB,MCJOB                                                    
         MVC   BODYUSRI(L'MCUSERID),MCUSERID                                    
                                                                                
         LA    R4,BODYUSRI                                                      
         LA    R3,L'MCUSERID                                                    
                                                                                
SM11     CLI   0(R4),C' '                                                       
         JE    SM13                                                             
         AHI   R4,1                                                             
         BCT   R3,SM11                                                          
SM13     MVI   0(R4),C','                                                       
         AHI   R4,1                                                             
                                                                                
         L     R3,MCVREMOT                                                      
         USING REMOTED,R3                                                       
         MVC   0(3,R4),REMOTJID     REPORT ID                                   
         AHI   R4,3                                                             
         MVI   0(R4),C','                                                       
         AHI   R4,1                                                             
                                                                                
         MVC   DUB,SPACES                                                       
         EDIT  REMOTRNO,DUB,ALIGN=LEFT                                          
         MVC   0(L'DUB,R4),DUB                                                  
         DROP  R3                                                               
                                                                                
         LA    R4,TOWHO            NOTIFY TALENT TEAM AND OPERATIONS            
         TM    PRGSTAT,TESTSYS+CSCSYS+FQASYS                                    
         BNZ   SM18                CSC / TST / FQA, JUST MF-DEV                 
         CLI   MCWRITE,C'Y'        ADV AND WRITE=YES                            
         JE    *+8                                                              
SM18     LA    R4,TOWHO2           JUST MF-DEV                                  
         DROP  RF                                                               
*                                                                               
         TM    TBOPTS2,TBWRNPRG                                                 
         JZ    *+8                                                              
         LA    R4,TOWHO2                                                        
         GOTOR =V(SMTP),DMCB,('SMTPAPRS',(R4)),(L'SUBJDSC,SUBJDSC)              
*                                                                               
         MVC   BODYERR(L'OOBUSID),OOBUSID     MOVE IN ERROR MSG                 
         LA    RE,BODYERR                                                       
SM20     CLI   0(RE),C' '          FOUND A SPACE                                
         JNH   SM30                                                             
         AHI   RE,1                                                             
         J     SM20                                                             
*                                                                               
SM30     MVI   0(RE),C','                                                       
         AHI   RE,1                                                             
         MVC   0(OOBLENQ,RE),OOBSYSP                                            
*                                                                               
         GOTOR (RF),DMCB,('SMTPAPTL',BODYLIN1)                                  
         CLC   DUB,SPACES                                                       
         JH    SM40                                                             
         GOTOR (RF),DMCB,('SMTPAPTL',BODYLIN3)                                  
SM40     GOTOR (RF),DMCB,('SMTPAPTL',BODYLIN2)                                  
         GOTOR (RF),DMCB,('SMTPAPTL',BODYLIN3)                                  
         GOTOR (RF),DMCB,('SMTPAPTL',BODYLIN4)                                  
         GOTOR (RF),DMCB,('SMTPAPTL',BODYLIN5)                                  
         GOTOR (RF),DMCB,('SMTPAPTL',BODYLIN6)                                  
*                                                                               
         GOTOR (RF),DMCB,('SMTPASND',0)                                         
         GOTOR (RF),DMCB,('SMTPAEND',0) DETACH SMTP                             
*                                                                               
XIT2     XIT1                                                                   
*                                                                               
OOBDEF   DC    0CL40                                                            
OOBUSID  DC    CL10'00000     '                                                 
OOBSYSP  DC    C'TNN*'                                                          
OOBDAYN  DC    C'DD'                                                            
OOBCLASS DC    C'C,'                                                            
OOBSEQN  DC    C'SSSS'                                                          
OOBLENQ  EQU   *-OOBSYSP                                                        
         DC    CL(L'OOBDEF-(*-OOBDEF))' '     SPARE SPACES                      
*                                                                               
JESMAIL  DC    CL8'JESMAIL '                                                    
*                                                                               
TOWHO    DC    C'US-TALENT_MF_DEV_TEAM,US-TALENT_PRODUCT_TEAM,US-OPERATX        
               IONS_SUPPORT_NY:'                                                
TOWHO2   DC    C'US-TALENT_MF_DEV_TEAM:'                                        
*                                                                               
SUBJDSC  DC    0CL80                                                            
         DC    C'** Worker File Out-Of-Balance  ('                              
SUBJJOB  DC    C'        '                                                      
         DC    C')'                                                             
         DC    CL(L'SUBJDSC-(*-SUBJDSC))' '     SPARE SPACES                    
BODYLIN1 DC    0CL80                                                            
         DC    C'PQ file: '                                                     
BODYUSRI DC    C'                         '     ERROR MESSAGE                   
         DC    CL(L'BODYLIN1-(*-BODYLIN1))' '   SPARE SPACES                    
BODYLIN2 DC    0CL80                                                            
         DC    C'Worker file: '                                                 
BODYERR  DC    C'                  '            ERROR MESSAGE                   
         DC    CL(L'BODYLIN2-(*-BODYLIN2))' '   SPARE SPACES                    
*                                                                               
BODYLIN3 DC    CL80' '                                                          
BODYLIN4 DC    CL80'Debits not equal to Credits'                                
BODYLIN5 DC    CL80'==========================='                                
BODYLIN6 DC    0CL80                                                            
BODYL6A1 DC    CL10'9999999.99'                                                 
         DC    C' vs '                                                          
BODYL6A2 DC    CL10'9999999.99'                                                 
         DC    CL(L'BODYLIN6-(*-BODYLIN6))' '   SPARE SPACES                    
                                                                                
         LTORG                                                                  
         EJECT                                                                  
*=====================================================================          
*        SEE IF WE NEED PAYROLL TAX SEPARATED (WA,HI,CA,NY)                     
*=====================================================================          
TXPUNIT  NTR1  BASE=*,LABEL=*                                                   
                                                                                
         MVC   BYTE,0(R1)                                                       
         L     R2,0(R1)            ACCUMULATOR                                  
         L     RF,4(R1)            UNIT                                         
         MVC   WORK(3),0(RF)                                                    
                                                                                
         MVI   ELCODE,TAXTELQ                                                   
         GOTO1 GETL,DMCB,(3,WORK)                                               
         JNE   XIT2                                                             
         L     RF,TGELEM                                                        
         USING TAXTD,RF                                                         
         L     R1,0(R2)                                                         
         ICM   RE,15,TAXTTAXS                                                   
         AR    R1,RE                                                            
         ST    R1,0(R2)                                                         
                                                                                
         TM    BYTE,X'80'          DO WE NEED TO REDUCE TAXES?                  
         JO    XIT2                NO, SKIP                                     
         L     R1,TBTAX                                                         
         SR    R1,RE                                                            
         ST    R1,TBTAX                                                         
         J     XIT2                                                             
                                                                                
         LTORG                                                                  
         EJECT                                                                  
*=====================================================================          
*        IF CALCULATING PAYROLL TAX THE NEW WAY,                                
*        STORE PAYROLL TAX OVERRIDE IN FULL                                     
*=====================================================================          
SETTAX   NTR1  BASE=*,LABEL=*                                                   
         XC    FULL,FULL                                                        
                                                                                
         TM    TBISTAT,TAPDSCAN    IF CANADIAN                                  
         BO    STTAXX              SKIP THIS                                    
                                                                                
         USING TMD,R3                                                           
         L     R3,ATALIM                                                        
         L     RE,TMACURPG         GET TAPG ELEMENT                             
         LTR   RE,RE               IF NEW POSTING ELEMENT EXISTS,               
         BZ    STTAXX                                                           
                                                                                
         USING TARAD,R4                                                         
         L     R4,AIO1                                                          
         MVI   ELCODE,TARAELQ      GET BOVER ON INVOICE                         
         BAS   RE,GETEL                                                         
         BNE   STTAXX                                                           
                                                                                
         TM    TARASTA2,TARASFUD   FUTA IN DOLLARS?                             
         BZ    STTAX10                                                          
         MVC   FULL,TARATFU        FUTA OVERRIDE                                
                                                                                
STTAX10  TM    TARASTA2,TARASSUD   SUTA IN DOLLARS?                             
         BZ    STTAX20                                                          
         L     R1,FULL                                                          
         ICM   R2,15,TARATSU       SUTA                                         
         AR    R1,R2                                                            
         ST    R1,FULL             FUTA + SUTA OVERRIDE                         
                                                                                
STTAX20  TM    TARASTA2,TARASFID   FICA IN DOLLARS?                             
         BZ    STTAX30                                                          
         L     R1,FULL                                                          
         ICM   R2,15,TARATFI       FICA                                         
         AR    R1,R2                                                            
         ST    R1,FULL             FUTA + SUTA + FICA OVERRIDE                  
                                                                                
STTAX30  TM    TARASTA2,TARASMED   MEDICARE IN DOLLARS?                         
         BZ    STTAX40                                                          
         L     R1,FULL                                                          
         ICM   R2,15,TARATM        MEDICARE                                     
         AR    R1,R2                                                            
         ST    R1,FULL             FUTA + SUTA + FICA + MED OVERRIDE            
                                                                                
STTAX40  TM    TARASTA2,TARASWCD   WC IN DOLLARS?                               
         BZ    STTAXX                                                           
         L     R1,FULL                                                          
         ICM   R2,15,TARATWC       WC                                           
         AR    R1,R2                                                            
         ST    R1,FULL             FUTA + SUTA + FICA + MED + WC                
                                                                                
STTAXX   XIT1                                                                   
         DROP  R3,R4                                                            
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------                
*                                                                               
*              SPLITS CSFRATE IF NEEDED                                         
*              IF NOTHING IN SITAB, LEAVE R1 ALONE                              
*              OTHERWISE MULTIPLY BY SUBSIDIARY PCTAGE                          
*                                                                               
CSFMULT  NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         TM    TBOPTS,TBRERUN      IS IT A RERUN?                               
         BZ    CSFMLT50            NO, CALCULATE IT                             
         USING TABDD,R4                                                         
         L     R4,AIO              USE EXISTING INFO                            
         MVI   ELCODE,TABDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   CSFMLTX                                                          
         L     R1,TABDCSF          USE WHAT WAS CALCULATED                      
         B     CSFMLTX                                                          
         DROP  R4                                                               
*                                                                               
CSFMLT50 L     RF,=V(SITAB)                                                     
         OC    0(4,RF),0(RF)       ARE THERE SUBSIDIARY INVOICES                
         BZ    CSFMLTX             NO, LEAVE R1 ALONE                           
*                                                                               
         USING SITABD,R6                                                        
         SR    R0,R0               YES, R6 POINTS TO CURR SITAB ENTRY           
         L     RF,SITBPCT                                                       
         MR    R0,RF                                                            
         D     R0,=F'1000000'                                                   
         SLA   R0,1                ROUND IT                                     
         C     R0,=F'1000000'                                                   
         JL    CSFMLTX                                                          
         AHI   R1,1                                                             
         B     CSFMLTX                                                          
*                                                                               
CSFMLTX  XIT1  REGS=(R1)                                                        
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
       ++INCLUDE TABIDSECT                                                      
         EJECT                                                                  
       ++INCLUDE TABILLD                                                        
         EJECT                                                                  
       ++INCLUDE TAINTERD                                                       
         EJECT                                                                  
*        OTHER DSECTS ARE HIDDEN IN HERE                                        
         SPACE 3                                                                
*DDSPOOLD                                                                       
*DDSPLWORKD                                                                     
*DDMASTD                                                                        
*TAGENFILE                                                                      
*CTGENFILE                                                                      
*ACGENBOTH                                                                      
*ACGENFILE                                                                      
*ACGENPOST                                                                      
*TASYSDSECT                                                                     
*TASYSEQUS                                                                      
*DDCOMFACSD                                                                     
*DDGETRETD                                                                      
*TAREPWORKD                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDSPOOLD                                                       
       ++INCLUDE DDSPLWORKD                                                     
       ++INCLUDE DDMASTD                                                        
       ++INCLUDE DDREMOTED                                                      
       ++INCLUDE TAGENFILE                                                      
       ++INCLUDE CTGENFILE                                                      
       ++INCLUDE ACGENBOTH                                                      
       ++INCLUDE ACGENFILE                                                      
       ++INCLUDE ACGENPOST                                                      
       ++INCLUDE TASYSDSECT                                                     
       ++INCLUDE TASYSEQUS                                                      
       ++INCLUDE DDSMTPD                                                        
       ++INCLUDE DMWRKRK                                                        
       ++INCLUDE DDCOMFACSD                                                     
       ++INCLUDE DDGETRETD                                                      
       ++INCLUDE TAREPWORKD                                                     
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'109TABIPOST  08/13/15'                                      
         END                                                                    
