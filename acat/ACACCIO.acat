*          DATA SET ACACCIO    AT LEVEL 028 AS OF 07/26/17                      
*CATALP ACCIO                                                                   
         TITLE 'ACCIO - ACCPAK I/O CONTROLLER'                                  
* MPEN 17JUL17 142  <DSRD-15589> RELINK UPDATE TIME NARR TO 200 CHARS           
ACCIO    CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 (LWSEND-LWSD),*ACCIO*,RA,RR=R2,CLEAR=YES                         
         LR    R9,RC                                                            
         USING LWSD,R9                                                          
         ST    R2,RELO                                                          
         L     R8,0(R1)                                                         
         USING ACCIOD,R8                                                        
         L     R7,ACCOMFAC                                                      
         USING COMFACSD,R7                                                      
         MVC   USERRD,4(RD)        SAVE LINK BACK TO USER                       
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
         MVI   DMINBTS,X'08'       PASS DELETED RECORDS                         
         MVC   FILNME,=CL8'ACCOUNT'                                             
         MVC   DATADISP,=H'49'                                                  
         L     R1,=V(CONVMOS)      RELOCATE LINKED MODULE                       
         A     R1,RELO                                                          
         ST    R1,CONVMOS                                                       
         SPACE 1                                                                
IN2      OC    LSTRQST,LSTRQST                                                  
         BZ    *+14                FIRST TIME                                   
         CLC   LSTRQST,QRECORD                                                  
         BE    NEXTREC             SAME REQUEST - CONTINUE                      
         MVC   LSTRQST,QRECORD     SAVE LAST REQUEST                            
         SPACE 1                                                                
IN4      BAS   RE,GETLISTS         GET LISTS IF NEEDED                          
         BAS   RE,OFFLIST          OPTIONAL PRODUCTION LISTS                    
         BAS   RE,MEDLIST                                                       
         BAS   RE,WORKLIST                                                      
         XC    FILTTAGY,FILTTAGY   SET UP 2 BYTE AGENCY FILTER CODE             
         CLI   QFILTAGY,X'41'                                                   
         BL    IN6                                                              
         MVC   FILTTAGY(1),QFILTAGY                                             
         L     R7,ACCOMFAC                                                      
         USING COMFACSD,R7                                                      
         GOTO1 CHEXIN,DMCB,QFILTAGY+1,FILTTAGY+1,2                              
         SPACE 1                                                                
IN6      DS    0H                                                               
         EJECT                                                                  
*              GET LENGTH OF START KEY                                          
         SPACE 1                                                                
         LA    R1,QACCOUNT+11                                                   
         LA    R2,14                                                            
         CLI   0(R1),X'40'                                                      
         BH    *+12                                                             
         BCTR  R1,0                                                             
         BCT   R2,*-10                                                          
         DC    H'0'                COMPANY NOT SPECIFIED                        
         STC   R2,EXQLN            SAVE ACCOUNT LENGTH                          
         SPACE 1                                                                
*              GET COMPANY RECORD                                               
         SPACE 1                                                                
         MVC   KEY,SPACES                                                       
         MVC   KEY(1),QCOMPANY                                                  
         BAS   RE,READ                                                          
         BNE   ERRXIT              CAN'T READ COMPANY RECORD                    
         LA    R1,CMPLST                                                        
         LA    R2,ADCMPEL                                                       
         BAS   RE,SETLADR          SET ELEMENT ADDRESSES                        
         BAS   RE,POSTCOMP                                                      
         BNE   XIT                 (BAD OFFICE - GETTING OUT)                   
         MVI   ACMODE,COMPFRST                                                  
         BAS   RE,GO                                                            
         SPACE 1                                                                
*              GET UNIT RECORD                                                  
         SPACE 1                                                                
         MVC   KEY+1(1),QUNIT                                                   
         CLI   QUNIT,X'41'                                                      
         BL    *+12                                                             
         BAS   RE,HIGH             UNIT SPECIFIED                               
         B     *+8                                                              
         BAS   RE,SEQ              UNIT NOT SPECIFIED - START AT FIRST          
         ZIC   R1,EXQLN                                                         
         EX    R1,KYCMP            IS THIS SAME COMP/UNIT ETC.                  
         BNE   ENDRQ               NO - SEND END OF REQUEST                     
         SPACE 1                                                                
UNITBRK  LA    R1,UNTLST                                                        
         LA    R2,ADUNTNAM                                                      
         BAS   RE,SETLADR          SET ELEMENT ADDRESSES                        
         MVI   ACMODE,UNITFRST                                                  
         BAS   RE,GO                                                            
         EJECT                                                                  
*              GET LEDGER RECORD                                                
         SPACE 1                                                                
         MVC   KEY+2(1),QLEDGER                                                 
         CLI   QLEDGER,X'41'                                                    
         BL    *+12                                                             
         BAS   RE,HIGH             LEDGER SPECIFIED                             
         B     *+8                                                              
         BAS   RE,SEQ              NOT SPECIFIED - START AT FIRST               
         ZIC   R1,EXQLN                                                         
         EX    R1,KYCMP            IS THIS SAME COMP/UNIT ETC.                  
         BNE   ENDRQ               NO - SEND END OF REQUEST                     
         SPACE 1                                                                
LEDGBRK  LA    R1,LDGLST                                                        
         LA    R2,ADLDGEL                                                       
         BAS   RE,SETLADR          SET ELEMENT ADDRESSES                        
         BAS   RE,POSTLEDG                                                      
         MVI   ACMODE,LEDGFRST                                                  
         BAS   RE,GO                                                            
         SPACE 1                                                                
*              GET LEVEL A RECORD                                               
         SPACE 1                                                                
         USING ACHEIRD,R4                                                       
         L     R4,ADLDGHIR                                                      
         ZIC   R3,ACHRLEVA         LEVEL A LENGTH                               
         BCTR  R3,0                                                             
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   KEY+3(0),QACCOUNT                                                
         CLI   QACCOUNT,X'41'                                                   
         BL    *+12                                                             
         BAS   RE,HIGH             LEVEL A SPECIFIED                            
         B     *+8                                                              
         BAS   RE,SEQ              NOT SPECIFIED - START AT FIRST               
         ZIC   R1,EXQLN                                                         
         EX    R1,KYCMP            IS THIS SAME COMP/UNIT ETC.                  
         BNE   ENDRQ               NO - SEND END OF REQUEST                     
         SPACE 1                                                                
LEVABRK  LA    R1,LVALST                                                        
         LA    R2,ADLVASTA                                                      
         BAS   RE,SETLADR          SET ELEMENT ADDRESSES                        
         MVI   THISLEV,1                                                        
         BAS   RE,SETFILT          SET FILTERS AT THIS LEVEL                    
         MVI   ACMODE,PROCLEVA                                                  
         BAS   RE,GO                                                            
         EJECT                                                                  
*              GET LEVEL B RECORD                                               
         SPACE 3                                                                
         L     R4,ADLDGHIR                                                      
         CLI   ACHRLEVB,0                                                       
         BE    ACC20               ACCOUNT LEVEL                                
         BAS   RE,POSTOFF                                                       
         BAS   RE,FILTOFF          MAY FILTER OFFICE AT THIS LEVEL              
         BNE   LEVASKIP                                                         
         BAS   RE,ACCCHECK         MAY BE LIMITED ACCESS AT THIS LEVEL          
         BNE   LEVASKIP                                                         
         BAS   RE,ANYBUDG          HANDLE BUDGETS IF NEEDED                     
         ZIC   R1,ACHRLEVA                                                      
         LA    R2,KEY+3(R1)        R2 TO SECOND LEVEL                           
         LA    R5,QCOMPANY+3(R1)   DITTO R5                                     
         ZIC   R3,ACHRLEVB         GET LEVEL B LENGTH                           
         SR    R3,R1                                                            
         BCTR  R3,0                                                             
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),0(R5)       QACCOUNT(LEVEL B) TO KEY                     
         CLI   0(R5),X'41'                                                      
         BL    *+12                                                             
         BAS   RE,HIGH             LEVEL B SPECIFIED                            
         B     *+8                                                              
         BAS   RE,SEQ              NOT SPECIFIED - START AT FIRST               
         ZIC   R1,EXQLN                                                         
         EX    R1,KYCMP            IS THIS SAME COMP/UNIT ETC.                  
         BNE   ENDRQ               NO - SEND END OF REQUEST                     
         CLI   0(R5),X'40'                                                      
         BH    LEVBBRK                                                          
         B     ACCREC                                                           
         SPACE 1                                                                
LEVBBRK  LA    R1,LVBLST                                                        
         LA    R2,ADLVBSTA                                                      
         BAS   RE,SETLADR          SET ELEMENT ADDRESSES                        
         MVI   THISLEV,2                                                        
         BAS   RE,SETFILT          SET FILTERS AT THIS LEVEL                    
         MVI   ACMODE,PROCLEVB                                                  
         BAS   RE,GO                                                            
         EJECT                                                                  
*              GET LEVEL C RECORD                                               
         SPACE 3                                                                
         L     R4,ADLDGHIR                                                      
         CLI   ACHRLEVC,0                                                       
         BE    ACC20               ACCOUNT LEVEL                                
         BAS   RE,POSTOFF                                                       
         BAS   RE,FILTOFF          MAY FILTER OFFICE AT THIS LEVEL              
         BNE   LEVBSKIP                                                         
         BAS   RE,ACCCHECK         MAY BE LIMITED ACCESS AT THIS LEVEL          
         BNE   LEVBSKIP                                                         
         BAS   RE,ANYBUDG          HANDLE BUDGETS IF NEEDED                     
         ZIC   R1,ACHRLEVB                                                      
         LA    R2,KEY+3(R1)        R2 TO THIRD LEVEL                            
         LA    R5,QCOMPANY+3(R1)   DITTO R5                                     
         ZIC   R3,ACHRLEVC         GET LEVEL C LENGTH                           
         SR    R3,R1                                                            
         BCTR  R3,0                                                             
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),0(R5)       QACCOUNT(LEVEL C) TO KEY                     
         CLI   0(R5),X'41'                                                      
         BL    *+12                                                             
         BAS   RE,HIGH             LEVEL C SPECIFIED                            
         B     *+8                                                              
         BAS   RE,SEQ              NOT SPECIFIED - START AT FIRST               
         ZIC   R1,EXQLN                                                         
         EX    R1,KYCMP            IS THIS SAME COMP/UNIT ETC.                  
         BNE   ENDRQ               NO - SEND END OF REQUEST                     
         CLI   0(R5),X'40'                                                      
         BH    LEVCBRK                                                          
         B     ACCREC                                                           
         SPACE 1                                                                
LEVCBRK  LA    R1,LVCLST                                                        
         LA    R2,ADLVCSTA                                                      
         BAS   RE,SETLADR          SET ELEMENT ADDRESSES                        
         MVI   THISLEV,3                                                        
         BAS   RE,SETFILT          SET FILTERS AT THIS LEVEL                    
         MVI   ACMODE,PROCLEVC                                                  
         BAS   RE,GO                                                            
         EJECT                                                                  
*              GET LEVEL D RECORD                                               
         SPACE 3                                                                
         L     R4,ADLDGHIR                                                      
         CLI   ACHRLEVD,0                                                       
         BE    ACC20               ACCOUNT LEVEL                                
         BAS   RE,POSTOFF                                                       
         BAS   RE,FILTOFF          MAY FILTER OFFICE AT THIS LEVEL              
         BNE   LEVCSKIP                                                         
         BAS   RE,ACCCHECK         MAY BE LIMITED ACCESS AT THIS LEVEL          
         BNE   LEVCSKIP                                                         
         BAS   RE,ANYBUDG          HANDLE BUDGETS IF NEEDED                     
         ZIC   R1,ACHRLEVC                                                      
         LA    R2,KEY+3(R1)        R2 TO THIRD LEVEL                            
         LA    R5,QCOMPANY+3(R1)   DITTO R5                                     
         ZIC   R3,ACHRLEVD         GET LEVEL D LENGTH                           
         SR    R3,R1                                                            
         BCTR  R3,0                                                             
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),0(R5)       QACCOUNT(LEVEL D) TO KEY                     
         CLI   0(R5),X'41'                                                      
         BL    *+12                                                             
         BAS   RE,HIGH             LEVEL D SPECIFIED                            
         B     *+8                                                              
         BAS   RE,SEQ              NOT SPECIFIED - START AT FIRST               
         ZIC   R1,EXQLN                                                         
         EX    R1,KYCMP            IS THIS SAME COMP/UNIT ETC.                  
         BNE   ENDRQ               NO - SEND END OF REQUEST                     
         CLI   0(R5),X'40'                                                      
         BH    LEVDBRK                                                          
         B     ACCREC                                                           
         SPACE 1                                                                
LEVDBRK  LA    R1,LVDLST                                                        
         LA    R2,ADLVDSTA                                                      
         BAS   RE,SETLADR          SET ELEMENT ADDRESSES                        
         MVI   THISLEV,4                                                        
         BAS   RE,SETFILT          SET FILTERS AT THIS LEVEL                    
         MVI   ACMODE,PROCLEVD                                                  
         BAS   RE,GO                                                            
         EJECT                                                                  
*              SET ACCOUNT ELEMENT ADDRESSES                                    
         SPACE 3                                                                
ACC20    LA    R1,ACCLST                                                        
         LA    R2,ADACCSTA                                                      
         BAS   RE,SETLADR                                                       
         SPACE 1                                                                
*                                 SAVE LOW LEVEL ACCOUNT                        
         USING ACKEYD,R2                                                        
         LA    R2,IO                                                            
         SR    R3,R3                                                            
         ICM   R3,3,ACLENGTH                                                    
         LA    R4,RECAREA                                                       
         ST    R4,ADACCREC         PASS ADDRESS OF RECORD TO USER               
         LA    R5,1000                                                          
         MVCL  R4,R2                                                            
         XC    ADHSTREC,ADHSTREC                                                
         XC    ADTRNREC,ADTRNREC                                                
         BAS   RE,POSTACC                                                       
*                                                                               
         MVI   FILTLEV,C'A'        (ACCOUNT LEVEL)                              
         BAS   RE,TESTFILT         POSSIBLE FILTER                              
         BNE   ACCSKIP                                                          
         BAS   RE,LIMSEC           CHECK LIMIT ACCESS AND SECURITY              
         BNE   ACCSKIP                                                          
         BAS   RE,ACCCHECK                                                      
         BNE   ACCSKIP                                                          
         MVI   ACMODE,PROCACC                                                   
         BAS   RE,GO                                                            
         BAS   RE,ANYBUDG          HANDLE BUDGETS IF NEEDED                     
         B     NEXTREC                                                          
         SPACE 1                                                                
         USING ACKEYD,R2                                                        
ACCSKIP  LA    R2,KEY                                                           
         MVI   ACKEYWRK,X'FF'      SKIP TO NEXT ACCOUNT                         
         BAS   RE,HIGH                                                          
         ZIC   R1,EXQLN                                                         
         EX    R1,KYCMP            IS THIS SAME COMP/UNIT ETC.                  
         BNE   ENDRQ               NO - SEND END OF REQUEST                     
         B     ACCREC                                                           
         SPACE 1                                                                
         EJECT                                                                  
*              PROCESS HISTORY RECORDS                                          
         SPACE 3                                                                
         USING ACKEYD,R2                                                        
NEXTREC  BAS   RE,SEQ                                                           
         SPACE 1                                                                
HISTREC  ZIC   R1,EXQLN                                                         
         EX    R1,KYCMP            IS THIS SAME COMP/UNIT ETC.                  
         BNE   ENDRQ               NO - SEND END OF REQUEST                     
         LA    R2,IO                                                            
         LA    R4,ACRECORD                                                      
         CLI   0(R4),X'43'                                                      
         BNE   TRNSREC             NOT A HISTORY                                
         USING ACKEYD,R2                                                        
         CLC   ACKEYWRK,=C'**'     IGNORE ORDERS                                
         BE    NEXTREC                                                          
         LA    R2,IO               SAVE THE HISTORY RECORD                      
         SR    R3,R3                                                            
         ICM   R3,3,ACLENGTH                                                    
         LA    R4,RECAREA                                                       
         LA    R4,1000(R4)                                                      
         ST    R4,ADHSTREC         PASS ADDRESS OF RECORD TO USER               
         LA    R5,1000                                                          
         MVCL  R4,R2                                                            
         LA    R2,IO                                                            
         XC    ADTRNREC,ADTRNREC                                                
         SPACE 1                                                                
         CLI   FCRDWORK,C'Y'       IS USER ONLY INTERESTED IN WORK CODE         
         BNE   HISTRECB                                                         
         MVI   ACMODE,PROCWORK                                                  
         BAS   RE,POSTWORK                                                      
         BAS   RE,GO                                                            
         B     WORKSKIP                                                         
         SPACE 1                                                                
HISTRECB CLI   FCRDHIST,C'Y'       DOES USER WANT HISTORIES                     
         BNE   NEXTREC                                                          
         LA    R1,HSTLST                                                        
         LA    R2,ADSUBAC                                                       
         BAS   RE,SETLADR          SET HISTORY ELEMENT ADDRESSES                
         LA    R2,IO                                                            
         LA    R4,ACRECORD                                                      
         SPACE 1                                                                
HISTREC2 CLI   0(R4),0                                                          
         BE    NEXTREC                                                          
         ZIC   R0,1(R4)                                                         
         AR    R4,R0                                                            
         CLI   0(R4),X'45'                                                      
         BE    HISTREC4                                                         
         CLI   0(R4),X'55'                                                      
         BNE   HISTREC2                                                         
         SPACE 1                                                                
HISTREC4 CLC   =C'SJ',ACKEYACC+1                                                
         BE    *+14                                                             
         CLC   ACKEYWRK,SPACES                                                  
         BH    NEXTREC                                                          
         MVI   ACMODE,PROCHIST                                                  
         ST    R4,ADHIST                                                        
         BAS   RE,POSTHIST         DEDUCE VALUES FOR HISTORY                    
         MVI   FILTLEV,C'H'                                                     
         BAS   RE,TESTFILT         CHECK FILTERS                                
         BNE   TRNSKIP                   SKIP TO NEXT C/A IF FAIL               
         BAS   RE,GO               RETURN TO USER                               
         B     HISTREC2                                                         
         EJECT                                                                  
*              PROCESS TRANSACTIONS                                             
         SPACE 3                                                                
TRNSREC  CLI   0(R4),X'44'         IS IT A TRANSACTION                          
         BNE   TMSREC              NO - MAYBE IT'S A TMS RECORD                 
         CLI   FCRDTRNS,C'Y'       DOES USER WANT TRANSACTIONS                  
         BNE   TRNSKIP             NO, SO SKIP TO NEXT CONTRA                   
         USING ACKEYD,R2                                                        
         CLC   ACKEYWRK,=C'**'     IGNORE ORDERS                                
         BE    NEXTREC                                                          
         GOTO1 =V(ACPTACNV),DMCB,(X'80',(R2)),ACCOMFAC,0                        
         LA    R2,IO               SAVE THE TRANSACTION                         
         SR    R3,R3                                                            
         ICM   R3,3,ACLENGTH                                                    
         LA    R4,RECAREA                                                       
         LA    R4,2000(R4)                                                      
         ST    R4,ADTRNREC         PASS ADDRESS OF RECORD TO USER               
         LA    R5,1000                                                          
         MVCL  R4,R2                                                            
         LA    R2,IO                                                            
         SPACE 1                                                                
         LA    R1,TRNLST                                                        
         LA    R2,ADTRANS                                                       
         BAS   RE,SETLADR          SET TRANSACTION ELEMENT ADDRESSES            
         MVI   ACMODE,PROCTRNS                                                  
         BAS   RE,POSTTRNS         DEDUCE VALUES FOR TRANSACTION                
         MVI   FILTLEV,C'T'                                                     
         BAS   RE,TESTFILT                                                      
         BNE   NEXTREC                                                          
         BAS   RE,ACCCHECK         SECURITY ACCESS                              
         BNE   NEXTREC                                                          
         BAS   RE,GO               GO TO USER FOR PROCTRNS                      
         BAS   RE,TRANSELS                                                      
         B     NEXTREC                                                          
         EJECT                                                                  
*              PROCESS TMS TRANSACTIONS                                         
         SPACE 3                                                                
TMSREC   LR    RF,R4               IS THIS A TMS RECORD?                        
         SR    R0,R0                                                            
TMSR02   CLI   0(RF),0             NO, TRY ACCOUNT                              
         BE    ACCREC                                                           
         CLI   0(RF),TIMELQ                                                     
         BE    TMSR04                                                           
         IC    R0,1(RF)                                                         
         AR    RF,R0                                                            
         B     TMSR02                                                           
*                                                                               
TMSR04   CLI   FCRDTRNS,C'Y'       DOES USER WANT TRANSACTIONS                  
         BNE   TRNSKIP             NO, SO SKIP TO NEXT CONTRA                   
         USING ACKEYD,R2                                                        
*                                                                               
         L     RF,AOFFBLK                                                       
         LA    RF,OFFALLEN(RF)                                                  
         ST    RF,ATMSIO                                                        
*                                                                               
         MVI   TMSSWT,X'00'        CLEAR SWITCH                                 
         LA    R2,IO                                                            
         SR    R3,R3                                                            
         ICM   R3,3,ACLENGTH                                                    
         L     R4,ATMSIO           SAVE OFF ORGINAL TRANSACTION                 
         LA    R5,L'TMSIO                                                       
         MVCL  R4,R2                                                            
*                                                                               
TMSR06   BAS   RE,BUILDELS         REBUILD RECORD IN IO                         
         BNE   NEXTREC             ALL DONE, GET NEXT RECORD                    
*                                                                               
         LA    R2,IO               MOVE NEW IO TO RECAREA                       
         SR    R3,R3                                                            
         ICM   R3,3,ACLENGTH                                                    
         LA    R4,RECAREA                                                       
         LA    R4,2000(R4)                                                      
         ST    R4,ADTRNREC         PASS ADDRESS OF RECORD TO USER               
         LA    R5,1000                                                          
         MVCL  R4,R2                                                            
*                                                                               
         LA    R1,TRNLST                                                        
         LA    R2,ADTRANS                                                       
         BAS   RE,SETLADR          SET TRANSACTION ELEMENT ADDRESSES            
*                                                                               
         MVI   ACMODE,PROCTRNS                                                  
         BAS   RE,POSTTRNS         DEDUCE VALUES FOR TRANSACTION                
*                                                                               
         MVI   FILTLEV,C'T'                                                     
         BAS   RE,TESTFILT                                                      
         BNE   TMSR06              GET NEXT TMS RECORD                          
*                                                                               
         BAS   RE,ACCCHECK         SECURITY ACCESS                              
         BNE   TMSR06              GET NEXT TMS RECORD                          
*                                                                               
         BAS   RE,GO               GO TO USER FOR PROCTRNS                      
         BAS   RE,TRANSELS                                                      
         B     TMSR06                                                           
         DROP  R2                                                               
         EJECT                                                                  
*              BUILD DUMMY ELEMENTS FOR TMS                                     
*                                                                               
         USING ACKEYD,RE                                                        
BUILDELS NTR1                                                                   
         CLI   TMSSWT,X'00'        FIRST TIME THROUGH?                          
         BNE   BUILD02             NO                                           
*                                                                               
         MVI   CONTR1N,C'N'        CLEAR INDICATOR                              
         LA    RE,IO               YES, CLEAR ALL ELEMENTS                      
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,3,ACLENGTH       GET LENGTH MINUS DATADISP                    
         SH    RF,DATADISP                                                      
*                                                                               
         AH    RE,DATADISP         BUMP TO RIGHT PLACE IN RECORD                
*                                                                               
         SR    R1,R1               CLEAR R1 TO MOVE ZEROS                       
         MVCL  RE,R0                                                            
         DROP  RE                                                               
*                                                                               
         USING TIMRECD,RF                                                       
         LA    RF,IO                                                            
         USING TRNELD,R2                                                        
         LA    R2,IO               READDRESS STARTING ELEMENT POSITION          
         AH    R2,DATADISP                                                      
         MVI   TRNEL,TRNELQ                                                     
         MVI   TRNLN,TRNLN1Q+150  $BILL W/O HAVE LONGER NARRATIVES              
         MVI   TRNTYPE,49                                                       
         MVC   TRNREF,TIMKREF                                                   
         MVI   TRNSTAT,X'80'                                                    
         MVC   TRNBTCH,SPACES                                                   
         ZAP   TRNAMNT,=P'0'                                                    
*                                                                               
         CLC   TIMKULC(2),=C'1N'   CONTRA ON 1N?                                
         BNE   *+8                 NO                                           
         MVI   CONTR1N,C'Y'        YES, INDICATE IT                             
*                                                                               
         SR    RF,RF                                                            
         IC    RF,TRNLN                                                         
         AR    R2,RF                                                            
*                                                                               
         CLI   CONTR1N,C'Y'        CONTRA 1N?                                   
         BE    BUILD01             YES, BYPASS 40 AND 51                        
*                                                                               
         USING PRTELD,R2                                                        
         MVI   PRTEL,PRTELQ        CREATE A PERSONNEL RATE ELEMENT              
         MVI   PRTLN,PRTLNQ                                                     
*                                                                               
         SR    RF,RF                                                            
         IC    RF,PRTLN                                                         
         AR    R2,RF                                                            
*                                                                               
         USING PCIELD,R2                                                        
         MVI   PCIEL,PCIELQ        CREATE A PROJECT CONTROL ELEMENT             
         MVI   PCILN,PCILN2Q                                                    
*                                                                               
         SR    RF,RF                                                            
         IC    RF,PCILN                                                         
         AR    R2,RF                                                            
*                                                                               
         USING SCIELD,R2                                                        
BUILD01  MVI   SCIEL,SCIELQ        CREATE A X'50' ELEMENT                       
         MVI   SCILN,SCILN1Q                                                    
         MVI   SCITYPE,SCITHOUR    ALWAYS HOURS                                 
*                                                                               
         SR    RF,RF                                                            
         IC    RF,SCILN                                                         
         AR    R2,RF                                                            
                                                                                
         USING TRSELD,R2                                                        
         MVI   TRSEL,TRSELQ        CREATE A STATUS ELEMENT                      
         MVI   TRSLN,TRSLNQ                                                     
*                                                                               
         SR    RF,RF                                                            
         IC    RF,TRSLN                                                         
         AR    R2,RF                                                            
*                                                                               
         USING ACKEYD,RF                                                        
         LR    R3,R2                                                            
         LA    RF,IO                                                            
         SR    R3,RF                                                            
         STCM  R3,3,ACLENGTH       GET LENGTH AND FIX THE STATUS                
         NI    ACSTATUS,X'80'                                                   
*                                                                               
         MVI   TMSSWT,X'80'        INDICATE THAT WE INITIALIZED                 
         L     R4,ATMSIO                                                        
         MVI   ELCODE,TIMELQ                                                    
         BAS   RE,GETEL                                                         
         BNE   BUILDN                                                           
         ST    R4,ANEXTEL          SHOULD ALWAYS GET A 1 FIRST                  
         DROP  R2                                                               
*                                                                               
         USING TIMELD,R4                                                        
         USING TRNELD,R2                                                        
BUILD02  LA    R2,IO               UPDATE FIELDS THAT CHANGE                    
         AH    R2,DATADISP                                                      
         LR    R3,R2               SAVE THIS SPOT                               
*                                                                               
         OC    ANEXTEL,ANEXTEL     ANY MORE DATA?                               
         BZ    BUILDN              NO                                           
         L     R4,ANEXTEL          YES                                          
*                                                                               
         MVC   TRNDATE,TIMADAT                                                  
         MVC   TRNOFFC,TIMOFF                                                   
*                                                                               
         MVC   WORK,SPACES                                                      
         MVC   WORK(1),TIMMOA                                                   
         OI    WORK,X'F0'                                                       
         SR    RE,RE                                                            
         IC    RE,TIMMOA+1                                                      
         LA    RE,MOSTAB-1(RE)                                                  
         MVC   WORK+1(1),0(RE)                                                  
         MVC   TRNMOS,WORK                                                      
*                                                                               
         MVI   TRNNARR,C' '                                                     
         MVC   TRNNARR+1(L'TRNNARR-1),TRNNARR                                   
*                                                                               
         SR    RF,RF                                                            
         IC    RF,TRNLN                                                         
         AR    R2,RF                                                            
*                                                                               
         CLI   CONTR1N,C'Y'        CONTRA 1N?                                   
         BE    BUILD05             YES, SKIP 40 AND 51 ELEMENTS                 
*                                                                               
         USING PRTELD,R2                                                        
         XC    PRTSTRT,PRTSTRT     CLEAR IN CASE NO DATA                        
         ZAP   PRTRATE,=P'0'                                                    
         MVC   PRTHOUR,TIMHRS                                                   
*                                                                               
         MVI   PRTSTAT,X'00'                                                    
         CLI   TIMTTYP,TIMTCB      IS THIS CLIENT TIME?                         
         BNE   *+8                 NO                                           
         MVI   PRTSTAT,PRTSBILQ    YES                                          
         CLI   TIMTTYP,TIMTCR      REALIZATION TIME?                            
         BNE   *+8                 NO                                           
         MVI   PRTSTAT,PRTSRTEQ    YES                                          
         CLI   TIMTTYP,TIMTCN      NON-BILLABLE TIME?                           
         BNE   BUILD03             NO                                           
         MVI   PRTSTAT,PRTSNOTQ    YES                                          
         B     BUILD04                                                          
                                                                                
BUILD03  TM    TIMLN,TIMILN2Q      DOES ELEMENT HAVE DATA I NEED?               
         BL    *+16                NO, SKIP OVER SOME STUFF                     
         MVC   PRTSTRT,TIMREFF                                                  
         MVC   PRTRATE,TIMRATE                                                  
*                                                                               
BUILD04  TM    TIMIND,TIMIADJ      RATE ADJUSTED?                               
         BZ    *+8                                                              
         OI    PRTSTAT,PRTSADJ                                                  
*                                                                               
         SR    RF,RF                                                            
         IC    RF,PRTLN                                                         
         AR    R2,RF                                                            
*                                                                               
         USING PCIELD,R2                                                        
         MVC   PCICLI,SPACES                                                    
         MVC   PCIPRJT,SPACES                                                   
*                                                                               
         MVC   PCITSK,TIMTSK                                                    
         MVC   PCICLI(1),QCOMPANY                                               
         MVC   PCICLI+1(L'TIMACC),TIMACC                                        
         MVC   PCIPRJT,PCICLI                                                   
*                                                                               
         SR    RF,RF                                                            
         IC    RF,PCILN                                                         
         AR    R2,RF                                                            
*                                                                               
         USING SCIELD,R2                                                        
BUILD05  ZAP   SCIAMNT,TIMHRS                                                   
                                                                                
         SR    RF,RF                                                            
         IC    RF,SCILN                                                         
         AR    R2,RF                                                            
*                                                                               
         USING TRSELD,R2                                                        
         MVC   TRSPMOS,TIMMOA                                                   
         DROP  R2                                                               
*                                                                               
BUILD06  MVI   ELCODE,TIMELQ                                                    
         BAS   RE,NEXTEL                                                        
         BNE   BUILD10             CLEAR ANEXTEL                                
         CLI   TIMETYP,TIMEINP     SET ANEXTEL IF INPUT DETAILS                 
         BE    BUILD08                                                          
         CLI   TIMETYP,TIMETAX     SKIP TAX AND COMMENTS                        
         BE    BUILD06                                                          
         CLI   TIMETYP,TIMEFLD                                                  
         BE    BUILD06                                                          
         CLI   TIMETYP,TIMENAR                                                  
         BNE   BUILD06                                                          
*                                                                               
         USING TRNELD,R2                                                        
         LR    R2,R3               GET BACK TO 44 ELEMENT                       
         SR    RF,RF                                                            
         IC    RF,TIMLN            GET LENGTH OF NARRATIVE                      
         SH    RF,=Y(TIMNARR-TIMEL)                                             
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     BUILD06                                                          
         MVC   TRNNARR(0),TIMNARR                                               
         DROP  R2                                                               
*                                                                               
BUILD08  ST    R4,ANEXTEL          SAVE A(NEXTEL) FOR NEXT TIME                 
         B     BUILDY              RETURN                                       
*                                                                               
BUILD10  XC    ANEXTEL,ANEXTEL     CLEAR FOR NEXT TIME                          
*                                                                               
BUILDY   CLI   *+1,0               SET CONDITION CODE                           
         B     BUILDX                                                           
*                                                                               
BUILDN   CLI   *+0,0                                                            
*                                                                               
BUILDX   XIT1  ,                                                                
         DROP  RF                                                               
         EJECT                                                                  
*              PROCESS SUBSIDIARY TRANSACTION ELEMENTS                          
         SPACE 3                                                                
TRANSELS NTR1                                                                   
         ZAP   ACIODR,=P'0'                                                     
         ZAP   ACIOCR,=P'0'                                                     
         L     R4,ADTRNREC                                                      
         AH    R4,DATADISP                                                      
         SPACE 1                                                                
TELS     ZIC   R1,1(R4)                                                         
         AR    R4,R1                                                            
         MVI   ACSUBMOD,0                                                       
         ST    R4,ADCUREL          PASS PRESENT ELEMENT                         
         CLI   0(R4),0                                                          
         BE    XIT                                                              
         CLI   0(R4),X'44'                                                      
         BE    TELS44                                                           
         CLI   0(R4),X'50'                                                      
         BE    TELS50                                                           
*&&US                                                                           
         CLI   0(R4),X'4B'                                                      
         BE    TELS4B                                                           
*&&                                                                             
         B     TELS                                                             
         SPACE 1                                                                
         USING TRANSD,R4                                                        
TELS44   MVC   SAVESTAT,TRNSSTAT                                                
         B     TELS                                                             
         SPACE 1                                                                
TELS50   BAS   RE,POSTSUBC                                                      
         MVI   ACMODE,PROCTRNS                                                  
         MVI   ACSUBMOD,PROC50                                                  
         BAS   RE,GO                                                            
         B     TELS                                                             
         SPACE 1                                                                
*&&US                                                                           
TELS4B   MVI   ACMODE,PROCTRNS                                                  
         MVI   ACSUBMOD,PROC4B                                                  
         BAS   RE,GO                                                            
         B     TELS                                                             
*&&                                                                             
         EJECT                                                                  
*              READ HIGH TO NEXT CONTRA                                         
         SPACE 1                                                                
         USING ACKEYD,R2                                                        
TRNSKIP  LA    R2,KEY                                                           
         MVI   ACKEYDTE,X'FF'                                                   
         BAS   RE,HIGH                                                          
         B     HISTREC                                                          
         SPACE 1                                                                
*              READ HIGH TO NEXT WORK CODE                                      
         SPACE 1                                                                
WORKSKIP LA    R2,KEY                                                           
         MVI   ACKEYCON,X'FF'                                                   
         BAS   RE,HIGH                                                          
         B     HISTREC                                                          
         EJECT                                                                  
*              SKIPPING DIFFERENT LEVELS                                        
         SPACE 3                                                                
         USING ACHEIRD,R4                                                       
LEVASKIP L     R4,ADLDGHIR                                                      
         ZIC   R1,ACHRLEVA                                                      
         B     ALLSKIP                                                          
         SPACE 1                                                                
LEVBSKIP L     R4,ADLDGHIR                                                      
         ZIC   R1,ACHRLEVB                                                      
         B     ALLSKIP                                                          
         SPACE 1                                                                
LEVCSKIP L     R4,ADLDGHIR                                                      
         ZIC   R1,ACHRLEVC                                                      
         B     ALLSKIP                                                          
         SPACE 1                                                                
LEVDSKIP L     R4,ADLDGHIR                                                      
         ZIC   R1,ACHRLEVD                                                      
         B     ALLSKIP                                                          
         SPACE 1                                                                
ALLSKIP  LA    R2,KEY+3(R1)                                                     
         MVI   0(R2),X'FF'                                                      
         BAS   RE,HIGH                                                          
         ZIC   R1,EXQLN                                                         
         EX    R1,KYCMP            TEST FOR SAME COMP/UNIT ETC.                 
         BNE   ENDRQ                                                            
         B     ACCREC                                                           
         EJECT                                                                  
*              PROCESS HIGH LEVEL ACCOUNTS                                      
         SPACE 3                                                                
ACCREC   LA    R2,KEY                                                           
         CLC   ACKEYWRK-ACKEYD(ACKEYSBR-ACKEYWRK,R2),SPACES                     
         BNE   NEXTREC             NOT SPACES CAN'T BE ACCOUNT                  
         CLC   ACKEYACC(2),KEYSAVE                                              
         BNE   UNITBRK             CHANGE OF UNIT                               
         CLC   ACKEYACC(3),KEYSAVE                                              
         BNE   LEDGBRK             CHANGE OF LEDGER                             
         L     R4,ADLDGHIR                                                      
         USING ACHEIRD,R4                                                       
         ZIC   R1,ACHRLEVA                                                      
         BCTR  R1,0                                                             
         EX    R1,ACCMP            CLC KEY+3(0),KEYSAVE+3                       
         BNE   LEVABRK             LEVEL A BREAK                                
         ZIC   R1,ACHRLEVB                                                      
         BCTR  R1,0                                                             
         EX    R1,ACCMP                                                         
         BNE   LEVBBRK             LEVEL B BREAK                                
         ZIC   R1,ACHRLEVC                                                      
         BCTR  R1,0                                                             
         EX    R1,ACCMP                                                         
         BNE   LEVCBRK             LEVEL C BREAK                                
         B     LEVDBRK             MUST BE 4 LEVEL                              
         SPACE 1                                                                
ENDRQ    MVI   ACMODE,REQLAST                                                   
         BAS   RE,GO                                                            
         B     XIT                 USER RETURN AFTER END                        
         SPACE 1                                                                
KYCMP    CLC   KEY(0),KEYSAVE                                                   
ACCMP    CLC   KEY+3(0),KEYSAVE+3                                               
         DROP  R2                                                               
         EJECT                                                                  
*              PASS MODE/RECORD TO USER                                         
         SPACE 3                                                                
GO       NTR1                                                                   
         L     RF,ACIOHOOK                                                      
         L     RE,USERRD                                                        
         LM    R0,RC,20(RE)                                                     
         BASR  RE,RF                                                            
         XIT1                                                                   
         EJECT                                                                  
*              HANDLE BUDGETS FOR ACCOUNT IF NEEDED                             
         SPACE 3                                                                
ANYBUDG  NTR1                                                                   
         OC    QBUDGET,QBUDGET                                                  
         BZ    XIT                                                              
***      CLI   FCRDBUDG,C'Y'                                                    
***      BNE   XIT                                                              
         CLI   BUDGLEV,0           IF WE KNOW THE LEVEL                         
         BE    BUDG1                                                            
         CLC   BUDGLEV,THISLEV     CHECK WE ARE AT THAT LEVEL                   
         BNE   XIT                                                              
         SPACE 1                                                                
BUDG1    MVC   MAINKEY,KEY         SAVE MAIN KEY                                
         XC    KEY,KEY                                                          
         LA    R4,KEY                                                           
         USING ACBTKEY,R4                                                       
         MVI   ACBTKTYP,ACBTKTEQ                                                
         MVC   ACBTKACC,MAINKEY    TAKE ACCOUNT FROM MAIN KEY                   
         BAS   RE,HIGH                                                          
         B     BUDG4                                                            
         SPACE 1                                                                
BUDG2    BAS   RE,SEQ                                                           
         SPACE 1                                                                
BUDG4    CLC   KEY(16),KEYSAVE                                                  
         BE    BUDG6                                                            
         MVC   KEY,MAINKEY         RESTORE MAIN ACCIO SEQUENCE                  
         BAS   RE,HIGH                                                          
         B     XIT                                                              
         SPACE 1                                                                
BUDG6    CLI   QBUDGET,X'FF'       MIGHT NEED ALL BUDGETS                       
         BNE   BUDG7                                                            
         CLC   QBUDGET,ACBTKBNO    ELSE CHECK MATCH ON BUDGET NUMBER            
         BNE   BUDG2                                                            
         BAS   RE,SETBLEV                                                       
         SPACE 1                                                                
BUDG7    LA    R4,IO               SAVE ADDRESS OF RECORD                       
         ST    R4,ADBUDGET                                                      
         AH    R4,DATADISP                                                      
         SPACE 1                                                                
BUDG8    CLI   0(R4),0                                                          
         BE    BUDG2                                                            
         CLI   0(R4),X'1D'         LOOK FOR BUDGET AMOUNT ELEMENTS              
         BNE   BUDG10                                                           
         ST    R4,ADBAEL           FOUND ONE SO SAVE ADDRESS                    
         MVI   ACMODE,PROCBUDG                                                  
         BAS   RE,POSTBUDG         DEDUCE VALUES FOR IT                         
         MVI   FILTLEV,C'H'                                                     
         BAS   RE,TESTFILT         POSSIBLE LOW-LEVEL FILTERING                 
         BNE   BUDG10                                                           
         BAS   RE,GO               AND LET USER HAVE A LOOK                     
         SPACE 1                                                                
BUDG10   ZIC   R1,1(R4)                                                         
         AR    R4,R1                                                            
         B     BUDG8                                                            
         EJECT                                                                  
*              CHECK TO ESTABLISH LEVEL OF BUDGET                               
         SPACE 3                                                                
SETBLEV  NTR1                                                                   
         CLI   BUDGLEV,0                                                        
         BNE   XIT                                                              
         L     R4,ADLDGHIR                                                      
         USING ACHEIRD,R4                                                       
         MVI   BUDGLEV,4                                                        
         ZIC   R1,ACHRLEVC                                                      
         LA    R1,KEY+4(R1)                                                     
         CLI   0(R1),C' '                                                       
         BH    XIT                                                              
         MVI   BUDGLEV,3                                                        
         ZIC   R1,ACHRLEVB                                                      
         LA    R1,KEY+4(R1)                                                     
         CLI   0(R1),C' '                                                       
         BH    XIT                                                              
         MVI   BUDGLEV,2                                                        
         ZIC   R1,ACHRLEVA                                                      
         LA    R1,KEY+4(R1)                                                     
         CLI   0(R1),C' '                                                       
         BH    XIT                                                              
         MVI   BUDGLEV,1                                                        
         B     XIT                                                              
         EJECT                                                                  
*              SET UP A(WORK CODE IN KEYS)                                      
         SPACE 3                                                                
SETAWC   NTR1                                                                   
         LA    R4,ACIOKEY                                                       
         USING ACKEYACC,R4                                                      
         LA    R2,ACKEYWRK         DIRECTLY ADDRESSED FOR PRODUCTION            
         CLC   QUNIT(2),=C'SJ'                                                  
         BE    SETAWCX                                                          
         XC    WORK,WORK                                                        
         MVC   WORK(4),=C'A086'    OTHERWISE NEED THE 86 PROFILE                
         MVC   WORK+4(1),QCOMPANY                                               
         MVC   WORK+12(2),ACIOAGYA                                              
         GOTO1 CGETPROF,DMCB,WORK,WORK+16,CDATAMGR                              
         ZIC   R2,WORK+16+1                                                     
         LTR   R2,R2                                                            
         BZ    *+8                                                              
         SH    R2,=H'2'                                                         
         LA    R2,ACKEYCON+1(R2)                                                
         CLI   WORK+16+3,C'Y'                                                   
         BE    *+8                                                              
         LA    R2,1(R2)                                                         
         SPACE 1                                                                
SETAWCX  ST    R2,AWRKCODE                                                      
         B     XIT                                                              
         EJECT                                                                  
*              BUILD ANY LISTS WE MAY NEED                                      
         SPACE 3                                                                
GETLISTS NTR1                                                                   
         LA    R2,ALISTUL          ACCOUNT LIST                                 
         LA    R3,QFILTAL                                                       
         BAS   RE,SETLIST                                                       
         LA    R2,CLISTUL          CONTRA LIST                                  
         LA    R3,QFILTCL                                                       
         BAS   RE,SETLIST                                                       
         LA    R2,WLISTUL          WORK LIST                                    
         LA    R3,QFILTWL                                                       
         BAS   RE,SETLIST                                                       
         B     XIT                                                              
         SPACE 1                                                                
SETLIST  NTR1                                                                   
         XC    0(253,R2),0(R2)     CLEAR UL ENTRY LEN AND LIST                  
         CLI   0(R3),X'41'                                                      
         BL    XIT                                                              
         SPACE 1                                                                
         LA    R4,KEY                                                           
         XC    KEY,KEY                                                          
         USING ACLKEY,R4                                                        
         MVI   ACLKCODE,ACLKCEQU                                                
         MVC   ACLKCOMP,QCOMPANY                                                
         MVC   ACLKLIST,0(R3)                                                   
         OI    ACLKLIST,X'40'                                                   
         BAS   RE,HIGH                                                          
         LA    R4,IO                                                            
         MVI   ELCODE,X'1F'                                                     
         BAS   RE,GETEL                                                         
         BNE   XIT                                                              
         USING ACLDATAD,R4                                                      
         MVC   0(2,R2),ACLDAUNT    PICK OUT UNIT AND LEDGER                     
         MVC   2(1,R2),ACLDITLN             ITEM LENGTH                         
         ZIC   R1,ACLDLEN                                                       
         SH    R1,=H'11'                                                        
         EX    R1,*+8                                                           
         B     XIT                                                              
         MVC   3(0,R2),ACLDACCS             AND THE LIST                        
         EJECT                                                                  
*              ROUTINE TO BUILD OFFICE LIST                                     
         SPACE 3                                                                
OFFLIST  NTR1                                                                   
         OC    ACIOAOGB,ACIOAOGB   WAS A BUFFER PROVIDED?                       
         BZ    XIT                                                              
         CLC   QUNIT(2),=C'SJ'     PRODUCTION ONLY                              
         BNE   XIT                                                              
         L     R2,ACIOAOGB         PICK UP PRESENT BUFFER POSITION              
         LR    R3,R2                                                            
         A     R3,ACIOLOGB                                                      
         BCTR  R3,0                R3=A(END OF BUFFER)                          
         MVI   0(R2),X'FF'                                                      
         SPACE 1                                                                
         MVC   KEY,SPACES          BUILD OFFICE KEY                             
         MVI   KEY,X'2C'                                                        
         MVI   KEY+1,X'04'                                                      
         MVC   KEY+2(3),QCOMPANY                                                
         BAS   RE,HIGH                                                          
         B     OL4                                                              
         SPACE 1                                                                
OL2      BAS   RE,SEQ                                                           
         SPACE 1                                                                
OL4      CLC   KEY(5),KEYSAVE                                                   
         BNE   XIT                                                              
         MVI   ELCODE,X'A0'                                                     
         LA    R4,IO                                                            
         BAS   RE,GETEL                                                         
         BNE   OL2                                                              
         USING ACGPD,R4                                                         
         CLI   ACGPCODE,X'41'                                                   
         BL    OL2                                                              
         MVC   0(1,R2),KEY+5       OFFICE                                       
         MVC   1(1,R2),ACGPCODE    OFFICE GROUP                                 
         DROP  R4                                                               
         LA    R2,2(R2)                                                         
         MVI   0(R2),X'FF'                                                      
         CR    R2,R3                                                            
         BL    OL2                                                              
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO BUILD MEDIA LIST                                      
         SPACE 3                                                                
MEDLIST  NTR1                                                                   
         OC    ACIOAMGB,ACIOAMGB   WAS A BUFFER PROVIDED?                       
         BZ    XIT                                                              
         CLC   QUNIT(2),=C'SJ'     PRODUCTION ONLY                              
         BNE   XIT                                                              
         L     R2,ACIOAMGB         PICK UP PRESENT BUFFER POSITION              
         LR    R3,R2                                                            
         A     R3,ACIOLMGB                                                      
         BCTR  R3,0                R3=A(END OF BUFFER)                          
         MVI   0(R2),X'FF'                                                      
         SPACE 1                                                                
         MVC   KEY,SPACES          BUILD MEDIA RECORD KEY                       
         MVI   KEY,X'09'                                                        
         MVC   KEY+1(1),QCOMPANY                                                
         BAS   RE,HIGH                                                          
         B     ML4                                                              
         SPACE 1                                                                
ML2      BAS   RE,SEQ                                                           
         SPACE 1                                                                
ML4      CLC   KEY(2),KEYSAVE                                                   
         BNE   XIT                                                              
         MVI   ELCODE,X'11'                                                     
         LA    R4,IO                                                            
         BAS   RE,GETEL                                                         
         BNE   ML2                                                              
         USING ACMEDIAD,R4                                                      
         CLI   ACMDGRP,X'41'                                                    
         BL    ML2                                                              
         MVC   0(1,R2),KEY+2       MEDIA                                        
         MVC   1(1,R2),ACMDGRP     MEDIA GROUP                                  
         LA    R2,2(R2)                                                         
         MVI   0(R2),X'FF'                                                      
         CR    R2,R3                                                            
         BL    ML2                                                              
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO BUILD WORK CODE LIST                                  
         SPACE 3                                                                
WORKLIST NTR1                                                                   
         OC    ACIOAWGB,ACIOAWGB   WAS A BUFFER PROVIDED?                       
         BZ    XIT                                                              
         CLC   QUNIT(2),=C'SJ'     PRODUCTION ONLY                              
         BNE   XIT                                                              
         L     R2,ACIOAWGB         PICK UP PRESENT BUFFER POSITION              
         LR    R3,R2                                                            
         A     R3,ACIOLWGB                                                      
         BCTR  R3,0                R3=A(END OF BUFFER)                          
         MVI   0(R2),X'FF'                                                      
         SPACE 1                                                                
         MVC   KEY,SPACES          BUILD WORK CODE KEY                          
         MVI   KEY,X'0A'                                                        
         MVC   KEY+1(3),QCOMPANY                                                
         BAS   RE,HIGH                                                          
         B     WL4                                                              
         SPACE 1                                                                
WL2      BAS   RE,SEQ                                                           
         SPACE 1                                                                
WL4      CLC   KEY(4),KEYSAVE                                                   
         BNE   XIT                                                              
         MVI   ELCODE,X'12'                                                     
         LA    R4,IO                                                            
         BAS   RE,GETEL                                                         
         BNE   WL2                                                              
         USING ACANALD,R4                                                       
         CLI   ACANGRUP,C' '       ENTRY IF WORK GROUP SPECIFIED                
         BNH   WL2                                                              
         MVC   0(2,R2),KEY+4       WORK CODE                                    
         MVC   2(1,R2),ACANGRUP    WORK CODE GROUP                              
         LA    R2,3(R2)                                                         
         MVI   0(R2),X'FF'                                                      
         CR    R2,R3                                                            
         BL    WL2                                                              
         DROP  R4                                                               
         B     XIT                                                              
         EJECT                                                                  
*              DEDUCE USEFUL DATA FROM BUDGET AMOUNTS                           
         SPACE 3                                                                
POSTBUDG NTR1                                                                   
         L     R4,ADBUDGET         KEY FIELDS                                   
         USING ACBTKEY,R4                                                       
         MVC   ACIOACC,ACBTKACC                                                 
         MVC   ACIOWORK,ACBTKWRK                                                
         MVC   ACIOCON,ACBTKCON                                                 
         MVC   ACIOTYPE,ACKEYREF                                                
         MVC   ACIOBCOD,ACBTKBNO                                                
         SPACE 1                                                                
         L     R4,ADBAEL           FROM BUDGET AMOUNT                           
         USING ACBAD,R4                                                         
         MVC   ACIOMON,ACBAMNTH                                                 
         ZAP   ACIODR,ACBABUDG                                                  
         ZAP   ACIOCR,=P'0'                                                     
         ZAP   ACIOAMNT,ACIODR                                                  
         MVI   ACIOSBTY,0                                                       
         SPACE 1                                                                
         BAS   RE,POSTMED          MEDIA                                        
         BAS   RE,POSTOFF          OFFICE                                       
         BAS   RE,POSTLIST         GROUP LISTS                                  
         B     XIT                                                              
         EJECT                                                                  
*              DEDUCE USEFUL DATA FROM COMPANY RECORDS                          
         SPACE 3                                                                
POSTCOMP NTR1                                                                   
         L     R4,ADCMPEL          SET COMPANY TYPE                             
         USING ACCOMPD,R4                                                       
         MVI   ACIOTCT,0                                                        
         TM    ACMPSTA3,X'02'                                                   
         BNO   *+8                                                              
         MVI   ACIOTCT,C'D'        TO D IF DPS                                  
         MVC   ACIOAGYA,ACMPALFA   PICK UP ALPHA AGENCY CODE                    
         BAS   RE,SETAWC           SET A(WHERE WORK CODE IS)                    
         DROP  R4                                                               
*                                  (NEEDED ACIOAGYA FOR THIS)                   
         SPACE 1                                                                
*        CLI   ACIOACCS,C'$'       IF LIMIT ACCESS OF OFFICE LIST               
*        BNE   XIT                                                              
*        XC    DMCB(4),DMCB        NEED ADDRESS OF OFFICER                      
*        MVC   DMCB+4(4),=X'D9000A38'                                           
*        GOTO1 CCALLOV,DMCB                                                     
*        MVC   OFFICER,DMCB                                                     
*        LA    R1,DUB                                                           
*        XC    DUB,DUB                                                          
*        USING OFFICED,R1          BUILD KEY TO GET OFFICE LIST                 
*        MVI   OFCSYS,C'A'                                                      
*        MVC   OFCAUTH,ACIOACCS                                                 
*        MVC   OFCAGY(1),QCOMPANY                                               
*        MVC   OFCALPHA,ACIOAGYA                                                
*        DROP  R1                                                               
*        GOTO1 OFFICER,DMCB,DUB,(X'80',ACCOMFAC),OFFCLIST                       
         SPACE 1                                                                
         XC    DMCB(4),DMCB        NEED ADDRESS OF OFFAL                        
         MVC   DMCB+4(4),=X'D9000A62'                                           
         GOTO1 CCALLOV,DMCB                                                     
         MVC   OFFAL,DMCB                                                       
         LA    R1,IO               GET OFFBLOCK ADDRESSABLE                     
         LA    R1,3000(R1)                                                      
         LA    R1,3000(R1)                                                      
         LA    R1,2000(R1)                                                      
         ST    R1,AOFFBLK                                                       
         USING OFFALD,R1                                                        
         MVC   OFFACOMF,ACCOMFAC   INITIALIZE OFFAL BLOCK                       
         MVC   OFFAALPH,ACIOAGYA                                                
         MVC   OFFACPY,QCOMPANY                                                 
         L     RF,ADCMPEL                                                       
         USING ACCOMPD,RF                                                       
         MVC   OFFACST1,ACMPSTAT                                                
         MVC   OFFACST2,ACMPSTA2                                                
         MVC   OFFACST3,ACMPSTA3                                                
         MVC   OFFACST4,ACMPSTA4                                                
         DROP  RF                                                               
         MVC   OFFALIMA,ACIOACCS                                                
         CLI   QFILTNOF+1,C'A'     ANY OFFICE FILTER?                           
         BL    *+10                                                             
         MVC   OFFANEWA,QFILTNOF   YES - USE INSTEAD OF LIM ACCESS              
         MVC   OFFAAUTH,ACIOAUTH                                                
         OC    ACIOAPRT,ACIOAPRT                                                
         BNZ   OFFOFF                                                           
         SPACE 1                                                                
OFFON    OI    OFFAINDS,OFFAIOFF                                                
         MVI   OFFAACT,OFFAINI                                                  
         GOTO1 OFFAL                                                            
         BE    OK                                                               
         B     NOTOK                                                            
         SPACE 1                                                                
*                                  OFFLINE UNDER SPOOF                          
OFFOFF   OI    OFFAINDS,OFFAIOFF+OFFAIOFS                                       
         MVI   OFFAACT,OFFAINI                                                  
         GOTO1 OFFAL                                                            
         BE    OK                                                               
         B     NOTOK                                                            
         DROP  R1                                                               
         EJECT                                                                  
*              DEDUCE USEFUL DATA FROM LEDGER RECORDS                           
         SPACE 3                                                                
POSTLEDG NTR1                                                                   
         LA    R4,ACIOKEY          KEY FIELDS                                   
         USING ACKEYACC,R4                                                      
*&&US                                                                           
         MVI   ACIOTLT,0           SET TALENT LEDGER TYPE                       
         CLC   ACKEYACC+1(2),=C'SN'                                             
         BNE   *+8                                                              
         MVI   ACIOTLT,C'P'        TO P FOR PERFORMER                           
         CLI   ACIOTCT,C'D'                                                     
         BE    POSTLDPS                                                         
         CLC   ACKEYACC+1(2),=C'SM'                                             
         BNE   *+8                                                              
         MVI   ACIOTLT,C'C'        TO C FOR COMMERCIAL                          
         B     POSTLHIR                                                         
         SPACE 1                                                                
POSTLDPS CLI   ACKEYACC+1,C'T'     CHECK DPS FOR UNIT T                         
         BNE   *+8                                                              
         MVI   ACIOTLT,C'C'        C FOR COMMERCIAL                             
*&&                                                                             
         SPACE 1                                                                
POSTLHIR CLI   QACCOUNT,X'41'      WAS ACCOUNT SPECIFIED?                       
         BL    XIT                                                              
*                                  RECOMPUTE COMPARE LENGTH                     
         LA    R1,QACCOUNT+11      GET LENGTH INTO BYTE                         
         LA    R2,12                                                            
         CLI   0(R1),X'40'                                                      
         BH    *+10                                                             
         BCTR  R1,0                                                             
         BCT   R2,*-10                                                          
         STC   R2,BYTE                                                          
         L     R4,ADLDGHIR         NOW LOOK IN HEIRARCHY ELEMENT                
         USING ACHEIRD,R4                                                       
         LA    R2,ACHRLEVA         FOR LEVEL THAT INCLUDES LENGTH               
         LA    R0,4                                                             
         SPACE 1                                                                
POSTLH2  CLC   BYTE,0(R2)                                                       
         BNH   POSTLH4                                                          
         LA    R2,16(R2)                                                        
         BCT   R0,POSTLH2                                                       
         B     XIT                                                              
         SPACE 1                                                                
POSTLH4  ZIC   R1,0(R2)                                                         
         LA    R1,2(R1)            +3 (FOR CUL) -1 FOR EX                       
         STC   R1,EXQLN            SAVE ACCOUNT LENGTH                          
         B     XIT                                                              
         EJECT                                                                  
*              DEDUCE USEFUL DATA FROM ACCOUNT RECORDS                          
         SPACE 3                                                                
POSTACC  NTR1                                                                   
         LA    R4,ACIOKEY          KEY FIELDS                                   
         USING ACKEYACC,R4                                                      
         MVC   ACIOACC,ACKEYACC                                                 
         XC    ACIOWORK,ACIOWORK                                                
         XC    ACIOCON,ACIOCON                                                  
         BAS   RE,POSTMED                                                       
         BAS   RE,POSTOFF                                                       
         BAS   RE,POSTLIST         GROUP LISTS                                  
         SPACE 1                                                                
         L     R4,ADACCSTA         CHECK FOR P&L OR BAL SHEET                   
         USING ACSTATD,R4                                                       
         TM    ACSTSTX,X'04'                                                    
         BO    POSTPNL                                                          
         TM    ACSTSTX,X'02'                                                    
         BO    POSTBALS                                                         
         SPACE 1                                                                
         L     R4,ADLDGSTA         CHECK IN LEDGER STATUS AS WELL               
         USING ACSTATD,R4                                                       
         TM    ACSTSTX,X'04'                                                    
         BO    POSTPNL                                                          
         TM    ACSTSTX,X'02'                                                    
         BO    POSTBALS                                                         
         SPACE 1                                                                
         LA    R4,ACIOKEY          IF NOT SPECIFIED, TEST U/L                   
         USING ACKEYACC,R4                                                      
         CLC   ACKEYACC+1(2),=C'GP'                                             
         BE    POSTPNL                                                          
         CLC   ACKEYACC+1(2),=C'SE'                                             
         BE    POSTPNL                                                          
         CLC   ACKEYACC+1(2),=C'SI'                                             
         BE    POSTPNL                                                          
         SPACE 1                                                                
POSTBALS MVI   ACIOBFT,C'B'                                                     
         B     XIT                                                              
         SPACE 1                                                                
POSTPNL  MVI   ACIOBFT,C'P'                                                     
         B     XIT                                                              
         EJECT                                                                  
*              DEDUCE USEFUL DATA FROM HISTORY RECORDS                          
         SPACE 3                                                                
POSTHIST NTR1                                                                   
         LA    R4,ACIOKEY          KEY FIELDS                                   
         USING ACKEYACC,R4                                                      
         MVC   ACIOACC,ACKEYACC                                                 
         L     R1,AWRKCODE                                                      
         MVC   ACIOWORK,0(R1)                                                   
         MVC   ACIOCON,ACKEYCON                                                 
         MVC   ACIOTYPE,ACKEYREF                                                
         MVC   ACIOTYP2,ACKEYREF+1                                              
         XC    ACIOBT,ACIOBT       (N/A FOR HISTORY)                            
         SPACE 1                                                                
         L     R4,ADHIST           FROM HISTORY ELEMENT                         
         MVI   ACIOSBTY,0                                                       
         CLI   0(R4),X'55'         MIGHT BE AN X'55' ELEMENT                    
         BE    POSTH55                                                          
         USING TRHISTD,R4                                                       
         MVC   ACIOMON,TRHSYEAR                                                 
         ZAP   ACIODR,TRHSDR                                                    
         ZAP   ACIOCR,TRHSCR                                                    
         B     POSTHEND                                                         
         SPACE 1                                                                
         USING ACPBKD,R4                                                        
POSTH55  MVC   ACIOMON,ACPBKHI     PICK VALUES FROM 55 ELEMENT                  
         ZAP   ACIODR,ACPBKDR                                                   
         ZAP   ACIOCR,ACPBKCR                                                   
         SPACE 1                                                                
POSTHEND ZAP   ACIOAMNT,ACIODR                                                  
         SP    ACIOAMNT,ACIOCR                                                  
         BAS   RE,POSTMED          MEDIA                                        
         BAS   RE,POSTOFF          OFFICE                                       
         BAS   RE,POSTLIST         GROUP LISTS                                  
         B     XIT                                                              
         SPACE 1                                                                
POSTWORK NTR1                                                                   
         LA    R4,ACIOKEY          WORKCODE ONLY                                
         USING ACKEYACC,R4                                                      
         MVC   ACIOACC,ACKEYACC                                                 
         L     R1,AWRKCODE                                                      
         MVC   ACIOWORK,0(R1)                                                   
         BAS   RE,POSTMED          MEDIA                                        
         BAS   RE,POSTOFF          OFFICE                                       
         BAS   RE,POSTLIST         GROUP LISTS                                  
         B     XIT                                                              
         EJECT                                                                  
*              DEDUCE USEFUL DATA FROM TRANSACTION RECORDS                      
         SPACE 3                                                                
POSTTRNS NTR1                                                                   
         LA    R4,ACIOKEY          KEY FIELDS                                   
         USING ACKEYACC,R4                                                      
         MVC   ACIOACC,ACKEYACC                                                 
         L     R1,AWRKCODE                                                      
         MVC   ACIOWORK,0(R1)                                                   
         MVC   ACIOCON,ACKEYCON                                                 
         MVC   ACIODTE,ACKEYDTE                                                 
         CLI   QNEEDCD,0           COMPRESSED DATE CONTROL                      
         BE    POSTTR2                                                          
         L     R7,ACCOMFAC                                                      
         USING COMFACSD,R7                                                      
         GOTO1 CDATCON,DMCB,(1,ACIODTE),(2,ACIOCDAT)                            
         SPACE 1                                                                
POSTTR2  L     R4,ADTRANS          FROM TRANSACTION                             
         USING TRANSD,R4                                                        
         EDIT  (1,TRNSTYPE),(2,ACIOBT),FILL=0                                   
         GOTO1 CONVMOS,DMCB,(R4),ACIOMON                                        
         ZAP   ACIODR,=P'0'                                                     
         TM    TRNSSTAT,X'80'                                                   
         BNO   *+10                                                             
         ZAP   ACIODR,TRNSAMNT                                                  
         ZAP   ACIOCR,=P'0'                                                     
         TM    TRNSSTAT,X'80'                                                   
         BO    *+10                                                             
         ZAP   ACIOCR,TRNSAMNT                                                  
         ZAP   ACIOAMNT,ACIODR                                                  
         SP    ACIOAMNT,ACIOCR                                                  
         SPACE 1                                                                
         MVI   ACIOREV,C'N'                                                     
         TM    TRNSSTAT,X'20'                                                   
         BNO   *+8                                                              
         MVI   ACIOREV,C'Y'                                                     
         BAS   RE,POSTMED          MEDIA                                        
         BAS   RE,POSTOFF          OFFICE                                       
         BAS   RE,POSTLIST         GROUP LISTS                                  
         SPACE 1                                                                
         MVI   ACIOSBTY,0                                                       
         MVI   ACIORTYP,0                                                       
         L     R4,ADTRNREC         LOOK FOR RTIME TYPE                          
         MVI   ELCODE,X'40'                                                     
         BAS   RE,GETEL                                                         
         BNE   POSTTR4                                                          
         USING ACPERSD,R4                                                       
         TM    ACPSSTAT,ACPSBIL                                                 
         BNO   *+8                                                              
         MVI   ACIORTYP,C'B'                                                    
         TM    ACPSSTAT,ACPSNOT                                                 
         BNO   *+8                                                              
         MVI   ACIORTYP,C'N'                                                    
         TM    ACPSSTAT,ACPSRTE                                                 
         BNO   *+8                                                              
         MVI   ACIORTYP,C'R'                                                    
         SPACE 1                                                                
POSTTR4  MVI   ACIOTTYP,C'N'       LOOK FOR TIME TYPE - ASSUME NONE             
         L     R4,ADTRNREC                                                      
         MVI   ELCODE,X'60'                                                     
         BAS   RE,GETEL                                                         
         BNE   POSTTR6                                                          
         USING TRSELD,R4                                                        
         TM    TRSSTAT2,X'08'      ADJUSTED?                                    
         BNO   *+8                                                              
         MVI   ACIOTTYP,C'A'                                                    
         TM    TRSSTAT2,X'04'      MISSING?                                     
         BNO   *+8                                                              
         MVI   ACIOTTYP,C'M'                                                    
         TM    TRSSTAT2,X'01'      TIME?                                        
         BNO   *+8                                                              
         MVI   ACIOTTYP,C'T'                                                    
         SPACE 1                                                                
POSTTR6  CLC   QUNIT(2),=C'1R'     WORKCODE IN DIFFERENT PLACE ON 1R            
         BNE   XIT                                                              
         L     R4,ADTRNREC                                                      
         MVI   ELCODE,X'51'                                                     
         BAS   RE,GETEL                                                         
         BNE   XIT                                                              
         USING ACPCD,R4                                                         
         MVC   ACIOWORK,ACPCTSK                                                 
         B     XIT                                                              
         SPACE 1                                                                
POSTSUBC NTR1                      POST SUBSIDIARY CASH                         
         USING TRCASHD,R4                                                       
         MVC   ACIOSBTY,TRCSTYPE                                                
         TM    SAVESTAT,X'80'      IF TRANS IS DEBIT                            
         BNO   POSTSBCR                                                         
         ZAP   ACIODR,TRCSAMNT        SUB IS DEBIT                              
         B     XIT                                                              
         SPACE 1                                                                
POSTSBCR ZAP   ACIOCR,TRCSAMNT     ELSE SUB IS CREDIT                           
         B     XIT                                                              
         EJECT                                                                  
*              FIGURE OUT MEDIA CODE                                            
         SPACE 3                                                                
POSTMED  NTR1                                                                   
         MVI   ACIOMED,0           ONLY VALID FOR PRODUCTION                    
         CLC   ACIOACC+1(2),=C'SJ'                                              
         BNE   XIT                                                              
         L     R1,ADLDGHIR                                                      
         USING ACHEIRD,R1                                                       
         ZIC   R0,ACHRLEVB         PICK UP L'PRODUCT CODE                       
         DROP  R1                                                               
         LA    R1,ACIOACC+3                                                     
         AR    R1,R0               DISPLACE TO JOB NUMBER                       
         MVC   ACIOMED,0(R1)       FIRST CHARACTER IS MEDIA CODE                
         B     XIT                                                              
         EJECT                                                                  
*              FIGURE OUT OFFICE CODE                                           
         SPACE 3                                                                
POSTOFF  NTR1                                                                   
         XC    ACIONOFF,ACIONOFF                                                
         L     R1,ADLDGEL                                                       
         USING ACLEDGD,R1                                                       
         CLI   ACLTOFF,C'C'        OFFICE TYPE IN LEDGER ELEMENT                
         BE    POSTOC                                                           
         CLI   ACLTOFF,C'T'                                                     
         BE    POSTOT                                                           
         CLI   ACLTOFF,0           (DEFAULT IS TRANS)                           
         BE    POSTOT                                                           
         CLI   ACLTOFF,C'P'                                                     
         BE    POSTOP                                                           
         CLI   ACLTOFF,C'1'                                                     
         BE    POSTOF1                                                          
         CLI   ACLTOFF,C'2'                                                     
         BE    POSTOF2                                                          
         CLI   ACLTOFF,C'3'                                                     
         BE    POSTOF3                                                          
         CLI   ACLTOFF,C'4'                                                     
         BE    POSTOF4                                                          
         ZIC   R2,ACLTOFF          1-12=POSITION IN ACCOUNT                     
         SLL   R2,28                                                            
         SRL   R2,28                                                            
         CH    R2,=H'12'                                                        
         BH    POSTOFFX                                                         
         DROP  R1                                                               
         LA    R4,ACIOKEY                                                       
         USING ACKEYACC,R4                                                      
         LA    R2,ACKEYACC+2(R2)                                                
         DROP  R4                                                               
         MVC   ACIONOFF,0(R2)                                                   
         B     POSTOFFX                                                         
         SPACE 1                                                                
POSTOC   L     R1,ADLVAPRO         CLIENT TYPE                                  
         LTR   R1,R1                                                            
         BZ    POSTOFFX                                                         
         USING ACPROFD,R1                                                       
*                                  OFFICE IS IN PROFILE ELEMENT                 
         MVC   ACIONOFF,ACPROFFC                                                
         DROP  R1                                                               
         B     POSTOFFX                                                         
         SPACE 1                                                                
POSTOP   L     R1,ADLVBPRO         PRODUCT TYPE FROM PROD OR CLI                
         LTR   R1,R1                                                            
         BZ    POSTOC                                                           
         USING ACPROFD,R1                                                       
*                                  OFFICE IS IN PROFILE ELEMENT                 
         MVC   ACIONOFF,ACPROFFC                                                
         CLI   ACIONOFF,C'A'       IF NOTHING SIGNIFICANT                       
         BL    POSTOC                 USE CLIENT                                
         DROP  R1                                                               
         B     POSTOFFX                                                         
         SPACE 1                                                                
POSTOT   CLI   ACMODE,PROCTRNS     OFFICE IS IN TRANSACTION                     
         BNE   POSTOFFX            SO REJECT OTHERS                             
         L     R1,ADTRANS                                                       
         USING TRANSD,R1                                                        
         MVC   ACIONOFF,TRNSOFFC                                                
         CLI   ACIONOFF,C' '       IF MISSING                                   
         BH    POSTOFFX                                                         
         MVC   ACIONOFF,=C'??'        RETURN ??                                 
         B     POSTOFFX                                                         
         SPACE 1                                                                
POSTOF1  DS    0H                  F1-F4 FROM FILTERS                           
         MVC   ACIONOFF,ACIOFILT                                                
         B     POSTOFFX                                                         
         SPACE 1                                                                
POSTOF2  MVC   ACIONOFF,ACIOFILT+1                                              
         B     POSTOFFX                                                         
         SPACE 1                                                                
POSTOF3  MVC   ACIONOFF,ACIOFILT+2                                              
         B     POSTOFFX                                                         
         SPACE 1                                                                
POSTOF4  MVC   ACIONOFF,ACIOFILT+3                                              
         B     POSTOFFX                                                         
         SPACE 1                                                                
POSTOFFX CLI   ACIONOFF,X'40'      RETURN SOMETHING SIGNIFICANT                 
         BH    *+10                                                             
         XC    ACIONOFF,ACIONOFF                                                
         MVC   ACIOOFF,ACIONOFF    RETURN OLD OFFICE CODE FOR NOW               
         B     XIT                                                              
         EJECT                                                                  
*              OFFICE, MEDIA WORK GROUPS FROM LISTS                             
         SPACE 3                                                                
POSTLIST NTR1                                                                   
         CLC   QUNIT(2),=C'SJ'     ONLY GOOD FOR PRODUCTION                     
         BNE   XIT                                                              
         L     R2,ACIOAOGB         OFFICE GROUP                                 
         LA    R3,ACIOOFF                                                       
         LA    R4,ACIOOG                                                        
         BAS   RE,POSTOM                                                        
         L     R2,ACIOAMGB         MEDIA GROUP                                  
         LA    R3,ACIOMED                                                       
         LA    R4,ACIOMG                                                        
         BAS   RE,POSTOM                                                        
         L     R2,ACIOAWGB         WORK CODE GROUP                              
         LA    R3,ACIOWORK                                                      
         LA    R4,ACIOWG                                                        
         BAS   RE,POSTWG                                                        
         B     XIT                                                              
         SPACE 1                                                                
POSTOM   MVI   0(R4),X'00'         PRESET BINARY ZERO                           
         LTR   R2,R2               MUST BE A BUFFER                             
         BZR   RE                                                               
         SPACE 1                                                                
POSTOM2  CLI   0(R2),X'FF'         END OF LIST?                                 
         BER   RE                                                               
         CLC   0(1,R2),0(R3)       MATCH ON CODE?                               
         BE    POSTOM4                                                          
         LA    R2,2(R2)                                                         
         B     POSTOM2                                                          
         SPACE 1                                                                
POSTOM4  MVC   0(1,R4),1(R2)       FOUND - RETURN GROUP                         
         BR    RE                                                               
         SPACE 1                                                                
POSTWG   MVI   0(R4),X'00'         PRESET BINARY ZERO                           
         LTR   R2,R2               MUST BE A BUFFER                             
         BZR   RE                                                               
         SPACE 1                                                                
POSTWG2  CLI   0(R2),X'FF'         END OF LIST?                                 
         BER   RE                                                               
         CLC   0(2,R2),0(R3)       MATCH ON CODE?                               
         BE    POSTWG4                                                          
         LA    R2,3(R2)                                                         
         B     POSTWG2                                                          
         SPACE 1                                                                
POSTWG4  MVC   0(1,R4),2(R2)       FOUND - RETURN GROUP                         
         BR    RE                                                               
         EJECT                                                                  
*              SET ADDRESSESS OF USEFUL ELEMENTS                                
*              R1    A(ELEMENT LIST)                                            
*              R2    A(ADDRESS LIST)                                            
         SPACE 3                                                                
SETLADR  NTR1                                                                   
         SPACE 1                                                                
*              FIND NEXT AVAILABLE AREA                                         
         SPACE 1                                                                
         LA    R5,RECAREA                                                       
         LA    R5,3000(R5)                                                      
*                                  START OF ELEMENT BUFFER                      
         OC    ADNEXT,ADNEXT       IS THIS FIRST TIME                           
         BZ    SETLNXT             YES, R5 IS AT START                          
         L     R5,ADNEXT           SET R5 TO NEXT AVAILABLE AREA                
         OC    0(4,R2),0(R2)       HAS THIS LEVEL BEEN USED                     
         BZ    SETLNXT             FIRST TIME FOR THIS LEVEL                    
         L     R5,0(R2)            REUSE IF NOT FIRST TIME                      
         SPACE 1                                                                
SETLNXT  ST    R5,ADNEXT           SAVE ADDRESS OF NEXT AREA                    
         SPACE 1                                                                
*              CLEAR LOWER LEVEL ADDRESS                                        
         SPACE 1                                                                
         LR    RE,R2               RE TO START OF LIST                          
         LA    RF,ACENDELS         RF TO END OF ELEMENTS                        
         SR    RF,R2               LENGTH TO END                                
         XCEF                                                                   
         SPACE 1                                                                
*              MOVE IN NEW ELEMENTS                                             
         SPACE 1                                                                
SETLAD2  L     R5,ADNEXT                                                        
         SPACE 1                                                                
SETLAD4  CLI   0(R1),X'FF'                                                      
         BE    XIT                 END OF LIST                                  
         MVC   ELCODE,0(R1)                                                     
         LA    R4,IO                                                            
         BAS   RE,GETEL            FIND ELEMENT IN RECOPRD                      
         BNE   SETLAD8             ELEMENT NOT FOUND                            
         ZIC   R3,1(R4)            ELEMENT LENGTH                               
         BCTR  R3,0                                                             
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R5),0(R4)       ELEMENT TO ELBUFF                            
         ST    R5,0(R2)            SAVE AD OF ELEMENT                           
         LA    R5,1(R3,R5)         R5 TO NEXT AREA                              
         ST    R5,ADNEXT                                                        
         SPACE 1                                                                
SETLAD8  LA    R1,1(R1)            NEXT ELEMENT CODE                            
         LA    R2,4(R2)            NEXT ADDRESS                                 
         B     SETLAD4                                                          
         EJECT                                                                  
*              ROUTINE TO SET FILTERS                                           
         SPACE 3                                                                
*              INPUT               THISLEV SET TO 1-4                           
         SPACE 1                                                                
SETFILT  NTR1                                                                   
         USING ACSTATD,R1                                                       
         L     R1,ADLVASTA                                                      
         USING ACSTATD,R1                                                       
         MVC   ACIOF1,ACSTFILT                                                  
         MVC   ACIOF2,ACSTFILT+1                                                
         MVC   ACIOANAL,ACSTANAL                                                
         MVC   ACIOCOST,ACSTCOST                                                
         MVC   ACIOSUB,ACSTSUB                                                  
         MVI   ACIOF5,0                                                         
         CLI   ACSTLEN,ACSTLNQ2                                                 
         BL    *+10                                                             
         MVC   ACIOF5,ACSTFLT5                                                  
         MVI   ACIOCLOS,C'N'                                                    
         MVI   ACIOLOCK,C'N'                                                    
         TM    ACSTSTAT,X'40'                                                   
         BNO   *+8                                                              
         MVI   ACIOCLOS,C'Y'                                                    
         TM    ACSTSTAT,X'20'                                                   
         BNO   *+8                                                              
         MVI   ACIOLOCK,C'Y'                                                    
         CLI   THISLEV,1                                                        
         BE    XIT                                                              
         L     R1,ADLVBSTA                                                      
         BAS   RE,SETF2                                                         
         CLI   THISLEV,2                                                        
         BE    XIT                                                              
         L     R1,ADLVCSTA                                                      
         BAS   RE,SETF2                                                         
         CLI   THISLEV,3                                                        
         BE    XIT                                                              
         L     R1,ADLVDSTA                                                      
         BAS   RE,SETF2                                                         
         B     XIT                                                              
         SPACE 1                                                                
SETF2    NTR1                                                                   
         LTR   R1,R1                                                            
         BZ    XIT                                                              
         TM    ACSTSTAT,X'40'                                                   
         BNO   *+8                                                              
         MVI   ACIOCLOS,C'Y'                                                    
         TM    ACSTSTAT,X'20'                                                   
         BNO   *+8                                                              
         MVI   ACIOLOCK,C'Y'                                                    
         CLI   ACSTFILT,X'41'      ONLY MOVE IN SIGNIFICANT DATA                
         BL    *+10                                                             
         MVC   ACIOFILT(1),ACSTFILT                                             
         CLI   ACSTFILT+1,X'41'                                                 
         BL    *+10                                                             
         MVC   ACIOFILT+1(1),ACSTFILT+1                                         
         CLI   ACSTANAL,X'41'                                                   
         BL    *+10                                                             
         MVC   ACIOFILT+2(1),ACSTANAL                                           
         CLI   ACSTSUB,X'41'                                                    
         BL    *+10                                                             
         MVC   ACIOFILT+3(1),ACSTSUB                                            
         CLI   ACSTCOST,X'41'                                                   
         BL    *+10                                                             
         MVC   ACIOCOST,ACSTCOST                                                
         CLI   ACSTLEN,ACSTLNQ2                                                 
         BL    XIT                                                              
         CLI   ACSTFLT5,X'41'                                                   
         BL    *+10                                                             
         MVC   ACIOF5,ACSTFLT5                                                  
         B     XIT                                                              
         DROP  R1                                                               
         EJECT                                                                  
*              OFFICE FILTERS                                                   
         SPACE 3                                                                
FILTOFF  NTR1                                                                   
         LA    R2,IO                                                            
         USING ACKEYD,R2                                                        
         TM    ACSTATUS,X'80'      DELETED RECORDS                              
         BNZ   NOTOK                                                            
         CLI   ACIONOFF,0          ONLY INTERESTED IF OFFICE                    
         BE    OK                       HAS ALREADY BEEN ESTABLISHED            
         CLI   QFILTNOF,C'@'       SPECIAL OFFICE FILTER                        
         BE    OK                                                               
         LA    R2,QFILTNOF         OFFICE CODE                                  
         LA    R3,ACIONOFF                                                      
         L     R1,ADCMPEL                                                       
         USING ACCOMPD,R1                                                       
         LA    R4,1                1 BYTE OFFICE                                
         TM    ACMPSTA4,X'01'                                                   
         BNO   FILTOFF2                                                         
         LA    R4,2                2 BYTES IF ON NEW OFFICE                     
         CLC   QFILTNOF,=C'??'     UNLESS TESTING FOR MISSINGS                  
         BNE   OK                  THIS WILL BE DONE BY ACCCHECK                
         SPACE 1                                                                
FILTOFF2 BAS   RE,TESTEM                                                        
         LA    R2,QFILTOG          OFFICE GROUP                                 
         LA    R3,ACIOOG                                                        
         LA    R4,1                                                             
         BAS   RE,TESTEM                                                        
         B     OK                                                               
         EJECT                                                                  
*              TEST FILTERS                                                     
         SPACE 3                                                                
*                                  FILTLEV - A=ACCOUNT H=HIST T=TRANS           
         SPACE 1                                                                
TESTFILT NTR1                                                                   
         LA    R2,IO                                                            
         USING ACKEYD,R2                                                        
         TM    ACSTATUS,X'80'      DELETED RECORDS ARE FILTERED                 
         BNZ   NOTOK                                                            
         LA    R2,QFILTERS         FILTER 1-4                                   
         LA    R3,ACIOFILT                                                      
         LA    R4,1                                                             
         BAS   RE,TESTEM                                                        
         LA    R2,QFILTERS+1                                                    
         LA    R3,ACIOFILT+1                                                    
         LA    R4,1                                                             
         BAS   RE,TESTEM                                                        
         LA    R2,QFILTERS+2                                                    
         LA    R3,ACIOFILT+2                                                    
         LA    R4,1                                                             
         BAS   RE,TESTEM                                                        
         LA    R2,QFILTERS+3                                                    
         LA    R3,ACIOFILT+3                                                    
         LA    R4,1                                                             
         BAS   RE,TESTEM                                                        
         LA    R2,QFILTF5                                                       
         LA    R3,ACIOF5                                                        
         LA    R4,1                                                             
         BAS   RE,TESTEM                                                        
         LA    R2,QFILTMED         MEDIA CODE                                   
         LA    R3,ACIOMED                                                       
         LA    R4,1                                                             
         BAS   RE,TESTEM                                                        
         LA    R2,QFILTMG          MEDIA GROUP                                  
         LA    R3,ACIOMG                                                        
         LA    R4,1                                                             
         BAS   RE,TESTEM                                                        
         CLI   QFILTNOF,C'@'       (SPECIAL OFFICE FILTER @)                    
         BE    TFILT3                                                           
         LA    R2,QFILTNOF         OFFICE CODE                                  
         LA    R3,ACIONOFF                                                      
         LA    R4,2                                                             
         L     R1,ADCMPEL                                                       
         USING ACCOMPD,R1                                                       
         TM    ACMPSTA4,X'01'                                                   
         BO    TFILT2B             (2 BYTE OFFICE DONE IN ACCCHECK)             
         LA    R4,1                                                             
         L     R1,ADLDGEL                                                       
         USING ACLEDGD,R1                                                       
         CLI   ACLTOFF,C'T'        IF OFFICE IS IN TRANSACTIONS,                
         BE    TFILT1                                                           
         CLI   ACLTOFF,0                                                        
         DROP  R1                                                               
         BNE   TFILT2                                                           
         SPACE 1                                                                
TFILT1   CLI   FILTLEV,C'T'        ONLY FILTER AT TRANS LEVEL                   
         BNE   TFILT4                                                           
         SPACE 1                                                                
TFILT2   BAS   RE,TESTEM                                                        
         SPACE 1                                                                
TFILT2B  LA    R2,QFILTOG          OFFICE GROUP                                 
         LA    R3,ACIOOG                                                        
         LA    R4,1                                                             
         BAS   RE,TESTEM                                                        
         B     TFILT4                                                           
         SPACE 1                                                                
TFILT3   CLC   ACIOCON+1(2),=C'1R' SPECIAL OFFICE TEST @                        
         BNE   TFILT4                                                           
         CLC   ACIOCON+3(1),ACIONOFF  IGNORE 1R C/A WITH OFFICE MATCH           
         BE    NOTOK                                                            
         SPACE 1                                                                
TFILT4   LA    R2,QFILTAL          ACCOUNT LIST                                 
         LA    R3,ACIOACC+3                                                     
         ZIC   R4,ALISTIL                                                       
         LA    R5,ACCLIST                                                       
         BAS   RE,TESTLIST                                                      
         LA    R2,QFILTLOK         LOCKED STATUS                                
         LA    R3,ACIOLOCK                                                      
         LA    R4,1                                                             
         BAS   RE,TESTEM                                                        
         LA    R2,QFILTCLS         CLOSED STATUS                                
         LA    R3,ACIOCLOS                                                      
         LA    R4,1                                                             
         BAS   RE,TESTEM                                                        
         CLI   FILTLEV,C'A'                                                     
         BE    OK                                                               
         LA    R2,QFILTCA+1        CONTRA ACCOUNT                               
         LA    R3,ACIOCON+1                                                     
         LA    R4,14                                                            
         BAS   RE,TESTEM                                                        
         LA    R2,QFILTWRK         WORK CODE                                    
         LA    R3,ACIOWORK                                                      
         LA    R4,2                                                             
         BAS   RE,TESTEM                                                        
         LA    R2,QFILTWG          WORK GROUP                                   
         LA    R3,ACIOWG                                                        
         LA    R4,1                                                             
         BAS   RE,TESTEM                                                        
         LA    R2,QFILTWL          WORK LIST                                    
         LA    R3,ACIOWORK                                                      
         ZIC   R4,WLISTIL                                                       
         LA    R5,WCODLIST                                                      
         BAS   RE,TESTLIST                                                      
         LA    R2,QFILTCL          CONTRA LIST                                  
         LA    R3,ACIOCON+3                                                     
         ZIC   R4,CLISTIL                                                       
         LA    R5,CACLIST                                                       
         BAS   RE,TESTLCAC                                                      
         SPACE 1                                                                
         CLI   FILTLEV,C'H'        TRANSACTION BASED FILTERS                    
         BE    OK                                                               
         LA    R2,IO                                                            
         TM    ACSTATUS-ACKEYD(R2),X'40'                                        
         BNZ   NOTOK               IGNORE DRAFT TRANSACTIONS                    
         LA    R2,QFILTBT          BATCH TYPE                                   
         LA    R3,ACIOBT                                                        
         LA    R4,2                                                             
         BAS   RE,TESTEM                                                        
         LA    R2,QFILTRTY         RTTYPE FILTER                                
         LA    R3,ACIORTYP                                                      
         LA    R4,1                                                             
         BAS   RE,TESTEM                                                        
         LA    R2,QFILTTTY         TIME TYPE FILTER                             
         LA    R3,ACIOTTYP                                                      
         LA    R4,1                                                             
         BAS   RE,TESTEM                                                        
         LA    R2,QFILTREV         REVERSE FILTER                               
         LA    R3,ACIOREV                                                       
         LA    R4,1                                                             
         BAS   RE,TESTEM                                                        
*&&US                                                                           
         CLI   FILTTAGY,X'41'      FILTER TALENT AGENCY                         
         BL    TFILT6                                                           
         L     R4,ADTRNREC                                                      
         MVI   ELCODE,X'4F'                                                     
         BAS   RE,GETEL                                                         
         BNE   NOTOK                                                            
         USING TRCPJD,R4                                                        
         MVC   THISTAGY,TRCPUL                                                  
         LA    R2,FILTTAGY                                                      
         LA    R3,THISTAGY                                                      
         LA    R4,2                                                             
         BAS   RE,TESTEM                                                        
*&&                                                                             
         SPACE 1                                                                
         DROP  R2                                                               
TFILT6   BAS   RE,TESTATT          CHECK FOR ATTRIBUTES                         
         CLI   QFILTDAT,C'Y'       OPTION TO FILTER ON DATES                    
         BNE   TFILT8                                                           
         L     R4,ADTRNREC                                                      
         USING ACKEYD,R4                                                        
         CLC   ACKEYDTE,QTRASTR                                                 
         BL    NOTOK                                                            
         CLC   ACKEYDTE,QTRAEND                                                 
         BH    NOTOK                                                            
         SPACE 1                                                                
TFILT8   DS    0H                                                               
         B     OK                                                               
         EJECT                                                                  
*              LITTLE ROUTINE TO TEST FILTERS                                   
         SPACE 3                                                                
*                                  R2=A(REQUESTED FILTER)                       
*                                  R3=A(DEDUCED VALUE)                          
*                                  R4=LENGTH                                    
         SPACE 1                                                                
TESTEM   LR    R1,R2               SAVE A(FIRST CHARACTER)                      
         CLI   0(R2),X'41'                                                      
         BLR   RE                                                               
         SPACE 1                                                                
TSTF2    CLI   0(R2),X'41'                                                      
         BL    TSTFNXT                                                          
         CLI   0(R2),C'*'          WILD CARD                                    
         BE    TSTFNXT                                                          
         TM    0(R1),X'40'         TEST FIRST BYTE HERE                         
         BNO   TSTFNEG                                                          
         CLC   0(1,R2),0(R3)                                                    
         BNE   NOTOK                                                            
         B     TSTFNXT                                                          
         SPACE 1                                                                
TSTFNEG  MVC   BYTE,0(R2)          NEGATIVE TEST                                
         OI    BYTE,X'40'                                                       
         CLC   BYTE,0(R3)                                                       
         BNER  RE                  IF ANY BYTE DIFFERS, ITS OK                  
         SPACE 1                                                                
TSTFNXT  LA    R2,1(R2)                                                         
         LA    R3,1(R3)                                                         
         BCT   R4,TSTF2                                                         
         TM    0(R1),X'40'         IF WE GOT THIS FAR FOR NEGATIVE              
         BNO   NOTOK               THEN IT'S NO GOOD                            
         BR    RE                                                               
         EJECT                                                                  
*              ROUTINE TO FILTER ATTRIBUTES                                     
         SPACE 3                                                                
TESTATT  CLI   QFILTATT+2,X'41'                                                 
         BLR   RE                                                               
         L     R1,ADATTR           IS THERE AN ATTRIBUTE ELEMENT                
         LTR   R1,R1                                                            
         BZ    NOTOK                                                            
         USING ACAPD,R1                                                         
         LA    R2,ACAPMLEN         POSTITION OF FIRST MINI                      
         ZIC   R0,ACAPMIN          PICK UP N'MINIS                              
         DROP  R1                                                               
         USING ACAPMLEN,R2                                                      
         LA    R3,QFILTATT+13      GET L'FILTER-3 EXPRESSION TO R1              
         LA    R1,11                                                            
         SPACE 1                                                                
TESTATT2 CLI   0(R3),X'40'                                                      
         BH    TESTATT4                                                         
         BCTR  R3,0                                                             
         BCT   R1,TESTATT2                                                      
         SPACE 1                                                                
TESTATT4 MVC   DUB(2),QFILTATT     TRY EACH MINI                                
         OI    DUB,X'40'                                                        
         CLC   DUB(2),ACAPACCT     MUST MATCH ON U/L                            
         BNE   TESTATTX                                                         
         TM    QFILTATT,X'40'      CHECK NEG INDICATOR                          
         BNO   TESTATT6                                                         
         EX    R1,*+8              MATCH ON ACCOUNT BIT                         
         B     *+10                                                             
         CLC   QFILTATT+2(0),ACAPACCT+2                                         
         BER   RE                  GOT A HIT                                    
         B     NOTOK                                                            
         SPACE 1                                                                
*                                  NEGATIVE TEST                                
TESTATT6 EX    R1,*+8              MATCH ON ACCOUNT BIT                         
         B     *+10                                                             
         CLC   QFILTATT+2(0),ACAPACCT+2                                         
         BNER  RE                  GOT A MISS SO ITS OK                         
         B     NOTOK                                                            
         SPACE 1                                                                
TESTATTX ZIC   RF,0(R2)                                                         
         AR    R2,RF                                                            
         BCT   R0,TESTATT4                                                      
         B     NOTOK               ALL FAILED SO ITS A MISS                     
         DROP  R2                                                               
         EJECT                                                                  
*              ROUTINE TO FILTER VALUE AGAINST LIST                             
         SPACE 3                                                                
*                                  R2=A(REQUESTED FILTER)                       
*                                  R3=A(DEDUCED VALUE)                          
*                                  R4=LENGTH OF ENTRY                           
*                                  R5=A(LIST)                                   
         SPACE 1                                                                
TESTLCAC CLI   0(R2),X'41'         CONTRA LIST MUST ALSO                        
         BLR   RE                  MATCH ON UNIT AND LEDGER                     
         CLC   ACIOCON+1(2),CLISTUL                                             
         BNE   TESTLNO                                                          
         SPACE 1                                                                
TESTLIST CLI   0(R2),X'41'                                                      
         BLR   RE                                                               
         BCTR  R4,0                                                             
         SPACE 1                                                                
TESTL2   CLI   0(R5),0                                                          
         BE    TESTLNO                                                          
         EX    R4,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R5),0(R3)                                                    
         BE    TESTLYES                                                         
         LA    R5,1(R4,R5)                                                      
         B     TESTL2                                                           
         SPACE 1                                                                
TESTLYES TM    0(R2),X'40'                                                      
         BNO   NOTOK                                                            
         BR    RE                                                               
         SPACE 1                                                                
TESTLNO  TM    0(R2),X'40'                                                      
         BO    NOTOK                                                            
         BR    RE                                                               
         EJECT                                                                  
*              TEST LIMIT ACCESS AND SECURITY                                   
         SPACE 3                                                                
LIMSEC   NTR1                                                                   
         CLI   ACIOAUTH+1,0        ANY SECURITY SET?                            
         BE    OK                                                               
         L     R6,ADACCSTA                                                      
         USING ACSTATD,R6                                                       
         CLC   ACSTSECY+1(1),ACIOAUTH+1                                         
         BH    NOTOK                                                            
         B     OK                                                               
         SPACE 1                                                                
*                                  CHECK FOR LIMIT ACCESS                       
ACCCHECK NTR1                                                                   
         CLC   ACIOACCS,SPACES     ANY LIMIT ACCESS?                            
         BE    OK                                                               
         CLC   ACIOACCS(2),SPACES                                               
         BNH   OFFCHECK                                                         
         CLI   ACIOACCS,X'41'      ANY LIMIT ACCESS SET?                        
         BL    OK                                                               
         CLI   ACIOACCS,C'*'       *=OFFICE                                     
         BE    OFFCHECK                                                         
*****    CLC   ACIOACCS(2),=C'-*'  -*=NEGATIVE OFFICE                           
*****    BE    NEGOFF                                                           
         CLI   ACIOACCS,C'$'       $=LIST                                       
*****    BE    LSTCHECK                                                         
         BE    OFFCHECK                                                         
         CLI   ACIOACCS,C'-'       -=NEGATIVE CLIENT                            
         BE    NEGCLI                                                           
         CLC   ACIOACCS(2),=C'T1'  NOT TALENT THOUGH                            
         BE    OK                                                               
         CLC   ACIOACCS(3),ACIOACC+3    CLIENT TEST                             
         BNE   NOTOK                                                            
         B     OK                                                               
         SPACE 1                                                                
NEGCLI   CLC   ACIOACCS+1(3),ACIOACC+3  NEGATIVE CLIENT TEST                    
         BE    NOTOK                                                            
         B     OK                                                               
         SPACE 1                                                                
*EGOFF   CLI   ACIOOFF,0                MAY NOT BE READY TO TEST                
*        BE    OK                                                               
*        CLC   ACIOACCS+2(1),ACIOOFF    NEGATIVE OFFICE TEST                    
*        BE    NOTOK                                                            
*        B     OK                                                               
         SPACE 1                                                                
*FFCHECK CLI   ACIOOFF,0                MAY NOT BE READY TO TEST                
*        BE    OK                                                               
*        CLC   ACIOACCS+1(1),ACIOOFF    OFFICE TEST                             
*        BNE   NOTOK                                                            
*        B     OK                                                               
         SPACE 1                                                                
OFFCHECK OC    ACIONOFF,ACIONOFF        MAY NOT BE READY TO TEST                
         BZ    OK                                                               
         CLC   QFILTNOF,=C'??'     MISSING TEST IN FILTOFF                      
         BE    OK                                                               
         L     R1,AOFFBLK                                                       
         USING OFFALD,R1                                                        
         MVC   OFFAOFFC,ACIONOFF   PASS OFFICE TO OFFAL                         
         L     RF,ADLDGEL                                                       
         USING ACLEDGD,RF                                                       
         MVC   OFFAOPOS,ACLTOFF                                                 
         DROP  RF                                                               
         LA    RF,IO                                                            
         ST    RF,OFFAREC                                                       
         MVI   OFFAACT,OFFATST     OFFICE TEST                                  
         GOTO1 OFFAL                                                            
         B     XIT                 (OFFAL WILL SET CC)                          
         SPACE 1                                                                
*STCHECK CLI   ACIOOFF,0                MAY NOT BE READY TO TEST                
*        BE    OK                                                               
*        XC    DUB,DUB                  LIST TEST                               
*        LA    R1,DUB                                                           
*        USING OFFICED,R1                                                       
*        MVI   OFCSYS,C'A'                                                      
*        MVC   OFCAUTH,ACIOACCS                                                 
*        MVC   OFCOFC,ACIOOFF                                                   
*        MVC   OFCAGY(1),QCOMPANY                                               
*        MVC   OFCALPHA,ACIOAGYA                                                
*        DROP  R1                                                               
*        GOTO1 OFFICER,DMCB,DUB,(X'80',ACCOMFAC),OFFCLIST                       
*        CLI   DMCB,X'FF'                                                       
*        BE    NOTOK                                                            
*        B     OK                                                               
         EJECT                                                                  
*              DATA MANAGER INTERFACE                                           
         SPACE 3                                                                
READ     MVC   COMMAND,=CL6'DMREAD'                                             
         B     ACCFILE                                                          
         SPACE 1                                                                
SEQ      MVC   COMMAND,=CL6'DMRSEQ'                                             
         B     ACCFILE                                                          
         SPACE 1                                                                
HIGH     MVC   COMMAND,=CL6'DMRDHI'                                             
         SPACE 1                                                                
ACCFILE  NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         GOTO1 CDATAMGR,DMCB,(DMINBTS,COMMAND),FILNME,KEY,IO                    
         MVC   KEY,IO                                                           
         MVC   ACIOKEY,IO                                                       
         TM    DMCB+8,X'10'                                                     
         BO    NOTOK               RECORD NOT FOUND                             
         TM    DMCB+8,X'02'                                                     
         BO    NOTOK               RECORD IS DELETED                            
         SPACE 1                                                                
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         LA    R1,IO               PASS USER A(CURRENT RECORD)                  
         ST    R1,ADCURREC                                                      
         BAS   RE,TRACIO           POSSIBLE TRACE                               
         B     OK                                                               
         SPACE 3                                                                
         GETEL R4,DATADISP,ELCODE                                               
         SPACE 1                                                                
OK       SR    R1,R1                                                            
         B     *+8                                                              
         SPACE 1                                                                
NOTOK    LA    R1,1                                                             
         LTR   R1,R1                                                            
         SPACE 1                                                                
XIT      XIT1                                                                   
         SPACE 1                                                                
ERRXIT   MVI   ACCERR,RECNTFND                                                  
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO TRACE                                                 
         SPACE 3                                                                
TRACIO   NTR1                                                                   
         CLC   KEY+3(12),ACIOTRON  DOES KEY FIT?                                
         BL    XIT                                                              
         CLC   KEY+3(12),ACIOTROF                                               
         BH    XIT                                                              
         SPACE 1                                                                
         OC    ACIOAPRT,ACIOAPRT   IS PRINT AROUND?                             
         BZ    XIT                                                              
         OC    ACIOTRMX,ACIOTRMX   MAX RECORDS                                  
         BZ    TRACIO6                                                          
         L     R1,TRACECNT             UPDATE COUNT SO FAR                      
         LA    R1,1(R1)                                                         
         ST    R1,TRACECNT                                                      
         OC    ACIOTRMX,ACIOTRMX                                                
         BZ    TRACIO6                                                          
         C     R1,ACIOTRMX         CHECK MAX RECORDS                            
         BH    XIT                                                              
         SPACE 1                                                                
TRACIO6  MVC   P(32),KEY                                                        
         LA    R4,KEY                                                           
         USING ACKEYD,R4                                                        
         CLI   ACKEYDTE,X'41'                                                   
         BL    TRACIO8                                                          
         L     R7,ACCOMFAC                                                      
         USING COMFACSD,R7                                                      
         GOTO1 CDATCON,DMCB,(1,ACKEYDTE),(8,P+33)                               
         MVC   P+42(6),ACKEYREF                                                 
         EDIT  (1,ACKEYSBR),(3,P+49)                                            
         SPACE 1                                                                
TRACIO8  GOTO1 ACIOAPRT,DMCB,P-1,=C'BL01'                                       
         B     XIT                                                              
         DROP  R4                                                               
         SPACE 1                                                                
TRACECNT DC    F'0'                TRACE COUNTER                                
         EJECT                                                                  
*              CONSTANTS TABLES ETC                                             
         SPACE 1                                                                
*              ELEMENT LIST                                                     
         SPACE 1                                                                
CMPLST   DC    X'10',X'20',X'22',X'FF'                                          
UNTLST   DC    X'20',X'FF'                                                      
LDGLST   DC    X'14',X'16',X'20',X'30',X'FF'                                    
LVALST   DC    X'30',X'24',X'FF'                                                
LVBLST   DC    X'30',X'24',X'25',X'FF'                                          
LVCLST   DC    X'30',X'24',X'FF'                                                
LVDLST   DC    X'30',X'24',X'FF'                                                
ACCLST   DC    X'30',X'20',X'24',X'32',X'FF'                                    
HSTLST   DC    X'43',X'FF'                                                      
TRNLST   DC    X'44',X'50',X'23',X'C0',X'FF'                                    
*                                                                               
MOSTAB   DC    C'123456789......ABC'                                            
         EJECT                                                                  
*              LTORG FOR ACCIO                                                  
         SPACE 3                                                                
         LTORG                                                                  
         EJECT                                                                  
*              DSECT FOR THIS MODULE                                            
         SPACE 3                                                                
LWSD     DSECT                                                                  
RELO     DS    A                                                                
OFFICER  DS    A                                                                
OFFAL    DS    A                                                                
AOFFBLK  DS    A                                                                
ATMSIO   DS    A                                                                
DMCB     DS    6F                                                               
DUB      DS    D                                                                
DOUBLE   DS    D                                                                
WORK     DS    CL64                                                             
FULL     DS    F                                                                
WORD     DS    F                                                                
HALF     DS    H                                                                
HALF2    DS    H                                                                
THREE    DS    CL3                                                              
CONTR1N  DS    C                                                                
BYTE     DS    CL1                                                              
SAVESTAT DS    XL1                                                              
FILTLEV  DS    XL1                                                              
BUDGLEV  DS    XL1                                                              
FILTTAGY DS    XL2                                                              
THISTAGY DS    XL2                                                              
CONVMOS  DS    V                                                                
         SPACE 1                                                                
FILNME   DS    CL8                                                              
COMMAND  DS    CL6                                                              
DMINBTS  DS    CL1                                                              
USERRD   DS    A                   USER REGISTER 13                             
SPACES   DS    CL133                                                            
         DS    XL1                                                              
P        DS    CL133                                                            
LSTRQST  DS    CL80                                                             
ADNEXT   DS    A                   A(NEXT AREA IN ELBUFF)                       
AWRKCODE DS    A                   A(WORK CODE IN KEY)                          
ELCODE   DS    CL1                                                              
DATADISP DS    H                                                                
TMSSWT   DS    X                   INITIALIZATION SWITCH FOR TMS                
ANEXTEL  DS    A                   ADDRESS NEXT TMS ELEMENT                     
EXQLN    DS    CL1                                                              
THISLEV  DS    XL1                                                              
KEY      DS    CL42                                                             
KEYSAVE  DS    CL42                                                             
MAINKEY  DS    CL42                                                             
OFFCLIST DS    CL32                                                             
         SPACE 1                                                                
WLISTUL  DS    CL2                 WORK LIST                                    
WLISTIL  DS    XL1                                                              
WCODLIST DS    CL250                                                            
         SPACE 1                                                                
ALISTUL  DS    CL2                 ACCOUNT LIST                                 
ALISTIL  DS    XL1                                                              
ACCLIST  DS    CL250                                                            
         SPACE 1                                                                
CLISTUL  DS    CL2                 CONTRA LIST                                  
CLISTIL  DS    XL1                                                              
CACLIST  DS    CL250                                                            
         SPACE 1                                                                
         DS    0D                                                               
IO       DS    CL1000                                                           
RECAREA  DS    CL3000              ****** DO NOT CHANGE *******                 
ELBUFF   DS    CL4000              ****** ELBUFF IS NOT ADDRESSABLE             
OFFBLOCK DS    CL(OFFALLEN)                                                     
TMSIO    DS    CL2000                                                           
LWSEND   EQU   *                                                                
         EJECT                                                                  
*              DSECT FOR I/O CONTROL                                            
ACCIOD   DSECT                                                                  
         SPACE 1                                                                
       ++INCLUDE ACCIOD                                                         
         EJECT                                                                  
       ++INCLUDE ACOFFALD                                                       
         EJECT                                                                  
*ACGENBOTH                                                                      
         PRINT OFF                                                              
       ++INCLUDE ACGENBOTH                                                      
         PRINT ON                                                               
*ACGENFILE                                                                      
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
*DDCOMFACS                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
*DDOFFICED                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDOFFICED                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'028ACACCIO   07/26/17'                                      
         END                                                                    
