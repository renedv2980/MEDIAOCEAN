*          DATA SET PRWRICFL   AT LEVEL 057 AS OF 02/22/05                      
*CATALP PRWRICFL                                                                
         TITLE 'PRWRICFL - COLUMN FILTERS - ENTRY POINTS'                       
         ENTRY COLFLTR                                                          
         ENTRY FNDFLTR                                                          
         ENTRY COUNT                                                            
PRWRICFL CSECT                                                                  
         TITLE 'PRWRICFL - CHANGE LOG'                                          
         TITLE 'PRWRICFL - COLUMN FILTERS '                                     
***********************************************************************         
*                                                                     *         
*        COLUMN FILTER ROUTINES                                       *         
*                                                                     *         
*        COLFLTR   APPLY COLUMN FILTERS TO DATA                       *         
*        FNDFLTR   FIND OUT IF SPECIFIC COLUMN FILTER ENTERED         *         
*                  FOR COLUMN                                         *         
*        COUNT     COUNT NUMBER OF OCCURENCES OF ROW                  *         
*                                                                     *         
*NTRY                                                                 *         
*                                                                     *         
*        R2==>  DRIVER INPUT AREA                                     *         
*        R8==>  SPOOL WORKAREA                                        *         
*        R9==>  PRINTWRITER WORKAREA                                  *         
*        RA==>  DRIVER GLOBAL AREA                                    *         
*        RC==>  GENCON WORKAREA                                       *         
*                                                                     *         
*        FILTERS ARE IN SAVEAREA BASED AT FLTTAB                      *         
*           EACH KEYWORD HAS 256 BYTES                                *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         PRINT NOGEN                                                            
         TITLE 'PRWRICFL - APPLY FILTERS - COLFLTR'                             
         DS    0D                                                               
COLFLTR  NMOD1 0,**#CFL                                                         
*                                                                               
         USING SPOOLD,R8           ESTABLISH SPOOL WORKAREA                     
         USING SYSD,R9             ESTABLISH PRINTWRITER WORKAREA               
         USING GLOBALD,RA          ESTABLISH DRIVER WORKAREA                    
*                                                                               
         LA    R7,PBLOCK           ESTABLISH PRINT BLOCK                        
         USING PBLOCK,R7                                                        
*                                                                               
         L     RC,GLAWORKD         RESET WORKING STORAGE POINTER                
         USING GEND,RC                                                          
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,1,GLARGS+6       GET KEYWORD NUMBER                           
         BZ    CFLKEEP             NONE GIVEN - KEEP RECORD                     
*                                                                               
         XC    CFLSWS(CFLSWSL),CFLSWS INIT SWITCHS                              
*                                                                               
         LA    RE,FLTAREAL         LENGTH OF FILTER SAVEAREA                    
         BCTR  RF,0                DECREMENMT FOR INDEXING                      
         MR    RE,RE               CALCULATE INDEX AMOUNT                       
*                                                                               
         L     R3,AFLTTAB          POINT TO FILTER SAVEAREAS                    
         LA    R3,0(RF,R3)         POINT TO THIS KEYWORD'S AREA                 
*                                                                               
         USING FLTAREA,R3          ESTABLISH FILTER SAVEAREA                    
*                                                                               
*        DETERMINE FILTER TYPE AND APPLY                                        
*                                                                               
         SR    R0,R0                                                            
         ICM   R0,1,FLTFNO         GET NUMBER OF FILTERS                        
         BZ    CFLKEEP             NONE GIVEN - KEEP RECORD                     
*                                                                               
         LA    R3,FLTCTL           POINT TO FIRST FILTER                        
*                                                                               
CFLLOOP  DS    0H                                                               
*                                                                               
         USING FLTCTL,R3           ESTABLISH FILTER                             
*                                                                               
*        HANDLE FILTERS THAT INVOLVE DATES                                      
*          DATES ARE AT FLTFVAL - START AND END DATES, 3 BYTES BINARY           
*                                                                               
CFLDTES  DS    0H                                                               
*                                                                               
*        INFO ON DATES STORED IN TABLE CFTAB                                    
*                                                                               
         LA    R1,CFLTABN          NUMBER OF ENTRIES IN TABLE                   
         LA    R5,CFLTAB           POINT TO DATE TABLE                          
         USING CFTABD,R5           ESTABLISH TABLE ENTRY                        
*                                                                               
CFLDTELP DS    0H                                                               
*                                                                               
         CLC   CFTFLTID,FLTFTIC    MATCH ON FILTER ICODE                        
         BNE   CFLDTECN                                                         
*                                                                               
         L     R4,AIO1             POINT TO RECORD IN CORE                      
         CLC   CFTRECID,PBUYKRCD-PBUYKEY(R4) MUST MATCH ON RECORD ID            
         BE    CFLDTEFD                                                         
*                                                                               
         L     R4,AIO2             MAY BE IN OTHER I/O AREA                     
         CLC   CFTRECID,PBUYKRCD-PBUYKEY(R4) MUST MATCH ON RECORD ID            
         BNE   CFLDTECN                                                         
*                                                                               
CFLDTEFD DS    0H                                                               
*                                                                               
*        FIND APPROPRIATE ELEMENT IN RECORD                                     
*                                                                               
         CLI   CFTELMID,PPAYELQ    IF PAY ELEMENT                               
         BNE   CFLDTPYN                                                         
*                                                                               
         ICM   R6,15,PBPAYELA          POINT TO CURRENT PAY ELEMENT             
         BNZ   CFLDTFD                                                          
         B     CFLDTECN                                                         
*                                                                               
CFLDTPYN DS    0H                                                               
*                                                                               
         CLI   CFTELMID,PBILELQ    IF BILL ELEMENT                              
         BNE   CFLDTBLN                                                         
*                                                                               
         ICM   R6,15,PBBLLELA          POINT TO CURRENT BILL ELEMENT            
         BNZ   CFLDTFD                                                          
         B     CFLDTECN                MUST HAVE ONE                            
*                                                                               
CFLDTBLN DS    0H                                                               
*                                                                               
         LR    R6,R4               POINT TO START OF RECORD                     
*                                                                               
         CLI   CFTELMID,0          IF NO ELEMENT ID GIVEN                       
         BE    CFLDTFD                THE DATA IS IN THE KEY                    
*                                                                               
         SR    RF,RF                                                            
         LA    R6,33(R6)           POINT TO FIRST ELEMENT                       
*                                                                               
         CLI   0(R6),0             SKIP IF END OF RECORD REACHED                
         BE    CFLDTECN                                                         
         CLC   CFTELMID,0(R6)      MATCH ON ELEMENT ID                          
         BE    CFLDTFD                                                          
         IC    RF,1(R6)                                                         
         LA    R6,0(RF,R6)         BUMP TO NEXT ELEMENT                         
         B     *-26                                                             
*                                                                               
CFLDTECN DS    0H                                                               
*                                                                               
         LA    R5,CFTABL(R5)       BUMP TO NEXT TABLE ENTRY                     
         BCT   R1,CFLDTELP                                                      
*                                                                               
CFLDTEDN DS           0H                                                        
*                                                                               
         B     CFLDTESN            NO MATCH - TRY OTHER FILTERS                 
*                                                                               
*        CORRECT ELEMENT FOUND                                                  
*                                                                               
CFLDTFD  DS    0H                                                               
*                                                                               
         SR    R1,R1                                                            
         IC    R1,CFTDISP          POINT TO DATE                                
         LA    R1,0(R1,R6)                                                      
*                                                                               
         CLC   CFTFLTID,=AL2(PRQCFDMS) IF MONTH OF SERVICE FILTER               
         BNE   *+8                                                              
         CLI   CFTRECID,PBILKIDQ       AND BILL RECORD                          
         BNE   *+18                                                             
         MVC   CFLDATE(2),0(R1)        DATE MUST BE MADE INTO YMD               
         MVI   CFLDATE+2,1                                                      
         LA    R1,CFLDATE                                                       
*                                                                               
         CLC   CFTFLTID,=AL2(PRQCFDBR) IF BILL RUN DATE                         
         BNE   CFLDTBRN                                                         
*                                                                               
         LR    RF,R1               SAVE REGISTER                                
         GOTO1 DATCON,CFLWORK,(0,0(RF)),(3,CFLDATE)   MAKE BINARY               
*                                                                               
         LA    R1,CFLDATE                                                       
*                                                                               
         B     CFLDTBX                                                          
*                                                                               
CFLDTBRN DS    0H                                                               
*                                                                               
         CLC   CFTFLTID,=AL2(PRQCFDED) IF BILL EDI DATE                         
         BNE   CFLDDEDN                                                         
*                                                                               
         LR    RF,R1               SAVE REGISTER                                
         GOTO1 DATCON,CFLWORK,(2,0(RF)),(3,CFLDATE)   MAKE BINARY               
*                                                                               
         LA    R1,CFLDATE                                                       
*                                                                               
CFLDDEDN DS    0H                                                               
*                                                                               
CFLDTBX  DS    0H                                                               
*                                                                               
*        MATCH DATE TO PERIOD IN FILTER                                         
*                                                                               
         TM    FLTCTL,FLTEQQ       SKIP IF NOT TO LIE IN RANGE                  
         BNO   CFLDTEQN                                                         
*                                                                               
         CLC   0(3,R1),FLTFVAL     MUST BE ON OR AFTER START DATE               
         BL    CFLDROP                                                          
*                                                                               
         CLI   FLTFVLEN,6          IF NO END DATE                               
         BL    *+14                                                             
         OC    FLTFVAL+3(3),FLTFVAL+3                                           
         BNZ   CFLDTEQ5                                                         
*                                                                               
         CLC   0(3,R1),FLTFVAL        MUST MATCH START DATE                     
         BNE   CFLDROP                                                          
         B     CFLDTESX                                                         
*                                                                               
CFLDTEQ5 DS    0H                                                               
*                                                                               
         CLC   0(3,R1),FLTFVAL+3   ELSE MUST BE ON OR BEFORE END DATE           
         BH    CFLDROP                                                          
         B     CFLDTESX                                                         
*                                                                               
CFLDTEQN DS    0H                                                               
*                                                                               
         TM    FLTCTL,FLTLTQ       SKIP IF NOT TO LIE BEFORE RANGE              
         BNO   CFLDTLTN                                                         
*                                                                               
         CLC   0(3,R1),FLTFVAL     MUST BE BEFORE START DATE                    
         BNL   CFLDROP                                                          
*                                                                               
         B     CFLDTESX                                                         
*                                                                               
CFLDTLTN DS    0H                                                               
*                                                                               
         TM    FLTCTL,FLTGTQ       SKIP IF NOT AFTER RANGE                      
         BNO   CFLDTGTN                                                         
*                                                                               
         CLI   FLTFVLEN,6          IF NO END DATE                               
         BL    *+14                                                             
         OC    FLTFVAL+3(3),FLTFVAL+3                                           
         BNZ   CFLDTGT5                                                         
*                                                                               
         CLC   0(3,R1),FLTFVAL        MUST BE AFTER START DATE                  
         BNH   CFLDROP                                                          
         B     CFLDTESX                                                         
*                                                                               
CFLDTGT5 DS    0H                                                               
*                                                                               
         CLC   0(3,R1),FLTFVAL+3   ELSE MUST BE AFTER END DATE                  
         BNH   CFLDROP                                                          
         B     CFLDTESX                                                         
*                                                                               
CFLDTGTN DS    0H                                                               
*                                                                               
         TM    FLTCTL,FLTNOTQ      IF TO LIE OUTSIDE  RANGE                     
         BNO   CFLDTNEN                                                         
*                                                                               
         CLC   0(3,R1),FLTFVAL        MUST BE BEFORE START DATE                 
         BL    CFLDTESX                                                         
*                                                                               
         CLI   FLTFVLEN,6          IF NO END DATE                               
         BL    *+14                                                             
         OC    FLTFVAL+3(3),FLTFVAL+3                                           
         BNZ   CFLDTNE5                                                         
*                                                                               
         CLC   0(3,R1),FLTFVAL        MUST BE AFTER START DATE                  
         BNH   CFLDROP                                                          
         B     CFLDTESX                                                         
*                                                                               
CFLDTNE5 DS    0H                                                               
*                                                                               
         CLC   0(3,R1),FLTFVAL+3      ELSE AFTER END DATE                       
         BNH   CFLDROP                                                          
         B     CFLDTESX                                                         
*                                                                               
CFLDTNEN DS    0H                                                               
*                                                                               
CFLDTESX DS    0H                                                               
*                                                                               
         B     CFLCONT                                                          
*                                                                               
CFLDTESN DS    0H                                                               
*                                                                               
*        SPACE DESCRIPTION                                                      
*                                                                               
CFLSPC   DS    0H                                                               
*                                                                               
         CLC   FLTFTIC,=AL2(PRQCFSP)  SPACE DESCRIPTION                         
         BNE   CFLSPCN                                                          
*                                                                               
         L     R4,AIO1             POINT TO BUY RECORD                          
         CLI   PBUYKRCD-PBUYKEY(R4),PBUYKIDQ  MUST HAVE BUY RECOD               
         BNE   CFLSPCX                                                          
*                                                                               
         MVC   WORK,SPACES         INIT WORKAREA                                
         SR    RF,RF                                                            
         ICM   RF,1,FLTFVLEN       VALUE LENGTH                                 
         BZ    CFLSPCX             NOTHING TO FILTER AGAINST                    
*                                                                               
         LA    R1,FLTFVAL-1(RF)    POINT TO LAST BYTE OF VALUE                  
*                                                                               
         CLI   0(R1),C'*'          IF A WILDCARD                                
         BNE   *+12                                                             
         BCT   RF,*+8                 DECREMENT LENGTH                          
         B     CFLSPCX                   NO FILTER                              
*                                                                               
         BCTR  RF,0                DECREMENT FOR EXECUTE                        
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   WORK(0),FLTFVAL     MOVE SPACE DESC TO WORK                      
*                                                                               
         CLI   0(R1),C'*'          IF NO WILDCARD                               
         BE    *+8                                                              
         LA    RF,L'PBDSPACE-1        MATCH TO WHOLE DESCRIPTION                
*                                                                               
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   WORK(0),PBDSPACE-PBUYKEY(R4) MATCH FILTER TO BUY SPACE           
         BNE   CFLDROP             DROP IF NOT EQUAL                            
*                                                                               
CFLSPCX  DS    0H                                                               
*                                                                               
         B     CFLCONT                                                          
*                                                                               
CFLSPCN  DS    0H                                                               
*                                                                               
*        ESTIMATE FILTER                                                        
*                                                                               
CFLEF    DS    0H                                                               
*                                                                               
         CLC   FLTFTIC,=AL2(PRQCFEF)  ESTIMATE FILTER                           
         BNE   CFLEFN                                                           
*                                                                               
         MVC   CFLARGS,GLARGS      SAVE CURRENT ARGUMENTS                       
         MVC   GLARGS(16),=CL16'EF'   SET TO ESTIMATE FILTER ARGS               
*                                                                               
         XC    CFLWORK,CFLWORK     INIT WORKAREA                                
         LA    R2,CFLWORK          SET DRIVER INPUT TO WORKAREA                 
*                                                                               
         GOTO1 =V(IESTBUF)         GET ESTIMATE FILTER                          
*                                                                               
         MVC   GLARGS(16),CFLARGS  RESTORE CURRENT ARGUMENTS                    
*                                                                               
         OC    CFLWORK,CFLWORK     DROP IF NO FILTER FOUND                      
         BZ    CFLDROP                                                          
*                                                                               
*        FILTER ESTIMATE                                                        
*                                                                               
         LA    RE,FLTFVAL          POINT TO START OF ESTIMATE FILTERS           
         LA    RF,CFLWORK+1        POINT TO ESTIMATE FILTER                     
         SR    R1,R1                                                            
         IC    R1,FLTFVLEN         MAX ITEMS IN FILTER                          
*                                                                               
CFLEFLP  DS    0H                                                               
*                                                                               
         CLI   0(RE),C'*'          IGNORE IF WILD CARD ENTRY                    
         BE    CFLEFCN                                                          
         CLI   0(RE),C' '          OR NO VALUE GIVEN                            
         BNH   CFLEFCN                                                          
*                                                                               
         TM    0(RE),X'40'         IF POSITIVE FILTER                           
         BZ    CFLEF10                                                          
*                                                                               
         CLC   0(1,RE),0(RF)          MUST MATCH ESTIMATE VALUE                 
         BNE   CFLDROP                DROP                                      
         B     CFLEFCN                                                          
*                                                                               
CFLEF10 DS     0H                  ELSE NEGATIVE FILTER                         
*                                                                               
         MVC   DUB(1),0(RE)        MOVE FILTER TO WORKAREA                      
         OI    DUB,X'40'           MAKE POSITIVE FILTER                         
         CLC   DUB(1),0(RF)        NOW MUST NOT MATCH ESTIMATE VALUE            
         BE    CFLDROP             REMOVE FROM BUFFER                           
*                                                                               
CFLEFCN DS     0H                                                               
*                                                                               
         LA    RE,1(RE)            BUMP POINTERS                                
         LA    RF,1(RF)                                                         
         BCT   R1,CFLEFLP                                                       
*                                                                               
CFLEFX   DS    0H                                                               
         B     CFLCONT                                                          
*                                                                               
CFLEFN   DS    0H                                                               
*                                                                               
*        TEARSHEET FILTERS                                                      
*                                                                               
CFLTS    DS    0H                                                               
*                                                                               
         CLC   FLTFTIC,=AL2(PRQCFTSS)  TEARSHEET STATUS                         
         BE    *+10                                                             
         CLC   FLTFTIC,=AL2(PRQCFTSR)  TEARSHEET REPRO QUALITY                  
         BNE   CFLTSN                                                           
*                                                                               
         MVC   CFLARGS,GLARGS      SAVE CURRENT ARGUMENTS                       
*                                                                               
         XC    GLARGS(16),GLARGS   INIT ARGUMENTS                               
*                                                                               
         MVC   GLARGS(5),=XL5'C2950D0140'   SET TO TSSTAT ARGS                  
         LA    R4,CFLTSSSW                  POINT TO STATUS SWITCH              
*                                                                               
         CLC   FLTFTIC,=AL2(PRQCFTSS)  TEARSHEET STATUS                         
         BE    *+14                                                             
         MVC   GLARGS(5),=XL5'C295170100'     SET TO TSREPRO ARGS               
         LA    R4,CFLTSRSW                    POINT TO REPRO SWITCH             
*                                                                               
         XC    CFLWORK,CFLWORK     INIT WORKAREA                                
         LA    R2,CFLWORK          SET DRIVER INPUT TO WORKAREA                 
*                                                                               
         GOTO1 =V(IELM)            GET TEARSHEET VALUE                          
*                                                                               
         MVC   GLARGS(16),CFLARGS  RESTORE CURRENT ARGUMENTS                    
*                                                                               
         OC    CFLWORK,CFLWORK     DROP IF NO VALUE FOUND                       
         BZ    CFLTSDRP                                                         
*                                                                               
         CLC   FLTFTIC,=AL2(PRQCFTSR)  IF TEARSHEET REPRO QUALITY               
         BNE   CFLTS10                                                          
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,1,CFLWORK        GET BINARY VALUE                             
         STCM  RF,15,CFLWORK       AND CONVERT TO 4 BYTES                       
*                                                                               
         LA    R1,CFLWORK          USE VLTEXTRA FOR BINARY VALUE                
         LA    RF,4                LENGTH OF DATA                               
*                                                                               
         B     CFLTSCMP                                                         
*                                                                               
CFLTS10  DS    0H                                                               
*                                                                               
*        VALIDATE INPUT                                                         
*                                                                               
         L     R1,AVLPARMS         POINT TO DDVAL PARAMETER LIST                
*                                                                               
         GOTO1 VDDVAL,(R1),('VLPVALQ',FLTFTIC),(1,CFLWORK),AVLTAB,     X        
               0,0,0                                                            
*                                                                               
         CLI   VLPERR-VLPARMS(R1),0                                             
         BNE   CFLTSDRP            DROP IF IN ERROR                             
*                                                                               
         L     R5,AVLTAB           ESTABLISH RETURNED TABLE                     
         USING VLTABD,R5                                                        
*                                                                               
         LA    R1,VLTICODE         DATA TO COMPARE IN ICODE                     
*                                                                               
         SR    RF,RF                                                            
         IC    RF,FLTFVLEN         GET FILTER VALUE LENGTH                      
*                                                                               
CFLTSCMP DS    0H                  COMPARE FILTER TO DATA                       
*                                                                               
         BCTR  RF,0                DECREMENT FOR EXECUTE                        
*                                                                               
         TM    FLTCTL,FLTNOTQ      SKIP IF FILTERING ON ABSENCE                 
         BO    CFLTSNOT                                                         
*                                                                               
         TM    FLTCTL,FLTEQQ       IF EQ                                        
         BNO   CFLTSEQX                                                         
*                                                                               
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R1),FLTFVAL        MATCH FILTER VALUE                        
         BE    CFLTSKP                                                          
*                                                                               
         B     CFLTSDRP                                                         
*                                                                               
CFLTSEQX DS    0H                                                               
*                                                                               
         TM    FLTCTL,FLTLTQ       IF LT                                        
         BNO   CFLTSLTX                                                         
*                                                                               
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R1),FLTFVAL        COMPARE FILTER VALUE                      
         BL    CFLTSKP                                                          
*                                                                               
         B     CFLTSDRP                                                         
*                                                                               
CFLTSLTX DS    0H                                                               
*                                                                               
         TM    FLTCTL,FLTGTQ       IF GT                                        
         BNO   CFLTSGTX                                                         
*                                                                               
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R1),FLTFVAL        COMPARE FILTER VALUE                      
         BH    CFLTSKP                                                          
*                                                                               
         B     CFLTSDRP                                                         
*                                                                               
CFLTSGTX DS    0H                                                               
*                                                                               
         B     CFLTSDRP                                                         
*                                                                               
CFLTSNOT DS    0H                                                               
*                                                                               
         TM    FLTCTL,FLTEQQ       IF NEQ                                       
         BNO   CFLTSNEX                                                         
*                                                                               
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R1),FLTFVAL        MATCH FILTER VALUE                        
         BNE   CFLTSKP                                                          
*                                                                               
         B     CFLTSDRP                                                         
*                                                                               
CFLTSNEX DS   0H                                                                
*                                                                               
         TM    FLTCTL,FLTLTQ       IF NOT LT                                    
         BNO   CFLTSNLX                                                         
*                                                                               
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R1),FLTFVAL        COMPARE FILTER VALUE                      
         BNL   CFLTSKP                                                          
*                                                                               
         B     CFLTSDRP                                                         
*                                                                               
CFLTSNLX DS   0H                                                                
*                                                                               
         TM    FLTCTL,FLTGTQ       IF NOT GT                                    
         BNO   CFLTSNGX                                                         
*                                                                               
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R1),FLTFVAL        COMPARE FILTER VALUE                      
         BNH   CFLTSKP                                                          
*                                                                               
         B     CFLTSDRP                                                         
*                                                                               
CFLTSNGX DS   0H                                                                
*                                                                               
         B     CFLTSDRP                                                         
*                                                                               
CFLTSDRP DS    0H                  FAILED FILTER                                
*                                                                               
         CLI   0(R4),0             IF SWITCH NOT AREADY SET                     
         BNE   *+8                                                              
         MVI   0(R4),C'D'             SET TO DROP DATA                          
*                                                                               
         B     CFLTSSX                                                          
*                                                                               
CFLTSKP  DS    0H                  KEEP DATA                                    
*                                                                               
         MVI   0(R4),C'K'          KEEP THIS DATA                               
*                                                                               
CFLTSSX  DS    0H                                                               
*                                                                               
         B     CFLCONT                                                          
*                                                                               
CFLTSN   DS    0H                                                               
*                                                                               
*        INVOICE MATCHED AND UNMATCHED                                          
*                                                                               
         CLC   FLTFTIC,=AL2(PRQWRCFL)  SKIP IF NOT COLUMN FILTER                
         BNE   CFLMATN                                                          
*                                                                               
CFLMAT   DS    0H                                                               
*                                                                               
         CLC   =AL2(PRQCFMAT),FLTFVAL  INVOICE MATCHED FILTER                   
         BE    *+10                                                             
         CLC   =AL2(PRQCFUNM),FLTFVAL  INVOICE UNMATCHED FILTER                 
         BNE   CFLMATN                                                          
*                                                                               
         CLI   PBMODE,PBPROCIV         SKIP IF NOT PROCESSING INVOICE           
         BNE   CFLMATBY                                                         
*                                                                               
         ICM   RF,15,PBIDTELA      POINT TO INVOICE DETAIL ELEMENT              
         BZ    CFLDROP             DROP IF NO DETAIL ELEMENT                    
*                                                                               
         USING PIMDTLEL,RF         ESTABLISH INVOICE DETAIL ELEMENT             
*                                                                               
         OC    PIMBDATE,PIMBDATE   IF MATCHED DETAIL                            
         BZ    CFLMAT20                                                         
*                                                                               
         CLC   =AL2(PRQCFMAT),FLTFVAL   KEEP IF MATCHED ONLY WANTED             
         BE    CFLMATX                                                          
         B     CFLDROP                  ELSE DROP                               
*                                                                               
CFLMAT20 DS    0H                  UNMATCHED DETAIL                             
*                                                                               
         CLC   =AL2(PRQCFUNM),FLTFVAL   KEEP IF UNMATCHED ONLY WANTED           
         BE    CFLMATX                                                          
         B     CFLDROP                  ELSE DROP                               
*                                                                               
CFLMATBY DS    0H                                                               
*                                                                               
         L     R4,AIO1             POINT TO BUY RECORD                          
         CLI   PBUYKRCD-PBUYKEY(R4),PBUYKIDQ  MUST HAVE BUY RECORD              
         BNE   CFLMATX                                                          
*                                                                               
         CLC   =AL2(PRQCFMAT),FLTFVAL  IF INVOICE MATCHED FILTER                
         BNE   *+16                                                             
         TM    PBDSTAT-PBUYKEY(R4),X'40'   BUY MUST BE MATCHED                  
         BNO   CFLDROP                                                          
         B     CFLMATX                                                          
*                                                                               
         TM    PBDSTAT-PBUYKEY(R4),X'40' ELSE BUY MUST BE UNMATCHED             
         BO    CFLDROP                                                          
         B     CFLMATX                                                          
*                                                                               
CFLMATX  DS    0H                                                               
*                                                                               
         B     CFLCONT                                                          
*                                                                               
CFLMATN  DS    0H                                                               
*                                                                               
*        HELD BUYS FROM SPECIAL FINANCIAL HANDLING                              
*                                                                               
         CLC   FLTFTIC,=AL2(PRQWRCFL)  SKIP IF NOT COLUMN FILTER                
         BNE   CFLHLDNN                                                         
*                                                                               
CFLHLD   DS    0H                                                               
*                                                                               
         CLC   =AL2(PRQCFRLO),FLTFVAL  RELEASED BUYS ONLY                       
         BE    *+10                                                             
         CLC   =AL2(PRQCFHLO),FLTFVAL  HELD BUYS ONLY                           
         BNE   CFLHLDN                                                          
*                                                                               
         L     R4,AIO1             POINT TO BUY RECORD                          
         CLI   PBUYKRCD-PBUYKEY(R4),PBUYKIDQ  MUST HAVE BUY RECORD              
         BNE   CFLHLDX                                                          
*                                                                               
         CLC   =AL2(PRQCFHLO),FLTFVAL  IF HELD BUYS ONLY                        
         BNE   *+16                                                             
         TM    PBDSTAT-PBUYKEY(R4),X'08'   BUY MUST BE HELD                     
         BNO   CFLDROP                                                          
         B     CFLHLDX                                                          
*                                                                               
         TM    PBDSTAT-PBUYKEY(R4),X'08' ELSE BUY MUST NOT BE HELD              
         BO    CFLDROP                                                          
         B     CFLHLDX                                                          
*                                                                               
CFLHLDX  DS    0H                                                               
*                                                                               
         B     CFLCONT                                                          
*                                                                               
CFLHLDN  DS    0H                                                               
*                                                                               
*                                                                               
*        *RATE BUYS                                                             
*                                                                               
CFLSRT   DS    0H                  *RATE BUYS                                   
*                                                                               
         CLC   =AL2(PRQCFSRO),FLTFVAL  *RATE BUYS ONLY                          
         BE    *+10                                                             
         CLC   =AL2(PRQCFSRN),FLTFVAL  *RATE BUYS EXCLUDED                      
         BNE   CFLSRTN                                                          
*                                                                               
         L     R4,AIO1             POINT TO BUY RECORD                          
         CLI   PBUYKRCD-PBUYKEY(R4),PBUYKIDQ  MUST HAVE BUY RECORD              
         BNE   CFLSRNX                                                          
*                                                                               
         CLC   =AL2(PRQCFSRO),FLTFVAL  IF *RATE BUYS ONLY                       
         BNE   *+16                                                             
         TM    PBDRLIND-PBUYKEY(R4),X'08'   BUY MUST BE *RATE                   
         BNO   CFLDROP                                                          
         B     CFLSRNX                                                          
*                                                                               
         TM    PBDRLIND-PBUYKEY(R4),X'08' ELSE BUY MUST NOT BE *RATE            
         BO    CFLDROP                                                          
         B     CFLSRNX                                                          
*                                                                               
CFLSRNX  DS    0H                                                               
*                                                                               
         B     CFLCONT                                                          
*                                                                               
CFLSRTN  DS    0H                                                               
*                                                                               
*        TRAFFICKED BUYS                                                        
*                                                                               
CFLTBS   DS    0H                  TRAFFICKED BUYS                              
*                                                                               
         CLC   =AL2(PRQCFTBS),FLTFVAL  TRAFFICKED BUYS ONLY                     
         BE    *+10                                                             
         CLC   =AL2(PRQCFTBX),FLTFVAL  TRAFFICKED BUYS EXCLUDED                 
         BNE   CFLTBSN                                                          
*                                                                               
         L     R4,AIO1             POINT TO BUY RECORD                          
         CLI   PBUYKRCD-PBUYKEY(R4),PBUYKIDQ  MUST HAVE BUY RECORD              
         BNE   CFLTBSX                                                          
*                                                                               
         CLC   =AL2(PRQCFTBS),FLTFVAL  IF TRAFFICKED BUYS ONLY                  
         BNE   *+16                                                             
         TM    PBDSTAT-PBUYKEY(R4),X'20'   BUY MUST NOT BE NON-TRAFFIC          
         BO    CFLDROP                                                          
         B     CFLTBSX                                                          
*                                                                               
         TM    PBDSTAT-PBUYKEY(R4),X'20' ELSE BUY MUST BE NON-TRAFFIC           
         BNO   CFLDROP                                                          
         B     CFLTBSX                                                          
*                                                                               
CFLTBSX  DS    0H                                                               
*                                                                               
         B     CFLCONT                                                          
*                                                                               
CFLTBSN  DS    0H                                                               
*                                                                               
CFLHLDNN DS    0H                                                               
*                                                                               
*=====================================================================*         
*                                                                     *         
*       SUBMEDIA CODE FILTERING                                       *         
*                                                                     *         
*=====================================================================*         
         SPACE 2                                                                
CFLSMD   DS    0H                                                               
*                                                                               
         CLC   =AL2(PRQCFSMD),FLTFTIC  SUBMEDIA FILTER                          
         BNE   CFLSMDN                                                          
*                                                                               
*        FIND PUB PAY ELEMENT IN PUB MASTER RECORD                              
*                                                                               
         L     R6,AIO3             POINT TO PUB MASTER RECORD                   
         LA    R6,33(R6)           POINT TO FIRST ELEMENT                       
*                                                                               
         SR    R0,R0                                                            
         SR    RF,RF               INIT REGISTER SAVEAREA                       
*                                                                               
CFLSMDLP DS    0H                                                               
*                                                                               
         USING PUBPAYEL,R6         ESTABLISH PUB PAY ELM                        
*                                                                               
         CLI   PUBPYELC,0          DONE AT END OF RECORD                        
         BE    CFLSMDDN                                                         
*                                                                               
         CLI   PUBPYELC,PUBPYELQ   DONE AT END OF RECORD                        
         BNE   CFLSMDCN               MATCH ON PUBPAY ELEMENT                   
*                                                                               
         CLC   PBCLT,PUBPYOFF      USE IF IT IS FOR CURRENT CLIENT              
         BNE   *+10                                                             
         LR    RF,R6               SAVE CURRENT ELEMENT POINTER                 
         B     CFLSMDDN            THIS IS ELEMENT TO USE                       
*                                                                               
         CLI   PUBPYOFF,X'FF'      IGNORE IF NOT A SPECIAL CODE                 
         BNE   CFLSMDCN                                                         
*                                                                               
         CLC   PUBPYOFF+1(1),PBCOFF   IF MATCH ON OFFICE                        
         BNE   *+10                                                             
         LR    RF,R6                     SAVE ELEMENT POINTER                   
         B     CFLSMDCN                  CONTINUE LOOKING                       
*                                                                               
         CLC   PUBPYOFF,=X'FFFFFF'  IF IT IS AN ALL AGENCY ELM                  
         BNE   CFLSMDCN                                                         
*                                                                               
         LTR   RF,RF               SKIP IF BETTER ELEMENT FOUND ALREADY         
         BNZ   CFLSMDCN                                                         
*                                                                               
         LR    RF,R6                     SAVE ELEMENT POINTER                   
*                                                                               
CFLSMDCN DS    0H                                                               
*                                                                               
         IC    R0,PUBPYELN         GET ELEMENT LENGTH                           
         AR    R6,R0               BUMP TO NEXT ELEMENT                         
*                                                                               
         B     CFLSMDLP                                                         
*                                                                               
*        ANALYZE RESULTS OF SEARCH                                              
*                                                                               
CFLSMDDN DS    0H                                                               
*                                                                               
         LTR   R6,RF               GET SAVED POINTER                            
         BZ    CFLSMDNF            NO PUBPYELM FOUND                            
*                                                                               
         CLI   PUBPYSMD,0          CHECK IF NO SUBMEDIA                         
         BE    CFLSMDNF                                                         
*                                                                               
*        WE HAVE SOME KIND OF SUBMEDIA CODE                                     
*                                                                               
         CLI   FLTFVAL,PBQNONEQ    IF REPORTING ALL WITHOUT SMED                
         BNE   CFLSMD10                                                         
*                                                                               
         TM    FLTCTL,FLTNOTQ         IF NEGATIVE FILTER                        
         BO    CFLSMDX                   KEEP                                   
         B     CFLDROP                ELSE DROP PUB FROM REPORT                 
*                                                                               
CFLSMD10 DS    0H                                                               
*                                                                               
         CLI   FLTFVAL,PBQONLYQ    IF REPORTING ONLY PUBS WITH SMED             
         BNE   CFLSMD20                                                         
*                                                                               
         TM    FLTCTL,FLTNOTQ         IF NEGATIVE FILTER                        
         BO    CFLDROP                   DROP                                   
         B     CFLSMDX                ELSE KEEP PUB                             
*                                                                               
CFLSMD20 DS    0H                                                               
*                                                                               
         CLC   PUBPYSMD,FLTFVAL    IF SUBMEDIA MATCHES FILTER                   
         BNE   CFLSMD30                                                         
*                                                                               
         TM    FLTCTL,FLTNOTQ         IF NEGATIVE FILTER                        
         BO    CFLDROP                   DROP                                   
         B     CFLSMDX                ELSE KEEP PUB                             
*                                                                               
CFLSMD30 DS    0H                  SUBMEDIA DOES NOT MATCH FILTER               
*                                                                               
         TM    FLTCTL,FLTNOTQ         IF NEGATIVE FILTER                        
         BO    CFLKEEP                   KEEP                                   
         B     CFLDROP                ELSE DROP PUB                             
*                                                                               
CFLSMDNF DS     0H                 WE HAVE NO SUBMEDIA CODE                     
*                                                                               
         CLI   FLTFVAL,PBQNONEQ    IF REPORTING ALL WITHOUT SMED                
         BNE   CFLSMD40                                                         
*                                                                               
         TM    FLTCTL,FLTNOTQ         IF NEGATIVE FILTER                        
         BO    CFLDROP                   DROP                                   
         B     CFLSMDX                ELSE KEEP PUB FROM REPORT                 
*                                                                               
CFLSMD40 DS    0H                                                               
*                                                                               
         CLI   FLTFVAL,PBQONLYQ    IF REPORTING ONLY PUBS WITH SMED             
         BNE   CFLSMD50                                                         
*                                                                               
         TM    FLTCTL,FLTNOTQ         IF NEGATIVE FILTER                        
         BO    CFLSMDX                   KEEP                                   
         B     CFLDROP                ELSE DROP PUB                             
*                                                                               
CFLSMD50 DS    0H                  FILTERING ON SINGLE SUBMEDIA                 
*                                                                               
         TM    FLTCTL,FLTNOTQ         IF NEGATIVE FILTER                        
         BO    CFLSMDX                   KEEP                                   
         B     CFLDROP                ELSE DROP PUB                             
*                                                                               
CFLSMDX  DS     0H                                                              
*                                                                               
         B     CFLCONT                                                          
*                                                                               
CFLSMDN  DS     0H                                                              
*                                                                               
         EJECT                                                                  
*                                                                               
CFLCONT  DS    0H                                                               
*                                                                               
         SR    RF,RF                                                            
         IC    RF,FLTFVLEN         GET LENGTH OF FILTER                         
         LA    R3,FLTFVAL(RF)      BUMP TO NEXT FILTER                          
         BCT   R0,CFLLOOP                                                       
*                                                                               
         OC    CFLSWS(CFLSWSL),CFLSWS SKIP IF NO SWITCHS SET                    
         BZ    CFLKEEP                                                          
*                                                                               
*        DROP IF ANY SWITCH SET TO DROP                                         
*                                                                               
         LA    R0,CFLSWSL          NUMBER OF SWITCHES                           
         LA    RF,CFLSWS           START OF SWITCHES                            
*                                                                               
         CLI   0(RF),C'D'          LOOK FOR A DROP INDICATOR                    
         BE    CFLDROP                                                          
         LA    RF,1(RF)                                                         
         BCT   R0,*-12                                                          
*                                                                               
CFLKEEP  DS    0H                                                               
*                                                                               
         CR    RB,RB               INDICATE RECORD TO BE KEPT                   
         B     COLFLTRX                                                         
*                                                                               
CFLDROP  DS    0H                                                               
*                                                                               
         LTR   RB,RB               DROP RECORD                                  
         B     COLFLTRX                                                         
*                                                                               
COLFLTRX DS    0H                                                               
         XIT1                                                                   
*                                                                               
CFLSWS   DS    0X                  SWITCHES FOR VARIOUS DATA TYPES              
CFLTSSSW DC    X'00'               TEARSHEET STATUS SWITCH                      
CFLTSRSW DC    X'00'               TEARSHEET REPRO  SWITCH                      
CFLSWSL  EQU   *-CFLSWS            SWITCH AREA LENGTH                           
*                                                                               
         TITLE 'PRWRICFL - DATE FILTER TABLE - CFLTAB'                          
***********************************************************************         
*                                                                     *         
*                                                                     *         
* DATE FILTER TABLE                                                   *         
*       +0-1   AL2(COLUMN FILTER INTERNAL CODE)                       *         
*       +2     RECORD  ID / WHICH RECORD       IS TO BE CHECKED       *         
*       +3     ELEMENT ID / WHICH ELEMENT DATE IS TO BE FOUND IN      *         
*                AL1(0) - MEANS DATA IS IN KEY OF RECORD              *         
*       +4     DISPLACEMENT OF DATE IN ELEMENT                        *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
CFLTAB   DS    0D                       DATE FILTER TABLE                       
*                                       PAID DATE                               
         DC    AL2(PRQCFDPD),AL1(PBUYKIDQ,PPAYELQ,PPDDATE-PPAYELD)              
CFLTABL  EQU   *-CFLTAB            LENGTH OF TABLE ENTRY                        
*                                       BILLABLE DATE                           
         DC    AL2(PRQCFDBL),AL1(PBUYKIDQ,PBDELQ,PBDBDATE-PBDELEM)              
*                                       SPACE CLOSING DATE                      
         DC    AL2(PRQCFDCL),AL1(PBUYKIDQ,PBDELQ,PBDCDATE-PBDELEM)              
*                                       INSERTION DATE                          
         DC    AL2(PRQCFDID),AL1(PBUYKIDQ,0,PBUYKDAT-PBUYKEY)                   
*                                       BILLED DATE                             
         DC    AL2(PRQCFDBD),AL1(PBUYKIDQ,PBILELQ,PBLDATE-PBILELD)              
*                                       MATERIALS CLOSING DATE                  
         DC    AL2(PRQCFDMC),AL1(PBUYKIDQ,PBDELQ,PBDMDATE-PBDELEM)              
*                                       INSERTION ORDER LAST ISSUE DATE         
         DC    AL2(PRQCFDIO),AL1(PBUYKIDQ,PIOELQ,PIODATE-PIOELEM)               
*                                       PAYABLE DATE                            
         DC    AL2(PRQCFDPY),AL1(PBUYKIDQ,PBDELQ,PBDPDATE-PBDELEM)              
*                                       ON SALE DATE                            
         DC    AL2(PRQCFDOS),AL1(PBUYKIDQ,PBDELQ,PBDSDATE-PBDELEM)              
*                                       UNPAID BY INSERT DATE                   
         DC    AL2(PRQCFDUP),AL1(PBUYKIDQ,0,PBUYKDAT-PBUYKEY)                   
*                                       MONTH OF SERVICE - BUYS                 
         DC    AL2(PRQCFDMS),AL1(PBUYKIDQ,PBDELQ,PBDBDATE-PBDELEM)              
*                                       MONTH OF SERVICE - BILLS                
         DC    AL2(PRQCFDMS),AL1(PBILKIDQ,0,PBILKMOS-PBILLKEY)                  
*                                       INVOICE DATE                            
         DC    AL2(PRQCFDBI),AL1(PBILKIDQ,8,PBILINVD-PBILLEL)                   
*                                       INVOICE DUE DATE                        
         DC    AL2(PRQCFDDU),AL1(PBILKIDQ,8,PBILDUED-PBILLEL)                   
*                                       INVOICE DUE DATE                        
         DC    AL2(PRQCFDED),AL1(PBILKIDQ,8,PBILEDID-PBILLEL)                   
*                                       INVOICE EDI DATE                        
         DC    AL2(PRQCFDBR),AL1(PBILKIDQ,8,PBILLDAT-PBILLEL)                   
*                                       CONTRACT START DATE                     
         DC    AL2(PRQCFDCS),AL1(PCONKIDQ,16,PCONSDT-PCONDESC)                  
*                                                                               
CFLTABX  DS    0X                                                               
CFLTABN  EQU   (*-CFLTAB)/CFLTABL    NUMBER OF TABLE ENTRIES                    
*                                                                               
         LTORG                                                                  
*                                                                               
         DS    CL256               FOR NEW Z/OS PROCESSOR                       
CFLARGS  DS    XL16                GLARGS SAVEAREA                              
CFLWORK  DS    XL32                WORKAREA                                     
CFLDATE  DS    XL3                 WORK DATE FOR YM DATES                       
*                                                                               
         DROP                                                                   
*                                                                               
         TITLE 'PRWRICFL - FIND COLUMN FILTER - FNDFLTR'                        
***********************************************************************         
*                                                                     *         
*        FIND A SPECIFIC COLUMN FILTER                                *         
*                                                                     *         
*        FILTERS ARE IN SAVEAREA BASED AT FLTTAB                      *         
*           EACH KEYWORD HAS 256 BYTES                                *         
*                                                                     *         
*NTRY    PARM 0   ==>  AL2(FILTER TYPE )                              *         
*        PARM 1+0 ==>  AL1(FILTER VALUE LENGTH) (0 DEFAULTS TO 2)     *         
*        PARM 1   ==>  FILTER VALUE (A(0) BYPASSES VALUE CHECK)       *         
*                                                                     *         
*EXIT    CC  NE   FILTER NOT FOUND                                    *         
*        PARM 3   = A(FILTER ENTRY)                                   *         
*                                                                     *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         DS    0D                                                               
FNDFLTR  NMOD1 0,**#FFL                                                         
*                                                                               
         USING SPOOLD,R8           ESTABLISH SPOOL WORKAREA                     
         USING SYSD,R9             ESTABLISH PRINTWRITER WORKAREA               
         USING GLOBALD,RA          ESTABLISH DRIVER WORKAREA                    
*                                                                               
         LA    R7,PBLOCK           ESTABLISH PRINT BLOCK                        
         USING PBLOCK,R7                                                        
*                                                                               
         L     RC,GLAWORKD         RESET WORKING STORAGE POINTER                
         USING GEND,RC                                                          
*                                                                               
         ST    R1,FFLPARMA         SAVE PARAMETER AREA ADDRESS                  
*                                                                               
         ICM   R2,15,0(R1)         POINT TO DESIRED FILTER TYPE  ID             
         BZ    FFLOKAY             SKIP IF NONE GIVEN                           
*                                                                               
         ICM   R3,15,4(R1)         POINT TO DESIRED FILTER VALUE ID             
*                                                                               
         MVC   FFLTLEN,4(R1)       SAVE FILTER VALUE LENGTH                     
*                                                                               
         CLI   FFLTLEN,0           IF NOT GIVEN                                 
         BNE   *+8                                                              
         MVI   FFLTLEN,2              DEFAULT TO 2                              
*                                                                               
         XC    8(4,R1),8(R1)       CLEAR RETURN ADDRESS                         
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,1,GLARGS+6       GET KEYWORD NUMBER                           
         BZ    FFLOKAY             NONE GIVEN - NO FILTER                       
*                                                                               
         LA    RE,FLTAREAL         LENGTH OF FILTER SAVEAREA                    
         BCTR  RF,0                DECREMENMT FOR INDEXING                      
         MR    RE,RE               CALCULATE INDEX AMOUNT                       
*                                                                               
         L     R7,AFLTTAB          POINT TO FILTER SAVEAREAS                    
         LA    R7,0(RF,R7)         POINT TO THIS KEYWORD'S AREA                 
*                                                                               
         USING FLTAREA,R7          ESTABLISH FILTER SAVEAREA                    
*                                                                               
*        DETERMINE FILTER TYPE AND APPLY                                        
*                                                                               
         SR    R0,R0                                                            
         ICM   R0,1,FLTFNO         GET NUMBER OF FILTERS                        
         BZ    FFLOKAY             NONE GIVEN - FILTER NOT FOUND                
*                                                                               
         LA    R6,FLTCTL           POINT TO FIRST FILTER                        
         SR    RE,RE                                                            
*                                                                               
         SR    RF,RF                                                            
         IC    RF,FFLTLEN          GET INPUT FILTER LENGTH                      
         BCTR  RF,0                DECREMENT FOR EXECUTE                        
         MVI   BYTE,0              INIT ACTIVITY INDICATOR                      
*                                                                               
FFLLOOP  DS    0H                                                               
*                                                                               
         USING FLTCTL,R6           ESTABLISH FILTER                             
*                                                                               
*        MATCH TO GIVEN FILTER                                                  
*                                                                               
         CLC   FLTFTIC,0(R2)       MATCH FILTER TYPE                            
         BNE   FFLCONT                                                          
*                                                                               
         MVI   BYTE,1              INDICATE FILTERING BEING DONE                
*                                                                               
         LTR   R3,R3               IF NO SPECIFIC ONE ASKED FOR                 
         BZ    FFLFND                 ACCEPT THIS ONE                           
*                                                                               
         CLC   FFLTLEN,FLTFVLEN    MATCH VALUE LENGTHS                          
         BNE   FFLCONT                                                          
*                                                                               
         TM    FLTCTL,FLTNOTQ      SKIP IF FILTERING ON ABSENCE                 
         BO    FFLNOT                                                           
*                                                                               
         TM    FLTCTL,FLTEQQ       IF EQ                                        
         BNO   FFLEQX                                                           
*                                                                               
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R3),FLTFVAL        MATCH FILTER VALUE                        
         BE    FFLFND                                                           
*                                                                               
         B     FFLCONT                                                          
*                                                                               
FFLEQX   DS    0H                                                               
*                                                                               
         TM    FLTCTL,FLTLTQ       IF LT                                        
         BNO   FFLLTX                                                           
*                                                                               
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R3),FLTFVAL        COMPARE FILTER VALUE                      
         BL    FFLFND                                                           
*                                                                               
         B     FFLCONT                                                          
*                                                                               
FFLLTX   DS    0H                                                               
*                                                                               
         TM    FLTCTL,FLTGTQ       IF GT                                        
         BNO   FFLGTX                                                           
*                                                                               
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R3),FLTFVAL        COMPARE FILTER VALUE                      
         BH    FFLFND                                                           
*                                                                               
         B     FFLCONT                                                          
*                                                                               
FFLGTX   DS    0H                                                               
*                                                                               
         B     FFLCONT                                                          
*                                                                               
FFLNOT   DS    0H                                                               
*                                                                               
         TM    FLTCTL,FLTEQQ       IF NEQ                                       
         BNO   FFLNEQX                                                          
*                                                                               
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R3),FLTFVAL        MATCH FILTER VALUE                        
         BNE   FFLFND                                                           
*                                                                               
         B     FFLCONT                                                          
*                                                                               
FFLNEQX  DS    0H                                                               
*                                                                               
         TM    FLTCTL,FLTLTQ       IF NOT LT                                    
         BNO   FFLNLTX                                                          
*                                                                               
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R3),FLTFVAL        COMPARE FILTER VALUE                      
         BNL   FFLFND                                                           
*                                                                               
         B     FFLCONT                                                          
*                                                                               
FFLNLTX  DS    0H                                                               
*                                                                               
         TM    FLTCTL,FLTGTQ       IF NOT GT                                    
         BNO   FFLNGTX                                                          
*                                                                               
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R3),FLTFVAL        COMPARE FILTER VALUE                      
         BNH   FFLFND                                                           
*                                                                               
         B     FFLCONT                                                          
*                                                                               
FFLNGTX  DS    0H                                                               
*                                                                               
         B     FFLCONT                                                          
*                                                                               
FFLCONT  DS    0H                                                               
*                                                                               
         IC    RE,FLTFVLEN         GET LENGTH OF FILTER                         
         LA    R6,FLTFVAL(RE)      BUMP TO NEXT FILTER                          
         BCT   R0,FFLLOOP                                                       
*                                                                               
FFLNOTF  DS    0H                                                               
*                                                                               
         CLI   BYTE,0              OKAY IF NOT FILTERING                        
         BE    FFLOKAY                                                          
*                                                                               
         LTR   RB,RB               SET NEQ CC                                   
         B     FNDFLTRX            FILTER NOT FOUND                             
*                                                                               
FFLFND   DS    0H                                                               
*                                                                               
         L     R1,FFLPARMA         POINT TO PARAMETER LIST                      
         ST    R6,8(R1)            RETURN FILTER ELEMENT ADDRESS                
*                                                                               
FFLOKAY  DS    0H                                                               
*                                                                               
         CR    RB,RB               INDICATE RECORD TO BE KEPT                   
         B     FNDFLTRX                                                         
*                                                                               
FNDFLTRX DS    0H                                                               
         XIT1                                                                   
*                                                                               
         DS    CL256               FOR NEW Z/OS PROCESSOR                       
FFLPARMA DS    A                   PARAMETER LIST ADDRESS                       
FFLTLEN  DS    X                   INPUT FILTER VALUE LENGTH                    
*                                                                               
         LTORG                                                                  
*                                                                               
         DROP                                                                   
*                                                                               
         TITLE 'PRWRICFL - MAINTAIN COUNT BUCKETS - COUNT'                      
***********************************************************************         
*                                                                     *         
*        MAINTAIN COUNT BUCKETS                                       *         
*              ONE BUCKET PER POSSIBLE LEVEL                          *         
*              EACH BUCKET IS UPDATED WHEN OUTPUT CHANGES             *         
*                                                                     *         
*NTRY    R3 ==>  OUTPUT                                               *         
*                                                                     *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         DS    0D                                                               
COUNT    NMOD1 0,**#CT                                                          
*                                                                               
         USING SPOOLD,R8           ESTABLISH SPOOL WORKAREA                     
         USING SYSD,R9             ESTABLISH PRINTWRITER WORKAREA               
         USING GLOBALD,RA          ESTABLISH DRIVER WORKAREA                    
*                                                                               
         L     RC,GLAWORKD         RESET WORKING STORAGE POINTER                
         USING GEND,RC                                                          
*                                                                               
         TM    GLINDS,GLTOTLIN     SKIP IF A TOTAL LINE                         
         BO    CTTOT                                                            
*                                                                               
         ZIC   R1,GLRECNO          PICK UP PRESENT RECORD NUMBER                
         BCTR  R1,0                                                             
         SLL   R1,2                                                             
         LA    R1,GLAINTD(R1)      GET TO DETAILS FOR THIS REPORT               
*========              ========*                                                
         L     R1,0(R1)                                                         
         USING GLINTD,R1                                                        
*========              ========*                                                
         LH    R4,GLPDISP          NOW PICK UP PRINT DISPLACEMENT               
         A     R4,GLAINTP1         AND GET ACTUAL PRINT ADDRESS                 
         LA    R4,1(R4)                                                         
*                                                                               
         DROP  R1                                                               
*                                                                               
         ICM   R5,15,GLADTENT      ESTABLISH DRIVE TABLE ENTRY                  
         USING DROD,R5             AS OUTPUT RECORD                             
*                                                                               
         OC    CTLBLSV,CTLBLSV     IF LABEL NOT FOUND YET                       
         BNZ   CTLBLX                                                           
*                                                                               
         MVC   CTWORK,LABLAREA        SAVE CURRENT LABEL                        
         MVC   LABLAREA,SPACES        INIT LABEL AREA                           
*                                                                               
         GOTO1 BLDLBLA                BUILD ONE                                 
*                                                                               
         MVC   CTLBLSV,LABLAREA       AND SAVE IT                               
*                                                                               
         MVC   LABLAREA,CTWORK        RESTORE CURRENT LABEL                     
*                                                                               
         LA    RF,L'CTLBLSV        MAX LABEL LENGTH                             
         LA    RE,CTLBLSV+L'CTLBLSV-1  END OF LABEL                             
*                                                                               
         CLI   0(RE),C' '          FIND END OF LABEL                            
         BH    *+10                                                             
         BCTR  RE,0                BACK UP A BYTE                               
         BCT   RF,*-10             DECREMENT LENGTH COUNTER                     
*                                                                               
         STH   RF,CTLBLL           SAVE LABEL LENGTH                            
*                                                                               
CTLBLX   DS    0H                                                               
*                                                                               
*        CHECK IF PRIOR SORT KEY HAS CHANGED                                    
*                                                                               
         L     RF,GLAIFLD          THIS INPUT FIELD                             
         L     R1,GLATHREC         START OF INPUT RECORD                        
*                                                                               
         SR    RF,R1               LENGTH OF PRIOR SORT KEY                     
*                                                                               
         BCTR  RF,0                DECREMENT FOR EXECUTE                        
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R1),CTHEADSV    CONTINUE IF UNCHANGED                        
         BE    CTHEADX                                                          
*                                                                               
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   CTHEADSV(0),0(R1)   SAVE NEW SORT KEY                            
*                                                                               
         XC    CTOUTSV,CTOUTSV     FORCE NEW OUTPUT SITUATION                   
*                                                                               
CTHEADX  DS    0H                                                               
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,1,DROLEN         GET OUTPUT DATA LENGTH                       
         BZ    COUNTX              NO OUTPUT                                    
*                                                                               
         BCTR  RF,0                DECREMENT FOR EXECUTE                        
         EX    RF,*+8                                                           
         B     *+10                COMPARE ALL OF LINE UP TO THIS FIELD         
         CLC   CTOUTSV(0),0(R3)    COMPARE TO LAST OUTPUT                       
         BE    COUNTX              DONE IF NO CHANGE                            
*                                                                               
*        INCREMENT ALL BUCKETS                                                  
*                                                                               
CTBUMP   DS    0H                                                               
*                                                                               
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   CTOUTSV(0),0(R3)    SAVE NEW OUTPUT VALUE                        
*                                                                               
         LA    RF,16               MAX 16 TOTALLING LEVELS                      
         LA    RE,CTBKTS           START OF BUCKETS                             
*                                                                               
         AP    0(L'CTBKTS,RE),=P'1'       INCREMENT COUNTERS                    
         LA    RE,L'CTBKTS(RE)     NEXT BUCKET                                  
         BCT   RF,*-10                                                          
*                                                                               
         B     COUNTX                                                           
*                                                                               
         TITLE 'PRWRICFL - COUNT HANDLING FOR TOTAL LINES'                      
***********************************************************************         
*                                                                     *         
*        FIND LEVEL FOR TOTAL LINE                                    *         
*        PRINT COUNT IN APPROPRIATE BUCKET                            *         
*        CLEAR BUCKET                                                           
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
CTTOT    DS    0H                                                               
*                                                                               
*        FORMAT PRINTOUT FOR COUNT VALUE                                        
*                                                                               
         SR    RE,RE                                                            
         ICM   RE,1,GLLEVEL        GET CONTROL BREAK LEVEL                      
         MH    RE,=Y(L'CTBKTS)     CALCULATE INDEX                              
         LA    RE,CTBKTS(RE)       POINT TO BUCKET                              
*                                                                               
         MVC   CTWORK(L'CTEDIT),CTEDIT  SET EDIT PATTERN                        
*                                                                               
         LA    R1,CTWORK+L'CTEDIT-1  PRINT 1 ZERO AT LEAST                      
*                                                                               
         EDMK  CTWORK(L'CTEDIT),0(RE)    PRINT COUNT                            
*                                                                               
         ZAP   0(L'CTBKTS,RE),=P'0'   CLEAR BUCKET                              
         XC    CTOUTSV,CTOUTSV     RESET OUTPUT SAVEAREA                        
*                                                                               
         LA    RF,CTWORK+L'CTEDIT                                               
         SR    RF,R1               LENGTH OF PRINTOUT                           
*                                                                               
         STH   RF,CTDTLL           SAVE PRINTOUT LENGTH                         
         ST    R1,CTDTLA           SAVE PRINTOUT ADDRESS                        
*                                                                               
*        FORMAT TITLE FOR COUNT                                                 
*        DEPENDING ON SPACE AVAILABLE CHOICES ARE                               
*              COUNT FOR (COLUMN TITLE)                                         
*              CT FOR (COLUMN TITLE)                                            
*              CT                                                               
*              NO TITLE                                                         
*                                                                               
         LA    R1,BLOCK            BUILD OUTPUT IN BLOCK                        
         MVC   BLOCK(128),SPACES   INIT BLOCK AREA                              
         MVC   BLOCK+128(128),SPACES   INIT BLOCK AREA                          
*                                                                               
         LH    RE,CTLBLL           GET LABEL LENGTH                             
         AH    RE,CTDTLL           ADD ON DETAIL LENGTH                         
*                                                                               
         LA    RF,L'CTTTL+1(RE)    ADD ON LEAD IN LENGTH                        
         CLM   RF,1,TOTROWID       IF ROOM FOR LONG TEXT                        
         BH    *+18                                                             
         MVC   0(L'CTTTL,R1),CTTTL    USE IT                                    
         LA    R1,L'CTTTL(R1)         BUMP OUTPUT POINTER                       
         B     CTTOTTLX                                                         
*                                                                               
         LA    RF,L'CTTTL2+1(RE)   ADD ON LEAD IN #2 LENGTH                     
         CLM   RF,1,TOTROWID       IF ROOM FOR SHORT TEXT                       
         BH    *+18                                                             
         MVC   0(L'CTTTL2,R1),CTTTL2  USE IT                                    
         LA    R1,L'CTTTL2(R1)        BUMP OUTPUT POINTER                       
         B     CTTOTTLX                                                         
*                                                                               
         SH    RE,CTLBLL           DROP COLUMN TITLE FROM TITLE                 
         LA    RF,L'CTTTL3(RE)     ADD ON LEAD IN #3 LENGTH                     
         CLM   RF,1,TOTROWID       IF ROOM FOR SHORT TEXT                       
         BH    *+18                                                             
         MVC   0(L'CTTTL3,R1),CTTTL3  USE IT                                    
         LA    R1,L'CTTTL3(R1)        BUMP OUTPUT POINTER                       
         B     CTTOTTLX                                                         
*                                                                               
         B     CTOTLB1X            NO ROOM SKIP COLUMN TITLE                    
*                                                                               
CTTOTTLX DS    0H                                                               
*                                                                               
*        ADD IN COLUMN LABEL                                                    
*                                                                               
         ICM   RF,3,CTLBLL         GET LABEL LENGTH                             
         BZ    CTOTLBLX            NO LABEL AVAILABLE                           
*                                                                               
         BCTR  RF,0                DECREMENT FOR EXECUTE                        
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R1),CTLBLSV     ADD IN LABEL                                 
*                                                                               
         LA    R1,2(RF,R1)         BUMP OUTPUT POINTER                          
*                                                                               
CTOTLBLX DS    0H                                                               
*                                                                               
         MVC   0(2,R1),=C'IS'                                                   
         LA    R1,2(R1)            BUMP OUTPUT POINTER                          
*                                                                               
CTOTLB1X DS    0H                                                               
*                                                                               
         ICM   RF,3,CTDTLL         GET DETAIL LENGTH                            
         BZ    CTOTDTLX            NO DETAIL AVAILABLE                          
*                                                                               
         L     RE,CTDTLA           POINT TO DETAIL DATA                         
*                                                                               
         BCTR  RF,0                DECREMENT FOR EXECUTE                        
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   1(0,R1),0(RE)       PRINT COUNT                                  
*                                                                               
         LA    R4,2(RF,R1)         NEXT OUTPUT POSITION                         
         LA    R1,BLOCK                                                         
         SR    R4,R1               OUTPUT LENGTH                                
*                                                                               
*        CHOP RESULT                                                            
*                                                                               
         LA    R1,6                MAX 6 LINES FOR CHOPPING                     
         ST    R1,DMCB+8                                                        
         MVI   DMCB+8,198                                                       
*                                                                               
         GOTO1 CHOPPER,DMCB,((R4),BLOCK),(TOTROWID,0(R3))                       
*                                                                               
CTOTDTLX DS    0H                                                               
*                                                                               
         B     COUNTX                                                           
*                                                                               
COUNTX   DS    0H                                                               
         XIT1                                                                   
*                                                                               
         DS    CL256               FOR NEW Z/OS PROCESSOR                       
CTTTL    DC    C'COUNT FOR '                                                    
CTTTL2   DC    C'CT FOR '                                                       
CTTTL3   DC    C'CT '                                                           
CTEDIT   DC    X'4020202020202120'                                              
CTLBLL   DS    H                   LABEL LENGTH                                 
CTDTLL   DS    H                   COUNT LENGTH                                 
CTDTLA   DS    A                   A(COUNT)                                     
CTLBLSV  DC    XL(L'LABLAREA)'00'  LABEL SAVEAREA                               
CTWORK   DS    CL32                WORKAREA                                     
CTOUTSV  DC    XL256'00'           LAST OUT SAVEAREA                            
CTHEADSV DC    XL256'00'           PRIOR SORT KEY SAVEAREA                      
*                                                                               
CTBKTS   DC    16PL4'0'            BUCKETS FOR COUNT                            
*                                  ONE FOR EACH OF 16 LEVELS                    
*                                                                               
         LTORG                                                                  
*                                                                               
         DROP                                                                   
*                                                                               
         TITLE 'PRWRICFL - DATE FILTER TABLE DSECT - CFTABD'                    
***********************************************************************         
*                                                                     *         
*                                                                     *         
* DATE FILTER TABLE DSECT                                             *         
*       +0-1   AL2(COLUMN FILTER INTERNAL CODE)                       *         
*       +2     RECORD  ID / WHICH RECORD       IS TO BE CHECKED       *         
*       +3     ELEMENT ID / WHICH ELEMENT DATE IS TO BE FOUND IN      *         
*                AL1(0) - MEANS DATA IS IN KEY OF RECORD              *         
*       +4     DISPLACEMENT OF DATE IN ELEMENT                        *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
CFTABD   DSECT                          DATE FILTER TABLE                       
CFTFLTID DS    AL2                 DATE FILTER ID                               
CFTRECID DS    AL1                 RECORD ID                                    
CFTELMID DS    AL1                 ELEMENT ID                                   
*                                    AL1(0) DATE LIES IN KEY                    
CFTDISP  DS    AL1                 DISPLACEMENT OF DATA IN ELEMENT              
*                                                                               
CFTABL   EQU   *-CFTABD            LENGTH OF TABLE ENTRY                        
*                                                                               
         TITLE   'PRWRICFL DSECTS / STORAGE'                                    
* PRWRIWORKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE PRWRIWORKD                                                     
         PRINT ON                                                               
* PRWRIEQUS                                                                     
         PRINT OFF                                                              
       ++INCLUDE PRWRIEQUS                                                      
         PRINT ON                                                               
* DRGLOBAL                                                                      
         PRINT OFF                                                              
       ++INCLUDE DRGLOBAL                                                       
         PRINT ON                                                               
* PRVALTABD                                                                     
         PRINT OFF                                                              
       ++INCLUDE PRVALTABD                                                      
         PRINT ON                                                               
* DRIVETABLE                                                                    
         PRINT OFF                                                              
       ++INCLUDE DRIVETABLE                                                     
         PRINT ON                                                               
* DRINTRECD2                                                                    
         PRINT OFF                                                              
       ++INCLUDE DRINTRECD2                                                     
         PRINT ON                                                               
* DDSPOOLD                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDSPOOLD                                                       
         PRINT ON                                                               
* DDSPLWORKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDSPLWORKD                                                     
         PRINT ON                                                               
* PRGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE PRGENFILE                                                      
         PRINT ON                                                               
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
* DDCOMFACSD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACSD                                                     
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'057PRWRICFL  02/22/05'                                      
         END                                                                    
