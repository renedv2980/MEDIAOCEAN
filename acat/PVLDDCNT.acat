*          DATA SET PVLDDCNT   AT LEVEL 021 AS OF 07/31/20                      
*PROCESS USING(WARN(15))                                                        
*CATALP PVLDDCNT                                                                
         TITLE 'LDCOUNT - DEMOS - DIRECTORY REC TYPE COUNT/PRINT'               
*        PARAMS VIA R1                                                          
*        XL1   COUNT/PRINT  X'01'=COUNT  X'FF'=PRINT                            
*        AL3   A(RECORD)                                                        
*        AL4   A(ISNAME)                                                        
*        AL4   A(ISDTF#)           INTERNAL I/S FILE NUMBER                     
*        AL4   A(INDICATOR BITS)   XL1: X'04' = PRINT BY BOOK WITH              
*                                                "OLD" RECORD BUCKETS           
*                                       X'08' = PRINT ALL BOOKS                 
*                                                                               
         PRINT NOGEN                                                            
LDCOUNT  CSECT                                                                  
         NMOD1 0,PVLDDCNT,R9                                                    
*                                                                               
         LR    R2,R1               R2=A(PARAMETER LIST)                         
*                                                                               
         L     RE,12(R2)           A(COUNT INDICATOR BITS)                      
         MVC   RPTINDS,0(RE)       SAVE INDICATOR BITS                          
*                                                                               
         BC    0,LD50              INITIALIZATION                               
         MVI   *-3,X'F0'           *** SELF-MODIFYING ***                       
*                                                                               
         L     R0,=A(ACCUMSMX)     MAXIMUM NUMBER OF ACCUMULATORS               
         MHI   R0,ACCUMSLQ         R0 = ACCUMULATOR TABLE SIZE                  
         STORAGE OBTAIN,LENGTH=(R0),BNDRY=PAGE                                  
         LTR   RF,RF                                                            
         JNZ   *+2                 STORAGE OBTAIN FAILURE ?!?                   
         ST    R1,AACCUMS          RETURNED A(ACCUMULATOR TABLE)                
*                                                                               
         L     RF,=V(LDDEFN)       GET A(BOOKTYPE TABLE)...                     
         ICM   RF,15,LDEMTABS-LDDEFND(RF)                                       
         GOTO1 (RF),DMCB,SPBOOKTB  ...FROM DEMTABS                              
         MVC   ABKTYPTB,0(R1)      SAVE A(BOOKTYPE TABLE)                       
         L     RF,4(R1)                                                         
         STH   RF,BKTYPLN          SAVE LENGTH OF TABLE ENTRY                   
*                                                                               
         LA    RF,ISFILTAB                                                      
LD20     CLI   0(RF),X'FF'         EOT?                                         
         JE    *+2                 ALL DIRECTORIES SHOULD BE SPLIT NOW!         
         L     RE,8(R2)            RE = A(INTERNAL DTF#)                        
         CLC   0(1,RF),0(RE)       MATCH ON ISFILE NUMBER?                      
         BE    *+12                YES                                          
         LA    RF,L'ISFILTAB(RF)                                                
         B     LD20                                                             
         MVC   ASPLITTB,4(RF)      A(SPLIT FILE TABLE)                          
*                                                                               
         CLI   0(RE),NTIDIR#       WE'RE NOT READY TO DO BREAKOUTS...           
         BE    LD50                ...BY BOOK FOR NTIDIR YET                    
*                                                                               
         TM    RPTINDS,X'04'       PRINT BOOK BREAKOUT?                         
         BZ    LD50                NO                                           
*                                                                               
* BUILD A BINSRCH TABLE TRANSLATING NSI WEEK NUMBERS TO DATES.                  
*                                                                               
         LA    R5,NSIWKTAB         TABLE OF NSIWEEKS                            
         USING NSIWEEKD,R5                                                      
*                                                                               
         SR    R0,R0               TABLE ENTRY COUNTER                          
         LHI   R3,LOWYEAR          FIRST YEAR IN TABLE                          
*                                                                               
LD30     DS    0H                                                               
         LHI   R4,WEEK_1           STARTING WEEK NUMBER                         
*                                                                               
LD35     DS    0H                                                               
         STC   R3,WKLYYR#          SAVE BINARY YEAR                             
         STC   R4,WKLYWK#          SAVE BINARY MONTH                            
         GOTO1 =V(NSIWEEK),DMCB,(C'D',WKLYYM),V(GETDAY),V(ADDAY),      +        
               V(DATCON)                                                        
         AHI   R0,1                BUMP TABLE COUNTER                           
         SR    RF,RF                                                            
         ICM   RF,7,DMCB+1         A(YYMMDD DATE)                               
         MVC   DUB(6),0(RF)        YYMMDD DATE                                  
         GOTO1 =V(DATCON),DMCB,DUB,(3,BINYMD)                                   
*                                                                               
         LA    R5,NSIWKLNQ(,R5)    BUMP TO NEXT TABLE ENTRY                     
         AHI   R4,1                BUMP WEEK NUMBER                             
         CHI   R4,WKSPERYR         NUMBER OF WEEKS PER YEAR                     
         BNH   LD35                CONTINUE THIS YEAR                           
*                                                                               
         AHI   R3,1                BUMP YEAR NUMBER                             
         CHI   R3,HIGHYEAR         FINISHED BUILDING TABLE?                     
         BNH   LD30                START THE NEXT YEAR                          
*                                                                               
LD40     DS    0H                                                               
         ST    R0,NSIWKSN          SAVE NUMBER OF TABLE ENTRIES                 
         DROP  R5                                                               
         EJECT                                                                  
LD50     DS    0H                                                               
         CLI   0(R2),X'FF'         TEST PRINT                                   
         BE    PRNTTOTS                                                         
*                                                                               
         L     R3,0(R2)            R3 = A(DIRECTORY RECORD)                     
         XC    SAVEYM,SAVEYM                                                    
*                                                                               
         XC    ACCREC,ACCREC                                                    
REC      USING ACCUMSD,ACCREC      BUILD BINSRCH RECORD IN "ACCREC"             
*                                                                               
         ZAP   REC.ACCREC#,=P'0'   INITIALIZE PACKED ACCUMULATORS               
         ZAP   REC.ACCREC#D,=P'0'                                               
*                                                                               
         CLC   =X'FFFFFF',0(R3)    TRAILER RECORD?                              
         BNE   LD110                                                            
         MVI   REC.ACCUMSD,X'FF'   FILL KEY WITH X'FF'S                         
         MVC   REC.ACCUMSD+1(ACCKEYLQ-1),REC.ACCUMSD                            
         B     LD600                                                            
*                                                                               
LD110    DS    0H                                                               
         CLI   0(R3),PRCODEQU      PAV ('P') KEY?                               
         BNE   LD300                                                            
         USING PRKEY,R3                                                         
         MVC   REC.ACCFIL,PRCODE   FILE                                         
         MVC   REC.ACCMED,PRMEDIA  MEDIA                                        
         MVC   REC.ACCSRC,PRSRC    SOURCE                                       
         MVC   REC.ACCBOOK,PRBOOK  BOOK                                         
         MVC   REC.ACCBTYP,PRBTYP  BOOKTYPE                                     
         B     LD500                                                            
         DROP  R3                                                               
*                                                                               
LD300    DS    0H                  UNKNOWN                                      
         MVC   REC.ACCFIL,0(R3)    FILE                                         
         MVC   REC.ACCMED,1(R3)    MEDIA                                        
         MVC   REC.ACCSRC,2(R3)    SOURCE                                       
*                                                                               
         IF (TM,18(R3),STATUS_DEL,Z) UNLESS KEY IS MARKED FOR DELETION:         
           MVI   UNKFLAG,C'Y'          ISSUE UNKNOWN DIR. TYPE WARNING          
         ENDIF ,                                                                
*                                                                               
LD500    DS    0H                                                               
         TM    RPTINDS,X'04'       PRINT BOOK BREAKOUT?                         
         BO    LD510               YES                                          
         XC    REC.ACCBOOK,REC.ACCBOOK                                          
         XC    REC.ACCSRTBK,REC.ACCSRTBK                                        
         MVI   REC.ACCBTYP,0                                                    
         B     LD620               COLLAPSE TOTALS TOGETHER BY F/M/S            
*                                                                               
LD510    DS    0H                                                               
         MVI   REC.ACCINTVL,ACCINTVL_MONTHLY  ASSUME MONTHLY BOOK               
         MVC   REC.ACCSRTBK,REC.ACCBOOK    SORTING YEAR/MONTH                   
*                                                                               
         CLI   REC.ACCMED,C'O'     OVERNIGHTS HAVE WEEK NUMBER                  
         BNE   LD530                                                            
*                                                                               
         MVI   REC.ACCINTVL,ACCINTVL_WEEKLY  IT'S A WEEKLY BOOK                 
         CLI   REC.ACCBOOK+1,0     WEEK NUMBER ZERO ?                           
         BNE   *+12                                                             
         MVI   REC.ACCSRTBK+1,0    YES: FORCE MONTH ZERO                        
         B     LD530                                                            
*                                                                               
         LARL  R4,NSIWKTAB         TABLE OF NSI WEEKS                           
         USING NSIWEEKD,R4                                                      
         GOTO1 =V(BINSRCH),DMCB,REC.ACCBOOK,(R4),NSIWKSN,NSIWKLNQ,     +        
               L'WKLYYM                                                         
         CLI   DMCB,X'01'          IS WEEK IN TABLE?                            
         BE    LD525               NO                                           
         L     R4,DMCB             A(FOUND ENTRY)                               
         MVC   REC.ACCSRTBK,BINYMD SORT ON NORMALIZED YEAR/MONTH                
         B     LD530                                                            
         DROP  R4                                                               
*                                                                               
LD525    DS    0H                                                               
         TM    18(R3),STATUS_DEL   IS RECORD MARKED FOR DELETION?               
         BO    *+6                 YES                                          
         DC    H'0'                INCREASE HIGHYEAR                            
*                                                                               
         MVC   REC.ACCSRTBK,REC.ACCBOOK  JUST SORT ON REGULAR "BOOK"            
*                                                                               
LD530    DS    0H                                                               
         MVC   SAVEYM,REC.ACCSRTBK BOOK YEAR/MONTH                              
*                                                                               
         TM    RPTINDS,X'08'       PRINT ALL BOOKS (EVEN "OLD" ONES)?           
         BO    LD600               YES                                          
         GOTO1 =V(DATCON),DMCB,(5,0),(3,FULL)                                   
         LLC   R0,FULL             CURRENT BINARY YEAR                          
         SHI   R0,2                > 2 YEARS OLD = "OLD"                        
         CLM   R0,1,REC.ACCBOOK                                                 
         BNH   LD600                                                            
         XC    REC.ACCBOOK,REC.ACCBOOK  THROW IT IN THE "OLD" BUCKET            
         XC    REC.ACCSRTBK,REC.ACCSRTBK                                        
*                                                                               
LD600    DS    0H                                                               
         XC    REC.ACCBOOK,=X'FFFF' FORCE REVERSE CHRONOLOGICAL SORT            
         XC    REC.ACCSRTBK,=X'FFFF'                                            
         DROP  REC                                                              
*                                                                               
         TM    18(R3),STATUS_DEL   IS RECORD MARKED FOR DELETION?               
         BZ    *+10                NO                                           
         XC    SAVEYM,SAVEYM       DON'T CONSIDER THIS FOR YEARLY RANGE         
*                                                                               
LD620    DS    0H                                                               
         L     R4,AACCUMS          A(ACCUMULATOR TABLE)                         
         GOTO1 =V(BINSRCH),DMCB,(X'01',ACCREC),(R4),ACCUMSN,ACCUMSLQ,  +        
               ACCKEYLQ,A(ACCUMSMX)                                             
         MVC   ACCUMSN,DMCB+8      SAVE NUMBER OF RECS IN TABLE                 
         SR    R4,R4                                                            
         ICM   R4,7,1(R1)                                                       
         BNZ   *+6                 ACCUMS TABLE IS FULL                         
         DC    H'0'                                                             
*                                                                               
         USING ACCUMSD,R4                                                       
         LA    RE,ACCREC#          ASSUME REC ISN'T MARKED FOR DELETION         
         LA    RF,DIRTOT#                                                       
         TM    18(R3),STATUS_DEL   IS IT?                                       
         BZ    *+12                                                             
         LA    RE,ACCREC#D         YES                                          
         LA    RF,DIRTOT#D                                                      
*                                                                               
         AP    0(L'ACCREC#,RE),=P'1'  BUMP APPROPRIATE ACCUMULATORS             
         AP    0(L'DIRTOT#,RF),=P'1'                                            
         DROP  R4                                                               
*                                                                               
         ICM   R6,15,ASPLITTB      FILE LIST FOR THIS DIRECTORY                 
         JZ    *+2                 ALL DIRECTORIES SHOULD BE SPLIT NOW!         
         USING SPLTFILD,R6                                                      
         LLC   R1,18(R3)           STATUS BYTE                                  
         NILF  GR1,X'003F'         FILE NUMBER IS IN LOW-ORDER 6 BITS           
         MHI   R1,SPLTFLNQ         DISPLACEMENT TO FILE TABLE ENTRY             
         AR    R6,R1               A(FILE TABLE ENTRY)                          
*                                                                               
         IF (TM,18(R3),STATUS_DEL,O)  IF KEY IS MARKED FOR DELETION:            
          AP    SPLTREC#_DEL,=P'1'      INCREMENT TOTAL DELETES COUNTER         
         ELSE ,                       O/W:                                      
          AP    SPLTREC#_KEEP,=P'1'     INCREMENT TOTAL KEEPS COUNTER           
         ENDIF ,                                                                
*                                  REMEMBER LOW/HIGH BOOKS FOR FILE             
         OC    SAVEYM,SAVEYM                                                    
         BZ    LDX900                                                           
         CLC   SPLTLOW,SAVEYM                                                   
         BL    *+10                                                             
         MVC   SPLTLOW,SAVEYM                                                   
         CLC   SPLTLST,SAVEYM                                                   
         BH    *+10                                                             
         MVC   SPLTLST,SAVEYM                                                   
*                                                                               
         DROP  R6                                                               
*                                                                               
LDX900   DS    0H                                                               
         B     XMOD1                                                            
         EJECT                                                                  
PRNTTOTS DS    0H                                                               
         L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
*                                                                               
         ICM   R4,15,ASPLITTB      FILE LIST FOR THIS DIRECTORY                 
         JZ    *+2                 ALL DIRECTORIES SHOULD BE SPLIT NOW!         
*                                                                               
* BEFORE PRINTING THE TABLE, SORT IT SUCH THAT THE TEST FILE IS ALWAYS          
* LAST IN THE LIST. NOTE: THE TABLE MAY NO LONGER BE INDEXED AFTER THE          
* SORT!                                                                         
*                                                                               
         SR    R0,R0               INIT ENTRY COUNTER                           
         LR    R1,R4               A(TABLE)                                     
         DO UNTIL=(CLI,0(R1),EQ,X'FF')                                          
           AHI   R0,1                INCREMENT NUMBER OF TABLE ENTRIES          
           LA    R1,SPLTFLNQ(,R1)    BUMP TO NEXT TABLE ENTRY                   
         ENDDO ,                   STOP AT EOT                                  
*                                                                               
         GOTO1 =V(XSORT),DMCB,(R4),(R0),SPLTFLNQ,L'SPLTSORT,           +        
               SPLTSORT-SPLTFILD                                                
*                                                                               
         MVC   TITLE,=CL60'DEMFILE RECORD COUNTS BY SPLIT FILENAME'             
         MVC   MID1,SPACES                                                      
         MVC   MID2,SPACES                                                      
         MVC   MID3,SPACES                                                      
         MVC   MID4,SPACES                                                      
         MVC   SUB1(L'MYSUB1F),MYSUB1F                                          
         MVC   SUB2(L'MYSUB2F),MYSUB2F                                          
         ZAP   LINE,=P'99'                                                      
*                                                                               
         USING SPLTFILD,R4                                                      
PT30     CLI   0(R4),X'FF'         EOT?                                         
         BE    PT50                YES                                          
*                                                                               
         CLC   SPLTFLNM,=C'???????'  IS THIS ENTRY JUST A FILLER?               
         BE    PT45                  YES: SKIP IT                               
*                                                                               
         MVC   PRTFILNM,SPLTFLNM   FILE NAME                                    
*                                                                               
         EDIT  SPLTREC#_KEEP,PRT#REC_KEEP,COMMAS=YES,ZERO=NOBLANK               
         EDIT  SPLTREC#_DEL,PRT#REC_DEL,COMMAS=YES,ZERO=NOBLANK                 
*                                                                               
         CLC   SPLTLOW,=X'FFFF'    WERE YEAR FIELDS UPDATED?                    
         BE    PT40                NO                                           
         MVC   FULL(2),SPLTLOW     LOWEST BOOK ON FILE                          
         MVI   FULL+2,X'01'        ANY DAY TO FAKE OUT DATCON                   
         GOTO1 =V(DATCON),DMCB,(3,FULL),(6,PRT1STYM)                            
         MVC   FULL(2),SPLTLST     LAST BOOK ON FILE                            
         MVI   FULL+2,X'01'        ANY DAY TO FAKE OUT DATCON                   
         GOTO1 =V(DATCON),DMCB,(3,FULL),(6,PRTLSTYM)                            
*                                                                               
PT40     DS    0H                                                               
         IF (CLI,SPLTFIL#,EQ,TESTFILE)                                          
           MVC  PRTTEST,=C'**TESTFILE**'                                        
         ENDIF                                                                  
*                                                                               
         GOTO1 =V(PRINTER)                                                      
*                                                                               
PT45     DS    0H                                                               
         LA    R4,SPLTFLNQ(R4)     BUMP TO NEXT SPLIT FILE TABLE ENTRY          
         B     PT30                                                             
         DROP  R4                                                               
*                                                                               
PT50     DS    0H                                                               
         MVC   P(L'MYSUB2F),MYSUB2F                                             
         GOTO1 =V(PRINTER)                                                      
         MVC   PRTFILNM,=C'*TOTAL*'    PRINT TOTAL                              
         EDIT  DIRTOT#,PRT#REC_KEEP,COMMAS=YES,ZERO=NOBLANK                     
         EDIT  DIRTOT#D,PRT#REC_DEL,COMMAS=YES,ZERO=NOBLANK                     
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         ICM   R4,15,ACCUMSN                                                    
         BZ    XMOD1                                                            
*                                                                               
         MVC   TITLE,=CL60'DEMFILE RECORD COUNTS BY BOOK'                       
         MVC   MID1,SPACES                                                      
         MVC   MID2,SPACES                                                      
         MVC   MID3,SPACES                                                      
         MVC   MID4,SPACES                                                      
         MVC   SUB1(L'MYSUB1),MYSUB1                                            
         MVC   SUB2(L'MYSUB2),MYSUB2                                            
         ZAP   LINE,=P'99'                                                      
*                                                                               
         L     R5,AACCUMS          A(ACCUMULATOR TABLE)                         
         USING ACCUMSD,R5                                                       
*                                                                               
PT70     DS    0H                                                               
         LA    R1,MEDSRC           MEDIA/SOURCE TABLE FOR PAVDIR                
         L     RE,8(R2)            RE = A(INTERNAL DTF#)                        
         CLI   0(RE),NTIDIR#       WE'RE NOT READY TO DO BREAKOUTS...           
         BNE   *+8                 ...BY FMS FOR NTIDIR YET                     
         LA    R1,MEDSTA           *** TEMPORARY *** TABLE FOR NATIONAL         
*                                                                               
         USING MEDSRCD,R1                                                       
PT80     CLI   MEDMEDCD,C'X'       UNKNOWN?                                     
         BE    PT90                YES                                          
         CLC   MEDMEDCD,ACCMED     MATCH ON MEDIA?                              
         BNE   *+14                NO                                           
         CLC   MEDSRCCD,ACCSRC     MATCH ON SOURCE?                             
         BE    PT90                YES                                          
         LA    R1,MEDSRCLQ(R1)                                                  
         B     PT80                                                             
*                                                                               
PT90     DS    0H                                                               
         MVC   PRTFILCD,ACCFIL                                                  
         MVC   PRTMEDCD,ACCMED                                                  
         MVC   PRTSRCCD,ACCSRC                                                  
         MVC   PRTMEDIA,MEDMEDIA                                                
         MVC   PRTSRC,MEDSOURC                                                  
         DROP  R1                                                               
*                                                                               
         XC    ACCBOOK,=X'FFFF'    RESTORE ORIGINAL INTERNAL BOOK VALUE         
*                                                                               
         OC    ACCSRTBK,ACCSRTBK   IS THIS A HDR/TLR RECORD?                    
         BZ    PT180               YES                                          
*                                                                               
         OC    ACCBOOK,ACCBOOK     IS THIS AN "OLD" BUCKET ENTRY?               
         BNZ   PT110                                                            
*                                                                               
         MVC   PRTBOOK(2),=C'< '   YES                                          
         GOTO1 =V(DATCON),DMCB,(5,0),(3,FULL)                                   
         LLC   R0,FULL             CURRENT BINARY YEAR                          
         SHI   R0,2                > 2 YEARS OLD = "OLD"                        
         STC   R0,FULL                                                          
         GOTO1 =V(DATCON),DMCB,(3,FULL),(20,DUB)                                
         MVC   PRTBOOK+2(4),DUB                                                 
         B     PT150                                                            
*                                                                               
PT110    DS    0H                                                               
         LLC   R1,ACCBOOK+1                                                     
         CLI   ACCINTVL,ACCINTVL_MONTHLY   MONTHLY BOOK?                        
         BE    PT130               YES                                          
         CVD   R1,DUB              WEEK NUMBER                                  
         OI    DUB+7,X'0F'                                                      
         UNPK  PRTBOOK+1(2),DUB                                                 
         CLC   PRTBOOK+1(2),=C'00'                                              
         BNE   *+10                                                             
         MVC   PRTBOOK+1(2),=C'**'                                              
         B     PT140                                                            
*                                                                               
PT130    MHI   R1,3                MONTHLY BOOK                                 
         LA    R1,MONTHS-3(R1)                                                  
         MVC   PRTBOOK(3),0(R1)                                                 
*                                                                               
PT140    MVI   PRTBOOK+3,C'/'                                                   
         LLC   R1,ACCBOOK                                                       
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  PRTBOOK+4(2),DUB                                                 
*                                                                               
PT150    DS    0H                                                               
         CLI   ACCBTYP,0           NOW CHECK FOR A BOOK TYPE                    
         BNE   *+18                                                             
         MVI   ACCBTYP,C' '        STANDARD BOOKTYPE                            
         MVC   PRTBKTYP,SPACES                                                  
         B     PT180                                                            
*                                                                               
         CLI   ACCBTYP,X'E0'       SPILL?                                       
         BNE   *+14                                                             
         MVC   PRTSPILL,=C'SPILL'                                               
         B     PT180                                                            
*                                                                               
         L     RF,ABKTYPTB         BOOKTYPE TRANSLATION TABLE                   
         USING SPBKTYPD,RF                                                      
PT160    CLI   0(RF),X'FF'         UNKNOWN?                                     
         BE    PT170               YES                                          
         CLC   SPBKTYPN,ACCBTYP    MATCH ON INTERNAL BOOKTYPE?                  
         BE    *+12                                                             
         AH    RF,BKTYPLN          LENGTH OF TABLE ENTRY                        
         B     PT160                                                            
*                                                                               
         MVC   PRTBKTYP,SPBKTYPA   2-CHAR ALPHA BOOKTYPE                        
         B     PT180                                                            
         DROP  RF                                                               
*                                                                               
PT170    DS    0H                                                               
         MVC   PRTBKTYP(5),=C'X''  '''  PRINT UNKNOWNS IN HEX                   
         L     RF,=V(LDDEFN)                                                    
         ICM   RF,15,LHEXOUT-LDDEFND(RF)                                        
         GOTO1 (RF),DMCB,ACCBTYP,PRTBKTYP+2,1,=C'TOG'                           
         CLC   =F'2',DMCB+16                                                    
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
PT180    DS    0H                                                               
         EDIT  ACCREC#,PRT#RECS,COMMAS=YES,ZERO=NOBLANK                         
         EDIT  ACCREC#D,PRT#RECD,COMMAS=YES                                     
*                                                                               
         GOTO1 =V(PRINTER)                                                      
         LA    R5,ACCUMSLQ(R5)                                                  
         BCT   R4,PT70                                                          
         DROP  R5                                                               
*                                                                               
PT200    DS    0H                                                               
         MVC   P(L'MYSUB2),MYSUB2                                               
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         MVC   P(7),=C'*TOTALS'    PRINT TOTALS                                 
         LA    R5,P+(PRT#RECS-DPRINT)  TOTAL NOT DELETED                        
         EDIT  DIRTOT#,(L'PRT#RECS,(R5)),COMMAS=YES,ZERO=NOBLANK                
         LA    R5,P+(PRT#RECD-DPRINT)  TOTAL MARKED DELETED                     
         EDIT  DIRTOT#D,(L'PRT#RECS,(R5)),COMMAS=YES,ZERO=NOBLANK               
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         AP    DIRTOT#,DIRTOT#D    COMPUTE GRAND TOTAL                          
         MVC   P(17),=C'** GRAND TOTAL **'                                      
         LA    R5,P+(PRT#RECS-DPRINT)  TOTAL                                    
         EDIT  DIRTOT#,(L'PRT#RECS,(R5)),COMMAS=YES,ZERO=NOBLANK                
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         CLI   UNKFLAG,C'Y'        ANY UNKNOWN RECORDS SEEN?                    
         BNE   XMOD1               NO                                           
         L     RE,8(R2)            RE = A(INTERNAL DTF#)                        
         CLI   0(RE),NTIDIR#       YES, BUT WE'RE NOT READY FOR...              
         BE    XMOD1               ...NTIDIR YET                                
         GOTO1 =V(DATAMGR),DMCB,=C'OPMSG',('WARNMS1Q',WARNMSG1)                 
*                                                                               
XMOD1    DS    0H                                                               
         XMOD1                                                                  
         EJECT                                                                  
         LTORG                                                                  
         SPACE 2                                                                
DUB      DS    D                                                                
FULL     DS    F                                                                
DMCB     DS    6F                                                               
WORK     DS    XL24                                                             
BYTE     DS    X                                                                
SAVEYM   DS    XL2                                                              
RPTINDS  DS    X                                                                
UNKFLAG  DC    C'N'                ASSUME NO UNKNOWNS                           
*                                                                               
* THESE BINSRCH COUNTERS ARE DELIBERATELY A-TYPES                               
ACCUMSN  DC    A(0)                ACCUMULATOR TABLE COUNTER                    
NSIWKSN  DC    A(0)                NSIWEEK TRANSLATION TABLE COUNTER            
*                                                                               
ACCREC   DS    XL(ACCUMSLQ)                                                     
ABKTYPTB DS    A                   A(BOOKTYPE TRANSLATION TABLE)                
BKTYPLN  DS    H                   LENGTH OF BOOKTYPE TABLE ENTRY               
         SPACE 2                                                                
MYSUB1F  DC    C'NEWDEMO=         RECORDS           DELETES       -STRT+        
               -   -LAST-'                                                      
MYSUB2F  DC    C'--------     --------------    --------------    -----+        
               -   ------'                                                      
*                                                                               
MYSUB1   DC    C' BOOK  FMS   MEDIA     SOURCE  BKTYPE   # MAJOR KEYS  +        
                MARKED DELETED'                                                 
MYSUB2   DC    C'------ ---   -----     ------  ------  -------------- +        
                --------------'                                                 
         SPACE 2                                                                
WARNMSG1 DC    C'AUTONOTE*US-MFDEMOSPROGRAMMERS:** WARNING ** UNKNOWN R+        
               ECORD TYPE(S) SEEN IN PVLDDCNT'                                  
WARNMS1Q EQU   *-WARNMSG1                                                       
         SPACE 2                                                                
MEDSTA   DS    0C               NETWORK MEDIA/STATION TABLE                     
         DC    X'FEFE',CL8'*DUMMY* ',CL8' '                                     
         DC    X'FFFF',CL8'*HDR/TLR',CL8' '                                     
         DC    C'XX',CL8'-------',CL8'-------'                                  
*                                                                               
MEDSRC   DS    0C               PAV MEDIA/SOURCE TABLE                          
         DC    C'TA',CL8'USA/TP',CL8'ARB TV'                                    
         DC    C'TN',CL8'USA/TP',CL8'NIELSEN'                                   
         DC    C'ON',CL8'USA/TP',CL8'NSI/ONIT'                                  
         DC    X'FFFF',CL8'*HDR/TLR',CL8' '                                     
         DC    C'XX',CL8'UNKNOWN',CL8'UNKNOWN'                                  
*                                                                               
         DS    0D                                                               
ISFILTAB DS    0XL8                                                             
         DC    AL1(PAVDIR#),AL3(0),A(PAVDIR)                                    
         DC    AL1(NTIDIR#),AL3(0),A(NTIDIR)                                    
         DC    X'FF'                                                            
*                                                                               
* NOTE: THESE TABLES ARE INDEXED BY INTERNAL FILE NUMBER (1-BASED).             
*       A FILE NUMBER OF ZERO INDICATES THAT THE DIRECTORY RECORD IS            
*       NOT ASSOCIATED WITH A FILE (I.E., IT IS EITHER A HEADER,                
*       TRAILER, OR PASSIVE).                                                   
*                                                                               
*       EACH TABLE MUST HAVE ENOUGH FILLER ENTRIES TO PERMIT INDEXING           
*       UP TO LOGICAL FILE #31 (THE TEST DEMOFILE).                             
*                                                                               
*       IMPORTANT: THE TABLE UNDERGOES AN IN-PLACE SORT AT LABEL                
*       PRNTTOTS. AFTER THAT, THE TABLE MAY NO LONGER BE INDEXED!!!             
*                                                                               
PAVDIR   DS    0D                                                               
 DC AL1(00),CL7'N/A    ',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(01),CL7'PAVFIL ',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(02),CL7'PAVFILA',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(03),CL7'PAVFILB',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(04),CL7'PAVFILC',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(05),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(06),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(07),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(08),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(09),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(10),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(11),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(12),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(13),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(14),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(15),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(16),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(17),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(18),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(19),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(20),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(21),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(22),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(23),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(24),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(25),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(26),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(27),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(28),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(29),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(30),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(TESTFILE),CL7'PAVFLTA',2PL6'0',X'FFFF',X'0000',X'FF',XL7'00'            
 DC X'FF'                          EOT                                          
*                                                                               
NTIDIR   DS    0D                                                               
 DC AL1(00),CL7'N/A    ',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(01),CL7'NTIFILO',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(02),CL7'NTIFILN',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(03),CL7'NTIFILP',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(04),CL7'NTIFILQ',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(05),CL7'NTIFILW',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(06),CL7'NTIFILY',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(07),CL7'NTIFILZ',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(08),CL7'NTIFILA',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(09),CL7'NTIFILB',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(10),CL7'NTIFILC',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(11),CL7'NTIFILD',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(12),CL7'NTIFILE',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(13),CL7'NTIFILF',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(14),CL7'NTIFILG',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(15),CL7'NTIFILH',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(16),CL7'NTIFILI',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(17),CL7'NTIFILJ',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(18),CL7'NTIFILK',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(19),CL7'NTIFILL',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(20),CL7'NTIFILM',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(21),CL7'NTIFILR',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(22),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(23),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(24),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(25),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(26),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(27),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(28),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(29),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(30),CL7'???????',2PL6'0',X'FFFF',X'0000',X'00',XL7'00'                  
 DC AL1(TESTFILE),CL7'NTIFLTA',2PL6'0',X'FFFF',X'0000',X'FF',XL7'00'            
 DC X'FF'                          EOT                                          
*                                                                               
TESTFILE EQU   31                  ALL TEST FILES ARE LOGICAL FILE #31          
ASPLITTB DC    A(0)                A(SPLIT FILE TABLE)                          
AACCUMS  DC    A(0)                A(ACCUMULATOR TABLE)                         
ACCUMSMX EQU   200000              MAXIMUM NUMBER OF ACCUMULATORS               
*                                                                               
DIRTOT#  DC    PL6'0'              TOTAL NO. OF "KNOWN" DIRECTORY RECS          
DIRTOT#D DC    PL6'0'              TOTAL NO. MARKED FOR DELETION                
*                                                                               
NTIDIR#  EQU   X'38'                                                            
PAVDIR#  EQU   X'2C'                                                            
*                                                                               
STATUS_DEL EQU X'80'               MARKED FOR DELETION                          
*                                                                               
         DS    0D                                                               
         DC    C'NSIWKTAB'                                                      
NSIWKTAB DS    (WKSPERYR*MAXYEARS)XL(NSIWKLNQ) TRANSLATE WEEK TO DATE           
LOWYEAR  EQU   YR_1980                                                          
HIGHYEAR EQU   YR_2024             COULD GO TO 2027 IF NECESSARY                
WKSPERYR EQU   WEEK_53             (CANADA HAS A WEEK 53)                       
MAXYEARS EQU   HIGHYEAR-LOWYEAR+1                                               
         EJECT                                                                  
ACCUMSD  DSECT                                                                  
ACCSRTBK DS    XL2                 MONTH-NORMALIZED BOOK (FOR SORTING)          
ACCINTVL DS    C                   BOOK INTERVAL                                
ACCINTVL_MONTHLY EQU C'M'           MONTHLY BOOK                                
ACCINTVL_WEEKLY  EQU C'W'           WEEKLY BOOK                                 
ACCBOOK  DS    XL2                 ACTUAL BOOK                                  
ACCFIL   DS    C                   FILE CODE                                    
ACCMED   DS    C                   MEDIA CODE                                   
ACCSRC   DS    C                   SOURCE CODE                                  
ACCBTYP  DS    C                   BOOK TYPE                                    
ACCKEYLQ EQU   *-ACCUMSD                                                        
ACCREC#  DS    PL6                 # OF DIRECTORY RECORDS                       
ACCREC#D DS    PL6                 # OF DIR. RECS MARKED FOR DELETION           
ACCUMSLQ EQU   *-ACCUMSD                                                        
         SPACE 2                                                                
MEDSRCD  DSECT                                                                  
MEDMEDCD DS    C                   MEDIA CODE                                   
MEDSRCCD DS    C                   SOURCE CODE                                  
MEDMEDIA DS    CL8                 MEDIA NAME                                   
MEDSOURC DS    CL8                 SOURCE NAME                                  
MEDSRCLQ EQU   *-MEDSRCD                                                        
         SPACE 2                                                                
SPLTFILD DSECT                                                                  
SPLTFIL# DS    AL1                 SPLIT FILE: LOGICAL FILENUM                  
SPLTFLNM DS    CL7                 FILENAME                                     
SPLTREC#_KEEP DS PL6               RECORD COUNT (KEEPS)                         
SPLTREC#_DEL  DS PL6               RECORD COUNT (DELETES)                       
SPLTLOW  DS    XL2                 YM: EARLIEST BOOK ON FILE                    
SPLTLST  DS    XL2                 YM: LATEST BOOK ON FILE                      
SPLTSORT DS    X                   FOR SORT BEFORE PRINTING                     
         DS    XL7                 SPARE                                        
SPLTFLNQ EQU   *-SPLTFILD                                                       
         SPACE 2                                                                
NSIWEEKD DSECT                                                                  
WKLYYM   DS    0XL2                WEEKLY YEAR/MONTH                            
WKLYYR#  DS    X                                                                
WKLYWK#  DS    X                                                                
BINYMD   DS    0XL3                START OF WEEK (BINARY YMD)                   
BINYEAR  DS    X                                                                
BINMONTH DS    X                                                                
BINDAY   DS    X                                                                
NSIWKLNQ EQU   *-NSIWEEKD                                                       
         EJECT                                                                  
* DDDPRINT                                                                      
* DEDEMFILE                                                                     
* DDMONYREQU                                                                    
* DEMLDDEFN                                                                     
* DEDEMTABD                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDDPRINT                                                       
         EJECT                                                                  
       ++INCLUDE DEDEMFILE                                                      
         EJECT                                                                  
       ++INCLUDE DDMONYREQU                                                     
         EJECT                                                                  
       ++INCLUDE DEMLDDEFN                                                      
         EJECT                                                                  
       ++INCLUDE DEDEMTABD                                                      
         EJECT                                                                  
         PRINT ON                                                               
DPRINT   DSECT                                                                  
         ORG   P                                                                
PRTFILNM DS    CL7                                                              
         DS    CL6                                                              
PRT#REC_KEEP DS CL14                                                            
         DS    CL4                                                              
PRT#REC_DEL  DS CL14                                                            
         DS    CL4                                                              
PRT1STYM DS    CL6                                                              
         DS    CL3                                                              
PRTLSTYM DS    CL6                                                              
         DS    CL2                                                              
PRTTEST  DS    C'**TESTFILE**'                                                  
         ORG                                                                    
         SPACE 3                                                                
         ORG   P                                                                
PRTBOOK  DS    CL6                                                              
         DS    C                                                                
PRTFILCD DS    C                                                                
PRTMEDCD DS    C                                                                
PRTSRCCD DS    C                                                                
         DS    CL3                                                              
PRTMEDIA DS    CL8                                                              
         DS    CL2                                                              
PRTSRC   DS    CL8                                                              
         DS    CL2                                                              
PRTBKTYP DS    CL2                                                              
         DS    CL3                                                              
         ORG   PRTBKTYP                                                         
PRTSPILL DS    CL5                                                              
         DS    C                                                                
PRT#RECS DS    CL14                                                             
         DS    CL2                                                              
PRT#RECD DS    CL14                                                             
         ORG                                                                    
         SPACE 3                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'021PVLDDCNT  07/31/20'                                      
         END                                                                    
