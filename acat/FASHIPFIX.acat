*          DATA SET FASHIPFIX  AT LEVEL 009 AS OF 04/23/10                      
*CATALP SHIPFIX                                                                 
         TITLE 'FIX SHIPIT BLOCKS IN CASE POST GETS LOST'                       
         PRINT NOGEN                                                            
***********************************************************************         
* NTRY: R1 = A(TCB)                                                   *         
* EXIT: TCBECB POSTED IF THIS IS A SHIPIT BLOCK THAT HAS COMPLETED    *         
***********************************************************************         
         EJECT                                                                  
SHIPFIX  CSECT                                                                  
         NMOD1 0,SHIPFIX*                                                       
         LR    R3,R1                                                            
         USING TCBD,R3             R3=A(TCB ENTRY)                              
*                                                                               
         XR    R4,R4                                                            
         ICM   R4,7,TCBSVECB+1     R4=A(ECB)                                    
         BZ    EXITOK                                                           
         LA    R5,TCBZECB                                                       
         CR    R4,R5               TEST ECB IS TCBZECB                          
         BNE   EXITOK              NO - THEN IGNORE THIS CODE                   
*                                                                               
         TM    TCBZECB,X'40'       WAS IT ALREADY POSTED?                       
         BO    EXITOK              YES                                          
*                                                                               
         BRAS  RE,ON31                                                          
         ICM   R5,15,TCBAZIP       SEE IF THERE IS A ZIP BLOCK HERE             
         BZ    EXITOK                                                           
         USING ZIPHDRD,R5                                                       
         CLI   ZIPSTAT,ZIPSDON                                                  
         BNE   EXITOK                                                           
*                                                                               
         ICM   R6,15,ZIPTIME       IGNORE UNLESS THIS SET                       
         BZ    EXITOK                                                           
*                                                                               
         A     R6,TU5SECS                                                       
         TIME  TU                  GET CURRENT TIME                             
         CR    R0,R6                                                            
         BL    EXITOK              5 SECONDS HAVE NOT PASSED YET                
*                                                                               
         BRAS  RE,OFF31                                                         
         POST  (R4)                DO OUT OWN POST INSTEAD                      
*                                                                               
         MVC   LOSTPST(L'TCBID),TCBID                                           
         MVC   LOSTPST+09(3),TCBLNSYS                                           
         MVC   LOSTPST+13(3),TCBLNPRG                                           
         WTO   TEXT=LOSTPSTH                                                    
         B     EXITOK                                                           
         DROP  R3,R5                                                            
         EJECT                                                                  
***********************************************************************         
* EXIT POINTS  AND USEFUL ROUTINES                                    *         
***********************************************************************         
         SPACE 1                                                                
ON31     O     RE,=XL4'80000000'                                                
         BSM   0,RE                                                             
*                                                                               
OFF31    N     RE,=XL4'7FFFFFFF'                                                
         BSM   0,RE                                                             
*                                                                               
EXITOK   CR    RB,RB               SET CC EQUAL                                 
         B     EXIT                                                             
*                                                                               
EXITL    CLI   *,255               SET CC LOW                                   
         B     EXIT                                                             
*                                                                               
EXIT     XIT1  ,                                                                
         ORG   *-2                                                              
         BSM   0,RE                                                             
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTANTS                                              *         
***********************************************************************         
         SPACE 1                                                                
LOSTPSTH DC    AL2(50)                                                          
LOSTPST  DC    CL50'???????? XXX/XXX POST LOST'                                 
*                                                                               
TU5SECS  DC    A(5*38400)                                                       
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
         SPACE 1                                                                
* FATCB                                                                         
         PRINT OFF                                                              
       ++INCLUDE FATCB                                                          
         PRINT ON                                                               
* DDZIPBLK                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDZIPBLK                                                       
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'009FASHIPFIX 04/23/10'                                      
         END                                                                    
